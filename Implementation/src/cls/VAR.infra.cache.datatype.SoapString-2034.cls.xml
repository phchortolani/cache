<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.infra.cache.datatype.SoapString">
<ClassType>datatype</ClassType>
<ProcedureBlock>0</ProcedureBlock>
<Super>%String</Super>

<Method name="LogicalToXSD">
<Description><![CDATA[Converts the Cache %String value to the canonical SOAP encoded value.]]></Description>
<ClassMethod>1</ClassMethod>
<CodeMode>generator</CodeMode>
<FormalSpec>%val:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	#define TAB  9
	#define LF  10
	#define CR  13

	if (($$$defClassKeyGet(%class, $$$cCLASSclasstype) = $$$cCLASSCLASSTYPEDATATYPE) ||
		$$$comMemberKeyGet(%class, $$$cCLASSparameter, "XMLENABLED", $$$cPARAMdefault)) {
		set chars = "$char(0)"
		for i = 1:1:31 {
			if (i = $$$TAB) continue
			if (i = $$$LF) continue
			if (i = $$$CR) continue

			set chars = chars_"_$char("_i_")"
		}

		set %codemode = $$$cMETHCODEMODEEXPRESSION
		set %code = "$zconvert($translate(%val, "_chars_", """"), ""O"", ""XML"")"
		if (+%parameter("TRUNCATE") && ($length(%parameter("MAXLEN")) > 0)) {
			set %code = "$extract("_%code_", 1, "_(+%parameter("MAXLEN"))_")"
		}
	}
	else {
		set %code = 0
	}

	quit $$$OK]]></Implementation>
</Method>
</Class>
</Export>