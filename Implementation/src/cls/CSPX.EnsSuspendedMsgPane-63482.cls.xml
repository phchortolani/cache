<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.EnsSuspendedMsgPane">
<ClassType/>
<IncludeCode>Ensemble,EnsUI</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%CSP.Util.TablePane</Super>
<System>3</System>

<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Method name="CreateResultSet">
<FormalSpec>&amp;pID:%String,&amp;pRS:%ResultSet</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	// If this is a submit of Resend, then display the result
	If +$get(%request.Data("$ID1",1)) > 0 {
		If $get(%request.Data("$ACTION",1))="Resend" Do ..ResendMessage()
		If $get(%request.Data("$ACTION",1))="Delete" Do ..DeleteMessage()
		If $get(%request.Data("$ACTION",1))="Discard" Do ..DiscardMessage()
	}
	Set tSC = $$$OK
	// get request parms via object
	Set tRequest = ##class(CSPX.EnsSuspendedMsgForm).ProcessSubmit(%request, .pID)
	Set tRequest.Status = 5	// Status = Suspended Only
	Set where = ""
	If (tRequest.Type '= "") {
		Set dtype = ##class(CSPX.EnsSuspendedMsgForm).TypeLogicalToDisplay(tRequest.Type)
		Set where = where _ $S(where="":"",1:" AND ") _ "Type = '" _ dtype _ "'"
	}

	If (tRequest.Status '= "") {
		Set dstatus = ##class(CSPX.EnsSuspendedMsgForm).StatusLogicalToDisplay(tRequest.Status)
		Set where = where _ $S(where="":"",1:" AND ") _ "Status = '" _ dstatus _ "'"
	}

	If (tRequest.IsError '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "IsError = '" _ tRequest.IsError _ "'"
	}

	If (tRequest.SessionStart '= "") {
		If (tRequest.SessionStart) {
			Set where = where _ $S(where="":"",1:" AND ") _ "SessionId = %ID"
		}
	}

	If (tRequest.StartId '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "%ID >= '" _ tRequest.StartId _ "'"
	}
	
	If (tRequest.EndId '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "%ID <= '" _ tRequest.EndId _ "'"
	}

	If (tRequest.StartTime '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "TimeCreated >= '" _ tRequest.StartTime _ "'"
	}
	
	If (tRequest.EndTime '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "TimeCreated <= '" _ tRequest.EndTime _ "'"
	}
	
	Set:where'="" where = "WHERE " _ where
	
	Set select = "ID,TimeCreated,SessionId,CASE IsError WHEN 1 THEN 'Error' ELSE 'OK' END As Error,SourceConfigName As Source"
	
	Set order = " ORDER BY %ID"
	If (tRequest.SortOrder = 2) {
		Set order = " ORDER BY %ID Desc"
	}
	
	Set maxRows = tRequest.MaxRows
	Set:(+maxRows<=0) maxRows = $G(%request.Data("MaxRows",1),1000)
	Set:(+maxRows>10000) maxRows = 10000
		
	Set query = "SELECT TOP " _ maxRows _ " " _ select _ " FROM Ens.MessageHeader " _ where _ order
	
	Set pRS = ##class(%ResultSet).%New()
	Set tSC = pRS.Prepare(query)
	
	Quit tSC
]]></Implementation>
</Method>

<Method name="ResendMessage">
<Implementation><![CDATA[
	Set tId = %request.Get("$ID1")
	If tId'="" {
		Set tStatus = ##class(Ens.MessageHeader).ResendMessage(tId)
		If $$$ISOK(tStatus) {
			Set tMsg = $$$LocalizeText("Ensemble","ResendComplete","Resend suspended message ID '%1' completed.",tId)
		} Else {
			Do $system.Status.DecomposeStatus(tStatus,.err)
			Set tMsg = $$$LocalizeText("Ensemble","ResendFailed","Resend suspended message ID '%1' failed: %2",tId,$g(err(1)))
		}
	} Else {
		Set tMsg = $$$LocalizeText("Ensemble","ResendFailed","Resend suspended message ID '%1' failed: %2",tId,"No ID specified")
	}
	//If we want to prompt the alert box: Write "<script language='javascript'>alert(",tMsg,");</script>"
	&html<<table border="0" cellpadding="3" width="100%"><tr>
		<td align="center" class="Text" nowrap><a class="LastUpdate"><b>#(tMsg)#</b></a></td>
		</td></tr></table>>
]]></Implementation>
</Method>

<Method name="DiscardMessage">
<Implementation><![CDATA[
	Set tId = %request.Get("$ID1")
	If tId'="" {
		Set tObj = ##class(Ens.MessageHeader).%OpenId(tId)
		If $isobject(tObj) {
			Do tObj.SetStatus($$$eMessageStatusDiscarded)
			Set tStatus=tObj.%Save()
			If $$$ISOK(tStatus) {
				Set tMsg = $$$LocalizeText("Ensemble","DiscardComplete","Discard suspended message ID '%1' completed.",tId)
			} Else {
				Do $system.Status.DecomposeStatus(tStatus,.err)
				Set tMsg = $$$LocalizeText("Ensemble","DiscardFailed","Discard suspended message ID '%1' failed: %2",tId,$g(err(1)))
			}
		} Else {
			Set tMsg = $$$LocalizeText("Ensemble","DiscardFailed","Discard suspended message ID '%1' failed: %2",tId,"Object failed on open")
		}
	} Else {
		Set tMsg = $$$LocalizeText("Ensemble","DiscardFailed","Discard suspended message ID '%1' failed: %2",tId,"No ID specified")
	}
	//Write "<script language='javascript'>alert('",tMsg,"');</script>"
	&html<<table border="0" cellpadding="3" width="100%"><tr>
		<td align="center" class="Text" nowrap><a class="LastUpdate"><b>#(tMsg)#</b></a></td>
		</td></tr></table>>
	Quit
]]></Implementation>
</Method>

<Method name="DeleteMessage">
<Implementation><![CDATA[
	Set tId = %request.Get("$ID1")
	If tId'="" {
		Set tStatus = ##class(Ens.MessageHeader).%DeleteId(tId)
		If $$$ISOK(tStatus) {
			Set tMsg = $$$LocalizeText("Ensemble","DeleteComplete","Delete suspended message ID '%1' completed.",tId)
		} Else {
			Do $system.Status.DecomposeStatus(tStatus,.err)
			Set tMsg = $$$LocalizeText("Ensemble","DeleteFailed","Delete suspended message ID '%1' failed: %2",tId,$g(err(1)))
		}
	} Else {
		Set tMsg = $$$LocalizeText("Ensemble","DeleteFailed","Delete suspended message ID '%1' failed: %2",tId,"No ID specified")
	}
	//Write "<script language='javascript'>alert('",tMsg,"');</script>"
	&html<<table border="0" cellpadding="3" width="100%"><tr>
		<td align="center" class="Text" nowrap><a class="LastUpdate"><b>#(tMsg)#</b></a></td>
		</td></tr></table>>
	Quit
]]></Implementation>
</Method>
</Class>
</Export>