<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="Report.Query.FINAPTBLine">
<ClassType>persistent</ClassType>
<ProcedureBlock>1</ProcedureBlock>
<Super>%Persistent</Super>

<Method name="FINAPTBLineClose">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;qHandle:%Binary</FormalSpec>
<PlaceAfter>FINAPTBLineExecute</PlaceAfter>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	;-------------------------------------------------------------------------------
	; Not used
	;
	; Params:
	; qHandle - Object to pass around required data, eg. current record id
	;
	; ByRefs: qHandle
	;
	; Returns:
	;
	; History:
	; 05-Sep-2006	PO		SR14872: Comments
	;-------------------------------------------------------------------------------

	Quit $$$OK
]]></Implementation>
</Method>

<Method name="FINAPTBLineExecute">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;qHandle:%Binary,CompanyNumber:%String,LocationNumber:%String,Language:%String,TrialBalanceDate:%Integer,YUSER:%String,InvoiceDateType:%String,SortBy:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	;-------------------------------------------------------------------------------
	; Create result data for line and Report.Query.FINAPTB.cls
	;
	; Params:
	; qHandle - Object to pass around required data, eg. current record id
	; CompanyNumber - Needs to be removed
	; LocationNumber - Needs to be removed
	; Language - Needs to be removed
	; TrialBalanceDate - Needs to be removed
	; YUSER - User session id
	; InvoiceDateType - Needs to be removed
	; SortBy - Needs to be removed
	;
	; ByRefs: qHandle
	;
	; Returns: $$$OK
	;
	; History:
	; 05-Sep-2006	PO		SR14872: Speed up attempt and use YUSER instead of $job
	;-------------------------------------------------------------------------------
	
	set curCurrent = 0
	set cur1Month = 0
	set cur2Month = 0
	set cur3Month = 0
	set curBalance = 0
	
	set idx = ""
	set idSupplier = ""
	for {
		set idSupplier = $order(^CacheTempSuppliers(YUSER,idSupplier))
		quit:(idSupplier="") //SR14451 Use CacheTemp
			
		set lstRow = $$GetSupplierRow^FINRPAPTB(idSupplier,TrialBalanceDate,InvoiceDateType,CompanyNumber,LocationNumber,Language)
		
		continue:$listget(lstRow,8)=0 ;skip zero balance suppliers
		
		set idx = idx + 1
		
		; Row Spec
		; 1. SupplierNumber
		; 2. SupplierName
		; 3. SupplierGroup
		; 4. CurrentAmount
		; 5. OneMonthAmount
		; 6. TwoMonthAmount
		; 7. ThreeMonthAmount
		; 8. Balance
		
		if (SortBy = 1) { 			;1 Month
			set arrOrder(YUSER,$listget(lstRow,5),idx) = lstRow // SR14872
		} elseif (SortBy = 2) {		;2 Month2
			set arrOrder(YUSER,$listget(lstRow,6),idx) = lstRow // SR14872
		} elseif (SortBy = 3) {		;3 Months
			set arrOrder(YUSER,$listget(lstRow,7),idx) = lstRow // SR14872
		} elseif (SortBy = 4) {		;Balance
			set arrOrder(YUSER,$listget(lstRow,8),idx) = lstRow // SR14872
		} elseif (SortBy = 5) {		;Current Amount
			set arrOrder(YUSER,$listget(lstRow,4),idx) = lstRow // SR14872
		} elseif (SortBy = 6) {		;Name
			set strName = $listget(lstRow,2)
			if strName = "" set strName = " "
			set arrOrder(YUSER,strName,idx) = lstRow // SR14872
		} elseif (SortBy = 7) {		;Number
			set arrOrder(YUSER,$listget(lstRow,1),idx) = lstRow // SR14872
		}
		
		set curCurrent = curCurrent + $listget(lstRow,4)
		set cur1Month = cur1Month + $listget(lstRow,5)
		set cur2Month = cur2Month + $listget(lstRow,6)
		set cur3Month = cur3Month + $listget(lstRow,7)
		set curBalance = curBalance + $listget(lstRow,8)
	}
	
	set ^CacheTempAPTBTotals(YUSER,1) = $listbuild(curCurrent,cur1Month,cur2Month,cur3Month,curBalance) // SR14872
	
	set strLoop = ""
	set intCount = 0
	
	for {
		set strLoop = $order(arrOrder(YUSER,strLoop)) // SR14872
		quit:(strLoop="")
		
		set idx = ""
		for {
			set idx = $order(arrOrder(YUSER,strLoop,idx)) // SR14872
			quit:(idx="")
		
			set intCount = intCount + 1
			set lstRow = $get(arrOrder(YUSER,strLoop,idx))	 // SR14872
			
			set ^CacheTempRow(YUSER,intCount) = lstRow // SR14872
		}	
	}
	
	set qHandle("Item") = ""
	set qHandle("YUSER") = YUSER // SR14872
	
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="FINAPTBLineFetch">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;qHandle:%Binary,&amp;Row:%List,&amp;AtEnd:%Integer=0</FormalSpec>
<PlaceAfter>FINAPTBLineExecute</PlaceAfter>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	;-------------------------------------------------------------------------------
	; Retrieve a row of the result
	;
	; Params:
	; qHandle - Object to pass around required data, eg. current record id
	; Row - Record to return
	; AtEnd - When last record then 1
	;
	; ByRefs: qHandle, ByRef & AtEnd
	;
	; Returns: $$$OK
	;
	; History:
	; 05-Sep-2006	PO		SR14872: Use YUSER instead of $job
	;-------------------------------------------------------------------------------

	set YUSER = qHandle("YUSER") // SR14872
	set qHandle("Item") = $order(^CacheTempRow(YUSER,qHandle("Item"))) // SR14872
	
	if qHandle("Item")'="" {
		set Row = ^CacheTempRow(YUSER,qHandle("Item")) // SR14872
	} else {
		set AtEnd = 1
		set Row = ""
		kill ^CacheTempRow(YUSER) // SR14872
	}
	
	Quit $$$OK
]]></Implementation>
</Method>
<Query name="FINAPTBLine">
<Type>%Query</Type>
<FormalSpec>CompanyNumber:%String(MAXLEN=30),LocationNumber:%String(MAXLEN=20),Language:%String(MAXLEN=2),TrialBalanceDate:%Integer,YUSER:%String(MAXLEN=15),InvoiceDateType:%String(MAXLEN=2),SortBy:%String(MAXLEN=2)</FormalSpec>
<SqlProc>1</SqlProc>

<Parameter name="ROWSPEC" value="SupplierNumber:%String(MAXLEN = 10),SupplierName:%String(MAXLEN = 50),SupplierGroup:%String(MAXLEN = 30),CurrentAmount:%Float,OneMonthAmount:%Float,TwoMonthAmount:%Float,ThreeMonthAmount:%Float,Balance:%Float"/>
</Query>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DataLocation>^Report.Query.FINAPTBLineD</DataLocation>
<DefaultData>FINAPTBLineDefaultData</DefaultData>
<IdLocation>^Report.Query.FINAPTBLineD</IdLocation>
<IndexLocation>^Report.Query.FINAPTBLineI</IndexLocation>
<StreamLocation>^Report.Query.FINAPTBLineS</StreamLocation>
<ExtentSize>100000</ExtentSize>
<Data name="FINAPTBLineDefaultData">
<Structure>listnode</Structure>
<Subscript/>
<Value name="1">
<Value>%%CLASSNAME</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>