<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="COM.fwk.biz.UserFootPrintBO">
<Description><![CDATA[
This class deals with information about how the user configures his interface,
so it will be shown the same way next time the user logs on the system.
<p>
The system keeps trace of forms configuration, like filtering and sorting, last form
accessed and so on.
<p>
Author: Tharsis Campos - Disclinc Brazil
]]></Description>
<ClassType/>
<Import>COM.fwk.entity</Import>
<ProcedureBlock>1</ProcedureBlock>
<Super>COM.fwk.biz.BO</Super>
<UDLText name="T">
<Content>
//quit  )

</Content>
</UDLText>


<Method name="getUserFootPrint">
<Description><![CDATA[
Retrieves the data of a specific parameter of the module informed.
<p>

Author: Tharsis Campos - Disclinc Brazil
]]></Description>
<FormalSpec>moduleAbbreviation:%String,parameterName:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	;-------------------------------------------------------------------------------
	; 
	; 
	; History
	; 12-Mar-2008	shobby	SRBR014897: parameterValue may require a ~ which had to be replaced before save
	;-------------------------------------------------------------------------------
	set userFootPrint = ##class(UserFootPrint).findByPrimaryKey(moduleAbbreviation, ..getSession().getCurrentUserName(), parameterName)

	if $isObject(userFootPrint)
	{
		quit $translate(userFootPrint.parameterValue,"|","~")  ;BR014897
	} 
	else
	{
		quit ""
	}
]]></Implementation>
</Method>

<Method name="getUserFootPrintSet">
<FormalSpec>moduleAbbreviation:%String,parameterName:%String</FormalSpec>
<ReturnType>%ListOfDataTypes</ReturnType>
<Implementation><![CDATA[
	set userFootPrints = ##class(UserFootPrint).findByParameterInitial(moduleAbbreviation, ..getSession().getCurrentUserName(), parameterName)
	
	set collection = ##class(%ListOfDataTypes).%New()
	for i = 1 : 1 : userFootPrints.Count()
	{
		do collection.Insert(userFootPrints.GetAt(i).parameterValueGet())
	}
	
	quit collection
]]></Implementation>
</Method>

<Method name="setUserFootPrint">
<Description><![CDATA[
Sets the value of a specific parameter of the module informed.
<p>

Author: Tharsis Campos - Disclinc Brazil
]]></Description>
<FormalSpec>moduleAbbreviation:%String,parameterName:%String,parameterValue:%String</FormalSpec>
<Implementation><![CDATA[
	;-------------------------------------------------------------------------------
	; 
	; 
	; History
	; 
	; 12-Mar-2008	shobby	SRBR014897: parameterValue may contain a ~ which won't save correctly
	; 10-Feb-2006	hjrb	SR14178:USE %Save() instead of save because save includes netManager  stuff
	; 
	;-------------------------------------------------------------------------------
	;
	set userFootPrint = ##class(UserFootPrint).findByPrimaryKey(moduleAbbreviation, ..getSession().getCurrentUserName(), parameterName)

	if '$isObject(userFootPrint)
	{
		set userFootPrint = ..create(moduleAbbreviation, parameterName)
	}
	set parameterValue=$translate(parameterValue,"~","|")  ;BR014897

	set userFootPrint.parameterValue = parameterValue
	;do userFootPrint.save()	// SR14178
	set sc=userFootPrint.%Save()	// SR14178
]]></Implementation>
</Method>

<Method name="create">
<Description><![CDATA[
Creates a parameter to be saved in the database.
<p>

Author: Tharsis Campos - Disclinc Brazil
]]></Description>
<FormalSpec>moduleAbbreviation:%String,parameterName:%String</FormalSpec>
<Private>1</Private>
<ReturnType>UserFootPrint</ReturnType>
<Implementation><![CDATA[
	set userFootPrint = ##class(UserFootPrint).%New()
	
	set userFootPrint.Company = ..getSession().getCurrentCompanyCode()
	set userFootPrint.moduleAbbreviation = moduleAbbreviation
	set userFootPrint.userName = ..getSession().getCurrentUserName()
	set userFootPrint.parameterName = parameterName
	
	quit userFootPrint
]]></Implementation>
</Method>
</Class>
</Export>