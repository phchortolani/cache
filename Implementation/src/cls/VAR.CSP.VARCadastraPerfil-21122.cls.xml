<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="VAR.CSP.VARCadastraPerfil">
<ClassType/>
<IncludeCode>COMSYS,COMConst,VARConst,INConst</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%CSP.Page</Super>

<Parameter name="CONTENTTYPE">
<Default>application/json</Default>
</Parameter>


<Parameter name="CHARSET">
<Default>utf-8</Default>
</Parameter>


<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim %request As %CSP.Request
	#dim %response As %CSP.Response
	Set Method 	= $Get(%request.Data("method",1))
	Xecute "do .."_Method_"()"
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="abreRotinas">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	$$$VAR
	set YM=0
	set vCodPerfil	= $Get(%request.Data("fCodPerfil",1))
	set vCodPerfil1	= $Get(%request.Data("fCodPerfil",1))
	set YUSER		= $Get(%request.Data("YUSER",1))
	do ..LimpaRegistro(YUSER)
	kill obj
	set existe=0	
	set vCodPerfilOriginal = ""
	if vCodPerfil'="",$data(^VARPerfilModelo(YM,vCodPerfil,1)){
		set vCodPerfilOriginal = vCodPerfil
		set objPerfilModelo = $get(^VARPerfilModelo(YM,vCodPerfil,1))
		set vCodPerfil = $$$VARPerfilModeloPerfisAplicados(objPerfilModelo)
		set existe=1
	}
	if vCodPerfilOriginal'="",$data(^WWW101(YM,"BERECHTIGUNG","PT",vCodPerfilOriginal,1)){
		set objPerfil = $get(^WWW101(YM,"BERECHTIGUNG","PT",vCodPerfilOriginal,1))
		set desativado = $$$WWW101HideParameter(objPerfil)
		set existe=1
		if desativado = 1 set existe = 2
	}
	set obj("existe",0,"existe")=existe
	set mult = 0
	if vCodPerfil'=""{
		for piece=1:1:$length(vCodPerfil,";"){
			set codPerfil = $piece(vCodPerfil,";",piece)
			set vCodPerfil(piece) = ";"_codPerfil_";"	
			set mult=mult+1
		}
		
	}
	set alteradoFlag = 0
	if mult>1 set alteradoFlag = 1
	set objParametro = $get(^VARParametroCliente(0,0,1))
	set menu = $$$VARParametroClienteNomedoMenuPrincipal(objParametro)
	if menu = "" set menu = " "
	;set menu = "VARMenuPrincipal"
	set contador = 0
	set nivel = ""
	for {
		set nivel = $order(^WWW004(0, menu, nivel)) quit:nivel=""
		set objWWW004 	= $get(^WWW004(0, menu, nivel, 1))
		set nome 		= $$$WWW004MenuDescription(objWWW004)
		set rotina 		= $$$WWW004FormName(objWWW004)
		set permissao 	= " "
		set excessao 	= " "
		if rotina'="" {
			set excessao = "^"
			set nome = nome
			set objRotina = $get(^WWW120(0,rotina,1))
			set nivelAcesso = ";"_$$$WWW120UserAccess(objRotina)_";"
			set somenteLeitura = ";"_$$$WWW120ViewAccess(objRotina)_";"
			set permissao=0
			set contPerfil = ""
			for {
				set contPerfil = $order(vCodPerfil(contPerfil)) quit:contPerfil=""
				set codPerfil = vCodPerfil(contPerfil)
				if $length(somenteLeitura,codPerfil)>1{
					set permissao=1
					quit
				}elseif $length(nivelAcesso,codPerfil)>1{
					set permissao=2
					quit
				}
			}
			if $data(^WWW124(0,rotina,"PT")){
				set $piece(excessao,"^",1) = 1
			}
			if $data(^WWW1203(0,rotina,"PT")){
				set $piece(excessao,"^",2) = 1
			}
		}
		set nivelFormatado = nivel
		if $extract(nivelFormatado,$length(nivelFormatado))'="." set nivelFormatado = nivelFormatado_"."
		set perm = ";"_$$$WWW004UserAccess(objWWW004)_";"
		set parentID = $piece(nivelFormatado,".",1,$length(nivelFormatado,".")-2)_"."
		if (parentID = nivelFormatado)||(parentID = ".") set parentID = 0
		;
		set ^VARCSPVARCadastraPerfilTemp(YUSER,nivel)=nome_"~"_permissao_"~"_excessao_"~"_alteradoFlag
		;
		set obj("dados",contador,"ID")=nivel
		set obj("dados",contador,"ParentID")=parentID
		set obj("dados",contador,"Telas")=$select(nome="":"Falta o Nome",1:nome)
		set obj("dados",contador,"Permissao")=permissao
		set obj("dados",contador,"Excecao")=excessao
		if rotina'="" set obj("dados",contador,"Rotina")=rotina
		;
		if excessao'=" "{
			set ^VARCSPVARCadastraPerfilTemp3(YUSER,nivel)=""
			set contPerfil = ""
			for {
				set contPerfil = $order(vCodPerfil(contPerfil)) quit:contPerfil=""
				set codPerfil = vCodPerfil(contPerfil)
				if $length(perm,codPerfil)>1{
					set obj("selectedRowKeys",nivel,nivel)=""
					quit
				}
			}
		}
		set contador = contador + 1
	}
	set rotina = ""
	for {
		set rotina = $order(^WWW124(0,rotina)) quit:rotina=""
		set botao = ""
		for {
			set botao = $order(^WWW124(0,rotina,"PT",botao)) quit:botao=""
			set objBotao = $get(^WWW124(0,rotina,"PT",botao,1))
			set nome = $$$WWW124ButtonDescription(objBotao)
			set nivelAcesso = $$$WWW124Authorization1(objBotao)
			set perm="0"
			if nivelAcesso=""{
				set perm=1
			}else{
				set contPerfil = ""
				for {
					set contPerfil = $order(vCodPerfil(contPerfil)) quit:contPerfil=""
					set codPerfil = vCodPerfil(contPerfil)
					if $length(";"_nivelAcesso_";",codPerfil)>1 set perm=1 quit
				}
			}			
			set ^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"botoes",botao)=nome_"~"_perm_"~"_alteradoFlag
		}
	}	
	set rotina = ""
	for {
		set rotina = $order(^WWW1203(0,rotina)) quit:rotina=""
		set aba = ""
		for {
			set aba = $order(^WWW1203(0,rotina,"PT",aba)) quit:aba=""
			set objAba = $get(^WWW1203(0,rotina,"PT",aba,1))
			set nome = $$$WWW1203PictureFileForPageTag(objAba)
			set nivelAcesso = $$$WWW1203UsersAccess(objAba)
			set perm="0"
			if (nivelAcesso="") {
				set perm=1
			}else{
				set contPerfil = ""
				for {
					set contPerfil = $order(vCodPerfil(contPerfil)) quit:contPerfil=""
					set codPerfil = vCodPerfil(contPerfil)
					if $length(";"_nivelAcesso_";",codPerfil)>1 set perm=1 quit
				}
			}
			set ^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"abas",aba)=nome_"~"_perm_"~"_alteradoFlag
		}
	}
		
	;=========== Dashboart =================;	
	set ^VARCSPVARCadastraPerfilTemp(YUSER,"99999999.")="Dashboard~ ~ "
	set ^VARCSPVARCadastraPerfilTemp3(YUSER,"99999999.")=""
	set contador = contador + 1
	set obj("dados",contador,"ID")="99999999."
	set obj("dados",contador,"ParentID")="0"
	set obj("dados",contador,"Telas")="Dashboard"
	set obj("dados",contador,"Permissao")=" "
	set obj("dados",contador,"Excecao")=" "
	if (vCodPerfil1'="" && ($data(^DashboardWidgetPerfil(0,vCodPerfil1)))) {
		set obj("selectedRowKeys","99999999.","99999999.")=""
	}			
	set widget = ""
	for {
		set widget = $order(^DashboardWidget(0,widget))
		quit:widget=""
		set descricao = $piece(^DashboardWidget(0,widget,1),"~",3)
		set subWidget = $piece(^DashboardWidget(0,widget,1),"~",5)
		set contador = contador + 1
		set obj("dados",contador,"ID")="99999999."_widget_"."
		set obj("dados",contador,"ParentID")="99999999."
		set obj("dados",contador,"Telas")=descricao
		set obj("dados",contador,"Permissao")=" "
		set obj("dados",contador,"Excecao")=" "
		set ^VARCSPVARCadastraPerfilTemp(YUSER,"99999999."_widget_".")=descricao_"~ ~ "
		set ^VARCSPVARCadastraPerfilTemp3(YUSER,"99999999."_widget_".")=""
		if (vCodPerfil1'="" && ($data(^DashboardWidgetPerfilWidget(0,vCodPerfil1,widget)))  && ('$data(^DashboardWidgetPerfilWidgetSubWidget(0,vCodPerfil1,widget)))) {
			set obj("selectedRowKeys","99999999."_widget_".","99999999."_widget_".")=""
		}			
		if subWidget="" continue
		set meuSQL = "Select CodigoBloco, Descricao FROM SQLUser."_subWidget_" Order By Descricao" 
		set objStatement	= ##class(%SQL.Statement).%New() 
		set qStatus 			= objStatement.%Prepare(meuSQL)
		set rSet 					= objStatement.%Execute()
	 	while (rSet.%Next()) {
			set codSubWidget 	= rSet.%Get("CodigoBloco")
			set descricao 		= rSet.%Get("Descricao")
			set contador = contador + 1
			set obj("dados",contador,"ID")="99999999."_widget_"."_codSubWidget_"."
			set obj("dados",contador,"ParentID")="99999999."_widget_"."
			set obj("dados",contador,"Telas")=descricao
			set obj("dados",contador,"Permissao")="0"
			set obj("dados",contador,"Excecao")=" "
			set permissao = " "
			if (vCodPerfil1'="" && ($data(^DashboardWidgetPerfilWidgetSubWidget(0,vCodPerfil1,widget,codSubWidget)))) {
				set permissao = ^DashboardWidgetPerfilWidgetSubWidget(0,vCodPerfil1,widget,codSubWidget,1)
				set obj("selectedRowKeys","99999999."_widget_"."_codSubWidget_".","99999999."_widget_"."_codSubWidget_".")=""
			}			
			set ^VARCSPVARCadastraPerfilTemp(YUSER,"99999999."_widget_"."_codSubWidget_".")=descricao_"~"_permissao_"~ "
			set ^VARCSPVARCadastraPerfilTemp3(YUSER,"99999999."_widget_"."_codSubWidget_".")=""
	 	}
	}
	;=========== Dashboart =================;
	do ##class(User.Search.PesquisasJson).ToJSON(.obj,1)
	;
	quit
]]></Implementation>
</Method>

<Method name="getBotoes">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set objParametro = $get(^VARParametroCliente(0,0,1))
	set menu = $$$VARParametroClienteNomedoMenuPrincipal(objParametro)
	if menu = "" set menu = " "
	;set menu = "VARMenuPrincipal"
	set YUSER = $Get(%request.Data("YUSER",1))
	set IDLinha = $Get(%request.Data("IDLinha",1))
	if IDLinha = "" quit
	set objWWW004 = $get(^WWW004(0, menu, IDLinha, 1))
	set rotina = $$$WWW004FormName(objWWW004)
	;
	kill ^VARCSPVARCadastraPerfilTemp2(YUSER)
	set ^VARCSPVARCadastraPerfilTemp2(YUSER) = rotina
	merge ^VARCSPVARCadastraPerfilTemp2(YUSER) = ^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"botoes")
	;
	set contador = 0
	set botao = ""
	for {
		set botao = $order(^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"botoes",botao)) quit:botao=""
		set objBotao = $get(^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"botoes",botao))
		set nome = $piece(objBotao,"~",1)
		set perm = $piece(objBotao,"~",2)
		set obj(contador,"ID")=botao
		set obj(contador,"NomeDoBotao")=nome
		set obj(contador,"Visualiza")=perm
		set contador = contador + 1
	}	
	do ##class(User.Search.PesquisasJson).ToJSON(.obj,1)
	quit
]]></Implementation>
</Method>

<Method name="getAbas">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set objParametro = $get(^VARParametroCliente(0,0,1))
	set menu = $$$VARParametroClienteNomedoMenuPrincipal(objParametro)
	if menu = "" set menu = " "
	;set menu = "VARMenuPrincipal"
	set YUSER = $Get(%request.Data("YUSER",1))
	set IDLinha = $Get(%request.Data("IDLinha",1))
	if IDLinha = "" quit
	set objWWW004 = $get(^WWW004(0, menu, IDLinha, 1))
	set rotina = $$$WWW004FormName(objWWW004)
	;
	kill ^VARCSPVARCadastraPerfilTemp2(YUSER)
	set ^VARCSPVARCadastraPerfilTemp2(YUSER) = rotina
	merge ^VARCSPVARCadastraPerfilTemp2(YUSER) = ^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"abas")
	;
	set contador = 0
	set aba = ""
	for {
		set aba = $order(^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"abas",aba)) quit:aba=""
		set objAba = $get(^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"abas",aba))
		set nome = $piece(objAba,"~",1)
		set perm = $piece(objAba,"~",2)
		set obj(contador,"ID")=aba
		set obj(contador,"NomeDaAba")=nome
		set obj(contador,"Visualiza")=perm
		set contador = contador + 1
	}	
	do ##class(User.Search.PesquisasJson).ToJSON(.obj,1)
	quit
]]></Implementation>
</Method>

<Method name="setTemporariaPermissao">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER,ID,valor</FormalSpec>
<Implementation><![CDATA[
	set ID = $piece(ID,"_",2,$length(ID,"_"))
	set ID = $replace(ID,"_",".")
	set $piece(^VARCSPVARCadastraPerfilTemp(YUSER,ID),"~",2)=valor
	set $piece(^VARCSPVARCadastraPerfilTemp(YUSER,ID),"~",4)=1
	quit
]]></Implementation>
</Method>

<Method name="getTemporariaPermissao">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER,ID</FormalSpec>
<Implementation><![CDATA[
	set ID = $replace(ID,"_",".")
	quit $piece($get(^VARCSPVARCadastraPerfilTemp(YUSER,ID)),"~",2)
]]></Implementation>
</Method>

<Method name="setTemporariaBotao">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER,ID,valor</FormalSpec>
<Implementation><![CDATA[
	set ID = $piece(ID,"_",2,$length(ID,"_"))
	set $piece(^VARCSPVARCadastraPerfilTemp2(YUSER,ID),"~",2)=valor
	set $piece(^VARCSPVARCadastraPerfilTemp2(YUSER,ID),"~",3)=1
	quit
]]></Implementation>
</Method>

<Method name="getTemporariaBotao">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER,ID</FormalSpec>
<Implementation><![CDATA[
	if (YUSER="")||(ID="") quit ""
	set ret = ""
	if $piece($get(^VARCSPVARCadastraPerfilTemp2(YUSER,ID)),"~",2)=1 set ret = 1
	quit ret
]]></Implementation>
</Method>

<Method name="setTemporariaAba">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER,ID,valor</FormalSpec>
<Implementation><![CDATA[
	set ID = $piece(ID,"_",2,$length(ID,"_"))
	set $piece(^VARCSPVARCadastraPerfilTemp2(YUSER,ID),"~",2)=valor
	set $piece(^VARCSPVARCadastraPerfilTemp2(YUSER,ID),"~",3)=1
	quit
]]></Implementation>
</Method>

<Method name="getTemporariaAba">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER,ID</FormalSpec>
<Implementation><![CDATA[
	if (YUSER="")||(ID="") quit ""
	set ret = ""
	if $piece($get(^VARCSPVARCadastraPerfilTemp2(YUSER,ID)),"~",2)=1 set ret = 1
	quit ret
]]></Implementation>
</Method>

<Method name="setBotoes">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER</FormalSpec>
<Implementation><![CDATA[
	set rotina = ^VARCSPVARCadastraPerfilTemp2(YUSER)
	set id=""
	for {
		set id = $order(^VARCSPVARCadastraPerfilTemp2(YUSER, id)) quit:id=""
		set ^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"botoes",id)=^VARCSPVARCadastraPerfilTemp2(YUSER,id)
	}
	kill ^VARCSPVARCadastraPerfilTemp2(YUSER)
	quit
]]></Implementation>
</Method>

<Method name="setAbas">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER</FormalSpec>
<Implementation><![CDATA[
	set rotina = ^VARCSPVARCadastraPerfilTemp2(YUSER)
	set id=""
	for {
		set id = $order(^VARCSPVARCadastraPerfilTemp2(YUSER, id)) quit:id=""
		set ^VARCSPVARCadastraPerfilTemp1(YUSER,rotina,"abas",id)=^VARCSPVARCadastraPerfilTemp2(YUSER,id)
	}
	kill ^VARCSPVARCadastraPerfilTemp2(YUSER)
	quit
]]></Implementation>
</Method>

<Method name="getPerfil">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER</FormalSpec>
<Implementation><![CDATA[
	Set parametro 	= $Get(%request.Data("parametro",1))
	set rs = ##class(%ResultSet).%New()  ;Create Result Set Object
	;
	set meuSQL = "SELECT CodModelo,NomedoModelo "
	set meuSQL = meuSQL_"from VARPerfilModelo "
	set meuSQL = meuSQL_"WHERE Company = 0 " ;and PerfisAplicados is not null and $find(PerfisAplicados,';')=0 "
	set:parametro'="" meuSQL = meuSQL_" and (%upper(CodModelo) like %upper('%"_parametro_"%') or %upper(NomedoModelo) like %upper('%"_parametro_"%'))"
	;set meuSQL = "SELECT SearchItem,Text "
	;set meuSQL = meuSQL_"from WWW101 "
	;set meuSQL = meuSQL_"WHERE ParameterDescription='BERECHTIGUNG' and Language1='PT' and (HideParameter is null or HideParameter <> 1) and SearchItem<'A'"
	;set:parametro'="" meuSQL = meuSQL_" and (%upper(Text) like %upper('%"_parametro_"%') or %upper(SearchItem) like %upper('%"_parametro_"%'))"
	;
	do rs.Prepare(meuSQL) ;Prepare Query
	do rs.Execute()       ;Execute Query

	kill obj
	set contador = 0
	while (rs.Next()) {      
	    set codPerfil = rs.GetData(1)
	    set descPerfil = rs.GetData(2)
		set obj(contador,"Codigo")=codPerfil
		set obj(contador,"Descricao")=descPerfil
		set contador = contador + 1
	}
	do ##class(User.Search.PesquisasJson).ToJSON(.obj,1)
]]></Implementation>
</Method>

<Method name="getDescricaoPerfil">
<ClassMethod>1</ClassMethod>
<FormalSpec>codPerfil</FormalSpec>
<Implementation><![CDATA[
	if codPerfil="" quit ""
	;set objPerfil = $get(^WWW101(0,"BERECHTIGUNG","PT",codPerfil,1))
	set objPerfil = $get(^VARPerfilModelo(0,codPerfil,1))
	quit $$$VARPerfilModeloNomedoModelo(objPerfil)
]]></Implementation>
</Method>

<Method name="salvaDados">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	$$$VAR
	Set YM = 0
	Set codPerfilModelo = $Get(%request.Data("codPerfil",1))
	Set Teste = $Get(%request.Data("codPerfil",1))
	Set descricao = $Get(%request.Data("descricao",1))
	set linhas	= $Get(%request.Data("linhas",1))
	Set YBED = $Get(%request.Data("YBED",1))
	Set YUSER = $Get(%request.Data("YUSER",1))
	set linhas = $replace(linhas,"""","")
	set linhas = $replace(linhas,"[","")
	set linhas = $replace(linhas,"]","")
	;
	tstart
	try {
		if codPerfilModelo="" {
			set codPerfilModelo = $$^WWWNEXT("VARPerfilModelo")
		}
		set objPerfilModelo = $get(^VARPerfilModelo(YM,codPerfilModelo,1))
		set codPerfil = $$$VARPerfilModeloPerfisAplicados(objPerfilModelo)
		if $find(codPerfil,";")>0 set codPerfil = ""
		;
		lock ^WWW101
		if codPerfil=""{
			set codPerfil = $O(^WWW101(0,"BERECHTIGUNG","PT","A"),-1)+1
			set ^COMTempList(0,YBED,"VARTCIAuthorizedProfiles",codPerfil,1)=descricao
		}
		;
		set idPerfil = "BERECHTIGUNG,PT,"_codPerfil
		;
		set objPerfil = $get(^WWW101(YM,"BERECHTIGUNG","PT",codPerfil,1))
		set $$$WWW101Text(objPerfil)=descricao
		set $$$WWW101SendForNewVersion(objPerfil)=1
		;
		set strStatus = $$$Save("WWW101",idPerfil,objPerfil,$$$OK)
		lock -^WWW101
		;
		set $$$VARPerfilModeloNomedoModelo(objPerfilModelo) = descricao
		set $$$VARPerfilModeloPerfisAplicados(objPerfilModelo) = codPerfil
		;k ^DashboardWidgetPerfil
		;k ^DashboardWidgetPerfilWidget
		;k ^DashboardWidgetPerfilWidgetSubWidget
		set strStatus = $$$Save("VARPerfilModelo",codPerfilModelo,objPerfilModelo,$$$OK)
		;
		set rotina = "VARTelaInicial"
		set idRotina = rotina
		set objRotina = $get(^WWW120(0,rotina,1))
		set nivelAcessoOld = $$$WWW120UserAccess(objRotina)
		set somenteLeituraOld = $$$WWW120ViewAccess(objRotina)
		set somenteLeituraNew = ..setPerfil(codPerfil,somenteLeituraOld,";")
		set nivelAcessoNew = ..setPerfil(codPerfil,nivelAcessoOld,";")
		set $$$WWW120UserAccess(objRotina)=nivelAcessoNew
		set $$$WWW120ViewAccess(objRotina)=somenteLeituraNew
		set strStatus = $$$Save("WWW120",idRotina,objRotina,$$$OK)	
		;
		set objParametro = $get(^VARParametroCliente(0,0,1))
		set menu = $$$VARParametroClienteNomedoMenuPrincipal(objParametro)
		if menu = "" set menu = " "
		;set menu = "VARMenuPrincipal"
		;;
		;; Armazena acesso ao menu principal
		;;
		set linhaAcesso = ","_linhas_","
		set nivelAnt = ""
		for {
			set nivelAnt = $order(^VARCSPVARCadastraPerfilTemp3(YUSER,nivelAnt)) quit:nivelAnt=""
			;
			set nivel1 = ","_$piece(nivelAnt,".",1)
			if $length(nivelAnt,".")>1 set nivel1 = nivel1_"."
			set nivel1 = nivel1_","
			;
			set nivel2 = ","_$piece(nivelAnt,".",1,2)
			if $length(nivelAnt,".")>2 set nivel2 = nivel2_"."
			set nivel2 = nivel2_","
			;
			set nivel3 = ","_$piece(nivelAnt,".",1,3)
			if $length(nivelAnt,".")>3 set nivel3 = nivel3_"."
			set nivel3 = nivel3_","
			;
			if (nivel1 = ",99999999.,") {
				if ($length(linhaAcesso,(","_nivelAnt_",")) < 2) {
					if ($piece(nivelAnt,".",2) = "" && ($piece(nivelAnt,".",3) = "")) {
						kill ^DashboardWidgetPerfil(0,codPerfilModelo)
						continue
					}
					if ($piece(nivelAnt,".",3) = "") {
						kill ^DashboardWidgetPerfilWidget(0,codPerfilModelo,$piece(nivelAnt,".",2))
						continue
					}
					kill ^DashboardWidgetPerfilWidgetSubWidget(0,codPerfilModelo,$piece(nivelAnt,".",2),$piece(nivelAnt,".",3))
					continue
				}
				continue	
			}
			;
			if ($length(linhaAcesso,nivel1)>1)||($length(linhaAcesso,nivel2)>1)||($length(linhaAcesso,nivel3)>1){
				set objWWW004 = $get(^WWW004(0, menu, nivelAnt, 1))
				set permOld = $$$WWW004UserAccess(objWWW004)
				set permNew = ..setPerfil(codPerfil,permOld,";")
				set $$$WWW004UserAccess(objWWW004) = permNew
				set idMenu = menu_","_nivelAnt
				set strStatus = $$$Save("WWW004",idMenu,objWWW004,$$$OK)	
				continue
			}
			if ($length(linhaAcesso,nivel1)<2)&&($length(linhaAcesso,nivel2)<2)&&($length(linhaAcesso,nivel3)<2){
				set objWWW004 = $get(^WWW004(0, menu, nivelAnt, 1))
				set permOld = $$$WWW004UserAccess(objWWW004)
				set permNew = ..delPerfil(codPerfil,permOld,";")
				set $$$WWW004UserAccess(objWWW004) = permNew
				set idMenu = menu_","_nivelAnt
				set strStatus = $$$Save("WWW004",idMenu,objWWW004,$$$OK)	
				continue
			}
		}
		;;
		;; Armazena permissão de acesso à rotina
		;;
		set nivel=""
		for {
			set nivel=$order(^VARCSPVARCadastraPerfilTemp(YUSER,nivel)) quit:nivel=""
			if ($piece(nivel,".",1)="99999999") {
				if ($piece(nivel,".",2) = "") { 
					if ($length((","_linhas_","),",99999999.,") > 1) {
						set ^DashboardWidgetPerfil(0,codPerfilModelo,1) =  1
						set nivelD = "99999999."
						for {
							set nivelD = $order(^VARCSPVARCadastraPerfilTemp(YUSER,nivelD)) 
							quit:nivelD=""
							if ($piece(nivelD,".",3)="") continue
							set objTemp = $get(^VARCSPVARCadastraPerfilTemp(YUSER,nivelD))
							set permissao = $piece(objTemp,"~",2)
							if permissao=" " continue
							set widget 		= $piece(nivelD,".",2)
							set subWidget	= $piece(nivelD,".",3)
							set ^DashboardWidgetPerfilWidgetSubWidget(0,codPerfilModelo,widget,subWidget,1) = permissao						
						}
					}
					continue	
				}
				if ($piece(nivel,".",3) = "") {
					if ($length(linhas,nivel) > 1){
						set widget = $piece(nivel,".",2)
						set ^DashboardWidgetPerfilWidget(0,codPerfilModelo,widget,1) = 1						
					}
					continue
				}
				set objTemp = $get(^VARCSPVARCadastraPerfilTemp(YUSER,nivel))
				set permissao = $piece(objTemp,"~",2)
				set alterado = $piece(objTemp,"~",4)
				if alterado'=1 continue
				if ($find(linhas,nivel) > 0){
					set widget 		= $piece(nivel,".",2)
					set subWidget	= $piece(nivel,".",3)
					set ^DashboardWidgetPerfilWidgetSubWidget(0,codPerfilModelo,widget,subWidget,1) = permissao						
				}
				continue	
			}
			;
			set objWWW004 = $get(^WWW004(0, menu, nivel, 1))
			set rotina = $$$WWW004FormName(objWWW004)
			if rotina = "" continue
			;
			set objTemp = $get(^VARCSPVARCadastraPerfilTemp(YUSER,nivel))
			set permissao = $piece(objTemp,"~",2)
			set alterado = $piece(objTemp,"~",4)
			if alterado'=1 continue
			;
			set objWWW004 = $get(^WWW004(0, menu, nivel, 1))
			set rotina = $$$WWW004FormName(objWWW004)
			;
			set idRotina = rotina
			set objRotina = $get(^WWW120(0,rotina,1))
			set nivelAcessoOld = $$$WWW120UserAccess(objRotina)
			set somenteLeituraOld = $$$WWW120ViewAccess(objRotina)
			;
			set idRotinaD = rotina_",0"
			set objRotinaD = $get(^WWW120D(0,rotina,0,1))
			set nivelAcessoDOld = $$$WWW120DUserAccess(objRotinaD)
			set somenteLeituraDOld = $$$WWW120DViewAccess(objRotinaD)
			;
			if permissao=1{
				set somenteLeituraNew = ..setPerfil(codPerfil,somenteLeituraOld,";")
				set nivelAcessoNew = ..setPerfil(codPerfil,nivelAcessoOld,";")
				;
				set somenteLeituraDNew = ..setPerfil(codPerfil,somenteLeituraDOld,";")
				set nivelAcessoDNew = ..setPerfil(codPerfil,nivelAcessoDOld,";")
				;
			}elseif permissao=2{
				set somenteLeituraNew = ..delPerfil(codPerfil,somenteLeituraOld,";")
				set nivelAcessoNew = ..setPerfil(codPerfil,nivelAcessoOld,";")
				;
				set somenteLeituraDNew = ..delPerfil(codPerfil,somenteLeituraDOld,";")
				set nivelAcessoDNew = ..setPerfil(codPerfil,nivelAcessoDOld,";")
				;
			}else{
				set somenteLeituraNew = ..delPerfil(codPerfil,somenteLeituraOld,";")
				set nivelAcessoNew = ..delPerfil(codPerfil,nivelAcessoOld,";")
				;
				set somenteLeituraDNew = ..delPerfil(codPerfil,somenteLeituraDOld,";")
				set nivelAcessoDNew = ..delPerfil(codPerfil,nivelAcessoDOld,";")
			}
			set $$$WWW120UserAccess(objRotina)=nivelAcessoNew
			set $$$WWW120ViewAccess(objRotina)=somenteLeituraNew
			set strStatus = $$$Save("WWW120",idRotina,objRotina,$$$OK)	
			;
			if objRotinaD'=""{
				set $$$WWW120DUserAccess(objRotinaD)=nivelAcessoDNew
				set $$$WWW120DViewAccess(objRotinaD)=somenteLeituraDNew
				set strStatus = $$$Save("WWW120D",idRotinaD,objRotinaD,$$$OK)	
			}
		}
		;;
		;; Armazena Permissões nos Botões e Abas
		;;
		set roti=""
		for {
			set roti=$order(^VARCSPVARCadastraPerfilTemp1(YUSER,roti)) quit:roti=""
			set tipo="" ; Tipo pode ser "botao" ou "aba"
			for {
				set tipo=$order(^VARCSPVARCadastraPerfilTemp1(YUSER,roti,tipo)) quit:tipo=""
				set nume=""
				for {
					set nume=$order(^VARCSPVARCadastraPerfilTemp1(YUSER,roti,tipo,nume)) quit:nume=""
					set objTemp = $get(^VARCSPVARCadastraPerfilTemp1(YUSER,roti,tipo,nume))
					set perm = $piece(objTemp,"~",2)
					set alteracao = $piece(objTemp,"~",3)
					if alteracao'=1 continue
					if tipo="botoes"{
						set objBotao = $get(^WWW124(0,roti,"PT",nume,1))
						set nivelAcessoOld = $$$WWW124Authorization1(objBotao)
						if perm=1{
							set idBotao = roti_",PT,"_nume
							if nivelAcessoOld="" {
								continue
							}
							set nivelAcessoNew = ..setPerfil(codPerfil,nivelAcessoOld,";")
							set $$$WWW124Authorization1(objBotao)=nivelAcessoNew
							set strStatus = $$$Save("WWW124",idBotao,objBotao,$$$OK)
						}else{
							set idBotao = roti_",PT,"_nume
							if nivelAcessoOld="" {
								set nivelAcessoNew = ..allPerfil(codPerfil,nivelAcessoOld,";")
							}else{
								set nivelAcessoNew = ..delPerfil(codPerfil,nivelAcessoOld,";")
							}
							set $$$WWW124Authorization1(objBotao)=nivelAcessoNew
							set strStatus = $$$Save("WWW124",idBotao,objBotao,$$$OK)
						}
					}elseif tipo="abas"{
						set objAba = $get(^WWW1203(0,roti,"PT",nume,1))
						set nivelAcessoOld = $$$WWW1203UsersAccess(objAba)
						set idAba = roti_",PT,"_nume
						if perm=1{
							if nivelAcessoOld="" {
								continue
							}
							set nivelAcessoNew = ..setPerfil(codPerfil,nivelAcessoOld,";")
							set $$$WWW1203UsersAccess(objAba)=nivelAcessoNew
							set strStatus = $$$Save("WWW1203",idAba,objAba,$$$OK)
						}else{
							if nivelAcessoOld="" {
								set nivelAcessoNew = ..allPerfil(codPerfil,nivelAcessoOld,";")
							}else{
								set nivelAcessoNew = ..delPerfil(codPerfil,nivelAcessoOld,";")
							}
							set $$$WWW1203UsersAccess(objAba)=nivelAcessoNew
							set strStatus = $$$Save("WWW1203",idAba,objAba,$$$OK)
						}
					}
				}
			}
		}
	}
	catch e {
		set strStatus = "Erro no armazenamento: "_e.Name _ " em " _ e.Location
		set strType = "error"
		set codPerfil = ""
		trollback
	}
	if strStatus=$$$OK{
		tcommit
		set strStatus="Dados armazenados com sucesso!"
		set strType = "success"
	}else{
		i $get(strType)=""{
			set strStatus="Erro no armazenamento dos dados: "_$get(strStatus)_"!"
			set strType = "error"
		}
	}
	;
	do ..LimpaRegistro(YUSER)
	;
	kill obj
	set obj(0,"id")=codPerfilModelo
	set obj(0,"retorno")=strStatus
	set obj(0,"type")=strType
	;
	do ##class(User.Search.PesquisasJson).ToJSON(.obj,1)
	quit
]]></Implementation>
</Method>

<Method name="setPerfil">
<ClassMethod>1</ClassMethod>
<FormalSpec>codPerfil,permOld,separador</FormalSpec>
<Implementation><![CDATA[
	kill obj
	for cont=1:1:$length(permOld,separador){
		set perf=$piece(permOld,separador,cont)
		if perf="" continue
		set obj(perf)=""
	}
	set obj(codPerfil)=""
	set permNew = ""
	set codperfil=""
	for {
		set codperfil = $order(obj(codperfil)) quit:codperfil=""
		set:permNew'="" permNew=permNew_separador
		set permNew = permNew_codperfil
	}	
	quit permNew
]]></Implementation>
</Method>

<Method name="delPerfil">
<ClassMethod>1</ClassMethod>
<FormalSpec>codPerfil,permOld,separador</FormalSpec>
<Implementation><![CDATA[
	kill obj
	for cont=1:1:$length(permOld,separador){
		set perf=$piece(permOld,separador,cont)
		if perf="" continue
		set obj(perf)=""
	}
	kill obj(codPerfil)
	set permNew = ""
	set codperfil=""
	for {
		set codperfil = $order(obj(codperfil)) quit:codperfil=""
		set:permNew'="" permNew=permNew_separador
		set permNew = permNew_codperfil
	}	
	quit permNew
]]></Implementation>
</Method>

<Method name="allPerfil">
<ClassMethod>1</ClassMethod>
<FormalSpec>codPerfil,permOld,separador</FormalSpec>
<Implementation><![CDATA[
	kill obj
	set perfil=""
	for {
		set perfil = $order(^WWW101(0,"BERECHTIGUNG","PT",perfil)) quit:perfil=""
		set obj(perfil)=""
	}
	kill obj(codPerfil)
	set permNew = ""
	set codperfil=""
	for {
		set codperfil = $order(obj(codperfil)) quit:codperfil=""
		set:permNew'="" permNew=permNew_separador
		set permNew = permNew_codperfil
	}	
	quit permNew
]]></Implementation>
</Method>

<Method name="desativaPerfil">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	quit
	$$$VAR
	Set YM = 0
	Set codPerfil = $Get(%request.Data("codPerfil",1))
	Set desativar = $Get(%request.Data("desativar",1))
	Set YUSER = $Get(%request.Data("YUSER",1))
	set idPerfil = "BERECHTIGUNG,PT,"_codPerfil
	set objPerfil = $get(^WWW101(YM,"BERECHTIGUNG","PT",codPerfil,1))
	set $$$WWW101HideParameter(objPerfil)=desativar
	set strStatus = $$$Save("WWW101",idPerfil,objPerfil,$$$OK)
	write 1
	quit
]]></Implementation>
</Method>

<Method name="LimpaRegistro">
<ClassMethod>1</ClassMethod>
<FormalSpec>YUSER</FormalSpec>
<Implementation><![CDATA[
	kill ^VARCSPVARCadastraPerfilTemp(YUSER)
	kill ^VARCSPVARCadastraPerfilTemp1(YUSER)
	kill ^VARCSPVARCadastraPerfilTemp2(YUSER)
	kill ^VARCSPVARCadastraPerfilTemp3(YUSER)
]]></Implementation>
</Method>
</Class>
</Export>