<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="SourceControl.control.RunAlphalincMac">
<ClassType/>
<ProcedureBlock>0</ProcedureBlock>
<Super>%RegisteredObject</Super>

<Method name="RunWWWSORT">
<ClassMethod>1</ClassMethod>
<FormalSpec>pClassNames:VAR.infra.util.Hashtable</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set status = $$$OK
	set strClassNames = ""

	do pClassNames.StartIterator()
    while (pClassNames.Next()) {
	    set strName = pClassNames.GetCurrentValue()
	    if ($length(strClassNames) = 0) {
		    set strClassNames = strName
	    }
	    else {
		    set strClassNames = strClassNames_","_strName
	    }
    }
	if ($length(strClassNames) '= 0) {
		if ($length($get(Y)) = 0) do ^WWWVAR
		set VORG(1) = strClassNames // FORMATO: "<CLASSE>,<CLASSE>,..." ou "<CLASSE>"
		do ^WWWSORT
	}
	else {
		set status = $$$ERROR(1,"No class loaded")
	}
	
	quit status
]]></Implementation>
</Method>

<Method name="RunGenerateCOMConst">
<ClassMethod>1</ClassMethod>
<FormalSpec>pModuleNames:VAR.infra.util.Hashtable</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set status = $$$OK
	set strModuleNames = ""

	do pModuleNames.StartIterator()
    while (pModuleNames.Next()) {
	    if ($length($get(Y)) = 0) do ^WWWVAR

	    set strName = pModuleNames.GetCurrentValue()
	    do Generate^COMConst(strName)
    }

	quit status
]]></Implementation>
</Method>

<Method name="ConvertMenuToCustomized">
<ClassMethod>1</ClassMethod>
<FormalSpec>idMenu:%String</FormalSpec>
<Implementation><![CDATA[
	new idItemMenu, objWWW004, strStatus

	do ^WWWVAR
	set YUSER = "TEST"
	set YBED = "TEST"

	set idItemMenu = $order(^WWW004(0, idMenu, ""))
	while ($length(idItemMenu) > 0) {
		set objWWW004 = ^WWW004(0, idMenu, idItemMenu, 1)
		if ('$data(^WWW004(0, "VAR"_idMenu, idItemMenu, 1))) {
			write !, "Creating menu 'VAR"_idMenu_","_idItemMenu_"': "_objWWW004
			set strStatus = $$Save^COMUtils("WWW004",
				"VAR"_idMenu_","_idItemMenu, objWWW004, 1)
			if ('strStatus) {
				write !, "Error: "_$$DecodeError^COMUtilError(strStatus)
				quit
			}
		}

		set idItemMenu = $order(^WWW004(0, idMenu, idItemMenu))
	}

	if ('$data(^WWW00411(0, "VAR"_idMenu, "PT", 1))) {
		set strDescricao = ""
		for i = 1:1:$length(idMenu) {
			if ((i > 1) &&
				($ascii($extract(idMenu, i, i)) >= $ascii("A")) &&
				($ascii($extract(idMenu, i, i)) <= $ascii("Z"))) {
				set strDescricao = strDescricao_" "
			}

			set strDescricao = strDescricao_$extract(idMenu, i, i)
		}

		write !, "Creating translation for menu 'VAR"_idMenu_",PT': "_strDescricao
		set strStatus = $$Save^COMUtils("WWW00411", "VAR"_idMenu_",PT",
			strDescricao, 1)
		if ('strStatus) {
			write !, "Error: "_$$DecodeError^COMUtilError(strStatus)
			quit
		}
	}

	write !!, "Menu '"_idMenu_"' converted."
]]></Implementation>
</Method>

<Method name="RemoveMenu">
<ClassMethod>1</ClassMethod>
<FormalSpec>idMenu:%String</FormalSpec>
<Implementation><![CDATA[
	new idItemMenu, objWWW004, strStatus

	do ^WWWVAR
	set YUSER = "TEST"
	set YBED = "TEST"

	set idItemMenu = $order(^WWW004(0, idMenu, ""))
	while ($length(idItemMenu) > 0) {
		set objWWW004 = ^WWW004(0, idMenu, idItemMenu, 1)
		if ($data(^WWW004(0, idMenu, idItemMenu, 1))) {
			write !, "Removing menu '"_idMenu_","_idItemMenu_"'"
			set strStatus = $$KILL^COMUtils("WWW004", idMenu_","_idItemMenu)
			if ('strStatus) {
				write !, "Error: "_$$DecodeError^COMUtilError(strStatus)
				quit
			}
		}

		set idItemMenu = $order(^WWW004(0, idMenu, idItemMenu))
	}

	write !!, "Menu '"_idMenu_"' removed."
]]></Implementation>
</Method>

<Method name="ReindexAllButtons">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	do ^WWWVAR
	set VORG(1) = "WWW124"
	set YQ = 1
	do ^WWWSORT
]]></Implementation>
</Method>
</Class>
</Export>