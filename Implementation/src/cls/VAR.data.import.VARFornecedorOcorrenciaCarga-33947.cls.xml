<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.data.import.VARFornecedorOcorrenciaCarga">
<ClassType/>
<Import>VAR.infra.integracao</Import>
<IncludeCode>VARConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>AbstractCarga</Super>

<Parameter name="TIPOARQUIVONENHUM">
<Description><![CDATA[Comandos:
> do ##class(VAR.data.import.VARFornecedorOcorrenciaCarga).carregarArquivo1()
> do ##class(VAR.data.import.VARFornecedorOcorrenciaCarga).carregarArquivo2()

Campos:
  Arquivo 1:
    CD_FORNECEDOR;NR_SEQUENCIA;CD_OCORRENCIA;CD_FUNCIONARIO;
      DT_OCORRENCIA;DS_COMPLEMENTO;IN_AUTOMATICO;SERIE;NR_NF

  Arquivo 2:
    CDQ;Data1;Cód. Fornecedor;Fornecedor;Entrega;N.F.;Emissao;
      Qtd Recebida1;Descricao1;Qtd Segregada1;M_1_Ocorrência;
      M_2_Ocorrência;M_3_Ocorrência;M_4_Ocorrência;Motivo;
      Parecer;Ação;Data2;Responsavel]]></Description>
<Type>%Integer</Type>
<Default>0</Default>
</Parameter>

<Parameter name="TIPOARQUIVO1">
<Type>%Integer</Type>
<Default>1</Default>
</Parameter>

<Parameter name="TIPOARQUIVO2">
<Type>%Integer</Type>
<Default>2</Default>
</Parameter>

<Parameter name="DEBUG">
<Type>%Integer</Type>
<Default>0</Default>
</Parameter>

<Property name="tipoArquivo">
<Type>%Integer</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Method name="carregarArquivo1">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	do ..importar(##class(DirectoryFile).GetNamespaceSubdir("carga")_
		"Registro ocorrencias - InfoSaúde.csv")]]></Implementation>
</Method>

<Method name="carregarArquivo2">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	do ..importar(##class(DirectoryFile).GetNamespaceSubdir("carga")_
		"Planilha de CDQ.csv")]]></Implementation>
</Method>

<Method name="%OnNew">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	if (..#DEBUG) set ..imprimirAndamento = 0

	set total = 0
	set codFornecedor = $order(^VARFornecedorOcorrencia(YM, ""))
	while ($length(codFornecedor) > 0) {
		set codOcorrencia = $order(^VARFornecedorOcorrencia(YM, codFornecedor, ""))
		while ($length(codOcorrencia) > 0) {
			set total = $increment(total)
			set codOcorrencia = $order(^VARFornecedorOcorrencia(YM, codFornecedor, codOcorrencia))
		}

		set codFornecedor = $order(^VARFornecedorOcorrencia(YM, codFornecedor))
	}

	write !, "Existem "_total_" registros na global ^VARFornecedorOcorrencia. "
	read "Deseja continuar? (S/N) ", continuar#1

	if ((continuar '= "S") && (continuar '= "s")) quit $$$CUSTOMERROR("Operação abortada.")

	quit ##super()]]></Implementation>
</Method>

<Method name="processar">
<FormalSpec>pintRegistros:%Integer,pstrLinha:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set $ztrap = "exceptionProc"
	set strStatus = $$$OK

	if (pintRegistros = 1) {
		if ($length(pstrLinha, ..separador) = 9) {
			set ..tipoArquivo = ..#TIPOARQUIVO1
		}
		elseif ($length(pstrLinha, ..separador) = 19) {
			set ..tipoArquivo = ..#TIPOARQUIVO2
		}

		quit strStatus
	}

	set obj = ""

	if (..tipoArquivo = ..#TIPOARQUIVO1) {
		set codFornecedor = ..getCampo(pstrLinha, 1)
		set strNDocIntegrado = ..getCampo(pstrLinha, 2)

		set strMotivo = ..getCampo(pstrLinha, 3)
		set strMotivo = ..conversaoMotivoAlphalinc(strMotivo)
		if ($length(strMotivo) = 0) {
			quit "0 Motivo '"_..getCampo(pstrLinha, 3)_"' da linha #"_
				pintRegistros_" não reconhecido"
		}

		set datOcorrencia = ..getCampo(pstrLinha, 5)
		set datOcorrencia = $piece(datOcorrencia, " ", 1)
		set datOcorrencia = $translate(datOcorrencia, "/", " ")
		set strPrefixoMesIngles = ..conversaoPrefixoMesIngles(
			$piece(datOcorrencia, " ", 2))
		if ($length(strPrefixoMesIngles) = 0) {
			quit "0 Data '"_..getCampo(pstrLinha, 5)_"' da linha #"_
				pintRegistros_" está num formato inválido"
		}
		set $piece(datOcorrencia, " ", 2) = strPrefixoMesIngles
		set datOcorrencia = $zdateh(datOcorrencia, 2)

		set strObservacoes = ..getCampo(pstrLinha, 6)
		set strObservacoes = ##class(Strings).FullReplace(strObservacoes,
			"$$$", $char(10))
		if ($extract(strObservacoes, 1, 1) = $char(10)) {
			set strObservacoes = ""
		}

		set strNumeroNotaFiscal = ..getCampo(pstrLinha, 9)
		set strSerie = ..getCampo(pstrLinha, 8)

		if (($length(strNumeroNotaFiscal) > 0) &&
			($length(strSerie) > 0)) {
			set strNotaFiscal =
				"Nota Fiscal: "_strNumeroNotaFiscal_" - "_
					"Série: "_strSerie
		}
		else {
			set strNotaFiscal = ""
		}

		if (($length(strObservacoes) > 0) &&
			($length(strNumeroNotaFiscal) > 0) &&
			($length(strSerie) > 0)) {
			set strQuebraAdicional = $char(10)
		}
		else {
			set strQuebraAdicional = ""
		}

		set strObservacoes =
			strObservacoes_strQuebraAdicional_strNotaFiscal
	}
	elseif (..tipoArquivo = ..#TIPOARQUIVO2) {
		set strLin = $piece(pstrLinha, ..separador, 2,
			$length(pstrLinha, ..separador))
		set strLin = $translate(strLin, ";", "")
		if ($length(strLin) = 0) quit $$$OK

		set codFornecedor = ..getCampo(pstrLinha, 3)
		if ($length(codFornecedor) = 0) {
			quit "0 Fornecedor em branco na linha #"_pintRegistros
		}
		set strNDocIntegrado = ""

		set strMotivo = 51

		set datOcorrencia = ..getCampo(pstrLinha, 2)
		set datOcorrencia = $zdateh(datOcorrencia, 4)

		set strProduto = ..getCampo(pstrLinha, 9)
		set strDetalhesDesvio = ..getCampo(pstrLinha, 15)
		set str1Ocorrencia = ..getCampo(pstrLinha, 11)
		set str2Ocorrencia = ..getCampo(pstrLinha, 12)
		set str3Ocorrencia = ..getCampo(pstrLinha, 13)
		set str4Ocorrencia = ..getCampo(pstrLinha, 14)
		set strQuantidadeRecebida = ..getCampo(pstrLinha, 8)
		set strQuantidadeSegregada = ..getCampo(pstrLinha, 10)
		set strDatasEntrega = ..getCampo(pstrLinha, 5)
		set strNotasFiscais = ..getCampo(pstrLinha, 6)
		set strNotasEmitidasEm = ..getCampo(pstrLinha, 7)
		set strParecerFornecedor = ..getCampo(pstrLinha, 16)
		set strAcaoTomada = ..getCampo(pstrLinha, 17)
		set strResponsavel = ..getCampo(pstrLinha, 19)

		if (##class(Strings).UpperCase(str1Ocorrencia) = "X") {
			set strOcorrencia = "Primeira Ocorrência"
		}
		elseif (##class(Strings).UpperCase(str2Ocorrencia) = "X") {
			set strOcorrencia = "Segunda Ocorrência"
		}
		elseif (##class(Strings).UpperCase(str3Ocorrencia) = "X") {
			set strOcorrencia = "Terceira Ocorrência"
		}
		elseif (##class(Strings).UpperCase(str4Ocorrencia) = "X") {
			set strOcorrencia = "Quarta Ocorrência"
		}
		else {
			set strOcorrencia = ""
		}

		set strDatasEntrega = ..getCampo(pstrLinha, 5)
		set strNotasFiscais = ..getCampo(pstrLinha, 6)
		set strNotasEmitidasEm = ..getCampo(pstrLinha, 7)
		if ($length(strDatasEntrega, ",") = 1) {
			set strEntregas =
				"Entrega - "_
					"Data: "_strDatasEntrega_" "_
					"Nota Fiscal: "_strNotasFiscais_" "_
					"Emissão: "_strNotasEmitidasEm_$char(10)
		}
		else {
			set strEntregas = ""
			for i = 1:1:$length(strDatasEntrega, ",") {
				set strEntregas = strEntregas_
					"Entrega "_i_" - "_
						"Data: "_$zstrip($piece(strDatasEntrega, ",", i), "<>W")_", "_
						"Nota Fiscal: "_$zstrip($piece(strNotasFiscais, ",", i), "<>W")_", "_
						"Emissão: "_$zstrip($piece(strNotasEmitidasEm, ",", i), "<>W")_$char(10)
			}
		}

		if ($length(strParecerFornecedor) > 0) {
			set strParecerFornecedor =
				"Parecer do Fornecedor: "_strParecerFornecedor_$char(10)
		}
		if ($length(strAcaoTomada) > 0) {
			set strAcaoTomada =
				"Ação Tomada: "_strAcaoTomada_$char(10)
		}

		if (($length(strParecerFornecedor) > 0) ||
			($length(strAcaoTomada) > 0)) {
			set strQuebraAdicional = $char(10)
		}
		else {
			set strQuebraAdicional = ""
		}

		set strObservacoes =
			"Produto: "_strProduto_$char(10)_
			"Detalhes do Desvio: "_strDetalhesDesvio_$char(10)_
			"Recorrência: "_strOcorrencia_$char(10)_
			$char(10)_
			"Quant. Recebida: "_strQuantidadeRecebida_", "_
				"Quant. Segregada: "_strQuantidadeSegregada_$char(10)_
			strEntregas_
			$char(10)_
			strParecerFornecedor_
			strAcaoTomada_
			strQuebraAdicional_
			"Responsável: "_strResponsavel
	}

	set codFornecedorAlphalinc = ##class(ConversaoInfoSaude).GetFornecedorAlphalinc(codFornecedor)
	if ($length(codFornecedorAlphalinc) = 0) {
		quit "0 Fornecedor '"_codFornecedor_"' desconhecido"
	}

	set codOcorrencia = $$^WWWNEXT1("VARFornecedorOcorrencia",
		codFornecedorAlphalinc, 2)
	set key = codFornecedorAlphalinc_$$$COMMA_codOcorrencia

	set $$$VARFornecedorOcorrenciaNDocIntegrado(obj) = strNDocIntegrado
	set $$$VARFornecedorOcorrenciaMotivo(obj) = strMotivo
	set $$$VARFornecedorOcorrenciaData(obj) = datOcorrencia
	set $$$VARFornecedorOcorrenciaObservacoes(obj) = strObservacoes
	set $$$VARFornecedorOcorrenciaResponsavel(obj) = YBED
	set $$$VARFornecedorOcorrenciaCarga(obj) = $$$YES

	if (..#DEBUG) {
		do ..imprimir(..quebraLinha_"key="_key_", obj="_obj)
		set strStatus = $$$OK
	}
	else {
		set strStatus = $$$Save("VARFornecedorOcorrencia", key, obj, 1)
	}

finallyProc
	quit strStatus

exceptionProc
 	set $ztrap = ""
	set strStatus = $$$CUSTOMERROR($zerror)

	goto finallyProc]]></Implementation>
</Method>

<Method name="conversaoMotivoAlphalinc">
<FormalSpec>pstrMotivoInfoSaude:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	if (pstrMotivoInfoSaude = "2" ) quit "13"
	if (pstrMotivoInfoSaude = "3" ) quit "1"
	if (pstrMotivoInfoSaude = "4" ) quit "5"
	if (pstrMotivoInfoSaude = "5" ) quit "3"
	if (pstrMotivoInfoSaude = "6" ) quit "4"
	if (pstrMotivoInfoSaude = "7" ) quit "6"
	if (pstrMotivoInfoSaude = "8" ) quit "9"
	if (pstrMotivoInfoSaude = "9" ) quit "10"
	if (pstrMotivoInfoSaude = "10") quit "11"
	if (pstrMotivoInfoSaude = "11") quit "12"
	if (pstrMotivoInfoSaude = "12") quit "7"

	quit ""]]></Implementation>
</Method>

<Method name="conversaoPrefixoMesIngles">
<FormalSpec>pstrPrefixoMesPortugues:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set idMes = $order(^WWW101(0, "COMMONTHSOFYEAR", "PT", ""))
	set idMesEncontrado = ""
	while ($length(idMes) > 0) {
		set strMesPortugues = $$$WWW101Text(^WWW101(0, "COMMONTHSOFYEAR", "PT", idMes, 1))
		if (##class(Strings).UpperCase($extract(strMesPortugues, 1, 3)) =
				##class(Strings).UpperCase(pstrPrefixoMesPortugues)) {
			set idMesEncontrado = idMes
			quit
		}

		set idMes = $order(^WWW101(0, "COMMONTHSOFYEAR", "PT", idMes))
	}

	if ($length(idMesEncontrado) > 0) {
		set strPrefixoMesIngles = $$$WWW101Text(^WWW101(0, "COMMONTHSOFYEAR", "EN", idMesEncontrado, 1))
		set strPrefixoMesIngles = $extract(strPrefixoMesIngles, 1, 3)
	}
	else {
		set strPrefixoMesIngles = ""
	}

	quit strPrefixoMesIngles]]></Implementation>
</Method>
</Class>
</Export>