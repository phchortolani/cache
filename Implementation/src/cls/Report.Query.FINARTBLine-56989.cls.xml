<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="Report.Query.FINARTBLine">
<ClassType>persistent</ClassType>
<ProcedureBlock>1</ProcedureBlock>
<Super>%Persistent</Super>

<Method name="FINARTBLineClose">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;qHandle:%Binary</FormalSpec>
<PlaceAfter>FINARTBLineExecute</PlaceAfter>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Quit $$$OK
]]></Implementation>
</Method>

<Method name="FINARTBLineExecute">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;qHandle:%Binary,CompanyNumber:%String,LocationNumber:%String,Language:%String,TrialBalanceDate:%Integer,YUSER:%String,InvoiceDateType:%String,SortBy:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set idx=""
	set idCustomer=""
	for  {
		set idCustomer=$order(^CacheTempCustomers(YUSER,idCustomer))
		quit:(idCustomer="") //SR14451 Use CacheTemp
		
		set lstRow=$$GetCustomerRow^FINRPARTB(idCustomer,TrialBalanceDate,InvoiceDateType,CompanyNumber,LocationNumber,Language)
		
		continue:$listget(lstRow,8)=0 ;skip zero balance customers
		
		set idx=idx+1
		
		; Row Spec
		; 1. CustomerNumber
		; 2. CustomerName
		; 3. CustomerGroup
		; 4. CurrentAmount
		; 5. OneMonthAmount
		; 6. TwoMonthAmount
		; 7. ThreeMonthAmount
		; 8. Balance
		
		if (SortBy=1) { 			;1 Month
			set arrOrder($job,$listget(lstRow,5),idx)=lstRow
		} elseif (SortBy=2) {		;2 Month2
			set arrOrder($job,$listget(lstRow,6),idx)=lstRow
		} elseif (SortBy=3) {		;3 Months
			set arrOrder($job,$listget(lstRow,7),idx)=lstRow
		} elseif (SortBy=4) {		;Balance
			set arrOrder($job,$listget(lstRow,8),idx)=lstRow
		} elseif (SortBy=5) {		;Current Amount
			set arrOrder($job,$listget(lstRow,4),idx)=lstRow
		} elseif (SortBy=6) {		;Name
			set strName=$listget(lstRow,2)
			if strName="" set strName=" "
			set arrOrder($job,strName,idx)=lstRow
		} elseif (SortBy=7) {		;Number
			set arrOrder($job,$listget(lstRow,1),idx)=lstRow
		}
	}
	
	set strLoop=""
	set intCount=0
	
	for {
		set strLoop=$order(arrOrder($job,strLoop))
		quit:(strLoop="")
		
		set idx=""
		for {
			set idx=$order(arrOrder($job,strLoop,idx))
			quit:(idx="")
		
			set intCount=intCount+1	
			set lstRow=$get(arrOrder($job,strLoop,idx))	
			
			set ^CacheTempRow($job,intCount)=lstRow
		}	
	}
	
	set qHandle("Item")=""
	
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="FINARTBLineFetch">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;qHandle:%Binary,&amp;Row:%List,&amp;AtEnd:%Integer=0</FormalSpec>
<PlaceAfter>FINARTBLineExecute</PlaceAfter>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set qHandle("Item")=$order(^CacheTempRow($job,qHandle("Item")))
	
	if qHandle("Item")'="" {
		set Row=^CacheTempRow($job,qHandle("Item"))
	} else {
		set AtEnd=1
		set Row=""
		kill ^CacheTempRow($job)
	}
	
	Quit $$$OK
]]></Implementation>
</Method>
<Query name="FINARTBLine">
<Type>%Query</Type>
<FormalSpec>CompanyNumber:%String,LocationNumber:%String,Language:%String,TrialBalanceDate:%Integer,YUSER:%String,InvoiceDateType:%String,SortBy:%String</FormalSpec>
<SqlProc>1</SqlProc>

<Parameter name="ROWSPEC" value="CustomerNumber:%String,CustomerName:%String,CustomerGroup:%String,CurrentAmount:%Float,OneMonthAmount:%Float,TwoMonthAmount:%Float,ThreeMonthAmount:%Float,Balance:%Float"/>
</Query>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DataLocation>^Report.Query.FINARTBLineD</DataLocation>
<DefaultData>FINARTBLineDefaultData</DefaultData>
<IdLocation>^Report.Query.FINARTBLineD</IdLocation>
<IndexLocation>^Report.Query.FINARTBLineI</IndexLocation>
<StreamLocation>^Report.Query.FINARTBLineS</StreamLocation>
<Data name="FINARTBLineDefaultData">
<Structure>listnode</Structure>
<Subscript/>
<Value name="1">
<Value>%%CLASSNAME</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>