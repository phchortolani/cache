<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.EnsBPInstanceTablePane">
<Description><![CDATA[
Specialized pane for displaying a list Business Process Instances.]]></Description>
<ClassType/>
<IncludeCode>Ensemble</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%CSP.Util.TablePane</Super>
<System>3</System>

<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Method name="CreateResultSet">
<FormalSpec>&amp;pID:%String,&amp;pRS:%ResultSet</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set $ZT = "Trap"
	Set tSC = $$$OK

	// get request parms via object
	// If this is a refresh, then set the saved session data for the filter into to %request.
	Set nm = ""
	For  set nm=$O(%session.Data("EnsBPInstanceFilterForm",nm)) q:nm=""  Set %request.Data(nm,1)=%session.Data("EnsBPInstanceFilterForm",nm,1)
	Set tRequest = ##class(CSPX.EnsBPInstanceFilterForm).ProcessSubmit(%request, .pID)

	If (tRequest.BusinessProcess '= "") {
		Set bpcls = tRequest.BusinessProcess
	}
	Else {
		Set bpcls = "Ens.BusinessProcess"
	}
	
	// get table name
	Set table = $$$comClassKeyGet(bpcls,$$$cCLASSsqlqualifiednameQ)
	If (table = "") {
		// this class is probably not compiled: skip it
		Quit $$$OK
	}
	
	// test for context
	Set hasContext = 0
	If ($zobjclassmethod(bpcls,"%IsA","Ens.BusinessProcessBPL")) {
		Set hasContext = 1
	}

	// where clause	
	Set where = ""
	If (tRequest.StartTime '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "%TimeCreated >= '" _ tRequest.StartTime _ "'"
	}
	
	If (tRequest.EndTime '= "") {
		Set where = where _ $S(where="":"",1:" AND ") _ "%TimeCreated <= '" _ tRequest.EndTime _ "'"
	}
	
	Set:where'="" where = " WHERE " _ where
	
	Set dateLen = 12
	Set:tRequest.TimeFormat'="" dateLen = tRequest.TimeFormat

	// list of properties
	Set select = "%ID As ID, %IsCompleted As IsCompleted," _
		"$Piece(x__ClassName,'~',$Length(x__ClassName,'~')-1) As ClassName," _
		"%SessionId As SessionId,"_
		"%PrimaryRequestHeader As PrimaryRequest,"_
		"{fn RIGHT(%EXTERNAL(%TimeCreated)," _ dateLen _ " )} As TimeCreated,"_
		"{fn RIGHT(%EXTERNAL(%TimeCompleted)," _ dateLen _ " )} As TimeCompleted"

	If (hasContext) {
		Set select = select _ ",%Context As ContextId"
	}

	Set maxRows = tRequest.MaxRows
	Set:(+maxRows<=0) maxRows = 50
	Set:(+maxRows>500) maxRows = 500

	Set order = " ORDER BY ID"
	If (tRequest.SortOrder = 2) {
		Set order = " ORDER BY ID Desc"
	}
	
	Set query = "SELECT TOP " _ maxRows _ " " _ select _ 
				" FROM " _ table _ where _ order
	
	Set pRS = ##class(%ResultSet).%New()
	Set tSC = pRS.Prepare(query)

Done	
	Quit tSC
Trap
	Set $ZT="",tSC=$$$EnsSystemError
	Goto Done
]]></Implementation>
</Method>
</Class>
</Export>