<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.CSP.FATModeloNotaFiscal">
<ClassType/>
<IncludeCode>VARConst,INConst,FATConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	$$$VAR
	#dim %request as %CSP.Request
	#dim %response as %CSP.Response
	quit:$$$ISERR(..validaSessao(1)) %response
	set job = %session.SessionId
	set method = $get(%request.Data("method",1))
	Xecute "do .."_method_"()"
	Quit $$$OK]]></Implementation>
</Method>

<Method name="getINPARA">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	set chave = $get(%request.Data("chave",1))
	set retorno = []
	set codigo=""
	for{
		set codigo 	= $order(^INPARA(0,chave,"PT",codigo)) quit:codigo=""
		set obj 	= $get(^INPARA(0,chave,"PT",codigo,1)) 
		set linha = {}
		set linha.codigo = codigo
		set linha.descricao = $$$INPARAText(obj)
		do retorno.%Push(linha)
	}
	write retorno.%ToJSON()
	quit]]></Implementation>
</Method>

<Method name="pesquisaModelos">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	set filter = $get(%request.Data("filter",1))
	set codigo = $get(%request.Data("codigo",1))
	Set take 	= $Get(%request.Data("take",1))
	Set skip 	= $Get(%request.Data("skip",1))
	Set sort 	= $Get(%request.Data("sort",1))
	Set searchValue = $Get(%request.Data("searchValue",1))
	;
	set sort = $tr(sort,"""{[]}","")
	set campoSort = $REPLACE($piece($piece(sort,",",1),":",2),"""","")
	set campoSortDesc = $select($piece($piece(sort,",",2),":",2)="false":"DESC",1:"ASC")
	set orderBy = ""
	
	if campoSort'="" set orderBy = "order by "_campoSort_" "_campoSortDesc
	
	set filter = $tr(filter,"""{[]}","")
	set campo = "",operacao = "",wherefilter="",proximacomb="",andant="",abrep=0
	for i=1:1:$length(filter,","){
		set ret = $piece(filter,",",i)
		if ret="" continue
		if proximacomb=1{
			if andant=1,ret="and" set wherefilter = wherefilter_" ) ",abrep=0
			if wherefilter'="" set wherefilter = wherefilter_" "_ret_" "
			set andant = ""
			if ret="and" set wherefilter = wherefilter_" ( ",andant = 1,abrep=1
			set proximacomb = ""
			continue
		}
		if campo="" {
			if ret = "and" continue
			set campo = ret continue
		}
		if operacao="" {
			if ret = "contains" set operacao = " like ",inicio="%",fim="%"
			if ret = "notcontains" set operacao = " not like ",inicio="%",fim="%"
			if ret = "startswith" set operacao = " like ",inicio="",fim="%"
			if ret = "endswith" set operacao = " like ",inicio="%",fim=""
			if ret = "=" set operacao = " = ",inicio="",fim=""
			if ret = "<>" set operacao = " <> ",inicio="",fim=""
			continue
		}
		set ret = $replace(ret," ","")
		set wherefilter = wherefilter_"%upper("_campo_")"_operacao_"%upper('"_inicio_ret_fim_"')"
		set campo="",operacao="",proximacomb=1
	}
	if wherefilter'="" {
		if abrep=1 set wherefilter = wherefilter_")"
	}
	;
	set contador = 0
	set select1 = "select CodigodoModelo,Nome,Localidade,Case when Ativo=1 then 'Ativo' else 'Inativo' end Ativo"
	set select1 = select1_" from  FATModeloNotaFiscal"
	set select1 = select1_" where Company=0 "
	if codigo '= "" set select1 = select1_" and CodigodoModelo = '"_codigo_"' "
	;
	if wherefilter'="" set wherefilter = "where "_wherefilter
	set take = skip + take
	set skip = skip + 1
	set top = take
	if top<=0 set top = 200
	set meuSelect="select * from ("
	set meuSelect=meuSelect_"select top "_top_" *,count(*) totalCount "
	set meuSelect=meuSelect_" from ("_select1_") "_wherefilter_" "_orderBy
	set meuSelect=meuSelect_")"
	if take>0 set meuSelect=meuSelect_" where %VID between "_skip_" and "_take
	set rs = ##class(%SQL.Statement).%ExecDirect("",meuSelect)
	set array = []
	set totalCount = 0
	while(rs.%Next()){
		set CodigodoModelo		= rs.%Get("CodigodoModelo")
		set Nome				= rs.%Get("Nome")
		set Localidade			= rs.%Get("Localidade")
		set Ativo				= rs.%Get("Ativo")
		set totalCount			= rs.%Get("totalCount")
		set linha = {}
		set linha.codigodomodelo	= CodigodoModelo
		set linha.nome 				= Nome
		set linha.localidade		= Localidade
		set linha.ativo				= Ativo
		do array.%Push(linha)
	}
	set retorno = {}
	set retorno.data = array
	set retorno.totalCount = totalCount
	write retorno.%ToJSON()
	quit $$$OK]]></Implementation>
</Method>

<Method name="recModelo">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	set codigo = $get(%request.Data("codigo",1))
	set formGeral = {}
	set formControle = {}
	set formGeral.situacao		= 1
	if codigo'=""{
		set objModelo = $get(^FATModeloNotaFiscal(YM,codigo,1))
		set formGeral.nome			= $$$FATModeloNotaFiscalNome(objModelo)
		set formGeral.localidade 	= $$$FATModeloNotaFiscalLocalidade(objModelo)
		set formGeral.situacao		= +$$$FATModeloNotaFiscalAtivo(objModelo)
		set formGeral.formato		= $$$FATModeloNotaFiscalFormatodaEmissao(objModelo)
		set tributos		= $$$FATModeloNotaFiscalTributos(objModelo)
		set arrayTributos = []
		for i=1:1:$length(tributos,";"){
			set tributo = $piece(tributos,";",i)
			if tributo="" continue
			do arrayTributos.%Push(tributo)
		}
		set formGeral.tributos		= arrayTributos
		set formGeral.discriminacao	= $$$FATModeloNotaFiscalDiscriminacaodosServicos(objModelo)
		
		;
		set formControle.criadoEm				= $$$FATModeloNotaFiscalCriadoEm(objModelo)
		if formControle.criadoEm'="" set formControle.criadoEm = $zdatetime(formControle.criadoEm,4)
		set formControle.criadoPor				= $$$FATModeloNotaFiscalCriadoPor(objModelo)
		set formControle.alteradoEm				= $$$FATModeloNotaFiscalAlteradoEm(objModelo)
		if formControle.alteradoEm'="" set formControle.alteradoEm = $zdatetime(formControle.alteradoEm,4)
		set formControle.alteradoPor			= $$$FATModeloNotaFiscalAlteradoPor(objModelo)
	}
	set retorno = {}
	set retorno.formGeral 		= formGeral
	set retorno.formControle 	= formControle
	write retorno.%ToJSON()
	quit]]></Implementation>
</Method>

<Method name="getModelos">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	set select = "select Codigodomodelo,Nome from FATModeloNotaFiscal where Company=0 "
	set retorno = []
	set rs = ##class(%SQL.Statement).%ExecDirect("",select)
	while(rs.%Next()){
		set linha={}
		set linha.codigo = rs.%Get("Codigodomodelo")
		set linha.descricao = "("_linha.codigo_") "_rs.%Get("Nome")
		do retorno.%Push(linha)
	}
	write retorno.%ToJSON()
	quit $$$OK]]></Implementation>
</Method>

<Method name="listTributos">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	set select = "select Codigodotributo,Nome from FATTributo where Company=0 and Ativo = 1"
	set retorno = []
	set rs = ##class(%SQL.Statement).%ExecDirect("",select)
	while(rs.%Next()){
		set linha={}
		set linha.codigo = rs.%Get("Codigodotributo")
		set linha.descricao = rs.%Get("Nome")
		do retorno.%Push(linha)
	}
	write retorno.%ToJSON()
	quit $$$OK]]></Implementation>
</Method>

<Method name="salvar">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	set modelo			= $get(%request.Data("modelo",1))
	set camposGeral		= $get(%request.Data("camposGeral",1))
	set strStatus = $$$OK
	tstart
	try{
		set camposGeral		= [].%FromJSON(camposGeral)
		;
		if '$data(^WWW128(0,"FATModeloNotaFiscal")){
			set obj = "MNF000000000"
			set strStatus = $$$Save("WWW128","FATModeloNotaFiscal",obj,$$$YES)
			quit:$$$ISERR(strStatus)
		}
		;
		if modelo="" Set modelo = $$GetNextNumber^COMUtils("FATModeloNotaFiscal","")
		;
		set nome = $zconvert(camposGeral.nome,"U")
		set nome = $zstrip(nome,"<>w")
		;
		set objModelo = $get(^FATModeloNotaFiscal(YM,modelo,1))
		set $$$FATModeloNotaFiscalNome(objModelo)				= nome
		set $$$FATModeloNotaFiscalLocalidade(objModelo)			= camposGeral.localidade
		set $$$FATModeloNotaFiscalAtivo(objModelo)				= camposGeral.situacao
		set $$$FATModeloNotaFiscalFormatodaEmissao(objModelo)	= camposGeral.formato
		set $$$FATModeloNotaFiscalFormatodaEmissao(objModelo)	= camposGeral.formato
		set tributos = ""
		if camposGeral.tributos'=""{
			set iterator = camposGeral.tributos.%GetIterator()
			while iterator.%GetNext(.indice,.valor){
				set:tributos'="" tributos = tributos_";"
				set tributos = tributos_valor
			}

		}
		set $$$FATModeloNotaFiscalTributos(objModelo)			= tributos
		set $$$FATModeloNotaFiscalDiscriminacaodosServicos(objModelo)	= camposGeral.discriminacao
		set strStatus = $$$Save("FATModeloNotaFiscal",modelo,objModelo,$$$YES)
	}
	catch e {
		#dim e as %Exception.SystemException
		set strStatus = $system.Status.GetErrorText(e.AsStatus())
	}
	set retorno = {}
	if strStatus=$$$OK{
		tcommit
		set strStatus="Dados salvos com sucesso!"
		set retorno.status = 1
		do retorno.%Set("modelo",modelo,"String")
		set retorno.type = "success"
		set retorno.mensagem = strStatus
	}else{
		trollback
		set retorno.status = 0
		set retorno.type = "error"
		set retorno.mensagem = $$$Text(strStatus)
	}
	write retorno.%ToJSON()
	quit $$$OK]]></Implementation>
</Method>

<Method name="validaSessao">
<ClassMethod>1</ClassMethod>
<FormalSpec>callMode=""</FormalSpec>
<Implementation><![CDATA[	set status = $$VerifySession^VARCSPUtils(YBED, YUSER)
	if status=0||(YBED="UNKNOWN"){
		set status=0
		set %response.Status="401 Unauthorized"
		set resp={}
		set resp.status=0
		set resp.mensagem="Usuário não está logado ou a sessão expirou."
		write resp.%ToJSON()
	}elseif callMode=""{
		set resp={}
		set resp.status=1
		write resp.%ToJSON()
	}
	quit status]]></Implementation>
</Method>
</Class>
</Export>