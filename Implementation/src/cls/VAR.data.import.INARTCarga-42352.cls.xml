<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.data.import.INARTCarga">
<ClassType/>
<IncludeCode>INConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>AbstractCarga</Super>

<Parameter name="DEBUG">
<Description><![CDATA[Comandos:
> do ##class(VAR.data.import.INARTCarga).carregarArquivo()

Campos:
  Arquivo:
    Código (CHAVE);Descrição (F1);ORIGEM (F26);Classificação VEN (F293);
		  Classificação XYZ (F277),Manipulado (286);Multidose Coletivo (F290);
		  Fracionado (F287);Multidose injetável (F296),ATB (SCIH) (F288);
		  Unitarizado (F297);Medicamentos de risco (F289);CÓDIGO ATC (F281);
		  Temperatura acondicionamento (F284);USO RACIONAL (F291);PROGRAMAS DO MS (F295);
Campos adicionados em 31/10/2012 para o AME
	vvvvvv
		  NIVEL DE ATENÇÃO (F282);CLASSIFICAÇÃO DE RISCO (F315);VIA DE ADMINISTRAÇÃO (F393)]]></Description>
<Type>%Integer</Type>
<Default>0</Default>
</Parameter>

<Property name="primeiraLinhaDados">
<Type>%Integer</Type>
</Property>

<Property name="globalControle">
<Type>%String</Type>

<Parameter name="TRUNCATE" value="1"/>
</Property>

<Method name="carregarArquivo">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	do ..importar(##class(DirectoryFile).GetNamespaceSubdir("carga")_
		"Classificação de medicamentos_"_$znspace_" - CARGA.csv")]]></Implementation>
</Method>

<Method name="%OnNew">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set ..ignorarPrimeiraLinha = 1
	if (..#DEBUG) set ..imprimirAndamento = 0
	set ..primeiraLinhaDados = 0
	set ..globalControle = "^VAR.data.import.INARTCarga("""_$znspace_""")"

	set total = 0
	set codProduto = $order(^INART(YM, ""))
	while ($length(codProduto) > 0) {
		set total = $increment(total)

		set codProduto = $order(^INART(YM, codProduto))
	}

	write !, "Existem "_total_" registros na global ^INART. "
	read "Deseja continuar? (S/N) ", continuar#1

	if ((continuar '= "S") && (continuar '= "s")) quit $$$CUSTOMERROR("Operação abortada.")

	quit ##super()]]></Implementation>
</Method>

<Method name="processar">
<FormalSpec>pintRegistros:%Integer,pstrLinha:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set $ztrap = "exceptionProc"
	set strStatus = $$$OK

	// Ignorar a segunda linha também
	if ($length(pstrLinha, ..separador) '= 19) {
		if ('..primeiraLinhaDados) {
			quit strStatus
		}
		else {
			quit "0 Linha "_pintRegistros_" com número de campos diferente "_
				"do esperado ("_$length(pstrLinha, ..separador)_")"
		}
	}

	set ..primeiraLinhaDados = 1

	set codProduto           = ..getCampo(pstrLinha, 1)
	set origem               = ..getCampo(pstrLinha, 3)
	set classificacaoVen     = ..getCampo(pstrLinha, 4)
	set classificacaoXyz     = ..getCampo(pstrLinha, 5)
	set isManipulado         = ..getCampo(pstrLinha, 6)
	set isFracionado         = ..getCampo(pstrLinha, 8)
	set isMedicamentoRisco   = ..getCampo(pstrLinha, 12)
	set isMultidoseColetivo  = ..getCampo(pstrLinha, 7)
	set codigoAtc            = ..getCampo(pstrLinha, 13)
	set temperatura          = ..getCampo(pstrLinha, 14)
	set isMultidoseInjetavel = ..getCampo(pstrLinha, 9)
	set isDoseUnitarizada    = ..getCampo(pstrLinha, 11)
	set usoRacional          = ..getCampo(pstrLinha, 15)
	set programaMs           = ..getCampo(pstrLinha, 16)
	;Adicionados em 31/10/2012 para o AME
	;vvvvvv
	set nivelAtencao         = ..getCampo(pstrLinha, 17)
	set classificacaoRisco   = ..getCampo(pstrLinha, 18)
	set viaAdministracao     = ..getCampo(pstrLinha, 19)
	
	if ('$data(^INART(YM, codProduto, 1))) {
		quit "0 Produto '"_codProduto_"' da linha #"_pintRegistros_" não "_
			"foi encontrado"
	}

	if (+$get(@..globalControle@(codProduto))) quit strStatus

	set key = codProduto
	set obj = ^INART(YM, key, 1)
	set $$$INARTItemType(obj) = origem
	set $$$INARTFREE18(obj)   = classificacaoVen
	set $$$INARTFREE2(obj)    = classificacaoXyz
	set $$$INARTFREE11(obj)   = +isManipulado
	set $$$INARTFREE12(obj)   = +isFracionado
	set $$$INARTFREE14(obj)   = +isMedicamentoRisco
	set $$$INARTFREE15(obj)   = +isMultidoseColetivo
	set $$$INARTFREE6(obj)    = codigoAtc
	set $$$INARTFREE9(obj)    = temperatura
	set $$$INARTMultidoseInjectable(obj) = +isMultidoseInjetavel
	set $$$INARTFREE22(obj)   = +isDoseUnitarizada
	set $$$INARTFREE16(obj)   = usoRacional
	set $$$INARTFREE20(obj)   = programaMs
	;Adicionados em 31/10/2012 para o AME
	;vvvvvv
	set $$$INARTFREE7(obj)                    = nivelAtencao
	set $$$INARTDangerousGoodsCategory(obj)   = classificacaoRisco
	set $$$INARTMedRouteOfAdministration(obj) = viaAdministracao

	if (..#DEBUG) {
		do ..imprimir(..quebraLinha_"key="_key_", obj="_obj)
	}
	else {
		set strStatus = $$$Save("INART", key, obj, 1)

		if ($$$ISOK(strStatus)) set @..globalControle@(codProduto) = 1
	}

finallyProc
	quit strStatus

exceptionProc
 	set $ztrap = ""
	set strStatus = $$$CUSTOMERROR($zerror)

	goto finallyProc]]></Implementation>
</Method>
</Class>
</Export>