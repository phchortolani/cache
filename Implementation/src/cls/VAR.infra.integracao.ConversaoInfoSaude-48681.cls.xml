<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.infra.integracao.ConversaoInfoSaude">
<ClassType/>
<IncludeCode>INConst</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%RegisteredObject</Super>

<Method name="GetFornecedorAlphalinc">
<ClassMethod>1</ClassMethod>
<FormalSpec>CodigoInfoSaude:%String,CnpjCpf:%String=""</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set codigoAlphalinc = ""

	if ($length(CodigoInfoSaude) > 0) {
		&sql(SELECT SupplierNumber
			 INTO   :codigoAlphalinc
			 FROM   SqlUser.INLIEF
			 WHERE  FREE2 = :CodigoInfoSaude)

		if ((SQLCODE) && ($length(CnpjCpf) > 0)) {
			&sql(SELECT SupplierNumber
				 INTO   :codigoAlphalinc
				 FROM   SqlUser.INLIEF
				 WHERE  CompanyNumber IS NOT NULL
				 AND    $$Strip^COMUtilStr(CompanyNumber) = $$Strip^COMUtilStr(:CnpjCpf))
		}
	}

	quit codigoAlphalinc]]></Implementation>
</Method>

<Method name="GetFornecedorInfoSaude">
<ClassMethod>1</ClassMethod>
<FormalSpec>CodigoAlphalinc:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set codigoInfoSaude = ""

	if ($length(CodigoAlphalinc) > 0) {
		&sql(SELECT FREE2
			 INTO   :codigoInfoSaude
			 FROM   SqlUser.INLIEF
			 WHERE  SupplierNumber = :CodigoAlphalinc)
	}

	quit codigoInfoSaude]]></Implementation>
</Method>

<Method name="GetAdmissaoAlphalinc">
<ClassMethod>1</ClassMethod>
<FormalSpec>Tipo:%String,AnoAdmissao:%String,NoAdmissao:%String,ValidaDataAdmissao:%Boolean,DataAdmissao:%TimeStamp</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	#define TIPOFIA "FIA"
	#define TIPOBAA "BAA"
     
	set codigoAlphalinc = ""

	if (($length(AnoAdmissao) > 0) && ($length(NoAdmissao) > 0)) {
		set numeroFinalAdmissao = AnoAdmissao_"/"_NoAdmissao
		if ($length(DataAdmissao) = 0) {
			set dataAdmissao = ""
			set horaAdmissao = ""
		}
		else {
			set dataAdmissao = $piece($zdatetimeh(DataAdmissao, 3, 1), ",", 1)
			set horaAdmissao = $piece($zdatetimeh(DataAdmissao, 3, 1), ",", 2)
		}

		if (Tipo = $$$TIPOFIA) {
			if ($length(DataAdmissao) = 0) { // Recuperar a última admissão ativa
				if (ValidaDataAdmissao) {
					&SQL(SELECT MAX(AdmissionID)
						 INTO   :codigoAlphalinc
						 FROM   SqlUser.MEDAdmission
						 WHERE  COMPANY = 0
						 AND	FREE4 = :numeroFinalAdmissao
						 AND    NVL(DateDischarged,0) =0
						 AND    NVL(TimeDischarged,0) =0)
				}
				else {
					&SQL(SELECT MAX(AdmissionID)
						 INTO   :codigoAlphalinc
						 FROM   SqlUser.MEDAdmission
						 WHERE COMPANY = 0
						 AND   FREE4 = :numeroFinalAdmissao)
				}
			}
			else {
				&SQL(SELECT AdmissionID
					 INTO   :codigoAlphalinc
					 FROM   SqlUser.MEDAdmission
					 WHERE  COMPANY = 0
				     AND 	FREE4 = :numeroFinalAdmissao
					 AND    DateAdmitted = :dataAdmissao
					 AND    TimeAdmitted = :horaAdmissao)
			}
		}
		elseif (Tipo = $$$TIPOBAA) {
			if ($length(DataAdmissao) = 0) { // Recuperar a última admissão ativa
				&SQL(SELECT MAX(AdmissionID)
					 INTO   :codigoAlphalinc
					 FROM   SqlUser.MEDAdmission
					 WHERE  COMPANY = 0
					 AND 	FREE5 = :numeroFinalAdmissao
					 AND    NVL(DateDischarged,0) =0
					 AND    NVL(TimeDischarged,0) =0)
			}
			else {
				&SQL(SELECT AdmissionID
					 INTO   :codigoAlphalinc
					 FROM   SqlUser.MEDAdmission
					 WHERE  COMPANY = 0
					 AND	FREE5 = :numeroFinalAdmissao
					 AND    DateAdmitted = :dataAdmissao
					 AND    TimeAdmitted = :horaAdmissao)
			}
		}
		else {
			quit ""
		}
		if (SQLCODE) set codigoAlphalinc = ""
	}
	quit codigoAlphalinc]]></Implementation>
</Method>

<Method name="GetPedidoCompraAlphalinc">
<ClassMethod>1</ClassMethod>
<FormalSpec>NumeroInfoSaude:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set numeroAlphalinc = ""

	if ($length(NumeroInfoSaude) > 0) {
		&sql(SELECT NoPedido
			 INTO   :numeroAlphalinc
			 FROM   SqlUser.VARPedidoCompra
			 WHERE  NDocIntegrado = :NumeroInfoSaude)
	}

	quit numeroAlphalinc]]></Implementation>
</Method>

<Method name="GetPedidoCompraInfoSaude">
<ClassMethod>1</ClassMethod>
<FormalSpec>NumeroAlphalinc:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set numeroInfoSaude = ""

	if ($length(NumeroAlphalinc) > 0) {
		&sql(SELECT NDocIntegrado
			 INTO   :numeroInfoSaude
			 FROM   SqlUser.VARPedidoCompra
			 WHERE  NoPedido = :NumeroAlphalinc)
	}

	quit numeroInfoSaude]]></Implementation>
</Method>

<Method name="GetPrescricaoAlphalinc">
<ClassMethod>1</ClassMethod>
<FormalSpec>AnoFiaInfoSaude:%String,NoFiaInfoSaude:%String,NoPrescricaoInfoSaude:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set codigoAlphalinc = ""

	if (($length(AnoFiaInfoSaude) > 0) &&
		($length(NoFiaInfoSaude) > 0) &&
		($length(NoPrescricaoInfoSaude) > 0)) {
		set numeroFinalFIA = AnoFiaInfoSaude_"/"_NoFiaInfoSaude

		&sql(SELECT PrescriptionNumber
			 INTO   :codigoAlphalinc
			 FROM   SqlUser.MEDPrescription
			 WHERE  FREE1 = :numeroFinalFIA
			 AND    FREE2 = :NoPrescricaoInfoSaude)
	}

	quit codigoAlphalinc]]></Implementation>
</Method>
</Class>
</Export>