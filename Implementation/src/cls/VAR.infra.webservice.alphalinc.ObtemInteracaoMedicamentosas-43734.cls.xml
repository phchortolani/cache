<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.infra.webservice.alphalinc.ObtemInteracaoMedicamentosas">
<Description><![CDATA[Esta classe implementa todos os web services a serem utilizados pela Integração de Interações Medicamentosas entre Alphalinc e InfoSaúde, especificamente para o cliente Heva.]]></Description>
<ClassType/>
<Import>VAR.infra.integracao,VAR.common.soap,VAR.infra.util,alWEB.WS</Import>
<IncludeCode>INConst,VARConst,MEDConst,VARLog,WWWSYS</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%SOAP.WebService</Super>

<Parameter name="NAMESPACE">
<Default>http://tempuri.org</Default>
</Parameter>

<Parameter name="SERVICENAME">
<Description><![CDATA[Name of the WebService.]]></Description>
<Default>ObtemInteracaoMedicamentosas</Default>
</Parameter>

<Parameter name="SOAPVERSION">
<Description><![CDATA[This is the SOAP version supported by the service.]]></Description>
<Default>1.1</Default>
</Parameter>

<Parameter name="MAXIMOREGISTROSPORVEZ">
<Type>%Integer</Type>
<Default>20</Default>
</Parameter>

<Method name="ObtemInteracaoMedicamentosas">
<Description><![CDATA[Método para ler as Interações Medicamentosas Pendentes de Integrar. <BR>
<BR>
Parâmetros:<BR><UL>
<LI>IdInicio (obrigatório) - ID de referência para buscar as próximas Interações Medicamentosas;
</UL>
<BR>
Retorno:<BR><UL>
<LI>ResultadoInteracoes - Contém uma Lista de Interacoes
</UL>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>SessionId:%String,MsgSequence:%Integer,IdInicio:%String</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>ResultadoInteracoes</ReturnType>
<WebMethod>1</WebMethod>
<Implementation><![CDATA[	set resultado = ##class(ResultadoInteracoes).%New()
 	set resultado.Interacoes = ##class(%ListOfObjects).%New()

	set strStatus = ##class(Session).ValidateSession(SessionId,
		MsgSequence, .strError, , ..%ClassName()_"."_$$$MethodName)

	if ('$$$ISOK(strStatus)) set strStatus = $$$MakeStatus(strError)
	
	if ($$$ISOK(strStatus)) {
		set %("%KEY", "YBED") = "WEBSERVICE"
		set %("%KEY", "YPWD") = ""
		set %("%KEY", "YM") = 0

		$$$VAR

		set SPRACHE = "PT"

		set entidade = ##class(GerenciaFilaPendencias).%GetParameter("ENTIDADEINTERACAOMEDICAMENTOSA")

		&sql(DECLARE cursorObtemInteracaoMedicamentosa CURSOR FOR
			 SELECT   VARDadoAIntegrar.ID,
			          VARDadoAIntegrar.Acao,
			          VARDadoAIntegrar.Chave1Entidade,
			          VARDadoAIntegrar.Chave2Entidade, 
			          VARDadoAIntegrar.OrdemNaFila,
			          VARDadoAIntegrar.AtualizadoEm
			 INTO     :idIntegracao, :acao, :chave1Entidade, :chave2Entidade, :ordemNaFila,
			 		  :atualizadoEm
			 FROM     SQLUser.VARDadoAIntegrar, SQLUser.MEDDrugInteraction
			 WHERE    VARDadoAIntegrar.Entidade = :entidade
			 AND      VARDadoAIntegrar.Chave1Entidade =* MEDDrugInteraction.Drug 
			 AND      VARDadoAIntegrar.Chave2Entidade =* MEDDrugInteraction.InteractiveDrug
			 AND      VARDadoAIntegrar.Integrado = 'N'
			 ORDER BY VARDadoAIntegrar.OrdemNaFila)
		&sql(OPEN cursorObtemInteracaoMedicamentosa)
		&sql(FETCH cursorObtemInteracaoMedicamentosa)

		if ($length(IdInicio) > 0) {
			while (SQLCODE = 0) {
				if (idIntegracao = IdInicio) {
					&sql(FETCH cursorObtemInteracaoMedicamentosa)
					quit
				}

				&sql(FETCH cursorObtemInteracaoMedicamentosa)
			}
		}

		while (SQLCODE = 0) {
			set medicamentoReferencia = chave1Entidade
			set medicamentoRelacionado = chave2Entidade
			set objInteracao = $get(^MEDDrugInteraction(YM, medicamentoReferencia, medicamentoRelacionado, 1))

			set wsInteracao = ##class(InteracaoMedicamentosa).%New()
			set wsInteracao.IdIntegracao = idIntegracao
			set wsInteracao.MedicamentoReferencia = medicamentoReferencia				
			set wsInteracao.MedicamentoRelacionado = medicamentoRelacionado
			set wsInteracao.Acao = acao
			set wsInteracao.Efeito = $$$MEDDrugInteractionEffect(objInteracao)
			set wsInteracao.Ativo = $$$MEDDrugInteractionActive1(objInteracao)
			set wsInteracao.OrdemNaFila = $zdatetime(ordemNaFila, 3, 1)
			set wsInteracao.AtualizadoEm = $zdatetime(atualizadoEm, 3, 1)
			do resultado.Interacoes.Insert(wsInteracao)
			if (resultado.Interacoes.Count() >= ..#MAXIMOREGISTROSPORVEZ) quit

			&sql(FETCH cursorObtemInteracaoMedicamentosa)
		}

		&sql(CLOSE cursorObtemInteracaoMedicamentosa)
	    set strStatus = $$$OK
	}

    if ($$$ISOK(strStatus)) {
        set resultado.CodigoRetorno = resultado.%GetParameter("CODIGORETORNOSUCESSO")
        set resultado.MensagemErro = ""
    }
    else {
        set resultado.CodigoRetorno = resultado.%GetParameter("CODIGORETORNOERROCONHECIDO")
        set resultado.MensagemErro = $$DecodeError^COMUtilError(strStatus)
    }

 	quit resultado]]></Implementation>
</Method>

<Method name="ConfirmaIntegracao">
<Description><![CDATA[Método para confirmar uma Interação Medicamentosa integrada. <BR>
<BR>
Parâmetros:<BR><UL>
<LI>SessionID - Código da Sessão 
<LI>MsgSequence - Seguência da chamada ao WebMethod
<LI>MedicamentoReferencia (obrigatório) - Codigo Medicamento de Referência;
<LI>MedicamentoRelacionado (obrigatório) - Codigo Medicamento de Relacionado;
</UL>
<BR>
Retorno:<BR><UL>
<LI>Resultado - resultado padrão
</UL>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>SessionId:%String,MsgSequence:%Integer,AtualizadoEm:%TimeStamp,MedicamentoReferencia:%String,MedicamentoRelacionado:%String</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>Resultado</ReturnType>
<WebMethod>1</WebMethod>
<Implementation><![CDATA[	set resultado = ##class(Resultado).%New()

	set strStatus = ##class(Session).ValidateSession(SessionId,
		MsgSequence, .strError, , ..%ClassName()_"."_$$$MethodName)
	
	if ('$$$ISOK(strStatus)) {
		set strStatus = $$$MakeStatus(strError)
	}	
	if ($$$ISOK(strStatus)) {
		set %("%KEY", "YBED") = "WEBSERVICE"
		set %("%KEY", "YPWD") = ""
		set %("%KEY", "YM") = 0

		$$$VAR

		set SPRACHE = "PT"

		set strStatus = ##class(GerenciaFilaPendencias).ConfirmaIntegracao(
			##class(GerenciaFilaPendencias).%GetParameter("ENTIDADEINTERACAOMEDICAMENTOSA"),
			AtualizadoEm,
			MedicamentoReferencia,
			MedicamentoRelacionado,
			##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
			##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
			##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"))
	}

    if ($$$ISOK(strStatus)) {
        set resultado.CodigoRetorno = resultado.%GetParameter("CODIGORETORNOSUCESSO")
        set resultado.MensagemErro = ""
    }
    else {
        set resultado.CodigoRetorno = resultado.%GetParameter("CODIGORETORNOERROCONHECIDO")
        set resultado.MensagemErro = $$DecodeError^COMUtilError(strStatus)
    }

 	quit resultado]]></Implementation>
</Method>
</Class>
</Export>