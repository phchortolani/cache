<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.infra.webservice.alphalinc.AtualizaOrdemCompra">
<Description><![CDATA[Esta classe implementa todos os web services a serem utilizados pela Integração de Ordem de Compra entre Alphalinc e InfoSaúde, especificamente para o cliente Heva.]]></Description>
<ClassType/>
<Import>VAR.infra.integracao,VAR.common.soap,VAR.infra.util,alWEB.WS</Import>
<IncludeCode>VARLog,COMAlphaWeb,COMSYS,WWWSYS</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%SOAP.WebService</Super>

<Parameter name="NAMESPACE">
<Default>http://tempuri.org</Default>
</Parameter>

<Parameter name="SERVICENAME">
<Description><![CDATA[Name of the WebService.]]></Description>
<Default>AtualizaOrdemCompra</Default>
</Parameter>

<Parameter name="SOAPVERSION">
<Description><![CDATA[This is the SOAP version supported by the service.]]></Description>
<Default>1.1</Default>
</Parameter>

<Method name="AtualizaOrdensCompra">
<Description><![CDATA[Método para criar ou atualizar uma ordem de compra. <BR>
<BR>
Parâmetros:<BR><UL>
<LI>OrdensCompra (obrigatório) - Lista de Ordens de Compra;
</UL>
<BR>
Retorno:<BR><UL>
<LI>ResultadoOrdensCompra - Contém uma Lista de Resultados com Código e Descrição de Erro
</UL>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>SessionId:%String,MsgSequence:%Integer,OrdensCompra:VAR.common.soap.OrdensCompra</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>ResultadoOrdemCompra</ReturnType>
<WebMethod>1</WebMethod>
<Implementation><![CDATA[	#dim ordemCompra as OrdemCompra
	
 	set resultado = ##class(ResultadoOrdemCompra).%New()
 	set resultado.Resultados = ##class(%ListOfObjects).%New()

	set strStatus = ##class(Session).ValidateSession(SessionId,
		MsgSequence, .strError, , ..%ClassName()_"."_$$$MethodName)
	if ('$$$ISOK(strStatus)) {
		set strStatus = $$$MakeStatus(strError)
	}

	if ($$$ISOK(strStatus)) {
		set %("%KEY", "YBED") = "WEBSERVICE"
		set %("%KEY", "YPWD") = ""
		set %("%KEY", "YM") = 0

		$$$VAR

		set SPRACHE = "PT"

	    for i = 1:1:OrdensCompra.OrdensCompra.Count() {
			set ordemCompra = OrdensCompra.OrdensCompra.GetAt(i)

			// Converte o Código Fornecedor do InfoSaúde em Código Fornecedor Alphalinc
			set fornecedor = ordemCompra.Fornecedor

	        set resultadoChave = ##Class(ResultadoChave).%New()
	        set resultadoChave.Chave1 = ordemCompra.AnoOrdem
	        set resultadoChave.Chave2 = ordemCompra.NoOrdem
	        set resultadoChave.Chave3 = ordemCompra.SerieOrdem

			set ordemCompra.Fornecedor = ##class(ConversaoInfoSaude).GetFornecedorAlphalinc(fornecedor)
			//pula as ordens de compras que nÃ£o possuem fornecedor no Alphalinc
			if ($length(ordemCompra.Fornecedor) = 0) {
				set resultadoChave.CodigoRetorno = resultadoChave.%GetParameter("CODIGORETORNOVALORINVALIDO")
		        set resultadoChave.MensagemErro = "Fornecedor "_fornecedor_" não encontrado no Alphalinc"
        		do resultado.Resultados.Insert(resultadoChave)
				continue
			}

			set idINAUF = $extract(ordemCompra.AnoOrdem, 3, 4)_"/"_ordemCompra.NoOrdem

			set mensagemErro = ""
	        set codigoRetorno = resultadoChave.%GetParameter("CODIGORETORNOSUCESSO")
			set strStatus = $$Transaction^COMTransaction("AtualizaOrdemCompra^VARAtualizaOrdemCompraWS(ordemCompra, idINAUF, .codigoRetorno, .mensagemErro)")
			if (codigoRetorno '= resultadoChave.%GetParameter("CODIGORETORNOSUCESSO")) {
		        set resultadoChave.CodigoRetorno = codigoRetorno
		        set resultadoChave.MensagemErro = mensagemErro
			}
	        elseif ($$$ISERR(strStatus)) {
		        set resultadoChave.CodigoRetorno = resultadoChave.%GetParameter("CODIGORETORNOERROCONHECIDO")
		        set resultadoChave.MensagemErro = $$DecodeError^COMUtilError(strStatus)
	        }
	        else {
		        set resultadoChave.CodigoRetorno = resultadoChave.%GetParameter("CODIGORETORNOSUCESSO")
		        set resultadoChave.MensagemErro = ""
	        }

        	do resultado.Resultados.Insert(resultadoChave)
	    }

	    set strStatus = $$$OK
	}

    if ($$$ISOK(strStatus)) {
        set resultado.CodigoRetorno = resultado.%GetParameter("CODIGORETORNOSUCESSO")
        set resultado.MensagemErro = ""
    }
    else {
        set resultado.CodigoRetorno = resultado.%GetParameter("CODIGORETORNOERROCONHECIDO")
        set resultado.MensagemErro = $$DecodeError^COMUtilError(strStatus)
    }

 	quit resultado]]></Implementation>
</Method>
</Class>
</Export>