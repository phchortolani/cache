<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.AutoFormCSP">
<Description><![CDATA[
Subclass of AutoForm that puts buttons at the top.]]></Description>
<ClassType/>
<IncludeCode>EnsUI</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%CSP.Util.AutoForm</Super>
<System>3</System>

<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Method name="DrawHTMLForm">
<Description><![CDATA[
Draw an HTML form for this form object.<BR>
<var>pObj</var> is an instance of the form object that
provides the initial values for the form.<BR>
<var>pActionURL</var> is the URL to submit this form to.<BR>
<var>pButtons</var> is an array of submit buttons to display for the form. If 
not provided, the default <I>Save</I> and <I>Cancel</I> buttons are shown.
<var>pButtons</var> is an integer-subscripted array of <class>FormButton</class> objects.]]></Description>
<ClassMethod>1</ClassMethod>
<CodeMode>objectgenerator</CodeMode>
<FormalSpec>pObj:%RegisteredObject,&amp;pID:%String,pActionURL:%String="&quot;,&amp;pButtons:FormButton,pTarget:%String="&quot;</FormalSpec>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	// test if object is valid
	//Do %code.WriteLine(" If ('$IsObject(pObj)) {")
	//Do %code.WriteLine(" 	Quit $$$ERROR($$$GeneralError,""Invalid object."")")
	//Do %code.WriteLine(" }")
	
	// generate FORM/TABLE headers
	Do %code.WriteLine(" &html<<FORM ACTION=""#(pActionURL)#"" METHOD=""POST"" NAME=""theForm"" TARGET=""#(pTarget)#"">>")
	Do %code.WriteLine(" Write ##class(%CSP.Page).InsertHiddenFields(pActionURL)")
	Do %code.WriteLine(" &html<<INPUT TYPE=""HIDDEN"" NAME=""$CLASS"" VALUE=""" _ %class.Name _ """>>")
	Do %code.WriteLine(" &html<<INPUT TYPE=""HIDDEN"" NAME=""$FRAME"" VALUE=""#(pTarget)#"">>")
	Do %code.WriteLine(" &html<<input type=""HIDDEN"" name=""CSPCHD"" VALUE=""#(%session.CSPSessionCookie)#"">>")

	Do %code.WriteLine(" Set id = $O(pID(""""))")
	Do %code.WriteLine(" While (id '= """") {")
	Do %code.WriteLine(" 	&html<<INPUT TYPE=""HIDDEN"" NAME=""$ID#(id)#"" VALUE=""#($ZCVT(pID(id),""O"",""HTML""))#"">>")
	Do %code.WriteLine(" 	Set id = $O(pID(id))")
	Do %code.WriteLine(" }")
	
	// do not create fields for ourself...
	If ('$G(%parameter("COMMANDFORM"))) {
		If (%class.Name '= "%CSP.Util.AutoForm") {
			Do %code.WriteLine(" &html<<TABLE CLASS=""AutoForm"">>")

			Do %code.WriteLine(" &html<<TR><td colspan=10><table><tr><TD CLASS=""AutoFormLabel"">&nbsp;</TD>>")
			Do %code.WriteLine(" &html<<TD CLASS=""AutoFormButton"">>")
		}
	} Else { // command form
	}
	
	// test for user-provided buttons method
	If $$$comMemberKeyGet(%class.Name,$$$cCLASSmethod,"OnDefineButtons",$$$cMETHrunnable) {
		Do %code.WriteLine(" If ('$D(pButtons)) {")
		Do %code.WriteLine(" 	Do ..OnDefineButtons(.pButtons)")
		Do %code.WriteLine(" }")
	}
	
	// button(s)
	Do %code.WriteLine(" Do ..DrawFormButtons(.pButtons)")
	
	If ('$G(%parameter("COMMANDFORM"))) {
		If (%class.Name '= "%CSP.Util.AutoForm") {

			Do %code.WriteLine(" &html<</TD></tr></table></td></TR>>")
			Do %code.WriteLine(" &html<<tr><td COLSPAN=""10"" ><hr></td></tr>>")

			// now generate HTML for all of our properties
			Set tCol = 0
			Set sc = ##class(%CSP.Util.AutoFormGenerator).GenerateHTMLForm(%class.Name,%code,.tCol,$G(%parameter("FORMCOLUMNS"),1))
			Quit:$$$ISERR(sc) sc

			If $$$comMemberKeyGet(%class.Name,$$$cCLASSmethod,"DrawHTMLExtra",$$$cMETHrunnable) {
				Do %code.WriteLine(" Do pObj.DrawHTMLExtra(.pID, pActionURL, .pButtons, .pTarget)")
			}
			Do %code.WriteLine(" &html<</TABLE>>")
		}
	}
	Do %code.WriteLine(" &html<</FORM>>")
	Do %code.WriteLine(" Quit $$$OK")
	
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="DrawHTMLExtra">
<FormalSpec>&amp;pID:%String,pActionURL:%String="&quot;,&amp;pButtons:FormButton,pTarget:%String="&quot;</FormalSpec>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[	Quit $$$OK
]]></Implementation>
</Method>

<Method name="DrawHEADExtra">
<FormalSpec>pInstance:%CSP.Util.PageInstance</FormalSpec>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[	Quit $$$OK
]]></Implementation>
</Method>

<Method name="CreateInstance">
<Description><![CDATA[
Create an instance of the AutoForm object using the provided ID value(s).
This is overridden by subclasses.<BR>
The default implementation will perform an OpenId if the AutoForm is a persistent object.]]></Description>
<ClassMethod>1</ClassMethod>
<CodeMode>objectgenerator</CodeMode>
<FormalSpec>&amp;pID:%String</FormalSpec>
<ReturnType>AutoForm</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	// do not invoke this for ourself...
	If (%class.Name = "CSPX.AutoFormCSP") {
		Do %code.WriteLine(" Quit """"")
		Quit $$$OK
	}

	If (%class.ClassType = "persistent") {
		Do %code.WriteLine(" Set tObj = ..%OpenId($G(pID(1)))")
		Do %code.WriteLine(" If ('$IsObject(tObj)) { Set tObj = ..%New() }")
		Do %code.WriteLine(" Quit tObj")
	}
	Else {
		Do %code.WriteLine(" Quit ..%New()")
	}

	Quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>