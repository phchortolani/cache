<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.infra.agendaEmail.agendaEmailRunner">
<ClassType/>
<IncludeCode>VARLog,VARService,VARConst,COMSYS,MEDConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>VAR.infra.services.ServiceRunner</Super>

<Method name="Register">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	quit ..RegisterRunner($$$ServiceAgendaEmail, $$$ServiceFrequencyMinutely, $$$ServicePriorityMediumLow, ..%ClassName(1), "Envia agenda médico por email")]]></Implementation>
</Method>

<Method name="Unregister">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	quit ..UnregisterRunner($$$ServiceAgendaEmail, "Envia agenda médico por email")]]></Implementation>
</Method>

<Method name="Execute">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set status = $$$OK

	if (('##class(VAR.infra.shadow.ShadowRunner).IsFunctional()) || 
			((..IsShadowComputer()) && (..IsIntegrationNamespace()))) {
		quit $$$OK
	}

	DO ^WWWVAR
	
	SET horaAtual = $PIECE($HOROLOG,",",2)
	
	SET objParametro = $GET(^VARParametroCliente(YM,0,1))
	
	SET horaPadrao = $$$VARParametroClienteHoradeEnviodoemail(objParametro)
	
	IF horaPadrao = "" QUIT status
	
	IF horaAtual < horaPadrao QUIT status
	
	IF $DATA(^VARPEPAgendaEmailDia(YM,+$HOROLOG,1)) QUIT status
	
	SET ^VARPEPAgendaEmailDia(YM,+$HOROLOG,1) = $HOROLOG
	
	SET proxDia = $PIECE($HOROLOG,",",1) + 1
	
	SET status = ..EnviaEmail(proxDia,"", horaPadrao) 
	
	QUIT status]]></Implementation>
</Method>

<Method name="EnviaEmail">
<ClassMethod>1</ClassMethod>
<FormalSpec>Data,idProfissional,horaEnvio</FormalSpec>
<Implementation><![CDATA[	DO ^WWWVAR
	
	SET nomeMaquina = ##class(VAR.infra.util.Util).GetComputerName()
	
	SET nameSpace = $NAMESPACE
	
	;Verificando o NameSpace e a máquina para executar somente na produção
	
	IF (nomeMaquina '= "SVWDASA01") || (nameSpace '= "CHSP") QUIT $$$OK
	
	IF Data["/" SET Data = $ZDH(Data,4)
	
	IF horaEnvio = "" SET horaEnvio = $PIECE($HOROLOG,",",2)
	
	SET horaConvertida = $ZT(horaEnvio,2)

	SET query = "SELECT $$GetPatientName^VARSQL(Patient) As Name, "_
	"$$SQLGetPatientAge^VARSQL($$SQLGetPatientDataNascimento^VARSQL(Patient)) As Idade, "_
	"AdmissionID, TipoConsulta, DataAgendamento, HoraAgendamento, $$SQLGetConvenioPaciente^VARSQL(Convenio) As Convenio, "_ 
	"Profissional,DateDischarged FROM MEDAdmission WHERE DataAgendamento = "_Data_" "_
	"AND TipoConsulta in('CONSULTA','PRIMCONSULTA','TELEMEDICINA','MIELOGRAMA') AND Company = 0"
	
	IF idProfissional '= "" SET query = query _ " AND Profissional = '" _ idProfissional _ "'"
	
	SET rs = ##class(%SQL.Statement).%ExecDirect("",.query)
	
	kill Vetor
	
	SET dataConvertida = $ZDATE(Data,4)
	
	SET Y = "~"
			
	WHILE (rs.%Next() '= 0)
	{
		SET dataAlta = rs.%Get("DateDischarged")
		IF dataAlta '= "" CONTINUE
		SET nomePac = rs.%Get("Name")
		SET Idade = rs.%Get("Idade")
		SET IDAdmissao = rs.%Get("AdmissionID")
		SET tipoConsulta = rs.%Get("TipoConsulta")
		SET dataAgendamento = rs.%Get("DataAgendamento")
		SET horaAgendamento = rs.%Get("HoraAgendamento")
		SET convenio = rs.%Get("Convenio")
		SET codMedico = rs.%Get("Profissional")
		IF codMedico = "" CONTINUE
		SET objMedico = $GET(^MEDProvider(0,codMedico,1))
		SET usuarioMedico = $ZCONVERT($$$MEDProviderUser1(objMedico),"U")
		SET nomeMedico = $$$MEDProviderName(objMedico)
		;SET emailMedico = $$$MEDProviderEmail(objMedico)
		IF usuarioMedico = "" CONTINUE
		SET objUsuarioMedico = $GET(^WWW013(0,usuarioMedico,1))
		SET emailMedico = $$$WWW013EMailAddress(objUsuarioMedico)
		IF emailMedico = "" CONTINUE
		
		SET $PIECE(Vetor(codMedico),Y,1) = emailMedico
		SET $PIECE(Vetor(codMedico),Y,2) = nomeMedico
		
		SET Vetor(codMedico,+horaAgendamento,IDAdmissao) = nomePac_Y_Idade_Y_tipoConsulta_Y_
		dataAgendamento_Y_convenio
		
	}
		
	SET codMedico = ""
	
	FOR
	{
		SET codMedico = $ORDER(Vetor(codMedico))
		IF codMedico = "" QUIT
		SET emailMedico = $PIECE($GET(Vetor(codMedico)),Y,1)
		SET nomeMedico = $PIECE($GET(Vetor(codMedico)),Y,2)
		KILL TEXT
		SET TEXT($INCREMENT(linha)) = "Caro Dr(a). "_nomeMedico _ "<br>"
		SET TEXT($INCREMENT(linha)) = "<br>"
		SET TEXT($INCREMENT(linha)) = "Segue a agenda do dia " _ dataConvertida _ " gerada até as "_horaConvertida _ " do dia corrente." _ "<br>"
		SET TEXT($INCREMENT(linha)) = "<br>"
		SET TEXT($INCREMENT(linha)) = "<table border=1 cellspacing=0 cellpadding=2 bordercolor='000000'>"
		SET TEXT($INCREMENT(linha)) = "<tr><th>Paciente</th><th>Idade</th><th>Data Agenda</th><th>Hora Agenda</th><th>Procedimento</th><th>Convênio</th></tr>"
		SET horarioAgendamento = ""
		FOR
		{
			SET horarioAgendamento = $ORDER(Vetor(codMedico,horarioAgendamento))
			IF horarioAgendamento = "" QUIT
			SET horarioAgendamentoConvertido = $ZTIME(horarioAgendamento,2)
			SET admissao = ""
			FOR
			{
				SET admissao = $ORDER(Vetor(codMedico,horarioAgendamento,admissao))
				IF admissao = "" QUIT				
				SET Paciente = $PIECE(Vetor(codMedico,horarioAgendamento,admissao),Y,1)
				SET Idade = $PIECE(Vetor(codMedico,horarioAgendamento,admissao),Y,2)
				SET Consulta = $PIECE(Vetor(codMedico,horarioAgendamento,admissao),Y,3)
				SET Consultaobj = $GET(^VARPARA(0,"TIPOCONSULTA","PT",Consulta,1))
				SET nomeConsulta = $$$VARPARAText(Consultaobj)
				SET dataAgendamento = $PIECE(Vetor(codMedico,horarioAgendamento,admissao),Y,4)
				SET dataAgendamentoConvertida = $ZDATE(dataAgendamento,4)
				SET convPaciente = $PIECE(Vetor(codMedico,horarioAgendamento,admissao),Y,5)
				SET TEXT($INCREMENT(linha)) = "<tr><td>"_Paciente_"</td><td>"_Idade_"</td><td>"_dataAgendamentoConvertida_"</td><td>"_horarioAgendamentoConvertido_"</td><td>"_nomeConsulta_"</td><td>"_convPaciente_"</td></tr>"
			}
		
		}
		
		SET TEXT($INCREMENT(linha)) ="</table>"
		
		SET TEXT($INCREMENT(linha)) = "<br>"
		
		SET TEXT($INCREMENT(linha)) = "Obrigado" _ "<br>"
		
		SET TEXT($INCREMENT(linha)) = "<br>"
		
		SET TEXT($INCREMENT(linha)) = "Suporte Alphalinc" _ "<br>"
		
		SET TEXT($INCREMENT(linha)) = "<br>"
		
		SET TEXT($INCREMENT(linha)) = "<font size=-1><i>* Este e-mail foi enviado automaticamente pelo Sistema Alphalinc. Favor não responder. Em caso de dúvidas, entre em contato com a central de agendamento do CHSP.</i></font>"
		
		SET email = $$^WWWMAILS("","",emailMedico,"CHSP - Agenda de Pacientes para o dia " _ dataConvertida,.TEXT)
		
	}
	
	QUIT $$$OK]]></Implementation>
</Method>
</Class>
</Export>