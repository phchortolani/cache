<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.CSP.FATPesquisaExame">
<ClassType/>
<IncludeCode>MEDConst,VARConst,INConst,WWWConst,FATConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	$$$VAR
	Set method = $Get(%request.Data("method",1))
	Set job = %session.SessionId
	Xecute "do .."_method_"()"
	Return $$$OK]]></Implementation>
</Method>

<Method name="ListarRelatorio">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	$$$VAR
	Set YM 	= 0
	Set Y		= "~"
	;
	Set YBED 				= $Get(%request.Data("YBED",1))
	Set pObjFiltros = $Get(%request.Data("pObjFiltros",1))
	Set pObjFiltros = [].%FromJSON(pObjFiltros)
	;
	Set pProduto		= pObjFiltros.procedimento
	Set pConvenio		= pObjFiltros.convenio
	Set pPlanoSaude	= pObjFiltros.planoSaude
	;
	Kill temp
	;
	If (pPlanoSaude = "" || (pPlanoSaude.%Size() = 0)) {
		If (pConvenio = "") {
			Set meuSQLx = "Select CodigodaOrganizacao From SQLUser.FATOrganizacao "
			Set meuSQLx = meuSQLx_"Where Company = 0 And Ativo = 1 "
			Set meuSQLx = meuSQLx_"And ConvenioAtivo = 1 "
			Set rSetx = ##class(%SQL.Statement).%ExecDirect("",.meuSQLx)	;
			While (rSetx.%Next()) {
				Set retorno = ##Class(VAR.CSP.VARCadAgendamento).GetPlanoSaudeCampoFilhos(rSetx.%Get("CodigodaOrganizacao"))
				For i = 0 : 1 : (retorno.%Size()-1) {
					Set temp(rSetx.%Get("CodigodaOrganizacao"), retorno.%Get(i).codOrganizacao) = retorno.%Get(i).nomeOrganizacao
				}
			}
		}Else{
			Set retorno = ##Class(VAR.CSP.VARCadAgendamento).GetPlanoSaudeCampoFilhos(pConvenio)
			For i = 0 : 1 : (retorno.%Size()-1) {
				Set temp(pConvenio, retorno.%Get(i).codOrganizacao) = retorno.%Get(i).nomeOrganizacao
			}
		}
	}Else{
		For i = 0 : 1 : (pPlanoSaude.%Size()-1) {
			Set temp(pConvenio, pPlanoSaude.%Get(i)) = ""
		}
	}
	;
	Set retorno = []
	For iProduto = 0 : 1 : (pProduto.%Size()-1) {
		Set codProduto = pProduto.%Get(iProduto)		
		Set codConvenio = ""
		For {
			Set codConvenio = $Order(temp(codConvenio))	
			Quit:codConvenio=""
			Set codPlanoSaude = ""
			For {
				Set codPlanoSaude = $Order(temp(codConvenio, codPlanoSaude))	
				Quit:codPlanoSaude=""
				;
				Set custoItem = 0, codExecutor = ""
				If ($Data(^VARManualExames(0,5,"A",codProduto))) {
					Set codExecutor = $Order(^VARManualExames(0,5,"A",codProduto,""))
				}ElseIf ($Data(^VARManualExames(0,5," ",codProduto))) {
					Set codExecutor = $Order(^VARManualExames(0,5," ",codProduto,""))
				}		
				If (codExecutor '= "") {
					Set custoItem = $$$VARManualExamePreco($Get(^VARManualExame(YM, codProduto, codExecutor, 1)))		
				}
				;
				Set objRegraComercial = ##Class(VAR.util.FATRegrasComerciais).%New(codProduto, codPlanoSaude)
				;
				Set list = {}
				Set list.codigoAlpha = codProduto
				Set list.descricaoAlpha = $$SQLGetDescricaoProduto^VARSQL(codProduto,1)
				Set list.flagPreReq	= $Data(^VARManualExame(YM, codProduto))
				Set list.codigoConvenio = codConvenio
				Set list.descricaoConvenio = $$GetOrganizacaoNome^VARSQL(codConvenio)
				Set list.codigoPlanoSaude = codPlanoSaude
				Set list.descricaoPlanoSaude = $$GetOrganizacaoNome^VARSQL(codPlanoSaude)
				Set list.codigoTISS = $Select($IsObject(objRegraComercial):objRegraComercial.CodigodoItem,1:"")
				Set list.descricaoTISS = $Select($IsObject(objRegraComercial):objRegraComercial.DescricaoDoItem,1:"")
				If (##Class(VAR.CSP.VARCadAdmissao).GetOrganizacaoEParticular(codPlanoSaude)) {
					Set list.valorUnitario 	= 0 ;+rSet.%Get("Valor")
				}Else{
					Set list.valorUnitario 	= +$Select($IsObject(objRegraComercial):objRegraComercial.ValorUnitario	,1:0)
					If ($IsObject(objRegraComercial)) {
						For count = 1 : 1 : objRegraComercial.Componentes.Count() {
							Set objRegraComerCompont = objRegraComercial.Componentes.GetAt(count)
							Set list.valorUnitario 	= list.valorUnitario + objRegraComerCompont.ValorUnitario	
						}			
					}
				}
				Set list.custodoItem = custoItem
				Set list.status = $Select(objRegraComercial.isInelegivel = 1:"NÃ£o atende",1:"")
				Do retorno.%Push(list)
			}
		}
	}
	;
	Do retorno.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>
</Class>
</Export>