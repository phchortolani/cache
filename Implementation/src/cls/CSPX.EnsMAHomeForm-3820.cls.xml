<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.EnsMAHomeForm">
<Description><![CDATA[
Ensemble Maintenance Home Page
We display and save the time out and refresh rate in SECONDS
When you use it you need to times 1000 because JaveScript uses milliseconds intervals]]></Description>
<ClassType/>
<ProcedureBlock>1</ProcedureBlock>
<Super>%RegisteredObject,%CSP.Util.AutoForm</Super>
<System>3</System>

<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Property name="AutoLogout">
<Description><![CDATA[
AutoLogout flag (True or False, default is True)]]></Description>
<Type>%Boolean</Type>
</Property>


<Property name="RefreshRate">
<Description><![CDATA[
Home page refresh rate (in seconds, 0 mean no refresh)]]></Description>
<Type>%Integer</Type>
<InitialExpression>23</InitialExpression>
</Property>


<Property name="AutoStart">
<Description><![CDATA[
AutoStart default production]]></Description>
<Type>%String</Type>
</Property>


<Method name="CreateInstance">
<Description><![CDATA[
Event Log Max Number to Delete]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;pID:%String</FormalSpec>
<ReturnType>%CSP.Util.AutoForm</ReturnType>
<Implementation><![CDATA[
	Set tObj = ..%New()
	Set tObj.AutoLogout = %request.GetCookie("AutoLogout")
	If tObj.AutoLogout = "" Set tObj.AutoLogout = 1
	Set tObj.RefreshRate = %request.GetCookie("RefreshRate")
	If tObj.RefreshRate	= "" Set tObj.RefreshRate = 23
	Set tObj.AutoStart = $G(^Ens.AutoStart)
	Set %session.Data("Home","AutoLogout") = tObj.AutoLogout
	Set %session.Data("Home","RefreshRate") = tObj.RefreshRate
	Set %session.Data("Home","Saved") = 0
	Set %session.Data("Home","AutoStart") = tObj.AutoStart
	Quit tObj
]]></Implementation>
</Method>

<Method name="OnSubmit">
<FormalSpec>&amp;pID:%String,pSubmit:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
        // pID contains $ID values from URL
        If (pSubmit = "$AUTOFORM_SAVE") {
	        Set ..AutoLogout = +(..AutoLogout)
	        //If ..AutoLogout = 0 Set %session.AppTimeout = 0  // disable session timeouts
	        Set ..RefreshRate = +(..RefreshRate)
			Set %session.Data("Home","AutoLogout") = ..AutoLogout
	   		Set %session.Data("Home","RefreshRate") = ..RefreshRate
			Set expires = "Thursday, 31-Dec-2020 23:59:59 GMT"
			Do %response.SetCookie("AutoLogout",..AutoLogout,expires)
			Do %response.SetCookie("RefreshRate",..RefreshRate,expires)
			Do ##class(Ens.Director).SetAutoStart(..AutoStart)
        }
        Quit $$$OK
]]></Implementation>
</Method>

<Method name="OnDefineButtons">
<Description><![CDATA[
define standard Login buttons]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;pButtons:%CSP.Util.FormButton</FormalSpec>
<Implementation><![CDATA[
	Set pButtons(1) = ##class(%CSP.Util.FormButton).%New()
	Set pButtons(1).Name = "$AUTOFORM_SAVE"
	Set pButtons(1).Caption = $$$Text("Save")
]]></Implementation>
</Method>

<Method name="DrawHTMLForm">
<Description><![CDATA[
Draw an HTML form for this form object.<BR>
<var>pObj</var> is an instance of the form object that
provides the initial values for the form.<BR>
<var>pActionURL</var> is the URL to submit this form to.<BR>
<var>pButtons</var> is an array of submit buttons to display for the form. If 
not provided, the default <I>Save</I> and <I>Cancel</I> buttons are shown.
<var>pButtons</var> is an integer-subscripted array of <class>FormButton</class> objects.]]></Description>
<ClassMethod>1</ClassMethod>
<CodeMode>objectgenerator</CodeMode>
<FormalSpec>pObj:%RegisteredObject,&amp;pID:%String,pActionURL:%String="&quot;,&amp;pButtons:%CSP.Util.FormButton,pTarget:%String="&quot;</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[

	Do %code.WriteLine(" Set AutoLogoutChecked = """"")
	Do %code.WriteLine(" If $G(%session.Data(""Home"",""AutoLogout"")) = 1 Set AutoLogoutChecked = ""checked""")
	// generate FORM/table headers
	Do %code.WriteLine(" &html<<form action=""#(pActionURL)#"" method=""POST"" name=""theForm"" TARGET=""#(pTarget)#"">>")
	Do %code.WriteLine(" &html<<input type=""HIDDEN"" name=""$CLASS"" VALUE=""" _ %class.Name _ """>>")
	Do %code.WriteLine(" &html<<input type=""HIDDEN"" name=""$FRAME"" VALUE=""#(pTarget)#"">>")
	Do %code.WriteLine(" &html<<input type=""HIDDEN"" name=""CSPCHD"" VALUE=""#(%session.CSPSessionCookie)#"">>")

	Do %code.WriteLine(" Set id = $O(pID(""""))")
	Do %code.WriteLine(" While (id '= """") {")
	Do %code.WriteLine(" 	&html<<input type=""HIDDEN"" name=""$ID#(id)#"" VALUE=""#($ZCVT(pID(id),""O"",""HTML""))#"">>")
	Do %code.WriteLine(" 	Set id = $O(pID(id))")
	Do %code.WriteLine(" }")

	Do %code.WriteLine(" Set AutoLogout = $G(%session.Data(""Home"",""AutoLogout""))")
	Do %code.WriteLine(" Set RefreshRate = $G(%session.Data(""Home"",""RefreshRate""))")
	Do %code.WriteLine(" Set AutoStart = $G(%session.Data(""Home"",""AutoStart""))")

	// do not create fields for ourself...
	If ('$G(%parameter("COMMANDFORM"))) {
		If (%class.Name '= "%CSP.Util.AutoForm") {
			Do %code.WriteLine(" Set MAAutoTimeout = $$$TextHTML(""Management Auto Timeout"")")
			Do %code.WriteLine(" Set MAAutoTimeoutHelp = $$$TextHTML(""Check here if you want Ensemble pages to automatically close and return to the Login page after a page is inactive for "")")
			Do %code.WriteLine(" Set AutoRefreshRate = $$$TextHTML(""Auto-Refresh Rate"")")
			Do %code.WriteLine(" Set AutoRefreshRateHelp = $$$TextHTML(""This rate is used to auto-refresh the Ensemble pages, when applicable. Enter 0 for no Refresh."")")
			Do %code.WriteLine(" Set seconds = $$$TextHTML("" seconds"")")
			Do %code.WriteLine(" Set Version = ##class(Ens.Version).GetVersion(0)")
			Do %code.WriteLine(" Set currentversion = $$$FormatText($$$Text(""Current Ensemble Version is %1""),Version)")
			Do %code.WriteLine(" Set AutoStartCaption = $$$TextHTML(""Auto-Start Production "")")
			Do %code.WriteLine(" Set AutoStartHelp = $$$TextHTML(""You may select a production to start automatically at system startup."")")
			Do %code.WriteLine(" &html<<table class='AutoForm'>>")
			Do %code.WriteLine(" &html<<tr class='AutoForm' valign='center'>>")
			Do %code.WriteLine(" &html<<tr><th nowrap colspan='3'>#(currentversion)#</th></tr>>")
			Do %code.WriteLine(" &html<<tr><td colspan='3'><hr></td></tr>>")
			Do %code.WriteLine(" &html<<tr><th nowrap class='AutoFormCaption'>#(MAAutoTimeout)#<input type='checkbox' name='AutoLogout' #(AutoLogoutChecked)#></th></tr><tr><td colspan='3' class='AutoFormValue'>#(MAAutoTimeoutHelp)# <strong>#(%session.AppTimeout)#</strong> #(seconds)#.</td></tr>>")
			Do %code.WriteLine(" &html<<tr><td colspan='3'><hr></td></tr>>")
			
			Do %code.WriteLine(" &html<<tr><th nowrap class='AutoFormCaption'>#(AutoRefreshRate)#<input type='text' name='RefreshRate' value='#(RefreshRate)#' size='5'> #(seconds)#</th></tr><tr><td colspan='3' class='AutoFormValue'>#(AutoRefreshRateHelp)#</td></tr>>")
			Do %code.WriteLine(" &html<<tr><td colspan='3'><hr></td></tr>>")

			Do %code.WriteLine(" &html<<tr><th nowrap class='AutoFormCaption'>#(AutoStartCaption)#<input type='hidden' name='AutoStart' id='AutoStart' value='#(AutoStart)#'><select name='AutoStartList' onchange='document.getElementById(""AutoStart"").value=this.value;'>>")
			Do %code.WriteLine(" Set tRS = ##class(%ResultSet).%New(""Ens.Config.Production:ProductionStatus"")")
			Do %code.WriteLine(" Do tRS.Execute()")
			Do %code.WriteLine(" &html<<option>&nbsp;</option>>")
			Do %code.WriteLine(" For  Quit:'tRS.Next()  Set production = tRS.GetData(1),sel = $S(production=AutoStart:""selected"",1:"""") &html<<option value='#(production)#' #(sel)#>#(production)#</option>>")
			Do %code.WriteLine(" &html<</select>>")
			Do %code.WriteLine(" &html<</th></tr><tr><td colspan='3' class='AutoFormValue'>#(AutoStartHelp)#</td></tr>>")
			Do %code.WriteLine(" &html<<tr><td colspan='3'><hr></td></tr>>")

			Do %code.WriteLine(" &html<<tr><td>&nbsp;</td>>")
			Do %code.WriteLine(" &html<<td class=""AutoFormButton"">>")

		}
	}
	Else {
		// command form
	}
	
	// test for user-provided buttons method
	If $$$comMemberKeyGet(%class.Name,$$$cCLASSmethod,"OnDefineButtons",$$$cMETHrunnable) {
		Do %code.WriteLine(" If ('$D(pButtons)) {")
		Do %code.WriteLine(" 	Do ..OnDefineButtons(.pButtons)")
		Do %code.WriteLine(" }")
	}
	
	// button(s)
	Do %code.WriteLine(" Do ..DrawFormButtons(.pButtons)")
	Do %code.WriteLine(" If (%request.Get(""$SUBMITBTN"") = ""$AUTOFORM_SAVE"") {")
	Do %code.WriteLine(" 	Set lsaved = $$$TextHTML(""Modification Saved"")")
	Do %code.WriteLine(" 	&html<<tr><td colspan=""3"" bgcolor=""white"">#(lsaved)#.</td></tr>>")
	Do %code.WriteLine(" 	&html<<tr><td colspan=""3"">&nbsp;</td></tr>>")
	Do %code.WriteLine(" }")

	
	If ('$G(%parameter("COMMANDFORM"))) {
		If (%class.Name '= "%CSP.Util.AutoForm") {
			Do %code.WriteLine(" &html<</td></tr></table>>")
		}
	}
	Do %code.WriteLine(" &html<</form>>")
	Do %code.WriteLine(" Quit $$$OK")
	
	Quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>