<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.data.import.VARProdutosFarmacotecnicaCarga">
<ClassType/>
<IncludeCode>VARConst,INConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>AbstractCarga</Super>

<Parameter name="DEBUG">
<Description><![CDATA[Comandos:
> do ##class(VAR.data.import.VARProdutosFarmacotecnicaCarga).carregarArquivo()

Campos:
  Arquivo:
    CÓDPRODUTO;DESCRESUMIDA;DESCCOMPLETA;STATUS;UNIDADE;TIPODISPENSACAO;GRUPO;ORIGEM;PADRONIZADO]]></Description>
<Type>%Integer</Type>
<Default>0</Default>
</Parameter>

<Property name="tipoArquivo">
<Type>%Integer</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Property name="globalControle">
<Type>%String</Type>

<Parameter name="TRUNCATE" value="1"/>
</Property>

<Method name="carregarArquivo">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	do ..importar(##class(DirectoryFile).GetNamespaceSubdir("carga")_
		"Produtos Farmacotecnica.csv")]]></Implementation>
</Method>

<Method name="%OnNew">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	if (..#DEBUG) set ..imprimirAndamento = 0
	
	set ..globalControle = "^VARProdutosFarmacotecnicaCarga("""_##class(Util).GetNamespace()_""")"	
	
	if ('$data(@..globalControle)) {
		set idINART 	= ""
		set objINART 	= ""
		set count		= 0
		set strStatus 	= $$$OK
		
		write !!,"Atualizando a flag dos produtos da INART...",!
		
		for { 
			set idINART = $order(^INART(YM,idINART))
			quit:idINART=""
			
			set $piece(^INART(YM,idINART,1),Y,418) = 1
		
			set count = $i(count)
		
			if (count#1000 = 0) {
	            w !,count_" registros alterados até o momento..."
	        }
		}
		
		w !,count_" registros alterados no total!"
		
		set lastUnidade = $order(^WWW101(0,"EINHEIT","PT",""),-1)
		quit:lastUnidade=""
	
		set objUnidade 	= ""
		set novaUnidade = lastUnidade + 1
	
		set key = "EINHEIT"_$$$COMMA_"PT"_$$$COMMA_novaUnidade
		set $$$WWW101Text(objUnidade) = "DOS - DOSADOR"
	
		set strStatus = $$$Save("WWW101",key,objUnidade,1)
		
		w !!,"Nova unidade criada com sucesso!"
	
		if ($$$ISOK(strStatus)){
			set keyAtivar = "WWW101"_$$$COMMA_"EINHEIT"_$$$COMMA_novaUnidade
			set objVARParametroAtivar = ""
			set $piece(objVARParametroAtivar,Y,1) = $$$YES
			
			set strStatus = $$$Save("VARParametroAtivar",keyAtivar,objVARParametroAtivar,1)	
			
			w:$$$ISOK(strStatus) !,"Nova unidade ativada com sucesso!"
		}
	
		if $$$ISOK(strStatus) {
			do ReIndex^COMIndex("INART",1)
			do ReIndex^COMIndex("INARTPACK",1)
			set @..globalControle@(novaUnidade) = 1
			w !,"Flag dos produtos atualizada com sucesso!",!
		} else {
			write !!,"Erro ao atualizar a flag dos produtos da INART!",!
			quit $$$CUSTOMERROR("Operação abortada.")
		}
	}	

	quit ##super()]]></Implementation>
</Method>

<Method name="processar">
<FormalSpec>pintRegistros:%Integer,pstrLinha:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set $ztrap = "exceptionProc"
	set strStatus = $$$OK

	if (pintRegistros = 0) quit strStatus

	set obj = ""

	set codProduto			= ..getCampo(pstrLinha, 1)
	
	if ($length(codProduto) = 0) {
		quit "0O código de produto da linha #"_pintRegistros_" está vazio."
	}
	
	set descResumida 		= ..getCampo(pstrLinha, 2)
	set descCompleta		= ..getCampo(pstrLinha, 3)
	set statusProduto		= ..getCampo(pstrLinha, 4)
	
	if ($length(statusProduto) = 0) {
		quit "0O status de produto da linha #"_pintRegistros_" está vazio."
	} elseif ($$$UPPER(statusProduto) = "ATIVO") {
		set statusProduto = 0
	} elseif ($$$UPPER(statusProduto) = "INATIVO"){
		set statusProduto = 1
	}
	
	set unidadeApresentacao	= ..getCampo(pstrLinha, 5)
	
	if ($length(unidadeApresentacao) = 0) {
		quit "0A unidade de produto da linha #"_pintRegistros_" está vazia."
	} elseif ($$$UPPER(unidadeApresentacao) = "DOS - DOSADOR") {
		set unidadeApresentacao = $qsubscript($query(@..globalControle),2)
	}
	
	set tipoDispensacao		= ..getCampo(pstrLinha, 6)
	set grupoProduto		= ..getCampo(pstrLinha, 7)
	set origemProduto		= ..getCampo(pstrLinha, 8)
	set produtoPadronizado	= ..getCampo(pstrLinha, 9)
	
	if ($length(produtoPadronizado) = 0) {
		quit "0A flag de produto padronizado da linha #"_pintRegistros_" está vazia."
	} elseif ($$$UPPER(produtoPadronizado) = "NÃO") 
		||($$$UPPER(produtoPadronizado) = "NAO"){
		set produtoPadronizado = $$$NO
	} elseif ($$$UPPER(produtoPadronizado) = "SIM"){
		set produtoPadronizado = $$$YES
	}
	
	set key = codProduto

	set $$$INARTSearchName(obj)					= descResumida
	set $$$INARTCustomerTextorItemTextNo(obj) 	= descCompleta
	set $$$INARTHoldReason(obj)					= statusProduto
	set $$$INARTUnitofMeasure(obj)				= unidadeApresentacao
	set $$$INARTItemPolicy(obj)					= tipoDispensacao
	set $$$INARTItemGroup(obj) 					= grupoProduto
	set $$$INARTItemType(obj) 					= origemProduto
	set $$$INARTFREE5(obj) 						= produtoPadronizado

	if (..#DEBUG) {
		do ..imprimir(..quebraLinha_"key="_key_", obj="_obj)
		set strStatus = $$$OK
	}
	else {
		set strStatus = $$$Save("INART", key, obj, 1)
	}

finallyProc
	quit strStatus

exceptionProc
 	set $ztrap = ""
	set strStatus = $$$CUSTOMERROR($zerror)

	goto finallyProc]]></Implementation>
</Method>
</Class>
</Export>