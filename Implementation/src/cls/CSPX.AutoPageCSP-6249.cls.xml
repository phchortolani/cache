<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.AutoPageCSP">
<Description><![CDATA[
Base class used by CSP pages that work within the AutoPage framework.]]></Description>
<ClassType/>
<IncludeCode>EnsUI</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%CSP.Page</Super>
<System>3</System>

<Parameter name="CHARSET">
<Default>utf-8</Default>
</Parameter>


<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Parameter name="LOGINPAGE">
<Description><![CDATA[
Name of the page to use for logins]]></Description>
<Default>UtilLogin.csp</Default>
</Parameter>


<Parameter name="PAGENAME">
<Description><![CDATA[
Name of this page]]></Description>
</Parameter>

<Parameter name="PARENTPAGE">
<Description><![CDATA[
Name of the parent page (used for the locator bar)]]></Description>
</Parameter>

<Method name="WriteTitlePane">
<Description><![CDATA[
Write out the title pane for this page]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pTitle:%String</FormalSpec>
<Implementation><![CDATA[
	Set tInstance = ##class(%CSP.Util.PageInstance).%New()
	Set tInstance.Title = $$$LocalizeText($P($P(..#PAGENAME,"@",2),":",1),$P($P(..#PAGENAME,"@",2),":",2),$P(..#PAGENAME,"@",3))
	Set tInstance.PageClass = ..%ClassName(1)
	
	Set tTitlePane = ##class(%CSP.Util.TitlePane).%New()
	Set tTitlePane.Text = $$$LocalizeText($P($P(pTitle,"@",2),":",1),$P($P(pTitle,"@",2),":",2),$P(pTitle,"@",3))
	
	// Title pane
	If ($IsObject($G(tTitlePane))) {
		Do ..GetLocator(tTitlePane)
		
		Do tTitlePane.DrawBODY(tInstance)
	}
]]></Implementation>
</Method>

<Method name="OnPreHTTP">
<ClassMethod>1</ClassMethod>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	Do SetIO^%NLS($$MapCharset^%NLS(..#CHARSET))

	// remove session lock to prevent blocking
	Do %session.Unlock()

	// test for autoform submit (for login)
	Set tSubmitBtn = ##class(%CSP.Util.AutoForm).DoSubmit()

	// test for login
	If ((..#LOGINPAGE '= "") && (%session.Username = "")) {
		// if this is not the login page, then go there

		If (..#LOGINPAGE '= %request.PageName) {
			// check if we are called from a frame...
			Set tf = $G(%request.Data("$FRAME",1))
			If (tf = "_top") {
				Set tf = ""
			}
			
			Set %response.Redirect = ..#LOGINPAGE _ "?$ID1=" _ ..EscapeURL(%request.URL _ "?$ID1=" _ $G(%request.Data("$ID1",1)) _ $S(tf="":"",1:"&$FRAME=" _ tf))
			Quit 1
		}
	}
	ElseIf ((tSubmitBtn = "$AUTOFORM_LOGIN") && (%session.Username '= "") && $D(%request.Data("$ID1",1))) {
		// redirect to original desination after login
		Set %response.Redirect = %request.Data("$ID1",1)
		Quit 1
	}
	Quit 1
]]></Implementation>
</Method>

<Method name="GetLocator">
<Description><![CDATA[
Get the list of items for the locator bar.]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pTitlePane:%CSP.Util.TitlePane,&amp;pAddress:%String,&amp;pBaseURL:%String=%request.URL</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	Set tParent = ..#PARENTPAGE
	Set tName = ..GetPageName()
	Set tURL = ..#CSPURL
	Set pAddress = ..#PAGENAME
	
	// get parent page locator
	Set tParentClass = $System.CSP.GetClassName(tParent,pBaseURL)
	If (tParentClass '= "") {
#if $system.Version.GetNumber()>5.0
		Set pBaseURL=$$CSPNormalizeURL^%SYS.cspServer(tParent,pBaseURL)
#else
		Set pBaseURL=$$CSPNormalizeURL^%cspServer(tParent,pBaseURL)
#endif
		Set tReqURL=%request.URL, %request.URL=pBaseURL
		Do $zobjclassmethod(tParentClass,"GetLocator",pTitlePane,.tAddress) ; ,pBaseURL) -if superclasses support the extra arg
		Set %request.URL=tReqURL
		Set:tAddress'="" pAddress = tAddress _ "," _ pAddress
	}
	Set tLink = ##class(%CSP.Util.Link).%New()
	Set tLink.Caption = tName
	Set tLink.HREF = tURL
	Do pTitlePane.Locator.Insert(tLink)
	Quit tName
]]></Implementation>
</Method>

<Method name="GetPageName">
<Description><![CDATA[
Return localized version of PageName]]></Description>
<ClassMethod>1</ClassMethod>
<CodeMode>objectgenerator</CodeMode>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
#if $system.Version.GetNumber()>5.0
	Do %code.WriteLine(" Quit $$$Text("""_$G(%parameter("PAGENAME"),"N/A")_""")")
#else
	Do %code.WriteLine(" Quit """_$G(%parameter("PAGENAME"),"N/A")_"""")
#endif
	Quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>