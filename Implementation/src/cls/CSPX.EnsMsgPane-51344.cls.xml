<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.EnsMsgPane">
<Description><![CDATA[
An AutoPage pane that displays a table based on a class query.]]></Description>
<IncludeCode>EnsUI,Ensemble</IncludeCode>
<Super>%CSP.Util.Pane</Super>
<System>3</System>

<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Method name="DrawBODY">
<Description><![CDATA[
Draw the BODY of the detail pane]]></Description>
<FormalSpec>pInstance:%CSP.Util.PageInstance</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Do ..DrawCommands(pInstance)

	// get page ID values
	Merge tID = pInstance.IDValues
	
	// this is an event log entry
	If ($G(%request.Data("EVENT",1))) {
		Quit ..DrawEVENT($G(tID(1),$G(%request.Data("ID1",1))))
	}

	// open header
	Set msgId = $G(tID(1),$G(%request.Data("ID1",1)))
	Set tHeader = ##class(Ens.MessageHeader).%OpenId(msgId,0)

	If ('$IsObject(tHeader)) {
		Set Clickon = $$$Text("Click on ")
		Set details = $$$Text("details")
		Set ClickonMsg = $$$Text(" in the above list to view Message Details.")
		
		&html<<div class="Text1">#(Clickon)# <i>#(details)#</i>#(ClickonMsg)#</div><hr>>
	}
	Else {
		Set DetailsOfSelected = $$$Text("Details of the selected ")
		If tHeader.Type=1 Set msgTypeMsg = $$$Text("request message:")
		Else  Set msgTypeMsg = $$$Text("response message:")
		Set msgBody = $$$Text("Message Body")
		Set msgHeader = $$$Text("Message Header")
		Set msgType = $$$Text("Message Type")
		Set msgContents = $$$Text("Contents")
		&html<<table border="0" cellspacing="0" cellpadding="0" cols="2" width="100%"><tr>
			<td align="left"><div class="Text1" style="margin: 3px;">#(DetailsOfSelected)# <i>#(msgTypeMsg)#</i></div></td>
			<td align="right"><div class="Text" style="color:darkblue;">[<a style="color:darkblue;" href="#(##class(%CSP.Page).Link("CSPX.EnsMsgContentsPane.cls?$ID1="_msgId))#">#(msgContents)#</a>]</div></td>
			</tr>
			</table>
			<hr>
			<table border="0" cols="2" width="100%"><tr>
			<td valign="top" width="30%">
			<div class="Text" style="padding-bottom:10px;"><b>#(msgHeader)#</b></div>>

		// show header
		Do tHeader.DrawHTMLTable(tHeader,.tID)
		
		&html<</td><td valign="top" width="70%">>

		// open body
		Set tBody = $S(""=tHeader.MessageBodyClassName:$$$NULLOREF, 1:$zobjclassmethod(tHeader.MessageBodyClassName,"%OpenId",tHeader.MessageBodyId))
		If ($IsObject(tBody)) {
			// show body
			&html<<DIV CLASS="Text" ><b>#(msgBody)#</b>: [#(msgType)#: #(tBody.%ClassName(1))#]</DIV>>
			If tBody.%Extends("%CSP.Util.AutoForm") { Do tBody.DrawHTMLTable(tBody,.tID) }
			Else { Do:'##class(Ens.VDoc.Utils).IsVirtualDocument(tBody.%ClassName(1))||$G(^Ens.Debug("UtilEnsMsgTrace","vdocdetails")) ##class(%CSP.Util.AutoFormDynamic).DrawHTMLTable(tBody,.tID) }
		}
		&html<</td></tr></table>>
	}
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="DrawEVENT">
<Description><![CDATA[
Draw details of the given event log entry.]]></Description>
<Internal>1</Internal>
<FormalSpec>pID:%Integer</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set tEvent = ##class(Ens.Util.Log).%OpenId(pID,0)
	If '$IsObject(tEvent) {
		&html<<div class="Text1" style="margin: 3px;">#($$$Text("Unable to display event log entry."))#</div>>
	}
	Else {
		&html<<div class="Text1" style="margin: 3px;">#($$$Text("Details for the selected event log entry:"))#</div><hr/>>
		&html<<table class="AutoFormTable" border="0" cellspacing="0">
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Event Id"))#</td><td class="AutoFormTableValue">#($ZCVT(pID,"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Type"))#</td><td class="AutoFormTableValue">#($ZCVT(tEvent.TypeLogicalToDisplay(tEvent.Type),"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Time Logged"))#</td><td class="AutoFormTableValue">#($ZCVT($$$timeUTCtoLocal(tEvent.TimeLogged),"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Session Id"))#</td><td class="AutoFormTableValue">#($ZCVT(tEvent.SessionId,"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Job"))#</td><td class="AutoFormTableValue">#($ZCVT(tEvent.Job,"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Source"))#</td><td class="AutoFormTableValue">#($ZCVT(tEvent.SourceClass,"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Method"))#</td><td class="AutoFormTableValue">#($ZCVT(tEvent.SourceMethod,"O","HTML"))#</td></tr>
		<tr class="AutoFormTableRow"><td class="AutoFormTableCaption">#($$$TextHTML("Text"))#</td><td class="AutoFormTableValue">#($ZCVT(tEvent.Text,"O","HTML"))#</td></tr>
		</table>>

	}
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="ProcessDates">
<ClassMethod>1</ClassMethod>
<FormalSpec>&amp;startDate:%String,&amp;endDate:%String</FormalSpec>
<Implementation><![CDATA[
	If (startDate = "") {
		Set startDate = $ZDT(+$H,3)
	}
	ElseIf ($zdth(startDate,3,,,,,,,,"error") = "error") {
		// try default format
		If ($zdth(startDate,-1,,,,,,,,"error") = "error") {
			Set startDate = "Error"
		}
		Else {
			Set startDate = $zdt($zdth(startDate,-1),3)
		}
	}
		
	If (endDate = "") {
		Set endDate = $ZDT($H,3)
	}
	ElseIf ($zdth(endDate,3,,,,,,,,"error") = "error") {
		// try default format
		If ($zdth(endDate,-1,,,,,,,,"error") = "error") {
			Set endDate = "Error"
		}
		Else {
			Set endDate = $zdt($zdth(endDate,-1),3)
		}
	}
]]></Implementation>
</Method>
</Class>
</Export>