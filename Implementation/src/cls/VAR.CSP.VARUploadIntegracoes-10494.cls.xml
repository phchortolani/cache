<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.CSP.VARUploadIntegracoes">
<ClassType/>
<IncludeCode>MEDConst,VARConst,INConst,WWWConst,FATConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	$$$VAR
	Set method = $get(%request.Data("method",1))
	Xecute "do .."_method_"()"
	Return $$$OK]]></Implementation>
</Method>

<Method name="GetDados">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	$$$VAR
	Set YM 		= 0
	Set Y 		= "~"
	Set YBED 	= $Get(%request.Data("YBED",1))
	;
	Set listRetorno 				= {}
	Set objWWW013 					= $Get(^WWW013(YM, YBED, 1))
	Set clienteIntAgend 		= $$$WWW013ClienteIntegracaoAgendame(objWWW013)
	Set descClienteIntAgend = $$SQLGetUserParamDescVAR^VARSQL("CLIENTEINTAGEND~"_clienteIntAgend)
	;
	Set objParInteg					= ""
	If (clienteIntAgend '= "") {
		Set objParInteg  = $Get(^VARParametrosIntegracoes(YM, clienteIntAgend, 1))
	}
	;
	Set listRetorno.codClienteIntAgendame = clienteIntAgend
	Set listRetorno.descClienteIntAgendame = descClienteIntAgend
	Set listRetorno.codModulo	= $$$VARParametrosIntegracoesModuloUploadArquivo(objParInteg)
	;
	Do listRetorno.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>

<Method name="GetArquivos">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	$$$VAR
	Set YM 					= 0
	Set Y 					= "~"
	Set YBED 				= $Get(%request.Data("YBED",1))
	Set pCodCliente = $Get(%request.Data("pCodCliente",1))
	Set retorno = []
	Set meuSQL = "Select * From SQLUser.VARArqUploadInteg Where Company = 0 And Cliente = '"_pCodCliente_"' Order By DataHoraUpload Desc"
	Set rSet = ##class(%SQL.Statement).%ExecDirect("",meuSQL)
	While(rSet.%Next()){
		Set list = {}
		Set list.dataHora = $ZDateTime(rSet.%Get("DataHoraUpload"),4,2)
		Set list.arquivo 	= rSet.%Get("NomedoArquivo")
		Do retorno.%Push(list)
	}	
	Do retorno.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>

<Method name="GetArquivosDownload">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	$$$VAR
	Set YM 					= 0
	Set Y 					= "~"
	Set YBED 				= $Get(%request.Data("YBED",1))
	Set pCodCliente = $Get(%request.Data("pCodCliente",1))
	Set codModulo		= 18
	;
	Set retorno = []
	;
	Set dname = $$GetDiretorioFisico^VARBRDiretorios(0,codModulo)_"\Download"
	If ($$$isWINDOWS) Set dname = $Replace(dname,"/","\"),del="\"
	Else  Set dname = $Replace(dname,"\","/"),del="/"
	;
	Set rSet = ##Class(%ResultSet).%New("%File:FileSet")
	Do rSet.Execute(dname)
	While rSet.Next() {
		Set list = {}
		Set list.dataHora = $ZDateTime($ZDateTimeH(rSet.Data("DateCreated"),3,2),4,2)
		Set list.arquivo 	= $Piece(rSet.Data("Name"),del,$L(rSet.Data("Name"),del))
		Set list.caminho 	= $Replace(YGIF_$Piece(rSet.Data("Name"),del,$Length(rSet.Data("Name"),del)-3,$Length(rSet.Data("Name"),del)),"\","/") 
		Do retorno.%Push(list)
	}	
	Do retorno.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>
</Class>
</Export>