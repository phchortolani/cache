<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.data.import.INARTPoliticaCarga">
<ClassType/>
<IncludeCode>INConst,VARLog</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>AbstractCarga</Super>

<Parameter name="DEBUG">
<Description><![CDATA[Comandos:
> do ##class(VAR.data.import.INARTPoliticaCarga).carregarArquivo()

Campos:
  Arquivo:
    Código (CHAVE);Tipo de Dispensação (D419);]]></Description>
<Type>%Integer</Type>
<Default>0</Default>
</Parameter>

<Property name="primeiraLinhaDados">
<Type>%Integer</Type>
</Property>

<Property name="globalControle">
<Type>%String</Type>

<Parameter name="TRUNCATE" value="1"/>
</Property>

<Method name="carregarArquivo">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	do ..importar(##class(DirectoryFile).GetNamespaceSubdir("carga")_
		"Politica de dispensação "_$znspace_" - CARGA.csv")]]></Implementation>
</Method>

<Method name="%OnNew">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set ..ignorarPrimeiraLinha = 1
	if (..#DEBUG) set ..imprimirAndamento = 0
	set ..primeiraLinhaDados = 0
	set ..globalControle = "^VAR.data.import.INARTPoliticaCarga("""_$znspace_""")"

	set total = 0
	set codProduto = $order(^INART(YM, ""))
	while ($length(codProduto) > 0) {
		set total = $increment(total)

		set codProduto = $order(^INART(YM, codProduto))
	}

	write !, "Existem "_total_" registros na global ^INART. "
	read "Deseja continuar? (S/N) ", continuar#1

	if ((continuar '= "S") && (continuar '= "s")) quit $$$CUSTOMERROR("Operação abortada.")

	quit ##super()]]></Implementation>
</Method>

<Method name="processar">
<FormalSpec>pintRegistros:%Integer,pstrLinha:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set $ztrap = "exceptionProc"
	set strStatus = $$$OK

	set ..primeiraLinhaDados = 1

	set codProduto           = ..getCampo(pstrLinha, 1)
	set idItemPolicy         = ..getCampo(pstrLinha, 3)
	
	if ('$data(^INART(YM, codProduto, 1))) {
		quit "0 Produto '"_codProduto_"' da linha #"_pintRegistros_" não "_
			"foi encontrado"
	}

	if (+$get(@..globalControle@(codProduto))) quit strStatus

	set key = codProduto
	set obj = ^INART(YM, key, 1)
	set $$$INARTItemPolicy(obj) = idItemPolicy

	if (..#DEBUG) {
		do ..imprimir(..quebraLinha_"key="_key_", obj="_obj)
	}
	else {
		set strStatus = $$$Save("INART", key, obj, 1)

		if ($$$ISOK(strStatus)) {
			set @..globalControle@(codProduto) = 1
		}
		else {
			$$$VARLOGERROR(strStatus)
		}
	}

finallyProc
	quit strStatus

exceptionProc
 	set $ztrap = ""
	set strStatus = $$$CUSTOMERROR($zerror)

	goto finallyProc]]></Implementation>
</Method>
</Class>
</Export>