<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.data.import.INDRPITEMCarga">
<ClassType/>
<IncludeCode>INConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>AbstractCarga</Super>

<Parameter name="DEBUG">
<Description><![CDATA[Comandos:
> do ##class(VAR.data.import.INDRPITEMCarga).carregarArquivo()

Campos:
  Arquivo:
    COD_GRUPO;NOME;GRUPO_PAI;ATIVO]]></Description>
<Type>%Integer</Type>
<Default>0</Default>
</Parameter>

<Method name="carregarArquivo">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	do ..importar(##class(DirectoryFile).GetNamespaceSubdir("carga")_
		"Local X Produtos X Quantidade.txt")]]></Implementation>
</Method>

<Method name="%OnNew">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	if (..#DEBUG) set ..imprimirAndamento = 0
	set ..separador = $char(9)
	set ..ignorarPrimeiraLinha = 1

	set total = 0
	set codLocal = $order(^INDRPITEM(YM, ""))
	while ($length(codLocal) > 0) {
		set codProduto = $order(^INDRPITEM(YM, codLocal, ""))
		while ($length(codProduto) > 0) {
			set total = $increment(total)
			set codProduto = $order(^INDRPITEM(YM, codLocal, codProduto))
		}

		set codLocal = $order(^INDRPITEM(YM, codLocal))
	}

	write !, "Existem "_total_" registros na global ^INDRPITEM. "
	read "Deseja continuar? (S/N) ", continuar#1

	if ((continuar '= "S") && (continuar '= "s")) quit $$$CUSTOMERROR("Operação abortada.")

	quit ##super()]]></Implementation>
</Method>

<Method name="processar">
<FormalSpec>pintRegistros:%Integer,pstrLinha:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	set $ztrap = "exceptionProc"
	set strStatus = $$$OK

	if (pintRegistros = 1) quit strStatus

	set codLocal			= ..getCampo(pstrLinha, 1)
	set codProduto			= ..getCampo(pstrLinha, 2)
	set estoqueMaximo		= ..getCampo(pstrLinha, 3)
	set pontoRessuprimento	= ..getCampo(pstrLinha, 4)

	if (($length(codLocal) = 0) || ($length(codProduto) = 0)) {
		quit "0 Local '"_codLocal_"' ou produto '"_codProduto_"' da linha "_
			"#"_pintRegistros_" inválido"
	}

	if ('$data(^WWW0121(YM, YM, codLocal, 1))) {
		quit "0 Local '"_codLocal_"' da linha #"_pintRegistros_" não "_
			"foi encontrado"
	}
	if ('$data(^INART(YM, codProduto, 1))) {
		quit "0 Produto '"_codProduto_"' da linha #"_pintRegistros_" não "_
			"foi encontrado"
	}

	set key = codLocal_$$$COMMA_codProduto

	set obj = $get(^INDRPITEM(YM, codLocal, codProduto, 1))

	set $$$INDRPITEMDefaultStockQuantity(obj)	= estoqueMaximo
	set $$$INDRPITEMReOrderPoint(obj)			= pontoRessuprimento

	if (..#DEBUG) {
		do ..imprimir(..quebraLinha_"key="_key_", obj="_obj)
		set strStatus = $$$OK
	}
	else {
		set strStatus = $$$Save("INDRPITEM", key, obj, 1)
	}

finallyProc
	quit strStatus

exceptionProc
 	set $ztrap = ""
	set strStatus = $$$CUSTOMERROR($zerror)

	goto finallyProc]]></Implementation>
</Method>
</Class>
</Export>