<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.EnsCredentialsEditor">
<Description><![CDATA[
Credentials Editor -
This CSP page is the bottom frame of the Credentials page, and handles creating new credentials,
editing existing credentials and deleting credentials.
]]></Description>
<Super>CSPX.AutoPageCSP</Super>

<Method name="DrawHTMLHead">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	&html<
	<html>
	
	<head>
	
	<link rel="stylesheet" type="text/css" href="/csp/sys/csputils.css" title="Standard Style" >

	<style>
	<!--
	-->
	</style>
	
	</head>
	>
]]></Implementation>
</Method>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	// This page displays a header message at the top, followed by an optional editor form, followed
	// by an optional error message.  It can also display a JS alert and abort rendering the pagee.
	// These contents are controlled by the variables below:
	// MsgHeader = Message to be displayed as an informative message or header message
	// ShowForm = Flag indicating if the editor form will be shown
	// MsgError = Message to be displayed as an error text
	// MsgErrorJS = Message to be displayed as a JavaScript alert and to abort rendering the page
	
	Do ..DrawHTMLHead()
	
	Set MsgHeader = $$$Text("Edit Credentials definition:")
	Set MsgID = $$$Text("ID")
	Set MsgUser = $$$Text("Username")
	Set MsgPw = $$$Text("Password")
	Set MsgError = ""
	Set MsgErrorJS = ""
	
	Set ShowForm = 1
	Set RefreshDetail = 0

	Set ParentPage = "UtilEnsCredentials.csp"
	
	Set CredId = $G(%request.Data("$ID1",1))
	Set CredUser = ""
	Set CredPw = ""
	
	// This is a submit of itself to save the form data (button 'OK')
	If $G(%request.Data("$AUTOFORM_SAVE",1))="Save" {
		Set CredId = $G(%request.Data("SystemName",1))
		Set CredUser = $G(%request.Data("Username",1))
		Set CredPw = $G(%request.Data("Password",1))
		Set CredOldId = $G(%request.Data("PreviousSystemName",1))
		If CredId="" {
			Set MsgError = $$$Text("Credentials ID cannot be blank")
		}
		Else {
			If CredId=CredOldId {
				Set pOverwrite=1
			}
			Else {
				Set pOverwrite=0
			}
			Set s = ##class(Ens.Config.Credentials).SetCredential(CredId, CredUser, CredPw, pOverwrite)
			If s {
				// Refresh the Detail frame (top frame), display an informative message in the bottom frame
				Set RefreshDetail = 1
				Set ShowForm = 0
				Set MsgHeader = $$$FormatText($$$Text("Credentials '%1' saved"), CredId)
			}
			Else {
				Set MsgError = $system.Status.GetOneStatusText(s)
			}
		}
	}
	
	// This is a submit to delete credentials
	ElseIf $G(%request.Data("DELETE",1))=1 {
		&SQL(DELETE FROM Ens_Config.Credentials WHERE SystemName = :CredId)
		If SQLCODE=0 {
			// Refresh the Detail frame (top frame), display an informative message in the bottom frame
			Set RefreshDetail = 1
			Set ShowForm = 0
			Set MsgHeader = $$$FormatText($$$Text("Credentials '%1' deleted"), CredId)
		}
		Else {
			// Display a JS alert with the error and quit
			Set t = $$$Text("ERROR")
			Set MsgErrorJS = t_" - SQLCODE="_SQLCODE_" "_$g(%msg)
		}
	}
	
	// Credentials editor
	Else {
		If CredId'="" && ShowForm {
			Set CredObj = ##class(Ens.Config.Credentials).%OpenId(CredId)
			If $IsObject(CredObj) {
				Set CredUser = CredObj.Username
				Set CredPw = CredObj.Password
			}
			Else {
				// Display a JS alert with the error and quit
				Set MsgErrorJS = $$$FormatText($$$Text("Error while opening credentials object '%1'"), CredId)
			}
		}
	}
	
	// If there was an error while initializing, we simply give an alert and abort, don't show the bottom frame
	If $G(MsgErrorJS)'="" {
		&html<
		<script language="javascript">
		alert(#(..QuoteJS(MsgErrorJS))#);
		parent.document.location.reload();
		</script>
		>
		Quit $$$OK
	}
	
	// Refresh the upper frame containing the list of credentials ('Detail' frame)
	If +$G(RefreshDetail) {
		&html<
		<script language="javascript">
		parent.frames['Detail'].location.reload();
		</script>
		>
	}
	
	// Draw the bottom frame ('Info' frame)
	&html<
	<body style="margin:0px; padding: 0px;">
	<table border="0" class="LayoutMain" width="100%" height="100%" cellpadding="0" cellspacing="0">
		<tr height="100%">
			<td class="LayoutContentCol">
			<div class="Text1">#(..EscapeHTML(MsgHeader))#</div><br>
	>
	
	If $G(ShowForm) {
		&html<
		<form action="" method="POST" name="theForm">
		<table class="AutoForm">
			<tr class="AutoForm" valign="CENTER">
				<td class="AutoFormCaption" nowrap>&nbsp; #(..EscapeHTML(MsgID))#:</td>
				<td class="AutoFormControl" nowrap><input class="AutoForm" type="TEXT" name="SystemName" size="50" maxlength="50" value='#(..EscapeHTML(CredId))#'></td></tr>
			<tr class="AutoForm" valign="CENTER">
				<td class="AutoFormCaption" nowrap>&nbsp; #(..EscapeHTML(MsgUser))#:</td>
				<td class="AutoFormControl" nowrap><input class="AutoForm" type="TEXT" name="Username" size="50" maxlength="200" value='#(..EscapeHTML(CredUser))#'></td></tr>
			<tr class="AutoForm" valign="CENTER">
				<td class="AutoFormCaption" nowrap>&nbsp; #(..EscapeHTML(MsgPw))#:</td>
				<td class="AutoFormControl" nowrap><input class="AutoForm" type="password" name="Password" size="50" maxlength="200" value='#(..EscapeHTML(CredPw))#'></td></tr>
			<tr><td class="AutoFormLabel">&nbsp;</td>
				<td class="AutoFormButton"><input class="AutoFormButton" TYPE="SUBMIT" name="$AUTOFORM_SAVE" value="Save">
					<input class="AutoFormButton" TYPE="BUTTON" name="$AUTOFORM_CANCEL" value="Cancel" onClick="goBack();"></td></tr>
		</table>
		<input type="HIDDEN" name="PreviousSystemName" value='#(..EscapeHTML(CredId))#'>
		</form>
		>
	}
	
	If $G(MsgError)'="" {
		&html<
		<table><tr><td><div class="ErrorBox">&nbsp; <b>#(..EscapeHTML(MsgError))#</b> &nbsp;</div></td></tr>
		</table>
		>
	}
	
	&html<
		</div>
		</td>
		</tr>
	</table>

	<form action="#(..Link(ParentPage,,1))#" target="_top" name="goBackForm">
	</form>
	
	</body>
	
	<script language="javascript">
	function goBack() {
		// Submits the goBackForm which is just an artifact to go the parent page target = _top
		self.document.goBackForm.submit();
	}
	</script>
	
	>
	
	
	// Debug info	
	//Set %response.TraceDump = 1
	//Merge ^helo($zh,"%request") = %request.Data

	
	Quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>