<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="CSPX.EnsMsgContentsPane">
<Description><![CDATA[
Page for displaying customized contents of messages]]></Description>
<ClassType/>
<ProcedureBlock>1</ProcedureBlock>
<Super>%CSP.Page</Super>
<System>3</System>

<Parameter name="DOMAIN">
<Description><![CDATA[
Use our own domain for localization]]></Description>
<Default>Ensemble</Default>
</Parameter>


<Parameter name="CONTENTTYPE">
<Default>text/html</Default>
</Parameter>


<Method name="OnPreHTTP">
<ClassMethod>1</ClassMethod>
<ReturnType>%Boolean</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	// Avoid IE HTTP 1.1 bugs
	Set %response.HTTPVersion = "HTTP/1.0"
	
	// Get the content type
	Set msg = ..GetMessage()
	If $IsObject(msg) {
		If msg.%Extends("Ens.Util.MessageBodyMethods") {
			Set %response.ContentType = msg.%GetContentType()
		} ElseIf msg.%Extends("%XML.Adaptor") {
			Set %response.ContentType = "text/xml"
		}
	}
	Quit 1
]]></Implementation>
</Method>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set msg = ..GetMessage()

 	If ($IsObject(msg) && (%response.ContentType '= "text/html")) {
	 	// non-html content
		If (msg.%Extends("%XML.Adaptor")) {
			// show contents as XML
			&html<<?xml version="1.0" ?><!-- type: #(msg.%ClassName(1))#  id: #(msg.%Id())# -->>
			Do msg.XMLExport()
		} Else {
	 		Do msg.%ShowContents()
		}
		Quit $$$OK
	}
	&html<<html><head>

	<title>Message Contents Pane</title>
<style>
body {
	background: #CCCCEE;
	margin: 20px;
	padding: 0px;
	font-family: Lucida sans unicode, verdana, sans-serif;
}

div.text 	{color: black; }
div.error 	{color: red; }

</style>>

	If ($IsObject(msg) && msg.%Extends("Ens.Util.MessageBodyMethods")) {
		Do msg.%ShowContentsHead()
	}
	&html<</head><body>>
		
	If ('$IsObject(msg)) {
		&html<<div class="errormsg">#($$$Text("There is no message body associated with this message."))#</div>>
	}
	Else {
		If msg.%Extends("Ens.Util.MessageBodyMethods") {
			Do msg.%ShowContents()
		} Else {
			&html<<div>$$$Text("There is no content viewer defined for this message type."))#</div>>
		}
	}
	&html<</body></html>>
	
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="GetMessage">
<Description><![CDATA[
Return the message we are supposed to display]]></Description>
<ClassMethod>1</ClassMethod>
<ReturnType>Ens.MessageBody</ReturnType>
<Implementation><![CDATA[
	Set msg = ""
	Set id = $G(%request.Data("$ID1",1)), idp2=$P(id,":",2), id=$P(id,":")
	
	If (id = "Body") {
		// ID2 contains MessageBody ID
		Set msg = $zobjclassmethod($S(""'=idp2:idp2,1:"Ens.MessageBody"),"%OpenId",$G(%request.Data("$ID2",1)))
	}
	ElseIf (id = "Context") {
		// ID2 contains BP Context ID
		Set msg = ##class(Ens.BP.Context).%OpenId($G(%request.Data("$ID2",1)))
	}
	Else {
		Set hdr = ##class(Ens.MessageHeader).%OpenId(id,0)
		If $IsObject(hdr)&&(hdr.MessageBodyClassName'="") {
			Set msg = $zobjclassmethod(hdr.MessageBodyClassName,"%OpenId",hdr.MessageBodyId)
		} Else {
			Set msg = $$$NULLOREF
		}
	}
	
	Quit msg
]]></Implementation>
</Method>
</Class>
</Export>