<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="alREC.dReceipt.ReceivedState">
<ClassType>persistent</ClassType>
<IncludeCode>COMSYS</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>alSYS.State.dState</Super>

<Property name="StateId">
<Type>alSYS.dt.dtInteger</Type>
<InitialExpression>"2&quot;</InitialExpression>
<Required>1</Required>
</Property>


<Method name="IsValid">
<Description><![CDATA[
Must be overridden, returns whether the instance can be changed to this state
]]></Description>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	;---------------------------------------------------------------------------------
	; History
	; 26-Mar-2008	HQN		Validate ReceiptLines returning specific errors
	;---------------------------------------------------------------------------------
	set sc = $$$OK
	if %this.StateMachine = $$$NULLOREF {
		set sc = $system.Status.Error(5659, "StateMachine") ; Property '%1' required
	}
	if $$$ISOK(sc) {
		set sc = %this.StateMachine.%ValidateObject()
	}
	if $$$ISOK(sc) {
		if %this.StateMachine.ReceiptLines.Count() = 0 {
			set sc = $system.Status.Error(5001,"No ReceiptLines assigned")
		} else {
			set scChildren = $$$OK
			for loopLines=1:1:%this.StateMachine.ReceiptLines.Count() {
				set objReceiptLine = %this.StateMachine.ReceiptLines.GetAt(loopLines)
				set sc = objReceiptLine.States.GetAt(2).IsValid()
				if $$$ISERR(sc) {
					set:$$$ISERR(scChildren) scChildren = $system.Status.AppendStatus(scChildren, $system.Status.Error(5001,%this.%ClassName(1)_"::ReceiptLines["_loopLines_"]"))
					set:$$$ISOK(scChildren) scChildren  = $system.Status.Error(5001,%this.%ClassName(1)_"::ReceiptLines["_loopLines_"]")
				}
			}
			if $$$ISERR(scChildren) {
				set sc = scChildren
			}
		}
	}
	quit sc
]]></Implementation>
</Method>

<Method name="OnStateChange">
<Description><![CDATA[
Change state for all ReceiptLines
]]></Description>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	/*------------------------------------------------------------------------------
	; History: 
	; 01-Aug-2008	Luke	SR15814: Corrected sc formation and use of $$$Text
	; 02-May-2008	Luke	SR15699: Language codes for errors 
	; 18-Mar-2008	HQN		SR15640: Created
	;-----------------------------------------------------------------------------*/

	set sc = $$$OK
	if %this.StateMachine = $$$NULLOREF {
		set strMessage = $$$Text("alREC0004")      // SR15814:
		set sc =$$$ERROR($$$GeneralError,strMessage)
		//set sc= $$$MakeStatus("alREC0004") ;"Receipt hasn't been assigned to this state" //SR15699
	}
	; record needs %Id() set for line references to work, save before processing
	; it will rollback on failure
	do %this.StateMachine.%Save()
	for loopLines=1:1:%this.StateMachine.ReceiptLines.Count() {
		set objReceiptLine = %this.StateMachine.ReceiptLines.GetAt(loopLines)
		set sc = objReceiptLine.ChangeState(2)
		quit:$$$ISERR(sc)
	}
	
	quit sc
]]></Implementation>
</Method>
<Storage name="Default">
<Type>%Library.CacheStorage</Type>
</Storage>
</Class>
</Export>