<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.infra.util.Pagination">
<ClassType/>
<IncludeCode>COMSYS</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>%RegisteredObject</Super>

<Parameter name="StatementTypeSELECT">
<Type>%String</Type>
<Default>1</Default>
</Parameter>

<Parameter name="Page">
<Type>%String</Type>
<Default>^VAR.infra.util.Pagination(YBED,YFORM,"PAGE")</Default>
</Parameter>

<Parameter name="QueryParameters">
<Type>%String</Type>
<Default>^VAR.infra.util.Pagination(YBED,YFORM,"QUERY")</Default>
</Parameter>

<Property name="MaxRows">
<Type>%Integer</Type>
</Property>

<Method name="%OnNew">
<FormalSpec>maxRows:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	if (+$get(maxRows) = 0) quit $$$NO
	set ..MaxRows = +maxRows
	quit $$$OK]]></Implementation>
</Method>

<Method name="ExecuteQuery">
<FormalSpec>&amp;status:%Status,queryCount,query,p1,p2,p3,p4,p5,p6,p7,p8,p9,p10,p11,p12,p13,p14,p15,p16</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>PaginationResultSet</ReturnType>
<Implementation><![CDATA[	new rs, orderByPosition
	
	set status = $$$OK

	set rs = ##class(PaginationResultSet).%New(..MaxRows)

	set status = rs.Prepare(query)
	if ($$$ISERR(status)) quit $$$NULLOREF

	if (rs.GetStatementType() '= ..#StatementTypeSELECT) {
		set status = $$$ERROR("Query statement type is not valid.")
		quit $$$NULLOREF
	}
	if (queryCount = -1) {
		set orderByPosition = $find(##class(Strings).UpperCase(query),"ORDER BY")
		if (orderByPosition = 0) {
			set orderByPosition = $length(query)
		}
		else {
			set orderByPosition = orderByPosition - 9
		}
		set status = rs.Prepare("SELECT COUNT(*) As QTD FROM ("_$extract(query,0,orderByPosition)_")")
		if ($$$ISERR(status)) quit $$$NULLOREF

		set status = rs.Execute($get(p1), $get(p2), $get(p3), $get(p4), $get(p5), $get(p6), $get(p7), $get(p8), $get(p9), $get(p10), $get(p11), $get(p12), $get(p13), $get(p14), $get(p15), $get(p16))
		if ($$$ISERR(status)) quit $$$NULLOREF

		if (rs.Next(.status)) set rs.TotalRows = rs.Data("QTD")
		if ($$$ISERR(status)) quit $$$NULLOREF
		set status = rs.Close() 
		if ($$$ISERR(status)) quit $$$NULLOREF
		set status = rs.Prepare(query)
		if ($$$ISERR(status)) quit $$$NULLOREF
	}
	else {
		set rsCount = ##class(%ResultSet).%New()
    	set status = rsCount.Prepare(queryCount)
    	if ($$$ISERR(status)) quit $$$NULLOREF
    	set status = rsCount.Execute()
    	if ($$$ISERR(status)) quit $$$NULLOREF
    	if (rsCount.Next(.status)) set rs.TotalRows = rsCount.GetData(1)
    	if ($$$ISERR(status)) quit $$$NULLOREF
    	do rsCount.Close()
		
	}
	set status = rs.Execute($get(p1), $get(p2), $get(p3), $get(p4), $get(p5), $get(p6), $get(p7), $get(p8), $get(p9), $get(p10), $get(p11), $get(p12), $get(p13), $get(p14), $get(p15), $get(p16))
	if ($$$ISERR(status)) quit $$$NULLOREF
	
	set queryParameters = query_$get(p1)_$get(p2)_$get(p3)_$get(p4)_$get(p5)_$get(p6)_$get(p7)_$get(p8)_$get(p9)_$get(p10)_$get(p11)_$get(p12)_$get(p13)_$get(p14)_$get(p15)_$get(p16)
	if (queryParameters '= $get(@(..#QueryParameters))){
		set @(..#QueryParameters) = queryParameters
		set @(..#Page) = 1
	}
	set status = rs.AdvanceTo(@(..#Page))
	
	quit rs]]></Implementation>
</Method>

<Method name="PrintNavigationBar">
<FormalSpec>rs:PaginationResultSet,navigationMethod:%String="REFRESH"</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<Implementation><![CDATA[	$$$VAR
	new style, navigation, onClickFirst, onClickPrevious, onClickNext, onClickLast, titleFirst, titlePrevious, titleNext, titleLast
	
	set navigation = ""
	if (navigationMethod = "REFRESH") {
		// do nothing
	}
	elseif (navigationMethod = "SAVE") {
		set navigation = "SAVENOW();"
	}
	
	if (rs.TotalRows = 0){
		set style = "style=""cursor:default; opacity:0.40; filter:alpha(opacity=40); /* For IE8 and earlier */"""
		set inputDisabled = "disabled"
		set onClickFirst = ""
		set onClickPrevious = ""
		set onClickNext = ""
		set onClickLast = ""
		set titleFirst = ""
		set titlePrevious = ""
		set titleNext = ""
		set titleLast = ""
	}
	else {
		set style = "style=""cursor:pointer;"""
		set inputDisabled = ""
		set onClickFirst = "Javascript:CallBackNow('##class(VAR.infra.util.Pagination).GoToPage','1~"_navigationMethod_"');"_navigation
		set onClickPrevious = "Javascript:CallBackNow('##class(VAR.infra.util.Pagination).GoToPage','"_(rs.CurrPage-1)_"~"_navigationMethod_"');"_navigation
		set onClickNext = "Javascript:CallBackNow('##class(VAR.infra.util.Pagination).GoToPage','"_(rs.CurrPage+1)_"~"_navigationMethod_"');"_navigation
		set onClickLast = "Javascript:CallBackNow('##class(VAR.infra.util.Pagination).GoToPage','"_rs.TotalPages_"~"_navigationMethod_"');"_navigation
		set titleFirst = $$^WWWTEXT("VARPageFirst")
		set titlePrevious = $$^WWWTEXT("VARPagePrevious")
		set titleNext = $$^WWWTEXT("VARPageNext")
		set titleLast = $$^WWWTEXT("VARPageLast")
	}
	
	if ($IsObject(rs)) {
		&html<
			<div style="width: 100%;">
				<img onClick="#(onClickFirst)#" #(style)# align="absbottom" border="0" marging="0" title="#(titleFirst)#" src="#(YGIF)#rrev.gif">
				<img onClick="#(onClickPrevious)#" #(style)# align="absbottom" border="0" title="#(titlePrevious)#" src="#(YGIF)#rev.gif">
				#($$^WWWTEXT("VARPage"))# <input type="text" #(inputDisabled)# onBlur="Javascript:CallBackNow('##class(VAR.infra.util.Pagination).GoToPage',this.value+'~#(navigationMethod)#');#(navigation)#"  size="1" value="#(rs.CurrPage)#" style="text-align:center"> #($$^WWWTEXT("VARPageOf"))# #(rs.TotalPages)#
				<img onClick="#(onClickNext)#" #(style)# align="absbottom" border="0" title="#(titleNext)#" src="#(YGIF)#for.gif">
				<img onClick="#(onClickLast)#" #(style)# align="absbottom" border="0" title="#(titleLast)#" src="#(YGIF)#ffor.gif">
				<br />
			</div>
		>
	}]]></Implementation>
</Method>

<Method name="GoToPage">
<ClassMethod>1</ClassMethod>
<FormalSpec>page:%Integer,navigationMethod:%String="REFRESH"</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<Implementation><![CDATA[	$$$VAR
	
	set @(..#Page) = page
	
	if (navigationMethod = "REFRESH") {
		do RefreshForm^COMUtilForm()
	}
	elseif (navigationMethod = "SAVE") {
		// do nothing
	}]]></Implementation>
</Method>
</Class>
</Export>