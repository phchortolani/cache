<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.CSP.VARPacienteCID">
<ClassType/>
<IncludeCode>MEDConst,VARConst,INConst,WWWConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>

<Parameter name="CONTENTTYPE">
<Default>application/json</Default>
</Parameter>

<Parameter name="CHARSET">
<Default>utf-8</Default>
</Parameter>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	$$$VAR
	Set method = $get(%request.Data("method",1))
	Xecute "do .."_method_"()"
	Return $$$OK]]></Implementation>
</Method>

<Method name="ListarRelatorio">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	#Dim json As %Library.DynamicArray
	
	Set YBED 				= $Get(%request.Data("YBED",1))
	Set pObjFiltros = $Get(%request.Data("pObjFiltros",1))
	
	Set sql = "SELECT "
					_		"DISTINCT(A.%ID),"
					_		"A.AdmissionID AS Admissao,"
					_		"PA.PatientID AS Cod_Paciente,"
					_		"PA.Name AS Paciente,"
	;				_		"CAST(PA.DOB AS DATE) Data_Nascimento,"
 					_		"PA.DOB Data_Nascimento,"
					_		"$$SQLGetSexoPaciente^VARSQL(PA.PatientID) AS Sexo,"
					_		"YEAR(A.DateAdmitted) As Ano_Admissao,"
					_		"MONTH(A.DateAdmitted) As Mes_Admissao,"
					_		"DAY(A.DateAdmitted) AS Dia_Admissao,"
	;				_		"CAST(A.DateAdmitted AS DATE) Data_Admissao,"
 					_		"A.DateAdmitted Data_Admissao,"
					_		"A.TimeAdmitted AS Hora_Admissao,"
	;				_		"CAST(A.DateDischarged AS DATE) Data_Alta,"
 					_		"A.DateDischarged Data_Alta,"
					_		"A.TimeDischarged AS Hora_Alta,"
					_		"A.TipoConsulta,"
					_		"CASE "
					_			"WHEN A.TipoConsulta = 'QUIMIOTERAPIA' THEN QUI.Diagnostico "
					_			"WHEN A.TipoConsulta = 'MIELOGRAMA' THEN MIE.HipoteseDiagnostica "
					_			"WHEN A.TipoConsulta = 'TRANSFUSAOSANGUE' THEN TRA.Diagnostico "
					_			"WHEN A.TipoConsulta = 'SANGRIATERAPEUTICA' THEN SAG.Diagnostico "
					_			"WHEN A.TipoConsulta = 'APLICACAOMEDICAMENTO' THEN APLI.Diagnostico "
					_			"WHEN A.TipoConsulta = 'PERMEABILIZACAOCATETER' THEN PER.Diagnostico "
					_			"ELSE PEP.AnamneseHD "
					_		"END Codigo_CID,"
					_		"CASE "
					_			"WHEN A.TipoConsulta = 'QUIMIOTERAPIA' THEN $$GetDescDiagnostico^VARSQL(QUI.Diagnostico) "
					_			"WHEN A.TipoConsulta = 'MIELOGRAMA' THEN $$GetDescDiagnostico^VARSQL(MIE.HipoteseDiagnostica) "
					_			"WHEN A.TipoConsulta = 'TRANSFUSAOSANGUE' THEN $$GetDescDiagnostico^VARSQL(TRA.Diagnostico) "
					_			"WHEN A.TipoConsulta = 'SANGRIATERAPEUTICA' THEN $$GetDescDiagnostico^VARSQL(SAG.Diagnostico) "
					_			"WHEN A.TipoConsulta = 'APLICACAOMEDICAMENTO' THEN $$GetDescDiagnostico^VARSQL(APLI.Diagnostico) "
					_			"WHEN A.TipoConsulta = 'PERMEABILIZACAOCATETER' THEN $$GetDescDiagnostico^VARSQL(PER.Diagnostico) "
					_			"ELSE $$GetDescDiagnostico^VARSQL(PEP.AnamneseHD)"
					_		"END Diagnostico,"
					_		"$$SQLGetUserParamDescVAR^VARSQL('STATUSADMISSAO~' || A.Status) as Status,"
					_		"P.Name AS Profissional "
					_ "FROM "
					_		"SQLUser.MEDAdmission A "
					_		"LEFT JOIN SQLUser.VARPEP PEP ON PEP.Admissao = A.AdmissionID "
					_		"LEFT JOIN SQLUser.VARCID CID ON CID.CodigodoCID = PEP.AnamneseHD "
					_		"LEFT JOIN SQLUser.MEDConvenio C ON C.Codigo = A.Convenio "
					_		"LEFT JOIN SQLUser.MEDProvider P ON P.ID1 = A.Profissional "
					_		"LEFT JOIN SQLUser.MEDPatient PA ON PA.PatientID = A.Patient "
					_		"LEFT JOIN SQLUser.VARPEPReceituario MED ON MED.NumerodoPEP = A.AdmissionID "
					_		"LEFT JOIN SQLUser.VARPEPExamesLista EX ON EX.NumerodoPEP = A.AdmissionID "
					_		"LEFT JOIN SQLUser.INART I ON I.ItemNumber = EX.CodigodoExame "
					_		"LEFT JOIN SQLUser.VARProntuarioEletronico PR ON A.AdmissionID = PR.NumeroAdmisao "
					_		"LEFT JOIN SQLUser.VARPEPSangriaTerapeutica SAG ON PR.Codigo = SAG.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.VARPEPMielograma MIE ON PR.Codigo = MIE.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.VARPEPTransfuncional TRA ON PR.Codigo = TRA.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.VARPEPQuimioterapia QUI ON PR.Codigo = QUI.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.VARPEPAplicacaoMedicamento APLI ON PR.Codigo = APLI.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.VARPEPPermeabilizacaoCateter PER ON PR.Codigo = PER.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.VARPEPReceituarioEnfermagem REC ON PR.Codigo = REC.CodigoProntuarioEletronic "
					_		"LEFT JOIN SQLUser.WWW0121 LOC ON A.Location = LOC.BillToLoc "
					_ "WHERE "
					_		"A.Company = 0 "
					
	//
	// FILTROS
	//
	Set objFiltros = [].%FromJSON(pObjFiltros)
	
	
	// Relação Tipo de Consulta com sua TABELA CID
	// utilizado para caso algum CID foi definido na pesquisa
	Set cidFilters = [
		"QUI.Diagnostico",					// QUIMIOTERAPIA
		"MIE.HipoteseDiagnostica",	// MIELOGRAMA
		"TRA.Diagnostico",					// TRANSFUSAOSANGUE
		"SAG.Diagnostico",					// SANGRIATERAPEUTICA
		"APLI.Diagnostico",					// APLICACAOMEDICAMENTO
		"PER.Diagnostico",					// PERMEABILIZACAOCATETER
		"PEP.AnamneseHD"						// CONSULTA | CONSULTACOMPLEMENTAR | TELEMEDICINA | PRIMCONSULTA | RETORNO | VISITA														
	]
	
	// Profissional
	if objFiltros.profissional '= "" Set sql = sql_ "AND P.LicenseNumber = (SELECT TOP 1 LicenseNumber FROM SQLUser.MEDProvider WHERE Company = 0 AND ID1 = "_+objFiltros.profissional_") "
	
	// Tipo Consulta
	if (objFiltros.tipoConsulta '= "" && objFiltros.tipoConsulta.%Size() > 0)
	{
		// define apenas os tipos selecionados para aplicar a tabela exata do CID
		Set selectCidFilters = []
		Set flagCidFilter = 0	// caso seja PEP.AnamneseHD define apenas 1 vez
		
		Set sql = sql _ "AND A.TipoConsulta IN ("
		For i=0:1:(objFiltros.tipoConsulta.%Size()-1)
		{
			Set tipoConsulta = objFiltros.tipoConsulta.%Get(i)
			Set sql = sql _ $Select(i=0:"",1:",") _ "'"_tipoConsulta_"'"
			
			if tipoConsulta="QUIMIOTERAPIA" { 							Do selectCidFilters.%Push("QUI.Diagnostico")}
			elseif tipoConsulta="MIELOGRAMA" { 							Do selectCidFilters.%Push("MIE.HipoteseDiagnostica")}
			elseif tipoConsulta="TRANSFUSAOSANGUE" { 				Do selectCidFilters.%Push("TRA.Diagnostico")}
			elseif tipoConsulta="SANGRIATERAPEUTICA" { 			Do selectCidFilters.%Push("SAG.Diagnostico")}
			elseif tipoConsulta="APLICACAOMEDICAMENTO" { 		Do selectCidFilters.%Push("APLI.Diagnostico")}
			elseif tipoConsulta="PERMEABILIZACAOCATETER" { 	Do selectCidFilters.%Push("PER.Diagnostico")}
			else
			{
				if 'flagCidFilter
				{
					Set flagCidFilter = 1
					Do selectCidFilters.%Push("PEP.AnamneseHD")
				}
			}
		}
		Set sql = sql _ ") "
		
		// define o cidFilters com apenas os que foram selecionados
		Set cidFilters = selectCidFilters
	}
	
	// CID
	If (objFiltros.cid '= "" && objFiltros.cid.%Size() > 0)
	{
		Set cid  = objFiltros.cid
		Set lb   = $li($lb(),2,*)	// cria ListBuild vazio
		
		// Itera pelos tipoConsulta selecinados ou todos caso não tenha sido definido algum
		For y=0:1:(cidFilters.%Size()-1)
		{
			Set tabela = cidFilters.%Get(y)	// pega tabela Ex.: "QUI.Diagnostico"
			For i=0:1:(cid.%Size()-1)
			{
				// Exp: ';'||PEP.AnamneseHD||';' LILE '%;A00.0;%'
				Set $li(lb,*+1) = "';'||"_tabela_"||';' LIKE '%;"_cid.%Get(i)_";%'"
			}
		}
		Set sql = sql _ "AND ("_$lts(lb," OR ", 1)_") "
	}
	
	// Datas Inicial/Final
	if (objFiltros.dataInicial '= "")
	{
		Set dataInicial = $ZDateH(objFiltros.dataInicial,3)
		if (objFiltros.dataFinal = "") Set dataFinal = dataInicial
		else  Set dataFinal = $ZDateH(objFiltros.dataFinal,3)
		
		Set sql = sql _ "AND A.DateAdmitted BETWEEN '"_dataInicial_"' AND '"_dataFinal_"' "
	}
	
	Set rSet = ##class(%SQL.Statement).%ExecDirect("", sql)
	Set json = []
	While rSet.%Next()
	{
		Set row = {}
		Set row.cod 					= rSet.ID
		Set row.admissao			= rSet.Admissao
		Set row.paciente			= rSet.Paciente_" ("_rSet.%Get("Cod_Paciente")_")"
		Set row.dtNasc				= $ZDate(rSet.%Get("Data_Nascimento"),4)
		Set row.sexo					= $Select($match(rSet.Sexo,"^M.*"):"M",1:"F")
		Set row.dtAdmissao		= $ZDate(rSet.%Get("Data_Admissao"),4)
		Set row.horaAdmissao	= $ZTime(rSet.%Get("Hora_Admissao"),2)
		Set row.dtAlta				= $ZDate(rSet.%Get("Data_Alta"),4)
		Set row.horaAlta			= $ZTime(rSet.%Get("Hora_Alta"),2)
		Set row.tipoConsulta	= rSet.TipoConsulta
		Set row.codCID				= rSet.%Get("Codigo_CID")
		Set row.diagnostico		= rSet.Diagnostico
		Set row.status				= rSet.Status
		Set row.profissional	= rSet.Profissional
		
		Do json.%Push(row)
	}
	
	Do json.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>
</Class>
</Export>