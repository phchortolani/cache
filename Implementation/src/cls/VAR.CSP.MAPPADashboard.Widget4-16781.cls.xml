<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Class name="VAR.CSP.MAPPADashboard.Widget4">
<ClassType/>
<IncludeCode>VARConst,WWWConst,INConst</IncludeCode>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	$$$VAR
	Set job = %session.SessionId 
	Quit:$$$ISERR(..validaSessao()) $$$OK
	Set method = $Get(%request.Data("method",1))
	Xecute "do .."_method_"()"
	Quit $$$OK]]></Implementation>
</Method>

<Method name="validaSessao">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	Set status = $$VerifySession^VARCSPUtils(YBED, YUSER)
	If status=0||(YBED="UNKNOWN"){
		Set status=0
		Set resp={}
		Set resp.erro=1
		Set resp.mensagem="SessÃ£o Expirou"
		Write resp.%ToJSON()
	}
	Quit status]]></Implementation>
</Method>

<Method name="getDados">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	;
	$$$VAR
	Set YM 	= 0
	Set Y 	= "~"
	;
	Set codCID = $Get(%request.Data("codCID",1))
	;
	Set arrayRetorno = []
	Kill temp
	Kill tempPaciente
	Set todosTrat = ""
	;
	Set meuSQL = "Select * From SQLUser.VARPEPPremiumTratamento "
	Set meuSQL = meuSQL _ "Where Company= 0 And CID = '"_codCID_"' "
	Set meuSQL = meuSQL _ "Group By CodigodoPaciente, CodigodoTratamento "
	Set meuSQL = meuSQL _ "Order By CodigodoPaciente, DatadoIniciodoCiclo "
	Set rSet 		= ##class(%SQL.Statement).%ExecDirect("",meuSQL)
	While(rSet.%Next()){
		If (rSet.%Get("CodigodoPaciente") = "") Continue
		;If (rSet.%Get("DataHoradaCriacao") = "") Continue
		Set data = rSet.%Get("DataHoradaCriacao")
		If (data = "") {
			Set data = rSet.%Get("DatadoIniciodoCiclo")
		}
		If (data = "") Continue
		If ($$GetDescRegimeTerapeutico^VARSQL(codCID, rSet.%Get("CodigodoTratamento")) = "") Continue
		Set $Piece(temp(rSet.%Get("CodigodoTratamento")),"~",1) = $Piece($Get(temp(rSet.%Get("CodigodoTratamento"))),"~",1) + 1
		Set tempPaciente(rSet.%Get("CodigodoPaciente"), data) = rSet.%Get("DatadoFinaldoCiclo")
	}
	;
	Kill temp1
	Set codTrat = ""
	For {
		Set codTrat = $Order(temp(codTrat),-1)
		Quit:codTrat=""
		;
		Set qtde = $Piece(temp(codTrat),"~",1)
		Set temp1(qtde,codTrat) = codTrat
	}
	;	
	Set qtde = ""
	For {
		Set qtde = $Order(temp1(qtde),-1)
		Quit:qtde=""
		Set desc = ""
		For {
			Set desc = $Order(temp1(qtde, desc))
			Quit:desc=""
			Set listRetorno = {}
			Set listRetorno.name = desc_"-"_$$GetDescRegimeTerapeutico^VARSQL(codCID, desc)
			Set listRetorno.codTrat = temp1(qtde, desc)
			Set listRetorno.y = qtde
			Do arrayRetorno.%Push(listRetorno)
		}	
	}
	;
	Do arrayRetorno.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>

<Method name="getDetalhe">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	;
	$$$VAR
	Set YM 	= 0
	Set Y 	= "~"
	;
	Set codCID 	= $Get(%request.Data("codCID",1))
	Set codTrat = $Get(%request.Data("codTrat",1))
	Kill tempToxicidade
	;
	Set meuSQL = "Select CodigodoPaciente, NumerodaFicha From SQLUser.VARPEPPremiumTratamento "
	Set meuSQL = meuSQL _ "Where Company= 0 And CID = '"_codCID_"' "
	Set meuSQL = meuSQL _ "And CodigodoTratamento = '"_codTrat_"' "
	Set meuSQL = meuSQL _ "Group By CodigodoPaciente "
	Set rSet 		= ##class(%SQL.Statement).%ExecDirect("",meuSQL)
	While(rSet.%Next()){
		Set codPaciente = rSet.%Get("CodigodoPaciente")
		Set numFicha 		= rSet.%Get("NumerodaFicha")
		;
		For codCampo = 1328,1329,1330,1331,1332,1333,1334,1335,1336,1361,1337 { 
			If $Data(tempToxicidade(codCampo)) Continue
			Set valor = $Piece(##Class(VAR.CSP.MAPPA).recValorSalvo(codPaciente,codCID,numFicha,1,codCampo),"~",1)
			If (codCampo = 1329 || (codCampo = 1333) || (codCampo = 1337)) {
				If (valor = "1") {
					Set tempToxicidade(codCampo) = ""
				}
			}Else{
				If ((";"_valor_";")[(";1;")) {
					Set tempToxicidade(codCampo) = ""
				}		
			}
		}
		;
		Set temp(codPaciente) = numFicha
	}
	Set (qtdeMMTProgDoen, qtdeMMTToxid, qtdeMMTOutr) = 0
	Set (qtdeTotal) = 0
	Set codPaciente = ""
	For {
		Set codPaciente = $Order(temp(codPaciente))
		Quit:codPaciente=""
		;
		Set numFicha = $Get(temp(codPaciente))
		Set (dtInicial, dtFinal) = ""
		&Sql(
			Select Min(DatadoIniciodoCiclo), Max(DatadoFinaldoCiclo) 
			Into :dtInicial, :dtFinal
			From SQLUser.VARPEPPremiumTratamento
			Where Company = 0  And CID = :codCID And CodigodoTratamento = :codTrat
			And CodigodoPaciente = :codPaciente And NumerodaFicha = :numFicha
			And IndicadordeCicloConcluido = 1
		)
		If (dtInicial="") Continue
		Set qtdeTotal = qtdeTotal + 1
		If (dtFinal="") Set dtFinal = +$Horolog
		;
		Set qtdeDias = dtFinal - dtInicial
		Set tempDias(qtdeDias, qtdeTotal) = ""
		;
		Set codCampo = 1312
		Set valor = $Piece(##Class(VAR.CSP.MAPPA).recValorSalvo(codPaciente,codCID,numFicha,1,codCampo),"~",1)
		If (valor = "1") Set qtdeMMTProgDoen = qtdeMMTProgDoen + 1
		;
		Set codCampo = 1313
		Set valor = $Piece(##Class(VAR.CSP.MAPPA).recValorSalvo(codPaciente,codCID,numFicha,1,codCampo),"~",1)
		If (valor = "1") Set qtdeMMTToxid = qtdeMMTToxid + 1
		;
		Set codCampo = 1314
		Set valor = $Piece(##Class(VAR.CSP.MAPPA).recValorSalvo(codPaciente,codCID,numFicha,1,codCampo),"~",1)
		If (valor = "1") Set qtdeMMTOutr = qtdeMMTOutr + 1
		;
	}
	;
	Set (qtdeDias, tQtdeDias, tMin, tMax, Mediana) = ""
	For {
		Set qtdeDias = $Order(tempDias(qtdeDias))
		Quit:qtdeDias=""
		If (tMin = "") Set tMin = qtdeDias
		Set tMax = qtdeDias
		Set count=""
		For {
			Set count = $Order(tempDias(qtdeDias,count))
			Quit:count=""
			Set tQtdeDias=tQtdeDias_$Select(tQtdeDias="":"",1:";")_qtdeDias
		}
	}
	If (qtdeTotal#2=0) {
		Set Mediana = $FNumber(($Piece(tQtdeDias,";",(qtdeTotal\2))+$Piece(tQtdeDias,";",((qtdeTotal\2)+1)))/2,"",0)
	}Else{
		Set Mediana = $Piece(tQtdeDias,";",((qtdeTotal\2)+1))
	}
	;
	Set tToxicidade = ""
	Set campo = ""
	For {
		Set campo = $Order(tempToxicidade(campo))	
		Quit:campo=""
		Set tToxicidade = tToxicidade _$Select(tToxicidade="":"",1:" | ")_##Class(VAR.CSP.MAPPA).recDescricaoWorkFlow(codCID, campo)
	}
	;
	Set listRetorno = {}
	Set listRetorno.tempMinimo = +tMin
	Set listRetorno.tempMaximo = +tMax
	Set listRetorno.mediana = Mediana
	Set listRetorno.perProg = $Select(qtdeTotal=0:0,1:$FNumber(qtdeMMTProgDoen*100/qtdeTotal,".",1))
	Set listRetorno.perTox = $Select(qtdeTotal=0:0,1:$FNumber(qtdeMMTToxid*100/qtdeTotal,".",1))
	Set listRetorno.perOut = $Select(qtdeTotal=0:0,1:$FNumber(qtdeMMTOutr*100/qtdeTotal,".",1))
	Set listRetorno.toxicidade = tToxicidade
	Do listRetorno.%ToJSON()
	Return $$$OK]]></Implementation>
</Method>
</Class>
</Export>