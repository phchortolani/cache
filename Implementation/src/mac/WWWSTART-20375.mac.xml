<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSTART" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSTART(YTITLE,YEXIT,YREFR,YNOOPEN,HTA)
	
#include COMSYS
	
#define jsMarker(%1)
#;define jsMarker(%1) 	write YCR,YCR,"<!-- ************************* ",%1," (WWWSTART)************************* -->",YCR,YCR
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		STARTEN HTML
	;
	; Inputs : 
	;	YTITLE = UEBERSCHRIFT
	;	YEXIT  = VORZEITIGES ENDE DES HEADERS (ZB WENN JAVASCRIPT) ;termination when 
	;	YREFR= ANZAHL DER REFRESHSEKUNDEN ;Number the 
	;	YNOOPEN=1 = NICHT DIE LEITUNG ÖFFNEN (Z.B. WEIL DIE HTMLSEITE AUF PLATTE
	;	            GESPEICHERNT WIRD UND NICHT HTTP) ;Not who conduction unclose since who upon And Not 
	;	HTA=1 = im Aplicationsmodus starten
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 29-Oct-2010	shobby	SR17596: If the form is WWWPARA then there could be a
	;							delay before the screen is ready to draw, which
	;							means the blendTrans will cause a partial (untidy)
	;							draw of the screen.  Disable in this case. 
	; 03-May-2010	shobby	SR17253: Remove annoying screen flashes when loading pages.
	; 23-Mar-2009	shobby	SR16427: Add some additional javascript references so
	; 							that calls can be made back to cache from popup menu
	; 							forms.
	; 07-May-2007	GRF		SR15511: Doco; quits; expand commands
	; 19-Dec-2006	JW		BR014262: Menu type 6 has been removed. Encapsulate Title
	; 22-Dec-2005	JW		SR13195: Add link class
	; 02-Dec-2005	RPW		SR13940: If the user can customize then allow the drag
	; 							and drop actions.
	; 25.04.2005	FIS		SR12200
	; 26.11.2003	Bec		24708
	; 15.04.2003	FIS		23391: DROP-DOWN MENU STATT BUTTONLEISTE
	; 03.06.1998	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	NEW YSTYLE,YDROPD,blnCanDrag
	
	; FIXME : Get ^WWW012(0,0,1) and ^WWW120(0,YFORM,1) records once and reuse obj... variables.
	
	IF '$DATA(YTITLE) SET YTITLE = ""
	IF '$DATA(YREFR)  SET YREFR  = ""
	
	; D76		$$$WWW012CharacterSet()
	SET YISO = $PIECE($GET(^WWW012(0,0,1)),Y,76)                  ;zeichensatz suchen ;seek 
	IF YISO'="" IF SPRACHE'="" SET YISO=$PIECE($GET(^WWW100(0,"CHARSET",SPRACHE,YISO,1)),Y,1) ; FIXME : CHARSET settings are common for all languages - should default still be active? <GRF>
	;IF YISO="" SET YISO="iso-8859-1"
	
	; D66		$$$WWW012DisplayButtonOnBottomLine	  0="Buttons Above", 3="Selection"
	SET YDROPD = $$$NO   ;EINSCHALTEN FILE LOAD FÜR MENU.HTC (SCRIPT FÜR POPUP-MENU IN WWWFORMC3) ;switch in to to within 
	IF $GET(YQUERY)'="" IF $GET(%(YQUERY,"EP"))'="WWWMENU" IF $PIECE($GET(^WWW012(0,0,1)),Y,66)=3 SET YDROPD=$$$YES  ;DROP-DOWN MENU STATT BUTTONLEISTE;TYBD;NUR WENN KEIN MENUE
	
	IF $GET(YNOOPEN)'=1 DO OPEN   ;start html
	IF YUSER'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,5)=1,$PIECE(^WWWUSER(0,YUSER,1),Y,6)=0  ;merker start und ende ;and 
	
	; vvvvv   TO DO : Implement in some form once standard achieved
	;write "<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.0//EN"">"
	;write "<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01 Transitional//EN"" ""http://www.w3.org/TR/html4/loose.dtd"">"
	;write "<!DOCTYPE html PUBLIC ""-//W3C//DTD XHTML 1.0 Strict//EN"" ""http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd"">"
	;write "<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01 Frameset//EN"" ""http://www.w3.org/TR/html4/frameset.dtd"">"
	; ^^^^^
	
	WRITE "<HTML"
	IF YDROPD=$$$YES WRITE " XMLNS:myMenu"
	WRITE ">"
	IF YDROPD=$$$YES WRITE "<?IMPORT namespace=""myMenu"" implementation="""_YGIF_"formc3.htc"" />"
	
	$$$jsMarker("Header")
	
	WRITE "<HEAD>"
	if $$SR16427^WWWFORMJavascript() do Event^WWWMENU5()
	do CanCustomize^WWWHELP($GET(YBEDBER),.blnCanDrag)    ; 1 = System Administrator 2 = DB Administrator 
	
	$$$StartScript()
	WRITE "var blnCanDrag="_$select(blnCanDrag:"true",1:"false")_";"
	$$$EndScript()
	
	IF +YREFR'=0 DO
	. WRITE "<META HTTP-EQUIV=""refresh"" content="""_YREFR_""">",!
	
	IF YISO'="" DO   ;CHARACTER
	. WRITE "<META HTTP-EQUIV=""Content-Type"" content=""text/html; charset="_YISO_""">",!
	. ;SR17596 if ($get(YUSERAGENT)="MSIE") do  ;SR17596
	. if ($get(YUSERAGENT)="MSIE") && ($get(YFORM)'="WWWPARA") do     ;SR17596
	. . WRITE "<META http-equiv=""Page-Enter"" content=""blendTrans(Duration=0)"">"  ;SR17253
	. . WRITE "<META http-equiv=""Page-Exit"" content=""blendTrans(Duration=0)""> "  ;SR17253

	
	; STYLE SHEET
	;---------------------------------------
	;	D83		$$$WWW012StylesheetCSSDataFile()
	;	D83		$$$WWW120StylesheetCSSFile()
	;---------------------------------------
	SET YSTYLE = $$$NO  ; no stylesheet
	
	DO
	. NEW STYLE
	. SET STYLE = $PIECE($GET(^WWW012(0,0,1)),Y,83)
	. IF $GET(YFORM)'="" IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,83)'="" SET STYLE=$PIECE($GET(^WWW120(0,YFORM,1)),Y,83)
	. IF STYLE'="" IF $EXTRACT(STYLE,1,2)="$$" DO  QUIT
	. . SET YSTYLE = $$$YES  ;STYLEANWEISUNG
	. . NEW EXEC
	. . SET EXEC = "WRITE "_STYLE
	. . XECUTE EXEC
	. ;
	. IF STYLE'="" DO
	. . SET YSTYLE=$$$YES  ;STYLEANWEISUNG
	. . IF $GET(YFORM)'="" IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,83)'="" DO  QUIT   ;FORMULAREIGEN
	. . . WRITE " <LINK REL=""stylesheet"" TYPE=""text/css"" HREF="""_YGIF_$PIECE($GET(^WWW120(0,YFORM,1)),Y,83)_""">"
	. . ;
	. . WRITE " <LINK REL=""stylesheet"" TYPE=""text/css"" HREF="""_YGIF_$PIECE($GET(^WWW012(0,0,1)),Y,83)_""">"
	
	DO  ;STYLE
	. IF YSTYLE=$$$YES DO  QUIT             ;ANDERE STYLES 
	. . WRITE "<STYLE type='text/css'></STYLE>"
	. ;
	. WRITE "<STYLE type='text/css'>",!
	. WRITE "<!--"
	. ;W "@media all {IE\:HOMEPAGE {behavior:url(#default#homepage)} }"
	. NEW COLOR
	. DO                               ;SCHRIFT ;typeface 
	. . NEW SCHRIFT  ;,COLOR
	. . SET SCHRIFT = $PIECE($GET(^WWW012(0,0,1)),Y,9)
	. . IF SCHRIFT'="" SET SCHRIFT = $PIECE($GET(^WWW100(0,"SCHRIFTART",SPRACHE,SCHRIFT,1)),Y,1)
	. . IF SCHRIFT=""  SET SCHRIFT = "SansSerif"
	. . SET COLOR = $PIECE($GET(^WWW012(0,0,1)),Y,6)
	. . IF COLOR'="" SET COLOR = $PIECE($GET(^WWW100(0,"FARBE",SPRACHE,COLOR,1)),Y,1)
	. . WRITE " body, td, th, legend {font-family:'"_SCHRIFT_"','SansSerif';"
	. . IF COLOR'="" WRITE " color:"_COLOR_";"
	. . WRITE "}",!
	. ;
	. IF $GET(YLINKCOL)=""  WRITE " a:hover{color:blue; text-decoration:underline}",!
	. IF $GET(YLINKCOL)'="" WRITE " a:hover{color:"_YBLUE_"; text-decoration:underline}",!
	. WRITE " a {text-decoration:none;"
	. IF COLOR'="" WRITE " color:"_COLOR_";"
	. WRITE "}",!
	. ;
	. IF $GET(YLINKCOL)=""  WRITE " a:active {color:blue"
	. IF $GET(YLINKCOL)'="" WRITE " a:active {color:"_YBLUE
	. IF $GET(YFORM)=""     WRITE "; font-weight:bold"
	. WRITE "}",!
	. IF $GET(YFORM)'="" IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,78)=1 DO   ;ORIENTIERUNG
	. . WRITE " @page { size:landscape; }"
	. ;
	. WRITE " THEAD {display:table-header-group}",!
	. ; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;do
	. . WRITE " #header {        "
	. . WRITE "   position: fixed;"
	. . WRITE "   width: 100%;"
	. . WRITE "   height: 60px;"
	. . WRITE "   top: 0;"
	. . WRITE "   right: 0;"
	. . WRITE "   bottom: auto;"
	. . WRITE "   left: 0;"
	. . WRITE " }"
	. . ;
	. . WRITE " #content {       "
	. . WRITE "   overflow: auto;"
	. . WRITE "   width: 100%;"
	. . WRITE "   height: auto;"
	. . WRITE "   top: auto;"
	. . WRITE "   right: auto;"
	. . WRITE "   bottom: auto;"
	. . WRITE "   left: 0;"
	. . WRITE " }"
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	. ;
	. write " .link { cursor:pointer; }",!	;SR17253
	. ;
	. WRITE "-->",!
	. WRITE "</STYLE>",YCR
	
	do Title(YTITLE)
	
	IF $DATA(YEXIT) QUIT
	
	IF $GET(HTA)=1 write "<HTA:APPLICATION ID=""WWW""  APPLICATIONNAME=""WWW"" BORDER=""none"" CAPTION=""no"" SHOWINTASKBAR=""no""  SINGLEINSTANCE=""yes"" SYSMENU=""no"" WINDOWSTATE=""maximize"">"
	
	WRITE "</HEAD>"
	QUIT
	
	
Title(pstrTitle)
	;-------------------------------------------------------------------------------
	; Add the title tag
	;
	; Params:	pstrTitle - title description
	;
	; Returns:
	;
	; History:
	; 20-Dec-2006	JW		BR014262: Created (Encapsulated).
	;-------------------------------------------------------------------------------
	write "<TITLE>"
	
	write $$^WWWUML(pstrTitle)   ;Heading
	if $get(YBED)'="" { 
		write " - "
		write $piece($get(^WWW013(0,YBED,1)),Y,1)
		write " - "
		write $$^WWWDATE($horolog)
		write " -  ["_$translate($get(YUCI),"_"," ")_"/"_$get(YM)
		write "/"_$get(SPRACHE)_"]"
	}
	write "</TITLE>"
	
	quit
 
	
OPEN
	;-------------------------------------------------------------------------------
	;	USE PIPE 
	; call as: DO OPEN^WWWSTART
	;-------------------------------------------------------------------------------
	IF YQUERY="%KEY" IF $GET(%ZCS("USE_PORT"))'="" XECUTE %ZCS("USE_PORT")
	;IF YQUERY'="%KEY" USE 56:(:1)
	QUIT
	
	
CLOSE
	;-------------------------------------------------------------------------------
	;	CLOSE PIPE
	; call as: DO CLOSE^WWWSTART
	;-------------------------------------------------------------------------------
	IF YQUERY="%KEY" IF $GET(%ZCS("FLUSH"))'="" XECUTE %ZCS("FLUSH")
	;IF YQUERY'="%KEY" USE 56:(::1)
	QUIT
	
	
]]></Routine>
</Export>