<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSAVE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSAVE
	
#include COMSYS
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SPEICHERN FORMULAR
	; 
	; JW - Note: A manual form does not run OnBeforeSave code.
	; 
	; Called by : User.www.cls based on entry point in form.
	;-------------------------------------------------------------------------------
	
	; -----  Entry point drops through to START  -----
	
START
	;-------------------------------------------------------------------------------
	; History :
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 04-Nov-2009	shobby	SR16935: Restrictions on 'Demo' User are now determined by
	; 							whether 'User Acess' as 33-Demo User.  Rather than
	; 							being hardcoded to the user DEMO.  
	; 06-Nov-2008	shobby	SR16123: Standardised routine to determine InputType
	; 16-Oct-2008	FIS		SR15947: UNDEF problem corrected; Re-done changes for
	; 							readabilty as $ztrap must be in same line; Turned
	; 							logic around to wrap Save and AfterSave as one
	; 							unit by default
	; 16-Oct-2008	GRF		SR15947: $get added around strTStatus since getting
	; 							UNDEFINED error (Not setting when form switch is
	; 							off); break up multi-set for readability.
    ; 02-Oct-2008	FIS		SR15947: Use DoNotPrepareHTMLSkeleton flag for old
    ; 							execute as well
    ; 30-Sep-2008	FIS		SR15947: Changed WWWTransactionTable into 
    ; 							WWWTransactionLine
    ; 29-Sep-2008	FIS		SR15947: Option to Trigger OnSave and OnAfterSave as one
    ; 							Transaction Unit
    ; 26-Sep-2008	FIS		SR15947: Transaction Wrapper for "Execute After Save"
    ; 							implemented
	; 07-Aug-2008	FIS		SR15828: compare stored data with WWWDATEN2 to stop save
	;                           if data has been changed by a different process.
	;                           Not activated yet because it also gets checked in
	;                           BeforeSave^WWWFORMValidation (might be enough)
	; 04-Aug-2008	FIS		SR15824: keep record as readonly even when the lock
	; 							has been released (data may be not up to date)
	; 12-Dec-2007	shobby	SRBR014737: If idType can't be found from the form
	; 							use the class.
	; 26-Nov-2007	GM/Shobby	SRBR014737: Get values from $$get^WWW122()
	; 05-Nov-2007	shobby	SRBR014748: Use standard routine to validate password.
	; 22-May-2007	RGB		SRBR014456: Added the pblnDoOnBeforeSave parameter
	; 							for the WWWSPEI call
	; 13-Mar-2007	GRF		SR12505: Doco
	; 21-Sep-2006	JW		SR15062: Removed FINGLBankRecon exception
	; 08-Aug-2006	JW		SR13594: Cleaned up. Brace syntax
	; 07-Feb-2006	JW		SR13305: FINGLBankRecon exception.
	; 18-Jul-2006	JW		SR14862: Decode VARHooks status
	; 13-Apr-2006	SC		SR14414: Moved VARHooks to WWWSPEI. Pass strStatus to
	; 							SPEI byref, allows VARCode to return an error msg.
	; 							Display error if returned.
	; 09-Dec-2005	JW		SR13195: Quit if trying to overwrite stored data
	; 31-Oct-2005	GRF		SR13627: Doco
	; 02-Sep-2005	JW		SR12966: Fixed WWWUSER locks
	; 16-Aug-2005	JW		SR12290: Get old YFELD to compare against.
	; 07-Jul-2005	shobby	SR12892: WWW126,WWW1261,WWW1262 are no longer shared.
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 05.08.1997	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	new (%request,%session,%KEY,%,%ZCS,%CGIEVAR)
	
	;+++++++++++++++++++++++++++++++++++++++
	; YVOR		objWWW120		Form Definition ^WWW120(0,YFORM,1)
	;+++++++++++++++++++++++++++++++++++++++
 
	DO ^WWWVAR

	SET $ZTRAP="^WWWERROR"
	IF YUSER'="" DO
	. SET $$$WWWUSERHTMLStarted(^WWWUSER(0,YUSER,1))=""
	. SET $$$WWWUSERBODYStarted(^WWWUSER(0,YUSER,1))=""
	. SET $$$WWWUSERFormStarted(^WWWUSER(0,YUSER,1))=""
	. SET $$$WWWUSERFormHeaderDisplayed(^WWWUSER(0,YUSER,1))=""
	
	IF $GET(YHYPER)'=1 IF $GET(%(YQUERY,"YEND"))'=1 DO  DO ^WWWFORM QUIT  ;UNVOLLSTAENDIG!KEIN SAVE
	. IF YFORM'="" KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D")
	
	IF YTRAKT0=1 KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D") DO ^WWWFORM QUIT  ;FALSCHE TRANSAKTIONSNR
	
	IF $GET(YSCREENM)=1 KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D") DO ^WWWFORM QUIT  ;SCREEN EDIT FUNKTION
	
	;OPTIONEN SICHERN UND NEUSTART ;safeguard And 
	IF YFORM'="" IF YOPTION="" IF $DATA(%(YQUERY,"Y"_YFORM_"O1")) IF $EXTRACT(YOPEN,5,8)="HELP" SET %("VAR","EP")="WWWHELP" DO ^WWWHELP QUIT  ;HILFE BEI OPTION;TYBD;8,7,2004;26060;
	
	IF YFORM'="" IF YOPTION="" IF $DATA(%(YQUERY,"Y"_YFORM_"O1")) SET YOPTION=%(YQUERY,"Y"_YFORM_"O1") DO  DO ^WWWFORM QUIT
	. KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D") 
	. SET YI=""
	. FOR  SET YI=$ORDER(%(YQUERY,"Y"_YFORM_"O1",YI)) QUIT:YI=""  DO
	. . SET:YOPTION'="" YOPTION=YOPTION_";"
	. . IF $GET(%(YQUERY,"Y"_YFORM_"O1",YI))'="" SET YOPTION=YOPTION_$GET(%(YQUERY,"Y"_YFORM_"O1",YI))
	. ;
	. SET %("VAR","YOPTION")=YOPTION
	
	SET YTIMEFORM = $GET(%(YQUERY,"YTIMEFORM"))  ;SAVE EINER ZEITABHÄNGIGEN ERFASSUNG      ;unit logging 
	SET YOPEN     = $GET(%(YQUERY,"YOPEN"))      ;OPEN ODER SAVE  	                   <-- Setting of YOPEN
 	
	;-------------------------------------------------------------------------------
	;YOPEN=0  = SAVE
	;YOPEN=1  = OPEN
	;YOPEN=1x = Open the last record 
	;YOPEN=2  = SAVE SEITENWECHSEL - save page overflows
	;YOPEN="SAVE"_XXXXX = Special save with function
	;-------------------------------------------------------------------------------
	
	IF $EXTRACT(YOPEN)=1 SET YOPEN1 = $EXTRACT(YOPEN,2,99) SET YOPEN=1  ;VORLETZTER DATENSATZ ;data record 
	
	IF YFORM=""                   DO ^WWWINFO($$^WWWTEXT(35))         quit    ; "No Form Default"
	IF '$DATA(^WWW120(0,YFORM,1)) DO ^WWWINFO($$^WWWTEXT(35))         quit    ; "No Form Default"
	SET YKFEHL=0
	DO ^WWWSAVV  ;VORGABEN AUS CGI ;out of
	if YOPEN="X"                  DO ^WWWINFO($$^WWWTEXT("WWW00035")) quit    ; "Cannot save, record already exists."
	
	;+++++++++++++++++++++++++++++++++++++++
	; sets
	;   YALLKEY
	;   YVOR
	;+++++++++++++++++++++++++++++++++++++++
	
	IF $EXTRACT(YOPEN,1,6)="SAVEWV" DO   ;WIEDERVORLAGEN
	. NEW DATE,HW,YDATEI,YBED1
	. SET YBED1=YBED
	. SET HW=$PIECE(YOPEN," ",2,99)
	. IF $PIECE(HW," ",1)'="" IF $DATA(^WWW013(0,$PIECE(HW," ",1),1)) SET YBED1=$PIECE(HW," ",1) SET HW=$PIECE(HW," ",2,99)  ;WIEDERVORLAGE AND JEMANDEN ANDERES
	. SET DATE = $$^WWWDATE1($EXTRACT($PIECE(YOPEN," ",1),7,20))
	. IF +DATE=0 SET DATE = +$HOROLOG
	. IF +DATE'=0 DO
	. . SET YDATEI=$$$WWW120ClassUsedInForm(YVOR)
	. . SET YMAXKEY=0
	. . IF YDATEI'="" SET YMAXKEY=+$ORDER(^WWW002(0,YDATEI,""),-1)
	. . SET YKEY=""
	. . FOR YI=1:1:YMAXKEY QUIT:'$DATA(YKEY(1))  SET YKEY=YKEY_YKEY(YI) IF YI'=YMAXKEY IF $GET(YKEY(YI+1))'="" SET YKEY=YKEY_","
	. . SET ^WWWWV(YM,YBED1,DATE,YFORM,$TRANSLATE(YKEY,",","/"),1)=+$HOROLOG_Y_$PIECE($HOROLOG,",",2)_Y_YBED_Y_HW
	. ;
	. SET YOPEN=2
	
	
	;SPECIAL VARIABLE
	IF $ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",""))'="" DO
	. NEW YVAR
	. SET YVAR=""
	. FOR  SET YVAR=$ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YVAR)) QUIT:YVAR=""  DO
	. . SET %(YQUERY,YVAR)=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YVAR,1))
	
	KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V")
	
	;-------------------------------------------------------------------------------
	; Form Type 8  : WIZARD
	;-------------------------------------------------------------------------------
	IF $$$WWW120FormType(YVOR)=8 DO  QUIT
	. do Wizard()
	
	;-------------------------------------------------------------------------------
	; Form Type 6  : MENU INPUT TYPE
	;-------------------------------------------------------------------------------
	IF $$$WWW120FormType(YVOR)=6 DO  QUIT
	. IF $EXTRACT(YOPEN,5,8)="HELP" SET %("VAR","EP")="WWWHELP" DO ^WWWHELP       ;ZUM HILFETEXT
	
	;-------------------------------------------------------------------------------
	; Form Type 7  : SEARCH ENGINE
	;-------------------------------------------------------------------------------
	IF $$$WWW120FormType(YVOR)=7 DO  QUIT
	. IF $EXTRACT(YOPEN,5,8)="HELP" SET %("VAR","EP")="WWWHELP" DO ^WWWHELP QUIT  ;ZUM HILFETEXT
	. DO ^WWWFORM   ;SUCHMASCHINE
	
	;--------------
	
	SET YDATEI = $$$WWW120ClassUsedInForm(YVOR)
	
	;------------------- 
	;New Tab
	;------------------- 
	IF YOPEN=2 SET YQ=0 DO  KILL ^WWWDUMMY(YUSER) QUIT:YQ=1   ;NUR SEITENWECHSEL ;only 
	. SET YABBR=0
	. IF YDATEI'="" SET YI="" FOR  SET YI=$ORDER(^WWW002(0,YDATEI,YI)) QUIT:YI=""  DO  QUIT:YABBR=1   ;PRIMÄRSCHL.
	. . SET YTYP=$PIECE($GET(^WWW002(0,YDATEI,YI,1)),Y,3)
	. . ;DO PRUEFP^WWWSAVP  ;PRUEFEN KEY
	. . IF '$DATA(YKEY(YI)) SET YABBR=1 QUIT
	. . IF YKEY(YI)="" SET YABBR=1 QUIT
	. . SET YKEY(YI)=$$GetInternal^WWWTR(YTYP,YKEY(YI))
	. ;
	. IF YDATEI'="" SET YI="" FOR  SET YI=$ORDER(^WWW003(0,YDATEI,YI)) QUIT:YI=""  DO
	. . set YTYP = $$GetInputType(YDATEI,YI,YFORM)
	. . SET $PIECE(YFELD,Y,YI)=$$GetInternal^WWWTR(YTYP,$PIECE(YFELD,Y,YI))
	. ;
	. SET ^WWWDUMMY(YUSER,"P") = $GET(YKEY(1))_","_$GET(YKEY(2))_","_$GET(YKEY(3))_","_$GET(YKEY(4))_","_$GET(YKEY(5))_","_$GET(YKEY(6))_","_$GET(YKEY(7))_","_$GET(YKEY(8))
	. SET ^WWWDUMMY(YUSER,"D") = YFELD
	. SET ^WWWDUMMY(YUSER,"M") = YMFELD
	. IF $GET(YTIMEFORM)=1 DO
	. . SET %("VAR","YFORM")=YFORM_"t"  ;ZEITABHÄNGIGE VORGABE ;default 
	. . IF YDATEI'="" DO                ;SICHER DATEN FÜR SEITENWECHSEL ;sure to 
	. . . SET YMAXKEY=+$ORDER(^WWW002(0,YDATEI,""),-1)
	. . . SET $PIECE(^WWWDUMMY(YUSER,"P"),",",YMAXKEY+1) = $$^WWWDATE1($GET(%(YQUERY,"Y"_YFORM_"P"_(YMAXKEY+1))))
	. ;
	. DO ^WWWFORM SET YQ=1
	
	;-------------------------------------------------------------------------------
	; Form Type 1  : STANDARD FORM
	; Form Type 3  : GRID FORM
	;-------------------------------------------------------------------------------
	IF $$$WWW120SaveAlsoWithoutDataFields(YVOR)'=1 IF $$$WWW120FormType(YVOR)=1||($$$WWW120FormType(YVOR)=3) IF YOPEN=0||(YOPEN=2) IF YFELD=$GET(^WWW1261(YM,YFORM,1)) DO
	. IF $$$WWW120FormType(YVOR)=3 IF $GET(YMAXKEY)=1 QUIT  ;NICHT;TYBD;13,10,2004
	. SET YOPEN=1    ;NEUER DATENSATZ OHNE WERTE SPEICHERN ODER NICHT ;data record without Save Or Not 
	
	;ALTE VORGABE SCHLUESSEL UND ÖFFNEN ALTEN DATENSATZ ;default And unclose data record 
	IF $GET(YOPEN1)'="" IF (YALLKEY=1) || (YALLKEY=2) SET YI="",YKEY="" FOR  SET YI=$ORDER(^WWW126(YM,YFORM,YBED,YI)) QUIT:YI=""  DO
	. SET YKEY(YI)=$GET(^WWW126(YM,YFORM,YBED,YI,1))  ;LETZTER DATENSATZ ;last data record 
	. IF $GET(YOPEN1)'="" SET YKEY(YI)=$GET(^WWW1262(YM,YFORM,YBED,YOPEN1,YI,1))   ;VORLETZTER DATENSATZ ;data record 
	. IF YKEY(YI)'="" SET:YKEY'="" YKEY=YKEY_"," SET YKEY=YKEY_YKEY(YI)
	. SET YALLKEY=9
	
	; shobby ---------------------------  SR12442
	; The previous section may have failed because an item has just been deleted meaning that it is no longer
	; in WWW126, although WWW1262 is still valid.
	IF $GET(YOPEN1)'="" IF (YALLKEY=1) || (YALLKEY=2) SET YI="",YKEY="" FOR  SET YI=$ORDER(^WWW1262(YM,YFORM,YBED,YOPEN1,YI)) QUIT:YI=""  DO
	. SET YKEY(YI)=$GET(^WWW1262(YM,YFORM,YBED,YOPEN1,YI,1))  ;LETZTER DATENSATZ ;last data record 
	. IF YKEY(YI)'="" SET:YKEY'="" YKEY=YKEY_"," SET YKEY=YKEY_YKEY(YI)
	. SET YALLKEY=9
	; shobby ---------------------------
	
	;ALTE VORGABE SCHLUESSEL UND ÖFFNEN ALTEN DATENSATZ ;default And unclose data record 
	IF YOPEN=1 IF $DATA(YKEY(1)) IF (YKEY(1)="") || (YKEY(1)="+") IF $TRANSLATE(YKEY,",+"_"""")="" IF ($TRANSLATE(YFELD,Y_"0 ,.")=$TRANSLATE($GET(^WWW1261(YM,YFORM,1)),Y_"0 ,.")) || ($TRANSLATE($GET(^WWW1261(YM,YFORM,1)),Y_"0 ,.")="") IF YALLKEY=1 SET YI="",YKEY="" FOR  SET YI=$ORDER(^WWW126(YM,YFORM,YBED,YI)) QUIT:YI=""  DO
	. SET YKEY(YI) = $GET(^WWW126(YM,YFORM,YBED,YI,1))  ;LETZTER DATENSATZ ;last data record 
	. IF $GET(YOPEN1)'="" SET YKEY(YI) = $GET(^WWW1262(YM,YFORM,YBED,YOPEN1,YI,1))  ;VORLEZTER DATENSATZ ;data record 
	. IF YKEY(YI)'="" SET:YKEY'="" YKEY=YKEY_"," SET YKEY = YKEY_YKEY(YI)
	. SET YALLKEY=9
	
	
	;-------------------------------------------------------------------------------
	;SUCHEN SCHLÜSSEL OHNE PRIMÄRSCHL ;seek key without 
	IF YALLKEY=1 IF $GET(YKEY(1))="" IF $TRANSLATE(YFELD,Y)'="" IF '$DATA(^WWW002(0,YDATEI)) DO PRIM^WWWLOOK IF YKEY'="" SET YALLKEY=9
	
	;-------------------------------------------------------------------------------
	; Form Type 3  : GRID FORM
	;-------------------------------------------------------------------------------
	IF $GET(YOPEN)=1 IF $$$WWW120FormType(YVOR)=3 IF (YALLKEY=1) || (YALLKEY=2) DO  IF YALLKEY=0 QUIT  ;DATEN AUS GRID GEFUNDEN;TYBD;10,04,2003
	. QUIT:YDATEI=""
	. SET YMAXKEY=+$ORDER(^WWW002(0,YDATEI,""),-1)
	. QUIT:YMAXKEY<2  ;KEIN GRID MIT MEHR ;no by means of more 
	. IF YALLKEY=2 IF $GET(YKEY(YMAXKEY))'="+" QUIT  ;LETZTE KEY NICHT + ABER VOLLSTÄNDIG ;last KEY Not yet integral 
	. SET YKEY=""
	. FOR YI=1:1:YMAXKEY-1 SET YKEY=YKEY_$GET(YKEY(YI)) IF YI'=YMAXKEY SET YKEY=YKEY_","
	. SET %("VAR","YKEY")=YKEY
	. SET %("VAR","YNEW")=1
	. DO ^WWWFORM
	. SET YALLKEY=0
	
	
	;SUCHEN KEYVORGABE MIT ANZEIGE AUSWAHL KEY ;seek by means of Show Selection KEY 
	IF $GET(YKEY(1))'="+" IF $GET(YKEY(2))'="+" IF $GET(YKEY(3))'="+" IF $GET(YKEY(4))'="+" IF $GET(YKEY(5))'="+" IF YRICHT="" IF YALLKEY=2 IF $EXTRACT(YOPEN,1,4)'="SAVE" DO
	. QUIT:'$DATA(^WWW121(0,YFORM))  ;KEINE PRIMÄRSCHL ;no 
	. DO KEY^WWWLOOK
	. SET YALLKEY=1
	. IF YKEY'="" SET YALLKEY=9    ;SUCHEN PASSENDEN DATENSATZ ;seek data record 
	
	;-------------------------------------------------------------------------------
	IF YFORM="WWW120" SET $$$WWW013LastFormUsed(^WWW013(0,YBED,1)) = $GET(YKEY(1))  ;VORGABE TEST ;default Test 
	
	;-------------------------------------------------------------------------------
	; Form Type 1  : STANDARD FORM
	; Form Type 3  : GRID FORM
	;-------------------------------------------------------------------------------
	
	;AUSWAHL DATENSATZ AUS FELDERN ;Selection data record out of 
	if YRICHT="" IF $$$WWW120FormType(YVOR)=1||($$$WWW120FormType(YVOR)=3) IF YALLKEY=1 IF $EXTRACT(YOPEN,1,4)'="SAVE" DO  QUIT
	. DO ^WWWINFO($GET(YKEY(1))_" "_$GET(YKEY(2))_" "_$GET(YKEY(3))_" "_$GET(YKEY(4))_" "_$$^WWWTEXT(86))  ;TYBD;4,2,2004;NICHT VORHANDEN 
	. ;DO ANZEIGE^WWWLOOK QUIT  ;MIT ANZEIGE;TYBD;4,2,2004
	
	;-------------------------------------------------------------------------------
	
	IF YRICHT="" IF ($$$WWW120FormType(YVOR)=3) || ($$$WWW120FormType(YVOR)=1) IF $EXTRACT(YOPEN,1,4)'="SAVE" IF $TRANSLATE(YFELD,Y)="" IF (YKEY(1)="") || ($GET(YKEY(1))="+") || ($GET(YKEY(2))="+") || ($GET(YKEY(3))="+") || ($GET(YKEY(4))="+") || ($GET(YKEY(5))="+") DO ANZEIGE^WWWLOOK QUIT  ;MIT ANZEIGE ;by means of Show 
	;-------------------------------------------------------------------------------
	
	;NÄCHSER DATENSATZ ;data record 
	IF $EXTRACT(YOPEN,1,4)'="SAVE" IF YRICHT'="" SET YKEY="" IF (YKEY(1)="") || ($GET(YKEY(1))="+") || ($GET(YKEY(2))="+") || ($GET(YKEY(3))="+") || ($GET(YKEY(4))="+") || ($GET(YKEY(5))="+") SET $PIECE(^WWWUSER(0,YUSER,1),Y,15)="" DO ^WWWFORM QUIT
	;-------------------------------------------------------------------------------
 
	;DATENSATZ ÖFFNEN ;data record unclose 
	IF $EXTRACT(YOPEN,1,4)'="SAVE" IF YALLKEY=9 DO  DO ^WWWFORM QUIT
	. IF $GET(YTIMEFORM)=1 DO
	. . SET %("VAR","YFORM")=YFORM_"t"  ;ZEITABHÄNGIGE VORGABE ;default 
	. . IF $GET(YDATEI)'="" DO   ;SICHER DATEN FÜR SEITENWECHSEL ;sure to 
	. . . SET YMAXKEY=+$ORDER(^WWW002(0,YDATEI,""),-1)
	. . . SET $PIECE(YKEY,",",YMAXKEY+1)=$$^WWWDATE1($GET(%(YQUERY,"Y"_YFORM_"P"_(YMAXKEY+1))))
	. . . SET $PIECE(^WWWDUMMY(YUSER,"P"),",",YMAXKEY+1)=$$^WWWDATE1($GET(%(YQUERY,"Y"_YFORM_"P"_(YMAXKEY+1))))
	. ;
	. SET %("VAR","YKEY")=YKEY
	
	; OPEN ;
	IF (YRICHT="") && (YOPEN=1) DO  QUIT  ;WENN ÖFFNEN DURCH SEITENWECHSEL ;when unclose trans- 
	. SET %("VAR","YKEY")="" 
	. SET %("VAR","YSEITE")="" 
	. SET YSEITE=1
	. DO ^WWWFORM
	
	;-------------------------------------------------------------------------------
	; Form Type 2  : LIST GENERATOR		LISTEN FELDDEFINITIONEN - LISTEN VORGABEN
	;-------------------------------------------------------------------------------
	SET Q=0
	IF $$$WWW120FormType(YVOR)=2 DO
	. FOR YI=1:1:5 SET YA=$GET(%(YQUERY,"Y"_YFORM_"LR"_YI_"")) IF YA'="" SET YLR(YI)=YA  ;RICHTUNG LISTGENERATORFILES ;trend 
	. FOR YI1=1:1:5 FOR YI=1:1:10 SET YA=$GET(%(YQUERY,"Y"_YFORM_"LP"_YI1_$EXTRACT(100+YI,2,3)_""))  DO MULTL^WWWSAVV("Y"_YFORM_"LP"_YI1_$EXTRACT(100+YI,2,3)) IF YA'="" SET YLP(YI1,$EXTRACT(100+YI,2,3))=YA  ;LISTGENERATOR PRIMÄRSCHL
	. FOR YI1=1:1:5 FOR YI=1:1:10 SET YA=$GET(%(YQUERY,"Y"_YFORM_"LP1"_YI1_$EXTRACT(100+YI,2,3)_"")) DO MULTL^WWWSAVV("Y"_YFORM_"LP1"_YI1_$EXTRACT(100+YI,2,3)) IF YA'="" SET YLP1(YI1,$EXTRACT(100+YI,2,3))=YA  ;LISTGENERATOR PRIMÄRSCHL BIS ;until 
	. FOR YI1=1:1:5 FOR YI=1:1:99 SET YA=$GET(%(YQUERY,"Y"_YFORM_"LD"_YI1_$EXTRACT(100+YI,2,3)_""))  DO MULTL^WWWSAVV("Y"_YFORM_"LD"_YI1_$EXTRACT(100+YI,2,3)) IF YA'="" SET YLD(YI1,$EXTRACT(100+YI,2,3))=YA  ;LISTGENERATOR DATENFELDER
	. FOR YI1=1:1:5 FOR YI=1:1:99 SET YA=$GET(%(YQUERY,"Y"_YFORM_"LD1"_YI1_$EXTRACT(100+YI,2,3)_"")) DO MULTL^WWWSAVV("Y"_YFORM_"LD1"_YI1_$EXTRACT(100+YI,2,3)) IF YA'="" SET YLD1(YI1,$EXTRACT(100+YI,2,3))=YA  ;LISTGENERATOR DATENFELDER BIS ;until 
	. FOR YI=1:1:5 SET YA=$GET(%(YQUERY,"Y"_YFORM_"S"_YI_""))  IF YA'="" SET YS(YI)=YA,SORT(YI)=YA,SORT=YA  ;SORTIERUNGEN
	. FOR YI=1:1:20 SET YA=$GET(%(YQUERY,"Y"_YFORM_"M"_YI_"")) IF YA'="" SET YM(YI)=YA  ;MANUELLE IN EINER LISTE ;within unit list 
	
	IF '$DATA(^WWWSOR(YUSER,"BIT")) {
		KILL ^WWWSOR(YUSER)
	} ELSE {
		KILL ^WWWSOR(YUSER,1)
		KILL ^WWWSOR(YUSER,2)
		KILL ^WWWSOR(YUSER,"PAGE")
	}
	
	IF $EXTRACT(YOPEN,1,4)="SAVE" DO  QUIT   ;DATEN ZWISCHENSPEICHERN WEGEN SUCHE/HILFE/BUTTON ;quibble 
	. do BufferSave()
	
	KILL ^WWWSOR(YUSER)
	
	; Check User
	SET YBED=$$$UPPER(YBED)
	IF '$DATA(^WWW013(0,YBED)) DO ^WWWINFO($$^WWWTEXT(5)) QUIT
	SET YVORM=^WWW013(0,YBED,1)
	IF '$$CHECK^WWWPWDCHECK($$$WWW013Password1(YVORM),YPWD) DO ^WWWINFO($$^WWWTEXT(5)) QUIT
	SET YBER=$$$WWW013UserAccess(YVORM)
	SET YMOD=$$$WWW013Module1(YVORM)
	
	DO ^WWWFORMX  ;VORGABEN
	
	;+++++++++++++++++++++++++++++++++++++++
	;		D22		$$$WWW120UserAccess()
	;		D23		$$$WWW120AuthorizationToModifyData()
	;		D24		$$$WWW120Modules()
	;+++++++++++++++++++++++++++++++++++++++
	IF ($$$WWW120UserAccess(YVOR)'="") || ($$$WWW120Modules(YVOR)'="") IF $$^WWWACCESS($$$WWW120UserAccess(YVOR)_";"_$$$WWW120DUserAccess($get(^WWW120D(0,YFORM,0,1))),$$$WWW120Modules(YVOR))'=1 SET Q=0 DO  IF Q=0 DO ^WWWINFO($$^WWWTEXT(5)) QUIT  ;BERECHTIGUNGEN;AUCH ERLAUBT WENN MITARBEITERMENÜ;FIS;26637;27.10.04
	. NEW SONDERMENU
	. IF ($PIECE($GET(YANZ),",",1)="") || ($PIECE($GET(YANZ),",",2)="") QUIT
	. SET SONDERMENU=$PIECE($GET(^WWW0132(0,YBED,YM,$PIECE(YANZ,",",1),1)),Y,1)
	. IF SONDERMENU'="" IF $FIND(";"_SONDERMENU_";",";"_$PIECE(YANZ,",",2)_";") SET Q=1  ;ACHTUNG ! NUR PRÜFUNG DES OBER-MENÜPUNKTES
	
	IF $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly DO  QUIT
	. SET $$$WWWUSERLastFormfield(^WWWUSER(0,YUSER,1))="" 
	. SET YKEY=""
	. FOR YI=1:1:YMAXKEY QUIT:'$DATA(YKEY(YI))  SET YKEY=YKEY_YKEY(YI) IF YI'=YMAXKEY IF $GET(YKEY(YI+1))'="" SET YKEY=YKEY_","
	. SET %("VAR","YKEY")=YKEY  ;KEY
	. DO ^WWWFORM
	
	IF $EXTRACT(YFORM,1,3)="WWW" if (","_$$^WWWBEDBER(YBED)_",")[(",33,") DO ^WWWINFO($$^WWWTEXT(12)) QUIT  ;"DEMO" ; "Attention! Access Denied!"
	
	SET Q=0
	IF $DATA(^WWW122s(0,4," ",YFORM)) DO ^WWWSAVM IF Q=1 QUIT  ;MANUELLE
	
	;NORMALER DATENSATZ SPEICHERN ;data record Save 
	KILL ^WWWSOR(YUSER,"PAGE")  ;TYBD;10,2,2004
	LOCK +^WWWUSER(0,YUSER):3  ;SICHERN FÜR 2 CLICKS TYBD;30.12.2002 ;safeguard to 
	SET YFELD=$TRANSLATE(YFELD,$CHAR(13,10),"|")  ;TEXTE
	IF $EXTRACT(YFELD,$LENGTH(YFELD))="|" SET YFELD=$EXTRACT(YFELD,1,$LENGTH(YFELD)-1)
	SET YABBR=0
	SET YDATEI=$PIECE(YVOR,Y,11)
	
	;-------------------------------------------------------------------------------
	; Forms other than
	; 	Form Type 2  : LIST GENERATOR
	; 	Form Type 4  : MANUAL INPUT (WITH BUTTON)
	;-------------------------------------------------------------------------------
	IF $$$WWW120FormType(YVOR)'=4 IF $$$WWW120FormType(YVOR)'=2 IF YDATEI'="" DO  LOCK -^WWWUSER(0,YUSER) QUIT
	. NEW Q,YLINK
	. SET YOK=1
	. SET YLFZ=0
	. ;
	. ;-------------------------------------
	. ;  Key Validation
	. ;-------------------------------------
	. SET YI=""
	. FOR  SET YI=$ORDER(^WWW002(0,YDATEI,YI)) QUIT:YI=""  DO  QUIT:YABBR=1   ;PRIMÄRSCHL.
	. . IF $GET(YKEY(YI))'="" IF YBED'="" SET ^WWW126(YM,YFORM,YBED,YI,1)=$GET(YKEY(YI)) SET ^WWW126(YM,YFORM,YUSER,YI,1)=$GET(YKEY(YI)) ;SPEICH DFLT
	. . SET YTYP=$PIECE($GET(^WWW002(0,YDATEI,YI,1)),Y,3)
	. . DO PRUEFP^WWWSAVP
	. . IF '$DATA(YKEY(YI)) SET YABBR=1 SET YOK=0 QUIT
	. . IF YKEY(YI)=""      SET YABBR=1 SET YOK=0 QUIT
	. . SET YKEY(YI)=$$GetInternal^WWWTR(YTYP,YKEY(YI))
	. ;
	. IF YOK=0 IF YRICHT'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,15)="" DO ^WWWFORM QUIT    ;DATENFELDER DURCHGEHEN ;peruse data fields 
	. IF YOK=0 DO ^WWWINFO($$^WWWTEXT(59)_" "_$$^WWWTEXT(86))                    QUIT    ; "Primary Key Not Available!"
	. ;
	. ;-------------------------------------
	. ;
	. ; Set oldYFELD to stored record.
	. SET YMAXKEY=+$ORDER(^WWW002(0,YDATEI,""),-1)
	. IF YMAXKEY=0 SET YMAXKEY=1  ;AUTOMATISCH LFD NUMMER ;automatic numeral 
	. SET myYKEY=""
	. FOR YI=1:1:YMAXKEY QUIT:'$DATA(YKEY(YI))  SET myYKEY=myYKEY_YKEY(YI) IF YI'=YMAXKEY IF $GET(YKEY(YI+1))'="" SET myYKEY=myYKEY_","
	. set oldYFELD = ""
	. if myYKEY'="" set oldYFELD = $$^WWWSETL("^"_YDATEI_"("_$$^WWWYM(YDATEI,1)_$$^WWWKEYBUILD(myYKEY)_",1)",1)
	. ;
	. ;-------------------------------------
	. ;  Data Validation
	. ;-------------------------------------
	. SET YI=""
	. FOR  SET YI=$ORDER(^WWW003(0,YDATEI,YI)) QUIT:YI=""  DO
	. . set YTYP = $$GetInputType(YDATEI,YI,YFORM) ;16123
	. . SET YSPX = $PIECE($GET(^WWW003(0,YDATEI,YI,1)),Y,17)
	. . DO PRUEFD^WWWSAVD(oldYFELD)
	. . IF YSPX=1 SET ^WWWDLF(0,YDATEI,YI,YBED,1)=$PIECE(YFELD,Y,YI)
	. ;
	. IF YOK=0 DO ^WWWINFO($$^WWWTEXT(59)_" "_$$^WWWTEXT(86)) QUIT  ;FEHLER ; error  ; "Primary Key Not Available!" - FIXME: data error not "Primary Key" <GRF>
	. ;
	. ;-------------------------------------
	. set YKEY = myYKEY
	. SET KEYKEY=$TRANSLATE(YKEY,"""")
	. SET FELDFELD=YFELD
	. ;
	. ;OPEN
	. IF YALLKEY=9 SET YOK=9
	. ;
	. ;+++++++++++++++++++++++++++++++++++++
	. ;	 YVOR	D16		$$$WWW120ExecuteOnAfterSaveData()
	. ;			D18		$$$WWW120ExecuteAfterDataModified()
	. ;			D23		$$$WWW120AuthorizationToModifyData()
	. ;			D89		$$$WWW120ExecuteOnBeforeSaveData()
	. ;+++++++++++++++++++++++++++++++++++++
	. ;
	. ;SAVE
	. SET YAEND=1  ;=ÄNDERUNG 
	. IF $FIND(YFELD,"() Error: URL Problem: SendRequest: java") SET ^WWWSOR(YUSER,1)="Explorer: () Error: URL Problem: SendRequest: java|"  ;FEHLER ;shortcoming 
	. ;
	. quit:$$OnBeforeSave()
	. ;
	. set strStatus = $$$OK
	. ;
	. ;********* Save *************
	. ;
	. ;#### Start Transaction Unit ################################  ; SR15947
	. ;#### if Save and Execute After Save gets processed as 1 unit
	. ;#### 1 - remember current error handler
	. ;#### 2 - change $ztrap to Transaction Error Handler
	. ;#### 3 - start transaction and try to lock tables
	. ;#### strTStatus = 1 -> Transaction Started, Locks set
	. ;#### strTStatus = 0 -> Transaction NOT Started, NO Locks set
	. if ($$$WWW120ExclAfterSave(YVOR)'=$$$YES) set strTZtrap=$ztrap,$ztrap="TransactionUnitError^COMTransaction" do  ;!!!! "set $ztrap..." MUST BE IN SAME LINE TO BE IN SAME FRAMESTACK !!!
	. . set strTStatus = $$StartUnit^COMTransaction(YFORM,"ExecuteAfterSave")
	. ;############################################################
	. ;############################################################
	. ;
	. new pblnDoOnBeforeSave
	. set pblnDoOnBeforeSave = $$$NO
	. IF '$DATA(^WWWSOR(YUSER,1))#10=1 IF YALLKEY'=9 SET YOK=$$^WWWSPEI(YDATEI,YKEY,YFELD,0,,,.strStatus,pblnDoOnBeforeSave) SET $PIECE(^WWWUSER(0,YUSER,1),Y,22)=YOK   ;KOPF AUS ;SAVE DES DATENSATZES
	. IF YOK=0 DO  QUIT
	. . ;#### End Transaction Unit - EARLY QUIT ###########
	. . ;#### If Save failed (Soft/- or Hardfail):
	. . ;#### 1 - Commit the Transaction (only if started)
	. . ;#### 2 - Change the Error Handler back to default
	. . if ($$$WWW120ExclAfterSave(YVOR)'=$$$YES) do
	. . . set:$$$ISOK(strTStatus) strTStatus=$$EndUnit^COMTransaction(YFORM,"ExecuteAfterSave",strTStatus,"")
	. . ;##################################################
	. . if $$$ISERR(strStatus) DO ^WWWINFO($$$Text(strStatus)) //VARCode has set an error message.
	. .	if $$$ISOK(strStatus)  DO ^WWWINFO($$^WWWTEXT(141))    //Otherwise, use default - "In Use"
	. ;
	. IF YOK'=9 IF YOK'=1 SET YCHANGE=1 IF $PIECE(YVOR,Y,18)'="" IF '$DATA(^WWWSOR(YUSER,1))#10=1 DO ^WWWFORM5 XECUTE $PIECE(YVOR,Y,18) ;EXECUTE BEI AENDERUNG ;EXECUTE next to 
	. ;
	. ;**** Execute After Save *****
	. IF YOK'=9 IF $PIECE(YVOR,Y,23)'=3 IF '$DATA(^WWWSOR(YUSER,1))#10=1 DO
	. . ;
	. . IF $GET(YTIMEFORM)'=1 && (($PIECE(YVOR,Y,16)'="") || ($data(^WWWTransactionLine(0,YFORM,"ExecuteAfterSave")))) DO
	. . . SET:$EXTRACT($PIECE(YVOR,Y,16))="#" $PIECE(YVOR,Y,16)=$EXTRACT($PIECE(YVOR,Y,16),2,99)
	. . . DO ^WWWFORM5
	. . . DO ^WWWLESE(YDATEI,YKEY)  ;LESEN WERTE ;read 
	. . . IF $PIECE(YVOR,Y,16)'="" XECUTE $PIECE(YVOR,Y,16)  ;EXECUTE NACH SPEICHERN Q=1 = FORM NICHT MEHR ANZEIGEN ;EXECUTE AFTER SAVE ; Q=1: do not display form
	. . . ;
	. . . ;### Execute Transaction Unit ###
	. . . do
	. . . . if $$$WWW120ExclAfterSave(YVOR)'=$$$YES quit:'$$$ISOK(strTStatus)  ;expects strTStatus form StartUnit
	. . . . set strTStatus = $$TransactionUnit^COMTransaction(YFORM,"ExecuteAfterSave",'$$$WWW120ExclAfterSave(YVOR),.strTError,.Q)
	. . . ;
	. . . IF $$EXIST^%R("Y"_YFORM_"onSave.OBJ",$GET(YUCI)) XECUTE "DO ^Y"_YFORM_"onSave"  ;CUSTOMIZED EXECUTE;FIS;24947;03.02.04
	. ;
	. ;#### End Transaction Unit - STANDARD QUIT ####
	. ;#### 1 - Commit or Rollback the Transaction (depending on strTStatus / strTError)
	. ;#### 2 - Change the Error Handler back to defeult
	. ;#### 3 - In case of Rollback: Change Save Flag to Hardfail and Quit 
	. if ($$$WWW120ExclAfterSave(YVOR)'=$$$YES) do  set $ztrap = strTZtrap    ;!!!! "set $ztrap..." MUST BE IN SAME LINE TO BE IN SAME FRAMESTACK !!!
	. . set strTStatus = $$EndUnit^COMTransaction(YFORM,"ExecuteAfterSave",strTStatus,$get(strTError))
	. . if '$$$ISOK(strTStatus) set YOK = 0
	. ;##############################
	. ;
	. IF YOK'=9 IF YRICHT="" IF $PIECE(YVOR,Y,23)<2 IF $$$WWW120FormType(YVOR)=1 IF $TRANSLATE($GET(YFKEY),","_"""")'=$TRANSLATE(YKEY,","_"""") IF $PIECE($GET(^WWW012(0,YM,1)),Y,149)=1 SET YKEY=$PIECE(YKEY,",",1,$LENGTH(YKEY,",")-1)   ;TYBD;WENN VORGABE = 1 IMMER NEUE DATENERFASSUNG  ;when default constantly data gathering 
	. ;
	. SET %("VAR","YKEY")=YKEY  ;KEY
	. IF YKFEHL=0 IF YOPEN=2 SET YOK=9 KILL ^WWWSOR(YUSER)  ;2.SEITE KEINE PRÜF ;no 
	. ;-------------------------------------------------------------------------------
	. ; Form Type 3  : GRID FORM
	. ; WENN GRID ODER OHNE PRIM KEY DANN DATENSATZ LÖSCHEN ;when Or without PRIM KEY data record Delete 
	. ;-------------------------------------------------------------------------------
	. IF $$$WWW120FormType(YVOR)=3 SET $PIECE(YKEY,",",YMAXKEY)="" SET %(YQUERY,"YKEY")=YKEY SET %("VAR","YKEY")=YKEY  ;LÖSCHEN LETZTER KEY BEI GRIDFELDER=NUR ANZEIGE IN GRID FÜR NEUERFASSUNG ;Delete last KEY next to Show within to 
	. ;
	. ;WENN ERFOLGREICH ;when successful 
	. IF $GET(YTIMEFORM)=1 DO
	. . SET %("VAR","YFORM")=YFORM_"t"  ;ZEITABHÄNGIGE VORGABE ;default 
	. . SET %("VAR","YKEY")=YKEY_","_$$^WWWDATE1($GET(%(YQUERY,"Y"_YFORM_"P"_(YMAXKEY+1))))
	. ;
	. IF $GET(Q)'=1 IF YOK=9||('$DATA(^WWWSOR(YUSER,1))#10=1) DO ^WWWFORM QUIT  ;ANZEIGE FORM NACH ERFOLGREICHEM SAVE ;Show shape within 
	. ;
	. ;WENN MELDUNG (Z.B. FEHLER) ;when status signal 
	. SET ^WWWUSE(0,YUSER,YFORM,"D",1)=YFELD
	. SET YA="",YI=""
	. FOR  SET YI=$ORDER(^WWWSOR(YUSER,YI)) QUIT:YI=""  IF +YI'=0 DO
	. . SET YA=YA_$GET(^WWWSOR(YUSER,YI))
	. . SET YKILL=2                         ;BERICHTIGUNG NOETIG ;correction 
	. . IF $GET(YAEND)=1 SET YKILL=3
	. . SET %("VAR","YKILL")=YKILL
	. ;
	. IF $DATA(^WWWSOR(YUSER,1))#10=1 IF YA="" SET YA=$$^WWWTEXT(391)         ; "Not Saved!"   ;NICHT GESPEICHERT;TYBD;10,2,2004
	. if '$data(@("^"_YDATEI_"("_$$^WWWYM(YDATEI,1)_$$^WWWKEYBUILD(YKEY)_",1)")) do
	. . SET %("VAR","YKEY") = ""
	. . SET YKEY  = ""
	. . set YFELD = ""
	. DO ^WWWINFO(YA,,$GET(YLINK))
	. WRITE " "                             ;RETURN SOME DATA;TYBD;4,2,2004
	
	;-------------------------------------------------------------------------------
	; Forms from type
	; 	Form Type 2  : LIST GENERATOR
	; 	Form Type 4  : MANUAL INPUT (WITH BUTTON)
	;-------------------------------------------------------------------------------
	
	;+++++++++++++++++++++++++++++++++++++++
	;	D16		$$$WWW120ExecuteOnAfterSaveData()
	;+++++++++++++++++++++++++++++++++++++++
	IF $PIECE(YVOR,Y,16)'="" && ($extract($PIECE(YVOR,Y,16),1)'=";") DO
	. SET YEXEC=$PIECE(YVOR,Y,16)
	. if $$$WWW120DoNotPrepareHTMLSkeleton(YVOR) = $$$YES set YEXEC = "#"_YEXEC  //Do Not Prepare HTML Skeleton
	. DO PGMSTART^WWWMANU  ;MANUELLE ;WENN YNOLIST=1 DANN SOLL DIE LISTE NICHT ANGEZEIGT WERDEN ;when who list Not will 
	
	if $data(^WWWTransactionLine(0,YFORM,"ExecuteAfterSave")){
		set YEXEC = "set strTStatus = $$TransactionUnit^COMTransaction(YFORM,""ExecuteAfterSave"",,.strTError)"
		if $$$WWW120DoNotPrepareHTMLSkeleton(YVOR) = $$$YES set YEXEC = "#"_YEXEC  //Do Not Prepare HTML Skeleton
		if ($piece(YVOR,Y,16)'="") && ($extract($PIECE(YVOR,Y,16),1)'=";") set YEXEC = "#"_YEXEC set YNOFOOT=1   //HTML already prepared
		do PGMSTART^WWWMANU
	}
	
	IF $$EXIST^%R("Y"_YFORM_"onSave.OBJ",$GET(YUCI)) XECUTE "DO ^Y"_YFORM_"onSave"  ;CUSTOMIZED EXECUTE;FIS;24947;03.02.04
	
	;-------------------------------------------------------------------------------
	; Form Type 2  : LIST GENERATOR
	;-------------------------------------------------------------------------------
	
	IF $$$WWW120FormType(YVOR)=2 DO
	. QUIT:$GET(YNOLIST)=1  ;LISTE NICHTMEHR ANZEIGEN NACH MANUELLEM EXECUTE;TYBD;23,6,2004;25978
	. DO ^WWWLIST0          ;LISTGENERATOR
	
	LOCK -^WWWUSER(0,YUSER)
	QUIT
	
OnBeforeSave()
	;-------------------------------------------------------------------------------
	; Run OnBeforeSave code (if not doing hyper validation)
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:	boolean - whether to stop save happening
	;
	; History:
    ; 27-Sep-2007	GRF		SR15603: Macro change
	; 10-Aug-2006	JW		SR13594: Created
	;-------------------------------------------------------------------------------
	new blnStop,strExec
	
	if '$$$HYPERValidation(YFORM) do
	. ;
	. set strExec = $$$WWW120ExecuteOnBeforeSaveData(YVOR)
	. IF '$DATA(^WWWSOR(YUSER,1))#10=1 IF strExec'="" SET:$EXTRACT(strExec)="#" strExec=$EXTRACT(strExec,2,99) DO ^WWWFORM5 XECUTE strExec IF $GET(Q)=1 DO  QUIT  ;;EXECUTE VOR SPEICHERN Q:FORMULAR FERTIG ;EXECUTE pre- Save ready-made 
	. . IF $$$WWWUSERHTMLStarted(^WWWUSER(0,YUSER,1))="" DO    ;TYBD;ALTE WERTE ANZEIGEN BEI FEHLER ;display next to shortcoming 
	. . . SET %("VAR","YKEY")=$GET(YKEY)  ;KEY
	. . . SET ^WWWUSE(0,YUSER,YFORM,"D",1)=YFELD
	. . . SET YKILL=2 
	. . . IF $GET(YAEND)=1 SET YKILL=3
	. . . SET %("VAR","YKILL")=YKILL
	. . . DO ^WWWFORM
	. ;
	. IF '$DATA(^WWWSOR(YUSER,1))#10=1 IF $$EXIST^%R("Y"_YFORM_"onBeforeSave.OBJ",$GET(YUCI)) DO ^WWWFORM5 XECUTE "DO ^Y"_YFORM_"onBeforeSave" IF $GET(Q)=1 DO  QUIT  ;CUSTOMIZING EXECUTE VOR SPEICHERN Q:FORMULAR FERTIG;FIS;11.03.04;25316
	. . IF $$$WWWUSERHTMLStarted(^WWWUSER(0,YUSER,1))="" DO    ;TYBD;ALTE WERTE ANZEIGEN BEI FEHLER ;display next to shortcoming 
	. . . SET %("VAR","YKEY")=$GET(YKEY)                       ;KEY
	. . . SET ^WWWUSE(0,YUSER,YFORM,"D",1)=YFELD
	. . . SET YKILL=2 
	. . . IF $GET(YAEND)=1 SET YKILL=3
	. . . SET %("VAR","YKILL")=YKILL
	. . . DO ^WWWFORM
	
	set blnStop = ($GET(Q)=1)
	quit blnStop
	
	
Wizard()
	;-------------------------------------------------------------------------------
	; Wizard form type
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 08-Aug-2006	JW		SR13594: Created (encapsulated)
	;-------------------------------------------------------------------------------
	NEW YI,YBBN
	
	DO
	. IF $GET(YOPTION)="" SET YOPTION=$GET(%(YQUERY,"YOPTION"))
	. SET $PIECE(YOPTION,"#",10)=$PIECE($GET(YOPTION),"#",10)+1
	. FOR YI=1:1:9  IF $GET(YKEY(YI))'=""     SET $PIECE(YOPTION,"#",YI)=$GET(YKEY(YI))   SET $PIECE(YOPTION,"#",10)=""  ;PRIMÄRSCHLÜSSEL SICHERN ;safeguard 
	. FOR YI=1:1:70 IF $PIECE(YFELD,Y,YI)'="" SET $PIECE(YPARA,"#",YI)=$PIECE(YFELD,Y,YI) SET $PIECE(YOPTION,"#",10)=YI  ;DATENFELDER UND MERKER FÜR LFD FELD ;And to field 
	. IF $GET(YOPTION)'="" SET %("VAR","YOPTION")=YOPTION
	. IF $GET(YPARA)'=""   SET %("VAR","YPARA")  =YPARA
	. ;
	. SET YBBN=$PIECE($GET(YOPTION),"#",10)
	. SET YBBN=$ORDER(^WWW122s(0,5,$$^WWWUMLAU(YFORM,1),YBBN))
	. IF YBBN'="" DO ^WWWFORM QUIT       ;WEITERER FELDER
	. SET YVOR=$GET(^WWW120(0,YFORM,1))  ;PRUEFEN EXEC]]><![CDATA[UTE
	. KILL %("VAR","YPARA")
	. KILL %("VAR","YOPTION")
	. KILL %(YQUERY,"YPARA") 
	. KILL %(YQUERY,"YOPTION")
	. IF $PIECE(YVOR,Y,59)'="" DO 
	. . IF $EXTRACT($PIECE(YVOR,Y,59))="#" SET $PIECE(YVOR,Y,59)=$EXTRACT($PIECE(YVOR,Y,59),2,99)
	. . XECUTE $PIECE(YVOR,Y,59)  ;EXE NACH DATENFELD ;within data item 
	. ;
	. IF $PIECE(YVOR,Y,16)'="" DO 
	. . IF $EXTRACT($PIECE(YVOR,Y,16))="#" SET $PIECE(YVOR,Y,16)=$EXTRACT($PIECE(YVOR,Y,16),2,99)
	. . XECUTE $PIECE(YVOR,Y,16)  ;EXE NACH DATENFELD ;within data item 
	
	QUIT
	
	
BufferSave()
	;-------------------------------------------------------------------------------
	; Fast Save ?
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 08-Aug-2006	JW		SR13594: Created (encapsulated)
	;-------------------------------------------------------------------------------
	do
	. SET YMAXKEY = 0
	. IF YDATEI'="" SET YMAXKEY = +$ORDER(^WWW002(0,YDATEI,""),-1)
	. SET YKEY = ""
	. FOR YI=1:1:YMAXKEY SET YKEY = YKEY_$GET(YKEY(YI)) IF YI'=YMAXKEY SET YKEY=YKEY_","
	. SET FFFELD = $TRANSLATE(YFELD,$CHAR(13,10),"|")
	. SET FMFELD = $TRANSLATE(YMFELD,$CHAR(13,10),"|")
	. SET FFKEY  = YKEY
	. SET FFFORM = YFORM
	. DO ^WWWFORM2  ;SPEICHERN ;Save 
	. IF $EXTRACT(YOPEN,5,8)="HELP" SET %("VAR","EP")="WWWHELP"      DO ^WWWHELP  QUIT  ;HILFE ;help 
	. IF $FIND(YOPEN,"^") SET YEXEC=$PIECE(YOPEN,"#",2) IF YEXEC'="" XECUTE YEXEC QUIT  ;SONDER EXECUTE
	. IF $EXTRACT(YOPEN,5,8)="SEAR" DO ^WWWSEAR                                   QUIT  ;SUCHEN ;search 
	. IF $EXTRACT(YOPEN,5,8)="FORM" DO  QUIT
	. . NEW YFORM1
	. . SET YFORM1=YFORM
	. . SET YFORM=$EXTRACT(YOPEN,9,99)
	. . SET %("VAR","YFORM")=$PIECE(YFORM,"|",1)
	. . IF $PIECE(YFORM,"|",1)'="" IF +$PIECE($GET(^WWW121(0,$PIECE(YFORM,"|",1),1,1)),Y,16)'=0 SET %("VAR","YKEY")=YKEY  ;WENN VORGABE BENÖTIGT ;when default 
	. . ;SET YMAP=$PIECE(YFORM,"|",2)
	. . SET YMAP=$$^WWWTRANSLATE($PIECE(YFORM,"|",2),"&quot;","""")  ;FIS;13.01.04;ANFÜHRUNGSZEICHEN ÜBERGEBEN
	. . IF YMAP'="" DO
	. . . DO
	. . . . IF $EXTRACT($PIECE(YMAP,"=",1))="""" SET $PIECE(YKEY,",",+$PIECE(YMAP,"=",2)) = $TRANSLATE($PIECE(YMAP,"=",1),"""")                  QUIT  ;FIS;13.01.04;FESTER WERT ;worthy 
	. . . . IF $EXTRACT($PIECE(YMAP,"=",1))="P"  SET $PIECE(YKEY,",",+$PIECE(YMAP,"=",2)) = $PIECE(FFKEY,",",+$EXTRACT($PIECE(YMAP,"=",1),2,99)) QUIT  ;FIS;27.09.04;26416;AUS PRIMÄRSCHLÜSSEL
	. . . . SET $PIECE(YKEY,",",+$PIECE(YMAP,"=",2)) = $PIECE(FFFELD,Y,+$PIECE(YMAP,"=",1))  ;AUS DATENFELD ;out of data item 
	. . . ;
	. . . SET %("VAR","YKEY") = YKEY
	. . . SET ^WWWUSE(0,YUSER,YFORM1,"D",1) = FFFELD
	. . . SET ^WWWUSE(0,YUSER,YFORM1,"M",1) = FMFELD
	. . . SET ^WWWUSE(0,YUSER,YFORM1,"P",1) = FFKEY
	. . ;
	. . DO ^WWWFORM
	. ;
	. IF $EXTRACT(YOPEN,5,8)="EXEC" SET YEXEC=$EXTRACT(YOPEN,9,99) SET %("VAR","YEXEC")=YEXEC SET:$TRANSLATE($GET(YKEY),"+")'="" %("VAR","YKEY")=YKEY DO ^WWWMANU
	. IF $EXTRACT(YOPEN,5,8)="MANU" SET YEXEC=$EXTRACT(YOPEN,9,99) SET %("VAR","YEXEC")=YEXEC SET:$TRANSLATE($GET(YKEY),"+")'="" %("VAR","YKEY")=YKEY DO ^WWWMANU1
	
	quit
	
	
GetInputType(pidClass,pintField,pidForm)
    ;-------------------------------------------------------------------------------
    ; Determines the InputType, including customisation if YFORM is defined
    ; 
    ; Called By: Validate^WWWSAVED, START^WWWSAVE (2), ^WWWSAVV, START^WWWSAVE
    ; 
    ; Returns:
    ;
    ; History:
    ; 09-Dec-2008	GRF		SR16123: InputType need not be "int".
    ; 06-Nov-2008	shobby 	SR16123
    ;-------------------------------------------------------------------------------
	new enumInputType
	
	set enumInputType = $$$WWW003InputType($GET(^WWW003(0,pidClass,pintField,1)))
	if (pidForm'="") && ($order(^WWW122s(0,4,pintField,pidForm,""))'="") {
		set enumInputType = $$$WWW122InputType($$Get^WWW122(pidForm,$order(^WWW122s(0,4,pintField,pidForm,""))))
	}
	quit enumInputType
	
]]></Routine>
</Export>