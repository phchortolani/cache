<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW003RF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW003RF
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN RELATIONSFELDER NACH DATEIEINGABE
	;	Suchen und anzeigen relationsfelder mit automatischer anzeige 2 felder weiter
	;	Search and Display by means of ulterior 
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 29-Jun-2009	PPP		SR16663: Add the Calculated Relation Display Fields
	; 13-Feb-2008	GRF		Doco; quits
	; 02.03.2001	DT		Created
	;-------------------------------------------------------------------------------
	NEW YLFNX,YI
	;SET YFELD=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1)) ;DATENSATZ
	new intFld,intDataField	

	SET %TXT(1)=""
	IF YINHALT="" DO  QUIT
	. FOR YI=1:1:40 DO
	. . SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_(YLFN+2)_"~"_YI_"~"_" "_"~"_YI
	
	IF $DATA(^WWW003(0,YINHALT)) DO
	. SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_(YLFN+2)_"~"_""_"~"_" "_"~"_0
	. SET YLFNX=""
	. FOR YI=1:1:40 SET YLFNX=$ORDER(^WWW003(0,YINHALT,YLFNX)) QUIT:YLFNX=""  DO
	. . SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_(YLFN+2)_"~"_YLFNX_"~"_YLFNX_" - "			//Field
	. . IF $PIECE($GET(^WWW0031(0,YINHALT,YLFNX,SPRACHE,1)),Y,1)'="" SET %TXT(1)=%TXT(1)_$PIECE($GET(^WWW0031(0,YINHALT,YLFNX,SPRACHE,1)),Y,1)_"~"_YI QUIT	//Description in Language
	. . SET %TXT(1)=%TXT(1)_$PIECE($GET(^WWW003(0,YINHALT,YLFNX,1)),Y,2)_"~"_YI			//Description
	. ;
	. FOR YI=YI:1:40 DO
	. . SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_(YLFN+2)_"~"_""_"~"_" "_"~"_YI
	. ;
	. IF $DATA(^WWW001(0,YINHALT)) DO  ;FIS;24693;14.04.04;NICHT ALLE RELATIONEN ANZEIGEN BEI > 40 DATENSÄTZE
	. . IF +$PIECE($GET(^WWW001(0,YINHALT,1)),Y,26)>40 DO  ;ERWARTETE ANZAHL DATENSÄTZE ;Number 
	. . . IF YFORM="WWW003" SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_(YLFN+12)_"~"_1
	. . . IF YFORM="WWW122" SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_(YLFN+3)_"~"_1

	//Calculated Fields	//SR16663
	if $data(^WWW003Calc(0,YINHALT)) do
	. set intDataField = "D37"
	. if YFORM="WWW122" set intDataField="D133"
	. set intFld=""
	. for YI=1:1:40 set intFld=$order(^WWW003Calc(0,YINHALT,intFld)) quit:intFld=""  do
	. . set %TXT(1)=%TXT(1)_"#Y"_YFORM_intDataField_"~"_intFld_"~"_intFld_" - "				//Field
	. . if $piece($get(^WWW003CalcLang(0,YINHALT,intFld,SPRACHE,1)),Y,1)'="" set %TXT(1)=%TXT(1)_$piece($get(^WWW0031(0,YINHALT,intFld,SPRACHE,1)),Y,1)_"~"_YI QUIT	//Description in Language
	. . set %TXT(1)=%TXT(1)_$piece($get(^WWW003Calc(0,YINHALT,intFld,1)),Y,1)_"~"_YI	//Description
	. for YI=YI:1:40 do
	. . set %TXT(1)=%TXT(1)_"#Y"_YFORM_intDataField_"~"_""_"~"_" "_"~"_YI
	//

	QUIT
	
CHECK
	;-------------------------------------------------------------------------------
	;		NICHT ALLE RELATIONEN ANZEIGEN BEI > 40 DATENSÄTZE
	;
	;	If the Data Relation Type has been set to 0 ("Display All Relations");
	;	 - Get the relation class.
	;	 - Check if there are expected to be more than 40 records.
	;	 - If so, display an alert:
	;	 	"Note! The number of expected data records for this Relation is: ###"
	;
	; 	D26		$$$WWW001NumberExpectedDataset
	; 	D32		$$$WWW122RelationClass
	; 	D8		$$$WWW003RelationDatabase
	; 
	; Called by : Execute On Blur : Form WWW122 F46/D35 Data Relation Type
	; 
	; History:
	; 14-Apr-2004	FIS		24693
	;-------------------------------------------------------------------------------
	NEW YDATEI
	
	QUIT:$GET(YINHALT)=""
	QUIT:$GET(YINHALT)'=0
	SET YDATEI=""
	IF YFORM="WWW122" SET YDATEI=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1)),Y,32) ;RELATIONSDATEI
	IF YFORM="WWW003" SET YDATEI=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1)),Y,8)  ;RELATIONSDATEI
	IF YDATEI'="" IF $DATA(^WWW001(0,YDATEI)) DO  ;FIS;24693;NICHT ALLE RELATIONEN ANZEIGEN BEI > 40 DATENSÄTZE ;Not display next to 
	. IF $PIECE($GET(^WWW001(0,YDATEI,1)),Y,26)>40 DO  ;ERWARTETE ANZAHL DATENSÄTZE ;Number 
	. . SET %TXT(1)="&"_$$^WWWTEXT(33837,,1)_" "_$PIECE($GET(^WWW001(0,YDATEI,1)),Y,26)  ;ACHTUNG ;estimation 
	
	QUIT
]]></Routine>
</Export>