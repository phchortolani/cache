<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARBancoPrecoEstimativaProp" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARBancoPrecoEstimativaProp
#include COMSYS
#include COMConst
#include VARConst

OnAfterDataFields(pYKEY,pYFELD,pYSEITE)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo OnAfterDataFields do form VARBancoPrecoEstimativaPropCad.
    ; 
    ; Histórico:
    ; 	10-Ago-2010		Criado
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	if ($get(YSEITE) = 1) {		//Aba Principal
		do LoadGrid(pYKEY)
		do PrintTableParticipantes(pYKEY)
	}
	
	quit
	
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo OnBeforeSave do form VARBancoPrecoEstimativaProp.mac.
    ; 
    ; Histórico:
    ; 	10-Ago-2010		Criado
    ;-------------------------------------------------------------------------------		
	new strStatus
	set strStatus = $$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
	
LoadGrid(pid="")
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARBancoPrecoEstimativaProp.mac.
    ; 
    ; Histórico:
    ; 	06-Ago-2010		Criado
    ;-------------------------------------------------------------------------------	
	new YFORM, YFELD, YKEY, YAUSWAHL
	
	set YFORM = "VARBancoPrecoEstimativaProp"
	set YFELD = ""
	set YKEY = $get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)	= 1
	set $$$COMGridEditParameterGridName(YAUSWAHL)	= YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)	= '($$$WWW120AuthorizationToModifyData(YVOR)=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)	= "VARBancoPrecoEstimativaPropCad"

	$$$GRIDStart(YFORM,YKEY)
	
	quit
	
OnBlurPrecoUnitario(pYINHALT,pYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (3)-Preço Unitário do form VARBancoPrecoEstimativaProp.
    ; 
    ; Histórico:
    ; 	11-Ago-2010		Criado
    ;-------------------------------------------------------------------------------		
	new precoUnitario, quantidade, precoTotal
	
	if (pYINHALT = "") {
		set $piece(YFELD,Y,4) = ""
		quit
		
	} elseif (pYINHALT = 0) {
		set $piece(YFELD,Y,3) = ""
		set $piece(YFELD,Y,4) = ""
		$$$Alert("O preço unitário não pode ser zero.")
		quit
		
	} elseif (pYINHALT < 0) {
		set $piece(YFELD,Y,3) = ""
		set $piece(YFELD,Y,4) = ""
		$$$Alert("O preço unitário não pode ser negativo.")
		quit
	
	} else {		
		set quantidade = $piece(YFELD,Y,2)		
		if (quantidade <= 0) {
			$$$Alert("Quantidade deve ser maior que zero.")
			set $piece(YFELD,Y,4) = ""
		}
		
		set precoUnitario = pYINHALT
		set precoTotal = precoUnitario * quantidade
		
		set $piece(YFELD,Y,4) = precoTotal	
	}


	quit
	
PrintTableParticipantes(pYKEY)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARBancoPrecoEstimativaProp.mac.
    ; 
    ; Histórico:
    ; 	10-Ago-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new idEstimativa, idEstimativaLinha, idFornecedor, lstHeader, objFornecedor, idParticipante, nomeFantasia, 
		cnpjcpf, email, telefone, fax
		
	set idEstimativa      = $$$KEY1(pYKEY)
	set idEstimativaLinha = $$$KEY2(pYKEY)
	
	set lstHeader = ""
 	set lstHeader = lstHeader_$listbuild("Seq.","Cód.","Fornecedor","CNPJ/CPF","Email","Telefone 1","Fax")

	set count = 0

	if $$Start^COMTable(lstHeader,"Fornecedores participantes da estimativa de preços",,,$$$NO) {
	
		$$$Order3(^VARBancoPrecoEstimativaForn,YM,idEstimativa,idParticipante)
			
			set idFornecedor = $piece($get(^VARBancoPrecoEstimativaForn(YM,idEstimativa,idParticipante,1)),Y,1)
			
			set count = $i(count)
			
	    	set objFornecedor = $get(^INLIEF(YM,idFornecedor,1))
	    	set nomeFantasia  = $piece(objFornecedor,Y,8)
	    	set cnpjcpf       = $piece(objFornecedor,Y,201)
	      	set email         = $piece(objFornecedor,Y,24)
	      	set telefone      = $piece(objFornecedor,Y,208)
	      	set fax       	  = $piece(objFornecedor,Y,22)		

			set callBack = "AdicionarFornecedor^VARBancoPrecoEstimativaProp"
			set param    = pYKEY_$$$COMMA_idFornecedor
			
			if '$data(^VARBancoPrecoEstimativaProps(YM,1,idFornecedor,idEstimativa,idEstimativaLinha)) {			
				do NewLine^COMTable()
      			do InsertCellCallback^COMTable(count,callBack,param)
      			do InsertCellCallback^COMTable(idFornecedor,callBack,param)
      			do InsertCellCallback^COMTable(nomeFantasia,callBack,param)
      			do InsertCellCallback^COMTable(cnpjcpf,callBack,param)
      			do InsertCellCallback^COMTable(email,callBack,param)
      			do InsertCellCallback^COMTable(telefone,callBack,param)
      			do InsertCellCallback^COMTable(fax,callBack,param)
      			do EndLine^COMTable()
			
			} else {
				do NewLine^COMTable()
      			do InsertCell^COMTable("<font color=#666666>"_count_"</font>")
      			do InsertCell^COMTable("<font color=#666666>"_idFornecedor_"</font>")
      			do InsertCell^COMTable("<font color=#666666>"_nomeFantasia_"</font>")
      			do InsertCell^COMTable("<font color=#666666>"_cnpjcpf_"</font>")
      			do InsertCell^COMTable("<font color=#666666>"_email_"</font>")
      			do InsertCell^COMTable("<font color=#666666>"_telefone_"</font>")
      			do InsertCell^COMTable("<font color=#666666>"_fax_"</font>")
      			do EndLine^COMTable()				
			}		
	
		$$$End
		
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum participante definido para a estimativa de preços.",,,,,,7)
			do EndLine^COMTable()
		}
		
		do Stop^COMTable()
	}

	quit
	
AdicionarFornecedor(pidEstimativa,pidEstimativaLinha,pidFornecedor)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função PrintTableParticipantes^VARBancoPrecoEstimativaProp.mac.
    ; 
    ; Histórico:
    ; 	29-Set-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit:( (pidEstimativa = "") || (pidEstimativaLinha = "") || (pidFornecedor = "") )
	
	new lastLine, nextLine, key, obj, keyHeader, strStatus	
	set strStatus = $$$OK
	
	if '$data(^VARBancoPrecoEstimativaProps(YM,1,pidFornecedor,pidEstimativa,pidEstimativaLinha)) {
		
		set lastLine = ""
		set lastLine = $Order(^VARBancoPrecoEstimativaProp(YM,pidEstimativa,pidEstimativaLinha,lastLine),-1)
		set nextLine = $i(lastLine)
		
		set key = pidEstimativa_$$$COMMA_pidEstimativaLinha_$$$COMMA_nextLine		
		
		set obj = ""		
		set $piece(obj,Y,1) = pidFornecedor
		set $piece(obj,Y,6) = $horolog
		
		set strStatus = $$$Save("VARBancoPrecoEstimativaProp",key,obj,1)
			
	} else {
		$$$Alert("Este fornecedor já foi adicionado.")		
	}
	
	set keyHeader = pidEstimativa_$$$COMMA_pidEstimativaLinha	
	do GoToForm^COMUtilForm("VARBancoPrecoEstimativaPropCad",keyHeader)
		
	quit
	
	
getQuantidadeLinha(pYKEY)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo "Default value" do campo Quantidade no form VARBancoPrecoEstimativaProp.
    ; 
    ; Histórico:
    ; 	10-Ago-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "") ""
	
	new idEstimativa, idEstimativaLinha, quantidade
	
	set idEstimativa = $$$KEY1(pYKEY)
	set idEstimativaLinha = $$$KEY2(pYKEY)
	
	set quantidade = $piece($get(^VARBancoPrecoEstimativaLinha(YM,idEstimativa,idEstimativaLinha,1)),Y,2)
	quit quantidade
	
	
GetPrecoUltimaCompra(pYKEY)
	;-------------------------------------------------------------------------------
    ; Pega o preço da última compra com a mesma função do alerta, só que remove o
    ; R$ (no alerta é uma string que vem com ele)
    ; 
    ; History:
    ; 13-Jul-2011	Marcelo Ottoni		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "") ""	
	new idEstimativa, idLinha, idProduto, fltPrecoUltimaCompra
	
	set idEstimativa = $$$KEY1(pYKEY)
	set idLinha      = $$$KEY2(pYKEY)	
	set idProduto    = $piece($get(^VARBancoPrecoEstimativaLinha(YM,idEstimativa,idLinha,1)),Y,1)
	
	set fltPrecoUltimaCompra = $$getPrecoUltimaCompra^VARCompra(idProduto)	
	quit fltPrecoUltimaCompra
]]></Routine>
</Export>