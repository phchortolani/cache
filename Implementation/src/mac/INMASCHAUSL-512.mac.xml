<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INMASCHAUSL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INMASCHAUSL
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		SPEICHERUNG DER MASCHINENAUSLASTUNG
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
    ;| 02-Sep-2005	JW		SR12966: TERMIN9 is shared 
	;| ULM	20.12.02
	;\------------------------------------------------------------------/
	NEW YII,AUF,AUF1,AUFART,BEGINN,LFN,MASCH,POS,POS1,SUCH,SUCH1
	
	SET ARBZEIT=$PIECE($GET(^INVORG(YM,YM,1)),Y,16)  ;ARBEITSZEIT AUS MANDANTENVORGABE ;working hours out of 
	IF ARBZEIT="" SET ARBZEIT=8  ;STUNDEN ;procrastinate 
	SET ARBZEIT=ARBZEIT*3600  ;IN SEKUNDEN ;within 
	SET ARBTAGE=$PIECE($GET(^INVORG(YM,YM,1)),Y,17)  ;ARBEITSTAGE AUS MANDANTENVORGABE ;out of 
	IF ARBTAGE="" SET ARBTAGE=5
	
	KILL ^INMASCHAUSL(YM)
	
	SET AUF=""
	FOR  SET AUF=$ORDER(^INAUF1SEL(YM,AUF),-1) QUIT:AUF=""  DO   ;OFFENE FERTIGUNGSAUFTRÄGE IN UMGEDREHTER FOLGE ;within sequel 
	. SET AUF1=$GET(^INAUF(YM,AUF,1))
	. ;QUIT:$PIECE(AUF1,Y,4)>(+$H-365)  ;AUFTRAGSDATUM -1JAHR
	. ;
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . QUIT:$PIECE(POS1,Y,7)'=3  ;NUR FÜR WARENHERKUNFT EIGENFERTIGUNG ;only to 
	. . SET SUCH="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS_")"
	. . ;
	. . ;TEILE
	. . FOR  DO  QUIT:SUCH=""  ;
	. . . SET SUCH=$QUERY(@SUCH)
	. . . IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. . . ;IF $PIECE(SUCH,",",2)'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; 
	. . . IF $PIECE(SUCH,",",3)'=POS SET SUCH="" QUIT
	. . . IF $PIECE($PIECE(SUCH,")",1),",",3)=POS DO
	. . . . IF $DATA(@SUCH)'=1 QUIT
	. . . . SET SUCH1=@SUCH
	. . . . IF $PIECE(SUCH1,Y,4)'="" IF $PIECE($GET(^INART(YM,$PIECE(SUCH1,Y,4),1)),Y,26)'=2 QUIT  ;NUR LEISTUNGEN ;only LEISTUNGEN 
	. . . . QUIT:$PIECE(SUCH1,Y,57)=""
	. . . . ;
	. . . . SET BEGINN=$PIECE(SUCH1,Y,168)   ;PRODUKTIONSBEGINN
	. . . . SET BEGINN=$PIECE(BEGINN,".",1)   ;WENN KALENDERWOCHE ;when calendar week 
	. . . . QUIT:BEGINN=""
	. . . . ;IF BEGINN<(TAG-21) QUIT  ;SCHON VORBEI (2 WOCHEN RÜCKWIRKEND, FALLS LÄNGERE AUSFÜHRUNGSDAUER BIS IN DIESE WOCHE REICHT)
	. . . . ;IF BEGINN>(TAG+6) QUIT  ;NOCH NICHT
	. . . . SET MASCH=$PIECE(SUCH1,Y,57)  ;MASCHINE ;machine 
	. . . . QUIT:MASCH=""
	. . . . ;
	. . . . SET SUCH(1)=$PIECE($PIECE(SUCH,")",1),",",4,99)
	. . . . SET SUCH(0)=$LENGTH(SUCH(1),",")  ;ANZAHL DER ,
	. . . . SET SUCH(2)=""
	. . . . FOR YYI=1:1:SUCH(0) SET SUCH(2)=SUCH(2)_$PIECE(SUCH(1),",",YYI)_"."
	. . . . IF SUCH(2)="" SET SUCH(2)="."  ;FÜR DIE HAUPTPOSITION ;to who 
	. . . . ;
	. . . . SET DAUER=$$^INPROSTEMP0($PIECE(SUCH1,Y,165),3)  ;DAUER JE STEMPELTAG ;permanence once 
	. . . . ;
	. . . . ;SAVE GEPLANTE FERTIGUNGSZEITEN
	. . . . IF (+$PIECE(SUCH1,Y,165)=0) || (+DAUER=0) || ($PIECE(SUCH1,Y,168)<$HOROLOG) DO  QUIT
	. . . . . ;
	. . . . . NEW ME,ZEIT,DAT
	. . . . . SET ME=$PIECE(SUCH1,Y,40)
	. . . . . IF (ME=2)!(ME=10)!(ME=11)!(ME="") SET DAUER=$PIECE(SUCH1,Y,45)*$PIECE(SUCH1,Y,39)  ;DAUER DER TÄTIGKEIT * PRUDUKTIONSMENGE ;permanence the activity 
	. . . . . ;
	. . . . . ;WRITE $PIECE($GET(^INMASCH(YM,MASCH,1)),Y,1)_"<BR>" QUIT
	. . . . . IF $PIECE(SUCH1,Y,166)'="" SET DAUER=$PIECE(SUCH1,Y,166)  ;ERRECHNETE DAUER DER Anweisung ;permanence the 
	. . . . . SET DAT=BEGINN-1
	. . . . . ;WRITE YM_" "_MASCH_" "_DAT_" "_AUF_" "_POS_" "_SUCH(2)_SUCH_ZEIT QUIT
	. . . . . IF +DAUER=0 QUIT  ;SET ^INMASCHAUSL(YM,MASCH,DAT,AUF,POS,SUCH(2),1)=SUCH_Y_ZEIT  ; QUIT   ;SAVE
	. . . . . FOR  QUIT:DAUER'>0  DO
	. . . . . . SET DAT=DAT+1  ;1 TAG SPÄTER ;TAG subsequent 
	. . . . . . QUIT:$DATA(^TERMIN9(0,SPRACHE,DAT))
	. . . . . . QUIT:$$^WWWDAY(DAT)>ARBTAGE
	. . . . . . SET ZEIT=DAUER
	. . . . . . IF DAUER>ARBZEIT SET ZEIT=ARBZEIT
	. . . . . . SET DAUER=DAUER-ZEIT
	. . . . . . ;IF MASCH=""!(DAT="")!(AUF="")!(POS="")!(SUCH="")!(ZEIT="") QUIT
	. . . . . . ;SET ^WWWSOR(YUSER,MASCH,DAT,AUF,POS,SUCH(2))=SUCH_Y_ZEIT   ;SAVE (SUCH IST DER KEY AUS INAUFPXL)
	. . . . . . SET ^INMASCHAUSL(YM,MASCH,DAT,AUF,POS,SUCH(2),1)=SUCH_Y_ZEIT  ;
	. . . . ;
	. . . . ;SAVE STEMPELZEITEN LT. BDE ;factory data capture 
	. . . . FOR YI=1:1  QUIT:$PIECE(DAUER,Y,YI,999)=""  DO
	. . . . . NEW DAT,ZEIT
	. . . . . SET DAT=+$PIECE($PIECE(DAUER,Y,YI),"#",1)  ;STEMPELTAG
	. . . . . SET ZEIT=+$PIECE($PIECE(DAUER,Y,YI),"#",2)  ;STEMPELZEIT
	. . . . . IF +DAT'=+BEGINN QUIT:+ZEIT=0  ;KEINE WERTE NUR AM FERTIGUNGSTAG ANZEIGEN ;no only to the display 
	. . . . . IF +DAT=0 SET DAT=BEGINN
	. . . . . SET ^INMASCHAUSL(YM,MASCH,DAT,AUF,POS,SUCH(2),1)=SUCH_Y_ZEIT  ;   ;SAVE (SUCH IST DER KEY AUS INAUFPXL) ;the KEY out of 
	
	QUIT
	
]]></Routine>
</Export>