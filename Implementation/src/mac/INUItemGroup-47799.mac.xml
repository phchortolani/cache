<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INUItemGroup" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INUItemGroup
#include COMSYS
	
OnAfterDataFields(pidGroup,pintPage)
	;-------------------------------------------------------------------------------
	; Display the group tree after data fields
	;
	; Params:	pidGroup	- INItemGroup key
	; 			pintPage	- current tab
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 23-Nov-2006	JW		BR014285: Created
	;-------------------------------------------------------------------------------
	if (pintPage=1) && '$$$NoKey(pidGroup) {
		do DisplayTree(pidGroup)
	}
	
	quit
	
	
DisplayTree(pidGroup)
	;-------------------------------------------------------------------------------
	; Display the hierarchy of a node in tree form
	;
	; Params:	pidGroup	- INItemGroup key
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 04-Dec-2006	JW		BR014285: Created
	;-------------------------------------------------------------------------------
	new idNode,strDesc,blnHighlight,idParent
	
	do GetNodes^INBItemGroup(pidGroup)		// Get the nodes to display
	do StartTree^COMTree(YPARA)
	
	set idNode = ""
	for {
		set idNode = $order(^CacheTempItemGroup(YUCI,YUSER,idNode))
		quit:idNode=""
		
		set idParent     = $get(^CacheTempItemGroup(YUCI,YUSER,idNode))
		set blnHighlight = (idNode=pidGroup)
		set strDesc      = $$^WWWFORMAT("INItemGroup","D",1,idNode)
		
		do AddNode^COMTree(idParent,idNode,$$GetCode^INBItemGroup(idNode),strDesc,,"INItemGroup",,blnHighlight,,blnHighlight,,,$$$YES)
	}
	do StopTree^COMTree($$$YES,,$$$YES)
	kill ^CacheTempItemGroup(YUCI,YUSER)
	
	quit
	
	
OnBeforeDelete(pidGroup)
	;-------------------------------------------------------------------------------
	; Check whether can delete
	;
	; Params:	pidGroup - INItemGroup key
	;
	; ByRefs:	Q
	;
	; Returns: boolean
	;
	; History:
	; 23-Nov-2006	JW		BR014285: Created
	;-------------------------------------------------------------------------------
	new blnInUse
	
	set blnInUse = $$InUse^INBItemGroup(pidGroup)
	if blnInUse {
		set Q = $$$QDontDelete_$listbuild("IN00587",pidGroup)  ; "This record (%1) is in use. It cannot be deleted."
	}
	
	quit 'blnInUse
	
	
OnBeforeConstruction(pidGroup,pblnActive,&pintHid)
	;-------------------------------------------------------------------------------
	; Disable the active checkbox if it is in use.
	; 
	; Params:	pidGroup 	- key
	; 			pblnActive 	- whether active
	; 			pintHid		- YHID
	;
	; Returns:	
	;
	; History:
	; 12-Jan-2007	JW		SR15309: Created
	;-------------------------------------------------------------------------------
	if (pidGroup'="") && pblnActive {
		if $$InUse^INBItemGroup(pidGroup) set pintHid=2		// Disable checkbox
	}
	
	quit
	
]]></Routine>
</Export>