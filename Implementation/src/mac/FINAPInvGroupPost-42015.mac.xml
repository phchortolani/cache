<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPInvGroupPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPInvGroupPost
	#Include FIN
	#Include FINConst
	#Include COMConst
	#Include COMSYS

GroupPostSearch(pidGroup="",pdteInvoiceFromDate="",pdteInvoiceToDate="",pdteTransFromDate="",pdteTransToDate="",pidSupplier="")
    ;-------------------------------------------------------------------------------
    ; Searches for invoices to be used in a Group Post which match the criteria
    ; specified.
    ;
    ; Returns: nil
    ;
    ; History:
    ; 21-Mar-2005	JW		Created (modified from FINARInvGroupPost)
    ;-------------------------------------------------------------------------------
	new lstInvoices,idInvoice,idGroup,dteInvoiceDate,dteTransDate,idInvoiceSupplier
	new objInvoice,objInvoiceGroupPostLine
	new fltTotal,fltSubTotal,fltTax,strStatus
	
	set lstInvoices=""
	set strStatus=$$$OK
	
	kill ^FINInvGroupPostLine(YM,YBED)
	
	set idInvoice=""
	set idGroup=$$$Index(pidGroup)
	for {
		// Loop through repeat invoices in specified group with no parent
		set idInvoice=$order(^FINAPInvs(YM,4,idGroup," ",idInvoice))
		quit:idInvoice=""
		
		if $$Postable^FINAPInvPost(idInvoice) {
			set objInvoice=$get(^FINAPInv(YM,idInvoice,1))
			set dteInvoiceDate=$$$FINAPInvInvoiceDate(objInvoice)
			set dteTransDate=$$$FINAPInvEnteredDate(objInvoice)
			set idInvoiceSupplier=$$$FINAPInvInvoiceSupplier(objInvoice)
			
			if ((pidSupplier="") || (idInvoiceSupplier=pidSupplier))
				&& $$WithinDateRange^COMUtilDate(dteInvoiceDate,pdteInvoiceFromDate,pdteInvoiceToDate)
				&& $$WithinDateRange^COMUtilDate(dteTransDate,pdteTransFromDate,pdteTransToDate) {
				
				set fltTotal=$$SumTotals^FINAPVoucher(idInvoice,.fltSubTotal,,.fltTax)
				
				set $$$FINInvGroupPostLineSupplier(objInvoiceGroupPostLine)=idInvoiceSupplier
				set $$$FINInvGroupPostLineInvoiceDate(objInvoiceGroupPostLine)=dteInvoiceDate
				set $$$FINInvGroupPostLineTransactionDate(objInvoiceGroupPostLine)=dteTransDate
				set $$$FINInvGroupPostLineSubTotal(objInvoiceGroupPostLine)=fltSubTotal
				set $$$FINInvGroupPostLineTax(objInvoiceGroupPostLine)=fltTax
				set $$$FINInvGroupPostLineTotal(objInvoiceGroupPostLine)=fltTotal
				set $$$FINInvGroupPostLinePost(objInvoiceGroupPostLine)=$$$YES
				set strStatus=$$$Save("FINInvGroupPostLine",YBED_","_idInvoice,objInvoiceGroupPostLine,1)
			}
		}
		quit:$$$ISERR(strStatus)
	}
	
	if $$$ISOK(strStatus) && '$data(^FINInvGroupPostLine(YM,YBED)) {
		set strStatus=$lb("Fin00573")  ;No invoices found.
	}	
	
	quit strStatus
	

	
	
	
]]></Routine>
</Export>