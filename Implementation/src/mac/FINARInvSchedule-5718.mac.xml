<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvSchedule" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvSchedule
	;--------------------------------------------------------------------------------------
	; Routines relating to schedules with customer invoices
	;--------------------------------------------------------------------------------------
	#Include FIN
	#Include FINConst
	#Include COMConst
	#Include COMSYS
	#Include WWWConst
 
HasSchedules(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, does it have any schedules?
    ;
    ; Returns:
    ;
    ; History:
    ; 05-Apr-2005		Steve S			Moved from FINARInv
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
	new idInvoice,lstSchedules
	
	set idInvoice=$get(pidInvoice)
	set lstSchedules=$$GetSchedulesList(idInvoice)
	
	quit ($listlength(lstSchedules)'=0)
	
	
KillSchedules(pidInvoice) //NOT IN USE
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, kills all associated schedules.
    ;
    ; Returns:
    ;
    ; History:
    ; 05-Apr-2005		Steve S			Moved from FINARInv
    ; 18-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
	new lstSchedules,idx,idSchedule
	
	if $get(pidInvoice)'="" {
		set lstSchedules=$$GetSchedulesList(pidInvoice)	
		for idx=1:1:$listlength(lstSchedules) {
			set idSchedule=$listget(lstSchedules,idx)
			if (idSchedule'="") {
				do KILL^COMUtils("COMSchedule",idSchedule)
			}
		}
	}
	
	quit
	
	
GetSchedulesList(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, builds a list of all associated schedules.
    ;
    ; Returns: a list of schedules
    ;
    ; History:
    ; 05-Apr-2005		Steve S			Moved from FINARInv
    ; 18-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
	new lstSchedules,idSchedule,strIdInvoice,idClass
	
	set lstSchedules=""
	set idSchedule=""
	set idClass="FINARINV"
	
	if $get(pidInvoice)'="" {
		set strIdInvoice=$$$Index(pidInvoice)
		for {
			set idSchedule=$order(^COMSchedules(YM,1,idClass,strIdInvoice,idSchedule))
			quit:idSchedule=""
			set lstSchedules=lstSchedules_$lb(idSchedule)
		}
	}
	
	quit lstSchedules
]]></Routine>
</Export>