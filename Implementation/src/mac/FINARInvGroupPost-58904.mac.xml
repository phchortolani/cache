<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvGroupPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvGroupPost
	;--------------------------------------------------------------------------------------
	; SR: 11837 - Created as a replacement for FINARRecurringInvoiceGroupPost.mac
	;--------------------------------------------------------------------------------------
	#Include FIN
	#Include FINConst
	#Include COMConst
	#Include COMSYS

GroupPostSearch(pidGroup,pdteInvoiceFromDate,pdteInvoiceToDate,pdteTransFromDate,pdteTransToDate,pidCustomer)
    ;-------------------------------------------------------------------------------
    ; Searches for invoices to be used in a Group Post which match the criteria
    ; specified.
    ;
    ; Returns: status
    ;
    ; History:
    ; 21-Mar-2005	JW			SR11405: Moved common functionality to FINInvGroupSearch
    ; 17-Jan-2005	Steve S		Created
    ;-------------------------------------------------------------------------------
	new idGroup,dteInvoiceFromDate,dteInvoiceToDate,lstInvoices,idInvoice
	new objInvoice,dteInvoiceDate,dteTransFromDate,dteTransToDate,idCustomer
	new dteTransDate,idInvoiceCustomer,blnCustomerOkay,strStatus
	
	// SR11405
	;set strStatus=$$ValidateSearchInput(pidGroup,pdteInvoiceFromDate,pdteInvoiceToDate,pdteTransFromDate,pdteTransToDate,pidCustomer)
	
	;if strStatus=$$$OK {
	set idGroup=$get(pidGroup)
	set dteInvoiceFromDate=$get(pdteInvoiceFromDate)
	set dteInvoiceToDate=$get(pdteInvoiceToDate)
	set dteTransFromDate=$get(pdteTransFromDate)
	set dteTransToDate=$get(pdteTransToDate)
	set idCustomer=$get(pidCustomer)
	
	set lstInvoices=""
	
	set idInvoice=""
	set idGroup=$$$Index(idGroup)
	for {
		set idInvoice=$order(^FINARInvs(YM,2,idGroup,idInvoice))
		quit:idInvoice=""
		
		if $$Postable^FINARInvPost(idInvoice,$$$YES) {
			set objInvoice=$get(^FINARInv(YM,idInvoice,1))
			set dteInvoiceDate=$$$FINARInvInvoiceDate(objInvoice)
			set dteTransDate=$$$FINARInvTransactionDate(objInvoice)
			set idInvoiceCustomer=$$$FINARInvCustomer(objInvoice)
			
			if idCustomer="" { 
				set blnCustomerOkay=$$$YES
			} else {
				set blnCustomerOkay=(idInvoiceCustomer=idCustomer)
			}
			
			if $$WithinDateRange^COMUtilDate(dteInvoiceDate,dteInvoiceFromDate,dteInvoiceToDate)
			   &&$$WithinDateRange^COMUtilDate(dteTransDate,dteTransFromDate,dteTransToDate)
			   &&(blnCustomerOkay) {
				set lstInvoices=lstInvoices_$lb(idInvoice)
			}	
		}
	}
	
	if lstInvoices="" {
		set strStatus=$lb("Fin00573")  ;No invoices found.
	} else {
		set strStatus=$$ShowResults(lstInvoices)
	}
	/*
	if strStatus'=$$$OK {
		do ReturnError^COMUtils(strStatus)
	} else {
		do ShowResults(lstInvoices)	
	}	
	*/
	quit strStatus
	

	// Currently not in use.
ValidateSearchInput(pidGroup,pdteInvoiceFromDate,pdteInvoiceToDate,pdteTransFromDate,pdteTransToDate,pidCustomer)
    ;-------------------------------------------------------------------------------
    ; Validates search input before looking for invoices to group post.
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Mar-2005	JW			SR11405: No longer in use.
    ; 17-Jan-2005	Steve S		Created
    ;-------------------------------------------------------------------------------
	new strStatus

	set strStatus=$$$OK
	
	if $get(pidGroup)="" set strStatus=$lb("Fin00571")  ;Must specify a Repeat Group.
	
	if (strStatus=$$$OK)&&(pdteInvoiceFromDate'="")&&(pdteInvoiceToDate'="") {
		if (pdteInvoiceFromDate>pdteInvoiceToDate) {
			set strStatus=$lb("Fin00572") ;From Invoice Date must not be after the To Invoice Date.
		}
	}
	
	if (strStatus=$$$OK)&&(pdteTransFromDate'="")&&(pdteTransToDate'="") {
		if (pdteTransFromDate>pdteTransToDate) {
			set strStatus=$lb("Fin00627")  ;From Transaction Date must not be after the To Transaction Date.		
		}	
	}
	
	if pidCustomer'="" {
		if (strStatus=$$$OK)&&('$data(^FINARCustomer(YM,pidCustomer,1))) {
			set strStatus=$lb("Fin00079",pidCustomer)  ;Customer number %1 not Found	
		}
	}
	
	quit strStatus
	
	
ShowResults(plstInvoices)
    ;-------------------------------------------------------------------------------
    ; Show list of invoices found.
    ;
    ; Returns: status
    ;
    ; History:
    ; 21-Mar-2005	JW			SR11405: Moved common functionality to FINInvGroupSearch
    ; 17-Jan-2005	Steve S		Created
    ;-------------------------------------------------------------------------------
	new idx,objInvoiceGroupPost,idInvoice,objInvoice
	new idCustomer,dteInvoiceDate,fltSubTotal,fltTax,fltTotal
	new intNumberOfInvoices,objInvoiceGroupPostLine
	new status,dteTransDate
	
	set status=$$$OK
	
	set intNumberOfInvoices=$ll(plstInvoices)
	
	kill ^FINInvGroupPostLine(YM,YBED)
	
	for idx=1:1:intNumberOfInvoices {
		quit:status'=$$$OK
		set objInvoiceGroupPostLine=""
		set idInvoice=$lg(plstInvoices,idx)
		set objInvoice=$get(^FINARInv(YM,idInvoice,1))
		
		; Get Invoice Data
		set idCustomer=$$$FINARInvCustomer(objInvoice)
		set dteInvoiceDate=$$$FINARInvInvoiceDate(objInvoice)
		set dteTransDate=$$$FINARInvTransactionDate(objInvoice)
		set fltSubTotal=$$GetSubTotal^FINARInv(idInvoice)
		set fltTax=$$$FINARInvTax(objInvoice)
		set fltTotal=fltSubTotal+fltTax
		
		; Now store it
		set $$$FINInvGroupPostLineCustomer(objInvoiceGroupPostLine)=idCustomer
		set $$$FINInvGroupPostLineInvoiceDate(objInvoiceGroupPostLine)=dteInvoiceDate
		set $$$FINInvGroupPostLineTransactionDate(objInvoiceGroupPostLine)=dteTransDate
		set $$$FINInvGroupPostLineSubTotal(objInvoiceGroupPostLine)=fltSubTotal
		set $$$FINInvGroupPostLineTax(objInvoiceGroupPostLine)=fltTax
		set $$$FINInvGroupPostLineTotal(objInvoiceGroupPostLine)=fltTotal
		set $$$FINInvGroupPostLinePost(objInvoiceGroupPostLine)=$$$YES
		set status=$$$Save("FINInvGroupPostLine",YBED_","_idInvoice,objInvoiceGroupPostLine,1)
	}
	
	/*
	if status=$$$OK {
		do RedirectForm^COMUtils("FINInvGroupPost",YBED,YFORM_",",YPARA,YSEITE)
	} else {
		do ^WWWINFO($$$Text(status))
	}
	*/
	
	quit status
	
	
 // 21-Mar-2005	JW			SR11405: Moved following functions to FINInvGroupPost
GetNumInvoices(pstrUserID)
GetSubTotalInvoices(pstrUserID)	
GetTaxInvoices(pstrUserID)
GetGrandTotalInvoices(pstrUserID)
AfterDataFields(pstrYKEY)
LoadGrid(pYKEY)
BeforeSave(pYKEY)
CanPostGroup()
PostGroup()
ShowGroupResults()
GetNumInvoicesSelected()
OnBlurPost()
OnBeforeButtonLine(pstrYKEY)
CheckForRepeatWarning(pstrYKEY) // 31-Mar-2005
	quit ""
	
	
]]></Routine>
</Export>