<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINInvGroupPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINInvGroupPost
	
#include COMSYS
#include COMConst
#include FINConst
	
#define IsAR(%type)	($case($get(%type),"AR":$$$YES,"AP":$$$NO,:""))
	
OnBeforeButtonLine(pYPARA="",pYKEY)
	;-------------------------------------------------------------------------------
	; Code to execute before button line.
	;
	; Returns:
	;
	; History:
	; 21-Mar-2005	JW		Save invoice type.
	; 24-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new strWarning
	
	set YOPTION = pYPARA	; Save Invoice type - "AP" or "AR"
	
	if $get(pYKEY)'="" {
		
		set strWarning = $$PostWarnings(pYKEY,pYPARA)
		// Set acknowledgment text on the post button
		set $piece(^WWW124(0,"FINInvGroupPost",SPRACHE,1,1),Y,17)=strWarning
	}
	quit
	
	
GroupPostSearch(pType,pidGroup,pdteInvFromDate="",pdteInvToDate="",pdteTransFromDate="",pdteTransToDate="",pidCustomer="",pidSupplier="")
	;           AR/AP  VORG(1)     VORG(2)          VORG(3)            VORG(4)          VORG(5)       VORG(6)    VORG(7)
	;-------------------------------------------------------------------------------
	; Searches for invoices to be used in a Group Post which match the criteria
	; specified.
	;
	; Returns: lstInvoices (a list of invoice id's to post).
	;
	; History:
	; 14-Jul-2011	GRF		-:Macros, cleanup
	; 21-Mar-2005	JW		Created
	;-------------------------------------------------------------------------------
	new strStatus,blnAR
	
	set blnAR = $$$IsAR(pType)
	quit:blnAR=""
	
	set strStatus = $$$OK
	
	if $get(pidGroup)="" {
		set strStatus = $listbuild("Fin00571")   ; "Must specify a Repeat Group."
		
	} elseif (pdteInvToDate'="") && (pdteInvFromDate>pdteInvToDate) {
		set strStatus = $listbuild("Fin00572")   ; "From Invoice Date must not be after the To Invoice Date."
	
	} elseif (pdteTransToDate'="") && (pdteTransFromDate>pdteTransToDate) {
		set strStatus = $listbuild("Fin00627")   ; "From Transaction Date must not be after the To Transaction Date."	
	
	} elseif blnAR {
		if (pidCustomer'="") && '$data(^FINARCustomer(YM,pidCustomer,1)) {
			set strStatus = $listbuild("Fin00079",pidCustomer)       ; "Customer number %1 not Found"
		}
	} else {
		if (pidSupplier'="") && '$data(^FINAPSupplier(YM,pidSupplier,1)) {
			set strStatus = $listbuild("Fin00164",pidSupplier)       ; "Supplier number %1 not Found"
		}
	}
	
	if $$$ISOK(strStatus) {
		if blnAR {
			set strStatus = $$GroupPostSearch^FINARInvGroupPost(pidGroup,pdteInvFromDate,pdteInvToDate,pdteTransFromDate,pdteTransToDate,pidCustomer)
		} else {
			set strStatus = $$GroupPostSearch^FINAPInvGroupPost(pidGroup,pdteInvFromDate,pdteInvToDate,pdteTransFromDate,pdteTransToDate,pidSupplier)
		}
		
		if $$$ISOK(strStatus) { // Save header
			set strStatus = $$$Save("FINInvGroupPost",YBED,"",$$$YES)
		}
	}
	
	if $$$ISOK(strStatus) {
		do RedirectForm^COMUtils("FINInvGroupPost",YBED,YFORM_",",YPARA,YSEITE)
	} else {
		do ReturnError^COMUtils(strStatus)
	}
	quit strStatus
	
	
OnAfterPrimaryKey(pYPARA)
	;-------------------------------------------------------------------------------
	; Write header after primary key
	;
	; Returns: nil
	;
	; History:
	; 27-May-2005	GRF		SR12481 : Macro corrected to actually use parameter
	; 21-Mar-2005	JW		Created
	;-------------------------------------------------------------------------------
	new str
	
	if $$$IsAR(pYPARA) {
		set str = $$$StrFINARInvCustomer
	} else {
		set str = $$$StrFINAPInvSupplier
	} 
	write "<b>&nbsp;"_str_" "_$$$Text("Fin00706")  ;Invoices
	quit
	
	
AfterDataFields(pstrYKEY)
	;-------------------------------------------------------------------------------
	; Method to run after data fields
	;
	; Returns:
	;
	; History:
	; 17-Jan-2005	SteveS	Created (SR10424)
	;-------------------------------------------------------------------------------
	do LoadGrid($get(pstrYKEY))
	quit
	
	
LoadGrid(pYKEY)
	;-------------------------------------------------------------------------------
	; Code to display Edit Grid
	;
	; Returns:
	;
	; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
	; 08-Feb-2005	JW		Was being handled by onblur.
	; 17-Jan-2005	Steve S	Created
	;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
	
 	set YFORM = "FINInvGroupPostLine"
  	set YFELD = ""
  	set YKEY  = $get(pYKEY)
  	if YKEY="+" set YKEY = ""
	
  	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
  	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 500
  	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
  	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
  	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "FINInvGroupPost"
  	do ^COMGridEdit31
  	
	quit
	
	
BeforeSave(pYKEY)
	;-------------------------------------------------------------------------------
	; Save Grid Data
	;
	; Returns:
	;
	; History:
	; 17-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus=$$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtils(strStatus)
	}
	quit
	
	
GetNumInvoices(pstrUserID)
	;-------------------------------------------------------------------------------
	; Returns the number of invoices found in the group search.
	;
	; Returns:
	;
	; History:
	; 17-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new idUser,intInvoiceCount,idInvoice
 
	set idUser = $get(pstrUserID)
	set intInvoiceCount = 0
	
	if (idUser'="") {
		set idInvoice = ""
		for {
			set idInvoice = $order(^FINInvGroupPostLine(YM,idUser,idInvoice))
			quit:idInvoice=""
			
			set intInvoiceCount = intInvoiceCount+1	
		}
	}
	quit intInvoiceCount
	
	
GetSubTotalInvoices(pstrUserID)
	;-------------------------------------------------------------------------------
	; Returns the sub total of invoices selected.
	;
	; Returns:
	;
	; History:
	; 20-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new idUser,fltSubTotal,idInvoice,objPostLine
 
	set idUser = $get(pstrUserID)
	set fltSubTotal = 0
	
	if (idUser'="") {
		set idInvoice = ""
		for {
			set idInvoice = $order(^FINInvGroupPostLine(YM,idUser,idInvoice))
			quit:idInvoice=""
			
			set objPostLine = $get(^FINInvGroupPostLine(YM,idUser,idInvoice,1))
			if $$$FINInvGroupPostLinePost(objPostLine) {
				set fltSubTotal = fltSubTotal+$$$FINInvGroupPostLineSubTotal(objPostLine)
			}
		}
	}
	quit fltSubTotal
	
	
GetTaxInvoices(pstrUserID)
	;-------------------------------------------------------------------------------
	; Returns total tax of the invoices selected.
	;
	; Returns:
	;
	; History:
	; 20-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new idUser,fltTax,idInvoice,objPostLine
	
	set idUser = $get(pstrUserID)
	set fltTax = 0
	
	if (idUser'="") {
		set idInvoice = ""
		for {
			set idInvoice = $order(^FINInvGroupPostLine(YM,idUser,idInvoice))
			quit:idInvoice=""
			
			set objPostLine = $get(^FINInvGroupPostLine(YM,idUser,idInvoice,1))
			if $$$FINInvGroupPostLinePost(objPostLine) {
				set fltTax = fltTax+$$$FINInvGroupPostLineTax(objPostLine)
			}
		}
	}
	quit fltTax
	
	
GetGrandTotalInvoices(pstrUserID)
	;-------------------------------------------------------------------------------
	; Return grand total of invoices selected (subtotal + tax).
	;
	; Returns:
	;
	; History:
	; 20-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new idUser
	
	set idUser = $get(pstrUserID)
	quit ($$GetSubTotalInvoices(idUser) + $$GetTaxInvoices(idUser))
	
	
CanPostGroup()
	;-------------------------------------------------------------------------------
	; Determines whether or not the Group Post button is enabled.
	;
	; Returns:
	;
	; History:
	; 17-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	if ($$GetNumInvoicesSelected()>0) {
		set YQ=0	
	} else {
		set YQ="1 "_$$$Text($listbuild("Fin00576")) ; "No invoices selected to post."
	}
	quit
	
	
PostGroupTxn(pType,plstPostList)
	;-------------------------------------------------------------------------------
	; Transaction protected 'PostGroup' code.
	;
	; Returns:
	;
	; History:
	; 05-May-2005	shobby		Created SR11875
	;-------------------------------------------------------------------------------
    new idInvoice,intNumberToPost,loop,strStatus
    
	set intNumberToPost = $listlength(plstPostList)
	
	for loop=1:1:intNumberToPost {
		set idInvoice = $listget(plstPostList,loop)
		
		if $$$IsAR(pType) {
			set strStatus = $$Post^FINARInvPost(idInvoice,$$$NO,$$$NO,$$$YES)
		} else {
			set strStatus = $$Post^FINAPInvPost(idInvoice,$$$YES)
		}
		quit:$$$ISERR(strStatus)
	}
    quit strStatus
	
	
PostGroup(pType="",pblnRedirect=$$$YES)
	;-------------------------------------------------------------------------------
	; Attempts to post a group of invoices to the interface.
	;
	; Called By :	Form FINInvGroupPost : Button 1 "Post Selected"
	;
	; Returns:
	;
	; History:
	; 27-May-2005	GRF		SR12481 : Don't call PostGroupTxn if no entries in the
	; 							list; pass pType to ShowGroupResults
	; 05-May-2005	shobby	SR11875 : Managed transactions to handle rollback after
	; 							cache errors.
	; 21-Mar-2005	JW		SR11405 : Now includes AP
	; 27-Jan-2005	RobertW	Fixed TCOMMIT and TROLLBACK (SR11273)
	; 17-Jan-2005	Steve S	Created
	;-------------------------------------------------------------------------------
	new idUser,idInvoice,lstPostList,status
	new objInvoicePost,objInvoicePostHeader,blnShowResults
	
	set status		= $$$OK
	set idUser		= $get(YBED)
	set lstPostList = ""
	
	set objInvoicePostHeader = $get(^FINInvGroupPost(YM,idUser,1))
	set blnShowResults		 = $$$FINInvGroupPostDisplaypostresults(objInvoicePostHeader)
 
	set idInvoice = ""
	for {
		set idInvoice = $order(^FINInvGroupPostLine(YM,idUser,idInvoice))
		quit:idInvoice=""
		
		set objInvoicePost=$get(^FINInvGroupPostLine(YM,idUser,idInvoice,1))
		
		if $$$FINInvGroupPostLinePost(objInvoicePost) {
			set lstPostList=lstPostList_$lb(idInvoice)
		}
	}
	
	kill ^CacheTempRecInvoiceChildren(YUSER)
	
	if lstPostList'="" {
		set status=$$Transaction^COMTransaction("PostGroupTxn^FINInvGroupPost("""_pType_""","""_lstPostList_""")",blnShowResults)
	}
	
	if status=$$$OK {
		if pblnRedirect {
			do ^WWWINFO($$$Text($listbuild("Fin00593")))  ; "Group post successful."
			if blnShowResults do ShowGroupResults(pType)
		}
	} 	
	kill ^CacheTempRecInvoiceChildren(YUSER)
	quit status
	
	
ShowGroupResults(pType)
	;-------------------------------------------------------------------------------
	; Display all the posted parent invoices and any children as well.
	; 
	; Results are stored in ^CacheTempRecInvoiceChildren(YUSER,<parent>)=<child>
	; 
	; NOTE: If an invoice is not repeating (ie, PARENT = PARENT), then we have
	; ^CacheTempRecInvoiceChildren(YUSER,<parent>)=<parent>
	; 
	; Returns:
	;
	; History:
	; 21-Mar-2005	JW		SR11405: Now includes AP
	; 21-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new lstHeader,strHeading,idInvoiceParent,idInvoiceChild,strForm
	
	if $$$IsAR(pType) {
		set strForm = "FINARInv"
	} else {
		set strForm = "FINAPVoucher"
	}
	
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild($$$Text("Fin00594"))   ; "Parent Invoice"
	set lstHeader = lstHeader_$listbuild($$$Text("Fin00595"))   ; "Child Invoice"
	set strHeading = $$$Text("Fin00597")                        ; "Results"
 
	do Start^COMTable(lstHeader,strHeading)
	
	set idInvoiceParent = ""
	for {
		set idInvoiceParent = $order(^CacheTempRecInvoiceChildren(YUSER,idInvoiceParent))
		quit:idInvoiceParent=""
		
		set idInvoiceChild = $get(^CacheTempRecInvoiceChildren(YUSER,idInvoiceParent))
		
		do NewLine^COMTable()
		do InsertCell^COMTable(idInvoiceParent,strForm,idInvoiceParent)   ; PARENT CELL
		do InsertCell^COMTable(idInvoiceChild,strForm,idInvoiceChild)     ; CHILD CELL
		do EndLine^COMTable()                                             ; END THE ROW
	}
	do Stop^COMTable()
	quit
	
	
GetNumInvoicesSelected()
	;-------------------------------------------------------------------------------
	; Returns the number of invoices selected for posting.
	;
	; Returns:
	;
	; History:
	; 17-Jan-2005	Steve S		Created
	;-------------------------------------------------------------------------------
	new idUser,intInvoiceCount,idInvoice
 
	set idUser			=$get(YBED)
	set intInvoiceCount =0
	set idInvoice		=""
	
	if (idUser'="") {
		for {
			set idInvoice=$order(^FINInvGroupPostLine(YM,idUser,idInvoice))
			quit:idInvoice=""
			
			if $$$FINInvGroupPostLinePost($get(^FINInvGroupPostLine(YM,idUser,idInvoice,1))) {
				set intInvoiceCount=intInvoiceCount+1
			}	
		}
	}
	quit intInvoiceCount
	
	
OnBlurPost()
	;-------------------------------------------------------------------------------
	; Code to execute on blur of the Post Yes/No field in group post selection.
	;
	; Returns:
	;
	; History:
	; 10-Apr-2010	GRF		SR17177: arrGrid rather than objGrid; GRID Macros
	; 20-Jan-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new arrGrid,intNumberToPost,idUser,idInvoice,objPostLine,fltTotal
	new fltTax,fltSubTotal
	
	set idUser			= $get(YBED)
	set intNumberToPost = 0
	set fltSubTotal		= 0
	set fltTax			= 0
	
	$$$GRIDGetContents(.arrGrid)
	
	set idInvoice = ""
	for {
		set idInvoice = $order(arrGrid(idUser,idInvoice))
		quit:idInvoice=""
		
		set objPostLine = $get(arrGrid(idUser,idInvoice))
		
		if $$$FINInvGroupPostLinePost(objPostLine) {
			set fltSubTotal		= fltSubTotal+$$$FINInvGroupPostLineSubTotal(objPostLine)
			set fltTax			= fltTax+$$$FINInvGroupPostLineTax(objPostLine)
			set intNumberToPost = intNumberToPost+1	
		}
	}
 
	set fltTotal = fltSubTotal+fltTax
	do UpdateHeaderField^COMGridEdit31G("M2",intNumberToPost)
	do UpdateHeaderField^COMGridEdit31G("M3",fltSubTotal)
	do UpdateHeaderField^COMGridEdit31G("M4",fltTax)
	do UpdateHeaderField^COMGridEdit31G("M5",fltTotal)
	quit
	
	
PostWarnings(pidUser,pType)
	;-------------------------------------------------------------------------------
	; Return warnings for the invoices that already have children posted today, or theire
	; tax total is different to the sum of their line taxes.
	;
	; Returns: warning
	;
	; History:
	; 31-Mar-2005	JW		Moved from FINAPInvGroupPost
	; 22-Mar-2005	JW		Created (copied from CheckForRepeatWarning^FINARInvGroupPost)
	;-------------------------------------------------------------------------------
	new idInvoice,objInvoice,strMessage,strChildren,strTaxDiff,blnChildren,blnTaxDiff,blnAR
	
	set strChildren =""
	set strTaxDiff  =""
	set strMessage  =""
	
	set blnAR = $$$IsAR(pType)
	
	if $get(pidUser)'="" {
		set idInvoice=""
		for {
			set idInvoice=$order(^FINInvGroupPostLine(YM,pidUser,idInvoice))
			quit:idInvoice=""
		
			set objInvoice=$get(^FINInvGroupPostLine(YM,pidUser,idInvoice,1))
		
			if $$$FINInvGroupPostLinePost(objInvoice) {
				if blnAR {
					do PostWarnings^FINARInv(idInvoice,.blnChildren,.blnTaxDiff)
				} else {
					do PostWarnings^FINAPVoucher(idInvoice,.blnChildren,.blnTaxDiff)
				}
				if blnChildren set strChildren=strChildren_","_idInvoice
				if blnTaxDiff set strTaxDiff=strTaxDiff_","_idInvoice
			}
		}
		
		if strChildren'="" {
			set $extract(strChildren) = "" ;remove first comma
			set strMessage=$$$Text($listbuild("Fin00602",strChildren))  ;Invoices: '%1' already have children posted today. Proceed with group post?
		}	
		if strTaxDiff'="" {
			set $extract(strTaxDiff) = "" ;remove first comma
			if strMessage'="" set strMessage = strMessage_"\n"_$$$Text("Fin00708")_": " ;Also
			set strMessage = strMessage_$$$Text($listbuild("Fin00709",strTaxDiff))  ;Invoices: `%1Â´ each have a difference between their tax total and the sum of their line taxes. Proceed with group post?
		}
	}
	quit strMessage
	
	
PostGroupSchedule(pidRepeatGroup="")
	;-------------------------------------------------------------------------------
	; Post a group of invoices via the scheduler
	;
	; Returns:
	;
	; History:
	; 07-Apr-2005	SteveS	Created SR:11669
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	if (pidRepeatGroup'="") {
		do GroupPostSearch^FINARInvGroupPost(pidRepeatGroup)
	;***************************************
		tstart
	;***************************************
		set strStatus = $$PostGroup("AR",$$$NO)
		
		if strStatus=$$$OK {
			do GroupPostSearch^FINAPInvGroupPost(pidRepeatGroup)
			set strStatus = $$PostGroup("AP",$$$NO)
		}
		
	;***************************************
		if strStatus=$$$OK {
			if $tlevel>0 tcommit
			set strStatus = $lb("Fin00593")  ; "Group post successful."
		} else {
			trollback	
		}
	;***************************************
	}
	quit $$$Text(strStatus)
	
]]></Routine>
</Export>