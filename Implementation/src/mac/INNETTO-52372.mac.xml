<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INNETTO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INNETTO(A,NN)      ;INNETTO;DT;ERRECHNEN NETTO EK AUS BASISPREIS;27.03.2000  ; Compiled March 1, 2005 11:41:03
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERRECHNEN NETTO EK AUS BASISPREIS
	;		Take the base value in D12 and return the discounted/surcharged/rebated value
	; 
	; Called By :
	;	INANG, INARTK, INARTKWHR, INAUF, INAUFANLAGE, INAUFDISC, INAUFPA, INAUFPAM44,
	;	INAUFPK, INAUFPKNEU, INAUFPOS, INAUFWH, INBMECSV1, INDRUCK6, INDRUCKXML6,
	;	INDRUCKXML6PRINT, INERECH1, INERECH1M44, INERECH1P1, INERECHFAST, INKALKEK,
	;	INNETTOBRUTTO, INPRIM, INSALESPRICE
	; 
	; Inputs :
	;	A (pobjSource)		Source record : objINAUFP, objINAUFPA, objINAUFPK, objINERECH1, +?
	;						FIXME : if A is objINARTK then NK is obtained from D25 - what happens if A is some other class? <GRF>
	;		A=~RAB1~RAB2~RAB3~RAB4~ZUSCHL/ABSCHL~BETRAG~%SKONTO~~~~BASISPREIS~NETTOPREIS~~~~~~~~~~~~Nachkommastellen
	;	NN					Procesing switch
	;		NN=0 NETTO/NETTO
	;		NN=1 OHNE BERECHNUNG VON SKONTO ;without 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	; 	NETTO		
	;
	;
	; History :
	; 28-Sep-2006	GRF		Doco
	; 04-Sep-2006	GRF		Doco
	; 27.03.2000	DT
	;-------------------------------------------------------------------------------
	new NETTO,YI,NK
	
	;+++++++++++++++++++++++++++++++++++++++  May vary in description between classes
	;  2 : Discount 1
	;  3 : Discount 2
	;  4 : Discount 3
	;  5 : Surcharge/Discount %
	;  6 : Surcharge/Discount Amount
	;  7 : DealerÂ´s Rebate %
	; 12 : Base Price
	; 25 : Number of Decimal Places
	;+++++++++++++++++++++++++++++++++++++++
	
	set NETTO = 0
	quit:'$data(A) NETTO
	quit:+$piece(A,Y,12)=0 NETTO  ;KEIN BASISPREIS ;no 
	
	set NETTO = $piece(A,Y,12)  ;NETTO=BRUTTO
	if $length($piece(NETTO,".",1))>9 set NETTO = 0     ; see FIXME below
	for YI=2,3,4 if +$piece(A,Y,YI)'=0 set:$piece(A,Y,YI)>100 $piece(A,Y,YI) = 100   set NETTO = NETTO-(NETTO/100*$piece(A,Y,YI))  ;RABATTE
	for YI=5     if +$piece(A,Y,YI)'=0 set:$piece(A,Y,YI)<-100 $piece(A,Y,YI) = -100 set NETTO = NETTO+(NETTO/100*$piece(A,Y,YI))  ;ZU/ABSCHLAG
	set NETTO=NETTO+$piece(A,Y,6)  ;ZUSCHLAG/ABSCHLAGSBETRAG
	if +$get(NN)=0 for YI=7 if +$piece(A,Y,YI)'=0 set NETTO = NETTO-(NETTO/100*$piece(A,Y,YI))   ;SKONTO NUR WENN MIT SKONTO ;only when by means of 
	
	; Number of decimal places
	set NK = $piece(A,Y,25)             ; $$$INARTKDecimalPlaces / $$$INAUFPKNumericDecimalPlace
	if NK="" set NK = 2
	if NK>4  set NK = 2
	if NK<2  set NK = 2
	; FIXME : forms are INERECH and INERECH1
	if $get(YFORM)="INERCH"  set NK = 2
	if $get(YFORM)="INERCH1" set NK = 2  ;;30,7,2004;TYBD;26189
	
	;  FIXME : May be inadequate for certain currencies - e.g. Vietnam   <GRF>
	if $length($piece(NETTO,".",1))>9 set NETTO = 0
	set NETTO = $justify(NETTO,0,NK)
	quit NETTO
]]></Routine>
</Export>