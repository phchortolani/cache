<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESTreatmentHCHist" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include INConst
#include VARConst
#include VARLog
 
VARSESTreatmentHCHist
	quit
 
GetInformacaoAfetada()
	quit $case($$$VARSESTreatmentHCHistLinha(YFELD), "":"", 0:"Tratamento",
		:"Linha")
 
GetCodigo()
	new strCodigo, objVARSESTreatmentHCLine
	set VORG(1)=$piece(YKEY,YKOMMA,1)
	set strCodigo = ""
	if ($$$VARSESTreatmentHCHistLinha(YFELD) = 0) {
		set strCodigo = "("_$$$VARSESTreatmentHCHistLinha(YFELD)_")&nbsp;"_
			"Tratamento"
	}
	elseif ($length($$$VARSESTreatmentHCHistLinha(YFELD)) > 0) &&
	   		($$$VARSESTreatmentHCHistLinha(YFELD) '= 0) &&
	   		($length(VORG(1)) > 0) {
		set objVARSESTreatmentHCLine = $get(^VARSESTreatmentHCLine(0, 
			VORG(1),
			$$$VARSESTreatmentHCHistLinha(YFELD), 1))
		set objINART = $get(^INART(0, $$$VARSESTreatmentHCLineCodigoMedicamento(objVARSESTreatmentHCLine), 1))
		set strCodigo = "("_$$$VARSESTreatmentHCLineCodigoMedicamento(objVARSESTreatmentHCLine)_")&nbsp;"_
			$$$INARTSearchName(objINART)
	}
 
	quit strCodigo
 
RegistrarAlteracao(pidTreatmentHC, pintAcao, pintLinha, pintDetalhe, pintQuantidade, pdatReferencia)
	quit $$RegistrarAlteracaoIdentificada($horolog, $get(YBED), pidTreatmentHC, pintAcao,
		pintLinha, pintDetalhe, pintQuantidade, pdatReferencia)
 
RegistrarAlteracaoIdentificada(pdatAlt, pstrUsuario, pidTreatmentHC, pintAcao, pintLinha,
	pintDetalhe, pintQuantidade, pdatReferencia)
	new objTreatmentHCHistAlter, idHistAlt, strStatus, strLog
 
	kill objTreatmentHCHistAlter
	set $$$VARSESTreatmentHCHistDataHora(objTreatmentHCHistAlter) = pdatAlt
	set $$$VARSESTreatmentHCHistUsuario(objTreatmentHCHistAlter) = pstrUsuario
	set $$$VARSESTreatmentHCHistAcao(objTreatmentHCHistAlter) = pintAcao
	set $$$VARSESTreatmentHCHistLinha(objTreatmentHCHistAlter) = pintLinha
	set $$$VARSESTreatmentHCHistDetalhe(objTreatmentHCHistAlter) = pintDetalhe
	set $$$VARSESTreatmentHCHistQuantidadeAlterada(objTreatmentHCHistAlter) = pintQuantidade
	set $$$VARSESTreatmentHCHistDataReferencia(objTreatmentHCHistAlter) = pdatReferencia
 
	set idHistAlt = $$^WWWNEXT1("VARSESTreatmentHCHist", pidTreatmentHC, 2)
	set strStatus = $$$Save("VARSESTreatmentHCHist",pidTreatmentHC_YKOMMA_idHistAlt,objTreatmentHCHistAlter,1)
 
	set strLog = "Data/hora='"_$zdatetime($horolog, 3, 1)_"', "_
		"usuário="_$get(YBED)_", "_
		"ação="_$$GetDescricaoAcao(pintAcao)_", "_
		"linha="_pintLinha_", "_
		"detalhe="_pintDetalhe_", "_
		"quantidade alterada="_pintQuantidade_", "_
		"status="_strStatus_", "_
		"data referencia='"_pdatReferencia_"'"
	$$$VARLOGINFO(strLog)
 
	quit strStatus
 
GetDescricaoAcao(pintAcao)
	if ('$data(^INPARA(YM, "VARSESHISTALTACAO", SPRACHE, pintAcao, 1))) {
		quit "Desconhecida"
	}
	
	quit $$$INPARAText(^INPARA(YM, "VARSESHISTALTACAO", SPRACHE, pintAcao, 1))
]]></Routine>
</Export>