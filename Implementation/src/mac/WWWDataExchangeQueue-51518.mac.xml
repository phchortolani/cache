<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWDataExchangeQueue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWDataExchangeQueue
	; Routines to display the current queue
	; 
 
#include COMSYS
 
OnAfterSave()
	;$$$StartScript()
	;write " document.WWW.YINSEITE.value=''; document.WWW.YSEITE.value='2'; SAVENOW(2);"
	;$$$EndScript()
	;do ChangeTabs^WWWFORM6(2,YSEITE)
	do SaveRecallRecord^WWWRECALL(YBED,YFORM,"M",$piece(YMFELD,Y,YSEITE))
	do ReloadForm^COMUtilForm()
	;do ^WWWFORM
	quit
	 
OnAfterDataFields()
	if YSEITE=1 { ; Queue
		do ShowProcessed()	; Show Errors		
	} elseif YSEITE=2 { ; Processed
		do ShowProcessed()	; Show Errors
	} elseif YSEITE=3 { ; Methods Run
		
	} elseif YSEITE=4 { ; Schedule
		do AfterDataField^COMSchedule(1,"COMDCMEventQueue","eventQueueManager^COMDCMControlModule()")
		}
	quit
	
ShowProcessed()
	;-------------------------------------------------------------------------------
	;
	; History:
	; 27-Jun-2005	GRF		SR12810 : Processed list increased from 10 to 25 lines
	; 18-Feb-2005	PO		SR11756 Added language text for Status of.
	;-------------------------------------------------------------------------------
	new Query,Title
	
	quit:VORG(1)=""
	quit:($order(^WWWDataExMessageSend(YM,VORG(1),""))="")		; Nothing to display
	
	set Title=$$$Text("Com00167")  ;Status of ; SR11756
	set Query("Name")=Title
	set Query("Class")="WWWDataExMessageSend"
	set Query("Form")="WWWDataExchangeQueue"
	;set Query("Link")="WWWDataExMessageData~P~1"
	set Query("Col",1)="P~1~L"
	set Query("Col",2)="D~4~L"
	set Query("Col",3)="D~1~L"
	set Query("Col",4)="D~2~L"
	set Query("Col",5)="D~3~L"
	set Query("Col",6)="D~6~L"
	set Query("Col",6,"DoInstead")="Format^WWWDataExchangeQueue"
	set Query("Loop")="ShowProcessedLoop^WWWDataExchangeQueue"
	;set Query("Get")="set id=$get(^WWWDataExMessageSend(YM,VORG(1),id(1),1))"
	set Query("Get")="set id=$get(^WWWDataExMessageSend(YM,VORG(1),id(1),1))"
	
	set Query("PageSize")=30
 
	write !,"<P><FONT SIZE=2><b>"_Title_"</b></P>"
	Do Query^COMQuery(.Query,.VORG)
 
	quit
	
ShowProcessedLoop(Query,Storage,VORG)
	new count,id,level,objWWWDataExMessageSend
 
	set count=0
 
	set id(1)=""
	for {
		set id(1)=$order(^WWWDataExMessageSend(YM,VORG(1),id(1)),-1)
		quit:id(1)=""
		set objWWWDataExMessageSend=$get(^WWWDataExMessageSend(YM,VORG(1),id(1),1))
		continue:(YSEITE=1)&&($$$WWWDataExMessageSendTimestampProcessed(objWWWDataExMessageSend)'="")
		continue:(YSEITE=2)&&($$$WWWDataExMessageSendTimestampProcessed(objWWWDataExMessageSend)="")
		set count=count+1
		merge @Storage@(count)=id
		
	}
	quit count
 
Format(pintCol,pstrData)
	;-------------------------------------------------------------------------------
	; Turns the status red if there is an error.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Dec-2008	shobby		SRBR014985: Created
	;-------------------------------------------------------------------------------
	if pstrData=0 set pstrData="<B><FONT color='red'>"_pstrData_"</FONT></B>"
	quit pstrData
	
ShowQueue()
	;-------------------------------------------------------------------------------
	; Show COMView for WWWDataExMessageSend
	; 
	; Called by : 
	;
	; History:
	; 21-Oct-2009	SCR		SR16929: Created
	;-------------------------------------------------------------------------------
	new idClass
 	set idClass = "WWWDataExMessageSend"
	do Initialise^COMViewCustom(idClass,,$$$NO,$$$NO,$$$YES) 
	
	
	do Show^COMViewCustom()
	
	quit $$$OK
	
PurgeDESend(pintDays=0)
    ;-------------------------------------------------------------------------------
	; Purge all data exchange send messages older than the specified number of days
	; 
	; History:
	; 11-Nov-2009	SCR	SR16929: Created	
    ;-------------------------------------------------------------------------------
	new intCount,idDEM,objDEM,dteDate
	 
	set intCount = 0
	set idDEM  = ""
	for {
		set idDEM = $order(^WWWDateExMessageSend(YM,idDEM))
		quit:idDEM=""
		
		set objDEM=$get(^WWWDateExMessageSend(YM,idDEM,1))
		if $$$WWWDataExMessageSendTimestampProcessed(idDEM)="" continue
		set dteDate = +$$$WWWDataExMessageSendTimestampCreated(idDEM)
		
		if ($horolog-dteDate) >= pintDays {
			do KILL^COMUtils("WWWDateExMessageSend",idDEM) ;kills header
			set intCount = intCount+1
		}
	}
	quit intCount_" items purged"

]]></Routine>
</Export>