<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRECShow" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRECShow
	; cf INRECTable - dynagrid for INReceipt/INRECSummary
	
#include COMSYS
#include INConst
	
	;Pre-Entered Incoming Goods (Not Transferred)                                                      <ShowReceipts>
	;---------------------------------------------------------------------------------------------------------------
	;Order  | Order Line |  Item  |   Site   |  Stock   |  Incoming  |  Goods   | Incoming  |  Incoming   | Receipt
	;Number |   Item     | Number | Location | Location | Goods Date | Recvd No | Goods Qty | Goods Flags |  Gate
	;---------------------------------------------------------------------------------------------------------------
	;       |            |        |          |          |            |          |           |             |
	;---------------------------------------------------------------------------------------------------------------
	; 	
	;Goods Already Received
	;---------------------------------------------------------------------------------------------------------------
	;Order  | Order Line |  Item  |   Site   |  Stock   |  Incoming  |  Goods   | Incoming  |  Incoming   | Receipt
	;Number |   Item     | Number | Location | Location | Goods Date | Recvd No | Goods Qty | Goods Flags |  Gate
	;---------------------------------------------------------------------------------------------------------------
	;       |            |        |          |          |            |          |           |             |
	;---------------------------------------------------------------------------------------------------------------
	
ShowReceipts(pidOrders="",pidxImport="")
	;-------------------------------------------------------------------------------
	; Display a list of receipts at various stages.
	;
	; History:
	; 23-Sep-2009	DWR		SR16361: Move Start ComTable Out of loop of records
 	; 10-Sep-2009	GRF		SR16361: Replaces ShowReceipts^INWEAUFVORAP to remove
 	; 							INWEAUF(VOR) processing
	;-------------------------------------------------------------------------------
	new arrRecLine,blnHeader,enumStatus,enumType
	new idGate,idKey,idLink,idOrder,idReceipt,idRecLine,idxOrdLine,idxOrdNo
	new loop,lstHeader,objLine,objReceipt,strPara,strText

	set idLink  = "INREC"
	set idOrder   = ""
	for loop=1:1:$length(pidOrders,",") {
		set idOrder = $piece(pidOrders,",",loop)
		quit:idOrder=""

		set idxOrdNo   = $$$Index(idOrder)
		set idxOrdLine = ""
		for {
			set idxOrdLine = $order(^INRECLines(0,4,idxOrdNo,idxOrdLine))
			quit:idxOrdLine=""
			
			set idReceipt = ""
			for {
				set idReceipt = $order(^INRECLines(0,4,idxOrdNo,idxOrdLine,idReceipt))
				quit:idReceipt=""
				
				set objReceipt = $get(^INREC(0,idReceipt,1))
				set enumType   = $$$INRECType(objReceipt)     ; 2 Supplier Receipt / 5 Supplier Reversal
				set enumStatus = +$$$INRECStatus(objReceipt)  ; 0 Unprocessed / 1 Processed
				
				set idRecLine = ""
				for {
					set idRecLine = $order(^INRECLines(0,4,idxOrdNo,idxOrdLine,idReceipt,idRecLine))
					quit:idRecLine=""
					
					set objLine = $get(^INRECLine(0,idReceipt,idRecLine,1))
					set arrRecLine(enumType,enumStatus,idReceipt,idRecLine) = objLine
				}
			}
		}
	}
	
	set lstHeader =""
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",1))  ; Order
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",2))  ; Line
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",7))  ; Item
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",10)) ; Location
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",11)) ; Storage
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",13)) ; Rec Date
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",,1)) ; Receipt Number    
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",64)) ; Unit Quantity   ; OrderQty?
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",65)) ; Received Unit   ; OrderUnit?
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INRECLine",20)) ; Flag
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("INREC",12))     ; Delivery Gate   

	for enumType = 2,5 {
		for enumStatus = 0,1 {
			if enumType = 2 {
				set strText = $$^WWWTEXT($select(enumStatus:33875,1:33950))
				; "Goods Already Received" / "Pre-Entered Incoming Goods (Not Transferred)"
			
			} else {
	; TODO : 4 headings Unprocessed Receipts / "Goods Already Received" / "Goods Already Received" awaiting Reversal / Reversed
				set strText = $$^WWWTEXT($select(enumStatus:33875,1:33950))
				; "Goods Already Received" / "Pre-Entered Incoming Goods (Not Transferred)"
			}
			if $data(arrRecLine(enumType,enumStatus)) {
				do Start^COMTable(lstHeader,strText,1)
			
				set idReceipt = ""
				for {
					set idReceipt = $order(arrRecLine(enumType,enumStatus,idReceipt))
					quit:idReceipt=""
				
					set idGate    = $$$INRECDeliveryGate($get(^INREC(YM,idReceipt,1)))
					set idRecLine = ""
					for {
						set idRecLine = $order(arrRecLine(enumType,enumStatus,idReceipt,idRecLine))
						quit:idRecLine=""
					
						set objLine = arrRecLine(enumType,enumStatus,idReceipt,idRecLine)
						set idKey   = idReceipt
						set strPara = idRecLine
					
						do NewLine^COMTable()
						do InsertCell^COMTable($$$INRECLineOrder1(objLine)         ,idLink,idKey,strPara)
						do InsertCell^COMTable($$$INRECLineOrderLine(objLine)      ,idLink,idKey,strPara)
						do InsertCell^COMTable($$$INRECLineItem(objLine)           ,idLink,idKey,strPara)
						do InsertCell^COMTable($$$INRECLineLocation(objLine)       ,idLink,idKey,strPara)
						do InsertCell^COMTable($$$INRECLineStockLocation(objLine)  ,idLink,idKey,strPara)
						do InsertCellDate^COMTable($$$INRECLineReceiptDate(objLine),idLink,idKey,strPara)
						do InsertCell^COMTable(idReceipt                           ,idLink,idKey,strPara)
						do InsertCell^COMTable($$$INRECLineUnitQuantity(objLine)   ,idLink,idKey,strPara)  ; ? INReceiptLine doesn't set 
						do InsertCell^COMTable($$$INRECLineReceivedUnit(objLine)   ,idLink,idKey,strPara)  ; ? make INRECLine constant between versions?
						do InsertCell^COMTable($$$INRECLineFlag(objLine)           ,idLink,idKey,strPara)
						do InsertCell^COMTable(idGate                              ,idLink,idKey,strPara)
					}
				}
				do Stop^COMTable()
			}
		}
	}
	quit
	
]]></Routine>
</Export>