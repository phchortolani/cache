<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPainelPDPASCompras" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESPainelPDPASCompras
#include COMSYS
#include VARSESConst
#include VARSESPainelAbastecimento
#include VARSESPainelPDPAS
 
PrintProdutosMaisComprados(pidUnidade,pGrupo,pDteInicial,pDteFinal,pQuantRegistros=30)
	new count, meuSQL, rsSQL, idProduto, descProduto, quantTotal, valorTotal, idGrupo, 
		descGrupo, bkgColor, totalGeral, UoM
	
	set:(pDteInicial '= "") pDteInicial = $zdateh(pDteInicial,4)
	set:(pDteFinal '= "") pDteFinal = $zdateh(pDteFinal,4)
	
	set count = 0
	set totalGeral = 0
 
	set meuSQL = "SELECT TOP "_pQuantRegistros_" Proposta.Produto as Codigo,"
	set meuSQL = meuSQL_" Proposta.Descricaodetalhada as Descricao,"
	set meuSQL = meuSQL_" SUM(Proposta.QuantidadeOfertada) as Quantidade, "
	set meuSQL = meuSQL_" Proposta.Unidade as UoM, "
	set meuSQL = meuSQL_" SUM(Proposta.PrecoTotal) as ValorTotal"
	set meuSQL = meuSQL_" FROM VARSESCOMPRASPropostaLinha Proposta"
	set meuSQL = meuSQL_" INNER JOIN VARSESCOMPRAS Compra on Compra.NoProcessoCompra = Proposta.NoProcessodeCompra"
	set meuSQL = meuSQL_" WHERE UPPER(Proposta.Resultado) = 1"
	set meuSQL = meuSQL_" AND Compra.Modalidade = '10'"	
	
	if (pidUnidade '= "") {
		set meuSQL = meuSQL_" AND $$getUnidadeSaudeLocal^VARSESWWW0121(Compra.LocaldeCompra) = "_pidUnidade
	}
	
	if (pDteInicial '= "") {
		set meuSQL = meuSQL_" AND $piece(Compra.CriadoEm,',',1) >= "_pDteInicial
	}
	
	if (pDteFinal '= "") {
		set meuSQL = meuSQL_" AND $piece(Compra.CriadoEm,',',1) <= "_pDteFinal
	}
	
	if (pGrupo '= "") {
		if (pGrupo = $$$Medicamento) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) = '"_$$$GrupoMedicamento_"'"
		
		} elseif ( (pGrupo = $$$MaterialMedico) || (pGrupo = $$$OPME) ) {
			
			if (pGrupo = $$$OPME) {
				set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,5) = '"_$$$GrupoOPME_"'"				
			} else {
				set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) = "_$$$GrupoMaterialMedico
				set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,5) != '"_$$$GrupoOPME_"'"
			}
		
		} elseif (pGrupo = $$$Almoxarifado) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) IN "_$$$GrupoAlmoxarifado
		
		} elseif (pGrupo = $$$Odontologico) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) = "_$$$GrupoOdontologico
		
		} elseif (pGrupo = $$$Laboratorio) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) = "_$$$GrupoLaboratorio
		
		} elseif (pGrupo = $$$Nutricao) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) = '"_$$$GrupoNutricao_"'"	
		
		} elseif (pGrupo = $$$Manutencao) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) IN "_$$$GrupoManutencao
		
		} elseif (pGrupo = $$$Informatica) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) = "_$$$GrupoInformatica
		
		} elseif (pGrupo = $$$Outros) {
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) != '"_$$$GrupoMedicamento_"'"
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) != "_$$$GrupoMaterialMedico
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) NOT IN "_$$$GrupoAlmoxarifado
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) != "_$$$GrupoOdontologico
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) != "_$$$GrupoLaboratorio
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) != '"_$$$GrupoNutricao_"'"
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) NOT IN "_$$$GrupoManutencao
			set meuSQL = meuSQL_" AND $extract($$SQLGetGrupoItem^VARSQL(Proposta.Produto),1,2) != "_$$$GrupoInformatica			
		}
	}
	
	set meuSQL = meuSQL_" GROUP BY Proposta.Produto"
	set meuSQL = meuSQL_" ORDER BY ValorTotal desc"
 	
	set rsSQL = ##class(%ResultSet).%New()  ;Create Result Set Object
	do rsSQL.Prepare(meuSQL) ;Prepare Query
	do rsSQL.Execute()  	 ;Execute Query
 
	write "<table cellspacing='1'>"
	write "	<tr style='background-color:#2c6c7f;color:white'>"
	write "		<th>#</th>"
	write " 	<th>Código</th>"
	write "		<th>Produto</th>"
	write "		<th>Grupo</th>"
	write "		<th>Quantidade</th>"
	write "		<th>UN</th>"
	write "		<th>Valor Total</th>"
	write "	</tr>"
 	
	while (rsSQL.Next()) {		
		set idProduto   = rsSQL.GetData(1)
		set descProduto = rsSQL.GetData(2)
		set quantTotal  = rsSQL.GetData(3)
		set UoM		    = rsSQL.GetData(4)
		set valorTotal  = rsSQL.GetData(5)
		
		set totalGeral = totalGeral + valorTotal
		
		set count = $increment(count)
 
	  	if (count#2=0) set bkgColor = "#F0F0F0"
	  	if (count#2=1) set bkgColor = "#E0E0E0"
		
		//Para poder usar a função WWWTR		
		set:($get(SPRACHE) = "") SPRACHE = "PT"
		set:($get(YBED) = "") YBED = "UNKNOWN"
		//
		
		set idGrupo 	   = $$SQLGetGrupoItem^VARSQL(idProduto)
		set idGrupoPDPAS   = $$GetGrupoPDPASFromID^VARSESPainelPDPASUtils(idGrupo)
			
		if (idGrupoPDPAS '= 99) { ;Outros
			set descGrupoPDPAS = $$GetDescGrupoPDPASFromID^VARSESPainelPDPASUtils(idGrupoPDPAS)
		} else {
			set descGrupoPDPAS = "Outros ("_$$SQLGetDescGrupoItem^VARSQL($extract(idGrupo,1,2))_")"
		}		
 
		write "	<tr style='background-color:"_bkgColor_"'>"
		write " <td>"_count_"</td>"
		write " <td><a href='VARSESPainelPDPASProduto.csp?fProduto="_idProduto_"&fLink=compras&fUnidade="_fUnidade_"&fGrupo="_fGrupo_"' class='link'>"_idProduto_"</td>"
		write " <td><a href='VARSESPainelPDPASProduto.csp?fProduto="_idProduto_"&fLink=compras&fUnidade="_fUnidade_"&fGrupo="_fGrupo_"' class='link'>"_descProduto_"</td>"		
		write " <td nowrap='nowrap'>"_descGrupoPDPAS_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,quantTotal)_"</td>"
		write " <td style='text-align:center'>"_$$SQLGetSiglaUnit^VARSQL(UoM)_"</td>"
		write " <td nowrap='nowrap' style='text-align:right'>"_$$FormatCurrency^COMTable(valorTotal)_"</td>"		
		write "</tr>"		
		
	}
	
	if (count = 0) {
		write "<tr style='background-color:white'>"
		write "	<td colspan='7'>Nenhum registro encontrado.</td>"
		write "</tr>"	
	} else {
		write "<tr style='background-color:lightsteelblue'>"
		write "	<td colspan='6' style='text-align:right'><strong>Total geral&nbsp;</strong></td>"
		write "	<td nowrap='nowrap' style='text-align:right'><strong>"_$$FormatCurrency^COMTable(totalGeral)_"</strong></td>"
		write "</tr>"
	}
 
	write "</table>"
	do rsSQL.Close()
 
	quit
 
PrintHeader(pidVisao,pidUnidade,pidGrupo,pdteDe,pdteAte)
	quit:(pidVisao = "")
 	
 	new periodo
 	set periodo = " de "_pdteDe_" a "_pdteAte
 	
	write "<div style='font-size:15px;color:#333333;margin-top:10px;padding-top:10px;padding-bottom:5px'>"
 
 	if (pidVisao = "Geral") {
		write "	<strong>Produtos mais comprados no PDPAS na Rede"_periodo_"</strong>" 	
 	} elseif (pidVisao = "Unidade") {
 		write "	<strong>Produtos mais comprados no PDPAS na Unidade "_$piece($get(^VARSESUNI(0,pidUnidade,1)),"~",1)_periodo_"</strong>" 	
 	} elseif (pidVisao = "Grupo") {
		write "	<strong>Produtos do grupo "_$$GetDescGrupoPDPASFromID^VARSESPainelPDPASUtils(pidGrupo)_" mais comprados pelo PDPAS na Rede"_periodo_"</strong>"  	
  	} elseif (pidVisao = "UnidadeeGrupo") {
		write "	<strong>Produtos do grupo "_$$GetDescGrupoPDPASFromID^VARSESPainelPDPASUtils(pidGrupo)_" mais comprados pelo PDPAS na Unidade "_$piece($get(^VARSESUNI(0,pidUnidade,1)),"~",1)_periodo_"</strong>"
 	}
 
	write "</div>"
 	
 	quit
]]></Routine>
</Export>