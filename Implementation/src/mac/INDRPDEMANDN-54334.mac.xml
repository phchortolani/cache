<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPDEMANDN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPDEMANDN
#include COMSYS
#include INConst
	;#define WSResponse(%Status,%TextCode) %Status_"~#"_%TextCode_": "_$$^WWWTEXT(%TextCode)  ;DEPRECATED
 
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		NACH NEU ERFASSEN DEMANDS DATEI SPEICHEN
	;	ACHTUNG EINSPRUNG AUCH AUS INULRUN  ;estimation too out of 
	; 
	; Called By:
	; 	AddDemand^INWSConsumptionInterface
	; 
	; Inputs : 
	;	VORG(1)   = ARTIKEL	                                ;Item
	;	VORG(2)   = MENGE                                   ;volume
	;	VORG(3)   = DUE DATUM                               ;Date 
	;	VORG(4)   = DUE UHRZEIT                             ;Due Date
	;	VORG(5)   = PRIORITÄT                               ;Priority
	;	VORG(9)   = Sourcing Location
	;	VORG(10)  = ANFORDERUNG VON BETRIEB                 ;location 
	;	VORG(11)  = ANFORDERUNG VON MITARBEITER             ;requirement of staff?
	;	VORG(12)  = TEXT
	;	VORG(17)  = LIEFERANT                               ;Supplier
	;	VORG(18)  = VERWENDUNG                              ;Use
	;	VORG(19)  = BESTIMMT FÜR KUNDE                      ;to customer 
	;	VORG(20)  = SPEZIFIKATION                           ;specification / classification
	;	VORG(99)  = $$$NO do not reload form, anything else including undefined, reload the form.
	;	VORG(202) = $$$YES, create a blocked demand.
	;	VORG(203) = defined, create/update the supply for this demand
	;	VORG(204) = the Requisition Number
	;	VORG(205) = the Group Number
	;	VORG("IssueType") = Inputted Issue Type
	;	VORG("ExternalUOM") = Inputted External Unit of Measure
	;   VORG("DispatchNumber") = entered when dispatch is created by a webservice so that the
	;			cleanup routines won't remove it.  ;SR17571
	;	YFORM=FORMULAR DES STARTS Z.B=YFORM="INDRPDEMANDP" PLANUNGSDEMANDS
	;
	; ByRef :
	;
	;
	; Returns :
	; 	VORG(200) = INDRPDEMAND Save status
	; 	VORG(201) = key for INDRPDEMAND : Locn,DemandNo
	;
	;
	; History :
	; 14-Oct-2010	shobby	SR17571: Included DispatchNumber
	; 16-Mar-2009	GRF		SR16418: VORG(202) already managing YES case - reverted;
	; 							clear old comments
	; 16-Mar-2009	DavidR	SR16418: Added the saving of External UOM to INDRPDEMAND 
	; 							for later validation.
	; 12-Mar-2009	DavidR	SR16418: added the saving of Issue type into INDRPDEMAND,
	;							added a further check of VORG(202) to set "YES"
	; 08-Nov-2007	FIS		BR014564: Modifications for call by Web Service
	; 30-May-2007	GRF		SR15513: Naked References
	; 15-May-2007	RPW		SR15513: Allow manual blocking of the demand and not
	; 						creating the supply record, pass in the requisition number
	; 						Added the group number
	; 21-Sep-2006	PO		SR15058: Set required for delivery if using form INTFRRequest
	; 24-Jul-2006	GRF		(In preparation)  Naked Reference; build ART2 in subroutine
	; 12-Jul-2006	PO		SR14833: Set due date on the demand to the due date specified on the transfer order
	; 18-Apr-2006	RPW/PO	SR14427: Do not reload the form anymore if VORG(99) is set to $$$NO
	; 07.12.2001	FAN
	;-------------------------------------------------------------------------------
	NEW ART,ART2,BET,KEY,MENGE,MIT,NR,SATZ,strPlanner,YFKEY
	
	SET BET = $GET(VORG(9))
	IF BET="" SET BET = YLOCATION
	IF BET="" SET BET = $ORDER(^WWW0121(0,YM,""))
	SET YFKEY = BET
	
	SET NR    = $$^WWWNEXT("INDRPDEMAND")
	SET ART   = $GET(VORG(1))
	SET MENGE = $GET(VORG(2))
	
	IF ART'="" IF +MENGE'=0 DO
	. IF +MENGE=0                                  DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$$^WWWTEXT(31407)_")")  QUIT    ; "Check please (Quantity)"    BITTE PRÜFEN (MENGE) 
	. IF YFORM="INDRPDEMANDP" IF $GET(VORG(11))="" DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$$^WWWTEXT(32847)_")")  QUIT    ; "Check please (Price per Unit)"    BITTE PRÜFEN (REFERENZ)
	. SET SATZ = ""
	. ;
	. set strPlanner = $$GetFieldFromDRP^INDRPITEM(BET,ART,6)
	. set MIT  = $TRANSLATE(strPlanner,",",";")        ;ZUSTÄDIG MITARBEITER
	. IF $FIND(";"_MIT_";",";"_YBED_";")  SET $PIECE(SATZ,Y,8) = YBED
	. IF '$FIND(";"_MIT_";",";"_YBED_";") SET $PIECE(SATZ,Y,8) = $PIECE(MIT,";",1)
	. ;
	. SET $PIECE(SATZ,Y,3)   = +$HOROLOG                     ;ERSTELLT AM 
	. SET $PIECE(SATZ,Y,10)  = $GET(VORG(11))                ;ANGEFORDERT VON
	. IF $PIECE(SATZ,Y,10)="" SET $PIECE(SATZ,Y,10)=YBED     ;ERSTELLT DURCH 
	. SET $PIECE(SATZ,Y,4)   = ART                           ;ARTIKELNUMMER
	. SET $PIECE(SATZ,Y,5)   = MENGE                         ;MENGE         ;quantity 
	. SET $PIECE(SATZ,Y,6)   = $get(VORG(3),+$horolog)       ;BENÖTIG DATUM ;Date
	. SET $PIECE(SATZ,Y,17)  = $GET(VORG(4))                 ;UHR           ;time
	. SET $PIECE(SATZ,Y,100) = $GET(VORG(12))                ;BEMERKUNG     ;remark 
	. SET $PIECE(SATZ,Y,105) = $GET(VORG(20))                ;SPEZIFIKATION
	. IF $get(VORG(202)) || ($PIECE($$^INDRPACCESS(,YBED),Y,16)'=$$$YES) do
	. . set $$$INDRPDEMANDBlocked(SATZ) = $$$YES   // SR15513 ;GESPERRT,WENN NICHT ERLAUBT
	. if $get(VORG(202))="00" set $$$INDRPDEMANDBlocked(SATZ) = $$$NO    ;explicit NO
	. SET $PIECE(SATZ,Y,71)  = $GET(VORG(5))                 ;PRIORITÄT     ;priority 
	. SET $PIECE(SATZ,Y,116) = $$^INARTKOST(ART)             ;PREIS         ;price 
	. SET $PIECE(SATZ,Y,1)   = $GET(VORG(10))                ;ANFORDERUNG VON BETRIEB ;location 
	. IF $PIECE(SATZ,Y,1)="" SET $PIECE(SATZ,Y,1) = BET      ;BET VON
	. SET $PIECE(SATZ,Y,115) = $GET(VORG(18))                ;VERWENDUNG
	. SET $PIECE(SATZ,Y,117) = $GET(VORG(19))                ;FÜR KUNDE ;to customer 
	. set $$$INDRPDEMANDDemandClosed(SATZ)      = $get(VORG(103))
	. set $$$INDRPDEMANDRequisitionNumber(SATZ) = $get(VORG(204))
	. set $$$INDRPDEMANDGroupNumber(SATZ)       = $get(VORG(205))
	. set $$$INDRPDEMANDReference(SATZ)         = $get(VORG(210))
	. set $$$INDRPDEMANDIssueType(SATZ)         = $get(VORG("IssueType"))      ; SR16418
	. set $$$INDRPDEMANDExternalUOM(SATZ)		= $get(VORG("ExternalUOM"))    ; SR16418
	. set $$$INDRPDEMANDDispatchNumber(SATZ)	= $get(VORG("DispatchNumber")) ; SR17571
	. if YFORM="INTFRRequest" set $$$INDRPDEMANDReadyForDelivery(SATZ) = $$$YES
	. ;
	. IF YFORM="INDRPDEMANDN" DO
	. . SET $PIECE(^INUSER(YM,YBED,1),Y,5)=$PIECE(SATZ,Y,1)
	. ;
	. IF (YFORM="INDRPUMKURZ") || (YFORM="INTFRPickLine") DO
	. . SET $PIECE(^INUSER(YM,YBED,1),Y,5)=$PIECE(SATZ,Y,1)
	. ;
	. DO
	. . IF $PIECE(SATZ,Y,1)'=BET SET $PIECE(SATZ,Y,70)=3 QUIT     ;TRANSFERAUFTRAG
	. . SET $PIECE(SATZ,Y,70)=0                   ;MANUELLER DEMAND / WARENANFORDERUNG
	. ;
	. IF YFORM="INDRPUMKURZ" DO ^INDRPUMKURZ    ;SCHNELL TOURENPLANEN
	. ;
	. DO
	. . NEW YFORM,YVOR
	. . SET KEY=BET_","_NR
	. . SET YOK=$$^WWWSPEI("INDRPDEMAND",KEY,SATZ,1)
	. . set VORG(200) = YOK
	. . set VORG(201) = KEY
	. ;
	. DO:$get(VORG(203))'="" ^INDRPSAVED(KEY,SATZ)  ;UPDATE/CREATE SUPPLY ON DESTINATION LOCATION 
	
	if $GET(VORG(99))'=$$$NO DO ^WWWFORM
	quit
	
	
Process(pidRequisition,pobjRequisition,pintGroupNumber)
	;-------------------------------------------------------------------------------
	; Create the Demand without a Supply for this Requisition.
	;
	; Inputs:
	;	pidRequisition
	;	pobjRequisition
	;	pintGroupNumber
	; 
	; ByRef:
	;
	; Returns:
	;	VORG(201)	key for INDRPDEMAND : Locn,DemandNo
	;	
	; History:
	; 15-May-2007	RPW		SR15513: Created
	;-------------------------------------------------------------------------------
	new YBED,VORG,strLanguage
	
	set VORG(1)   = $$$INRequisitionItem(pobjRequisition)
	set VORG(2)   = $$$INRequisitionQuantity(pobjRequisition)
	set VORG(3)   = $$$INRequisitionDueDate(pobjRequisition)
	set VORG(9)   = $$$INRequisitionSourcingLocation(pobjRequisition)
	set VORG(10)  = $$$INRequisitionLocation(pobjRequisition)
	set VORG(204) = pidRequisition 
	set VORG(205) = pintGroupNumber
	
	set YBED = $$$INRequisitionCreatedBy(pobjRequisition)
	;do GetUserDetails^INDRPSTART1(.YBED,.strLanguage)
	
	set VORG(11)  = YBED
	set VORG(12)  = $$$Text($listbuild("IN00681",pidRequisition,$$$INRequisitionLocation(pobjRequisition)))  ;Demand created for Requisition %1 at Location %2 
	set VORG(99)  = $$$NO
	set VORG(202) = $$$YES // Block the Demand
	set VORG(203) = $$$NO  // Do not create the supply.
	
	do ^INDRPDEMANDN
	
	quit $get(VORG(201))
]]></Routine>
</Export>