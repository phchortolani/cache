<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARVolumeEstoqueLocal" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include VARConst
#include COMSYS
#include INConst

#define DeEstoque 1
#define Embalagem 2

VARVolumeEstoqueLocal
	quit
	
OnAfterSave
	new RefModulo
	set RefModulo = ""

	set Local 				= $get(VORG(1))
	set DesdeModulo			= $get(VORG(2))
	set AteModulo 			= $get(VORG(3))
	set DesdeRua			= $get(VORG(4))
	set AteRua	 			= $get(VORG(5))
	set Mes 				= $get(VORG(6))
	set Ano 				= $get(VORG(7))
	set TipoUnidade			= $get(VORG(8))
	set Formato 			= $get(VORG(9))
	set Programa			= $get(VORG(10))
	
	k ^VARTempVolumeEstoqueLocal(YM,YBED)
	
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,1) = Local
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,2) = DesdeModulo
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,3) = AteModulo
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,4) = DesdeRua
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,5) = AteRua
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,6) = Mes
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,7) = Ano
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,8) = TipoUnidade
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,9) = Formato
	set $piece(^VARTempVolumeEstoqueLocal(YM,YBED,1),Y,10) = Programa
	
	if ((Mes="")||(Ano="")||(TipoUnidade="")||(Formato="")||(Local="")){
		$$$Alert("É necessário preencher todos os filtros obrigatórios.")
		do GoToForm^COMUtilForm("VARVolumeEstoqueLocal","",,,,,)
		quit
	} elseif ((DesdeModulo'="")||(AteModulo'="")){
		if ((DesdeModulo'="")&&(AteModulo="")){
			if (DesdeRua'="")||(AteRua'=""){
				set RefModulo = $number(DesdeModulo)
			}
		} elseif ((DesdeModulo="")&&(AteModulo'="")){
			if (DesdeRua'="")||(AteRua'=""){
				set RefModulo = $number(AteModulo)
			}
		}		
	}
	do MontaTabela(Local,DesdeModulo,AteModulo,RefModulo,DesdeRua,AteRua,Mes,Ano,Programa,TipoUnidade)

	do RunReportVolumeEstoqueLocal^VARJasperRunReport(Local,DesdeModulo,AteModulo,DesdeRua,AteRua,Mes,Ano,Programa,TipoUnidade,Formato)
	
	quit
	
OnBeforeButtonLine
	do LoadListaLocaisDeEstoque^VAREstoqueZerado
	quit
	
OnAfterDataFields
	do PrintJSModuloRua^VARVolumeEstoque("M2","M3","M4","M5")
	do ObservacoesVolumeLocal^VARVolumeEstoque
	quit
	
GetEstoqueNaData(Produto,Data,Local="",DesdeModulo="",AteModulo="",RefModulo="",DesdeRua="",AteRua="",Programa="")
	set Quantidade=0
	
	//Rubens, alteração para testes:
	set Quantidade=$$GetEstoqueOnHandAtDate^VARReposicao(Produto, Local,,Programa,Data,RefModulo,DesdeModulo,AteModulo,DesdeRua,AteRua)
	q Quantidade
	/*
	&sql(SELECT TOP 1 QOHLocn,QOHStorage,QtyMoved
		INTO :QuantidadeLocal,:QuantidadeEndereco,:QuantidadeMovimentada
		FROM alSOH.dStockHistory
		WHERE Item=:Produto 
		AND Location=:Local
		AND DateConfirmed<=:Data
		AND NonStock=0
		AND Storage->Code != 'X'
		AND ((:DesdeModulo IS NULL) OR ($$GetEnderecoConsulta^VARVolumeEstoqueLocal(Storage->Physical->Code,1) >= :DesdeModulo))
		AND ((:AteModulo IS NULL) OR ($$GetEnderecoConsulta^VARVolumeEstoqueLocal(Storage->Physical->Code,1) <= :AteModulo))
		AND ((:RefModulo IS NULL) OR ($$GetEnderecoConsulta^VARVolumeEstoqueLocal(Storage->Physical->Code,1) = :RefModulo))
		AND ((:DesdeRua IS NULL) OR ($$GetEnderecoConsulta^VARVolumeEstoqueLocal(Storage->Physical->Code,2) >= :DesdeRua))
		AND ((:AteRua IS NULL) OR ($$GetEnderecoConsulta^VARVolumeEstoqueLocal(Storage->Physical->Code,2) <= :AteRua))
		AND ((:Programa IS NULL) OR (Bundle->Program = :Programa))
		order by ID desc)
	
	if (SQLCODE=0) {
		if ((DesdeModulo'="")||(AteModulo'="")||(RefModulo'="")||(Programa'="")){
			set Total = QuantidadeEndereco+QuantidadeMovimentada
			quit Total
		} else {
			set Total = QuantidadeLocal+QuantidadeMovimentada
			quit Total
		}
	}
	
	quit Quantidade
	*/

GetEstoqueNaDataRede(Data,Local="",DesdeModulo="",AteModulo="",RefModulo="",DesdeRua="",AteRua="",Programa="",TipoUnidade)
	new Produto,VolumeTotal,VolumeProd,objProduto,Unidade,EstoqueNaData
	
	set Produto = ""
	set Unidade = ""
	set QuantidadeTotal = 0
	set VolumeTotal = 0
	set VolumeProd = ""
	set EstoqueNaData = 0
	
	if (Local = ""){
		for{
			set Local = $order(^WWW0121(0,0,Local))
			quit:Local=""
			
			for{
				set Produto = $order(^INART(0,Produto))
				quit:Produto=""
			
				set objProduto = $get(^INART(0,Produto,1))
			
				if (TipoUnidade = $$$DeEstoque){
					set Unidade = $$$INARTUnitofMeasure(objProduto)
				} elseif (TipoUnidade = $$$Embalagem){
					set Unidade = $$$INARTBaseUnit(objProduto)
				}
			
				continue:((Unidade = "")||('$$ExisteVolumeCadastrado^VARVolumeEstoque(Produto,Unidade)))
				
				set objUnidade = $get(^INARTPACK(0,Produto,Unidade,1))
				set VolumeProd = $$$INARTPACKFREE1(objUnidade)
			
				set EstoqueNaData = $$GetEstoqueNaData(Produto,Data,Local,DesdeModulo,AteModulo,RefModulo,DesdeRua,AteRua,Programa)
				set QuantidadeTotal=QuantidadeTotal+EstoqueNaData
				set VolumeTotal = VolumeTotal+(VolumeProd*EstoqueNaData)
			}
		}
		quit QuantidadeTotal_Y_VolumeTotal
	} else {
		for{
			set Produto = $order(^INART(0,Produto))
			quit:Produto=""
			
			set objProduto = $get(^INART(0,Produto,1))
			
			if (TipoUnidade = $$$DeEstoque){
				set Unidade = $$$INARTUnitofMeasure(objProduto)
			} elseif (TipoUnidade = $$$Embalagem){
				set Unidade = $$$INARTBaseUnit(objProduto)
			}
		
			continue:((Unidade = "")||('$$ExisteVolumeCadastrado^VARVolumeEstoque(Produto,Unidade)))
			
			set objUnidade = $get(^INARTPACK(0,Produto,Unidade,1))
			set VolumeProd = $$$INARTPACKFREE1(objUnidade)
			
			set EstoqueNaData = $$GetEstoqueNaData(Produto,Data,Local,DesdeModulo,AteModulo,RefModulo,DesdeRua,AteRua,Programa)
			set QuantidadeTotal=QuantidadeTotal+EstoqueNaData
			set VolumeTotal = VolumeTotal+(VolumeProd*EstoqueNaData)
		}
		quit QuantidadeTotal_Y_VolumeTotal
	}

	quit QuantidadeTotal

MontaTabela(Local,DesdeModulo,AteModulo,RefModulo,DesdeRua,AteRua,Mes,Ano,Programa,TipoUnidade)
	new strStatus,quantidadeMes,count,objReport,DataDia,stKey,QntTotal,VolumeTotal
	set strStatus 	  = $$$OK
	set QntTotal	  = 0
	set count 	 	  = 0
	set VolumeTotal	  = 0
	
	set Periodo 	= $$GetPeriodo^VARVolumeEstoque(Mes,Ano)
	set DataInicial = $ZDH($piece(Periodo,Y,1),3)
	set DataFinal   = $ZDH($piece(Periodo,Y,2),3)
	
	if (RefModulo=""){
		set DesdeModulo = $number(DesdeModulo)
		set AteModulo 	= $number(AteModulo)
	} elseif (RefModulo'=""){
		set DesdeModulo = ""
		set AteModulo 	= ""
		set RefModulo	= $number(RefModulo)
	}
	
	kill ^VARTempVolEstoqueProdReport(YM,YBED)
	
	for DataDia=DataInicial:1:DataFinal {
		set objReport = ""
		set count 	  = $i(count)
		set stKey	  = YBED_","_count
		
		set Valores = $$GetEstoqueNaDataRede(DataDia,Local,DesdeModulo,AteModulo,RefModulo,DesdeRua,AteRua,Programa,TipoUnidade)
		
		set QntTotal 	= $piece(Valores,Y,1)
		set VolumeTotal = $piece(Valores,Y,2)
		
		set $piece(objReport,Y,1) = "" ;nao utilizado nesta tela
		set $piece(objReport,Y,2) = "" ;nao utilizado nesta tela
		set $piece(objReport,Y,3) = $$SQLGetDataFormatada^VARSQL(DataDia)
		set $piece(objReport,Y,4) = QntTotal
		set $piece(objReport,Y,5) = "" ;nao utilizado nesta tela
		set $piece(objReport,Y,6) = VolumeTotal
		
		set strStatus = $$$Save("VARTempVolEstoqueProdReport",stKey,objReport,1)
		
		if $$$ISERR(strStatus) {
			set $zerror = $$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
	}
	
	quit

GetValorPadraoLocal()
	new Local
	set Local = $piece($get(^VARTempVolumeEstoqueLocal(YM,YBED,1)),Y,1)
	
	if (Local = ""){
		set Local = YLOCATION
	}
	
	quit Local

GetValorPadraoMes()
	new Mes
	set Mes = $piece($get(^VARTempVolumeEstoqueLocal(YM,YBED,1)),Y,6)
	
	if (Mes = ""){
		set Mes = $number($piece($zdate($h,3),"-",2))
	}
	
	quit Mes
	
GetValorPadraoAno()
	new Ano
	set Ano = $piece($get(^VARTempVolumeEstoqueLocal(YM,YBED,1)),Y,7)
	
	if (Ano = ""){
		set Ano = $piece($zdate($h,3),"-",1)
	}

	quit Ano
	
GetValorPadraoTipoUnidade()
	new TipoUnidade
	set TipoUnidade = $piece($get(^VARTempVolumeEstoqueLocal(YM,YBED,1)),Y,8)
	
	if (TipoUnidade = ""){
		set TipoUnidade = 1
	}

	quit TipoUnidade
	
GetValorPadraoFormato()
	new Formato
	set Formato = $piece($get(^VARTempVolumeEstoqueLocal(YM,YBED,1)),Y,9)
	
	if (Formato = ""){
		set Formato = 1
	}

	quit Formato
	
GetEnderecoConsulta(pEndereco,pPosicao)
	;1 - módulo
	;2 - rua
	
	quit:((pEndereco="")&&(pPosicao="")) ""
	
	new Result
	set Result = $number($piece(pEndereco,"-",pPosicao))
	
	quit Result

]]></Routine>
</Export>