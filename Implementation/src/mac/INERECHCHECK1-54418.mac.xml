<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INERECHCHECK1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INERECHCHECK1
#include INConst
#include COMSYS
	;-------------------------------------------------------------------------------
	;| Description of Function :
	;		PRÜFEN OB GÜLTIGER LIEFERANT  
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 26-Mar-2010	GRF		-: move idSupplier up and use; Key macros
	; 15-Feb-2006	JW		SR14303: Encapsulate amounts check
	; 06-Apr-2005	JW		SR12037: Don't allow duplicate invoice numbers for a supplier
	; 14.04.2005	BEC		Created
	;-------------------------------------------------------------------------------
	new CHECK,idDate,idInvoice,idSupplier
	
	SET CHECK = 1    ;NICHT SPEICHERN
	set idSupplier = $$$KEY1($get(YKEY))
	
	IF idSupplier'="" IF $DATA(^INLIEF(YM,idSupplier,1)) SET CHECK = ""    ;NUR SPEICHERN WENN DATEN VORHANDEN
	;IF CHECK'=1 KILL ^WWWUSETMP(0,YUSER,YFORM)
	
	IF CHECK=1 {
		set Q = 1
	;	set ^WWWUSETMP(0,YUSER,YFORM,"D",1)=$GET(YFELD)
		do ^WWWINFO($$^WWWTEXT(32224))  ; "Supplier Not Created"   ; "Lieferant nicht angelegt" 
	
	} else { // SR12037
		set idInvoice  = $$$KEY2($get(YKEY))
		set idDate     = $$$KEY3($get(YKEY))
		if (idSupplier'="") && (idInvoice'="") && (idDate'="") {
			// If there is another invoice with this supplier and number, don't save
			if '$data(^INERECH(YM,idSupplier,idInvoice,idDate)) && $data(^INERECH(YM,idSupplier,idInvoice)) {
				set Q = 1
				do ^WWWINFO($$^WWWTEXT(34154))
			} ; "The invoice number already exists for the supplier. Please choose another number."
		}
	}
	
	if $get(Q)'=1 && ($get(YFELD)'="") { // 10722
		if $$$INERECHReleaseInvoice(YFELD) { // releasing invoice
		//	if ($$$INERECHNetAmount(pYFELD) - $$GetCalculatedAmount^INERECH(pYKEY) '= 0) {  // not equal
			if '$$AmountsMatch(YKEY,YFELD) { // SR14303
				set Q = 1
				do ^WWWINFO($$^WWWTEXT(34162))
			} ; "Net Total must match Calculated Total before invoice can be relased."
		}
	}
	quit
	
	
AmountsMatch(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Encapsulate amounts check
	;
	; Params: key and data record
	;
	; ByRefs:
	;
	; Returns: boolean
	;
	; History:
	; 15-Feb-2006	JW		SR14303: Created
	;-------------------------------------------------------------------------------
	new blnMatch
	
	set blnMatch = ($$$INERECHNetAmount(pYFELD) - $$GetCalculatedAmount^INERECH(pYKEY) = 0)
	quit blnMatch
	
]]></Routine>
</Export>