<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINTestData" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINTestData
	#Include FIN
	#Include FINConst

CreateSupplierAdjustments(pintLines=100)
	;-------------------------------------------------------------------------------
	; Create an adjustment for the specified supplier.
	;
	; Returns:
	;
	; History:
	; 10-Mar-2005	RobertW		Fixed TCOMMIT and TROLLBACK (SR11273)
    ; 18-Jun-2004		shobby		Foreign Currency
	; 01-Mar-2004		shobby		Used the WWWNEXT numbering system and quit with the total adjustment.
	; 17-Feb-2004		shobby		Creation
	;-------------------------------------------------------------------------------
 	;
	new strStatus,objFINAPADJ,objFINAPADJLine,idFINAPADJ,idFINAPADJLine,idFINAPSupplier
	new fltAmount,idAccount,intLength,intSuppliers,intAccounts,fltTotal
	
	tstart   ;---------------------------------Transaction Start--------------------------------
	
	set strStatus=$$$OK

	;set idFINAPADJ=$order(^FINAPADJ(YM,""),-1)+1
	set idFINAPADJ=$$^WWWNEXT("FINAPADJ")
	set intLength=$length(pintLines)
	set objFINAPADJ=""
	set fltTotal=0
	write !
	set $$$FINARADJTransactionDate(objFINAPADJ)=+$h
	set $$$FINARADJComment(objFINAPADJ)=$$$Text("Fin00155")  ;Adjustment
	set strStatus=$$^WWWSPEI("FINAPADJ",idFINAPADJ,objFINAPADJ)
	if $$$ISOK(strStatus) {
		set intSuppliers=$$CreateSupplierList()
		set intAccounts=$$CreateAccountList()
		for idFINAPADJLine=1:1:pintLines {
			if (idFINAPADJLine/100)=(idFINAPADJLine\100) {
				w ((idFINAPADJLine/pintLines)*100)\1_"% Complete                     "_$char(13)
			}
			set objFINAPADJLine=""
			set idFINAPSupplier=$$GetSupplier(intSuppliers)
			set $$$FINAPADJLineSupplier(objFINAPADJLine)=idFINAPSupplier
			set $$$FINAPADJLineRef(objFINAPADJLine)="Invoice:"_$translate($justify(idFINAPADJLine,intLength)," ",0)
			set fltAmount=($random(1000000)-50000)/100
			if fltAmount'<0 {
				set $$$FINAPADJLineCharge(objFINAPADJLine)=fltAmount
				if $random(2) {
					set $$$FINAPADJLineDiscount(objFINAPADJLine)=fltAmount*$random(25)/100
				}
			} else {
				set $$$FINAPADJLineCredit(objFINAPADJLine)=$$$FCSign(fltAmount)
			}
			set fltTotal=fltTotal+fltAmount
			set $$$FINAPADJLineInvoiceDate(objFINAPADJLine)=$h+$random(10)+5
			set $$$FINAPADJLineDue(objFINAPADJLine)=$$$FINAPADJLineInvoiceDate(objFINAPADJLine)+$random(30)+5
			set $$$FINAPADJLineGLAccount(objFINAPADJLine)=$$GetAccount(intAccounts)
			
			set strStatus=$$^WWWSPEI("FINAPADJLine",idFINAPADJ_","_idFINAPADJLine,objFINAPADJLine)
		}
		if $$$ISOK(strStatus) set strStatus=$$Balance^FINAPADJ(idFINAPADJ)
	}
	
	if $$$ISERR(strStatus) {
		write !,"Failed",!
		trollback ;---------------------------Transaction Rollback------------------------------
	} else {
		if $tlevel>0 tcommit   ;---------------------------Transaction Commit--------------------------------
		write !,"Success",!
	}
	quit "Adjustment="_idFINAPADJ_" : Total="_fltTotal


CreateSupplierList()
	;-------------------------------------------------------------------------------
	; Creates a list of Supplier numbers
	;
	; Returns:
	;
	; History:
	; 18-Feb-2004		shobby		Creation
	;-------------------------------------------------------------------------------
 	;
	new idx,i
	set idx=""
	set i=0
	kill ^CacheTempTestData("Supplier")
	for {
		set idx=$order(^FINAPSupplier(YM,idx))
		quit:idx=""
		set i=i+1
		set ^CacheTempTestData("Supplier",i)=idx
	}
	quit i


CreateAccountList()
	;-------------------------------------------------------------------------------
	; Creates a list of Account numbers
	;
	; Returns:
	;
	; History:
	; 18-Feb-2004		shobby		Creation
	;-------------------------------------------------------------------------------
 	;
	new idx,i,objFINGLAccount
	set idx=""
	set i=0
	kill ^CacheTempTestData("Account")
	for {
		set idx=$order(^FINGLAccount(YM,idx))
		quit:idx=""
	 	set objFINGLAccount=$get(^FINGLAccount(YM,idx,1))
	 	if $$$FINGLAccountAccountUsage(objFINGLAccount)=$$$EnumFINGLACCOUNTUSAGETransaction {
		 	if $$$FINGLAccountCurrency(objFINGLAccount)="" {
				set i=i+1
				set ^CacheTempTestData("Account",i)=idx
		 	}
	 	}
	}
	quit i


GetSupplier(pintSuppliers)
	;-------------------------------------------------------------------------------
	; Gets a random Supplier
	;
	; Returns:
	;
	; History:
	; 18-Feb-2004		shobby		Simplified and sped up by using CacheTempTestData 
	; 18-Feb-2004		shobby		Make sure that this loops at least once
	; 17-Feb-2004		shobby		Creation
	;-------------------------------------------------------------------------------
 	;
 	quit $get(^CacheTempTestData("Supplier",$random(pintSuppliers)+1))


GetAccount(pintAccounts)
	;-------------------------------------------------------------------------------
	; Gets a random GL Account
	;
	; Returns:
	;
	; History:
	; 18-Feb-2004		shobby		Simplified and sped up by using CacheTempTestData 
	; 18-Feb-2004		shobby		blnFound flag must be set to $$$NO for each cycle through the loop.
	; 18-Feb-2004		shobby		Make sure that this loops at least once
	; 17-Feb-2004		shobby		Creation
	;-------------------------------------------------------------------------------
 	;
 	quit $get(^CacheTempTestData("Account",$random(pintAccounts)+1))
]]></Routine>
</Export>