<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPPLineQtyGenRule" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPPLineQtyGenRule ; Price Plane Line Quantity Break Generate Rule
	
#include INConst
#include COMSYS
	
OnBeforeFormat(pstrYFieldName,pstrText,pobjRuleLineQty)
	;-------------------------------------------------------------------------------
	; Display field in error with message if data entry is incorrect
	;
	; Params:
	; pstrYFieldName - Current Edit Grid field name
	; pstrText - Current fields content
	; pobjRuleLineQty - Current Edit Grid record
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 25-Feb-2009	GRF		SR16394: Clarify idClassField rather than idField
	; 24-Jan-2007	JW		SR15280: Multiple calculation removed.
	; 06-Dec-2006	PO		SR15280: Created
	;-------------------------------------------------------------------------------
	new idClassField,idCol,idQtyBreakLines,idRow,idRuleLineKey,strFormula
	
	$$$GRIDSplitKey(pstrYFieldName,idRow,idCol) 
	
	set idClassField = $$$GetClassField(YFORM,idCol)
	
	if idClassField = $$$FldINPPLineQtyGenRuleMinQuantity {
		;if $$$INPPLineQtyGenRuleMinQuantity(pobjRuleLineQty) = "" {
		;	set YBACKGROUNDCOLOR = YRED
		;	set YTOOLTIP = $$$Text("IN00567")  ;Must provide a minimum quantity.
		;} else
		if $$$INPPLineQtyGenRuleMinQuantity(pobjRuleLineQty) '> 0 {
			set YBACKGROUNDCOLOR = YRED
			set YTOOLTIP         = $$$Text("IN00566")
		; "Minimum Quantity must be greater than zero, for a quantity break starting at zero enter pricing formula on rule line."
		
		} else {
			set idRuleLineKey = $$$GRIDYKEYContainer
			if '$$NoOverlappingMinQuantities^INDPPLineDispRule($$$KEY1(idRuleLineKey),$$$KEY2(idRuleLineKey),.idQtyBreakLines) {
				set YBACKGROUNDCOLOR = YRED
				set YTOOLTIP         = $$$Text($listbuild("IN00568",idQtyBreakLines))
			; "The two lines %1 have quantity breaks for the same quantity."
			}
		}
	
	} elseif idClassField = $$$FldINPPLineQtyGenRulePricingFormula {
		;if $$$INPPLineQtyGenRulePricingFormula(pobjRuleLineQty) = "" {
		;	set YBACKGROUNDCOLOR = YRED
		;	set YTOOLTIP = $$$Text("IN00582")  ;Must provide a pricing formula.
		;}
		set strFormula = $$$INPPLineQtyGenRulePricingFormula(pobjRuleLineQty)		//JW
		if '$$$BracketsMatch(strFormula) {
			set YBACKGROUNDCOLOR = YRED
			set YTOOLTIP         = $$$Text("IN00579")  ; "Brackets not matching in pricing forumla."
		
		} elseif '$$$ValidCharsInExpression(strFormula) {
			set YBACKGROUNDCOLOR = YRED
			set YTOOLTIP         = $$$Text("IN00580")  ; "Invalid characters in pricing formula."
		}
	}
	
	quit
	
]]></Routine>
</Export>