<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INCategory" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INCategory
 
#include INConst
 
GetPriceCategoryForProduct(pidItem)
	;-------------------------------------------------------------------------------
	; Get the Price Category on a Product and if there is none attempt to find it on the 
	; Item Group
	;
	; Params:
	; pidItem: The id of the product
	;
	; ByRefs:
	;
	; Returns:
	; either a price category or "" if not found
	;
	; History:
	; 05-Dec-2006	RPW		SR15279: Created
	;-------------------------------------------------------------------------------
 
	new idPriceCategory,objItem
	
	if pidItem="" {
		set idPriceCategory=""
	} else {	
		set objItem=$get(^INART(YM,pidItem,1))
		
		set idPriceCategory=$$$INARTPriceCategory(objItem)
		
		if idPriceCategory="" {
			set idPriceCategory=$$GetPriceCategoryForItemGroup($$$INARTItemGroup(objItem))
		}
	}
	quit idPriceCategory
	
GetPriceCategoryForItemGroup(pidItemGroup)
	;-------------------------------------------------------------------------------
	; Get the Price Category on an Item Group and if there is none look up the heirarchy.
	;
	; Params:
	; pidItemGroup: The id of the Item Group
	;
	; ByRefs:
	;
	; Returns:
	; either a price category or "" if not found
	;
	; History:
	; 05-Dec-2006	RPW		SR15279: Created
	;-------------------------------------------------------------------------------
 
	new idPriceCategory,objItemGroup,idParent
	
	if pidItemGroup="" {
		set idPriceCategory=""
	} else {
		set objItemGroup=$get(^INItemGroup(YM,pidItemGroup,1))
		
		set idPriceCategory=$$$INItemGroupPriceCategory(objItemGroup)
		
		if idPriceCategory="" {
			set idParent=$$$INItemGroupParentGroup(objItemGroup)
			if idParent'="" {
				set idPriceCategory=$$GetPriceCategoryForItemGroup(idParent)
			}
		}
	}
	
	quit idPriceCategory
]]></Routine>
</Export>