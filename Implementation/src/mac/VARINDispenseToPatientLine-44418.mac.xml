<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINDispenseToPatientLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINDispenseToPatientLine
	#include COMSYS
	#include INConst
	#include VARConst
	#include MEDConst
	#include COMConst
	#include COMGridEdit31
	#include WWWConst

	Quit
	
LoadDispenses(pidCurDisp,&parrDrugs)
	;-------------------------------------------------------------------------------
	; Load Dispensed Drugs 
	; 
	; Params: Rx & Array
	;
	; Returns: Updated array
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
	;-------------------------------------------------------------------------------
	New idxRx,idDisp,idIssueLine,objIssueLine,idIssueItem,dtTo,objRxLine,objDisp,blnDispensed
	
	Quit:pidCurDisp=""
	
	Set idDisp      = pidCurDisp
	Set idIssueLine	= ""
	Set idparrDrugs = ""
	Kill ^parrDrugs
	
	For 
	{
		Set idIssueLine = $Order(^INIssueLine(YM,pidCurDisp,idIssueLine))
		Quit:idIssueLine=""
		
		Set objIssueLine = $Get(^INIssueLine(YM,pidCurDisp,idIssueLine,1))
		Set idIssueItem	 = $$$INIssueLineItem(objIssueLine)
		
		If (idIssueItem '= "")
		{
			Set ^parrDrugs("h",idIssueItem)=idIssueItem
		}
	}
	
	Quit
	
GetInteractionMessage(pidCurDisp, &arrDrugs)
	;-------------------------------------------------------------------------------
	; Get Interaction Message for Drugs
	; 
	; Params:  parrDrugs List of drugs , Count , Rx
	;
	; Returns: Message
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
	;-------------------------------------------------------------------------------	
	New strMsg,idItem1,idItem2,objItem ;,idDiluent,objRx,idPatient
	
	Set strMsg  = ""
	Set idItem1 = ""
	Set i=""

	do LoadDispenses(pidCurDisp,.arrDrugs)
	
	For 
	{
		Set idItem1 = $Order(^parrDrugs("h",idItem1))
		Quit:idItem1=""
		
		Set idItem2 = ""
		
		For 
		{
			Set idItem2 = $Order(^parrDrugs("h",idItem2))
			Quit:idItem2=""
			Continue:idItem1=idItem2
			Continue:('$Data(arrDrugs("l",idItem1))&&'$Data(arrDrugs("l",idItem2)))
			
			Set objItem = $Get(^MEDDrugInteraction(0,idItem1,idItem2,1))
			
			If (objItem '= "") && $$$MEDDrugInteractionActive1(objItem) 
			{
				Set strMsg = strMsg_$$GetMessage(idItem1,idItem2,objItem, , .pintCont)
				Set parrDrugs("x",idItem1,idItem2)=""
				Set parrDrugs("x",idItem2,idItem1)=""
			}
		}
	}

	Quit strMsg
	

GetMessage(pidItem1,pidItem2,pobjItem1, &parrItems, &pintCont)
	;-------------------------------------------------------------------------------
	; Display Interaction Message for Drugs
	; 
	; Params:
	; pidItem1,pidItem2,pobjItem1, &parrItems, &pintCont
	;
	; ByRefs:
	;
	; Returns:
	; None
	;
	; History:
	; 25-Jul-2012	PPP		SR18067: Initialize variable strMsg
	; 13-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------	
	New idClinic,idInteraction,idProbablity,strMsg
					
	Set idClinic 	  = ""
	Set idInteraction = ""
	Set idProbablity  = ""
	Set parrItems(pidItem1,pidItem2) = $Increment(parrItems(pidItem1,pidItem2))
	Set strMsg 		  = ""	//SR18067
	
	If ($$$MEDDrugInteractionClinicalImportance(pobjItem1) '= "")
	{ 
    	Set idClinic = $$GetParamDesc^INPARA("MEDCLIIMP",$$$MEDDrugInteractionClinicalImportance(pobjItem1))
	}
	
	If ($$$MEDDrugInteractionInteractionImportance(pobjItem1) '= "")
	{ 
    	Set idInteraction = $$GetParamDesc^INPARA("MEDCLIIMP",$$$MEDDrugInteractionInteractionImportance(pobjItem1))
	}
	
	If ($$$MEDDrugInteractionOccuranceProbability(pobjItem1) '= "")
	{ 
    	Set idProbablity  = $$GetParamDesc^INPARA("MEDPROBABILITY",$$$MEDDrugInteractionOccuranceProbability(pobjItem1))
	}

	If (parrItems(pidItem1,pidItem2) < 2)
	{
		Set pintCont = $Increment(pintCont) 
		Set strMsg   = "<h4>"_$$^WWWFORMNAME("MEDDrugInteraction")_" #"_pintCont_"</h4>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","P",1)_": </b> "_"("_pidItem1_") "_$$^INARTNAME(pidItem1)_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","P",2)_": </b> "_"("_pidItem2_") "_$$^INARTNAME(pidItem2)_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",1)_": </b> "_idClinic_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",2)_": </b> "_idInteraction_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",3)_": </b> "_$$$MEDDrugInteractionEarlyInteraction(pobjItem1)_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",4)_": </b> "_idProbablity_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",5)_": </b> "_$$$MEDDrugInteractionEffect(pobjItem1)_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",6)_": </b> "_$$$MEDDrugInteractionMechanism(pobjItem1)_"</br>"_
					   "<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",7)_": </b> "_$$$MEDDrugInteractionSuggestion(pobjItem1)_"</br></br>"
	}

	Quit strMsg
	
CheckDrugInteraction(pYKEY,pblnValidOnly=$$$NO,pblnProcess=$$$NO,&pstrMessage,&pintCont)
	;-------------------------------------------------------------------------------
	; Check if Interaction Message exist for Drugs called by Drug Interaction Button 
	; on INDispenseToPatientLine
	;
	; Params: pYKEY -> KEY ID
	;
	; ByRefs:
	;
	; Returns: Boolean
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
	;-------------------------------------------------------------------------------		
	new blnExist,idDispense,idLine1,idLine2,objLine1,objLine2,arrGrid,idItem1
	new objItem1,idItem2,idDiluent,objItem2,objDispense,idRx,arrDrugs,dtTo
	
	Set YQ 		   = $$$YQDisable("MED00417")	//Dispense does not contain any Drug Interactions.
	Set blnExist   = $$$NO
	Set idDispense = $Get(pYKEY)	// ID Dispensação/Saída Ex: 11/000057
	Set idRx 	   = ""

	If '$$$NoKey(pYKEY) 
	{
		$$$GRIDGetContents(.arrGrid,"INDispenseToPatientLine")
		Set idLine1 = ""
		
		For 
		{
			Set idLine1 = $Order(arrGrid(idDispense,idLine1))
			Quit:idLine1=""
			
			Set objLine1 = $Get(arrGrid(idDispense,idLine1))
			Set idItem1  = $$$INIssueLineItem(objLine1)
			Set arrDrugs("l",idItem1) = ""
		}
	}
	
	Set idOrder = ""
	For i=1:1:2 
	{
		Set idOrder = $Order(arrDrugs("l",idOrder))
	}
	
	Set pstrMessage = $$GetInteractionMessage($Get(idDispense),.arrDrugs)
	
	If (pstrMessage '= "") 
	{
		Set blnExist = $$$YES		
	 	$$$YQHandler(blnExist)
	}
	
	Quit blnExist

DrugInteraction(pidDispense)
	;-------------------------------------------------------------------------------
	; Check the Drug Interaction
	;
	; Params:	pidDispense	- Dispense
	;
	; Returns:	
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
    ;-------------------------------------------------------------------------------
	New blnInteraction
	
	Set blnInteraction = $$CheckDrugInteraction(pidDispense)

	If (blnInteraction)
	{
		//Show messages
		&js< messageInteractions(0); > 
	}

	Quit

CheckInteraction(YKEY)
	;-------------------------------------------------------------------------------
	; Check the Drug Interaction
	;
	; Params:	pidDispense	- Dispense
	;
	; Returns:	
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
    ;-------------------------------------------------------------------------------
	
	New tCheckInteracao, tStatusInteracao
	
	Quit:YKEY=""
	
	// Verificar se o checkbox "Validar Interações Medicamentosas" está marcado
	Set tCheckInteracao = $Piece($Get(^VARParametroCliente(YM,YM,1)),"~",83)
	
	If (tCheckInteracao '= "")
	{
		// Verificar se existem itens com interação medicamentosa
		Set tStatusInteracao = $$CheckDrugInteraction^VARINDispenseToPatientLine(YKEY)
		
		If (tStatusInteracao=1)
		{
			&js< messageValid(); >
		}
		Else
		{
			do Post^VARINDispenseToPatientLine(YKEY)
		}
	}
	Else
	{
		do Post^VARINDispenseToPatientLine(YKEY)
	}
		
	Quit 
	
BuildMessage(pYKEY)
	;-------------------------------------------------------------------------------
	; Check the Drug Interaction
	;
	; Params:	pidDispense	- Dispense
	;
	; Returns:	
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
    ;-------------------------------------------------------------------------------
	
	New strWarning, intSize
	
	// Monto a mensagem
	Set intSize    = 120
	Set strWarning = "A dispensação de nº: "_pYKEY_" possui itens com interação medicamentosa. <p> Deseja prosseguir? "
	
	// Faço a chamada Javascript
	$$$JS("messageWarningInteraction('"_strWarning_"','"_intSize_"');")
	
	Quit $$$OK
	
	
Post(pidIssue)
	;-------------------------------------------------------------------------------
	; Post adjustment
	;
    ; Called By: Form INDispenseToPatient : Button 1 - Execute OnClick
    ; 
	; Returns:Status
	;
	; History:
	; 2014-07-16	Henrique Dias	HEVA-1348: Created
	;-------------------------------------------------------------------------------
	New strStatus
	
	Set strStatus = $$Transaction^COMTransaction("PostTxn^INIssueAuto("""_pidIssue_""")",$$$YES)
	
	If $$$ISOK(strStatus)
	{
		do GoToForm^COMUtilForm("INDispenseToPatient",pidIssue)
	}
	
	Quit strStatus
	
	
ShowInteractionMessage(pYKEY="", pblnValidOnly=$$$NO, window=1,pidPrescription="") ;SMSPE-15
	;-------------------------------------------------------------------------------
	; Show Interaction Message for Drugs
	; called by Drug Interaction Button on MEDDispenseLine
	; 
	; Params:
	; pYKEY : KEY ID
	;
	; ByRefs:
	;
	; Returns:
	; Boolean
	;
	; History:
	; 31-Jan-2013	SCR		HEVA-792: Use common code
	; 07-Aug-2012	PPP		SR18067: Updated to check diluents
	; 16-Jul-2012	SCR		SR18002: Changed to look at all active 
	; 13-Jun-2012	PPP		SR18002.1: Added Diluent Drug Interaction
	; 04-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------	
	new strMsg,intCont
	
	set intCont=0
	
	do CheckDrugInteraction(pYKEY, pblnValidOnly,,.strMsg,.intCont)

	if strMsg'="" do PrepareMessage(strMsg,window,.intCont)	
	
	quit

PrepareMessage(pstrMsg="",pblnWindow=1,pintCont)
	;-------------------------------------------------------------------------------
	; Show Interaction Message for Drugs from the Prescription
	; 
	; Params:
	; pYKEY : KEY ID
	;
	; ByRefs:
	;
	; Returns:
	; Boolean
	;
	; History:
	; 20-Jan-2014	shobby		SMSPE-15: Created
	;-------------------------------------------------------------------------------
	new intLines,intSize
	
	if (pblnWindow){				
		set intLines = $l(pstrMsg,"</br>")+2
		set intSize = intLines * 27
		
		if (intSize > 600){
			set intSize = 600
		}
		set pstrMsg = $replace(pstrMsg,"'","`")
		set pstrMsg = $$$Text($lb("MED00416",pintCont))_pstrMsg		//Dispense contains <b>%1</b> Drug Interaction(s)</br>
		
		do Show^WWWPopupMessage(pstrMsg,,,,"dscPopupDrugInteraction",,,,$$$YES,,600) ;SMSPE-15
		
	} else {				

	    write "<IMG SRC='"_YGIF_"delimiter.gif' border=0>"      
   		write "<a onclick='doLink(this);' href='javascript:history.go(-1);'><IMG SRC="_YGIF_"hback.gif id='BtnIni' TITLE='Voltar' border=0 align='top'></a>"     
   		write "<IMG SRC='"_YGIF_"delimiter.gif' border=0>"      
		write pstrMsg
	    write "</BR><IMG SRC='"_YGIF_"delimiter.gif' border=0>"      
   		write "<a onclick='doLink(this);' href='javascript:history.go(-1);'><IMG SRC="_YGIF_"hback.gif id='BtnIni' TITLE='Voltar' border=0 align='top'></a>"     
   		write "<IMG SRC='"_YGIF_"delimiter.gif' border=0></BR>"      
	}
	quit
]]></Routine>
</Export>