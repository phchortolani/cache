<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARFornecedorAvaliacao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARFornecedorAvaliacao
#include COMSYS
#include VARConst

#define Qualitativa 1
#define Quantitativa 2

OnAfterDataFields(pYKEY)
	new count, codFornecedor, codAvaliacao, Avaliacaode, Avaliacaoate, AvaliacaoPor, Qualidade,
		Pontualidade, Conformidade, Condicoes, Atendimento, Considerar, key, TipoAvaliacao
	
	set count = 0

	//Monta cabeçalho da tabela
   	new lstHeader
 	set lstHeader=""
  	set lstHeader=lstHeader_$listbuild("Seq.","Tipo","Avaliação de","Avaliação até","Avaliação por","Qualidade","Pontualidade","Conformidade","Condições","Atendimento","Considerar?","Desempenho")
				 			    			    							 	
  	if $$Start^COMTable(lstHeader,"<div style=margin:3px;>Avaliações realizadas para o fornecedor</div>",,,$$$NO) {

	 	set rs = ##class(%ResultSet).%New()
	    do rs.Prepare("SELECT CodFornecedor, CodAvaliacao, Avaliacaode, Avaliacaoate, AvaliacaoPor, Qualidade, Pontualidade, Conformidade, Condicoes, Atendimento, Considerar, TipoAvaliacao FROM VARFornecedorAvaliacao where CodFornecedor = "_$$$KEY1(pYKEY)_" order by CodAvaliacao desc")
	    do rs.Execute()

	    while rs.Next() {
			if ($SYSTEM.Status.IsOK(rs)) {  // check if this succeeded without errors		
        	
	        	//Pegando valores da query
	        	set codFornecedor = rs.GetData(1)
	        	set codAvaliacao  = rs.GetData(2)
	        	set Avaliacaode   = rs.GetData(3)
	        	set Avaliacaoate  = rs.GetData(4)
	        	set AvaliacaoPor  = rs.GetData(5)
	        	set Qualidade     = rs.GetData(6)
	        	set Pontualidade  = rs.GetData(7)
	        	set Conformidade  = rs.GetData(8)
	        	set Condicoes     = rs.GetData(9)
	        	set Atendimento   = rs.GetData(10)
	        	set Considerar    = rs.GetData(11)
	        	set TipoAvaliacao = rs.GetData(12)	        		        
	        	
				set count = $i(count)
				
	        	if Considerar = 1 { set Considerar = "Sim" }
	        	else { set Considerar = "Não" }				
				
				set key = codFornecedor_","_codAvaliacao
				
				if (TipoAvaliacao = $$$Qualitativa) {
					do NewLine^COMTable()
	    			do InsertCell^COMTable(count,"VARFornecedorAvaliacao",key)
	    			do InsertCell^COMTable("Qualitativa","VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable($zdate(Avaliacaode,4),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable($zdate(Avaliacaoate,4),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(AvaliacaoPor,"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable("")
				    do InsertCell^COMTable("")
				    do InsertCell^COMTable("")
				    do InsertCell^COMTable("")
				    do InsertCell^COMTable("")				    				    				    				    
	   			    do InsertCell^COMTable("<strong>"_Considerar_"</strong>","VARFornecedorAvaliacao",key,,"CENTER")
	   			    do InsertCell^COMTable("")
			        do EndLine^COMTable()
				
				} else {
					do NewLine^COMTable()
	    			do InsertCell^COMTable(count,"VARFornecedorAvaliacao",key)
	    			do InsertCell^COMTable("Quantitativa","VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable($zdate(Avaliacaode,4),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable($zdate(Avaliacaoate,4),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(AvaliacaoPor,"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(Qualidade_" - "_$piece($get(^VARPARA(YM,"AVALIACAO",SPRACHE,Qualidade,1)),Y,1),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(Pontualidade_" - "_$piece($get(^VARPARA(YM,"AVALIACAO",SPRACHE,Pontualidade,1)),Y,1),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(Conformidade_" - "_$piece($get(^VARPARA(YM,"AVALIACAO",SPRACHE,Conformidade,1)),Y,1),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(Condicoes_" - "_$piece($get(^VARPARA(YM,"AVALIACAO",SPRACHE,Condicoes,1)),Y,1),"VARFornecedorAvaliacao",key)
				    do InsertCell^COMTable(Atendimento_" - "_$piece($get(^VARPARA(YM,"AVALIACAO",SPRACHE,Atendimento,1)),Y,1),"VARFornecedorAvaliacao",key)
	   			    do InsertCell^COMTable("<strong>"_Considerar_"</strong>","VARFornecedorAvaliacao",key,,"CENTER")
	   			    do InsertCell^COMTable("<strong>"_$$CalculaDesempenho(key)_" <font color=gray>/ 100</font></strong>","VARFornecedorAvaliacao",key,,"CENTER")
			        do EndLine^COMTable()					
				}	        		                	       
			}
	    }
	    
	    if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhuma avaliação realizada para este fornecedor.",,,,,,12)
			do EndLine^COMTable()   
	    
	    } else {		   
		    new fltDesempenhoFornecedor
		 	set fltDesempenhoFornecedor = $$CalculaDesempenhoFornecedor(codFornecedor)
		    
	    	do NewLine^COMTable()	    	 
			do InsertCell^COMTable("<em>Índice de Desempenho do fornecedor</em> ",,,,"RIGHT",,11)
	    	do InsertCell^COMTable("<font color=blue><strong>"_fltDesempenhoFornecedor_" / 100</font></strong>",,,,"CENTER")
	    	do EndLine^COMTable()		   
	    }
		
		do NewLine^COMTable("lightsteelblue")
		do InsertCell^COMTable("<strong>Cadastrar nova avaliação</strong>","VARFornecedorAvaliacao",$$$KEY1(pYKEY),,"RIGHT",,12)
		do EndLine^COMTable()	    
	    
	 	do Stop^COMTable()
  	}

	quit
	
CalculaDesempenho(pYKEY)
	new codFornecedor, codAvaliacao, objAvaliacao, Qualidade, Pontualidade, Conformidade,
		Condicoes, Atendimento, pntQualidade, pntPontualidade, pntConformidade, pntCondicoes,
		pntAtendimento, fltPontuacao
	
	set codFornecedor = $$$KEY1(pYKEY)
	set codAvaliacao  = $$$KEY2(pYKEY) 

	if ( (codFornecedor = "") || (codAvaliacao = "") || (codAvaliacao = "+") ) {
		quit ""
	}
	
	set objAvaliacao = $get(^VARFornecedorAvaliacao(YM,codFornecedor,codAvaliacao,1))
	
	quit:($$$VARFornecedorAvaliacaoTipoAvaliacao(objAvaliacao) = $$$Qualitativa) ""
	
	set Qualidade    = $piece(objAvaliacao,Y,4)
	set Pontualidade = $piece(objAvaliacao,Y,5)
	set Conformidade = $piece(objAvaliacao,Y,6)
	set Condicoes    = $piece(objAvaliacao,Y,7)
	set Atendimento  = $piece(objAvaliacao,Y,8)

	set pntQualidade    = $Case(Qualidade,1:20,2:15,3:10,4:5,5:0)
	set pntPontualidade = $Case(Pontualidade,1:20,2:15,3:10,4:5,5:0)
	set pntConformidade = $Case(Conformidade,1:20,2:15,3:10,4:5,5:0)
	set pntCondicoes 	= $Case(Condicoes,1:20,2:15,3:10,4:5,5:0)
	set pntAtendimento  = $Case(Atendimento,1:20,2:15,3:10,4:5,5:0)
	
	set fltPontuacao = 0
	set fltPontuacao = pntQualidade + pntPontualidade + pntConformidade + pntCondicoes + pntAtendimento

	quit fltPontuacao
	
CalculaDesempenhoFornecedor(pidFornecedor)
	quit:(pidFornecedor = "") ""
	
	new fltNotaTotal, fltNota, keyAvaliacao, objAvaliacao, count, fltDesempenho
		
	set fltNotaTotal = 0
	set count = 0
	
	$$$Order3(^VARFornecedorAvaliacao,YM,pidFornecedor,idAvaliacao)
		
		set keyAvaliacao = pidFornecedor_$$$COMMA_idAvaliacao
		
		set objAvaliacao = $get(^VARFornecedorAvaliacao(YM,pidFornecedor,idAvaliacao,1))	
		continue:($$$VARFornecedorAvaliacaoConsiderar(objAvaliacao) '= $$$YES)
		continue:($$$VARFornecedorAvaliacaoTipoAvaliacao(objAvaliacao) = $$$Qualitativa)
		
		set fltNota = $$CalculaDesempenho(keyAvaliacao)
		
		if (fltNota >= 0) {		
			set fltNotaTotal = fltNotaTotal + fltNota
			set count = $i(count)
		}	
	$$$End
	
	quit:(count = 0) ""
	
	set fltDesempenho = (fltNotaTotal / count)	
	
	quit $$Ceiling^COMUtilNum(fltDesempenho)
	
	
]]></Routine>
</Export>