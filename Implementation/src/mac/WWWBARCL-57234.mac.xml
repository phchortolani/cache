<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWBARCL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWBARCL(T,N,E,P)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		BARCODE-BIBILIOTHEK
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 17.APR.2000	KAS		(BURKHARD KASTEN) created
	;-------------------------------------------------------------------------------
	NEW PTR
	
	SET PTR="" 
	DO  ;GOTO:PTR'="" @T
	. NEW I 
	. SET I="" 
	. FOR  SET I=$ORDER(^WWWBARC(0,I)) QUIT:I=""  SET:$PIECE(^WWWBARC(0,I,"???",1),"~",1)=T PTR="^WWWBARC(0,"_I_")"
	
	IF PTR'="" GOTO @T
	QUIT ""
	
B25     ; 2/5 Code
	NEW C,CD,I,J 
	SET N=N_$$PR1(31)
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789")'="" CD
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CD10
	FOR I=1:1:$LENGTH(N) SET C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2) DO CD10
	SET C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CD10
	SET $EXTRACT(CD,$LENGTH(CD))=""
	QUIT $$BAR(CD,E)
	
B25A ; 2/5 Code IATA
	NEW C,CD,I,J 
	SET N=N_$$PR1(31)
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789")'="" CD
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CD10
	FOR I=1:1:$LENGTH(N) SET C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2) DO CD10
	SET C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CD10
	SET $EXTRACT(CD,$LENGTH(CD))=""
	QUIT $$BAR(CD,E)
	
B25I ; 2/5 Code interleaved
	NEW C,D,CD,I,J 
	SET N=N_$$PR1(13)
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789")'="" CD 
	QUIT:$LENGTH(N)#2 CD
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CD10  ;Start/Stop
	FOR I=1:2:$LENGTH(N) DO
	. SET C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2),D=$PIECE(@PTR@($EXTRACT(N,I+1),1),"~",2)
	. FOR J=1:1:$LENGTH(C) SET CD=CD_$TRANSLATE($EXTRACT(C,J),"10","21")_$TRANSLATE($EXTRACT(D,J),"10","43")
	. QUIT
	SET C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CD10   ;Start/Stop
	SET $EXTRACT(CD,$LENGTH(CD))=""
	QUIT $$BAR(CD,E)
	
B25M ; 2/5 Code 3 Striche Matrix
	NEW C,CD,I,J 
	SET N=N_$$PR1(13)
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789")'="" CD
	SET C("SL")=$PIECE(@PTR@("SL",1),"~",2),C(0)=13,C(1)=24,C(2)=66
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CDSL
	FOR I=1:1:$LENGTH(N) SET CD=CD_"5",C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2) DO CDSL
	SET CD=CD_"5",C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CDSL
	QUIT $$BAR(CD,E)
	
B25D ; 2/5 Code 3 Striche Datalogic
	NEW C,CD,I,J 
	SET N=N_$$PR1(13)
	SET CD="" QUIT:$TRANSLATE(N,"0123456789")'="" CD
	SET C("SL")=$PIECE(@PTR@("SL",1),"~",2),C(0)=13,C(1)=24
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CDSL
	FOR I=1:1:$LENGTH(N) SET CD=CD_"5",C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2) DO CDSL
	SET CD=CD_"5",C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CDSL
	QUIT $$BAR(CD,E)
	
BCB ; Code Codabar
	NEW C,CD,I,J 
	SET N=N_$$PR3("0123456789-$:/.+ABCD")
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789$+-./:")'="" CD
	SET C("SL")=$PIECE(@PTR@("SL",1),"~",2),C(0)=13,C(1)=24
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CDSL
	FOR I=1:1:$LENGTH(N) SET CD=CD_"5",C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2) DO CDSL
	SET CD=CD_"5",C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CDSL
	QUIT $$BAR(CD,E)
	
C39 ; Code 39
	NEW C,CD,I,J 
	SET N=N_$$PR2("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ-. $/+%")
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ-. $/+%")'="" CD
	SET C("SL")=$PIECE(@PTR@("SL",1),"~",2),C(0)=13,C(1)=24
	SET C=$PIECE(@PTR@("START",1),"~",2) 
	DO CDSL
	FOR I=1:1:$LENGTH(N) SET CD=CD_"5",C=$PIECE(@PTR@($EXTRACT(N,I),1),"~",2) DO CDSL
	SET CD=CD_"5",C=$PIECE(@PTR@("STOP",1),"~",2) 
	DO CDSL
	QUIT $$BAR(CD,E)
	
EAN13 ; 13 stelliger EAN-Code ;EAN Code 
	NEW C,CD,CH,CO,CW,I,J,P 
	SET P=1,N=N_$$PR1(31)
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789")'="" CD 
	QUIT:$LENGTH(N)'=13 CD
	SET CW=$PIECE(@PTR@("W"_$EXTRACT(N),1),"~",2)
	DO EAN131("STSL","START")
	FOR I=2:1:7 DO EAN131($EXTRACT(CW,I-1)_"SL",$EXTRACT(CW,I-1)_$EXTRACT(N,I))
	DO EAN131("TRSL","TRENN")
	FOR I=8:1:13 DO EAN131("CSL","C"_$EXTRACT(N,I))
	DO EAN131("SPSL","STOP")
	QUIT $$BAR(CD,E)
	
EAN131(SLS,COD) ;
	SET SLS=$PIECE(@PTR@(SLS,1),"~",2)
	SET COD=$PIECE(@PTR@(COD,1),"~",2)
	FOR J=1:1:$LENGTH(COD) DO  SET CD=CD_CO
	. SET CH=$TRANSLATE($EXTRACT(SLS,J),"SL","13")
	. SET CO="",$PIECE(CO,CH,$EXTRACT(COD,J))=CH
	. QUIT
	QUIT
	
ITF14 ; 13 stelliger EAN-Code + 1 ;EAN Code 
	NEW C,CD,CH,CO,CW,I,J,P 
	SET P=1,N=N_$$PR1(31)
	SET CD="" 
	QUIT:$TRANSLATE(N,"0123456789")'="" CD 
	;QUIT:$LENGTH(N)'=14 CD
	SET CW=$PIECE(@PTR@("W"_$EXTRACT(N),1),"~",2)
	DO ITF141("STSL","START")
	FOR I=2:1:7 DO ITF141($EXTRACT(CW,I-1)_"SL",$EXTRACT(CW,I-1)_$EXTRACT(N,I))
	DO ITF141("TRSL","TRENN")
	FOR I=8:1:$L(N) DO ITF141("CSL","C"_$EXTRACT(N,I))
	DO ITF141("SPSL","STOP")
	QUIT $$BAR(CD,E)
	
ITF141(SLS,COD) ;
	SET SLS=$PIECE(@PTR@(SLS,1),"~",2)
	SET COD=$PIECE(@PTR@(COD,1),"~",2)
	FOR J=1:1:$LENGTH(COD) DO  SET CD=CD_CO
	. SET CH=$TRANSLATE($EXTRACT(SLS,J),"SL","13")
	. SET CO="",$PIECE(CO,CH,$EXTRACT(COD,J))=CH
	
	QUIT
	
CD10 ;
	FOR J=1:1:$LENGTH(C) SET CD=CD_$TRANSLATE($EXTRACT(C,J),"10","21")_"5"
	QUIT
	
CDSL ;
	FOR J=1:1:$LENGTH(C) SET CD=CD_$TRANSLATE($EXTRACT(C("SL"),J),"SL",C($EXTRACT(C,J)))
	QUIT
	
PR1(W) ;
	QUIT:'$DATA(P) "" 
	QUIT:'P "" 
	SET P=0 
	FOR I=1:1:$LENGTH(N) SET P=$EXTRACT(W,I#2+1)*$EXTRACT(N,I)+P
	QUIT 10-(P#10)#10
	
PR2(W) ;
	QUIT:'$DATA(P) "" 
	QUIT:'P "" 
	SET P=0 
	FOR I=1:1:$LENGTH(N) SET P=$FIND(W,$EXTRACT(N,I))-2+P
	QUIT $EXTRACT(W,P#$LENGTH(W)+1)
	
PR3(W) ;
	QUIT:'$DATA(P) "" 
	QUIT:'P "" 
	SET P=0 
	FOR I=1:1:$LENGTH(N) SET P=$FIND(W,$EXTRACT(N,I))-2+P
	QUIT $EXTRACT(W,16-(P+16+16#16)+1)
	
BAR(X,E) ;BARCODE BILDEN ;shape 
	NEW I 
	SET:'E E=12 ;DRUCKVERHAELTNIS
	SET E(1)="",$PIECE(E(1),1,E\10)=1
	SET E(2)="",$PIECE(E(2),1,E#10)=1
	SET E(3)="",$PIECE(E(3),0,E\10)=0
	SET E(4)="",$PIECE(E(4),0,E#10)=0
	SET E(5)="",$PIECE(E(5),0,E\10)=0
	SET E(6)="",$PIECE(E(6),0,E#10*1.5)=0
	SET E(0)="" 
	FOR I=1:1:$LENGTH(X) SET E(0)=E(0)_E($EXTRACT(X,I))
	QUIT E(0)
	
]]></Routine>
</Export>