<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHCTaxLocationAccounts" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHCTaxLocationAccounts	; COM Health Checks
	#include COMConst
 
 
Check(Start="",End="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; A Top Level Tax Location must have a GL Account
    ;
    ; Returns:
    ;
    ; History:
    ; 06-May-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
    ;
	new strStatus,idTaxLocation,objCOMTAXLocation,intErrorCounter
	
	set strStatus=1
	set intErrorCounter=0
	kill Result
 
	set idTaxLocation=""
	for {
		set idTaxLocation=$order(^COMTAXLocations(YM,2," ",idTaxLocation))
		quit:idTaxLocation=""
		set objCOMTAXLocation=$get(^COMTAXLocation(YM,idTaxLocation,1))
		if $$$COMTAXLocationSellingGLAccount(objCOMTAXLocation)="" {
			set intErrorCounter=intErrorCounter+1
			set Result("Hash","Error No. "_intErrorCounter)="Tax Location '"_idTaxLocation_"' is a Top Level Tax Location and must have a GL Account setup."
			set strStatus=0
		}
	}
	if strStatus=1 {
		set strStatus=2  ; Because this is a Standalone Check return 2 if Ok
	}
 
	quit strStatus
]]></Routine>
</Export>