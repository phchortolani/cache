<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMultiLockClear" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMultiLockClear
	
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; NOTE Distinction between globals used
	;  - ^CacheTempMultiLock	(formheader,formline,key,namespace,user) = tmsLock
	;  - ^CacheTempMultiLocks	(namespace,user,formheader,formline,key)
	;  - ^CacheTempMultiLockIds	(user,namespace,keys)
	;-------------------------------------------------------------------------------
	
OnBeforeButtonLine()
	;-------------------------------------------------------------------------------
	; Routine to run before button line.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	do BuildUserList()
	quit
	
	
BuildUserList()
	;-------------------------------------------------------------------------------
	; Build the user list of people who own locks.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 24-May-2011	GRF		SR17732: replace Order macros
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	new objWWWUSER,strYUSER,idUser,strName,strNS
	
	kill ^COMTempList(0,YUSER,"UserList")
	set strNS = $zutil(5)
	
	set strYUSER = ""
	for {
		set strYUSER = $order(^CacheTempMultiLocks(strNS,strYUSER))
		quit:strYUSER=""
		
		set objWWWUSER = $get(^WWWUSER(0,strYUSER,1))
		set idUser 	   = $$$WWWUSERUser1(objWWWUSER)
		set strName    = $$$WWW013Name($get(^WWW013(0,idUser,1)))
		set ^COMTempList(0,YUSER,"UserList",strYUSER,1) = $$$JSText(idUser_" ("_strName_")")
	}
	quit
	
	
OnAfterSave()
	;-------------------------------------------------------------------------------
	; Routine to run after submitting the form (saving).
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	new strNS
	
	set strNS = $zutil(5)
	
	set ^CacheTempYUSER(strNS,YUSER) = $get(VORG(1))
	do ReloadForm^COMUtilForm()
	quit
	
	
DefaultUser()
	;-------------------------------------------------------------------------------
	; Return the default YUSER for the drop-down
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	new strDef,strNS
	
	set strNS = $zutil(5) 
	set strDef = $get(^CacheTempYUSER(strNS,YUSER))
	if (strDef'="") {
		if '$data(^CacheTempMultiLocks(strNS,strDef)) {
			set strDef=""
			kill ^CacheTempYUSER(strNS,YUSER)
		}
	}
	quit strDef
	
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Routine to run after data fields.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 24-May-2011	GRF		SR17732: replace Order macros
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	new blnShared,idClass,idFormHeader,idFormLine,idYUSER,lstHeader
	new strKey,tmsLock,strNS
	
 	set strNS   = $zutil(5)
	set idYUSER = $get(^CacheTempYUSER(strNS,YUSER))
	
	if (idYUSER'="") && ($data(^CacheTempMultiLocks(strNS,idYUSER))) {
		set lstHeader  = ""
		set lstHeader = lstHeader_$listbuild($$^WWWTEXT(33143))  ; Header
		set lstHeader = lstHeader_$listbuild($$^WWWTEXT(179)) 	 ; Line
		set lstHeader = lstHeader_$listbuild($$^WWWTEXT(59)) 	 ; Primary Key
		set lstHeader = lstHeader_$listbuild($$^WWWTEXT(23)) 	 ; Date / Time
		
		do Start^COMTable(lstHeader,$$^WWWTEXT(33323))			 ; LockFile
		
		set idFormHeader = ""
		for {
			set idFormHeader = $order(^CacheTempMultiLocks(strNS,idYUSER,idFormHeader))
			quit:idFormHeader=""
			
			set idFormLine = ""
			for {
				set idFormLine = $order(^CacheTempMultiLocks(strNS,idYUSER,idFormHeader,idFormLine))
				quit:idFormLine=""
				
				set idClass = $piece($get(^WWW120(0,idFormLine,1)),"~",11)
				set blnShared = $piece($get(^WWW001(0,idClass,1)),"~",3)
				set strKey = ""
				for {
					set strKey = $order(^CacheTempMultiLocks(strNS,idYUSER,idFormHeader,idFormLine,strKey))
					quit:strKey=""
					continue:($piece(strKey,$$$COMMA,1)'=YM)&&('blnShared) // not for this company
					
					set tmsLock = $get(^CacheTempMultiLock(idFormHeader,idFormLine,strKey,strNS,idYUSER))
					do NewLine^COMTable()
					do InsertCell^COMTable(idFormHeader)
					do InsertCell^COMTable(idFormLine)
					do InsertCell^COMTable($piece(strKey,",",2,99))
					do InsertCell^COMTable($$$FormatTimeStamp(tmsLock))
					do EndLine^COMTable()
				}
			}
		}
		do Stop^COMTable()
	}
	quit
	
	
Clear()
	;-------------------------------------------------------------------------------
	; Clear the locks
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	new idYUSER,strNS
	
	set strNS = $zutil(5) 
	set idYUSER = $get(^CacheTempYUSER(strNS,YUSER))
	if (idYUSER'="") do ClearOld^WWWMultiLock(idYUSER)
	
	do ReloadForm^COMUtilForm()
	quit
	
	
OnAfterFormConstruction()
	;-------------------------------------------------------------------------------
	; Routine to run after form construction of the session id field
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 18-Jan-2007	SteveS	SR15355: Created
	;-------------------------------------------------------------------------------
	if '$data(^COMTempList(0,YUSER,"UserList")) {
		write $$$Text("WWW00062")  ; "No locks in system"
	}
	quit
	
]]></Routine>
</Export>