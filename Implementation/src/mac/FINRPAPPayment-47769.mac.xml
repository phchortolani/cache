<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPAPPayment" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPAPPayment
	#include COMSYS
	#include FINConst
 
DiscAmount(pidPay,pidPayLine,pidCompany)
	;-------------------------------------------------------------------------------
	; Get the total amount of applicable discount on a supplier payment line
	;
	; Returns: float (currency)
	;
	; History:
	; 19-Oct-2005	Steve S		SR12828: Created
	;-------------------------------------------------------------------------------
	new curAmount,idInv,objInvoice
 
	set curAmount=0
	
	if ($get(pidPay)'="")&&($get(pidPayLine)'="")&&($get(pidCompany)'="") {
		set idInv=""
		for {
			set idInv=$order(^FINAPPaymentInvoice(pidCompany,pidPay,pidPayLine,idInv))
			quit:idInv=""
			
			set objInvoice=$get(^FINAPPaymentInvoice(pidCompany,pidPay,pidPayLine,idInv,1))
			if $$$FINAPPaymentInvoiceDiscountApplied(objInvoice)&&$$$FINAPPaymentInvoicePay(objInvoice) {
				set curAmount=curAmount+$$$FINAPPaymentInvoiceDiscount(objInvoice)
			}	
		}
	}
 
	quit curAmount
	
TotalPaymentValue(pidPay,pidCompany)
	;-------------------------------------------------------------------------------
	; Get the total value of a payment run
	;
	; Returns: float (currency)
	;
	; History:
	; 20-Oct-2005	Steve S		SR12828: Created
	;-------------------------------------------------------------------------------
	new YM
	set YM=pidCompany
	quit $$$FCBase($$PaymentTotal^FINAPPayment(pidPay))
	
	
OnBeforeRunning(pidPayment)
	;-------------------------------------------------------------------------------
	; Routine to run before running a report
	;
	; Returns:
	;
	; History:
	; 21-Oct-2005	Steve S		SR12828: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$CanPrint^FINAPPayment2(pidPayment)
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit strStatus
	
GetSupplierName(pidSupplier,pidCompany)
	;-------------------------------------------------------------------------------
	; Get a supplier's name
	;
	; Returns: String
	;
	; History:
	; 21-Oct-2005	Steve S		SR12828: Created
	;-------------------------------------------------------------------------------
	new strName
 
	set strName=""
	
	if ($get(pidCompany)'="")&&($get(pidSupplier)'="") {
		set strName=$$$FINAPSupplierSupplierName($get(^FINAPSupplier(pidCompany,pidSupplier,1)))
		if strName="" set strName=pidSupplier
	}
 
	quit strName
	
	
GetRemittanceName(pidSupplier,pidCompany)
	;-------------------------------------------------------------------------------
	; Return the remittance name for a supplier (if it exists)
	;
	; Params: pidSupplier	: For this supplier
	; 		  pidCompany	: For this company (YM)
	;
	; ByRefs:
	;
	; Returns: strName
	;
	; History:
	; 10-Nov-2006	Steve S		SR14516: Created
	;-------------------------------------------------------------------------------
	new YM,strName
 
	set strName=""
	set YM = $get(pidCompany)
 
	if (YM'="")&&($get(pidSupplier)'="") {
		set strName=$$GetRemittanceName^FINAPSupplierUtils(pidSupplier,$$$NO)
	}
 
	quit strName
]]></Routine>
</Export>