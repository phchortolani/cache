<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewSetup" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewSetup
    ;-------------------------------------------------------------------------------
	; Sets up screen for COM View Use.
    ;-------------------------------------------------------------------------------
#include WWWConst
#include COMConst
#include COMSYS
	
Setup()
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Aug-2006	JW		SR14763: Added YM to JS file. (Temp fix)
	; 15-Jul-2005	shobby	SR12754: Replaced LANGUAGE global (not always reliable)
	; 11-Feb-2005	PK & PO	SR10965: Changed over to handle JS being sourced from
	; 							multiple routines.
	;-------------------------------------------------------------------------------
	new dteDate,strLanguageCheckSum
	
	set dteDate = $get(^rMAC("COMViewSetupJS",0))
	set dteDate = $$Max^COMUtilDate(dteDate,$get(^rMAC("COMViewSetupJS1",0)))
	set dteDate = $$Max^COMUtilDate(dteDate,$get(^rMAC("COMViewSetupJS2",0)))
	set dteDate = $$Max^COMUtilDate(dteDate,$get(^rMAC("COMViewSetupJS3",0)))
	set dteDate = $$Max^COMUtilDate(dteDate,$get(^rMAC("COMViewSetupJS4",0)))
	set dteDate = $$Max^COMUtilDate(dteDate,$get(^rMAC("COMViewStructure",0)))
	
	set strLanguageCheckSum = $$$COMViewLanguageTextText($get(^COMViewLanguageText(0,SPRACHE,1)))
 	
 	//do CreateFile("COMView"_SPRACHE,"do Setup^COMViewSetupJS()",$$$EnumCOMFILETYPEJavaScript,dteDate,strLanguageCheckSum)		SR14763
 	do CreateFile("COMView"_SPRACHE_"_"_YM,"do Setup^COMViewSetupJS()",$$$EnumCOMFILETYPEJavaScript,dteDate,strLanguageCheckSum)
 	do CreateFile("COMView"_YM,"do Setup^COMViewSetupStyle()",$$$EnumCOMFILETYPEStyleSheet,)
 	do Setup^COMViewSetupVB()
 	
 	quit 
 	
 	
CreateFile(pstrFile,pstrCodeToPopulate,pstrType=$$$EnumCOMFILETYPEJavaScript,pdteRefresh="",pstrLanguageText="")
    ;-------------------------------------------------------------------------------
    ; Sets links to files in @Net screens, potentially refreshes the file if it is the incorrect age.
    ;
    ; History:
    ; 17-Mar-2011	shobby	SR17249: put 16677 changes back in.
    ; 10-Aug-2010	shobby	SR17489: As a result of SR17483, header lines of js/css files not
    ; 							being interpreted correctly, leading to crash.
    ; 02-Aug-2010	shobby	SR17483: Corrected invalid comment tag syntax.
    ; 14-Oct-2009	GRF		SR16894: Clear old commented code
    ; 22-Sep-2009	shobby	SR16894: When charset is UTF8 write the js files in UTF8.
    ; 							Removed changes from 16677.
    ; 26-Jun-2009	shobby	SR16677: Add iso charset direction to Script language
    ; 02-Feb-2006	RPW		SR14093: Do not use !, it causes issues with Debugging this code!
    ; 29-Aug-2005	sh/rw	SR13356:More robust handling of missing or incomplete js files.
    ; 29-Aug-2005	shobby	SR13356:Put $get around strLanguageText
    ; 14-Jul-2005	shobby	SR12754:Changes to determine if Language texts have changed.
    ; 17-Jun-2005	RPW		Fixed status check so in line with coding standards, ie
    ; 							$$$ISOK(strStatus), $$$ISERR(strStatus), $$$ISOK.
    ; 							Also do not $$$Text a value that is already $$$Texted.
    ; 01-Jun-2005	PK		Added Status to check for file able to be created.
    ; 09-Mar-2005	PK		Added Fast file check
    ; 09-Mar-2005	PK		Modifed to look at the first line of the file for a
    ; 							modified date. (ST#11847) && SR#11850)
    ;-------------------------------------------------------------------------------
	new blnError,blnGenerate,io,ipio,objCompany,objConfig
	new strDate,strEnd,strEnd1,strFile,strLanguageText,strMAC,strStart,strStart1,strStatus
	
	set objConfig  = $get(^COMViewConfig(0,YM,1))
	set objCompany = $get(^WWW012(0,YM,1))
	set strStart   = ""
	set strMAC     = $piece($piece(pstrCodeToPopulate,"^",2),"(",1)
	set strStatus  = $$$OK
	
	set pdteRefresh = $$Max^COMUtilDate(pdteRefresh,$get(^rMAC(strMAC,0)))
	set pdteRefresh = $$Max^COMUtilDate(pdteRefresh,$$$COMViewConfigModifiedDate(objConfig))
	
	if pstrType=$$$EnumCOMFILETYPEJavaScript {
		set pstrFile  = pstrFile_".js"
		;SESDF-822 set strStart="<script language=javascript charset='iso-8859-1' type='text/javascript' SRC="""_$$$WWW012PictureDirectorySystem(objCompany)_pstrFile_""">" ;SR17249
		set strStart="<script language=javascript charset='"_$$CharacterSet^WWW012()_"' type='text/javascript' SRC="""_$$$WWW012PictureDirectorySystem(objCompany)_pstrFile_""">" ;SR17249 ;SESDF-822 
	;SR17249 set strStart  = "<script language=javascript type='text/javascript' SRC="""_$$$WWW012PictureDirectorySystem(objCompany)_pstrFile_""">"  ; SR16677
		set strEnd    = "</script>"
		set strStart1 = "<script>"
		set strEnd1   = "</script>"
	
	} elseif pstrType=$$$EnumCOMFILETYPEStyleSheet {
		set pstrFile  = pstrFile_".css"
		set strStart  = "<LINK REL=""stylesheet"" TYPE=""text/css"" HREF="""_$$$WWW012PictureDirectorySystem(objCompany)_pstrFile_""">"
		set strEnd    = ""
		set strStart1 = "<style>"
		set strEnd1   = "</style>"
		
	} elseif pstrType=$$$EnumCOMFILETYPEVBScript {
		set pstrFile  = pstrFile_".vbs"
		set strStart  = "<script language=vbscript SRC="""_$$$WWW012PictureDirectorySystem(objCompany)_pstrFile_""">"
		set strEnd    = "</script>"
		set strStart1 = "<script>"
		set strEnd1   = "</script>"
	}
	
	if strStart'="" {
		if $$$COMViewConfigDevelopmentMode(objConfig) {
			write YCR,strStart1,YCR
			xecute pstrCodeToPopulate
			write strEnd1,YCR
			
		} else {
			write YCR,strStart,YCR
			
			set io = ##Class(%File).NormalizeDirectory($$$WWW012PhysicalWWWDirectory(objCompany))_pstrFile
			set blnGenerate = $$$NO
			//
			
			if '##Class(%Library.File).Exists(io) {
				set blnGenerate     = $$$YES
				set strLanguageText = $$UpdateLanguageText^COMViewSetupJS()
			
			} elseif $$$COMViewConfigFastFileCheck(objConfig) {
				if $$$COMViewFileModifiedDate($get(^COMViewFile(0,io,1)))'=pdteRefresh set blnGenerate = $$$YES
			
			} else {
				set ipio = $io
				open io:"rs":0
				use io
				read strDate
				set strDate = $piece(strDate,": ",2)
				set strDate = $$ConvertDate(strDate)
				
				if strDate'=pdteRefresh {		// SR13913 - these are not matching
					set blnGenerate     = $$$YES
					set strLanguageText = $$UpdateLanguageText^COMViewSetupJS()
					if strLanguageText="" {
						set strLanguageText = pstrLanguageText
					}
				}
				if $get(strLanguageText)="" {
					set blnError = $$ReadFile(.strLanguageText)
					if blnError {
						set blnGenerate     = $$$YES
						set strLanguageText = $$UpdateLanguageText^COMViewSetupJS()
					} else {
						if ($piece(strLanguageText,": ",2)'=pstrLanguageText) && (pstrLanguageText'="") {
							set blnGenerate     = $$$YES
							set strLanguageText = pstrLanguageText
						}
					}
				}
				use ipio
				close io
				;if YBED["SHOBBY" for l=1:1:2 hang 1 ;SR17669
			}
			
			if blnGenerate {
				set $$$COMViewFileModifiedDate(^COMViewFile(0,io,1)) = pdteRefresh
				set ipio = $io
				; SR16894 VVVV
				
				if $$$WWW012CharacterSet(objCompany)=7 {  ;UTF8
					open io:("wns":/IOTABLE="UTF8"):1  else  set strStatus = $listbuild("Com00190",io)  ; "Unable to Create File %1. Check Setup."
				} else {
					open io:"wns":1  else  set strStatus = $listbuild("Com00190",io)  ; "Unable to Create File %1. Check Setup."
				}					
				; SR16894 ^^^^
				if $$$ISOK(strStatus) {
					use io
					write "/* Modified Date: "_$zdt(pdteRefresh,3)_": */"					;SR17483 ;SR17489
					write $c(13,10)_"/* Modified Language: "_$get(strLanguageText)_": */"	;SR17483 ;SR17489
					xecute pstrCodeToPopulate
					use ipio
					close io
				}
			}
			write strEnd,YCR
		}
	}
	if $$$ISERR(strStatus) {
		write "<br>"_$$$Text(strStatus)
	}
	quit
	
	
ReadFile(pstrText="")
	new blnError
	
	set blnError = $$$NO
	set $ztrap   = "ReadFileError"
	read pstrText
	quit blnError
	
ReadFileError ; Internal Tag
	set $ztrap   = ""
	set blnError = $$$YES
	quit blnError
	
ConvertDate(pstrDate)
	set $ztrap   = "ConvertDateError"
	set pstrDate = $zdth(pstrDate,3)
	quit pstrDate
	
ConvertDateError
	set $ztrap = ""
	quit ""
	
]]></Routine>
</Export>