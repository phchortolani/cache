<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvoiceFilter" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvoiceFilter ; Invoice Filter
	
#include COMSYS
#include FINConst
#include WWWConst
	
QueryCheck(pKey="",pobjInvoice="",pidCompany="")
    ;-------------------------------------------------------------------------------
    ; Check Invoice filter for Query
    ; 
    ; pKey = Customer,InvoiceNumber
    ; pobjInvoice = Invoice Details
    ;
    ; Returns:
    ;
    ; History:
    ; 27-May-2005	SCR		Created SR12220:
    ;-------------------------------------------------------------------------------
    ;
    if pidCompany="" set pidCompany=$get(YM)
	new objSettings,blnOK,FilterType
	set blnOK=$$$NO
	set objSettings=$get(^FINGeneralSetup(pidCompany,pidCompany,1))
	set FilterType=+$$$FINGeneralSetupCustomerQueryInvoiceFilte(objSettings)
	if (FilterType=1)||(FilterType=0) {
		set blnOK=$$$YES
	} elseif (FilterType=2) {
		if $get(YLOCATION)=$$$FINARInvoiceLocation(pobjInvoice) {
			set blnOK=$$$YES
		}
	} 
 
	quit blnOK
 
HasFilter(pidCompany,&pidFilterType)
	;-------------------------------------------------------------------------------
	; Determine whether there is a filter
	;
	; Params:
	; pidCompany - Company Id
	;
	; ByRefs:
	; pidFilterType - Current filter
	;
	; Returns: Whether there is a filter
	;
	; History:
	; 16-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new objSettings
 
	set objSettings = $get(^FINGeneralSetup(pidCompany,pidCompany,1))
	set pidFilterType = +$$$FINGeneralSetupCustomerQueryInvoiceFilte(objSettings)
 
	quit '((pidFilterType = 1) || (pidFilterType = 0))
 
Filter(pobjInvoice,pidFilter,pidLocation="")
	;-------------------------------------------------------------------------------
	; Apply filter
	;
	; Params:
	; pobjInvoice - Invoice record
	; pidFilter - Filter to apply
	; pidLocation - Location Id ~ OPTIONAL
	;
	; ByRefs: None
	;
	; Returns: Whether or not to include this record
	;
	; History:
	; 16-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new blnOK
	set blnOK = $$$NO
 
	if pidFilter = 2 {
		if pidLocation = "" set pidLocation = $get(YLOCATION)
		if pidLocation '= $$$FINARInvoiceLocation(pobjInvoice) {
			set blnOK = $$$YES
		}
	}
 
	quit blnOK
 
QueryStatus(pidSup="")
    ;-------------------------------------------------------------------------------
    ; Return Text that shows Status of Query Filter
    ; 
    ; pidSup = Customer
    ;
    ; Returns: Text
    ;
    ; History:
    ; 16-Aug-2006	PO		SR14549: Use Filter
    ; 30-May-2005	SCR		Created SR12220:
    ;-------------------------------------------------------------------------------
    ;
    new strText,FilterType,objSettings
    set strText=""
	set objSettings=$get(^FINGeneralSetup(YM,YM,1))
	set FilterType=+$$$FINGeneralSetupCustomerQueryInvoiceFilte(objSettings)
	if (FilterType=1)||(FilterType=0) {
		set strText=$$$Text("Fin00779") ; No Filter
	} elseif (FilterType=2) {
		set strText=$$$Text($listbuild("Fin00780",$get(YLOCATION))) ; if $get(YLOCATION)=$$$FINAPInvoiceLocation(pobjInvoice) {
	} 
 
	quit strText
 
BankingLocation(pKey="",pUser="") 
    ;-------------------------------------------------------------------------------
    ; Get Location for Customer Banking Based on Finance Settings
    ;
    ; Returns: Location (or Null)
    ;
    ; History:
    ; 16-Aug-2006	PO		SR14549: Use Filter
    ; 27-May-2005	SCR		Created SR12220:
    ;-------------------------------------------------------------------------------
    ;
    new Location,objSettings,blnOK,FilterType
	set Location="" 
	set objSettings=$get(^FINGeneralSetup(YM,YM,1))
	set FilterType=+$$$FINGeneralSetupCustomerBankingInvoiceFil(objSettings)
	if (FilterType=2) {
		set Location=YLOCATION
	} 
    quit Location
 
 
]]></Routine>
</Export>