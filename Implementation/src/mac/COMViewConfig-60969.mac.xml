<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewConfig" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewConfig
	
#include COMSYS
#include COMConst
	
UseKeyStroke()
	;-------------------------------------------------------------------------------
	; Determines whether we are using keystroke or not
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns: boolean
	;
	; History:
	; 09-Feb-2007	RPW			SR15426: If this is an In Form COMView, alway shows
	; 								the tick button for manual submission
	; 08-Jun-2006	Steve S		SR14613: Created
	;-------------------------------------------------------------------------------
	new enumSubmission
	
	set enumSubmission = $$$COMViewConfigSearchSubmission($get(^COMViewConfig(0,0,1)))
	if enumSubmission="" set enumSubmission = $$$EnumCOMVIEWSEARCHSUBMISSIONKeystrokeDelay
	
	if $get(^CacheTempView(YUSER,YUCI,"InForm")) {
		set enumSubmission = $$$EnumCOMVIEWSEARCHSUBMISSIONManual
	}
	quit (enumSubmission=$$$EnumCOMVIEWSEARCHSUBMISSIONKeystrokeDelay)
	
	
GetMaximum(pblnLoadAll)
	;-------------------------------------------------------------------------------
	; Get the Maximum Results Returned from COMViewConfig.
	; If this is blank get the default from the COMVIEWCOUNT application parameter.
	;
	; Params:
	; pblnLoadAll: If this is true, then always return 0.
	;
	; ByRefs:
	;
	; Returns:
	; 0,1000 or the number entered by the user.
	;
	; History:
	; 04-Sep-2006	RPW		SR15005: Created.  JW - changed enum to 1000.
	;-------------------------------------------------------------------------------
	new intMaxCount
	
	set intMaxCount = 0
	if 'pblnLoadAll {
		set intMaxCount = $$$COMViewConfigMaximumResultsReturned($get(^COMViewConfig(0,0,1)))
		if intMaxCount="" set intMaxCount = 1000
	}
	quit intMaxCount
	
	
OnBlur(pidField,pobjConfig)
	;-------------------------------------------------------------------------------
	; If the value is negative make it blank
	;
	; Params:
	; pidField  : Which field was changed
	; pobjConfig: The COMViewConfig Object
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 18-Dec-2010	shobby	SR15753: Removed redundant call.
	; 04-Sep-2006	RPW		SR15005: Created
	;-------------------------------------------------------------------------------
	if pidField=$$$FldCOMViewConfigMaximumResultsReturned {
		if $$$COMViewConfigMaximumResultsReturned(pobjConfig)<0 {
			set $$$COMViewConfigMaximumResultsReturned(pobjConfig) = ""
		}
	}
	quit
	
	
ExpandSelection()
	quit $$$COMViewConfigExpandSelection($get(^COMViewConfig(0,0,1)))
	
]]></Routine>
</Export>