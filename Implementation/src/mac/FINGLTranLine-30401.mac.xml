<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLTranLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLTranLine
	
#include COMSYS
#include FINConst
#include WWWConst
	
GetTransactionType(pidTransaction)
    ;-------------------------------------------------------------------------------
    ;
    ; Returns:
    ;
    ; History:
	; 10-May-2005	SteveS	SR12315: Call to GetTransactionBatchType
	; 14-Apr-2005	RPW		SR12159: Make sure that FINGLTranType is no longer shared.
    ;-------------------------------------------------------------------------------
	new strTransactionType,objTransaction,idTranType,objTranType
 
	set strTransactionType = ""
	
	if $get(pidTransaction)'="" {
		set strTransactionType = $$GetBatchDescription^FINGLTran(pidTransaction) ;SR12315
		if strTransactionType="" {
			set objTransaction = $get(^FINGLTran(YM,pidTransaction,1))
			if objTransaction'="" {
				set idTranType         = $$$FINGLTranTransactionType(objTransaction)
				set objTranType        = $get(^FINGLTranType(YM,idTranType,1))
				set strTransactionType = $$$FINGLTranTypeDescription(objTranType)
			}
		}		
	}
	quit strTransactionType
	
	
GetTransactionKey(pidTransaction)
	;-------------------------------------------------------------------
	; Get the Key for the transaction type on an Transaction
	; 
	; History
	; 30-Jun-2003	shobby	Creation (TR005505)
	;-------------------------------------------------------------------
	new idTranType,objTransaction
	
	set objTransaction=$get(^FINGLTran(YM,pidTransaction,1))
	if objTransaction'="" {
		set idTranType=$$$FINGLTranTransactionType(objTransaction)
	}
	quit idTranType
	
	
FindAmount(pcurAmount) // ??? Is this in use
	;-------------------------------------------------------------------------------
	; Finds any FINGLTranLines with amounts that match the passed in amount
	;
	; Returns:
	;
	; History:
	; 13-Nov-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new idxTransaction,idxNumber,objFINGLTranLine
	
	do ^WWWVAR
	set idxTransaction=""
	for {
		set idxTransaction=$order(^FINGLTranLine(YM,idxTransaction))
		quit:idxTransaction=""
		set idxNumber=""
		for {
			set idxNumber=$order(^FINGLTranLine(YM,idxTransaction,idxNumber))
			quit:idxNumber=""
			set objFINGLTranLine=$get(^FINGLTranLine(YM,idxTransaction,idxNumber,1))
			if +$$$FINGLTranLineDebit(objFINGLTranLine)=+pcurAmount {
				write idxTransaction_","_idxNumber_"  Debit",!
			}
			if +$$$FINGLTranLineCredit(objFINGLTranLine)=+pcurAmount {
				write idxTransaction_","_idxNumber_"  Credit",!
			}
			if +$$$FINGLTranLineDebit(objFINGLTranLine)=-pcurAmount {
				write idxTransaction_","_idxNumber_"  Debit -",!
			}
			if +$$$FINGLTranLineCredit(objFINGLTranLine)=-pcurAmount {
				write idxTransaction_","_idxNumber_"  Credit -",!
			}
		}
	}
	quit
	
 
OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Checks whether this item can be deleted.
	; 
	; Notes: (Do not remove the following notes, used by 'Structure')
	; 		**Delete Check** FINGLBankReconCleared - FINGLTranLine can not be deleted.
	; 		**Delete Check** FININTBatchLine - FINGLTranLine can not be deleted.
	; 
	; History
	; 10-Mar-2004	shobby	Creation
	;-------------------------------------------------------------------------------
	quit $$$OK
	
GetTransactionAmount(pidCompany,pcurDebit,pcurCredit)
	;-------------------------------------------------------------------------------
	; Handler for the Calculate field on FINGLTranLine called Amount
	;
	; Params:
	; pidCompany: The company we are in
	; pcurDebit : The amount to debit (includes Foreign Currency)
	; pcurCredit: The amount to credit (includes Foreign Currency)
	;
	; ByRefs:
	;
	; Returns:
	; The base amount of Debit - Credit, with correct decimal places.
	;
	; History:
	; 29-Jun-2006	SteveS	SR14603: (Peer Review) Changed curamount to curAmount
	; 27-Jun-2006	RPW		SR14603: Created
	;-------------------------------------------------------------------------------
	new curAmount
	
	set curAmount=$$$FCSub(pcurDebit,pcurCredit)
	
	quit $$^WWWZAHL($$$FCBase(curAmount),,,$$$WWW012StandardCurrency($get(^WWW012(0,pidCompany,1))),,,,$$$NO)
	
]]></Routine>
</Export>