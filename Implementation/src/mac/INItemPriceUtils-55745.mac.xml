<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INItemPriceUtils" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INItemPriceUtils
	
#include COMSYS
#include INConst
	
GetPriceLocation(pidLocn)
	;-------------------------------------------------------------------------------
	; Get the Pricing Location for the Current Location 
	;
	;
	; Inputs:
	;
	; Returns: Pricing Location
	;
	; History:
	; 11-Nov-2009	GRF		SR16842: Missing ",1" in class get
	; 27-Aug-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	new idPriceLocn,objLocation
	
	set objLocation = $get(^WWW0121(0,0,pidLocn,1))
	set idPriceLocn	= $$$WWW0121PricingLocation(objLocation)
	
	quit idPriceLocn
	
	
GetParentLocation(pidLocn)
	;-------------------------------------------------------------------------------
	; Get the Pricing Location for the Parent Location 
	;
	;
	; Inputs:
	;
	; Returns: Parent Location
	;
	; History:
	; 11-Nov-2009	GRF		SR16842: Missing ",1" in class get
	; 27-Aug-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	new idParentLocn,objLocation
	
	set objLocation  = $get(^WWW0121(0,0,pidLocn,1))
	set idParentLocn = $$$WWW0121ParentLocn(objLocation)
	
	quit idParentLocn


GetPPLineRecord(pstrPPLine)
	;-------------------------------------------------------------------------------
	; Get the Pricing Record ID given a PPLine Identifier
	;
	;
	; Inputs:
	;
	; Returns: Record ID
	;
	; History:
	; 03-Sep-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	new idSeq,SQLCODE
	
	set idSeq = ""	
	&SQL(SELECT Sequence INTO :idSeq FROM INItemPrice WHERE PricePreviewLine = :pstrPPLine)
	
	quit idSeq


	/// ***************************************************************************************///	
	/// ******* THE FOLLOWING ROUTINES ARE CALLED BY CALCULATED FIELDS ON INItemPrice *********///
	/// ***************************************************************************************///

GetSystemPackCost(pidItem,pidUnit,pidLocn)
	;-------------------------------------------------------------------------------
	; Gets the System Pack Cost = Average Cost * UOM Conversion to Inv UOM
	; Called by Calculated field on INItemPrice - SystemPackCost
	; 
	; Called By: Class INItemPrice : Calculated Field 2 "SystemPackCost"
	; 
	; Inputs:
	;
	; Returns: 
	;
	; History:
	; 28-Oct-2009	GRF		SR16871: INAvgCost with cost centre replaces INARTAVG;
	; 							add location parameter in and to INPP01Preview
	; 03-Sep-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	$$$VAR
	new fltCost
	
	set fltCost = ""
	if (pidItem'="") && (pidUnit'="") {
		set fltCost = $$GetSystemPackCost^INPP01Preview(pidItem,pidUnit,pidLocn)
	}
	
	quit $justify(fltCost,0,4)
	
	
GetManualPackCost(pidItem,pidLocn)
	;-------------------------------------------------------------------------------
	; Gets the Manual Pack Cost (entered manually for an Item ot Item Locn)
	; Called by Calculated field on INItemPrice - ManualPackCost
	;
	;
	; Inputs:
	;
	; Returns: 
	;
	; History:
	; 03-Sep-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	$$$VAR
	new fltCost
	
	set fltCost = ""
	if (pidItem'="") && (pidLocn'="") {
		set fltCost = $$GetManualPackCost^INPP01Preview(pidItem,pidLocn)
	}
	
	quit $justify(fltCost,0,4)
	
	
GetCurrentPrice(pidItem,pstrPPreview,pfltCost)
	;-------------------------------------------------------------------------------
	; Get the Current Price based on the Last Pricing Preview Record 
	; Called by Calculated field on INItemPrice - CurrentPrice
	;
	;
	; Inputs:
	;
	; Returns: 
	;
	; History:
	; 11-Nov-2009	GRF		SR16842: Variables not newed
	; 03-Sep-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	new fltPrice,idPP,idPPLine,idRule,idRuleLine,objPreview,objPreviewLine
	
	set fltPrice = ""
	$$$VAR
	
	set idPP     = $$$KEY1(pstrPPreview)
	set idPPLine = $$$KEY2(pstrPPreview)
	
	if (idPP'="") && (idPPLine'="") {
		set objPreview = $get(^INPP01Preview(0,idPP,1))
		set idRule     = $$$INPP01PreviewPricePlanRule(objPreview)
		
		set objPreviewLine = $get(^INPP01PreviewLine(0,idPP,idPPLine,1))
	//	set idRuleLine = $$$INPP01PreviewLinePlanNo(objPreviewLine)
		set idRuleLine = $$GetRule^INPP01PreviewLine(idRule,$$$INPP01PreviewLocation(objPreview),$$$INARTPriceCategory($get(^INART(0,pidItem,1)))," ",pfltCost)

		if (idRule'="") && (idRuleLine'="") {
			set fltPrice = $$GetPrice^INPP01Preview(idRule,idRuleLine,pfltCost)
		}
	}
	
	quit $justify(fltPrice,0,2)
	
	
Variance(pfltPrcOld,pfltPrcNew)
	;-------------------------------------------------------------------------------
	; Get the % Variance
	; Called by Calculated field on INItemPrice - Variance
	;
	;
	; Inputs:
	;
	; Returns: 
	;
	; History:
	; 03-Sep-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	new fltPer

	$$$VAR
	
	if pfltPrcOld {
		set fltPer = (pfltPrcNew-pfltPrcOld)/pfltPrcOld*100
	} else {
		set fltPer = 0	
	}

	quit $justify(fltPer,0,2)
	
]]></Routine>
</Export>