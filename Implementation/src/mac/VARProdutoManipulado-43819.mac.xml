<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARProdutoManipulado" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARProdutoManipulado
	#include COMSYS
	#include COMConst
	#include VARConst
	#include INConst
	
	quit
	
OnBeforeFormConstruction
	set YBACK = $piece(YBACK,YFORM,1)
	
	if (YKEY=""){
		if (YFKEY'=""){
			set YKEY = YFKEY
		} else {
			set YKEY = $get(^VARTempINARTT1(YM,YBED))
		}
	}
	
	set ^CacheTempEvent(YUCI,YFORM,"Combo") = $$$YES
	
	quit

OnBeforeSave1(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Creates INART records from the list provided.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-Nov-2013	shobby		HEVA-1285: Call to update INARTA
	; 20-Nov-2013	shobby		HEVA-1148.40: Set the FREE44 (Não considerar no fechamento contábil)
	; 16-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new objINART,idINART,strStatus,idINARTNew
	
	set idINART=$$$KEY1(pYKEY)
	set idINARTNew=$$$VARProdutoManipuladoCodigoManipulado(pYFELD)
	set objINART=$get(^INART(YM,idINARTNew,1))
	if objINART="" {
		set objINART=$get(^INART(YM,idINART,1))
	}
	set $$$INARTSearchName(objINART)=$$$VARProdutoManipuladoDescricao(pYFELD)
	set $$$INARTItemPolicy(objINART)=6 ;kits
	set $$$INARTFREE44(objINART)=1 ;HEVA-1148.40
	set strStatus=$$Save^COMUtils("INART",idINARTNew,objINART,1)
	if $$$ISOK(strStatus) set strStatus=$$UpdateAlternative(idINART,idINARTNew,pYFELD) ;HEVA-1285
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	quit strStatus

UpdateAlternative(pidINART,pidINARTNew,pYFELD)
	;-------------------------------------------------------------------------------
	; Creates/Updates INARTA record.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 20-Nov-2013	shobby		HEVA-1185: Created
	;-------------------------------------------------------------------------------
	new strStatus,idINARTA,objINARTA,idInventoryUOM
	
	set idINARTA=pidINART_","_pidINARTNew
	set idInventoryUOM=$$GetInventoryUOM^INUOMConversion(pidINART,.strStatus)
	if $$$ISOK(strStatus) {
		set objINARTA=$get(^INARTA(YM,pidINART,pidINARTNew,1))
		set $$$INARTAType(objINARTA)=3 ; Manipulated Alternative
		set $$$INARTAConversion(objINARTA)=$$Convert^INUOMConversion(pidINART,$$$VARProdutoManipuladoQtyConcentracao(pYFELD),$$$VARProdutoManipuladoUnConcentracao(pYFELD),idInventoryUOM,$$$NO)
		if $$$INARTAConversion(objINARTA)'=0 set $$$INARTAConversion(objINARTA)=1/$$$INARTAConversion(objINARTA)
		set strStatus=$$Save^COMUtils("INARTA",idINARTA,objINARTA,1)
	}
	quit strStatus

OnBeforeSave(pYKEY)
	new strStatus
	
	set strStatus = $$$GRIDSave(pYKEY)

	if $$$ISERR(strStatus){
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit

OnBeforeDisplayCombo(pstrKeys,pobj,pintCol,&pSUCH)
	new blnOverride,enumLineType,enumUnit,idClassField,idItem
 
	set blnOverride = $$$NO
	
	if ((pintCol = $$$FldVARProdutoManipuladoUnidade) || (pintCol = $$$FldVARProdutoManipuladoUnConcentracao)) {  
		set blnOverride = $$LoadCombo^INARTPACK($$$KEY1(pstrKeys),blnOverride,.pSUCH)
 	}
 	
	quit blnOverride

OnAfterDataFields(pYKEY)
	set ^VARTempINARTT1(YM,YBED) = pYKEY
	do LoadGrid(pYKEY)

	quit	
	
LoadGrid(pid="")
 	
	new YFORM,YKEY,YAUSWAHL
	set YFORM="VARProdutoManipulado"
	set YFELD=""
	set YKEY=$get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)=1
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)=400
	set $$$COMGridEditParameterGridName(YAUSWAHL)=YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)='($$$WWW120AuthorizationToModifyData(YVOR)=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)="VARManipulado"
	
	$$$GRIDStart(YFORM,YKEY)
	
	quit
	
GetCodigoManipulado(pYKEY)
	new pProdutoMestre, pLinha
	set pProdutoMestre = $$$KEY1(pYKEY)
	set pLinha	 	   = $$$KEY2(pYKEY)
	quit pProdutoMestre_"-M"_pLinha
	
GetCustoProducao(pYKEY,pYFELD)
	new custoProducao, produtoMestre
	set custoProducao = 0
	set produtoMestre = $$$KEY1(pYKEY)
	
	set qty 				= $$$VARProdutoManipuladoQtyConcentracao(pYFELD)
	set unitEstoqueMestre	= $$SQLGetUnitIdItem^VARSQL(produtoMestre)
	set unidadeConcentracao = $$$VARProdutoManipuladoUnConcentracao(pYFELD)
	set qtyUOM 				= $$Convert^INUOMConversion(produtoMestre,qty,unidadeConcentracao,unitEstoqueMestre)
	set custoProducao 		= qtyUOM * $$SQLGetCustoMedioProd^VARSQL(produtoMestre)

	quit $$^WWWTR(0,8,custoProducao,2)
	
CanDelete(pYKEY,pYFELD)
	new produtoManipulado, count
	; ATENÇÃO: Revisar essa implementação após conclusão do caso de uso 1
	set produtoManipulado = $$$VARProdutoManipuladoCodigoManipulado(pYFELD)
	&sql(select count(*) into :count from alSOH.dStockHistory where Item = :produtoManipulado)
	if 'SQLCODE {
		if count > 0 set Q = $$$QDontDelete_$$$Text($$$MakeStatus("IN34361",produtoManipulado))
	}
	quit	
	
OnBeforeDataAccess(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Limits the list of VARProdutoManipulado displayed in the list on the VARManipulacaoMagistral screen
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 16-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$OK
	if $data(^rOBJ(YFORM_"OBDA")) xecute "set strStatus=$$OnBeforeDataAccess^"_YFORM_"OBDA(YKEY,YFELD)"		;SR17034
	quit strStatus
	
isProdutoManipulado(pItem)

	quit $data(^VARProdutoManipulados(0,2,pItem)) > 0
]]></Routine>
</Export>