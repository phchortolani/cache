<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASPublicacao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASPublicacao
#include COMSYS
#include COMConst
 
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Salva o grid e alerta o usuário caso ele tenha não tenha registrado a publicação
    ; pelo menos no mural.
    ;
    ; History:
    ; 27-Aug-2009	Gustavo Fiuza	Changed
    ; 22-Aug-2009	Gustavo Fiuza	Changed
    ; 17-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new strStatus, blnIsPublicado
	set strStatus = $$$OK
	
	set strStatus = $$$GRIDSave(pYKEY)
 
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
	}
	
	set blnIsPublicado = $$checkPublicacaoLancamento(pYKEY)
	
	if (blnIsPublicado = $$$NO) {
		w "javascript:alert('Atenção: é necessário publicar o lançamento da compra no mural. A posição do processo não será atualizada.');"
	}
	
	quit
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Abre grid para registro das publicações do processo de compra.
    ; 
    ; History:
    ; 17-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	w "<br /><strong>Publicações/Divulgações do Processo</strong>"
	do LoadGrid(pYKEY)
	quit
	
	
LoadGrid(pYKEY) 
 	;-------------------------------------------------------------------------------
    ; Carrega o grid.
    ; 
    ; History:
    ; 17-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
 
	new idForm,YAUSWAHL
	set idForm="VARSESCOMPRASPublicacaoLinha"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 300
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($piece(YVOR,Y,23)'=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "VARSESCOMPRASPublicacao"
		
	$$$GRIDStart(idForm,pYKEY)
	
	quit
	
checkPublicacaoLancamento(pYKEY)
 	;-------------------------------------------------------------------------------
    ; Conta o número de publicações de um determinado tipo que foram registradas até
    ; o momento
    ; 
    ; History:
    ; 21-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
	new isPublicado, blnMural, blnPortal, idPublicacao, objPublicacao, tipoPublicacao,
		veiculoPublicacao
	
	set isPublicado = $$$NO
	set blnMural  	= $$$NO
	;set blnPortal 	= $$$NO
	
	$$$Order3(^VARSESCOMPRASPublicacaoLinha,YM,pYKEY,idPublicacao)		
		set objPublicacao = $get(^VARSESCOMPRASPublicacaoLinha(YM,pYKEY,idPublicacao,1))
		
		set tipoPublicacao = $piece(objPublicacao,Y,1)
		continue:(tipoPublicacao '= 1) ;1 - Lançamento
		
		set veiculoPublicacao = $piece(objPublicacao,Y,2)
		if veiculoPublicacao = 1 set blnMural = $$$YES
		;if veiculoPublicacao = 2 set blnPortal  = $$$YES
				
	$$$End
	
	;if (blnPortal = $$$YES) && (blnMural = $$$YES) {
	if (blnMural = $$$YES) {
		set isPublicado = $$$YES	
	}
	
	quit isPublicado
]]></Routine>
</Export>