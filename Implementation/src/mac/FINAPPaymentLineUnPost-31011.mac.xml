<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPPaymentLineUnPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPPaymentLineUnPost
	#include COMSYS
	#include FINConst
	#include WWWConst
 
Unpostable(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Determines whether a supplier payment line is unpostable.
    ;
    ; Returns: strStatus
    ;
    ; History:
    ; 06-Jan-2006 	SC			SR13798: Decode wrapper removed. Decode in COMSYS.INC
    ; 14-Oct-2005	Steve S		SR13216: Created
    ;-------------------------------------------------------------------------------
	new strStatus,idPayment,idPaymentLine,objFINAPPaymentLine,intStatus
 
	set strStatus=$$$OK
 
	if $get(pYKEY)'="" {
		set idPayment=$piece(pYKEY,$$$COMMA,1)
		set idPaymentLine=$piece(pYKEY,$$$COMMA,2)
		
		if ($get(idPayment)'="")&&($get(idPaymentLine)'="") {
			set objFINAPPaymentLine=$get(^FINAPPaymentLine(YM,idPayment,idPaymentLine,1))
			set intStatus=$$$FINAPPaymentLineStatus(objFINAPPaymentLine)
			set strStatus=$$UnPostable^FINSYSReview("FINAPPayment",intStatus)
		} else {
			set strStatus=$lb("Fin00089")  ;No Record Selected.	
		}
	} else {
		set strStatus=$lb("Fin00089")  ;No Record Selected.
	}
	
	if $$$ISOK(strStatus) {
		set YQ=$$$YQEnable
	} else {
		set YQ=$$$YQDisable(strStatus)    ;SR13798
 
	}
 
	quit strStatus
	
	
UnPostLine(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Unpost a single supplier payment line
    ;
    ; Returns: strStatus
    ;
    ; History:
    ; 17-May-2006	PO/SS		SR14004: Handle errors in UnPost^FINAPPaymentUnPost
    ; 14-Oct-2005	Steve S		SR13216: Created
    ;-------------------------------------------------------------------------------
	new strStatus,idPayment,idPaymentLine
	set strStatus=$$Unpostable(pYKEY)
 
	if $$$ISOK(strStatus) {
		set idPayment=$piece(pYKEY,$$$COMMA,1)
		set idPaymentLine=$piece(pYKEY,$$$COMMA,2)
		
		if ($get(idPayment)'="")&&($get(idPaymentLine)'="") {
			set strStatus=$$UnPost^FINAPPaymentUnPost(idPayment,,idPaymentLine)
		} else {
			set strStatus=$lb("Fin00089")  ;No Record Selected.	
		}
	}
	
	if $$$ISOK(strStatus) {
		do ReloadForm^COMUtilForm()
	;} else { // Done by TransactionComplete^FINSYS - called by UnPost^FINAPPaymentUnPost
	;	do ReturnError^COMUtilError(strStatus)
	}
 
	quit strStatus
	
	
ClearanceCheck(pYKEY,pblnUpdateButton=$$$YES)
    ;-------------------------------------------------------------------------------
    ; See if this payment line has been cleared in a bank rec.
    ;
    ; Returns: nothing
    ;
    ; History:
    ; 24-Oct-2005	JW&SS		SR13216: Check correct tran lines
    ; 17-Oct-2005	Steve S		SR13216: Created
    ;-------------------------------------------------------------------------------
	new strMessage,idPayment,idPaymentLine,objPay,objPayLine,idBankAcc,dteTranDate
	new idPostBatch,idBankAccIdx,idTran,idxSource,idTranLine,objTranLine
	new objUnPostButton,intLineStatus,blnFound,objBatchLine,idBatchLine
	
	set blnFound=$$$NO
	set strMessage=""
	
	if $get(pYKEY)'="" {
		set idPayment			=$piece(pYKEY,$$$COMMA,1)
		set idPaymentLine		=$piece(pYKEY,$$$COMMA,2)
		
		if (idPayment'="")&&(idPaymentLine'="") {
			set objPay			=$get(^FINAPPayment(YM,idPayment,1))
			set objPayLine		=$get(^FINAPPaymentLine(YM,idPayment,idPaymentLine,1))
			
			set idBankAcc		=$$$FINAPPaymentBankAccount(objPay)
			set dteTranDate		=$$$FINAPPaymentPaymentDate(objPay)
			
			set idPostBatch		=$$$FINAPPaymentLinePostingBatchNumber(objPayLine)
			set intLineStatus	=$$$FINAPPaymentLineStatus(objPayLine)
			
			if (idPostBatch'="") && (intLineStatus=$$$EnumFINSYSSTATUSPosted) {
				set idTran = $$$FININTBatchLedgerTransaction($get(^FININTBatch(YM,idPostBatch,1)))
				if idTran'="" {
					set idxSource = $$$Index(pYKEY)
					
					set idBatchLine=""
 
					for {
						set idBatchLine=$order(^FININTBatchLines(YM,3,"PAYMENT",idxSource,idPostBatch,idBatchLine))
						quit:idBatchLine=""
						
						set objBatchLine = $get(^FININTBatchLine(YM,idPostBatch,idBatchLine,1))
						set idTranLine = $$$FININTBatchLineTranLine(objBatchLine)
 
						set objTranLine=$get(^FINGLTranLine(YM,idTran,idTranLine,1))
						if $$$FINGLTranLineCleared(objTranLine)'="" {
							set strMessage=$$$Text($lb("Fin00912",$$$FINGLTranLineCleared(objTranLine)))  ;This payment line has been cleared in Bank Reconciliation %1. Proceed?	
							set blnFound=$$$YES
						}
					}
				}
			}
						
			; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
			; Replaced with code above	
			/*
			set idBankAccIdx=$$$Index(idBankAcc)
			
			if (idBankAccIdx'="")&&(dteTranDate'="")&&(intLineStatus=$$$EnumFINSYSSTATUSPosted) {
				set idTran=""
				for {
					set idTran=$order(^FINGLTranLines(YM,1,idBankAccIdx,dteTranDate,idTran))
					quit:idTran=""
					
					set strSource=$$$FINGLTranSource($get(^FINGLTran(YM,idTran,1)))
					
					continue:$piece(strSource,$$$COMMA,2)'="FININTBatch"
					continue:$piece(strSource,$$$COMMA,1)'=idPostBatch
					
					set idTranLine=""
					for {
						set idTranLine=$order(^FINGLTranLines(YM,1,idBankAccIdx,dteTranDate,idTran,idTranLine))
						quit:idTranLine=""
						
						set objTranLine=$get(^FINGLTranLine(YM,idTran,idTranLine,1))
						if $$$FINGLTranLineCleared(objTranLine)'="" {
							set strMessage=$$$Text($lb("Fin00912",$$$FINGLTranLineCleared(objTranLine)))  ;This payment line has been cleared in Bank Reconciliation %1. Proceed?	
							set blnFound=$$$YES
						}
					}	
				}
			}
			*/
			; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
		}
	}
	
	if pblnUpdateButton {
		set objUnPostButton=$get(^WWW124(0,"FINAPPaymentLine",SPRACHE,5,1))
		set $$$WWW124AcknowledgementTextBefore(objUnPostButton)=strMessage
		do Save^COMUtils("WWW124","FINAPPaymentLine,"_SPRACHE_",5",objUnPostButton,1)
	}
	
	quit blnFound
]]></Routine>
</Export>