<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewColumn" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewColumn
    ;-------------------------------------------------------------------------------
	; Saving and retrieving column properties
    ;-------------------------------------------------------------------------------
#include COMConst
#include COMSYS
	
SetColumnsForView(pidClass,pidView)
    ;-------------------------------------------------------------------------------
    ; Called on new or when changing views to reset what columns are to be displayed.
    ;
    ; History:
    ; 01-Feb-2006	PO		SR13158: Keep sort order, as now able to turn off sorting
    ; 30-Jun-2005	PO		SR12519: Switch off column sorting when loading search
    ; 							results on form load / change of view (favourites)
    ; 30-Dec-2004	PK		Created
	;-------------------------------------------------------------------------------
	new idField,objCOMViewColumnUser
	
	if '$data(^COMViewColumn(0,pidClass,pidView)) do SetDefaultColumns(pidClass,pidView)
	if '$data(^COMViewColumnUser(0,pidClass,pidView,YBED)) {
		merge ^COMViewColumnUser(0,pidClass,pidView,YBED)=^COMViewColumn(0,pidClass,pidView)
	}
	
	set idField=""
	for {
		set idField = $order(^COMViewColumnUser(0,pidClass,pidView,YBED,idField))
		quit:idField=""
		
		set objCOMViewColumnUser = $get(^COMViewColumnUser(0,pidClass,pidView,YBED,idField))
		;set $$$COMViewColumnUserSort1(objCOMViewColumnUser) = "" // SR13158
		set ^COMViewColumnUser(0,pidClass,pidView,YBED,idField) = objCOMViewColumnUser
	}
	
	quit
	
SaveColumns(pidClass,pidView)
    ;-------------------------------------------------------------------------------
    ; Used to save columns to disc.
    ;
    ; History:
    ; 30-Dec-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	// FIXME: ^COMViewColumn isn't using the standard @nm global structure
	// 		  of YM,...Keys...,1, hence can't use WWWKILL / WWWSPEI here.
	
	kill ^COMViewColumn(0,pidClass,pidView)
	merge ^COMViewColumn(0,pidClass,pidView)=^COMViewColumnUser(0,pidClass,pidView,YBED)
	
	quit
	
	
SetDefaultColumns(pidClass,pidView)
    ;-------------------------------------------------------------------------------
    ; If no columns are defined yet, make a best guess of what the default
    ; columns should be.
    ;
    ; History:
    ; 09-Sep-2008	PP		SR15866:Update COMView to Objects
    ; 30-Dec-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	new blnObj,idColumn,idField,idKey
	
	set blnObj   = +$get(^CacheTempObj(YUSER,"Object"))		//SR15866	
	set idKey    = ""
	set idColumn = 0
	
	if 'blnObj {
		for {
			set idKey=$order(^WWW002(0,pidClass,idKey))
			quit:idKey=""
			
			set ^COMViewColumn(0,pidClass,pidView,"P"_idKey) = $increment(idColumn)
		}
		set idField = $order(^WWW003(0,pidClass,""))
		if idField'="" {
			set ^COMViewColumn(0,pidClass,pidView,"D"_idField) = $increment(idColumn)
		}
 
	} else {
		set ^COMViewColumn(0,pidClass,pidView,"ID") = $increment(idColumn)
	}
 
	quit
	
]]></Routine>
</Export>