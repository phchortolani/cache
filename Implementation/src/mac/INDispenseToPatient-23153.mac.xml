<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDispenseToPatient" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDispenseToPatient
	
#include COMSYS
#include COMConst
#include INConst
#include COMGridEdit31
	
	; 16-Feb-2010	shobby	SR17184 Class/Form changed (Do Not remove comment)
	
CanViewReport()
	;-------------------------------------------------------------------------------
	; Disable the report button if the request has not been processed
	; 
	; Called By: Form INDispenseToPatient : Button 2 - Execute To Check For Disabled
	; 
	; History:
	; 27-Jan-2009 	DavidR	SR16378: commented out this check to keep button disabled
	; 							can be re-enabled after JasperReport is created
	; 19-Jan-2009	DavidR	SR16303: based on VARSESCONSUMOPATIENTE
	;-------------------------------------------------------------------------------
	set YQ = $$$YQEnable
	quit
	
	
OnBeforeEditAccess(pYKEY,pYFELD,&pobjForm)     ; DEPRECATED
	;-------------------------------------------------------------------------------
	; Called By : 
	; 
	; History:
	; 05-May-2009	GRF		SR16409: Code revisions incorporated in Editable and
	; 							OnBeforeButtonLine
	; 02-Mar-2009	DavidR	SR16380: modified the form and placed it for use in @NM
	; 							form INDispenseToPatient -> OnBeforeButtonLine;
	; 							this removes the need for routine OnBeforeButtonLine
	; 							in this routine
	; 19-Jan-2009	DavidR 	SR16303: Based on VARSESCONSUMOPATIENTE
	;-------------------------------------------------------------------------------
	new enumLocnType,idLocn
	
	if (pYKEY'="") {			
		set idLocn  = $$$INIssueFromLocn($get(^INIssue(0,pYKEY,1)))
 
		if (idLocn'="") && (idLocn'=YLOCATION) {
			// Blocks the form if the user's location does not match the record's
			$$$Alert($listbuild("IN00927",idLocn,YLOCATION))
			set $$$WWW120AuthorizationToModifyData(pobjForm) = $$$EnumReadOnly
		} ; "This document belongs to Location %1! This Location is %2. The screen will be available only for viewing documents"
	}
	quit
	
	
IsDeletable(pidKey,pYFELD)
	;-------------------------------------------------------------------------------
	; Description : Determines if the delete button should be disabled
	;			
	; Called By : INIssue FORMSPEC
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 13-May-2009	PPP		Created
	;-------------------------------------------------------------------------------
	quit:$$$NoKey(pidKey)
	
	if ($$$INIssueStatus(pYFELD)>1) || ($$$INIssuePosted(pYFELD)'="") {
		set Q = $$$QDontDelete
	}
	quit
	
	
OnBeforeButtonLine(pidIssue,pobjIssue,&pobjForm)
    ;-------------------------------------------------------------------------------
    ; Set form to read-only if not editable
    ; 
    ; Called By : Form INDispenseToPatient : D93 "Execute Before Buttonline"
    ; 
    ; Params:	pidAdj		- Adj key
    ;			pobjAdj		- Adj object
    ;
    ; Returns:	pobjForm	- Form object (WWW120)
    ;
    ; History:
	; 05-May-2009	GRF		SR16409: Code revisions incorporated from OnBeforeEditAccess
	; 							and this tag activated in form in place of that tag.
    ; 02-Mar-2009	DavidR	SR16380: Removed from form INDispenseToPatient, in favour
    ; 							of OnBeforeEditAccess.
    ; 19-Jan-2009	DavidR  SR16303: copied for CORE use VARSESCONSUMOPATIENTE
	; 15-Aug-2006	JW		SR14826: Created
    ;-------------------------------------------------------------------------------
	new idLanguageText,strMessage,strStatus
	
	quit:$$$NoKey(pidIssue)
	
	set strStatus = $$Editable(pidIssue)
	
	if $$$ISERR(strStatus) {		//|| ($data(^INIssueLine(0,pidIssue))) {
		set $$$WWW120AuthorizationToModifyData(pobjForm) = $$$EnumReadOnly
	}
	quit
	
	
Editable(pidIssue) 
    ;-------------------------------------------------------------------------------
    ; Is form editable (unprocessed) ?
    ;
    ; Params:	pidIssue - Issue id
    ;
    ; Returns:	status
    ;
    ; History:
    ; 20-Feb-2012	GRF		SR17980: use elseif
	; 05-May-2009	GRF		SR16409: Code revisions incorporated from OnBeforeEditAccess
	; 							for better security of form and of buttons.
    ; 04-Mar-2009	DavidR	SR16409: copied code from on before button line to be
    ; 							utilised in this routine
    ; 19-Jan-2009	DavidR	SR16303: copied and modified for CORE use 
    ;							VARSESCONSUMOPATIENTE
    ; 15-Aug-2006	JW		SR14826: Created
    ;-------------------------------------------------------------------------------
	new enumLocnType,idLocn,objIssue,strStatus
	
	set strStatus = $$$OK
	quit:$$$NoKey(pidIssue) strStatus
	
	if $$$ISOK(strStatus) {
		set idLocn  = $$$INIssueFromLocn($get(^INIssue(0,pidIssue,1)))
		if (idLocn'="") && (idLocn'=YLOCATION) {
			set strStatus = $$$MakeStatus("IN00927",idLocn,YLOCATION)
		} ; "This document belongs to Location %1! This Location is %2. The screen will be available only for viewing documents"
	}
 
	if $$$ISOK(strStatus) && $data(^INIssue(0,pidIssue,1)) {
		set objIssue = $get(^INIssue(0,pidIssue,1))
		
		if $$$INIssueStatus(objIssue) = 9  {
			set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"

		} elseif $$$INIssuePosted(objIssue)'="" {     ; SR17980
			set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"
		}
	}
	quit strStatus
	
	
Postable(pidIssue)
	;-------------------------------------------------------------------------------
	; Test whether this record is postable
	; 
	; Called By: Form INDispenseToPatient : Button 1 - Execute To Check For Disabled
	; 
	; Params:	pidIssue - Issue id
	;
	; ByRefs:
	;
	; Returns:	postable status
	;
	; History:
	; 22-Feb-2010	PPP		SR17185: Added Patient ID as one of the new fields to 
	;								 check before Post
	; 27-Jul-2009	DWR		SR16768: added missing "" around language text enum
	; 20-Jan-2009	DWR		SR16303: Copied and modified for core use from INADJ
	; 25-Oct-2007,  PJones	BR014730, lock a site location when stocktaking
	; 15-Aug-2006	JW		SR14826: Created
	;-------------------------------------------------------------------------------
	new blnOk,objIssue,strRefCheck,strStatus
	
	quit:$$$NoKey(pidIssue) '$$$OK   ; FIXME : <GRF> 1) strStatus? what error message 2) YQ = QDontSave with error?
	
	set strStatus = $$Editable(pidIssue)
	
	if $$$ISOK(strStatus) {
		if '$data(^INIssueLine(0,pidIssue)) {
			set strStatus = $$$MakeStatus("IN00908")  ; "Issue must have lines"
		}
	}
	
	if $$$ISOK(strStatus) {
		set objIssue = $get(^INIssue(0,pidIssue,1))
		if objIssue '= "" {
			if $$IsLockedSiteLocation^INLocking($$$INIssueFromLocn(objIssue)) {
				set strStatus = $$ReasonLocked^INLocking($$$INIssueFromLocn(objIssue))
			}

			if $$$ISOK(strStatus) {
				if $$$INIssueType(objIssue) = 1 {
					set blnOk = $$TransferLocation^WWW0121Rules(0,$$$INIssueToLocn(objIssue))
					if blnOk = $$$NO {
						set strStatus = $$$MakeStatus("IN01069",$$$INIssueToLocn(objIssue))  ;SR16768
					} ; "To Location (%1) not setup as a Transfer Location"
				}
			}
		}
	}
	
	if $$$ISOK(strStatus) {
		set strRefCheck = $$$INIssueCustomerID(objIssue)    _
		                  $$$INIssueCustomerName(objIssue)  _
		                  $$$INIssueReferrerID(objIssue)    _
		                  $$$INIssueReferrerName(objIssue)  _
		                  $$$INIssueDocketNo(objIssue)      _
		                  $$$INIssuePatientID(objIssue)       	 ; SR17185 - add Patient ID
		
		if strRefCheck="" {
			set strStatus = $$$MakeStatus("IN00882")
		} ; "Must have at least one "ID" or "Name" field completed"
	}
	
	;if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
	;	if $$$ISOK(strStatus) {
	;		set strStatus = $$Postable^VARINDispenseToPatient(pidIssue)
	;	}
	;}
	
	if $$$ISOK(strStatus) {
		set YQ = $$$QSave
	} else {
		$$$YQHandler(strStatus)
	}
	quit strStatus
	
	
OnBeforeSave(pidIssue, pobjIssue)
    ;-------------------------------------------------------------------------------
    ; Stock Adjustment
    ; 
    ; Called By: Form INDispenseToPatient
    ; 
	; Params:	pidIssue - INIssue id
    ; 
    ; Returns: 
    ;
    ; History:
    ; 20-Feb-2012	GRF		SR17980: Correct macro use - doesn't need $$$Text
    ; 01-Jul-2009	PPP		SR16703: Invalid options for Dispense (2,4,5)
    ; 19-Jan-2009	DavidR  SR16303: copied for CORE use VARSESCONSUMOPATIENTE
    ; 17-Aug-2006	JW		SR14826: Rewrote
    ; 21-Jul-2006	FAN		SR14826: Created
    ;-------------------------------------------------------------------------------
	new idType,strStatus
	new idToLocn, objToLocn
	
	set strStatus = $$$OK
	
	set idType = $$$INIssueType(pobjIssue)

	;if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
	;	set idToLocn = $piece(YFELD,"~",22)
	;
	;	if (idToLocn '= "") {	
	;		set objToLocn = ^WWW0121(YM,0,idToLocn,$$$YES)
	;		if ($$$WWW0121LocationType(objToLocn)'=2){ // verifica se o local é do tipo "2 - Departamento"
	;			set strStatus = $$$MakeStatus("Não é possível selecionar o local '"_idToLocn_"'")
	;		}
	;	}
	;}

	if $$$ISOK(strStatus) {    ; FIXME : Use elseif tests below instead
		
		//Inter-Department Issue
		if (idType=1) && ($$$INIssueToLocn(pobjIssue) = "") {
			set strStatus = $$$MakeStatus("IN01030",1)
		} ; "To-Location is invalid for current Issue Type (%1)"
		
		if $$$ISOK(strStatus) {
			//Inter-Site
			if (idType=2) || (idType=4) || (idType=5) {		//Inter-Site, Kits, Prescriptions
				set strStatus = $$$MakeStatus("IN01031",2)
			} ; "Invalid Issue Type (%1) for current operation "
 
			if $$$ISOK(strStatus) {	
				//Dispense/Sale Issue
				if (idType=3) && ($$$INIssueToLocn(pobjIssue) '= "") {
					set strStatus = $$$MakeStatus("IN01030",1)
				} ; "To-Location is invalid for current Issue Type (%1)"
			}
		}
	}
	
	if $$$ISOK(strStatus) {
		set strStatus = $$$GRIDSave(pidIssue)
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	quit
	
	
Post(pidIssue)
	;-------------------------------------------------------------------------------
	; Post adjustment
	;
    ; Called By: Form INDispenseToPatient : Button 1 - Execute OnClick
    ; 
	; Returns:Status
	;
	; History:
	; 15-May-2009	GRF		SR16199: sc/strStatus use clarified
	; 11-May-2009	PPP		SR16199: Updated as a Single Interface for both
	; 							Department Issue and POS/Sale Issue
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$Transaction^COMTransaction("PostTxn^INIssueAuto("""_pidIssue_""")",$$$YES)
	
	if $$$ISOK(strStatus) {
		do ReloadForm^COMUtilForm()
	}
	quit strStatus
	
	
PostTxn(pidIssue)    
	; >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
	; *********************************************************************************
	; **DEPRECATED*****DEPRECATED*****DEPRECATED*******DEPRECATED******DEPRECATED******
	; *********************************************************************************
	; >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
	;, now going though PostTxn^INIssueAuto
	;-------------------------------------------------------------------------------
	; Post Dispense to Patient Issue
	;
	; Returns:Status
	;
	; History:
	; 25-Jun-2009	PPP		DEPRECATED DEPRECATED DEPRECATED DEPRECATED DEPRECATED
    ; 25-Feb-2010	PPP		SR17815: Update the Cost on the Line
	; 28-Jan-2009	DavidR	SR16303: 
	;-------------------------------------------------------------------------------
	new idLine,idLocn,objV1Issue,objIssue,objIssueLine,strStatus,sc
	
	set strStatus = $$Postable(pidIssue)
	set sc = $$$OK 
	
	if $$$ISOK(strStatus) {

		set objV1Issue = $get(^INIssue(0,pidIssue,1))
		set idLocn = $$$INIssueFromLocn(objV1Issue)

		//SR17299
		if objV1Issue '= "" {
			if $$IsLockedSiteLocation^INLocking($$$INIssueFromLocn(objV1Issue)) {
				set strStatus = $$ReasonLocked^INLocking($$$INIssueFromLocn(objV1Issue))
			}
		}

		if $$$ISOK(strStatus) {
			set strStatus = $$UpdateCostOnLines(pidIssue)
	
			if $$$ISOK(strStatus) {
				//set objV1Issue = $get(^INIssue(0,pidIssue,1))
				//set idLocn = $$$INIssueFromLocn(objV1Issue)
			
				set idLine = ""
				for {
					set idLine = $order(^INIssueLine(0,pidIssue,idLine))
					quit:idLine=""

					set objIssueLine = ##class(alISS.dUDispenseToPatientLine).%OpenId("0||"_pidIssue_"||"_idLine)
					set sc = objIssueLine.FirmIssue()
					set:$$$ISERR(sc) strStatus = $$ISStatusToDLStatus^COMUtilError(sc)
					quit:$$$ISERR(strStatus)
				}
			
				if $$$ISOK(strStatus) {
					set $$$INIssueStatus(objV1Issue) = 9 ; Auto Closed
					set $$$INIssuePosted(objV1Issue) = $horolog
					set strStatus = $$$Save("INIssue",pidIssue,objV1Issue,$$$YES)
				}
			}
		
		}
		
	}
	
	quit strStatus
	
	
UpdateCostOnLines(pidIssue)
    ;-------------------------------------------------------------------------------
    ; Update the Unit Cost for Line 
    ;
    ; Returns: Status
    ;
    ; History:
    ; 09-Aug-2010	PPP		SR17185:1 Check the Error Status before proceeding
    ; 25-Feb-2010	PPP		SR17185: Update the Cost on the Line
    ;-------------------------------------------------------------------------------
	new idItem,intLn,objHeader,objLine,strStatus
	
	set strStatus = $$$OK
	set objHeader = $get(^INIssue(0,pidIssue,1))
	
	set intLn = 0
	for {
		quit:$$$ISERR(strStatus)		//SR17185:1
		
		set intLn = $order(^INIssueLine(0,pidIssue,intLn))
		quit:'intLn
		
		set objLine = $get(^INIssueLine(0,pidIssue,intLn,1))
		set idItem  = $$$INIssueLineItem(objLine)
 		set $$$INIssueLineUnitCost(objLine) = $$GetCost^INItemCost(idItem,$$$INIssueFromLocn(objHeader))*$$GetUOMConversion^INUOMConversion(idItem,$$$INIssueLineUnit(objLine)) 
		
		set strStatus = $$$Save("INIssueLine",pidIssue_","_intLn,objLine,$$$YES)	
	}
	
	quit strStatus
	
	
OnAfterDataFields(pidForm,pidKey,pobjDispense)
    ;-------------------------------------------------------------------------------
    ; Code for Grid Edit 
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Jul-20014	Henrique Dias	HEVA-1348	Call for a custom code VARINDispenseToPatientJS
    ; 19-Jan-2009	DavidR  SR16303: based on VARSESCONSUMOPATIENTE
    ;-------------------------------------------------------------------------------
	new idForm,YAUSWAHL,blnEnabled
	
	set idForm     = "INDispenseToPatientLine"
	set blnEnabled = ($$$INIssueStatus(pobjDispense) '= 9)
 
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "INDispenseToPatient"
 
	$$$GRIDStart(idForm,pidKey)
 
 	if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1))))
	{
		do SetupJS^VARINDispenseToPatientJS("INDispeseToPatient")
	}
	
	quit 
	
	
OnAfterPrimaryKey(pYM,pYFORM,pYSEITE,pstrKey,&pYFELD,&pobjForm)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By : Form INDispenseToPatient
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 23-Jul-2009	DWR		SR16642: added a redirect if the issue type is 5 (Prescription)
	; 04-Mar-2009	DavidR	SR16409:added check for null
	; 19-Jan-2009	DavidR	SR16303: based on INIssue
	;-------------------------------------------------------------------------------
	; code sanitisation, shouldn't ever get to this state because of Garbage In filters!
	if ($$$INIssueFromLocn(pYFELD) '="") && ($$$INIssueFromLocn(pYFELD) '= YLOCATION) {
		set $$$WWW120AuthorizationToModifyData(pobjForm) = $$$EnumReadOnly
	}
	if (+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(YM,YM,1)))) {
		do OnAfterPrimaryKey^VARINDispenseToPatient(pstrKey,pYFELD)
	}
	if $$$INIssueType(pYFELD)= 5 { ;prescription
		do RedirectForm^COMUtils("MEDPrescriptionDispense",pstrKey)
	}
	quit
	
	
OnBeforeFormConstructionType(pidIssue)
	;-------------------------------------------------------------------------------
	; Check for Type is to be made Read Only
	;
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 12-May-2009	PPP		SR16199: Created:If lines exist cannot Change Type of Issue
	; 						set field to Read Only
	;-------------------------------------------------------------------------------	
	quit:$$$NoKey(pidIssue)
	
	if $data(^INIssueLine(0,pidIssue)) set YHID = 2
	quit
 
OnBeforeConstructionToLocn(pidIssue,pYFELD)
	;-------------------------------------------------------------------------------
	; Check for To Location is to made Read Only
	;
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 12-May-2009	PPP		SR16199: Created:If lines exist cannot Change ToLocation
	; 						set field to Read Only
	;-------------------------------------------------------------------------------	
	quit:$$$NoKey(pidIssue)
	
	if ($$$INIssueType(pYFELD)=3) || $data(^INIssueLine(0,pidIssue)) set YHID = 2
 
	quit
	

OnBlurToLocn(pYFELD)
	;-------------------------------------------------------------------------------
	; Depending on the Locations, set the IssueType to the correct one
	; 
	; ByRef:
	; 	YM		Company ID
	; 	YFORM	Form ID
	; 
	; History:
	; 17-Jul-2009	PPP		SR16768: Created
	;-------------------------------------------------------------------------------
	new intReqType
	
	if $$$INIssueType(pYFELD)="" {
		if $$MainSite^WWW0121Utils(0,$$$INIssueFromLocn(pYFELD)) = $$MainSite^WWW0121Utils(0,$$$INIssueToLocn(pYFELD)) {
			set intReqType = 1
		
		} else {
			set intReqType = 2
		}
		set %TXT(1) = %TXT(1)_"#Y"_YFORM_"D"_$$$FldINIssueType_"~"_intReqType
	}
	quit
	
 
OnBlurType(pYFORM, &pobjIssue)
	;-------------------------------------------------------------------------------
	; Check for To Location is to be Hidden
	;
	;
	; ByRefs: pobjIssue
	;
	; Returns:
	;
	; History:
	; 02-Feb-2010	shobby	SR15753: Removed redundant code (rules)
	; 12-May-2009	PPP		SR16199: Created
	;-------------------------------------------------------------------------------	
 	set $$$INIssueReference(pobjIssue)="Issue"
 	quit
	
	
PrescriptionExists(pYKEY)
	;-------------------------------------------------------------------------------
	; checks for an existing Prescription Record, and will not change the type if it
	; exists
	;
	; Called by: Core rule 3 Field 14 (Type) INDispenseToPatient
	; 
	; Inputs:
	;
	; Returns: YES/NO (can you change the record?)
	;
	; History:
	; 23-Jul-2009	DAVID		SR<>: Created
	;-------------------------------------------------------------------------------
	new blnChange,idReference,objIssue
	
	set blnChange = $$$YES
	if '$$$NoKey(pYKEY){
		set objIssue = $get(^INIssue(0,pYKEY,1))
		set idReference = $$$INIssueDocketNo(objIssue)
		
		if $data(^MEDPrescription(0,idReference)) {
			set blnChange = $$$NO
		}
	}
	quit blnChange
	
	
HasKey(pYKEY)
	;-------------------------------------------------------------------------------
	; Dont let users change the from location after saving, 
	; otherwise errors can occur if grid lines exist and THEN change the from location
	;
	; Called By :Core rule 1 for F18 INDispenseToPatient
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 30-Nov-2009	DWR		SR17030: Created
	;-------------------------------------------------------------------------------
	quit '$$$NoKey(pYKEY)
	
]]></Routine>
</Export>