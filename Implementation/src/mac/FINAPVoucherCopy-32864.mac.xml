<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPVoucherCopy" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPVoucherCopy
	#include FINConst
	#include COMSYS
	#include WWWConst
 
CanCopy(pidVoucher)
    ;--------------------------------------------------------------------------------
    ; Determines whether a voucher record can be copied
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Jan-2006 	SC			SR13798: Decode wrapper removed. Decode in COMSYS.INC
    ; 11-Nov-2005	Steve S		SR13663: Created
    ;--------------------------------------------------------------------------------
	new strStatus,objVoucher,intStatus
 
	set strStatus=$$$OK
	
	if '$$$NoKey(pidVoucher) {
		set objVoucher=$get(^FINAPInv(YM,pidVoucher,1))
		set intStatus=$$$FINAPInvStatus(objVoucher)
		
		if (intStatus'=$$$EnumFINSYSSTATUSCancelled) {
			set strStatus=$listbuild("Fin00937",pidVoucher,$$$AppEnum("FINSYSSTATUS",intStatus))  ;Can only copy cancelled vouchers. Voucher `%1` has status `%2`.
		}
	} else {
		set strStatus=$listbuild("Fin00089")  ;No Record Selected.	
	}
	
	if $$$ISOK(strStatus) {
		set YQ=$$$YQEnable	
	} else {
		set YQ=$$$YQDisable(strStatus)	;SR13798
	}
 
	quit strStatus
 
 
DoCopy(pidSource,pidDest)
    ;--------------------------------------------------------------------------------
    ; Performs the copy operation
    ;
    ; Returns:
    ;
    ; History:
    ; 11-Nov-2005	Steve S		SR13663: Created
    ;--------------------------------------------------------------------------------
	new strStatus,objVoucher,idLine,objLine,strText
 
	tstart
	set strStatus=$$CanCopy(pidSource)
	
	set pidDest=$piece(pidDest,",",1)
	
	if $$$ISOK(strStatus) {
		if $get(pidDest)="" {
			set strStatus=$listbuild("Fin00915",$$^WWWFELDNAME("FINAPVoucherCopy","M",2))  ;Require value for field `%1`. 	
		} else {
			if pidDest="+" set pidDest=$$^WWWNEXT("FINAPInv")
			if $data(^FINAPInv(YM,pidDest)) {
				set strStatus=$listbuild("Fin00938",pidDest)  ;Voucher `%1` already exists!
			} else {
				set objVoucher=$get(^FINAPInv(YM,pidSource,1))
				if (objVoucher="") {
					set strStatus=$listbuild("Fin00565",pidSource) ;No invoice data - Invoice Number ´%1´.	
				} else {
					set $$$FINAPInvStatus(objVoucher)=$$$EnumFINSYSSTATUSUnposted
					
					if $$$FINAPInvNotes(objVoucher)="" {
						set $$$FINAPInvNotes(objVoucher)=$$$Text($listbuild("Fin00939",pidSource))  ;Copied from voucher `%1`.	
					}
					
					set strStatus=$$$Save("FINAPInv",pidDest,objVoucher,1)
					
					if $$$ISOK(strStatus) {
						set idLine=""
						for {
							set idLine=$order(^FINAPInvCharge(YM,pidSource,idLine))
							quit:(idLine="")||($$$ISERR(strStatus))
							
							set objLine=$get(^FINAPInvCharge(YM,pidSource,idLine,1))
							if (objLine="") {
								set strStatus=$listbuild("Fin00566",pidSource,idLine) ;No invoice line data - Invoice ´%1´, Line ´%2´.	
							} else {
								set strStatus=$$$Save("FINAPInvCharge",pidDest_","_idLine,objLine,1)	
							}
						}	
					}
				}
			}
		}
	}
	
	if $$$ISOK(strStatus) {
		if $tlevel>0 tcommit
		do GoToForm^COMUtilForm("FINAPVoucher",pidDest,"","")
		;set strText=$$^WWWFELDNAME("FINAPVoucherCopy","M",2)_": "_pidDest
		;do ^WWWINFO($$$Text("Fin00840")_" ("_strText_")") ;Copy Successful
	} else {
		trollback
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit strStatus
]]></Routine>
</Export>