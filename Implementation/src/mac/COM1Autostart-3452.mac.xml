<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COM1Autostart" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COM1Autostart
	; @@AUTOSTART@@
	;Any required commands to run after a build has been installed.
	;
#include COMSYS
	if $get(Y)="" do ^WWWVAR
	do FixRequiredUserParameters()
	do FixCOMViewConfig()
	do FixCacheTemp()
	quit
	
FixCacheTemp()
    ;-------------------------------------------------------------------------------
    ; Kill the cached text results
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 29-Nov-2006	JW		SR15257: Added ^CacheTempEvent
    ;  7-Jul-2006	JW		SR12775: Added ^CacheTempCheckRelation
    ; 06-Jul-2005	shobby	SR12887
    ;-------------------------------------------------------------------------------
    new strNS
    
    set strNS = $zutil(5)
    
    kill ^CacheTempCheckRelation(strNS)
    kill ^CacheTempWWWTEXT(strNS)
    kill ^CacheTempWWWTEXTERROR(strNS)
    kill ^CacheTempEvent(strNS)			//SR15257
    
	quit
	
FixRequiredUserParameters()
    ;-------------------------------------------------------------------------------
    ; This particular user parameter is require for CUSTOMS import costs to work.
    ; User parameters not generally distributed but this one is.
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Jun-2005	shobby	Created SR12739
    ;-------------------------------------------------------------------------------
    ;
	new objINPARA,SPRACHE,strEnglish,strText
	
	set SPRACHE="EN"
	set strEnglish=$$^WWWTEXT("IN00346") ;Calculated Customs
	set SPRACHE=""
	for {
		set SPRACHE=$order(^WWW009(0,SPRACHE))
		quit:SPRACHE=""
		set strText=$$^WWWTEXT("IN00346")
		if strText="IN00346" set strText=strEnglish
		set objINPARA=$get(^INPARA(YM,"KOSTENARTEN",SPRACHE,"CUSTOMS",1))
		set $piece(objINPARA,Y,1)=strText
		if objINPARA'=$get(^INPARA(YM,"KOSTENARTEN",SPRACHE,"CUSTOMS",1)) {
			do Save^COMUtils("INPARA","KOSTENARTEN,"_SPRACHE_",CUSTOMS",objINPARA,1)
		}
	}
	quit
 
FixCOMViewConfig()
    ;-------------------------------------------------------------------------------
    ; Automatically fixes the COMViewConfig which may have been deleted to allow
    ; COM to be installed.
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jan-2008	GRF		SR15627: use $$$Save
    ; 02-Mar-2005	Shobby	Created (SR11789)
    ;-------------------------------------------------------------------------------
	new objCOMViewConfig,idCompany,strStatus,strMessage
	
	set strStatus=$$$OK
	
	set idCompany=""
	for {
		set idCompany=$order(^WWW012(0,idCompany))
		quit:idCompany=""
		
		if '$data(^COMViewConfig(0,idCompany,1)) {
			set objCOMViewConfig="300~.5~129~137~35~~78~~1~~UPGRADE~"_$horolog_"~~"
			set strStatus  = $$$Save("COMViewConfig",idCompany,objCOMViewConfig,$$$YES)
			set strMessage = "FixCOMViewConfig:Company "_idCompany_", Success="_strStatus
			;w !,strMessage
			do AddMessage^COMLog($get(^CacheTempUpgrade($job)),strMessage) ;Assumption is made that this only runs during an upgrade when 'Log' exists.
		}
	}
	quit
	
]]></Routine>
</Export>