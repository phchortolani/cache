<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASParticipantes" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASParticipantes
#include COMSYS
 
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Mostra cada um dos itens da lista e seus participantes, ou a opção para associar
    ; participantes.
    ; 
    ; History:
    ; 16-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
		
	new Linha, objProcessoCompraLinha, Produto, count, Participante, objParticipante, chave, chave2,
		strHint, Participante, idFornecedor, strBotaoRemover, strRemoverCallBack, strLinkAssociar, 
		strBotaoAssociar, strAssociarCallBack, beginStyle, endStyle, descFornecedor, blnItemEncerrado

	set beginStyle = "<font style='line-height:26px'>"
	set endStyle = "</font>"
	 
	//Tabela
   	new lstHeader
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","Participantes Selecionados","")
  	
	$$$Order3(^VARSESCOMPRASLinha,YM,pYKEY,Linha)
	
		set blnItemEncerrado = $$isItemEncerrado^VARSESCOMPRASPDPASLinha(pYKEY,Linha)
 		
 		//Não mostra o item caso tenha sido encerrado
 		continue:(blnItemEncerrado = $$$YES)
 		
		set objProcessoCompraLinha = $get(^VARSESCOMPRASLinha(YM,pYKEY,Linha,1))
		set Produto	= $piece(objProcessoCompraLinha,Y,2)
 
		set chave = pYKEY_","_Linha	
 
 		if $$Start^COMTable(lstHeader,"&nbsp;<font style=font-size:14px>Item "_Linha_": "_Produto_" - "_$extract($$SQLGetDescProduto^VARSQL(Produto),1,80)_"</font>") {
			
			set count = 0

			&sql(DECLARE Participantes CURSOR FOR

				 SELECT Participante, $$SQLGetDescFornecedor^VARSQL(Fornecedor) as descFornecedor
				   INTO :Participante, :descFornecedor
				   FROM VARSESCOMPRASParticipantesLine
				  WHERE $UPPER(NoProcessodeCompra) = :pYKEY
				    AND LinhadoProcessodeCompra = :Linha
			   ORDER BY descFornecedor

 			)

 			&sql(OPEN Participantes )
 			&sql(FETCH Participantes )
			
 			while ('SQLCODE) {	

				set objParticipante = $get(^VARSESCOMPRASParticipantesLine(YM,pYKEY,Linha,Participante,1))				
				set idFornecedor 	= $piece(objParticipante,Y,1)				
				
				set count = $i(count)
				set chave2 = pYKEY_","_Linha_","_Participante
				
				set strBotaoRemover = "<IMG SRC="_YGIF_"RemoverParticipante.gif TITLE='Clique para remover este participante para este item' border=0 align=""center"" >"
				set strRemoverCallBack = "RemoverParticipante^VARSESCOMPRASParticipantes"
			
				do NewLine^COMTable()
				do InsertCell^COMTable(beginStyle_count_endStyle)
				do InsertCell^COMTable(beginStyle_$extract(descFornecedor,1,80)_" ("_idFornecedor_")"_endStyle,,,,,,,,,1)
			    do InsertCellCallback^COMTable(strBotaoRemover,strRemoverCallBack,chave2)
				do EndLine^COMTable()
				
	 			&sql(FETCH Participantes )
 			}
 			
 			&sql(CLOSE Participantes )
 			
 			
			if ($data(^VARSESCOMPRASParticipantesLine(YM,pYKEY,Linha)) = 0) {
				
				set strHint = "Clique para adicionar um participante para este item"
								
				do NewLine^COMTable()
			    do InsertCell^COMTable("")
			    do InsertCell^COMTable(beginStyle_"<font color=gray>Nenhum participante selecionado para este Item.</font>"_endStyle)
			    do InsertCell^COMTable("")
			    do EndLine^COMTable()
			}
			
			else {
				set strHint = "Clique para adicionar mais um participante para este item"
			}
	 
		    set strLinkAssociar  = strHint
			set strBotaoAssociar = "<IMG SRC="_YGIF_"AdicionarParticipante.gif TITLE='"_strHint_"' border=0 align=""center"" >"
			set strAssociarCallBack = "AssociarParticipantes^VARSESCOMPRASParticipantes"
		    
		    do NewLine^COMTable("lightsteelblue")
		    do InsertCell^COMTable("")
		    do InsertCellCallback^COMTable(strLinkAssociar,strAssociarCallBack,chave)
			do InsertCellCallback^COMTable(strBotaoAssociar,strAssociarCallBack,chave)
		    do EndLine^COMTable()
		
			do Stop^COMTable()
		}	
	
	$$$End
 
	quit
	
	
AssociarParticipantes(pidProcessoCompra,pidLinha)
	new chave, isReadOnly
 
	set isReadOnly = $$CheckProcessoReadOnly^VARSESCOMPRAS(pidProcessoCompra)
	if isReadOnly = $$$YES {
		$$$Alert("Não é possível associar participantes. Processo somente-leitura para este usuário ou posição.")
		quit
	}
		
	set chave = pidProcessoCompra_","_pidLinha
	do GoToForm^COMUtilForm("VARSESCOMPRASParticipantesSel",chave)
 
	quit
	
	
RemoverParticipante(pidProcessoCompra,pidLinha,pItemParticipante)
	;-------------------------------------------------------------------------------
    ; Remove o participante e atualiza, dependendo da situação, o processo de compra.
    ; 
    ; History:
    ; 30-Apr-2011	Gustavo Fiuza		Updated - Verifica se o participante tem proposta
    ;									antes que possa ser removido.
    ; 17-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
	new strStatus, chave, isReadOnly, idFornecedorParticipante, blnPossuiProposta
	
	//Verifica se o processo de compra está somente-leitura
	set isReadOnly = $$CheckProcessoReadOnly^VARSESCOMPRAS(pidProcessoCompra)
	if (isReadOnly = $$$YES) {
		$$$Alert("Não é possível remover participantes. Processo somente-leitura para este usuário ou posição.")
		quit
	}
	
	//Verifica se o fornecedor participante possui proposta cadastrada para algum item
	set idFornecedorParticipante = $$GetFornecedorParticipante(pidProcessoCompra,pidLinha,pItemParticipante)	
	
	set blnPossuiProposta = $$isFornecedorComProposta^VARSESCOMPRASProposta(pidProcessoCompra,idFornecedorParticipante)	
	if (blnPossuiProposta = $$$YES) {
		$$$Alert("Não é possível remover este participante pois ele possui proposta cadastrada para um ou mais itens deste processo de compra.")
		quit
	}
	
	//Remove o participante do processo	
	set strStatus = $$$OK		
	set strStatus = $$$Kill("VARSESCOMPRASParticipantesLine",pidProcessoCompra_YKOMMA_pidLinha_YKOMMA_pItemParticipante)
 
	set chave = pidProcessoCompra_","_pidLinha
	
	set strStatus = $$AtualizaPosicaoParticipante^VARSESCOMPRAS(pidProcessoCompra)
 
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	}
	
	do GoToForm^COMUtilForm("VARSESCOMPRASParticipantes",chave)
 
	quit
	
GetFornecedorParticipante(pidProcessoCompra,pidLinha,pItemParticipante)
	quit:( (pidProcessoCompra = "") || (pidLinha = "") || (pItemParticipante = "") ) ""

	new idFornecedor
	set idFornecedor = ""
	
	&sql(	SELECT Fornecedor
			  INTO :idFornecedor
			  FROM VARSESCOMPRASParticipantesLine
			 WHERE $UPPER(NoProcessodeCompra) = :pidProcessoCompra
			   AND LinhadoProcessodeCompra = :pidLinha
			   AND Participante = :pItemParticipante
	)

	quit idFornecedor

]]></Routine>
</Export>