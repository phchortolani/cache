<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSUPDiscount" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSUPDiscount
	
#include COMSYS
#include INConst
	
OnBlur(pidField) 
    ;-------------------------------------------------------------------------------
    ; Handle the Blur for INSUPDiscount.
    ;
    ; Params:
    ; pidField: The field number we have just blurred.
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Jul-2005	RobertW	SR11983: Created
    ;-------------------------------------------------------------------------------
 	quit:$get(pidField)=""
 	
 	if pidField=$$$FldINSUPDiscountDiscountCode {
	 	; If we blur the Discount Code, then handle this
	 	do DiscountCodeBlur(YFKEY,.YFELD)
 	}
	quit
	
	
DiscountCodeBlur(pstrYKEY,&pstrYFELD) 
    ;-------------------------------------------------------------------------------
    ; When the user changes the discount code, repopulate the form.
    ;
    ; Params:
    ; pstrYKEY: The key of the INSUPDiscount record
    ; pstrYFELD: The data of the INSUPDiscount record
    ;
    ; Returns:
    ;
    ; History:
    ; 19-Jan-2010	shobby	SR15753: Simplified Code.
    ; 22-Aug-2005	RobertW	SR11983: Modified for new structures and added
    ; 							Cost Distribution and the GL Account
    ; 21-Jul-2005	RobertW	SR11983: Created
    ;-------------------------------------------------------------------------------
	new idSupplier,idEntry,strCode,objBD,strForm
 
	set idSupplier = $$$KEY1(pstrYKEY)
	set idEntry    = $$$KEY2(pstrYKEY)
	
	set strCode = $$$INSUPDiscountDiscountCode(pstrYFELD)
	
	if strCode'="" {
		set objBD=$get(^INSUPBuyingDiscount(YM,strCode,1))
		set strForm="#Y"_YFORM_"D"
		set $$$INSUPDiscountDescription(pstrYFELD)		= $$$INSUPBuyingDiscountDescription(objBD)
		set $$$INSUPDiscountSurcharge(pstrYFELD)		= $$$INSUPBuyingDiscountSurcharge(objBD)
		set $$$INSUPDiscountCostAllocation(pstrYFELD)	= $$$INSUPBuyingDiscountCostAllocation(objBD)
		set $$$INSUPDiscountAmountCalculation(pstrYFELD)= $$$INSUPBuyingDiscountAmountCalculation(objBD)
		set $$$INSUPDiscountAmount(pstrYFELD)			= $$$INSUPBuyingDiscountAmount(objBD)
		set $$$INSUPDiscountLockMode(pstrYFELD)			= $$$INSUPBuyingDiscountLockMode(objBD)
		set $$$INSUPDiscountGLAccount(pstrYFELD)		= $$$INSUPBuyingDiscountGLAccount(objBD)
		set $$$INSUPDiscountCostDistribution(pstrYFELD)	= $$$INSUPBuyingDiscountCostDistribution(objBD)
	}
	quit
	
	
OnBeforeDelete(pstrYKEY) 
    ;-------------------------------------------------------------------------------
    ; Determine if we can delete this discount.
    ;
    ; Params:
    ; pstrYKEY: The key of the discount
    ;
    ; Returns:
    ;
    ; History:
    ; 22-Aug-2005	RobertW	SR11983: Created
    ;-------------------------------------------------------------------------------
	; TODO: check for any FINAPInvLine with this discount attached.
 
	set Q = $$$QDelete
	quit
	
]]></Routine>
</Export>