<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWPIC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWPIC ;ERZEUGEN EINER BILD-DATEI IM BMP-FORMAT;27MAR00
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		27MAR00
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| 	
	;|
	;\------------------------------------------------------------------/
	;
	;(c) 14.APR.2000 BURKHARD KASTEN
	QUIT
	
NEW(LEN,HIG,X) ;BMP INITIALISIEREN
	NEW I
	SET BMPX0=0,BMPX1=LEN,BMPY0=0,BMPY1=HIG
	SET BMPLE=LEN,BMPHI=HIG
	DO COL(X) 
	SET BMPBK=0,BMPEN=1
	SET BMPGL="^WWWSOR(YUSER,""B"")" 
	KILL @BMPGL
	SET BMPGF="^WWWSOR(YUSER,""G"")" 
	KILL @BMPGF
	FOR I=0:1:BMPHI-1 SET $PIECE(@BMPGL@(I),BMPBK,LEN*BMPTY+31\32*32\BMPTY+1)=""
	QUIT
	
SKAL(X0,X1,Y0,Y1)  ;SKALIERUNG
	SET BMPX0=X0,BMPX1=X1,BMPY0=Y0,BMPY1=Y1
	QUIT
	
POIN(X0,Y0,TY) ;ZEICHNE EINZELNEN PUNKT
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	SET $EXTRACT(@BMPGL@(Y0),X0)=BMPEN
	SET TY=+$GET(TY) 
	QUIT:'TY
	SET TY=$PIECE("1111110001100011000111111~1111111111112111111111111~0010001110112110111000100~1000101010000000101010001","~",TY) QUIT:TY=""
	FOR I=0:1:24 SET:$EXTRACT(TY,I+1) $EXTRACT(@BMPGL@(I\5-2+Y0),I#5-2+X0)=$SELECT(I=12:BMPBK,1:BMPEN)
	QUIT
	
LINE(X0,Y0,X1,Y1) ;LINIE ZEICHNEN VON X0/Y0 NACH X1/Y1
	NEW I,L
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET X1=BMPLE-1/(BMPX1-BMPX0)*(X1-BMPX0)+1\1
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	SET Y1=BMPHI-1/(BMPY1-BMPY0)*(Y1-BMPY0)\1
	SET L=((X1-X0)*(X1-X0))+((Y1-Y0)*(Y1-Y0))**0.5
	FOR I=1:1:L\1+(L#1>0) SET $EXTRACT(@BMPGL@(Y0+(Y1-Y0/L*I\1)),X0+(X1-X0/L*I)+1)=BMPEN
	QUIT
	
XAXE(X0,Y0,LEN,PIK,PLE,XT,FT) ;ZEICHNEN X-ACHSE
	NEW I,FO,H1,H2,JU,K,L,M,TX,X,X1,Y1
	SET X1=BMPLE-1/(BMPX1-BMPX0)*(X0+LEN-BMPX0)+1\1
	SET Y1=BMPHI-1/(BMPY1-BMPY0)*(Y0+$GET(PLE)-BMPY0)\1
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	SET:X0>X1 I=X0,X0=X1,X1=I
	SET K="",$PIECE(K,BMPEN,X1-X0+2)="",$EXTRACT(@BMPGL@(Y0),X0,X1)=K
	FOR L=Y0:6:Y1 FOR I=0:1:PIK IF +PIK'=0 SET $EXTRACT(@BMPGL@(L-1),X0+(X1-X0/PIK*I))=BMPEN,$EXTRACT(@BMPGL@(L-2),X0+(X1-X0/PIK*I))=BMPEN,$EXTRACT(@BMPGL@(L-3),X0+(X1-X0/PIK*I))=BMPEN
	DO TVOR
	IF $DATA(@XT@(0)) SET X=X0,Y0=Y0-H2,JU=1 FOR M=0:1:PIK IF +PIK'=0 KILL TX SET TX=$PIECE(@XT@(0),"~",M+1),X0=X+(X1-X/PIK*M)\1+10 DO XT SET X0=X
	SET M=0 
	FOR  SET M=$ORDER(@XT@(M)) QUIT:M=""  SET X=X0,Y0=Y0-H2+H1-2,JU=1 KILL TX SET TX=@XT@(M),X0=X+(X1-X\2)\1 DO XT
	QUIT
	
YAXE(X0,Y0,LEN,PIK,PLE,XT,FT) ;ZEICHNEN Y-ACHSE
	NEW FO,H1,H2,I,JU,K,L,M,TX,X1,Y,Y1
	SET X1=BMPLE-1/(BMPX1-BMPX0)*(X0+$GET(PLE)-BMPX0)+1\1
	SET Y1=BMPHI-1/(BMPY1-BMPY0)*(Y0+LEN-BMPY0)\1
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	SET:Y0>Y1 I=Y0,Y0=Y1,Y1=I
	FOR I=Y0:1:Y1 SET $EXTRACT(@BMPGL@(I),X0)=BMPEN
	SET K="",$PIECE(K,BMPEN,4)=""
	FOR L=X0:6:X1 FOR I=0:1:PIK IF +PIK'=0 SET $EXTRACT(@BMPGL@(Y0+(Y1-Y0/PIK*I\1)),L-3,L-1)=K
	DO TVOR
	IF $DATA(@XT@(0)) SET X0=X0-$SELECT($GET(FT)>0:9,$GET(FT)<0:6,1:7),Y=Y0,JU=1 FOR M=0:1:PIK IF +PIK'=0 KILL TX SET TX=$PIECE(@XT@(0),"~",M+1),Y0=Y+(Y1-Y/PIK*M)\1 DO YT SET Y0=Y
	SET M=0 FOR  SET M=$ORDER(@XT@(M)) QUIT:M=""  SET Y=Y0,X0=X0-H2+H1-2,JU=1 KILL TX SET TX=@XT@(M),Y0=Y+(Y1-Y\2)\1 DO YT
	QUIT
	
XHIS(AR,Y0,PX,OF) ;HISTOGRAMM AUF X-ACHSE
	NEW I,L,LS,X,Y,X1,Y1
	SET:'$GET(PX) PX=1 
	IF PX<4 SET L="",$PIECE(L,BMPEN,PX+1)=""
	IF PX'<4 SET L="",$PIECE(L,BMPEN,PX-2)="",L=L_111  ;RECHTE ZEILE SCHWARZ
	SET LS=$EXTRACT("01111111111111111111111111111111111111111111111111111111111111111111111",1,$LENGTH(L))
	SET OF=$GET(OF),Y0=$GET(Y0)
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	SET X=""
	FOR  SET X=$ORDER(@AR@(X)) QUIT:X=""  DO
	. SET X1=X+OF  ;S X1=BMPLE-1/(BMPX1-BMPX0)*(X-BMPX0)+1\1+OF
	. SET Y1=BMPHI-1/(BMPY1-BMPY0)*(@AR@(X)-BMPY0)\1
	. SET Y=Y0
	. SET:Y>Y1 I=Y,Y=Y1,Y1=I
	. IF Y1'=Y SET I=Y1+2 SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)="00"_$EXTRACT(LS,1,$LENGTH(LS)-2)  ;OBERE ZEILE SCHWARZ WENN NICHT ZU
	. IF Y1'=Y SET I=Y1+1 SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)="0"_$EXTRACT(LS,1,$LENGTH(LS)-1)  ;OBERE ZEILE SCHWARZ WENN NICHT ZU KLEIN
	. IF Y1'=Y SET I=Y1 SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)=LS  ;OBERE ZEILE SCHWARZ WENN NICHT ZU KLEIN
	. IF Y>3 FOR I=Y+2:1:Y1-1 SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)=L
	. IF Y'>3 FOR I=Y:1:Y1-1 SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)=L
	. IF Y>3 IF Y1>Y DO   ;UNTERE ZEILEN RECHTS BLANK
	. . SET I=Y+1 SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)=$EXTRACT(L,1,$LENGTH(L)-1)_"0"
	. . SET I=Y SET $EXTRACT(@BMPGL@(I),X1,X1+PX-1)=$EXTRACT(L,1,$LENGTH(L)-2)_"00"
	. . QUIT
	. QUIT
	QUIT
	
YHIS(AR,X0,PX,OF) ;HISTOGRAMM AUF Y-ACHSE
	NEW I,L,X,X1,Y,Y1
	SET:'$GET(PX) PX=1 SET L="",$PIECE(L,BMPEN,PX+1)=""
	SET OF=$GET(OF),X0=$GET(X0)
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET Y="" FOR  SET Y=$ORDER(@AR@(Y)) QUIT:Y=""  DO
	. SET X1=BMPLE-1/(BMPX1-BMPX0)*(@AR@(Y)-BMPX0)+1\1+OF
	. SET Y1=BMPHI-1/(BMPY1-BMPY0)*(Y-BMPY0)\1
	. SET X=X0 SET:X>X1 I=X,X=X1,X1=I
	. SET L="",$PIECE(L,BMPEN,X1-X+2)=""
	. FOR I=1:1:PX SET $EXTRACT(@BMPGL@(Y1+I+OF-1),X,X1)=L
	. QUIT
	QUIT
	
KURV(AR,TY) ;ZEICHNEN EINER KURVE
	NEW X,X0,X1,Y0,Y1 SET (X1,Y1)="" SET TY=+$GET(TY)
	SET X="" 
	FOR  SET X=$ORDER(@AR@(X)) QUIT:X=""  DO
	. SET X0=X1,Y0=Y1,X1=X,Y1=@AR@(X)
	. IF X0'="" DO LINE(X0,Y0,X1,Y1)
	. IF TY DO POIN(X1,Y1,TY)
	. QUIT
	QUIT
	
XTXT(X0,Y0,TX,JU,FT) ;TEXT IN X-RICHTUNG
	NEW FO,H1,H2 
	DO TVOR
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	DO XT
	QUIT
	
XT  ;    
	QUIT:TX=""  NEW I,K
	FOR K=1:1:$LENGTH(TX) FOR I=H1:1:H2 SET TX(I)=$GET(TX(I))_$PIECE(^WWWCHAR(0,FO,$ASCII(TX,K),1),"~",I)
	SET K=0 
	SET:$GET(JU) K=$LENGTH(TX(H1))\2
	FOR I=H1:1:H2 SET $EXTRACT(@BMPGL@(Y0-5+I),X0-K,X0-K+$LENGTH(TX(I))-1)=$TRANSLATE(TX(I),"01",BMPBK_BMPEN)
	QUIT
	
YTXT(X0,Y0,TX,JU,FT) ;TEXT IN Y-RICHTUNG
	NEW FO,H1,H2 
	DO TVOR
	SET X0=BMPLE-1/(BMPX1-BMPX0)*(X0-BMPX0)+1\1
	SET Y0=BMPHI-1/(BMPY1-BMPY0)*(Y0-BMPY0)\1
	DO YT
	QUIT
	
YT ;
	QUIT:TX=""  NEW CN,I,K,L,M
	SET CN=0 
	FOR K=1:1:$LENGTH(TX) FOR L=1:1 SET CN=CN+1 DO  IF M="" SET CN=CN-1 QUIT
	. FOR I=H2:-1:H1 SET M=$EXTRACT($PIECE(^WWWCHAR(0,FO,$ASCII(TX,K),1),"~",I),L) QUIT:M=""  SET TX(CN)=$GET(TX(CN))_M
	SET K=0 
	SET:$GET(JU) K=CN\2
	FOR I=1:1:CN SET $EXTRACT(@BMPGL@(Y0-K+I-1),X0-H2+5,X0-H2+5+$LENGTH(TX(I))-1)=$TRANSLATE(TX(I),"01",BMPBK_BMPEN)
	QUIT
	
BMP(FIL) ;SCHREIBEN BMP-DATEI
	NEW A,C,I,K,%DEV
	SET C=BMPLE*BMPTY+31\32*32\BMPTY ;DATENSATZLAENGE
	SET %DEV=$$^WWWDEV(FIL)
	IF %DEV="" QUIT
	WRITE "BM"_$$B4(BMPLE*BMPTY+31\32*BMPHI*4+54+$LENGTH(BMPCO))_$$B2(0)_$$B2(0)_$$B4(54+$LENGTH(BMPCO))
	WRITE $$B4(40)_$$B4(BMPLE)_$$B4(BMPHI)_$$B2(1)_$$B2(BMPTY)_$$B4(0)_$$B4(BMPLE*BMPHI*BMPTY\8)_$$B4(0)_$$B4(0)_$$B4(BMPCN)_$$B4(0)_BMPCO
	DO:BMPTY=1
	. SET A="0000~0001~0010~0011~0100~0101~0110~0111~1000~1001~1010~1011~1100~1101~1110~1111"
	. FOR I=0:1:255 SET K($PIECE(A,"~",I\16+1)_$PIECE(A,"~",I#16+1))=I
	. FOR I=0:1:BMPHI-1 SET A=@BMPGL@(I) FOR K=1:8:C-7 WRITE $CHAR(K($EXTRACT(A,K,K+7)))
	. QUIT
	DO:BMPTY=4
	. FOR A=1:1:16 SET K($EXTRACT("0123456789abcdef",A))=A-1
	. FOR I=0:1:BMPHI-1 SET A=@BMPGL@(I) FOR K=1:2:C-1 WRITE $CHAR(K($EXTRACT(A,K))*16+K($EXTRACT(A,K+1)))
	. QUIT
	CLOSE %DEV
	DO OPEN^WWWSTART
	QUIT
	
GIF(FIL,TRA) ;SCHREIBEN GIF-DATEI
	NEW B,I,K,L,M,S,T,W,X,%DEV
	NEW BMPEN SET BMPEN="" IF $GET(TRA)'="" DO PEN($GET(TRA))
	SET B="0000~0001~0010~0011~0100~0101~0110~0111~1000~1001~1010~1011~1100~1101~1110~1111"
	FOR I=0:1:255 SET K($PIECE(B,"~",I\16+1)_$PIECE(B,"~",I#16+1))=I
	SET %DEV=$$^WWWDEV(FIL)
	IF %DEV="" QUIT
	WRITE "GIF89a"
	WRITE $$B2(BMPLE)_$$B2(BMPHI)_$CHAR(128+(BMPTY-1*16)+(BMPTY-1),0,0)
	FOR I=0:1:$SELECT(BMPTY=1:1,1:15) SET L=$EXTRACT(BMPCO,4*I+1,4*I+3) WRITE $SELECT(L="":$CHAR(0,0,0),1:$EXTRACT(L,3)_$EXTRACT(L,2)_$EXTRACT(L,1))
	WRITE "!"_$CHAR($$HD("F9"))_$CHAR(4)_$CHAR(BMPEN'="",0,0,$SELECT(BMPEN="":0,1:BMPEN))_$CHAR(0)
	WRITE ","_$CHAR(0,0,0,0)_$$B2(BMPLE)_$$B2(BMPHI)_$CHAR(BMPTY-1)
	WRITE $CHAR($SELECT(BMPTY=1:2,1:BMPTY))
	SET S="",T=$SELECT(BMPTY=1:5,1:17) DO GIFIN
	SET (X,W)="" FOR I=BMPHI-1:-1:0 SET X=X_$EXTRACT(@BMPGL@(I),1,BMPLE) DO
	. SET M=1 FOR L=1:1:$LENGTH(X) IF '$DATA(@BMPGF@($EXTRACT(X,M,L))) SET S=$$GB(@BMPGF@($EXTRACT(X,M,L-1)))_S,T=T+1,@BMPGF@($EXTRACT(X,M,L))=T,M=L DO
	. . IF T=4095 DO GIFIN 
	. . IF $LENGTH($ZREFERENCE)>254 DO GIFIN
	. . QUIT
	. SET:M>1 $EXTRACT(X,1,M-1)=""
	. QUIT:$LENGTH(S)<8
	. FOR L=$LENGTH(S):-8:8 SET W=W_$CHAR(K($EXTRACT(S,L-7,L)))
	. SET S=$EXTRACT(S,1,L-8)
	. IF $LENGTH(W)>254 WRITE $CHAR(255),$EXTRACT(W,1,255) SET $EXTRACT(W,1,255)=""
	. QUIT
	SET:X'="" S=$$GB(@BMPGF@(X))_S
	SET S="0000000"_$$GB($SELECT(BMPTY=1:5,1:17))_S
	FOR L=$LENGTH(S):-8:8 SET W=W_$CHAR(K($EXTRACT(S,L-7,L)))
	SET S=$EXTRACT(S,1,L-8)
	IF $LENGTH(W)>254 WRITE $CHAR(255),$EXTRACT(W,1,255) SET $EXTRACT(W,1,255)=""
	IF $LENGTH(W) WRITE $CHAR($LENGTH(W)),W
	WRITE $CHAR(0)
	WRITE $CHAR($$HD("3B"))
	CLOSE %DEV
	DO OPEN^WWWSTART
	KILL @BMPGF
	QUIT
	
GIFIN ;INITIALISIERE KOMPRESSIONSTABELLE
	KILL @BMPGF SET S=$$GB($SELECT(BMPTY=1:4,1:16))_S
	FOR T=0:1:$SELECT(BMPTY=1:1,1:15) SET @BMPGF@($EXTRACT("0123456789abcdef",T+1))=T
	SET T=$SELECT(BMPTY=1:5,1:17)
	QUIT
	
GB(A) ;ERMITTLE KOMPRESSIONSCODE
	QUIT:T\256 $EXTRACT($PIECE(B,"~",A\256+1)_$PIECE(B,"~",A#256\16+1)_$PIECE(B,"~",A#16+1),$SELECT(T\2048:1,T\1024:2,T\512:3,1:4),12)
	QUIT:T\16 $EXTRACT($PIECE(B,"~",A\16+1)_$PIECE(B,"~",A#16+1),$SELECT(T\128:1,T\64:2,T\32:3,1:4),8)
	QUIT $EXTRACT($PIECE(B,"~",A+1),$SELECT(T\8:1,1:2),4)
	;
	
TVOR ;
	SET FO="5MAL7",H1=3,H2=11 
	SET:$GET(FT)>0 FO="7MAL9",H1=1,H2=16 
	SET:$GET(FT)<0 FO="3MAL5",H1=4,H2=9 
	QUIT
	
B4(X) ;
	QUIT $CHAR(X#65536#256,X#65536\256,X\65536#256,X\65536\256)
	
B2(X) ;
	QUIT $CHAR(X#256,X\256)
	
HD(X) ;
	NEW C,I SET X=$TRANSLATE(X,"ABCDEF","abcdef")
	SET C=0 
	FOR I=1:1:$LENGTH(X) SET C=C*16+$FIND("0123456789abcdef",$EXTRACT(X,I))-2
	QUIT C
	
PEN(X) ;STIFTFARBE WAEHLEN
	NEW I,A 
	FOR I=1:1:$LENGTH(BMPFC) SET A=$PIECE(BMPFC,"~",I) IF A'="" IF X=A SET BMPEN=$EXTRACT("0123456789abcdef",I) QUIT  ;FARB-NR
	QUIT
	
COL(X) ;FARBTABELLE AUFBAUEN
	NEW B,C,I,L,N 
	SET (BMPCO,BMPFC)="",BMPCN=0
	SET:X="" X="WS,SW"
	SET BMPTY=1 SET:$LENGTH(X,",")>2 BMPTY=4
	FOR C=1:1:$SELECT(BMPTY=4:16,1:2) SET L=$PIECE(X,",",C) DO
	. IF $FIND(L,"#") SET BMPFC=BMPFC_$PIECE(L,"#",1)_"~",L=$PIECE(L,"#",2),BMPCO=BMPCO_$CHAR($$HD($EXTRACT(L,5,6)),$$HD($EXTRACT(L,3,4)),$$HD($EXTRACT(L,1,2)),0),BMPCN=BMPCN+1 QUIT
	. IF $LENGTH(L)=2 DO  QUIT
	. . FOR I=1:1 SET N=$PIECE($TEXT(COLOR+I),";",2) QUIT:N=""  DO  SET N=$PIECE($TEXT(COLOR+I),";",3) DO  QUIT:N=""
	. . . IF $PIECE(N,"~",1)=L SET BMPFC=BMPFC_L_"~",N=$PIECE(N,"~",3),BMPCO=BMPCO_$CHAR($$HD($EXTRACT(N,5,6)),$$HD($EXTRACT(N,3,4)),$$HD($EXTRACT(N,1,2)),0),BMPCN=BMPCN+1,N=""
	. . . QUIT
	. . QUIT
	. FOR I=1:1 SET N=$PIECE($TEXT(COLOR+I),";",2) QUIT:N=""  DO  SET N=$PIECE($TEXT(COLOR+I),";",3) DO  QUIT:N=""
	. .IF $PIECE(N,"~",2)=L SET BMPFC=BMPFC_L_"~",N=$PIECE(N,"~",3),BMPCO=BMPCO_$CHAR($$HD($EXTRACT(N,5,6)),$$HD($EXTRACT(N,3,4)),$$HD($EXTRACT(N,1,2)),0),BMPCN=BMPCN+1,N=""
	. .QUIT
	. QUIT
	;QUIT
	
COLOR ;* * * * * * * * F A R B T A F E L * * * * * * * *
	;WS~WHITE~FFFFFF~              ;RT~RED~FF0000~
	;GN~GREEN~00FF00~              ;BL~BLUE~0000FF~
	;MG~MAGENTA~FF00FF~            ;CY~CYAN~00FFFF~
	;GB~YELLOW~FFFF00~             ;SW~BLACK~000000~
	;AQ~AQUAMARINE~70DB93~         ;  ~BACKER'SCHOCOLATE~5C3317~
	;  ~BLUEGREEN~408080~          ;  ~BLUEVIOLET~9F5F9F~
	;  ~BRASS~B5A642~              ;  ~BRIGHTGOLD~D9D919~
	;BN~BROWN~A62A2A~              ;  ~BRONZE~8C7853~
	;  ~BRONZELL~A67D3D~           ;  ~CADETBLUE~5F9F9F~
	;  ~COOLCOPPER~D98719~         ;CU~COPPER~B87333~
	;  ~CORAL~FF7F00~              ;  ~CORNFLOWERBLUE~42426F~
	;  ~DARKBROWN~5C4033~          ;  ~DARKGREEN~2F4F2F~
	;  ~DARKGREENCOPPER~4A766E~    ;  ~DARKOLIVEGREEN~4F4F2F~
	;  ~DARKORCHID~9932CD~         ;  ~DARKPURPLE~871F78~
	;  ~DARLSLADEBLUE~6B238E~      ;DG~DARKSLATEGREY~2F4F4F~
	;  ~DARKTAN~97694F~            ;  ~DARKTURQUOISE~7093DB~
	;  ~DARKWOOD~855E42~           ;  ~DIMGREY~545454~
	;  ~DUSTYROSE~856363~          ;  ~FELDSPAR~D19275~
	;  ~FIREBRICK~8E2323~          ;  ~FORESTGREEN~238E23~
	;AU~GOLD~CD7F32~               ;  ~GOLDENROD~DBDB70~
	;GR~GREY~C0C0C0~               ;  ~GREENCOPPER~527F76~
	;  ~GREENYELLOW~93DB70~        ;  ~HUNTERGREEN~215E21~
	;  ~INDIANRED~4E2F2F~          ;OV~KHAKI~9F9F5F~
	;  ~LIGHTBLUE~C9D9D9~          ;  ~LIGHTGREY~A8A8A8~
	;  ~LIGHTSTEELBLUE~8F8FBD~     ;  ~LIGTHWOOD~E9C2A6~
	;LI~LIMEGREEN~32CD32~          ;  ~MANDARIANORANGE~E47833~
	;MA~MAROON~8E236B~             ;  ~MEDIUMAQUAMARIN~32CD99~
	;  ~MEDIUMBLUE~3232CD~         ;  ~MEDIUMFORESTGREEN~6B8E23~
	;  ~MEDIUMFOLDENROD~EAEAAE~    ;  ~MEDIUMORCHID~9370DB~
	;  ~MEDIUMSEAGREEN~426F42~     ;  ~MEDIUMSLATEBLUE~7F00FF~
	;  ~MEDIUMSPRINGGREEN~7FFF00~  ;  ~MEDIUMTURQUOISE~70DBDB~
	;  ~MEDIUMVIOLETRED~DB7093~    ;  ~MEDIUMWOOD~468064~
	;  ~MIDNIGHTBLUE~2F2F4F~       ;NA~NAVYBLUE~23238E~
	;  ~NEONBLUE~4D4DFF~           ;  ~NEONPINK~FF6EC7~
	;  ~NEWMIDNIGHTBLUE~00009C~    ;  ~NEWTAN~EBC79E~
	;  ~OLDGOLD~CFB53B~            ;OR~ORANGE~FF7F00~
	;  ~ORANGERED~FF2400~          ;  ~ORCHID~DB70DB~
	;  ~PALEGREEN~8FBC8F~          ;RS~PINK~BC8F8F~
	;  ~PLUM~EAADEA~               ;QU~QUARTZ~D9D9F3~
	;  ~RICHBLUE~5959AB~           ;  ~SALMON~6F4242~
	;  ~SCARLET~8C1717~            ;  ~SEAGREEN~238E68~
	;  ~SEMI-SWEETCHOCOLATE~6B4226~;  ~SIENNA~8E6B23~
	;AG~SILVER~E6E8FA~             ;  ~SKYBLUE~3299CC~
	;  ~SLATEBLUE~007FFF~          ;  ~SPICYPINK~FF1CAE~
	;  ~SPRINGGREEN~00FF7F~        ;  ~STEELBLUE~236B8E~
	;  ~SUMMERSKY~38B0DE~          ;  ~TAN~DB9370~
	;  ~THISTLE~D8BFD8~            ;  ~TURQUOISE~ABEAEA~
	;  ~VERYDARKBROWN~5C4033~      ;HG~VERYLIGHTGREY~CDCDCD~
	;VL~VIOLET~4F2F4F~             ;  ~VIOLETRED~CC3299~
	;  ~WHEAT~D8D8BF~              ;  ~YELLOWGREEN~99CC32~
	;;;**** ENDE DER FARBTAFEL! DIESE ZEILE NICHT LOESCHEN/AENDERN!!! ****
	;
	QUIT
]]></Routine>
</Export>