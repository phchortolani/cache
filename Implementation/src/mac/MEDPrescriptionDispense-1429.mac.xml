<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescriptionDispense" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescriptionDispense
	
#include COMConst
#include MEDConst
#include INConst
#include COMGridEdit31
	
OnBeforeButtonLine(pidPres,pobjPres,&pobjForm)
    ;-------------------------------------------------------------------------------
    ; Set form to read-only if not editable
    ; 
    ; Called By : Form INDispenseToPatient : D93 "Execute Before Buttonline"
    ; 
    ; Params:	pidPres		- Prescription dispense key
    ;			pobPres		- Prescription dispense object
    ;
    ; Returns:	pobjForm	- Form object (WWW120)
    ;
    ; History:
    ; 30-Jun-2009	DWR		SR16642: Modified from INDispenseToPatient
    ;-------------------------------------------------------------------------------
	new strStatus
	
	quit:$$$NoKey(pidPres)
	
	set strStatus = $$Editable(pidPres)
	
	if $$$ISERR(strStatus) {		//|| ($data(^INIssueLine(0,pidIssue))) {
		set $$$WWW120AuthorizationToModifyData(pobjForm) = $$$EnumReadOnly
	}
	quit
	
	
Editable(pidPres) 
    ;-------------------------------------------------------------------------------
    ; Is form editable (unprocessed) ?
    ;
    ; Params:	pidPres - Prescription Dispense id
    ;
    ; Returns:	status
    ;
    ; History:
    ; 30-Jun-2009	DWR		SR16642: Modified from INDispenseToPatient
    ;-------------------------------------------------------------------------------
	new idLocn,objIssue,strStatus
	
	set strStatus = $$$OK
	quit:$$$NoKey(pidPres) strStatus
	
	if $$$ISOK(strStatus) {
		set idLocn  = $$$INIssueFromLocn($get(^INIssue(0,pidPres,1)))
		if (idLocn'="") && (idLocn'=YLOCATION) {
			set strStatus = $$$MakeStatus("IN00927",idLocn,YLOCATION)
		} ; "This document belongs to Location %1! This Location is %2. The screen will be available only for viewing documents"
	}
	
	if $$$ISOK(strStatus) && $data(^INIssue(0,pidPres,1)) {
		set objIssue = $get(^INIssue(0,pidPres,1))
		
		if $$$INIssueStatus(objIssue) = 9  {
			set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"
		}
		
		if $$$INIssuePosted(objIssue)'="" {
			set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"
		}
	}
	quit strStatus
	
	
OnAfterPrimaryKey(pYM,pYFORM,pYSEITE,pstrKey,&pYFELD,&pobjForm)
	;-------------------------------------------------------------------------------
	; Disable form if not FromLocn is empty or not user's locn
	;			
	; Called By : Form INDispenseToPatient
	;		
	; Inputs : 
	;
	; ByRef : YLOCATION
	;
	; Returns : Nothing
	;
	; History :
    ; 30-Jun-2009	DWR		SR16642: Copied from INDispenseToPatient
	;-------------------------------------------------------------------------------
	if ($$$INIssueFromLocn(pYFELD)'="") && ($$$INIssueFromLocn(pYFELD)'=YLOCATION) {
		set $$$WWW120AuthorizationToModifyData(pobjForm) = $$$EnumReadOnly
	}
	quit
	
	
OnAfterDataFields(pidForm,pidKey,pobjDispense)
    ;-------------------------------------------------------------------------------
    ; Code for Grid Edit 
    ;
    ; Returns:
    ;
    ; History:
 	; 05-Nov-2010	PPP		SR16642:Showing of grid is automatic if configured 
	;						in Form metadata.
    ; 30-Jun-2009	DWR		SR16642: Created, modified version INDispenseToPatient
    ;-------------------------------------------------------------------------------
	//new idForm,YAUSWAHL,blnEnabled
	
	//set idForm     = "MEDPresDispLine"
	//set blnEnabled = ($$$INIssueStatus(pobjDispense) '= 9)
	
	//set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	//set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
	//set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	//set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	//set $$$COMGridEditParameterContainer(YAUSWAHL)     = "MEDPrescriptionDispense"
	
	//$$$GRIDStart(idForm,pidKey)
	if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
		do PrintJS^VARMEDPresDispLine(YFORM)
	}

	quit
	

Enabled(YFELD)
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescriptionDispense : Grid Tab (COMGridEditParameter)
	;
	;
	; History:
	; 05-Nov-2010	PPP		SR16642: Created
	;-------------------------------------------------------------------------------
	quit ($$$INIssueStatus(YFELD) < 2)
	//quit $$$YES

	
OnBeforeSave(pidIssue, pobjIssue)
    ;-------------------------------------------------------------------------------
    ; Called By: Form MEDPrescriptionDispense
    ; 
	; Params:	pidIssue - INIssue id
    ; 
    ; Returns: 
    ;
    ; History:
    ; 31-Aug-2009	GRF		SR16642: Use elseif structure
    ; 30-Jun-2009	DWR		SR16642: Modified from INDispenseToPatient
    ;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	if ($$$INIssueType(pobjIssue)=1) && ($$$INIssueToLocn(pobjIssue)="") {          // Inter-Department Issue
		set strStatus = $$$MakeStatus($$$Text($listbuild("IN01030",1)))
		;   "To-Location is invalid for current Issue Type (%1)~"
		
	} elseif ($$$INIssueType(pobjIssue)=2) {                                        // Inter-Site
		set strStatus = $$$MakeStatus($$$Text($listbuild("IN01031",2)))
		;   "Invalid Issue Type (%1) for current operation ~"
		
	} elseif ($$$INIssueType(pobjIssue)=3) && ($$$INIssueToLocn(pobjIssue)'="") {   // Dispense/Sale Issue
		set strStatus = $$$MakeStatus($$$Text($listbuild("IN01030",1)))
		;   "To-Location is invalid for current Issue Type (%1)~"
	}
	
	//if $$$ISOK(strStatus) set strStatus = $$$GRIDSave(pidIssue,"MEDPresDispLine")
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	quit
	
	
IsDeletable(pidKey,pYFELD)
	;-------------------------------------------------------------------------------
	; Determines if the delete button should be disabled
	;			
	; Called By : MEDPrescriptionDispense FORMSPEC
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
    ; 30-Jun-2009	DWR		SR16642: Created, copied version INDispenseToPatient
	;-------------------------------------------------------------------------------
	quit:$$$NoKey(pidKey)
	
	if ($$$INIssueStatus(pYFELD)>1) || ($$$INIssuePosted(pYFELD)'="") {
		set Q = $$$QDontDelete
	}
	quit
	
	
Postable(pidIssue,pobjIssue)
	;-------------------------------------------------------------------------------
	; Test whether this record is postable
	; 
	; Called By: Form MEDPrescriptionDispense : Button 1 - Execute To Check For Disabled
	; 
	; Params:	pidIssue - Issue id
	;
	; ByRefs:
	;
	; Returns:	postable status
	;
	; History:
	; 09-Feb-2012	SCR		SR17979: Include Transfer to Ward (Transfer)
	; 27-Aug-2009	DWR		SR16859: Expanded error reporting back to user with more 
	; 							specific information.
    ; 30-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new idLine,objIssue,strStatus
	
	set strStatus = $$$OK
	if '$$$NoKey(pidIssue) {
		if $$$ISOK(strStatus) {
			if $$$INIssueStatus(pobjIssue) > 1 {
				set strStatus = $$$MakeStatus("MED00361")  ; "Prescription already Firmed"
			}
		}
		
		if $$$ISOK(strStatus) {
			if ($$$INIssueType(pobjIssue) '= 5) && ($$$INIssueType(pobjIssue) '= 1) { ;SR17979
				set strStatus = $$$MakeStatus("MED00360")  ; "Can only dispense a Prescription or Transfer Type"
			;if $$$INIssueType(pobjIssue) '= 5 {
			;	set strStatus = $$$MakeStatus("MED00360")  ; "Can only dispense a Prescription Type"
			}
		}
		
		if $$$ISOK(strStatus) {
			if '$data(^INIssueLine(0,pidIssue)) {
				set strStatus = $$$MakeStatus("MED00356")  ; "Prescription has no lines"
			
			} else {
				
				set idLine = ""
				for {	
					set idLine = $order(^INIssueLine(0,pidIssue,idLine))
					quit:idLine=""
					
					set objIssue = $get(^INIssueLine(0,pidIssue,idLine,1))
					if $$$INIssueLineFromStorage(objIssue) = "" {
						set strStatus = $$$MakeStatus("MED00394",idLine)     ; "'From Storage' required on line %1"
					
					} elseif $$$INIssueLineBundle(objIssue) = "" {
						set strStatus = $$$MakeStatus("MED00395",idLine)     ; "Bundle required on line %1"			
					}
					quit:$$$ISERR(strStatus)
				}
			}
		}
		if $$$ISOK(strStatus) {
			set strStatus = $$CheckLines^MEDPresDispLine(pidIssue,pobjIssue)
		}
	}
	$$$YQHandler(strStatus)
	quit strStatus
	
	
Post(pidIssue)
	;-------------------------------------------------------------------------------
	; Post adjustment - back populates the originating prescription if a successful
	; issue.
	;
    ; Called By: Form MEDPrescriptionDispense : Button 1 - Execute OnClick
    ; 
	; Returns:Status
	;
	; History:
	; 17-Sep-2010	PPP		SR16642: Set the $$$MEDPrescriptionLastIssued
    ; 30-Jun-2009	DWR		SR16642: copied here from INDispenseToPatient
	;-------------------------------------------------------------------------------
	new objIssue,objPres,strStatus
	
	set objIssue  = $get(^INIssue(0,pidIssue,1))
	set strStatus = $$Postable(pidIssue,objIssue)
	
	if $$$ISOK(strStatus) {
		set strStatus = $$Transaction^COMTransaction("PostTxn^INIssueAuto("""_pidIssue_""")",$$$YES)
		
		if $$$ISOK(strStatus) {
			set objPres = $get(^MEDPrescription(0,$$$INIssueReference(objIssue),1))
			
			set $$$MEDPrescriptionLastIssued(objPres)		= $horolog	
			set $$$MEDPrescriptionRepeatsDispensed(objPres) = $$$MEDPrescriptionRepeatsDispensed(objPres) + 1  ; add 1 to "prescriptions dispensed" as integrity check
			
			if $$$MEDPrescriptionRepeatsDispensed(objPres) < ($$$MEDPrescriptionNumberOfRepeats(objPres) + 1) {
				set $$$MEDPrescriptionStatus(objPres) = 3     ; Active
			} else {
				set $$$MEDPrescriptionStatus(objPres) = 9     ; A-Close
			}
			set strStatus = $$$Save("MEDPrescription",$$$INIssueReference(objIssue),objPres,$$$YES)
		}
	}
	
	if $$$ISOK(strStatus) {
		do ReloadForm^COMUtilForm()
  	} else {
	 	do ReturnError^COMUtilError(strStatus)
 	}
	quit strStatus
	
]]></Routine>
</Export>