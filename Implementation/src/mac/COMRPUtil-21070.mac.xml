<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMRPUtil" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMRPUtil
 
#include WWWConst
#include COMSYS
 
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; 		Interface code and common reporting functions
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 
Text(plstMessage,pidLanguage)
	;-------------------------------------------------------------------------------
	; Decodes a listbuild language text list into the appropriate string
	;
	; Params:
	; plstMessage - Message Id
	; pidLanguage - Language Id
	;
	; ByRefs: None
	;
	; Returns: Message
	;
	; History:
	; 16-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new SPRACHE
 
	set SPRACHE = pidLanguage
 
	quit $$DecodeError^COMUtilError(plstMessage,,pidLanguage)
 
 
Date(pdteDate,pidLanguage)
	;-------------------------------------------------------------------------------
	; Return a formatted date 
	;
	; Params:
	; pdteDate - Internal date
	; pidLanguage - Language Id
	;
	; ByRefs: None
	;
	; Returns: Formatted date
	;
	; History:
	; 16-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new SPRACHE,Y
	
	set SPRACHE = pidLanguage
 
	set Y = "~" // Hardcoded in WWWVORG
 
	quit $$^WWWDATE(pdteDate,pidLanguage)
	
	
DateToHorolog(pstrDate,pidLanguage)
	;-------------------------------------------------------------------------------
	; Convert a formatted date back to horolog
	;
	; Params: pstrDate		: The formatted date
	; 		  pidLanguage	: The language
	;
	; ByRefs:
	;
	; Returns: horolog date format
	;
	; History:
	; 28-Sep-2006	Steve S		SR14952: Created
	;-------------------------------------------------------------------------------
	new Y,SPRACHE
	
	set SPRACHE = pidLanguage	
	set Y = "~" // Hardcoded in WWWVORG
 
	quit $$^WWWDATE1(pstrDate)
	
 
Time(ptmeTime,pidLanguage)
	;-------------------------------------------------------------------------------
	; Return a formatted time 
	;
	; Params:
	; pdteDate - Internal time
	; pidLanguage - Language Id
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 30-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new SPRACHE,Y
	
	set SPRACHE = pidLanguage
	set Y = "~" // Hardcoded in WWWVORG
 
	quit $$^WWWTIME(ptmeTime,pidLanguage)
	
 
FormatCurrency(pcurAmount,pidCompany,pidLanguage,pstrSuppressZeros="-",pblnSymbol=$$$NO)
	;-------------------------------------------------------------------------------
	; Format a currency amount in standard accounting format
	;
	; Params:
	; curAmount - Currency Amount
	; pidCompany - Company Id
	; pidLanguage - Language Id
	; pstrSuppressZeros - String to display instead of zero values
	;
	; ByRefs: None
	;
	; Returns: Formatted currency amount
	;
	; History:
	; 21-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new objCurrency,idCurrency,intDP,strThousandsSep,objCompany,strAmount
 
	set objCompany = $get(^WWW012(0,pidCompany,1))
	set idCurrency = $$$WWW012StandardCurrency(objCompany)
	
	set intDP = $$GetDecimalPoints^WWWZAHL(idCurrency)
 	
 	// NOTE: This perpetuates already existing code functionality
	if pidLanguage = "DE" {
		set strThousandsSep = "."
	} else {
		set strThousandsSep = ","
	}
 
	if (pstrSuppressZeros = "") || (pstrSuppressZeros '= "") && (+pcurAmount '= 0) {
		if 'pblnSymbol {
			set strAmount = $fnumber(pcurAmount,"P"_strThousandsSep,intDP)
		} else { 
			set strAmount = $$CurrencyDisplay(pidCompany,pidLanguage,pcurAmount)
		}
	} else {
		set strAmount = pstrSuppressZeros
		if pblnSymbol set strAmount = $$CurrencyDisplay(pidCompany,pidLanguage,0)
	}
 
	quit strAmount
	
	
CurrencyDisplay(pidCompany,pidLanguage,pfltAmount)
	;-------------------------------------------------------------------------------
	; Format a currency to display (with symbol)
	;
	; Params: pidCompany		: The company number (YM)
	; 		  pidLanguage		: The language (SPRACHE)
	; 		  pfltAmount		: The amount to format
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-Oct-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new YM,SPRACHE,Y
 
	set YM 		= pidCompany
	set SPRACHE = pidLanguage
	set Y 		= "~"
	
	quit $$^WWWZAHL(pfltAmount,,,$$$WWW012StandardCurrency($get(^WWW012(0,YM,1))))
		
	
TimeStamp(pidLanguage)
	;-------------------------------------------------------------------------------
	; Return a formatted timestamp string
	;
	; Params:	pidLanguage		: The language we're in
	;
	; ByRefs:
	;
	; Returns: strTS	(The formatted string)
	;
	; History:
	; 26-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new dteToday,tmeNow
 
	set dteToday = $piece($horolog,$$$COMMA,1)
	set tmeNow	 = $piece($horolog,$$$COMMA,2)
 
	quit ($$Date(dteToday,pidLanguage)_" "_$$Time(tmeNow,pidLanguage))
	
	
InterpretDateString(pstrDate,pidLanguage)
	;-------------------------------------------------------------------------------
	; Try to translate a string into a +$horolog counter.
	; 
	; If it's already a valid +$horolog, return this.
	; If not, try to translate it to $horolog using WWWDATE1.
	;
	; Params: pstrDate		: The string to interpret
	; 		  pidLanguage	: The language we're in
	;
	; ByRefs:
	;
	; Returns: strInterpretation
	;
	; History:
	; 19-Sep-2006	Steve S		SR14954: Created
	;-------------------------------------------------------------------------------
	new strInterpretation
 
	set strInterpretation=$get(pstrDate)
 
	if (pstrDate'="") {
		if '$isValidNum(pstrDate) {
			set strInterpretation=$$DateToHorolog(pstrDate,pidLanguage)
			if '$isValidNum(strInterpretation) {
				set strInterpretation=""
			}
		}
	}
 
	quit strInterpretation
	
	
GetDecimalPoints(pidCurrency)
	;-------------------------------------------------------------------------------
	; Wrapper for Getting the number of d.p. for a currency
	;
	; Params:	pidCurrency			: The currency we're looking at
	;
	; ByRefs:
	;
	; Returns: the number of d.p.
	;
	; History:
	; 28-Sep-2006	Steve S		SR14956: Created
	;-------------------------------------------------------------------------------
	quit $$GetDecimalPoints^WWWZAHL(pidCurrency)
	
	
PrepareData(pYUSER)
	;-------------------------------------------------------------------------------
	; Merge the appropriate data over for the query.
	;
	; Params: pYUSER		: The YUSER subscript
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 18-Jan-2007	Steve S		SR15355: Use namespace subscript
	; 10-Oct-2006	Steve S		SR15012: Use $query
	; 28-Sep-2006	Steve S		SR14956: Created
	;-------------------------------------------------------------------------------
	new ret,strQuery,strNS
 
	set strNS 	= $zutil(5) 
	
	set ret 	= $sortbegin(^CacheTempRow)
 
	$$$Query("^CacheTempData("""_strNS_""","""_pYUSER_""")",strQuery)
		set ^CacheTempRow(strNS,pYUSER,$increment(^CacheTempRow(strNS,pYUSER))) = @strQuery
	$$$End
 
	kill ^CacheTempData(strNS,pYUSER)
	
	set ret 	= $sortend(^CacheTempRow)
 
	quit
	
	
NextKey(pidCompany,pidClass)
	;-------------------------------------------------------------------------------
	; Return the next key for a class
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidClass		: The table (class) name.
	;
	; ByRefs:
	;
	; Returns: the key
	;
	; History:
	; 29-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new Y,YM
 
	set YM = pidCompany
	set Y  = "~" // hard-coded in WWWVORG
 
	quit $$^WWWNEXT(pidClass)
	
	
Save(pidCompany,pstrYUSER,pidClass,pidKey,pobjRecord)
	;-------------------------------------------------------------------------------
	; WWWSPEI wrapper for reporting code
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pstrYUSER		: The session id (YUSER)
	; 			pidClass		: Class to save to
	; 			pidKey			: Primary Key
	; 			pobjRecord		: Data record
	;
	; ByRefs:
	;
	; Returns: status
	;
	; History:
	; 29-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new Y,YM,YBED
 
	set YM 		= pidCompany
	set Y 		= "~"	// hard-coded in WWWVORG
	set YBED 	= $$$GetUserID(pstrYUSER)
 
	quit $$$Save(pidClass,pidKey,pobjRecord,$$$YES)
]]></Routine>
</Export>