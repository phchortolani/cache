<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INKUNDEGRUPPE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INKUNDEGRUPPE(YINHALT,AUSW) ;INKUNDEGRUPPE;FIS;PRUEFEN UND ANZEIGEN KUNDENGRUPPEN;14.05.2003
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		PRUEFEN UND ANZEIGEN KUNDENGRUPPEN
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	14.05.2003
	;|
	;\------------------------------------------------------------------/
	;
	NEW CUST1,GUELTIG
	QUIT:$GET(YINHALT)=""
	QUIT:+$GET(AUSW)=0
	SET GUELTIG=0
	SET CUST1=$GET(^INKUNDE(YM,YINHALT,1))
	IF $GET(AUSW)=1 IF $PIECE(CUST1,Y,216)=1 SET GUELTIG=1
	IF $GET(AUSW)=2 IF $PIECE(CUST1,Y,217)=1 SET GUELTIG=1
	IF $GET(AUSW)=3 IF $PIECE(CUST1,Y,218)=1 SET GUELTIG=1
	;IF $GET(AUSW)=4 IF $PIECE(CUST1,Y,219)=1 SET GUELTIG=1  ;FIS;AUSGESCHALTET;23963;14.11.03
	IF GUELTIG=0 SET %TXT(1)="#Y"_YFORM_YART_(35+AUSW)_"#!"_$$^WWWTEXT(33416,,1)  ;kunde ist für diese auswahl nicht gültig ;is to this Selection not valuable 
	QUIT
	
SUCH(AUSW) ;SUCHFUNKTION KUNDEN ALS ANSCHRIFTEN  -> EINSPRUNG AUS FORMULAR ;when out of form 
	;AUSW=ADRESSENART 1=KUNDENGRUPPE 2=VERKAUFSGRUPPE 3=RECHNUNGSEMPFÄNGER 4=LIEFERANSCHRIFT
	DO
	. WRITE YCR
	. WRITE "<A HREF="_""""
	. SET URL=YAKTION_"EP=WWWMANU1&YEXEC=D|SUCH1^INKUNDEGRUPPE("_+$GET(AUSW)_")&YUSER="_YUSER_"&YBED="_YBED_"&YTRAKT="_YTRAKT_"&YUCI="_$GET(YUCI)_"&YM="_YM_"&YKEY="_$GET(YKEY)_"&YFORM="_$GET(YFORM)_"&YPARA="_$GET(YART)_$GET(YLFN)
	. SET OPT="HEIGHT=350,WIDTH=260,SCROLLBARS=YES,RESIZEABLE=YES"
	. WRITE "javascript:var parameter=window.open('"_URL_"','Suche','"_OPT_"');"  
	. WRITE """"
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"search1.gif"_""""_" ALIGN=ABSBOTTOM BORDER=0 TITLE="_""""_$$^WWWTEXT(148)_""""_" border=0>"  ;suchen ;seek 
	. WRITE "</A>"
	. QUIT
	QUIT
	
SUCH1(AUSW) ;ANZEIGEN DER FERTIGUNGSNUMMERN ;display the 
	NEW ADR,ADR1,ANZ
	DO ^WWWVAR
	;IF $PIECE($GET(^WWW120(0,"INKUNDE",1)),Y,123)=1 SET YHTMFORM="WWW2"
	DO ^WWWSTART()
	DO
	. NEW YFORM,YVOR
	. SET $PIECE(YVOR,Y,5)=""
	. DO ^WWWBODY(2)
	. QUIT
	KILL ^WWWSOR(YUSER,"ADR")
	IF YPARA="" IF $GET(AUSW)=1 SET YPARA="D36"
	IF YPARA="" IF $GET(AUSW)=2 SET YPARA="D37"
	IF YPARA="" IF $GET(AUSW)=3 SET YPARA="D38"
	;IF YPARA="" IF $GET(AUSW)=4 SET YPARA="D39"  ;FIS;AUSGESCHALTET;23963;14.11.03
	SET ANZ=0
	WRITE "<TABLE BORDER=0 CELLSPACING=0 CELLPADDING=0>"
	SET ADR="" FOR  SET ADR=$ORDER(^INKUNDE(YM,ADR)) QUIT:ADR=""  DO
	. SET ADR1=$GET(^INKUNDE(YM,ADR,1))
	. QUIT:ADR1=""
	. IF +$GET(AUSW)=1 QUIT:$PIECE(ADR1,Y,216)'=1  ;KUNDE IST NICHT KREDITEINHEIT ;customer Not 
	. IF +$GET(AUSW)=2 QUIT:$PIECE(ADR1,Y,217)'=1  ;KUNDE IST NICHT VERKAUFSEINHEIT ;customer Not 
	. IF +$GET(AUSW)=3 QUIT:$PIECE(ADR1,Y,218)'=1  ;KUNDE IST NICHT RECHNUNGSANSCHRIFT ;customer Not 
	. ;IF +$GET(AUSW)=4 QUIT:$PIECE(ADR1,Y,219)'=1  ;KUNDE IST NICHT LIEFERANSCHRIFT  ;FIS;AUSGESCHALTET;23963;14.11.03
	. IF $PIECE(ADR1,Y,8)="" SET $PIECE(ADR1,Y,8)=$PIECE(ADR1,Y,1)  ;KUNDNENAME
	. IF $PIECE(ADR1,Y,8)="" SET $PIECE(ADR1,Y,8)=" "
	. SET ^WWWSOR(YUSER,"ADR",$PIECE(ADR1,Y,8),ADR)=""  ;MÖGLICHE KUNDEN
	. QUIT
	SET NAME="" FOR  SET NAME=$ORDER(^WWWSOR(YUSER,"ADR",NAME)) QUIT:NAME=""  DO
	. SET ADR="" FOR  SET ADR=$ORDER(^WWWSOR(YUSER,"ADR",NAME,ADR)) QUIT:ADR=""  DO
	. . SET ADR1=$GET(^INKUNDE(YM,ADR,1))
	. . QUIT:ADR1=""
	. . ;
	. . SET ANZ=ANZ+1
	. . WRITE "<TR><TD NOWRAP ALIGN=LEFT>"
	. . WRITE "<FONT FACE=ARIAL SIZE=2>"
	. . WRITE "<A onClick='return doLink(this)' HREF=""JavaScript:opener.document."_YHTMFORM_".Y"_YFORM_YPARA_".value='"_ADR_"';opener.document."_YHTMFORM_".Y"_YFORM_YPARA_".focus();document.close();close();"_""""
	. . WRITE ">"
	. . WRITE NAME_" ("_ADR_")"
	. . WRITE "</A>"
	. . WRITE "</FONT>"
	. . WRITE "</TD></TR>"
	. . WRITE YCR
	. . QUIT
	. QUIT
	IF ANZ=0 WRITE "<tr><td align=left><font size=2>"_$$^WWWTEXT(46)_"</font></td></tr>"  ;KEINE DATEN ;no 
	WRITE "</TABLE>"
	WRITE "<BR>"
	KILL ^WWWSOR(YUSER,"ADR")
	DO ^WWWSTOP
	QUIT
]]></Routine>
</Export>