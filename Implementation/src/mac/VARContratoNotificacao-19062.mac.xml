<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContratoNotificacao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContratoNotificacao
#include COMSYS
#include VARConst
 
OnAfterPrimaryKey(pYKEY)
	kill ^COMTempList(YM,YBED,"VARContratoNotificRecebedores")
	
	new CodContrato, objExecutor, tipoExecutor, descTipo, matricula, nome
	
	if (pYKEY = "") {
		set ^COMTempList(YM,YBED,"VARContratoNotificRecebedores",1,1) = "Executor"
		quit
	}
	
	set CodContrato = $piece(pYKEY,",",1)
	
	$$$Order3(^VARContratoExecutor,YM,CodContrato,idRecebedor)
		
		set objExecutor = $get(^VARContratoExecutor(YM,CodContrato,idRecebedor,1))
		
		set tipoExecutor = $piece(objExecutor,Y,1)
		
		set descTipo  = $piece($get(^VARContratoExecutorTipo(YM,tipoExecutor,1)),Y,1)
		set matricula = $piece(objExecutor,Y,2)
		set nome 	  = $piece($get(^VARContratoExecutorCad(YM,matricula,1)),Y,1)
 
		set ^COMTempList(YM,YBED,"VARContratoNotificRecebedores",idRecebedor,1) = "Executor "_descTipo_" - "_nome
		
	$$$End
 
	quit
	
	
ConfirmaNotificacao(pYKEY)
 	;-------------------------------------------------------------------------------
    ; Faz o envio e a confirmação da notificação do executor.
    ; 
    ; History:
    ; 14-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:($$$KEY2(pYKEY) = "")
	
	new strStatus, objNotificacao, enviadoEm
	set strStatus = $$$OK
 
 	set objNotificacao = $get(^VARContratoNotificacao(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
	set enviadoEm = $piece(objNotificacao,Y,6)
	
	if (enviadoEm '= "") {
		set strStatus = $$$MakeStatus("IN00377")  ;"Record already processed"
	} else {
		set strStatus = $$Transaction^COMTransaction("ConfirmaNotificacaoTxn^VARContratoNotificacao("""_pYKEY_""")",$$$YES)
	}
	
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	} else {
		$$$Alert("Notificação enviada com sucesso.")
	}
	
	do RefreshForm^COMUtilForm()	
 
	quit
	
ConfirmaNotificacaoTxn(pYKEY) 
 	new CodContrato, NoNotificacao, objNotificacao, nextNotificacao, Remetente, Assunto, Mensagem,
 		strDestinatarios, cnt, idExecutor, User, objContrato, NoContrato, Tipo, DescTipo, Categoria,
 		DescCategoria, EmailDestino, EmailGerencia
 	
 	set CodContrato   = $$$KEY1(pYKEY)
 	set NoNotificacao = $$$KEY2(pYKEY)
 	
 	set objNotificacao = $get(^VARContratoNotificacao(YM,CodContrato,NoNotificacao,1))
 
	set $piece(objNotificacao,Y,6) = $horolog  					;Enviado Em
	set $piece(objNotificacao,Y,7) = YBED  	   					;Enviado Por
	
	set nextNotificacao = $$GetNextLine(CodContrato) ;No. Notificação
	set $piece(objNotificacao,Y,8) = nextNotificacao
	
	set strStatus = $$$Save("VARContratoNotificacao",pYKEY,objNotificacao,1) 	
 
 	if $$$ISOK(strStatus) {	
 	
		;Envia o email com a notificação para os usuários selecionados
		set NoContrato    = $$GetNumeroContrato^VARContrato(CodContrato)
		set Tipo 	  	  = $$GetTipoContrato^VARContrato(CodContrato)
		set DescTipo  	  = $$GetDescTipoContrato^VARContrato(CodContrato)
		set Categoria 	  = $$GetDescTipoContrato^VARContrato(CodContrato)	
		set DescCategoria = $$GetDescCategoriaContrato^VARContrato(CodContrato)
		
		set Remetente = YBED
		set Assunto   = "Notificação - "_DescTipo_" No. "_NoContrato_": "_$piece(objNotificacao,Y,1)
			
		set Mensagem = "(Mensagem enviada pelo sistema)<br /><br />"
		set Mensagem = Mensagem_DescTipo_" No. "_NoContrato_"<br />"
		set Mensagem = Mensagem_"Tipo de Contrato:  "_DescTipo_"<br />"	
		
		if DescCategoria '= "" {
			set Mensagem = Mensagem_"Categoria:  "_DescCategoria_"<br />"
		}
		
		set Mensagem = Mensagem_"Objeto: "_$$GetObjetoContrato^VARContrato(CodContrato)_"<br />"	
		set Mensagem = Mensagem_"Enviado por "_$piece($get(^WWW013(YM,$piece(objNotificacao,Y,7),1)),Y,1)_" em "_$zdt($piece(objNotificacao,Y,6),4)_"<br /><br />"
		set Mensagem = Mensagem_"Notificação: <br />--<br />"_$piece(objNotificacao,Y,2)_"<br />--"
	 
		set strDestinatarios = $piece(objNotificacao,Y,3)
 
	 	//Envia o email para os destinatários			
		for cnt = 1:1:$length(strDestinatarios,";") {
	    	set idExecutor 	 = $piece(strDestinatarios,";",cnt)
	    	set User 		 = $piece($get(^VARContratoExecutor(YM,CodContrato,idExecutor,1)),Y,2)
    		set EmailDestino = $piece($get(^VARContratoExecutorCad(YM,User,1)),Y,3) 
    		
			continue:(EmailDestino = "")
			continue:($$IsValidEmailAddress^VAREmail(EmailDestino) = $$$NO)
	    	
			set strStatus = $$EnviaEmailContratos^VAREmail(Remetente,User,Assunto,Mensagem)
		}
		
		//Envia cópia do email para a gerência
		set EmailGerencia = $piece($get(^VARContratoParametrosEmail(YM,YM,1)),Y,1)
		if ((EmailGerencia '= "") && ($$IsValidEmailAddress^VAREmail(EmailGerencia) = $$$YES)) {
			set strStatus = $$EnviaEmailContratos^VAREmail(Remetente,"GerenciaDeContratos",Assunto,Mensagem)
		}
	}
	
	quit strStatus
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Mostra a tabela de notificações para um contrato
    ; 
    ; History:
    ; 10-Fev-2011	Gustavo Fiuza		Mostrar notificação mesmo que não esteja enviada
    ; 14-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new currentContrato, CodContrato, NoNotificacao, Assunto, Notificacao, EnviadoEm, EnviadoPor
	
	set currentContrato = $piece(pYKEY,",",1) 	
	
	&sql(DECLARE Notificacoes CURSOR FOR
		 SELECT CodContrato,CodNotificacao,Assunto,Notificacao,EnviadoEm,EnviadoPor,NoNotificacao
		 INTO :CodContrato,:CodNotificacao,:Assunto,:Notificacao,:EnviadoEm,:EnviadoPor,:NoNotificacao
		 FROM VARContratoNotificacao
		 WHERE CodContrato = :currentContrato
		   --AND EnviadoEm is not null
		 ORDER BY EnviadoEm desc
	)
 	
	&sql(open Notificacoes)
    if (SQLCODE) {
		quit
    }
 	
	//Tabela
 	new lstHeader, chave, count	
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","Assunto","Notificação","Enviado Em","Enviado Por")
  	
  	set count = 0
 
  	if $$Start^COMTable(lstHeader,"Notificações enviadas deste contrato",,,$$$NO) { 	
	  	
		for { &sql(fetch Notificacoes)
				   quit:SQLCODE 	  	
 			
 			set chave = CodContrato_","_CodNotificacao
 			
			set count = $i(count)
			
			if $length(Notificacao) > 220 set Notificacao = $extract(Notificacao,1,220)_"..."
			
			if (EnviadoEm  '= "") set EnviadoEm = $zdt(EnviadoEm,4)			
			if (EnviadoPor '= "") set EnviadoPor = EnviadoPor_" - "_$$SQLGetUserName^VARSQL(EnviadoPor)
			
			do NewLine^COMTable()
			do InsertCell^COMTable(count,"VARContratoNotificacao",chave)
			do InsertCell^COMTable(Assunto,"VARContratoNotificacao",chave,,,,,,,1)			
			do InsertCell^COMTable(Notificacao,"VARContratoNotificacao",chave,,,,,,,1)			
			do InsertCell^COMTable(EnviadoEm,"VARContratoNotificacao",chave)			
			do InsertCell^COMTable(EnviadoPor,"VARContratoNotificacao",chave)
			do EndLine^COMTable()
			
		}
		
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhuma notificação enviada neste contrato","VARContratoNotificacao",currentContrato,,,,5)
			do EndLine^COMTable()				
		}		
		
		do Stop^COMTable()		
  	}
  	
	&sql(close Notificacoes)
	
	quit
	
GetNextLine(pCodContrato)
	new idNotificacao, idNextNotificacao, isEnviado
	set idNotificacao = ""
	
	set idNextNotificacao = 0
	
	$$$Order3(^VARContratoNotificacao,YM,pCodContrato,idNotificacao)	
		set isEnviado = $piece($get(^VARContratoNotificacao(YM,pCodContrato,idNotificacao,1)),Y,6)
		continue:(isEnviado = "")
		set idNextNotificacao = idNextNotificacao + 1
	$$$End
	
	set idNextNotificacao = idNextNotificacao + 1  //Setando a próxima
	quit idNextNotificacao
	
CanConfirmarNotificacao(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o usuário pode confirmar/enviar a notificação
    ;
    ; History:
    ; 26-Nov-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
    new strStatus, objNotificacao, enviadoEm, idMatricula, idExecutor, listaRecebedores, count
    
    set strStatus = $$$OK
    
 	if $$$KEY2(pYKEY) = "" {
	 	set strStatus = $$$MakeStatus("É necessário criar a notificação antes de enviá-la.")	
 	}
	
	if $$$ISOK(strStatus) {
	 	set objNotificacao = $get(^VARContratoNotificacao(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
		set enviadoEm = $piece(objNotificacao,Y,6)
	
		if (enviadoEm '= "") {
			set strStatus = $$$MakeStatus("A notificação já foi enviada.")	
		}
	}

	if $$$ISOK(strStatus) {
		if ($data(^VARContratoExecutor(YM,$$$KEY1(pYKEY))) > 0) {
			if ($length($$$VARContratoNotificacaoRecebedores(objNotificacao)) > 0) {
				set listaRecebedores = $$$VARContratoNotificacaoRecebedores(objNotificacao)
				for {
					set count = $increment(count)
					set idExecutor = $piece(listaRecebedores,";",count)
					if (idExecutor = "") quit
					set idMatricula = $$$VARContratoExecutorMatricula(^VARContratoExecutor(YM,$$$KEY1(pYKEY),idExecutor,1))
					if ($$$VARContratoExecutorCadEmail(^VARContratoExecutorCad(YM,idMatricula,1)) = "") {
						set strStatus = $$$MakeStatus("O Executor de matrícula "_idMatricula_" não possúi email cadastrado.")
					}
					if ($$$ISERR(strStatus)) quit
				}
			}
		}
		else {
			set strStatus = $$$MakeStatus("Esse contrato não possúi executores cadastrados.")
		}
	}
	
	$$$YQHandler(strStatus)
	
	quit
]]></Routine>
</Export>