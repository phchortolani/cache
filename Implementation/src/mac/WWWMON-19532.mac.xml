<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMON" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMON ;WWWMON;DT;MONATSABSCHLUSS;09.11.1999  ; Compiled January 17, 2005 16:44:24
	#include COMSYS
	#include WWWConst
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		MONATSABSCHLUSS
	;		MONTH-END CLOSING
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 07-Dec-2007	GRF		Doco; quits; parentheses missing around date check!!!
	; 09-Sep-2005	JW		SR12966: Disabled WWWUSER block.
	; 05-Sep-2005	RPW		SR13410: The second loop through WWWUSER should use the
	; 							first subscript, not the second.
	; 02-Sep-2005	JW		SR12966: WWWUSER is shared. 
	; 						WWWBENCH/WWWLAST/WWW1205B/WWW1205/WWWPRO are not shared.
	; 07-Jul-2005	shobby	SR12892: WWW1262 is no longer shared.
	; 07-Jul-2005	shobby	SR12892: WWW126 is no longer shared.
	; 05-Apr-2005	SCR		SR12040	Disabled Re-Index , Now part of COMClassCompile
	; 27.09.2004	BEC		26420
	; 09.11.1999	DT
	;-----------------------------------------------------------------------------*/
	IF +$GET(VORG(1))=0 DO ^WWWINFO($$^WWWTEXT(32017)) QUIT  ;NICHT STARTEN ;Not launching 
	DO ^WWWINFO($$^WWWTEXT(32018))  ;GESTARTET
	;D START W "OK" Q 
	;JOB START^WWWMON::1    ;JOB STARTEN
	JOB START1^WWWMON($GET(YM),$GET(SPRACHE))::1    ;BEC;26420;27.09.04
	QUIT
	
START    ;!EINSPRUNG VON AUSSEN (WWWIN)  
	;SET $ZTRAP="^WWWERROR"
	;DO LOW^%PRIO
	;DO ^WWWVAR 
	DO START1($GET(YM),$GET(SPRACHE))      ;BEC;26420;27.09.04
	QUIT
	
START1(YM,SPRACHE)    ;BEC;26420;27.09.04
	SET $ZTRAP="^WWWERROR"
	DO LOW^%PRIO
	DO    ;BEC;26420;27.09.04
	. NEW YM,SPRACHE
	. DO ^WWWVAR     ;VARIABLEN LADEN ;charge 
	
	; "Job Started : DD/MM/YYYY, HH:MM:SS"
	;---------------------------------------
	SET $PIECE(^WWW120(0,"WWWMON",1),Y,110)=$$^WWWTEXT(32028)_": "_$$^WWWDATE($HOROLOG)_", "_$$^WWWTIME($HOROLOG)  ;AUFBEREITUNG GESTARTET
	
	; Combine System and Application Parameters (Authorisation)
	; and save as Application only
	;---------------------------------------
	IF $DATA(^WWW100(0,"BERECHTIGUNG")) DO  ;UMKOPIEREN DER WWW100 BERECHTIGUNG ;the 
	. NEW YI,YII
	. ;
	. ;DANN ZURÜCK IN DIE NEUE DATEI ;back within who data file
	. ;--------------------------------------------------------
	. SET YI=""
	. FOR  SET YI=$ORDER(^WWW101(0,"BERECHTIGUNG",YI)) QUIT:YI=""  DO
	. . SET YII=""
	. . FOR  SET YII=$ORDER(^WWW101(0,"BERECHTIGUNG",YI,YII)) QUIT:YII=""  DO
	. . . SET ^WWW100(0,"BERECHTIGUNG",YI,YII,1)=$GET(^WWW101(0,"BERECHTIGUNG",YI,YII,1))
	. ;
	. NEW YI,YII
	. SET YI=""
	. FOR  SET YI=$ORDER(^WWW100(0,"BERECHTIGUNG",YI)) QUIT:YI=""  DO
	. . SET YII=""
	. . FOR  SET YII=$ORDER(^WWW100(0,"BERECHTIGUNG",YI,YII)) QUIT:YII=""  DO
	. . . SET ^WWW101(0,"BERECHTIGUNG",YI,YII,1)=$GET(^WWW100(0,"BERECHTIGUNG",YI,YII,1))
	. ;
	. KILL ^WWW100(0,"BERECHTIGUNG")  ;LÖSCHEN ALTE FILE ;Delete 
	
	
	IF '$DATA(^WWW101(0,"BERECHTIGUNG")) DO   ;UMKOPIEREN BACKUP
	. NEW YI,YII
	. SET YI=""
	. FOR  SET YI=$ORDER(^WWW100(0,"XBERECHTIGUNG",YI)) QUIT:YI=""  DO 
	. . SET YII=""
	. . FOR  SET YII=$ORDER(^WWW100(0,"XBERECHTIGUNG",YI,YII)) QUIT:YII=""  DO 
	. . . SET ^WWW101(0,"BERECHTIGUNG",YI,YII,1)=$GET(^WWW100(0,"XBERECHTIGUNG",YI,YII,1))
	. ;
	. KILL ^WWW100(0,"BERECHTIGUNG")  ;LÖSCHEN ALTE FILE ;Delete 
	
	DO MONATE          ;GENERIEREN MONATSVORGABEN 
	DO WOCHEN          ;GENERIEREN WOCHENVORGABEN
	DO BERECHTIGUNG    ;SETZEN BERECHTIGUNGEN    BEC;25.11.03 DA KEINE BERECHTIGUNG IN GMBH   ;typeset yonder no within 
	DO PROTOKOLL       ;RÜCKSETZEN PROTOKOLL ;protocol 
	
	;ZWISCHENDATEIEN LÖSCHEN ;Delete Buffer Files
	DO  ;AKTUELLE USER
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWUSER(0,YA)) QUIT:YA=""  DO
	. . IF ($PIECE($GET(^WWWUSER(0,YA,1)),Y,3)+1)<$HOROLOG KILL ^WWWUSER(0,YA) KILL ^WWWUSER1(0,YA)  ;FIS;06.05.04;NEU:WWWUSER1
	
	/* SR12966: Advised by FIS to disable code.
	DO  ;AKTUELLE USER
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWUSER(YA)) QUIT:YA=""  IF '$DATA(^WWW012(0,YA)) KILL ^WWWUSER(YA) KILL ^WWWUSER1(0,YA)  ;FIS;06.05.04;NEU:WWWUSER1 ; SR13410
	
	DO  ;AKTUELLE USER
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWUSER(0,YA)) QUIT:YA=""  DO
	. . IF ($PIECE($GET(^WWWUSER(0,YA,1)),Y,3)+1)<$HOROLOG KILL ^WWWUSER(0,YA) KILL ^WWWUSER1(0,YA)  ;FIS;06.05.04;NEU:WWWUSER1
	*/
	
	DO  ;AKTUELLE USERFILE
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWUSE(0,YA)) QUIT:YA=""  DO
	. . QUIT:$DATA(^WWWUSER(0,YA))
	. . KILL ^WWWUSE(0,YA)
	
	DO  ;TEMP FILE ;FIS;25.06.04;25998
	. KILL ^WWWUSETMP(0)
	
	DO  ;AKTUELLE USERFILE XML MESSAGES
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWXML(0,YA)) QUIT:YA=""  DO
	. . QUIT:$DATA(^WWWXML(0,YA))     ; FIXME : <GRF> Should this be ^WWWUSER or remove?
	. . KILL ^WWWXML(0,YA)
	
	DO  ;AKTUELLE SEITENFILE
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWPAGE(0,YA)) QUIT:YA=""  DO
	. . QUIT:$DATA(^WWWUSER(0,YA))
	. . KILL ^WWWPAGE(0,YA)
	
	DO  ;AKTUELLE ZWISCHENDATEI
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWZWS(0,YA)) QUIT:YA=""  DO
	. . QUIT:YA'<$HOROLOG
	. . KILL ^WWWZWS(0,YA)
	
	DO  ;AKTUELLE USER ZWISCHENDATEI
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWSOR(YA)) QUIT:YA=""  DO
	. . QUIT:$DATA(^WWWUSER(0,YA))
	. . QUIT:+YA=0
	. . KILL ^WWWSOR(YA)
	
	DO  ;AKTUELLE ZWISCHENDATEI
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWDATEN(YM,YA)) QUIT:YA=""  DO
	. . QUIT:YA'<$HOROLOG
	. . KILL ^WWWDATEN(YM,YA)
	
	DO  ;AKTUELLE ZWISCHENDATEI WELCHE DATEN WURDEN ANGEFRAGT ;who were 
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWW126(YM,YA)) QUIT:YA=""  DO
	. . SET YA(1)=""
	. . FOR  SET YA(1)=$ORDER(^WWW126(YM,YA,YA(1))) QUIT:YA(1)=""  DO
	. . . QUIT:$DATA(^WWW013(0,YA(1)))  ;MITARBEITERDATEN
	. . . QUIT:$DATA(^WWWUSER(0,YA(1)))
	. . . KILL ^WWW126(YM,YA,YA(1))
	. . . KILL ^WWW1262(YM,YA,YA(1))
	
	DO  ;AKTUELLE ZWISCHENDATEI LETZTEN 10 EINHEITEN
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWW1262(YM,YA)) QUIT:YA=""  DO
	. . SET YA(1)=""
	. . FOR  SET YA(1)=$ORDER(^WWW1262(YM,YA,YA(1))) QUIT:YA(1)=""  DO
	. . . IF '$DATA(^WWW126(YM,YA,YA(1))) KILL ^WWW1262(YM,YA,YA(1))
	. . . QUIT:$DATA(^WWW013(0,YA(1)))  ;MITARBEITERDATEN
	. . . KILL ^WWW1262(YM,YA,YA(1))
	. . . KILL ^WWW126(YM,YA,YA(1))
	
	KILL ^WWWPARAEXPL  ;FIS;23441;02.06.03;LÖSCHEN ZWISCHENDATEI SUCHANZEIGE
	
	DO  ;LÖSCHEN PROTOKOLL HINTERGRUNDJOBS ;Delete protocol 
	. NEW YA,DATE
	. SET YA=""
	. FOR  SET YA=$ORDER(^INBACKJOBS1(YM,YA)) QUIT:YA=""  DO
	. . SET DATE=""
	. . FOR  SET DATE=$ORDER(^INBACKJOBS1(YM,YA,DATE)) QUIT:DATE=""  DO
	. . . IF DATE<($HOROLOG-30) KILL ^INBACKJOBS1(YM,YA,DATE)             ; 07-Dec-2007 GRF
	
	DO  ;AKTUELLE ZWISCHENDATEI ÄLTER 1 MONAT ;month 
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWBENCH(YM,YA)) QUIT:YA=""  DO
	. . IF YA<($HOROLOG-31) KILL ^WWWBENCH(YM,YA)
	
	DO  ;FOOL DATEI 1 JAHR;TYBD;16,12,2003
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWFOOL(0,YA)) QUIT:YA=""  DO
	. . IF YA<($HOROLOG-365) KILL ^WWWFOOL(0,YA)
	
	DO  ;LOGIN DATEI 1 JAHR;TYBD;16,12,2003
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWLOGIN(0,YA)) QUIT:YA=""  DO
	. . IF YA<($HOROLOG-365) KILL ^WWWLOGIN(0,YA)
	
	DO  ;ZWISCHENDATEI WWWRELOAD LÖSCHEN ;Delete 
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWRELOAD(0,YA)) QUIT:YA=""  DO
	. . IF YA'=+$HOROLOG KILL ^WWWRELOAD(0,YA)
	
	DO  ;DATEI WWW013M ÄLTER 1 MONAT LÖSCHEN  ;data file month Delete 
	. SET YN=""
	. FOR  SET YN=$ORDER(^WWW013M(0,YN)) QUIT:YN=""  DO
	. . SET YNN=""
	. . FOR  SET YNN=$ORDER(^WWW013M(0,YN,YNN)) QUIT:YNN=""  DO
	. . . SET YA=+$PIECE($GET(^WWW013M(0,YN,YNN,1)),Y,2)
	. . . IF YA'=0 IF YA<($HOROLOG-31) DO ^WWWSKILL("WWW013M",YN_","_YNN)
	
	DO
	. NEW FORM,DATE,MIT
	. SET FORM=""
	. FOR  SET FORM=$ORDER(^WWW1205B(YM,FORM)) QUIT:FORM=""  DO  
	. . SET MIT=""
	. . FOR  SET MIT=$ORDER(^WWW1205B(YM,FORM,MIT)) QUIT:MIT=""  DO
	. . . IF '$DATA(^WWW013(0,MIT)) KILL ^WWW1205B(YM,FORM,MIT)   ;LÖSCHEN WENN MITARBEITER NICHT MEHR EXISTIERT  ;Delete when Not more 
	
	DO
	. NEW FORM,YA
	. SET FORM=""
	. FOR  SET FORM=$ORDER(^WWW1205(YM,FORM)) QUIT:FORM=""  DO  
	. . SET YA=""
	. . FOR  SET YA=$ORDER(^WWW1205(YM,FORM,YA)) QUIT:YA=""  DO
	. . . IF YA<($HOROLOG-365) KILL ^WWW1205(YM,FORM,YA)   ;LÖSCHEN ALTE DATEN ;Delete 
	
	DO  ;LÖSCHEN LETZTE ÄNDERUNGEN ;Delete last 
	. NEW TAGE,LOEDAT,KEY
	. SET TAGE=357
	. SET LOEDAT=$HOROLOG-TAGE
	. SET FORM=""
	. FOR  SET FORM=$ORDER(^WWWLAST(YM,FORM)) QUIT:FORM=""  DO  ;Letzte änderung
	. . SET KEY=""
	. . FOR  SET KEY=$ORDER(^WWWLAST(YM,FORM,KEY)) QUIT:KEY=""  DO  ;Letzte änderung
	. . . SET DAT=""
	. . . FOR  SET DAT=$ORDER(^WWWLAST(YM,FORM,KEY,DAT)) QUIT:DAT=""  QUIT:DAT>LOEDAT  DO
	. . . . KILL ^WWWLAST(YM,FORM,KEY,DAT)
	
	DO ^WWW009  ;STARTEN GRUPPENBEZEICHNUNG (::TEXT) ÜBESETZTEN ;launching 
	
	; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	;ÜBERSETZUNG BERICHTIGEN; ;version regulate 
	;IF '$DATA(^WWWDIC(0,"DE","EN")) DO
	. NEW TEXT,TEXT1,TEXTV
	. MERGE ^XWWWDIC=^WWWDIC
	. KILL ^WWWDIC
	. SET TEXT=""
	. FOR  SET TEXT=$ORDER(^XWWWDIC(0,TEXT)) QUIT:TEXT=""  DO
	. . SET TEXTV=TEXT
	. . FOR  QUIT:$EXTRACT($REVERSE(TEXTV))'=" "  SET TEXTV=$EXTRACT(TEXTV,1,$LENGTH(TEXTV)-1)
	. . QUIT:+TEXTV'=0
	. . SET TEXTV=$TRANSLATE(TEXTV,""""_"#!*")
	. . QUIT:TEXTV=""
	. . SET TEXT1=$GET(^XWWWDIC(0,TEXT,1))
	. . FOR  QUIT:$EXTRACT($REVERSE(TEXT1))'=" "  SET TEXT1=$EXTRACT(TEXT1,1,$LENGTH(TEXT1)-1)
	. . SET TEXT1=$TRANSLATE(TEXT1,""""_"#!*")
	. . QUIT:TEXT1=""
	. . SET ^WWWDIC(0,"DE","EN",TEXTV,1)=TEXT1
	. . SET ^WWWDIC(0,"EN","DE",TEXT1,1)=TEXTV
	. ;
	. KILL ^XWWWDIC
	; ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	
	/*
	;AUTOMATISCHE SORTKEY GENERIERTUNG
	; 
	; 05-Apr-2005		SCR			SR12040	Disabled Re-Index , Now part of COMClassCompile
	;	
	;SET SORT=""
	;FOR  SET SORT=$ORDER(^WWWSORTKEY(0,SORT)) QUIT:SORT=""  DO
	;. NEW NEWINDEX
	;. QUIT:$PIECE($GET(^WWWSORTKEY(0,SORT,1)),Y,1)'=""  ;SCHON GELAUFEN ;yet 
	;. SET NEWINDEX=$PIECE($GET(^WWWSORTKEY(0,SORT,1)),Y,3)  ;NUR BESTIMMTE INDEX ;only index 
	;. SET VORG(1)=SORT
	;. DO
	;. . NEW SORT
	;. . SET YQ=1
	;. . SET VORG1=VORG(1)
	;. . DO NEW^WWWSORT(VORG1,NEWINDEX)
	;. ;
	;. IF $$^WWWUPER($PIECE($GET(^WWW012(0,YM,1)),Y,94))="DEVELOPMENT" QUIT  ;NICHT BEI ENTWICKLUNG ;Not next to development 
	;. SET $PIECE(^WWWSORTKEY(0,SORT,1),Y,1)=$HOROLOG
	*/
	
	; "Last Preparation : DD/MM/YYYY, HH:MM:SS"
	;---------------------------------------
	SET $PIECE(^WWW120(0,"WWWMON",1),Y,110)=$$^WWWTEXT(32471)_": "_$$^WWWDATE($HOROLOG)_", "_$$^WWWTIME($HOROLOG)  ;LETZE AUFBEREITUNG
	QUIT
	
PROTOKOLL ;LÖSCHEN PROTOKOLLDATEI  ;WIRD AUCH IN INTAGES GEPRÜFT ;Delete too within 
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 05-Feb-2008	shobby	SRBR014891: Separated processing from parameter collection
	; 							to aid in moving the 'date' calculation from
	; 							INVORG to WWW012
	;-------------------------------------------------------------------------------
	do RemoveProtocol($$ModificationLogRetention^WWW012(YM))
	;NEW FORM,DAT,LOEDAT
	
	;SET TAGE=+$PIECE($GET(^INVORG(YM,YM,1)),Y,14)
	;IF TAGE'=0 QUIT  ;ABERBEITUNG ERFOLGT DURCH INTAGES ;trans- 
	;SET TAGE=357
	;SET LOEDAT=$HOROLOG-TAGE
	;SET FORM=""
	;FOR  SET FORM=$ORDER(^WWWPRO(YM,FORM)) QUIT:FORM=""  DO  ;ÄNDERUNGSPROTOKOLL
	;. SET DAT=""
	;. FOR  SET DAT=$ORDER(^WWWPRO(YM,FORM,DAT)) QUIT:DAT=""  QUIT:DAT>LOEDAT  DO
	;. . KILL ^WWWPRO(YM,FORM,DAT)
	
	QUIT
 
RemoveProtocol(pintDays="",pstrPrefix="")
	;-------------------------------------------------------------------------------
	; Removes logging profiles (WWWPRO : Change Log) for the specified class and
	; number of days in the past.
	;
	; Inputs:
	; 		pintDays:   Number of days back to start deleting
	; 		pstrPrefix: Prefix of the class to be removed
	; 					Can specify a module prefix "WWW", or an individual class
	; 					"WWW124".  If left blank processes for all classes
	;
	; Returns:
	;
	; History:
	; 23-Mar-2009	shobby	SRBR014891: Renamed idForm to idClass
	; 06-Jun-2008	GRF		SRBR014891: calculate length of prefix and loop end
	; 							date once to improve performance
	; 05-Feb-2008	shobby	SRBR014891: Created
	;-------------------------------------------------------------------------------
	new dteKeep,idClass,intDate,intLength
	
	set intLength = $length(pstrPrefix) ; SRBR014891
	set dteKeep   = $horolog-pintDays   ; SRBR014891
	
	if pintDays'="" {
		set idClass=""
		for {
			set idClass=$order(^WWWPRO(YM,idClass))
			quit:idClass=""
			
			if (pstrPrefix="") || ($extract(idClass,1,intLength)=pstrPrefix) { ; SRBR014891
				set intDate=""
				for {
					set intDate=$order(^WWWPRO(YM,idClass,intDate))
					quit:intDate=""
					quit:intDate>dteKeep                   ; SRBR014891
					
					kill ^WWWPRO(YM,idClass,intDate)
				}
			}
		}
	}
	quit
 
	
MONATE ;MONATSPARAMETER
	NEW SPRACH
	
	FOR SPRACH="DE","EN","FR","PT" FOR WW=$$^WWWYEAR():1:($$^WWWYEAR()+10) DO
	. KILL ^WWW101(0,"JAHR",SPRACH,WW)
	
	FOR SPRACH="DE","EN","FR","PT" FOR WW=$$^WWWYEAR():1:($$^WWWYEAR()+10) DO
	. SET ^WWW101(0,"JAHR",SPRACH,WW,1)=WW
	
	SET VON=$$^WWWYEAR($HOROLOG)-1  ;VORJAHR
	SET BIS=VON+2
	FOR SPRACH="DE","EN","FR","PT" FOR WW=VON:1:BIS DO
	. FOR MONAT=1:1:12 DO
	. . SET TEXT=$PIECE($$^WWWTEXT(30023,,1,SPRACH),",",MONAT)  ;JANUAR,FEBRUAR,...
	. . SET ^WWW101(0,"MONATE",SPRACH,WW_"-"_$EXTRACT(MONAT+100,2,3),1)=TEXT_" "_WW
	. ;
	. FOR QUARTER=1:1:4 DO  ;FIS;QUARTAL;11.01.05;26433
	. . SET TEXT=$PIECE($$^WWWTEXT(34107,,1,SPRACH),",",QUARTER)  ;1.QUARTAL, 2.QUARTAL,...
	. . SET ^WWW101(0,"QUARTER",SPRACH,WW_"-"_QUARTER,1)=TEXT_" "_WW
	
	;ALTE WERTE LÖSCHEN ;Delete 
	FOR SPRACH="DE","EN","FR","PT" FOR WW=VON:1:BIS DO
	. FOR MONAT=13:1:100 DO
	. . KILL ^WWW101(0,"MONATE",SPRACH,WW_"-"_$EXTRACT(MONAT+100,2,3),1)
	. ;
	. FOR QUARTER=5:1:50 DO  ;FIS;QUARTAL;11.01.05;26433
	. . KILL ^WWW101(0,"QUARTER",SPRACH,WW_"-"_QUARTER,1)
	
	;VON = VORJAHR
	FOR SPRACH="DE","EN","FR","PT" FOR WW=(VON-1):-1:(VON-10) DO
	. FOR MONAT=1:1:100 DO
	. . KILL ^WWW101(0,"MONATE",SPRACH,WW_"-"_$EXTRACT(MONAT+100,2,3),1)
	. ;
	. FOR QUARTER=1:1:50 DO  ;FIS;QUARTAL;11.01.05;26433
	. . KILL ^WWW101(0,"QUARTER",SPRACH,WW_"-"_QUARTER,1)
	
	;LÖSCHEN FALSCHE WERTE ;Delete 
	FOR SPRACH="DE","EN","FR","PT" SET VON="" FOR  SET VON=$ORDER(^WWW101(0,"MONATE",SPRACH,VON)) QUIT:VON=""  DO
	. IF $LENGTH(+VON)'=4 DO
	. . KILL ^WWW101(0,"MONATE",SPRACH,VON)
	
	FOR SPRACH="DE","EN","FR","PT" SET VON="" FOR  SET VON=$ORDER(^WWW101(0,"QUARTER",SPRACH,VON)) QUIT:VON=""  DO  ;FIS;QUARTAL;11.01.05;26433
	. IF $LENGTH(+VON)'=4                            KILL ^WWW101(0,"QUARTER",SPRACH,VON)
	. IF ($PIECE(VON,"-",2)<1)!($PIECE(VON,"-",2)>4) KILL ^WWW101(0,"QUARTER",SPRACH,VON)
	
	QUIT
	
WOCHEN ;WOCHENPARAMETER
	NEW SPRACH
	
	SET VON=+$HOROLOG-40  ;VORJAHR
	SET BIS=VON+365
	FOR SPRACH="DE","EN" FOR WW=VON:1:BIS DO
	. QUIT:$$^WWWDAY(WW)'=1
	. SET WOCHE=$$^WWWWEEK(WW)
	. SET ^WWW101(0,"WOCHEN",SPRACH,$EXTRACT(WOCHE,3,6)_"-"_$EXTRACT(WOCHE,1,2),1)=$EXTRACT(WOCHE,1,2)_". "_$$^WWWTEXT(31001,,,SPRACH)_" "_$EXTRACT(WOCHE,3,6)_" "_$$^WWWTEXT(31002,,,SPRACH)_" "_$$^WWWDATE(WW,SPRACH)_" "_$$^WWWTEXT(31003,,,SPRACH)_" "_$$^WWWDATE(WW+4,SPRACH)
	
	;ALTE WERTE LÖSCHEN ;Delete 
	SET WEEK=$$^WWWWEEK($HOROLOG-60)
	FOR SPRACH="DE","EN","FR","PT" SET WEEK1="" FOR  SET WEEK1=$ORDER(^WWW101(0,"WOCHEN",SPRACH,WEEK1)) QUIT:WEEK1=""  QUIT:WEEK1]($EXTRACT(WEEK,3,6)_"-"_$EXTRACT(WEEK,1,2))  DO
	. KILL ^WWW101(0,"WOCHEN",SPRACH,WEEK1)
	
	QUIT
	
BERECHTIGUNG ;SEZTEN BERECHTIGUNGEN IN MENU UND FORMULARE  !!EINSPRUNG VON AUSSEN ;within And 
    ;-------------------------------------------------------------------------------
    ; Does processing to add program entitlements.
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 14-Jun-2011	Shobby	SR17792: Use Button index for improved performance
    ; 10-Apr-2007	HeberB	BR014350: prevent GenerateCustomButtons being called more than once
    ; 04-Aug-2006	FIS		SR14630: Added CacheTempButtonAccess to limit visibilty of buttons.
    ; 20-Jan-2006	SteveS	SR13171: Commented -- add custom buttons
    ;-------------------------------------------------------------------------------
	;1. MEUE MENUESTRUKTUR PRÜFEN         ;check menu
	;2. MENUE BERICHTIGEN                 ;regulate 
	;3. FORMULARE DEN MENUES ANPASSEN     ;take one´s measure for 
	;4. BUTTONS DEN FORMULAREND ANPASSEN  ;take one´s measure for 
	QUIT:$ORDER(^WWW0044(0,""))=""    ;KEINE DATEN WEGEN MENUES FITCOM FALSCH IN ENTWICKLUNG;TYBD;29,5,2004;SIEHE INSERV174
	
	DO LOESCH  ;LÖSCHEN BERECHTIGUNGEN ;Delete
	
	IF $DATA(^WWWMENUCHANGE(0)) DO  ; MENUEUMSTRUKTURIERUNG;26370;TYBD;8,9,2004
	. NEW MENU,MENU1,MENUNEW,MENUNEW1,VERSION,APL
	. ;
	. SET APL=""
	. FOR  SET APL=$ORDER(^WWWMENUCHANGE(0,APL)) QUIT:APL=""  DO
	. . SET VERSION=$PIECE($GET(^WWWVERSION1(0,APL,1)),Y,2)-1
	. . IF VERSION>10000 FOR  SET VERSION=$ORDER(^WWWMENUCHANGE(0,APL,VERSION)) QUIT:VERSION=""  DO
	. . . SET MENU=""
	. . . FOR  SET MENU=$ORDER(^WWWMENUCHANGE(0,APL,VERSION,MENU)) QUIT:MENU=""  DO
	. . . . QUIT:$PIECE($GET(^WWWMENUCHANGE(0,APL,VERSION,MENU,1)),Y,2)'=""     ;TRANSFER DONE
	. . . . SET $PIECE(^WWWMENUCHANGE(0,APL,VERSION,MENU,1),Y,2)=+$HOROLOG      ;UMGESETZT  
	. . . . SET MENUNEW=$PIECE($GET(^WWWMENUCHANGE(0,APL,VERSION,MENU,1)),Y,1)  ;NEW
	. . . . SET MENU1=MENU SET MENUNEW1=MENUNEW
	. . . . DO  FOR  SET MENU1=$ORDER(^WWW00441(0,APL,MENU1)) QUIT:MENU1=""  QUIT:$EXTRACT(MENU1,1,$LENGTH(MENU))'=MENU  DO
	. . . . . SET MENUNEW1=MENUNEW_$PIECE(MENU1,MENU,2,99)
	. . . . . MERGE ^WWW00441(0,APL,MENUNEW1)=^WWW00441(0,APL,MENU1)
	. . . . . KILL ^WWW00441(0,APL,MENU1)
	. . . . . MERGE ^WWW00442(0,APL,MENUNEW1)=^WWW00442(0,APL,MENU1)
	. . . . . KILL ^WWW00442(0,APL,MENU1)
	. . . . . MERGE ^WWW00443(0,APL,MENUNEW1)=^WWW00443(0,APL,MENU1)
	. . . . . KILL ^WWW00443(0,APL,MENU1)
	. . . . . MERGE ^WWW00444(0,APL,MENUNEW1)=^WWW00444(0,APL,MENU1)
	. . . . . KILL ^WWW00444(0,APL,MENU1)
	. . . . . MERGE ^WWW00445(0,APL,MENUNEW1)=^WWW00445(0,APL,MENU1)
	. . . . . KILL ^WWW00445(0,APL,MENU1)
	. ;
	. DO NEW^WWWSORT("WWW00441")
	. DO NEW^WWWSORT("WWW00442")
	. DO NEW^WWWSORT("WWW00443")
	. DO NEW^WWWSORT("WWW00444")
	. DO NEW^WWWSORT("WWW00445")
	
	NEW PGM,MENU,MENUP,FORM,FORMB,BER,MOD,BUTT,BUTT1,LANG,MOD1,MENU1,EINB,YQ,MOD1,SEITT,SEITT1
	
	;+++++++++++++++++++++++++++++++++++++++++
	;
	; WWW004: Menu Class
	; 
	; D2	: Form Name
	; D3	: User Access
	; D4	: Module
	;  
	;+++++++++++++++++++++++++++++++++++++++++
	
	SET EINB=$PIECE($GET(^WWW012(0,YM,1)),Y,93) ;EINGESCHRÄNKTE MODULBERECHTIGUNG
	IF $TRANSLATE(EINB," ;,")="" SET EINB=""
	SET PGM=""
	FOR  SET PGM=$ORDER(^WWW0044(0,PGM)) QUIT:PGM=""  DO
	. SET YQ=0
	. QUIT:PGM="@net_Manager"
	. SET PGM(1)=$GET(^WWW0044(0,PGM,1))
	. IF '$DATA(^WWW004(0,PGM)) KILL ^WWW0044(0,PGM),^WWW00441(0,PGM) QUIT   ;KEIN EINTRAG VORHANDEN ;no on hand 
	. SET BER=$PIECE(PGM(1),Y,3)
	. SET MOD=$PIECE(PGM(1),Y,4)
	. SET MENU=""
	. FOR  SET MENU=$ORDER(^WWW004(0,PGM,MENU)) QUIT:MENU=""  DO
	. . ;BERECHTIGUNG MANDANT ;Company 
	. . SET YQ=0
	. . IF EINB'="" DO 
	. . . SET MOD1=$PIECE(^WWW004(0,PGM,MENU,1),Y,4)  ;BERECHTIGUNGS MODULE
	. . . QUIT:MOD1=""
	. . . SET YQ=1
	. . . FOR YQ(1)=1:1 SET YQ(2)=$PIECE($TRANSLATE(MOD1,",",";"),";",YQ(1)) QUIT:YQ(2)=""  DO  QUIT:YQ=0
	. . . . IF $FIND(";"_EINB_";",";"_YQ(2)_";") SET YQ=0  ;BERECHTIGT
	. . ;
	. . QUIT:YQ=1
	. . SET $PIECE(^WWW004(0,PGM,MENU,1),Y,3)=BER  ;BERECHTIGUNG
	. . SET $PIECE(^WWW004(0,PGM,MENU,1),Y,4)=MOD  ;BERECHTIGUNGS MODULE
	. ;
	. SET MENUP=""
	. FOR  SET MENUP=$ORDER(^WWW00441(0,PGM,MENUP)) QUIT:MENUP=""  DO   ;SUCHEN UNTERMENUEPUNKTE ;seek 
	. . SET MENUP(1)=$GET(^WWW00441(0,PGM,MENUP,1))
	. . QUIT:'$DATA(^WWW004(0,PGM,MENUP,1))  ;MENU NICHT VORHANDEN ;Not on hand 
	. . SET MENU1(1)=^WWW004(0,PGM,MENUP,1)  ;MENU
	. . SET BER=$PIECE(MENUP(1),Y,3)
	. . SET MOD=$PIECE(MENUP(1),Y,4)
	. . ;BERECHTIGUNG MANDANT ;Company 
	. . SET YQ=0
	. . IF EINB'="" DO 
	. . . SET MOD1=$PIECE(^WWW004(0,PGM,MENUP,1),Y,4)  ;BERECHTIGUNGS MODULE
	. . . QUIT:MOD1=""
	. . . SET YQ=1
	. . . FOR YQ(1)=1:1 SET YQ(2)=$PIECE($TRANSLATE(MOD1,",",";"),";",YQ(1)) QUIT:YQ(2)=""  DO  QUIT:YQ=0
	. . . . IF $FIND(";"_EINB_";",";"_YQ(2)_";") SET YQ=0  ;BERECHTIGT
	. . ;
	. . QUIT:YQ=1
	. . ;
	. . IF $PIECE(MENU1(1),Y,2)'="" DO  QUIT          ;GENAUES PROGRAMM ;programme 
	. . . SET $PIECE(^WWW004(0,PGM,MENUP,1),Y,3)=BER  ;BERECHTIGUNG
	. . . SET $PIECE(^WWW004(0,PGM,MENUP,1),Y,4)=MOD  ;BERECHTIGUNGS MODULE
	. . ;
	. . IF $PIECE(MENU1(1),Y,12)'="" DO  QUIT         ;VERKNÜPFUNG
	. . . SET $PIECE(^WWW004(0,PGM,MENUP,1),Y,3)=BER  ;BERECHTIGUNG
	. . . SET $PIECE(^WWW004(0,PGM,MENUP,1),Y,4)=MOD  ;BERECHTIGUNGS MODULE
	. . ;
	. . SET MENU1=MENUP FOR  SET MENU1=$ORDER(^WWW004(0,PGM,MENU1)) QUIT:MENU1=""  QUIT:$EXTRACT(MENU1,1,$LENGTH(MENUP))'=MENUP  DO
	. . . QUIT:$EXTRACT(MENU1,1,$LENGTH(MENUP))'=MENUP   ;NICHT IN DER GRUPPE ;Not within the group 
	. . . SET $PIECE(^WWW004(0,PGM,MENU1,1),Y,3)=BER  ;BERECHTIGUNG
	. . . SET $PIECE(^WWW004(0,PGM,MENU1,1),Y,4)=MOD  ;BERECHTIGUNGS MODULE
 
	;PRUEFEN Formulare
	QUIT:$ORDER(^WWW0044(0,""))=""    ;KEINE DATEN WEGEN MENUES FITCOM FALSCH IN ENTWICKLUNG;TYBD;29,5,2004;SIEHE INSERV174
	
	KILL ^CacheTempButtonAccess(YUSER)  ;FIS;SR14630;04.08.2006
	
	/*****************************
	*
	* FIXME: This routine seems particularly ineffecient. Goes through every form for every form. O(N^N)
	* 
	*****************************/
	; BR014350
	NEW TEMP
	
	SET PGM = ""
	FOR  SET PGM = $ORDER(^WWW004(0,PGM)) QUIT:PGM=""  DO
	. QUIT:PGM="@net_Manager"
	. ;
	. SET MENU = ""
	. FOR  SET MENU = $ORDER(^WWW004(0,PGM,MENU)) QUIT:MENU=""  DO
	. . SET MENU(1) = $GET(^WWW004(0,PGM,MENU,1))
	. . SET FORM    = $PIECE(MENU(1),Y,2)
	. . SET BER     = $PIECE(MENU(1),Y,3)
	. . SET MOD     = $PIECE(MENU(1),Y,4)
	. . SET MOD1    = $PIECE($GET(^WWW00441(0,PGM,MENU,1)),Y,104)  ; $$$WWW00441FromItReadOnly()
	. . ;
	. . IF FORM'="" DO   ;GENAUES PROGRAMM ;programme 
	. . . QUIT:'$DATA(^WWW120(0,FORM,1))
	. . . ; BR014350
	. . . ; do GenerateCustomButtons^WWW124Custom(FORM) ;SR13171
	. . . IF '$DATA(TEMP(FORM)) DO
	. . . . do GenerateCustomButtons^WWW124Custom(FORM) ;SR13171
	. . . . SET TEMP(FORM)=1
	. . . ; 	
	. . . NEW YZWB
	. . . ;
	. . . SET YZWB=$PIECE(^WWW120(0,FORM,1),Y,22)          ; D22	$$$WWW120UserAccess()
	. . . FOR YZWB(1)=1:1 QUIT:$PIECE(BER,";",YZWB(1))=""  IF '$FIND(";"_YZWB_";",";"_$PIECE(BER,";",YZWB(1))_";") SET YZWB=YZWB_";"_$PIECE(BER,";",YZWB(1))
	. . . IF $EXTRACT(YZWB)=";" SET YZWB=$EXTRACT(YZWB,2,99)
	. . . SET $PIECE(^WWW120(0,FORM,1),Y,22)=YZWB  ;BERECHTIGUNG
	. . . ;
	. . . SET YZWB=$PIECE(^WWW120(0,FORM,1),Y,24)          ; D24	$$$WWW120Modules()
	. . . FOR YZWB(1)=1:1 QUIT:$PIECE(MOD,";",YZWB(1))=""  IF '$FIND(";"_YZWB_";",";"_$PIECE(MOD,";",YZWB(1))_";") SET YZWB=YZWB_";"_$PIECE(MOD,";",YZWB(1))
	. . . IF $EXTRACT(YZWB)=";" SET YZWB=$EXTRACT(YZWB,2,99)
	. . . SET $PIECE(^WWW120(0,FORM,1),Y,24)=YZWB  ;BERECHTIGUNG MODULE
	. . . ;
	. . . SET YZWB=$PIECE(^WWW120(0,FORM,1),Y,104)         ; D104	$$$WWW120ReadOnlyAccessForModules()
	. . . FOR YZWB(1)=1:1 QUIT:$PIECE(MOD1,";",YZWB(1))=""  IF '$FIND(";"_YZWB_";",";"_$PIECE(MOD1,";",YZWB(1))_";") SET YZWB=YZWB_";"_$PIECE(MOD1,";",YZWB(1))
	. . . IF $EXTRACT(YZWB)=";" SET YZWB=$EXTRACT(YZWB,2,99)
	. . . SET $PIECE(^WWW120(0,FORM,1),Y,104)=YZWB  ;BERECHTIGUNG MODULE NUR LESEN ;only read 
	. . . ;
	. . . ;SR17792 vvvvvvvvvvvvvvvvv
	. . . new idxForm
	. . . set idxForm = $$$Index(FORM)
	. . . set FORMB = ""
	. . . for  set FORMB = $order(^WWW124s(0,3,idxForm,FORMB)) quit:FORMB=""  do
	. . . . set LANG=""
	. . . . for  set LANG = $order(^WWW124s(0,3,idxForm,FORMB,LANG)) quit:LANG=""  do
	. . . . . set BUTT=""
	. . . . . for  set BUTT = $order(^WWW124s(0,3,idxForm,FORMB,LANG,BUTT)) quit:BUTT=""  do
	. . . . . . set $piece(^WWW124(0,FORMB,LANG,BUTT,1),Y,22) = BER  ;BERECHTIGUNG           ; D22	$$$WWW124Authorization1()
	. . . . . . set $piece(^WWW124(0,FORMB,LANG,BUTT,1),Y,23) = MOD  ;BERECHTIGUNGS MODULE   ; D23	$$$WWW124Module1()
	. . . ;SR17792 ; BUTTON - other forms calling this form     ; FIXME : see above - perhaps add index to D7 so find matching forms directly <GRF>
	. . . ;SR17792 SET FORMB=""
	. . . ;SR17792 FOR  SET FORMB=$ORDER(^WWW124(0,FORMB)) QUIT:FORMB=""  DO
	. . . ;SR17792 . SET LANG=""
	. . . ;SR17792 . FOR  SET LANG=$ORDER(^WWW124(0,FORMB,LANG)) QUIT:LANG=""  DO
	. . . ;SR17792 . . SET BUTT=""
	. . . ;SR17792 . . FOR  SET BUTT=$ORDER(^WWW124(0,FORMB,LANG,BUTT)) QUIT:BUTT=""  DO
	. . . ;SR17792 . . . QUIT:$PIECE($GET(^WWW124(0,FORMB,LANG,BUTT,1)),Y,7)'=FORM  ;NICHT DIE GLEICHE FORM ;Not who same shape 
	. . . ;SR17792 . . . SET $PIECE(^WWW124(0,FORMB,LANG,BUTT,1),Y,22)=BER  ;BERECHTIGUNG           ; D22	$$$WWW124Authorization1()
	. . . ;SR17792 . . . SET $PIECE(^WWW124(0,FORMB,LANG,BUTT,1),Y,23)=MOD  ;BERECHTIGUNGS MODULE   ; D23	$$$WWW124Module1()
	. . . ;SR17792 ^^^^^^^^^^^^^^^^^^
	. . . ;
	. . . ; BUTTONS on this form - menu entitlements applied to buttons
	. . . SET LANG=""
	. . . FOR  SET LANG=$ORDER(^WWW124(0,FORM,LANG)) QUIT:LANG=""  DO
	. . . . SET BUTT=""
	. . . . FOR  SET BUTT=$ORDER(^WWW124(0,FORM,LANG,BUTT)) QUIT:BUTT=""  DO
	. . . . . SET BUTT1=$GET(^WWW00442(0,PGM,MENU,BUTT,1))
	. . . . . QUIT:BUTT1="" 
	. . . . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,22)=$PIECE(BUTT1,Y,22)  ;BERECHTIGUNG           ; D22	$$$WWW124Authorization1()
	. . . . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,23)=$PIECE(BUTT1,Y,23)  ;BERECHTIGUNGS MODULE   ; D23	$$$WWW124Module1()
	. . . . . SET ^CacheTempButtonAccess(YUSER,FORM,LANG,BUTT)=""  ;FIS;SR14630;04.08.2006
	. . . ;
	. . . ; TABS - SEITENREITERBERECHTIGUNG;TYBD;16.07.2003;23940;
	. . . SET LANG=""
	. . . FOR  SET LANG=$ORDER(^WWW1203(0,FORM,LANG)) QUIT:LANG=""  DO
	. . . . SET SEITT=""
	. . . . FOR  SET SEITT=$ORDER(^WWW1203(0,FORM,LANG,SEITT)) QUIT:SEITT=""  DO
	. . . . . SET SEITT1=$GET(^WWW00444(0,PGM,MENU,SEITT,1))
	. . . . . QUIT:SEITT1="" 
	. . . . . SET $PIECE(^WWW1203(0,FORM,LANG,SEITT,1),Y,6)=$PIECE(SEITT1,Y,22)  ;BERECHTIGUNG
	. . . . . SET $PIECE(^WWW1203(0,FORM,LANG,SEITT,1),Y,5)=$PIECE(SEITT1,Y,23)  ;BERECHTIGUNGS MODULE
	
	IF $DATA(^WWW120BUTTOND1(0)) DO  ;FIS;SR14630;04.08.2006;SET ACCESS OUT OF CUSTOMIZING FILE
	. SET FORM=""
	. FOR  SET FORM=$ORDER(^WWW120BUTTOND1(0,FORM)) QUIT:FORM=""  DO
	. . SET BUTT=""
	. . FOR  SET BUTT=$ORDER(^WWW120BUTTOND1(0,FORM,BUTT)) QUIT:BUTT=""  DO
	. . . SET BUTT1=$GET(^WWW120BUTTOND1(0,FORM,BUTT,1))
	. . . SET LANG=""
	. . . FOR  SET LANG=$ORDER(^WWW124(0,FORM,LANG)) QUIT:LANG=""  DO
	. . . . QUIT:$DATA(^CacheTempButtonAccess(YUSER,FORM,LANG,BUTT))    ; already updated // SR14630
	. . . . IF $DATA(^WWW124(0,FORM,LANG,BUTT,1)) DO
	. . . . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,22)=$PIECE(BUTT1,Y,1)  ;BERECHTIGUNG
	. . . . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,23)=$PIECE(BUTT1,Y,2)  ;BERECHTIGUNGS MODULE
	. . . . IF $DATA(^WWW124D(0,FORM,LANG,BUTT,1)) DO
	. . . . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,22)=$PIECE(BUTT1,Y,1)  ;BERECHTIGUNG
	. . . . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,23)=$PIECE(BUTT1,Y,2)  ;BERECHTIGUNGS MODULE
	
	KILL ^CacheTempButtonAccess(YUSER)  ;FIS;SR14630;04.08.2006
	QUIT
	
BERECHT ;EINSPRUNG AUSBUTTON
    /*-------------------------------------------------------------------------------
    ; Process to enable program entitlements
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Jan-2006	Steve S		SR13171: Commented -- use appropriate message
    ;-------------------------------------------------------------------------------*/
	;DO ^WWWVAR
	DO    ;BEC;26420;27.09.04
	. NEW YM,SPRACHE
	. DO ^WWWVAR     ;VARIABLEN LADEN ;charge 
	 
	IF $GET(YM)="" SET YM=0   ;BEC;26420;27.09.04
	IF $GET(SPRACHE)="" SET SPRACHE="EN"   ;BEC;26420;27.09.04
	;SR13171: Give an appropriate message
	DO ^WWWINFO($$^WWWTEXT(383)) ;Program Has Started In Background
	;DO ^WWWINFO($$^WWWTEXT(30013))
	JOB BERECHT1^WWWMON($GET(YM),$GET(SPRACHE))::1
	QUIT
	
BERECHT1(YM,SPRACHE)
	DO    ;TYBD;8,10,2004
	. NEW YM,SPRACHE
	. DO ^WWWVAR     ;VARIABLEN LADEN ;charge 
	
	DO BERECHTIGUNG
	QUIT    
	
SAVE ;SPEICHERN DER DATEN ;Save the 
	NEW PGM,MENU,MENUP,FORM,FORMB,BER,MOD,BUTT,LANG
	
	DO ^WWWVAR
	KILL ^WWW0044(0)
	KILL ^WWW00441(0)
	KILL ^WWW00442(0)
	SET PGM=""
	FOR  SET PGM=$ORDER(^WWW004(0,PGM)) QUIT:PGM=""  DO
	. QUIT:PGM="@net_Manager"
	. SET MENU=$ORDER(^WWW004(0,PGM,""))
	. SET PGM(1)=""
	. IF MENU'="" SET PGM(1)=$GET(^WWW004(0,PGM,MENU,1))
	. QUIT:PGM(1)=""
	. SET BER=$PIECE(PGM(1),Y,3)
	. SET MOD=$PIECE(PGM(1),Y,4)
	. SET ^WWW0044(0,PGM,1)=Y_Y_BER_Y_MOD
	. SET MENU=""
	. FOR  SET MENU=$ORDER(^WWW004(0,PGM,MENU)) QUIT:MENU=""  DO
	. . SET BER=$PIECE(^WWW004(0,PGM,MENU,1),Y,3)  ;BERECHTIGUNG
	. . SET MOD=$PIECE(^WWW004(0,PGM,MENU,1),Y,4)  ;BERECHTIGUNGS MODULE
	. . SET ^WWW00441(0,PGM,MENU,1)=Y_Y_BER_Y_MOD
	. . IF $PIECE(^WWW004(0,PGM,MENU,1),Y,2)'="" DO
	. . . SET MOD1=$PIECE($GET(^WWW120(0,$PIECE(^WWW004(0,PGM,MENU,1),Y,2),1)),Y,104)
	. . . IF MOD1'="" SET $PIECE(^WWW00441(0,PGM,MENU,1),Y,104)=MOD1
	. . SET FORM=$PIECE(^WWW004(0,PGM,MENU,1),Y,2)
	. . IF FORM'="" DO
	. . . QUIT:$EXTRACT(FORM,1,3)="WWW"
	. . . SET LANG="DE"
	. . . DO  ;FOR  SET LANG=$ORDER(^WWW124(0,FORM,LANG)) QUIT:LANG=""  DO
	. . . . SET BUTT=""
	. . . .  FOR  SET BUTT=$ORDER(^WWW124(0,FORM,LANG,BUTT)) QUIT:BUTT=""  DO
	. . . . . SET BUTT1=$GET(^WWW124(0,FORM,LANG,BUTT,1))
	. . . . . SET $PIECE(^WWW00442(0,PGM,MENU,BUTT,1),Y,22)=$PIECE(BUTT1,Y,22)  ;BERECHTIGUNG
	. . . . . SET $PIECE(^WWW00442(0,PGM,MENU,BUTT,1),Y,23)=$PIECE(BUTT1,Y,23)  ;BERECHTIGUNGS MODULE
	
	QUIT
	
LOESCH ;LÖSCHT DIE VORGABEN DER BERECHTIGUNG ;who the 
    /*-------------------------------------------------------------------------------
    ; Blows away any existing program entitlements.
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 01-Oct-2007	GM		SRBR014630: Quit for "VAR" routines
    ; 20-Jan-2006	Steve S	SR13171: Commented -- clear custom buttons
    ;-------------------------------------------------------------------------------*/
	NEW PGM,MENU,MENUP,FORM,FORMB,BER,MOD,BUTT,LANG
	
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++
	;
	;	WWW120	:		Form Class
	;	
	;	D22		:		User Access
	;	D24		:		Modules
	;	D104	:		Read Only Access for Modules
	;
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++
	if $get(Y)="" DO ^WWWVAR
	SET PGM=""
	FOR  SET PGM=$ORDER(^WWW004(0,PGM)) QUIT:PGM=""  DO
	. QUIT:PGM="@net_Manager"
	. SET MENU=""
	. FOR  SET MENU=$ORDER(^WWW004(0,PGM,MENU)) QUIT:MENU=""  DO
	. . SET $PIECE(^WWW004(0,PGM,MENU,1),Y,3)=""  ;BERECHTIGUNG
	. . SET $PIECE(^WWW004(0,PGM,MENU,1),Y,4)=""  ;BERECHTIGUNGS MODULE
	
	do ClearCustomButtons^WWW124Custom() ;SR13171
	
	SET FORM=""
	FOR  SET FORM=$ORDER(^WWW120(0,FORM)) QUIT:FORM=""  DO
	. QUIT:($EXTRACT(FORM,1,3)="VAR")  ;BR014630
	. QUIT:($EXTRACT(FORM,1,3)="WWW")
	. QUIT:('$DATA(^WWW120(0,FORM,1)))
	. SET $PIECE(^WWW120(0,FORM,1),Y,22)=""       ;BERECHTIGUNG
	. SET $PIECE(^WWW120(0,FORM,1),Y,24)=""       ;BERECHTIGUNGS MODULE
	. SET $PIECE(^WWW120(0,FORM,1),Y,104)=""      ;BERECHTIGUNGS MODULE NUR LESEN ;only read 
	. ;
	. ;BUTTON
	. SET LANG=""
	. FOR  SET LANG=$ORDER(^WWW124(0,FORM,LANG)) QUIT:LANG=""  DO
	. . SET BUTT=""
	. . FOR  SET BUTT=$ORDER(^WWW124(0,FORM,LANG,BUTT)) QUIT:BUTT=""  DO
	. . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,22)=""  ;BERECHTIGUNG
	. . . SET $PIECE(^WWW124(0,FORM,LANG,BUTT,1),Y,23)=""  ;BERECHTIGUNGS MODULE
	. ;
	. ;SEITENREITERBERECHTIGUNG;TYBD;16.07.2003;23940;
	. SET LANG=""
	. FOR  SET LANG=$ORDER(^WWW1203(0,FORM,LANG)) QUIT:LANG=""  DO
	. . SET BUTT=""
	. . FOR  SET BUTT=$ORDER(^WWW1203(0,FORM,LANG,BUTT)) QUIT:BUTT=""  DO
	. . . SET $PIECE(^WWW1203(0,FORM,LANG,BUTT,1),Y,6)=""  ;BERECHTIGUNG
	. . . SET $PIECE(^WWW1203(0,FORM,LANG,BUTT,1),Y,5)=""  ;BERECHTIGUNGS MODULE
	
	QUIT
	
	
]]></Routine>
</Export>