<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW124" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW124
#include COMSYS
#include WWWConst
#include COMConst
 
	;14-Jun-2011	shobby	SR17792:   Class/Form changed (Do Not remove comment)
	;09-Sep-2008	shobby	SRBR014980 Class/Form changed (Do Not remove comment)
	;29-Apr-2008	shobby	SRBR014939 Class/Form changed (Do Not remove comment)
	 
OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Routine to run after deleting a button
	;
	; Params: pYKEY	: The YKEY
	;
	; ByRefs:
	;
	; Returns: strStatus
	;
	; History:
	; 26-May-2009	shobby	SRBR014988: Kill other languages only when the EN button
	; 							is killed.
	; 10-Sep-2008	shobby	Removed kill of COMHelp* (temporary data now)
	; 22-Feb-2007	JW		SR15452: Kill other languages
	; 08-Nov-2006	SS		SR14915: Created
	;-------------------------------------------------------------------------------
	new strStatus,idForm,idLang,idSeq
 
	set strStatus=$$$OK
 
	//if $$GetKey^COMUtilClass(pYKEY,3) {
	if $$GetKey^COMUtilClass(pYKEY,3,3,.idForm,.idLang,.idSeq) {		//SR15452 ;BR014988
		;set strStatus = $$$Kill("COMHelpButton",pYKEY)  BR014980
		
		if idLang="EN" {      // SR15452 - Kill other languages
			if $$$ISOK(strStatus) {
				set idLang = ""
				for {
					set idLang = $order(^WWW124(0,idForm,idLang))
					quit:idLang=""
					continue:(idLang="EN") ;BR014988
					
					if $data(^WWW124(0,idForm,idLang,idSeq)) {
						set strStatus = $$$Kill("WWW124",idForm_$$$COMMA_idLang_$$$COMMA_idSeq)
					}
					quit:$$$ISERR(strStatus)
				}
			}
		}
	}
	quit strStatus
	
	
OnAfterSave(pYKEY,pobjButton)
	;-------------------------------------------------------------------------------
	; Routine to run after saving a button
	;
	; Params: pYKEY	: The YKEY
	;
	; ByRefs:
	;
	; Returns: strStatus
	;
	; History:
	; 15-Jul-2011	shobby	SR17829:  New properties added to a button weren't being copied.
	; 26-May-2009	shobby	SRBR014988: Reworked to only modify other buttons when
	; 							English button changes.  Include additional fields
	; 							for preservation.
	; 10-Sep-2008	shobby	Removed kill of COMHelp* (temporary data now)
	; 22-Feb-2007	JW		SR15452: Copy to English/Portuguese. Added pobjButton
	; 08-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strStatus,idForm,idLang,idSeq,idCopyLang,objOldLangButton,objLangButton,intField
	
	set strStatus = $$$OK
	
	if $$GetKey^COMUtilClass(pYKEY,3,3,.idForm,.idLang,.idSeq) {
		if idLang="EN" {                                // Copy to other languages
			if $$$ISOK(strStatus) {
				for idCopyLang = "PT","ES" {			// Add other languages when needed
					set objLangButton = $get(^WWW124(0,idForm,idCopyLang,idSeq,1),pobjButton)
					
					;for intField=1:1:$length(objLangButton,Y) {			;SR17829
					for intField=1:1:$length(pobjButton,Y) {				;SR17829
						if '$$Ignore^WWWBUTTONCHECK(intField) {
							set $piece(objLangButton,Y,intField) = $piece(pobjButton,Y,intField)
						}
					}
					set strStatus = $$$Save("WWW124",idForm_","_idCopyLang_","_idSeq,objLangButton,$$$YES)
					quit:$$$ISERR(strStatus)
				}
			}
		}
	}
	quit strStatus
	
	
GetButtonImage(pidForm,pidLang,pidButton,pblnDisabled)
	;-------------------------------------------------------------------------------
	; Works out the name of the image to be displayed based on the Id and the disabled condition.
	; There are a few images that we know exist and can display the disabled version even if none
	; specified.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 04-Jul-2007	RPW		SRBR014409: pYFORM > pidForm, pSPRACHE > pidLang
	; 26-Jun-2007	shobby	SRBR014409:	Call to standard routine to get the known
	; 							equivalent disabled image name
	; 25-Jun-2007	shobby	SRBR014409: Created
	;-------------------------------------------------------------------------------
	new objWWW124,strImage
	
	set strImage = ""
	if (pidForm'="") && (pidLang'="") && (pidButton'="") {
		set objWWW124 = $get(^WWW124(0,pidForm,pidLang,pidButton,1))
		set strImage  = $$$WWW124PictureFileOnButton(objWWW124)
		if strImage="" {
			set strImage = $$$LOWER($extract($$$WWW124ButtonDescription(objWWW124)))_".gif"
		}
		if pblnDisabled {
			if $$$WWW124ImageFileForDisabledButto(objWWW124)'="" {
				set strImage = $$$WWW124ImageFileForDisabledButto(objWWW124)
			} else {
				set strImage = $$Name^WWWIMAGE(strImage,$$$YES)
			}
		}
	}
	quit strImage
	
	
GetButtonDescription(pidForm,pidLang,pidButton)
	;-------------------------------------------------------------------------------
	; Works out the title description of the button to be displayed in the tooltip.
	; If the button calls another form gets the form header or image file, if not
	; get the button description in ^WWW124.
	; 
	; Called By: ^WWWFORMC, BUTTON^WWWHELP
	; 
	; Params:
	;
	; Returns:	Button description
	;
	; History:
	; 13-Jul-2009	GRF		SR16691: obj variable
	; 10-Jul-2007	RPW		SRBR014589: Coding standards
	; 27-Jun-2007	Karine	SRBR014535: Created
	;-------------------------------------------------------------------------------
	new objButton,strTitle
	
	set objButton = $get(^WWW124(0,pidForm,pidLang,pidButton,1))
	
	if (pidForm'="") && (pidLang'="") && (pidButton'="") {     ; FIXME : we will have already crashed in the previous line - unnecessary? <GRF>
		if $$$WWW124NewFormOnClick(objButton)'="" {
			set strTitle = $$^WWWFORMNAME($$$WWW124NewFormOnClick(objButton)) 
		} else {
	 		set strTitle = $$$WWW124ButtonDescription(objButton) 
		}
	}
			
	; set "form header + (form name)" for sysadmin users
	if $get(YBEDBER)=1 {
	   set strTitle = strTitle_" ("_$$$WWW124NewFormOnClick(objButton)_$$$WWW124ExecuteOnClick(objButton)_")" 
	}
	
	quit strTitle	
	
]]></Routine>
</Export>