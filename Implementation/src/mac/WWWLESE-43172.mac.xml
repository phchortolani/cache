<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWLESE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWLESE(YDATEI,YKEY,YTEST,YNEWYM,YREADONLY)
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LESEN DATENSATZ
	;		Read Record
	;			D ^WWWLESE(YDATEI,"KEY1,KEYN")      ;INHALT IN YFELD
	; 
	; Inputs : 
	;	YDATEI  	Class
	;	YKEY    	Keys      SCHLUESSEL
	;	YTEST   	1 = PRUEFUNG OB DER DATENSATZ VORHANDEN IST / CHECK IF $D ;whether the data record on hand 
	;	        	2 = NUR PRUEFUNG OB DER DATENSATZ VORHANDEN IST / CHECK IF $D (KEIN LOCK !!!) ;only whether the data record on hand 
	;	YNEWYM  	WENN VON EINEM SPEZIELLEM MANDANT GELESEN WERDEN SOLL.  ;BEC;30.09.04;26476
	;	YREADONLY	=1 KEIN LOCK NEUSETZTEN ODER LÖSCHEN                    ;BEC;TYBD;27477;14.03.05
	;
	;
	; ByRef : see list of variables in exclusive NEW
	;	YFELD - contents of record specified by YDATEI & YKEY
	;
	; Returns :
	;
	;
	; History :
	; 03-May-2010	GRF		SR15961: use dteToday for efficiency
	; 20-Feb-2009	GRF		SR16356: call CreateLock^WWWFORMLocking as common
	; 							locking code; cleanup old change comments
	; 19-Feb-2009	HQN		SR16356: Handle locking as a pair of entries
	; 							WWWDATEN(,,,,"LOCK") and WWW006() records may exist
	; 							only if $$CanLock^WWWFORMLocking() is true
	; 18-Jun-2007	RPW		SRBR014510: Use YA1 instead of YA(1). YA1 is the object
	; 							id, YA(1) is the data.
	; 13-Mar-2007	GRF		SR12505: FIXME; comment unnecessary new command - covered
	; 							by exclusive new
	; 02-Jan-2007	GRF		SR15336: Doco; quits; naked reference; move set inside
	; 							DO loop to simplify line
	; 05-May-2006	Steve S	SR14508: Only lock specific forms
	; 13-Apr-2006	JW		Removed + from user id's in lock check
	; 07-Jul-2005	shobby	SR12892: WWW126,WWW1262,WWW1262 are no longer shared.
	; 08.08.1997	DT
	;-------------------------------------------------------------------------------
	new (YREADONLY,YUMLAU,Y,YM,YMANDANT,LANGUAGE,SPRACHE,YFORM,YBEARB,YUSER,YBED,YDATEI,YKEY,YFELD,%FELD,%SCHLUESSEL,YTIMEFORM,YLOCATION,YTEST,YNEWYM)
	
	set dteToday = +$horolog   ; SR15961
	
	if YDATEI'="" {                              ; ALTERNATIVE CLASS
		set idxYDATEI = $$$Index(YDATEI)
		if $ORDER(^WWW0011s(0,1,idxYDATEI,""))'="" SET YDATEI = $ORDER(^WWW0011s(0,1,idxYDATEI,""))
	}
	
	SET YQ          = 0
	SET YFELD       = ""
	SET %FELD       = ""
	SET %SCHLUESSEL = ""
	SET YTEST       = $GET(YTEST)
	SET YTEST1      = ""
	IF YTEST=2 SET YTEST=1 SET YTEST1=1    ; KEIN LOCK EINTRAGEN
	
	if (YFORM'="") && (YKEY'="") && (YBED'="") && '$find(YKEY,"+") {
		set dteKillDate = dteToday
		for {
			set dteKillDate = $order(^WWW1264(YM,YBED,dteKillDate),-1)
			quit:dteKillDate=""
			
			kill ^WWW1264(YM,YBED,dteKillDate)
		}
		set intTime = ""
		for {
			set intTime = $order(^WWW1264(YM,YBED,dteToday,intTime))
			quit:intTime=""
			
			if $get(^WWW1264(YM,YBED,dteToday,intTime,1))=(YFORM_Y_YKEY) {
				kill ^WWW1264(YM,YBED,dteToday,intTime,1)
			}
		}
		set ^WWW1264(YM,YBED,dteToday,$piece($horolog,",",2),1)=YFORM_Y_YKEY
	}
	
	IF YDATEI'="" DO
	. SET YDATA   = $GET(^WWW001(0,YDATEI,1))
	. SET MAXYKEY = +$ORDER(^WWW002(0,YDATEI,""),-1)
	. IF MAXYKEY=0 SET MAXYKEY = 1
	. IF MAXYKEY'=0 DO
	. . SET Q   = 0                           ;SAVE OK?
	. . SET YOK = 0                           ;MULTISCHLUESSEL PRUEFUNG 
	. . SET SCHLUESSEL = "^"_YDATEI_"("
	. . IF $GET(YTIMEFORM)=1 SET SCHLUESSEL = "^"_YDATEI_"t("  ;zeitabhängige änderung
	. . SET SCHLUESSEL = SCHLUESSEL_$$^WWWYM(YDATEI,1)
	. . SET YNUMM = ""                              ;LFD NUMMER FÜR MULTI SCHLUESSELVORGABE ;numeral to 
	. . IF YFORM'="" IF +$PIECE($GET(^WWW120(0,YFORM,1)),Y,88)'=0 DO       ; D88	$$$WWW120NumberofMRURecordListItem()
	. . . SET YNUMM = $$^WWWNEXT("WWW1262")
	. . . IF $ORDER(^WWW1262(YM,YFORM,YBED,""),-1)>YNUMM DO
	. . . . SET YNUMM = $ORDER(^WWW1262(YM,YFORM,YBED,""),-1)+1
	. . ; 
	. . FOR I=1:1:MAXYKEY SET XYKEY = $TRANSLATE($PIECE(YKEY,",",I),"""") SET SCHLUESSEL = SCHLUESSEL_""""_XYKEY_""""_"," DO  IF XYKEY="" SET Q = 1
	. . . QUIT:'$DATA(YFORM)
	. . . QUIT:YFORM=""
	. . . SET ^WWW126(YM,YFORM,YBED,I,1)  = XYKEY  ;SPEICHERN VORGABEN ;Save 
	. . . SET ^WWW126(YM,YFORM,YUSER,I,1) = XYKEY  ;SPEICHERN VORGABEN ;Save 
	. . . IF YNUMM'="" DO                          ;MULTISCHLUESSEL VORGABE ;default 
	. . . . IF XYKEY="" SET YOK = 2 QUIT           ;NICHT SPEICHERN ;Not Save 
	. . . . SET YNUMM(1)=$ORDER(^WWW1262(YM,YFORM,YBED,""),-1)
	. . . . IF YNUMM(1)="" SET YOK = 1             ;SAVE OK
	. . . . IF YNUMM(1)'="" IF $GET(^WWW1262(YM,YFORM,YBED,YNUMM(1),I,1))'=$GET(XYKEY) SET YOK = 1  ;NICHT GLEICH ;Not without delay 
	. . . . SET ^WWW1262(YM,YFORM,YBED,YNUMM,I,1) = XYKEY  ;SPEICH DFLT
	. . ;
	. . IF (YOK=0) || (YOK=2) IF +YNUMM'=0 KILL ^WWW1262(YM,YFORM,YBED,YNUMM)  ;DOPPELTER DATENSATZ ;data record 
	. . IF $GET(YTIMEFORM)=1 DO  ;ZEITABHÄNGIGE ERFASSUNG ;logging 
	. . . NEW YABDATE
	. . . SET YABDATE = +$PIECE(YKEY,",",MAXYKEY+1)
	. . . IF YABDATE=0              SET SCHLUESSEL = $PIECE($PIECE(SCHLUESSEL,"(",1),"t",1)_"("_$PIECE(SCHLUESSEL,"(",2,999) QUIT  ;new record 
	. . . IF YABDATE>($HOROLOG-300) SET SCHLUESSEL = SCHLUESSEL_YABDATE_","
	. . ;
	. . IF $PIECE(YDATA,Y,8)'=4     SET SCHLUESSEL = SCHLUESSEL_"1"
	. . IF $PIECE(YDATA,Y,8)=4 IF $EXTRACT(SCHLUESSEL,$LENGTH(SCHLUESSEL))="," SET SCHLUESSEL = $EXTRACT(SCHLUESSEL,1,$LENGTH(SCHLUESSEL)-1)
	. . SET SCHLUESSEL = SCHLUESSEL_")"
	. . ;
	. . IF Q=0 DO
	. . . SET YFELD  = $$^WWWSETL(SCHLUESSEL,$GET(YTEST))   ;LESEN DATENSATZ
	. . . SET YBEARB = 3
	. . . IF $GET(YFORM)'="" IF $$^WWWLOCATION($GET(YLOCATION),YFORM,YFELD)'=1 SET YBEARB = 8    ;KEINE BERECHTIGUNG, DA FALSCHER BETRIEB 
	. . . SET %SCHLUESSEL = SCHLUESSEL
	. . . SET %FELD = YFELD                                   ; COPY FÜR AENDERUNGSPRÜFUNG ;to 
	. . . SET YLOCK = +$PIECE($GET(^WWW001(0,YDATEI,1)),Y,6)  ;SATZLOCKSEKUNDEN
	. . . ;
	. . . ;---------------------------------
	. . . ;   YA1    WWW006 P2   idFile : Class and keys (e.g. "^INReqToSupOrder/0.REQM0001.1/")
	. . . ;   YA(1)  objWWW006   Lock-File
	. . . ;---------------------------------
	. . . IF $GET(YREADONLY)'=1 SET YA1 = "" FOR  SET YA1 = $ORDER(^WWW006(0,dteToday,YA1)) QUIT:YA1=""  DO
	. . . . SET YA(1) = $GET(^WWW006(0,dteToday,YA1,1))                       ; SR15336
	. . . . IF $PIECE(YA(1),Y,1)=YUSER IF ($PIECE(YA(1),Y,2)+YLOCK)<$PIECE($HOROLOG,",",2) KILL ^WWW006(0,dteToday,YA1) 
	. . . . IF $PIECE(YA(1),Y,1)=YUSER IF YA1'=$TRANSLATE(%SCHLUESSEL,",()"_"""",".//")    KILL ^WWW006(0,dteToday,YA1) ; ALTEN LOCK LÖSCHEN
	. . . . ;RÜCKHOLEN LETZTER LOCK WENN FORMULAR IN SEPARATEM FENSTER
	. . . . IF $PIECE($GET(^WWWUSER(0,YUSER,1)),Y,25)'="" DO
	. . . . . NEW YTRAKTOLD,YA1,YA,YT
	. . . . . SET YTRAKTOLD = $PIECE($GET(^WWWUSER(0,YUSER,1)),Y,25)      ; Transaction no. Lock Return Info
	. . . . . SET YT  = 0
	. . . . . SET YA1 = ""
	. . . . . FOR  SET YA1 = $ORDER(^WWW0061(0,dteToday,YA1)) QUIT:YA1=""  DO
	. . . . . . IF $DATA(^WWW0061(0,dteToday,YA1,YTRAKTOLD)) DO
	. . . . . . . SET YA(1) = $GET(^WWW0061(0,dteToday,YA1,YTRAKTOLD,1))
	. . . . . . . IF $PIECE(YA(1),Y,1)=YUSER IF ($PIECE(YA(1),Y,2)+300)>$PIECE($HOROLOG,",",2) DO  QUIT  ;PRÜFEN OB NOCH GÜLTIG (MAX. 5 MINUTEN) ;check whether yet valuable
	. . . . . . . . IF '$DATA(^WWW006(0,dteToday,YA1)) SET ^WWW006(0,dteToday,YA1,1) = $GET(^WWW0061(0,dteToday,YA1,YTRAKTOLD,1))  ;LOCK ZURÜCKSETZTEN
	. . . . . . . . SET YT = 1
	. . . . . . . ;
	. . . . . . . KILL ^WWW0061(0,dteToday,YA1)  ;LÖSCHEN WENN UNGÜLTIG ;Delete when 
	. . . . . ;
	. . . . . IF YT'=1 SET $PIECE(^WWWUSER(0,YUSER,1),Y,25)=""
	. . . ;
	. . . IF ($GET(YREADONLY)'=1) && (YDATEI'="WWW006") && (YLOCK'=0) DO
	. . . . new strKey
	. . . . set strKey = $TRANSLATE(%SCHLUESSEL,",()"_"""",".//")
	. . . . IF $DATA(^WWW006(0,dteToday,strKey,1)) DO
	. . . . . SET YA(1) = $get(^WWW006(0,dteToday,strKey,1))
	. . . . . IF YUSER'=$$$WWW006User1(YA(1)) IF ($$$WWW006LockedUntilTime(YA(1))+YLOCK)>$PIECE($HOROLOG,",",2) SET Q=1 SET YBEARB=4  ;IN USE
	. . . . . ;
	. . . . . ;*****************************
	. . . . . ;  FIXME : Do we really want an unconditional lock (which unlocks ALL locks held by this process)?
	. . . . . ;          e.g.  lock batch, save block [unconditionally unlock], save batch, unlock batch
	. . . . . ;          Other processes can sieze control of the batch between unlock and save.
	. . . . . ;          This depends on whether the following lock is ever executed.                  <GRF>
	. . . . . ;          Most calls don't pass YREADONLY
	. . . . . ;          Only executed if Lock Seconds assigned in WWW001
	. . . . . ;*****************************
	. . . . . ;
	. . . . . LOCK
	. . . ;
	. . . IF $GET(YREADONLY)'=1 DO      ;BEC;TYBD;27477;14.03.05
	. . . . IF YTEST1=1 QUIT:YFELD'=""  ;FIS;24.10.03;24469;NUR LOCK EINTRAGEN WENN DATENSATZ VORHANDEN
	. . . . ; SR16356 vvvv
	. . . . ; Only lock from the allowed forms
	. . . . IF (Q=0) && (YDATEI'="WWW006") && $$CanLock^WWWFORMLocking(YFORM) do CreateLock^WWWFORMLocking(%SCHLUESSEL,YUSER,$horolog,YFORM)
	
	;. . . .  ^^^ REPLACES vvvv
	;. . . . IF (Q=0) && (YDATEI'="WWW006") && $$CanLock^WWWFORMLocking(YFORM) DO
	;. . . . . SET ^WWW006(0,dteToday,$TRANSLATE(%SCHLUESSEL,",()"_"""",".//"),1) = YUSER_Y_$PIECE($HOROLOG,",",2) ;SPEICHERN NEUEN LOCK ;Save 
	;. . . . . IF $GET(YFORM)'="" SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1) = "^WWW006(0,"_dteToday_","_""""_$TRANSLATE(SCHLUESSEL,",()"_"""",".//")_""""_",1)"
	;. . . .  ^^^ REPLACES vvvv
	;. . . . IF Q=0 IF YDATEI'="WWW006" SET:$$CanLock^WWWFORMLocking(YFORM) ^WWW006(0,dteToday,$TRANSLATE(%SCHLUESSEL,",()"_"""",".//"),1)=YUSER_Y_$PIECE($HOROLOG,",",2) ;SPEICHERN NEUEN LOCK ;Save 
	;. . . . IF $GET(YFORM)'="" SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1)="^WWW006(0,"_dteToday_","_""""_$TRANSLATE(SCHLUESSEL,",()"_"""",".//")_""""_",1)"
	;. . . .  SR16356 REPLACED ^^^^
 
	QUIT
	
]]></Routine>
</Export>