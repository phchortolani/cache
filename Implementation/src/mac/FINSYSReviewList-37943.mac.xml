<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINSYSReviewList" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINSYSReviewList
#include COMConst
#include FINConst
#include WWWConst
#include COMSYS
	
OnBeforeButton()
	;-------------------------------------------------------------------------------
	; Build Lists ready for form drop dowm
	; 
	; History
	; 10-Aug-2006	GRF		SR14582: Created
	;-------------------------------------------------------------------------------
	do PopulateTxTypes()
	quit
 
	
PopulateTxTypes()
	;--------------------------------------------------------------------------
	; Populates the COMTempList for authorised transaction selection 
	; 
	; Returns: Nothing
	; 
	; History:
	; 10-Aug-2006	GRF		SR14582: Created
	;-------------------------------------------------------------------------------
	new idTranType,objParameter,objReview,objSelectList,objUser
	
	kill ^COMTempList(YM,YUSER,"FINSYSReviewTrans")
	set objUser   = $get(^WWW013(0,YBED,1))
	
	set idTranType=""
	for {
		set idTranType=$order(^WWW101(0,"FINSYSREVIEWTRANTYPE",SPRACHE,idTranType))
		quit:idTranType=""
		continue:'$data(^FINSYSReview(YM,idTranType))
		
		set objReview = $get(^FINSYSReview(YM,idTranType,1))
		
	;	only show those transactions to which the user has access
 		if $$UserAccessValid^FINSYSReview(objReview,objUser) {
			set objParameter  = $get(^WWW101(0,"FINSYSREVIEWTRANTYPE",SPRACHE,idTranType,1))
			set objSelectList = ""
			set $$$COMTempListField1(objSelectList) = $$$WWW101Text(objParameter)
			do Save^COMUtils("COMTempList",YUSER_",FINSYSReviewTrans,"_idTranType,objSelectList,$$$YES,$$$YES)
		}
	}
	
	quit
	
	;*******************************************************************************
	
ShowList()
	;-------------------------------------------------------------------------------
	; Search invoices - "(S)how Transactions" Button on Form FINSYSReviewList
	;
	; Returns:
	;
	; History:
	; 15-Aug-2006	PO 		SR14582: Added call to SourceSelected and enabled COMView to appear in main frame
	; 10-Aug-2006	GRF		SR14582: Copied from MatchInvoices^FINAPInvSearch & Modified
	;-------------------------------------------------------------------------------
	new idClass,idFilterLocn,idForm,objParams
	
	;	FINAPADJ			Supplier Adjustments 
	;	FINAPInv			Supplier Invoices 
	;	FINAPInvBatch		AP Invoice Batches 
	;	FINAPPayment		Supplier Payments 
	;	FINARADJ			Customer Adjustments 
	;	FINARBank			Customer Banking 
	;	FINARInv			Customer Invoices 
	;	FINGLBankRecon		Bank Reconciliati 
	;	FINGLDistribution	Distribution/Accrual 
	;	FINGLJournal		Journals  
 
	; FIXME: Shouldn't be referencing WWWDATEN directly. Use GetFormData^COMUtilForm.
 
	set objParams    = $get(^WWWDATEN(YM,+$horolog,YUSER,"FINSYSReviewList","M",1))
	set idClass      = $piece(objParams,Y,1)
	set idFilterLocn = $piece(objParams,Y,2)
	
	set idForm = idClass
	if idClass="FINAPInv" {
		set idForm = "FINAPVoucher"
	}
	
	if idClass="" {
		$$$Alert("Fin00690")  ; "Please Select A Data Record First."
	} else {
		set ^CacheTempReviewListForm(YUSER) = idForm
		do Initialise^COMViewCustom(idForm,"SourceSelected^FINSYSReviewList")
		
		if idClass="FINAPADJ" {
			do AddFilter^COMViewCustom("D"_$$$FldFINAPADJStatus,"=",$$$EnumFINSYSSTATUSPending)
			if idFilterLocn'="" {
				do AddFilter^COMViewCustom("D"_$$$FldFINAPADJCreatorsLocation ,"=",idFilterLocn)
			}
		
		
		} elseif idClass="FINAPInv" {
			do AddFilter^COMViewCustom("D"_$$$FldFINAPInvStatus,"=",$$$EnumFINSYSSTATUSPending)
			if idFilterLocn'="" {
				do AddFilter^COMViewCustom("D"_$$$FldFINAPInvCreatorsLocation ,"=",idFilterLocn)
			}
		
		
		} elseif idClass="FINAPInvBatch" {
			do AddFilter^COMViewCustom("D"_$$$FldFINAPInvBatchStatus,"=",$$$EnumFINSYSSTATUSPending)
	;		** No Location Filter **
		
		
		} elseif idClass="FINAPPayment" {
			do AddFilter^COMViewCustom("D"_$$$FldFINAPPaymentStatus,"=",$$$EnumFINSYSSTATUSPending)
			if idFilterLocn'="" {
				do AddFilter^COMViewCustom("D"_$$$FldFINAPPaymentLocation ,"=",idFilterLocn)
			}
		
		
		} elseif idClass="FINARADJ" {
			do AddFilter^COMViewCustom("D"_$$$FldFINARADJStatus,"=",$$$EnumFINSYSSTATUSPending)
			if idFilterLocn'="" {
				do AddFilter^COMViewCustom("D"_$$$FldFINARADJCreatorsLocation ,"=",idFilterLocn)
			}
		
		
		} elseif idClass="FINARBank" {			
			do AddFilter^COMViewCustom("D"_$$$FldFINARBankStatus,"=",$$$EnumFINSYSSTATUSPending)
	;		** No Location Filter **
		
		
		} elseif idClass="FINARInv" {
			do AddFilter^COMViewCustom("D"_$$$FldFINARInvStatus,"=",$$$EnumFINSYSSTATUSPending)
			if idFilterLocn'="" {
				do AddFilter^COMViewCustom("D"_$$$FldFINARInvCreatorsLocation ,"=",idFilterLocn)
			}
		
		
		} elseif idClass="FINGLBankRecon" {
			do AddFilter^COMViewCustom("D"_$$$FldFINGLBankReconStatus,"=",$$$EnumFINSYSSTATUSPending)
			if idFilterLocn'="" {
				do AddFilter^COMViewCustom("D"_$$$FldFINGLBankReconLocation ,"=",idFilterLocn)
			}
		
		
		} elseif idClass="FINGLDistribution" {
			do AddFilter^COMViewCustom("D"_$$$FldFINGLDistributionStatus,"=",$$$EnumFINSYSSTATUSPending)
	;		** No Location Filter **
		
		
		} elseif idClass="FINGLJournal" {
			do AddFilter^COMViewCustom("D"_$$$FldFINGLJournalStatus,"=",$$$EnumFINSYSSTATUSPending)
	;		** No Location Filter **
		}
		
		do Start^COMViewCustom(,,$$$YES)
	}
	quit
	
SourceSelected(pobjResultSet)
	;-------------------------------------------------------------------------------
	; Open the appropriate form with the record selected.
	; Link passed to Initialise^COMViewCustom via ShowList above.
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 11-Aug-2006	PO		SR14582: Created
	;-------------------------------------------------------------------------------
	new idKey
	
	if pobjResultSet.Next() {
		set idKey=$$GetID^COMViewCustom(pobjResultSet)
		
	 	do GoToForm^COMUtilForm($get(^CacheTempReviewListForm(YUSER)),idKey,,,$$$YES,"FINSYSReviewListing")
	}
	
	quit
	
]]></Routine>
</Export>