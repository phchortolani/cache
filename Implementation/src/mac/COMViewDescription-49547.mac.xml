<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewDescription" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewDescription ; plays with the description of the search grid.
	#include COMConst
	#include COMSYS
	#include WWWConst
	#include %occInclude
 
Show()
    ;-------------------------------------------------------------------------------
    ; Re-displays the menu header for a COMViewSearch
    ;
    ; Returns:
    ;
    ; History:
    ; 15-Jul-2014	shobby	HEVA-1565: MenuType should also consider WWW012
    ; 08-Apr-2011	shobby	SR17707: Highlight the link on the WindowTitle.
    ; 21-Mar-2011	shobby	SR17258: Get the COMView title from the form name rather than
    ;							the class name.
    ; 03-Mar-2010	GRF		-: true boolean value; clear old commented code
    ; 03-Dec-2009	PPP		SR17069: Add the 'Start' & 'Menu' buttons for Flexible Menu (10)
    ;							when there is no @NM class defined for the COMView class
    ; 09-Sep-2008	PP		SR15866: Update COMView to Objects
    ; 12-Sep-2006	RPW		SR15029: Don't show plan unless you are DBAdmin or SysAdmin
    ; 29-Aug-2006	JW		SR14763: add in call to JS:Search
    ; 19-Jul-2006	JW		SR14832: Support 'between' clause
    ; 29-May-2006	Steve S	SR14675: Support 'LIKE' clause
    ; 11-May-2006	RPW		SR14420: Finished checking for calculated fields so that
    ; 							the header gets the description correctly.
    ; 13-Feb-2006	SC & JW	SR14250: Set form passed to HasViewAccess. COMViewSearch
    ; 							was not checking correct form's access.
    ; 31-Jan-2006	PO		SR14250: If user doesn't have View Access then disallow
    ; 							condition removal
    ; 15-Jul-2005	PO & JW	Added language text
    ; 11-May-2005	PO & PK	SR12142 Enabling select all feature
    ; 29-Mar-2005	Paul K	Modified for related classes
    ; 10-Feb-2005	PO		SR10965 Switched over to using new list form of DisplayValue.
    ; 06-Dec-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	new blnCanRemoveCondition,blnObj,COLOR,COLORR
	new idClass,idField,idFilter,idForm,idRealClass,lstValue,objFilter
	new strAccess,strCaption,strComp,strFilterDescription,strHTML,strShowPlan,strText
		
	set blnObj = ''$get(^CacheTempObj(YUSER,"Object"))
	
	quit:+$get(^CacheTempView(YUSER,YUCI,"InForm"))
	
	set idForm = $get(^CacheTempView(YUSER,"Form"))
	set blnCanRemoveCondition = $$HasViewAccess^COMView(YBED,idForm,YM) //SR14250
	
	set strHTML = ""
	set idClass = $get(^CacheTempView(YUSER,"Class"))	
	if idClass'="" {
		set strText = $$$Text($select($get(^CacheTempView(YUSER,"CallBack"))="":"Com00122",1:"Com00189"))
	;  		"Search : "   "Select : "
 		//SR17069
 		set strHTML=strHTML_"<table border=0 nowrap width=100%> <tr> <td> <b> <font size=3 color=white>" ;SR17707
 			
 		set strAccess=","_$$^WWWBEDBER(YBED)_","
 		set strShowPlan=""
 		if ($find(strAccess,",1,"))||($find(strAccess,",2,")) {
	 		set strShowPlan="onclick='ShowPlan();'"
			;SR17707 vvvvvv
	 		set strShowPlan=strShowPlan_" onmouseover='"
	 		set strShowPlan=strShowPlan_"document.getElementById(""WindowTitle"").style.cursor=""pointer"";"
	 		set strShowPlan=strShowPlan_"document.getElementById(""WindowTitle"").style.color=""wheat"";"
	 		set strShowPlan=strShowPlan_"'"
	 		set strShowPlan=strShowPlan_" onmouseout='"
	 		set strShowPlan=strShowPlan_"document.getElementById(""WindowTitle"").style.color=""white"";"
	 		set strShowPlan=strShowPlan_"'"
			;SR17707 ^^^^^^^
 		}
		
		set strHTML = strHTML_"<span "_strShowPlan_" id='WindowTitle'>"_strText_"</span>"  ; "Search : " / "Select : "

		set strText=$$^WWWFORMNAME(idForm)						 ;SR17258 
		if strText="" set strText=$$GetClassDescription(idClass) ;SR17258 Just in case
		set strHTML = strHTML_strText			  				 ;SR17258 
 
		set strFilterDescription = ""
		if $order(^CacheTempView(YUSER,"FixedFilter",""))'="" {
			set strFilterDescription = strFilterDescription_" "_$$$Text("Com00219")_" ("   ; "where"
			
			set idFilter = ""
			for {
				set idFilter = $order(^CacheTempView(YUSER,"FixedFilter",idFilter))
				quit:idFilter=""
				
				if $extract(strFilterDescription,$length(strFilterDescription))'="(" set strFilterDescription = strFilterDescription_", "
				
				set objFilter = $get(^CacheTempView(YUSER,"FixedFilter",idFilter))
				set idField   = $$$COMViewFilterField(objFilter)
				
				if blnCanRemoveCondition set strFilterDescription = strFilterDescription_"<span ondblclick='Searching(); CallBack(""RemoveFixedFilter^COMViewDescription"","_idFilter_");'>"
				
				set idRealClass=idClass
				if $$$COMViewFilterGroupBy(objFilter) {
					set strFilterDescription=strFilterDescription_" "_$$$StrCOMViewFilterGroupBy_" "
				}
				
				if 'blnObj {
					; FIXME : <GRF> GetRelation^COMViewUtils is a function.
					do GetRelation^COMViewUtils(.idRealClass,.idField)
					
					if $extract(idField,1)'="C" {
						set strFilterDescription = strFilterDescription_$$^WWWFELDNAME(idRealClass,$extract(idField,1),$extract(idField,2,99))
					} else {
						set strCaption = $$$WWW003CalcLangDescription($get(^WWW003CalcLang(0,idRealClass,$extract(idField,2,99),SPRACHE,1)))
						if strCaption = "" {
							set strCaption = $$$WWW003CalcCaption($get(^WWW003Calc(0,idRealClass,$extract(idField,2,99),1)))
						}
						set strFilterDescription = strFilterDescription_strCaption
					}
 
				} else {
					set strFilterDescription = strFilterDescription_idField		
				}
				
				set strComp = $$$COMViewFilterComparator(objFilter)
				if strComp="" {
					// Do nothing
				
				} elseif $$$Contains(($$$EnumCOMVIEWCOMPARATORStartsWith,$$$EnumCOMVIEWCOMPARATORLike,$$$EnumCOMVIEWCOMPARATORBetween),strComp) {
					set strFilterDescription = strFilterDescription_" "_$$$AppEnum("COMVIEWCOMPARATOR",strComp)_" "
				
				} else {
					set strFilterDescription = strFilterDescription_$case(strComp,
					                                       $$$EnumCOMVIEWCOMPARATORNotEquals   :" not = ",
					                                       $$$EnumCOMVIEWCOMPARATORGreaterThan :" > ",
					                                       $$$EnumCOMVIEWCOMPARATORLessThan    :" < ",
					                                                                           :" = "  )
				}
				
				if $$$COMViewFilterValue1(objFilter)="" {
					set strFilterDescription = strFilterDescription_$$$Text("Com00149")  ; "Nothing"
				
				} else {
					set lstValue = $$DisplayValue^COMViewFilter(idRealClass,idField,$$$COMViewFilterValue1(objFilter))
					set strFilterDescription = strFilterDescription_$listget(lstValue,2)_" "
				}
				if blnCanRemoveCondition set strFilterDescription = strFilterDescription_"</span>"
			}
			set strFilterDescription = strFilterDescription_" )"
		}
	}
	set strHTML=strHTML_$$CreateFilterDescriptionButton(strFilterDescription) 

	//SR17069
	;HEVA-1565 set objWWW013 = $get(^WWW013(0,YBED,1))
	;HEVA-1565 if ($$$WWW013MenuType(objWWW013) = 10) {
	if $$MenuType^WWW013()=10 {		;HEVA-1565
		// if not @NM class (alSOH.dPeriodBalance) of the Form does not have a class defined e.g.INAUFRELEASEORDER)
		if '$data(^WWW001(0,idClass)) || ($$GetClassForForm($get(YFORM))="") {
			do GetCOMViewHeaderColors(.COLOR,.COLORR)
			set strHTML = strHTML_"</b></td>"
			set strHTML = strHTML_"<td width=5% style=""""background-color:"_COLORR_";text-align:right;width:20px;"""">"_$$GetButtonStyle^WWWKOPF()_$$GetOverviewButton^WWWKOPF()_"</td>"
			set strHTML = strHTML_"<td width=5% style=""""background-color:"_COLORR_";text-align:right;width:20px"""">"_$$GetMenuButton^WWWKOPF()_"</td>"
			set strHTML = strHTML_"</tr></table>"
		}
	}
	
	write "desc.innerHTML='"_$zcvt(strHTML,"o","JS")_"';",!
	
	quit
	
	
CreateFilterDescriptionButton(pstrFilterDescription)
    ;-------------------------------------------------------------------------------
    ; Creates a +/- button to show or hide the selection filter details
    ; Default date is defined by the 'Hide Filter Description' in 
    ;
    ; Returns:
    ;
    ; History:
    ; 20-Sep-2007	shobby	SRBR014666: The ability to view the filter description
    ; 							can now be limited by permissions in the
    ; 							COMViewConfig screen.
    ; 20-Sep-2007	shobby	SRBR014666: Remember the current users state when
    ; 							refreshing the filter description (after double
    ; 							clicking to remove conditions).
    ; 01-Sep-2007	shobby	SRBR014666:	Created
    ;-------------------------------------------------------------------------------
	new strHTML,objCOMViewConfig,strTitle,blnCanAccess
	
	set strHTML=""
	if pstrFilterDescription'="" {
		set objCOMViewConfig=$get(^COMViewConfig(0,YM,1))
		set blnCanAccess=$$^WWWACCESS($$$COMViewConfigAbletoviewfilterdescripti(objCOMViewConfig))
		if blnCanAccess {
			set strHTML=strHTML_"<IMG  id='COMViewFilterButton' "
			
			if '$data(^CacheTempHideFilterDescription(YUCI,YUSER,YFORM)) { ;BR014666
				set ^CacheTempHideFilterDescription(YUCI,YUSER,YFORM)=$$$COMViewConfigHideFilterDescription(objCOMViewConfig)
			}
			if $get(^CacheTempHideFilterDescription(YUCI,YUSER,YFORM)) {
				set strHTML=strHTML_"SRC='"_YGIF_"COAexpand.gif' "
				set strTitle=$$$TextSimple("Com00299")					;Expand
			} else {
				set strHTML=strHTML_"SRC='"_YGIF_"COAcollapse.gif' "
				set strTitle=$$$TextSimple("Com00300")					;Collapse
			}
			set strHTML=strHTML_"ALIGN=ABSMIDDLE TITLE="_strTitle_" border=0 "
			set strHTML=strHTML_"onclick='if (document.getElementById(""COMViewFilter"").style.display==""none"") {"
			set strHTML=strHTML_"  document.getElementById(""COMViewFilter"").style.display=""inline"";"
			set strHTML=strHTML_"  with (document.getElementById(""COMViewFilterButton"")) {"
			set strHTML=strHTML_"      src="""_YGIF_"COAcollapse.gif"";"
			set strHTML=strHTML_"      title="""_$$$TextSimple("Com00300")_""";"   ;Collapse
			set strHTML=strHTML_"  }"
			set strHTML=strHTML_"  CallBackNow(""SetFilterDescription^COMViewDescription"",document.WWW.YUCI.value,document.WWW.YUSER.value,document.WWW.YFORM.value,""0"");"   ;BR014666
			set strHTML=strHTML_"} else {"
			set strHTML=strHTML_"  document.getElementById(""COMViewFilter"").style.display=""none"";"
			set strHTML=strHTML_"  with (document.getElementById(""COMViewFilterButton"")) {"
			set strHTML=strHTML_"      src="""_YGIF_"COAexpand.gif"";"
			set strHTML=strHTML_"      title="""_$$$TextSimple("Com00299")_""";"   ;Expand
			set strHTML=strHTML_"  }"
			set strHTML=strHTML_"  CallBackNow(""SetFilterDescription^COMViewDescription"",document.WWW.YUCI.value,document.WWW.YUSER.value,document.WWW.YFORM.value,""1"");"	;BR014666
			set strHTML=strHTML_"}'"
			set strHTML=strHTML_">"
			set strHTML=strHTML_"<span id='COMViewFilter'"
			if $get(^CacheTempHideFilterDescription(YUCI,YUSER,YFORM)) {
				set strHTML=strHTML_"STYLE='DISPLAY:NONE;'"
			}
			set strHTML=strHTML_">"_pstrFilterDescription_"</span>"
		}
	}
	quit strHTML
 
SetFilterDescription(pYUCI,pYUSER,pYFORM,pintValue)
    ;-------------------------------------------------------------------------------
    ; Remember the current users state when refreshing the filter description
    ; (after double clicking to remove conditions)..
    ;
    ; Returns:
    ;
    ; History:
    ; 20-Sep-2007	shobby	SRBR014666: Created
    ;-------------------------------------------------------------------------------
	set ^CacheTempHideFilterDescription(YUCI,YUSER,YFORM)=pintValue
	quit
 
 
RemoveFixedFilter(pidFilter)
    ;-------------------------------------------------------------------------------
    ; Called to remove a filter from the fixed filters.
    ;
    ; Returns:1
    ;
    ; History:
    ; 06-Dec-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	if pidFilter'="" {
		kill ^CacheTempView(YUSER,"FixedFilter",pidFilter)
		do Show()
		do DisplayHeader^COMViewFilterColumn()
		do DisplayGrid^COMViewFilter()
	}
	quit 1
	
	
GetClassDescription(pidClass="")
    ;-------------------------------------------------------------------------------
    ; Gets a class description
    ;
    ; Returns:String
    ;
    ; History:
    ; 09-Sep-2008	PP		SR15866:Update COMView to Objects
	; 15-Jul-2005	shobby	SR12754:Replaced LANGUAGE global (not always reliable)
    ; 12-Apr-2005	Paul K	Commented
    ;-------------------------------------------------------------------------------
	new objClass,strDescription
	
	set strDescription = ""
	if pidClass'="" {
		set strDescription = $$$WWW0011ClassDescription($get(^WWW0011(0,pidClass,SPRACHE,1)))
		if strDescription="" set strDescription = $$$WWW001ClassDescription($get(^WWW001(0,pidClass,1)))
		
		//SR15866
		if strDescription="" {
			set objClass = ##class(%Library.ClassDefinition).%OpenId(pidClass)
			if objClass'=$$$NULLOREF {
				set strDescription = $translate(objClass.Description,$char(13,10)," ")
				if strDescription = "" {
					set strDescription = objClass.Name
				}
				do objClass.%Close()
			}
		}
	}
	quit strDescription
	
	
GetDescription(pidClass,pstrFieldType="",pintClassField="")
	;-------------------------------------------------------------------------------
	; Rewrite of GetDescription
	;
	; This procedure is designed to return the relevant field description given a
	; class and field, source the description from the form definition. This is
	; since form defs get updated but classes generally will not be.
	;
	; Called By:
	; 	COMViewChoose           (case 1)
	; 	COMViewFilterColumn     (case 1 & case 2/3)
	; 	COMViewFilterControl    (case 2/3)
	; 
	; Inputs:						  case	 1)          2)          3)
	;                                       Form       Field       Calc'n
	; 	pidGlobal		Class ID			 *           *			 *
	; 	pstrFldTyp		Field Type			 -          P/D           C
	; 	pintFldNo		Field Number		 -       ClassFld#     CalcNo
	; 
	; Returns:
	; case	1)			Form Name (Language based) if field type & number are omitted
	; 		2)			Form or Class name for field (Type "P" or "D")
	; 		3)			Calculated Field Name (Type "C")
	;
	; History:
    ; 24-Jun-2008	shobby	SRBR014958: Use standard routine to get calculated field description.
	; 21-Feb-2008	shobby	SRBR014900: Moved from COMUtilClass.  Keep all the COMView stuff together.
	; 11-Jan-2008	shobby	SRBR014869: Make sure idForm is not empty before attempting to use it to find form information.
	; 10-Jan-2008	shobby	SRBR014790: Renamed to avoid the double call.
	; 10-Jan-2008	GRF		SRBR014790: Added parameter defaults so not missed if
	; 							renamed
	; 20-Dec-2007	shobby	SRBR014751: Parameter pidForm no longer required.
	; 20-Dec-2007	shobby	SRBR014751: Corrected issue referencing WWW003Calc
	; 19-Dec-2007	shobby	SRBR014751: Rewrite of GetDescription
	;-------------------------------------------------------------------------------
	new strDescription,intFormField,idClass,idForm
	
	set strDescription=""
	
	;---------------------------------------
	; Try to determine which form should be used to get the description of the field.
	; Normally this is held in the CacheTempView global.
	; But if not, try looking in YFORM or assume that the form is the same name as the class. 
	; If we find a valid idForm that still doesn't guarantee that we will find the description
	; of the field.  So we check various places where it might exist in decreasing order of likely
	; accuracy.
	; This may be reviewed later to determine if it is appropriate to just find a value from
	; anywhere and we may decide that if the value is not found on the 'known' form.
	;---------------------------------------
	
	set idForm=$get(^CacheTempView(YUSER,"Form"))
	
	if (idForm="") || ($$$WWW120ClassUsedInForm($get(^WWW120(0,idForm,1)))'=pidClass) {
		set idForm=$get(YFORM)
		if idForm="" {
			set idForm=pidClass
		} elseif $$$WWW120ClassUsedInForm($get(^WWW120(0,idForm,1)))'=pidClass {
			set idForm=pidClass
		}
	}	
	
	;---------------------------------------
	
	if pstrFieldType      = "M" {                                              ; Case 2 (M)
		set strDescription = $$^WWWFELDNAME(idForm,pstrFieldType,pintClassField)
		
		
	} elseif pstrFieldType = "" {                                              ; Case 1
		set strDescription = $$^WWWFORMNAME(idForm)
		
		
	} elseif pstrFieldType = "C" {                                             ; Case 3
		set strDescription = $$GetDescription^COMViewCalculatedField(pidClass,pintClassField,SPRACHE)
		
	} elseif pstrFieldType = "D" {                                             ; Case 2 (D)
		;if idForm'="" {
			; Look first in the form that we are using.
			set strDescription = $$GetFromWWWFELDNAME^COMUtilClass(pidClass,.idForm,pintClassField,.intFormField,$$$NO)
			
			if ($extract(strDescription,1,5)="_FREE") || (strDescription="") {
				; If not look in the form that matches the class name
				set idForm         = pidClass
				set strDescription = $$GetFromWWWFELDNAME^COMUtilClass(pidClass,.idForm,pintClassField,.intFormField,$$$NO)
			}
			
			if ($extract(strDescription,1,5)="_FREE") || (strDescription="") {
				; If not then look in first form that relates to this class
				set idForm         = ""
				set strDescription = $$GetFromWWWFELDNAME^COMUtilClass(pidClass,.idForm,pintClassField,.intFormField,$$$YES)
			}
			
			if ($extract(strDescription,1,5)="_FREE") || (strDescription="") {
				; Ok.  We didn't find anything useful so lets go back to the form that matches the class name
				; and we will accept _FREE at this point.
				; Not sure whether it is better to show _FREE fields that haven't been customised or just
				; leave them hidden.
				set idForm       = pidClass
				set intFormField = $order(^WWW122s(0,4,pintClassField,idForm,""))
				if intFormField'="" {
					set strDescription = $$^WWWFELDNAME(idForm,"D",pintClassField)
				
				} else {  ; If not then let's just get something from the class.
					set idForm         = "*none*"
					set strDescription = $get(^WWW0031(0,pidClass,pintClassField,SPRACHE,1))
					if strDescription="" set strDescription = $$$WWW003PropertyDescription($get(^WWW003(0,pidClass,pintClassField,1)))
				}
			}
			;  First attempt.  A lot shorter but the code above does a more detailed search.
			;  Not sure if we do really want to go searching all over the place.
			;  
			;set intFormField=""
			;set intFormField=$order(^WWW122s(0,4,pintClassField,idForm,""))
			;if intFormField="" {
			;	set idForm=idClass
			;	set intFormField=$order(^WWW122s(0,4,pintClassField,idForm,""))
			;}
			;if intFormField'="" {
			;	set strDescription=$$^WWWFELDNAME(idForm,"D",pintClassField)
			;} else {
			;	; Get it from the class
			;	set strDescription=$get(^WWW0031(0,idClass,pintClassField,SPRACHE,1))
			;	if strDescription="" set strDescription=$$$WWW003PropertyDescription($get(^WWW003(0,idClass,pintClassField,1)))
			;}
		;}
		
		
	} elseif pstrFieldType="P" {                                               ; Case 2 (P)
		;Primary key.  Form field and classfield always the same
		if idForm'="" {
			set intFormField   = pintClassField
			set strDescription = $$^WWWFELDNAME(idForm,"P",pintClassField)
 
		}
	}
	if $$$COMViewConfigFieldDebug($get(^COMViewConfig(0,YM,1))) {
		; Some debugging information if we are trying to work out where the text came from
		set strDescription = idForm_"/"_pidClass_"("_$get(pstrFieldType)_":C"_$get(pintClassField)_":F"_$get(intFormField)_"):"_strDescription
	}
	
	quit strDescription


GetCOMViewHeaderColors(&COLOR,&COLORR)
	;-------------------------------------------------------------------------------
	; Get the Colors for the Header Line
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 03-Dec-2009	PPP		SR17069: Created
	;-------------------------------------------------------------------------------
	if $get(YVOR1) = "" set YVOR1 = $get(^WWW012(0,0,1))
	
	set COLOR  = $$$SysEnum("FARBE",$$$WWW012ColorCodeForHeaderLeft(YVOR1))
	if COLOR="" set COLOR = "DIMGRAY"
	
	set COLORR = $$$SysEnum("FARBE",$$$WWW012ColorCodeForHeaderRight(YVOR1))
	if COLORR="" set COLORR = COLOR

	if $get(YLOCATION)'="" {
		set objLocn = $get(^WWW0121(0,0,YLOCATION,1))
		if +$$$WWW0121ColorTableHeader(objLocn)'=0 { 
			set (COLOR,COLORR) 	= $$$SysEnum("FARBE",+$$$WWW0121ColorTableHeader(objLocn))			
		}
	}
	
	quit
	
	
GetClassForForm(pidForm="")
	;-------------------------------------------------------------------------------
	; Get the Colors for the Header Line
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 03-Dec-2009	PPP		SR17069: Created
	;-------------------------------------------------------------------------------
	set idClass=""
	
	if $get(pidForm)'="" {
		set idClass = $$$WWW120ClassUsedInForm($get(^WWW120(0,pidForm,1)))
	}
	
	quit idClass
	
]]></Routine>
</Export>