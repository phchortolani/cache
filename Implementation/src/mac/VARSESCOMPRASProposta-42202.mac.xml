<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASProposta" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASProposta
#include COMSYS
 
OnAfterDataFields(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Mostra a tabela com os proponentes para cadastramento das propostas de cada
    ; item.
    ; 
    ; History:
    ; 18-Aug-2009   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
    quit:(pYKEY = "")
    
    new Fornecedor, objCompraLinha, Produto, chave, objProposta, textItem, dteProposta, chaveIndex,
        vlueTotal, dteValidade, beginStyle, endStyle, blnItemEncerrado, beginInactiveStyle, endInactiveStyle

	set beginStyle = "<font style='line-height:23px'>"
	set endStyle = "</font>"
 
    //Tabela
    new lstHeader
    set lstHeader = ""
    set lstHeader = lstHeader_$listbuild("Fornecedor / Produto","Data da Proposta","&nbsp;Valor Total","Prazo de Entrega","Válida Até","","")
    
    set chaveIndex = $$$Index(pYKEY)
    
    if $$Start^COMTable(lstHeader,"Proponentes Selecionados") {
        
		&sql(DECLARE Fornecedores CURSOR FOR

			 SELECT DISTINCT Fornecedor, $$SQLGetDescFornecedor^VARSQL(Fornecedor) as descFornecedor
			   INTO :Fornecedor, :descFornecedor
			   FROM VARSESCOMPRASParticipantesLine
			  WHERE $UPPER(NoProcessodeCompra) = :pYKEY
		   ORDER BY descFornecedor

 			)
 			
 		&sql(OPEN Fornecedores )
 		&sql(FETCH Fornecedores )
		
 		while ('SQLCODE) {	 			
        
            do NewLine^COMTable("lightsteelblue")
            do InsertCell^COMTable(beginStyle_"<strong>"_$extract($$SQLGetDescFornecedor^VARSQL(Fornecedor),1,80)_" ("_Fornecedor_")</strong>"_endStyle,,,,,,7)
            do EndLine^COMTable()
            
            $$$Order6(^VARSESCOMPRASParticipantesLines,YM,1,chaveIndex,Fornecedor,pYKEY,Linha)
 				
                set objCompraLinha = $get(^VARSESCOMPRASLinha(YM,pYKEY,Linha,1))
                set Produto = $piece(objCompraLinha,Y,2)
                
                set chave = pYKEY_YKOMMA_Linha_YKOMMA_Fornecedor
                
                set objProposta = $get(^VARSESCOMPRASPropostaLinha(YM,pYKEY,Linha,Fornecedor,1))
                
                if (objProposta '= "") {
	                 if ($$isPropostaZerada(objProposta) = $$$YES) {
	                 	set textItem = "Alterar"
	                 } else {
		                set textItem = "Ver Proposta Completa do Item"
	                 }	                 
                
                } elseif (objProposta  = "") {
	                set textItem = "Cadastrar Proposta para esse Item"
                }

				set strBotao = "<img src="_YGIF_"nopps.gif TITLE='Clique aqui caso o fornecedor tenha sido contactado, mas não tenha apresentado nenhuma proposta para este item.' border=0 align=""CENTER"">"

                set dteProposta  = $$GetDataProposta(objProposta)
                set dteValidade  = $$GetDataValidadeProposta(objProposta)
                set prazoEntrega = $$GetPrazoEntregaProposta(objProposta)
                
                if (prazoEntrega = 0) {
                    set prazoEntrega = "Imediata"
                } elseif (prazoEntrega > 0) {
                    set prazoEntrega = prazoEntrega_" dia(s)"
                }	
                
                set vlueTotal = $piece(objProposta,Y,18)
                
                
                set blnItemEncerrado = $$isItemEncerrado^VARSESCOMPRASPDPASLinha(pYKEY,Linha)
                
                if (blnItemEncerrado '= $$$YES) {
                	
               		do NewLine^COMTable("white")
                	do InsertCell^COMTable(beginStyle_"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Item "_Linha_" - Produto: "_Produto_" - "_$extract($$SQLGetDescricaoProduto^VARSQL(Produto),1,40)_endStyle,"VARSESCOMPRASPropostaLinha",chave)
				
					if (objProposta = "") {
						do InsertCell^COMTable("")
						do InsertCell^COMTable("")
						do InsertCell^COMTable("")
						do InsertCell^COMTable("")
					
					} elseif ($$isPropostaZerada(objProposta) = $$$YES) && $data(^VARSESCOMPRASPropostaLinha(YM,pYKEY,Linha,Fornecedor)) {
	                	do InsertCell^COMTable(beginStyle_"<font color=gray>Fornecedor foi contactado, mas não apresentou proposta para o item</font>"_endStyle,,,,"RIGHT",,4)
                	
                	} else {
                	
                 		do InsertCell^COMTable(beginStyle_dteProposta_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"CENTER")
                		do InsertCell^COMTable(beginStyle_$$FormatCurrency^COMTable(vlueTotal)_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"RIGHT")
                		do InsertCell^COMTable(beginStyle_prazoEntrega_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"CENTER")
                		do InsertCell^COMTable(beginStyle_dteValidade_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"CENTER")
                	}
                	                	
                	do InsertCell^COMTable(beginStyle_textItem_endStyle,"VARSESCOMPRASPropostaLinha",chave)
                	
                	if '$data(^VARSESCOMPRASPropostaLinha(YM,pYKEY,Linha,Fornecedor)) {	                	
	                	if '$$GetVencedorFornecedor^VARSESCOMPRASParecer(pYKEY,Linha) {	                	               
                			do InsertCellCallback^COMTable(strBotao,"ZerarProposta^VARSESCOMPRASProposta",chave)
	                	 } else {
	                		do InsertCell^COMTable("")
                		}	                	
                	} else {
	                	do InsertCell^COMTable("")
                	}
	                	
                	do EndLine^COMTable()
                
                } else {
	                
	                set beginInactiveStyle = "<font style=color:gray;text-decoration:line-through;>"
	                set endInactiveStyle = "</font>"
	                
                	do NewLine^COMTable("white")
                	do InsertCell^COMTable(beginInactiveStyle_"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Item "_Linha_" - Produto: "_Produto_" - "_$extract($$SQLGetDescricaoProduto^VARSQL(Produto),1,40)_endInactiveStyle,,)
                	do InsertCell^COMTable(beginInactiveStyle_dteProposta_endInactiveStyle,,,,"CENTER")
                	do InsertCell^COMTable(beginInactiveStyle_$$FormatCurrency^COMTable(vlueTotal)_endInactiveStyle,,,,"RIGHT")
                	do InsertCell^COMTable(beginInactiveStyle_prazoEntrega_endInactiveStyle,,,,"CENTER")
                	do InsertCell^COMTable(beginInactiveStyle_dteValidade_endInactiveStyle,,,,"CENTER")
                	do InsertCell^COMTable("<font color=gray>Item encerrado</font>",,)
                	do InsertCell^COMTable("")
                	do EndLine^COMTable()	                
                }
				            
            $$$End
            
			do NewLine^COMTable("#E0E0E0")
        	do InsertCell^COMTable("",,,,,,7)
        	do EndLine^COMTable()           
            
	 		&sql(FETCH Fornecedores )
 		}
 		
 		&sql(CLOSE Fornecedores )
        
        do Stop^COMTable()
    }   
    
    quit

OnBeforeSave(pYKEY,&pYFELD)
    ;-------------------------------------------------------------------------------
    ; Calcula o preço total da proposta do fornecedor.
    ; 
    ; History:
    ; 29-Apr-2011   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
 	
	new custosImportacao, quantidade, precoUnitario, precoTotal
	
	set custosImportacao = $piece(pYFELD,Y,5)
	set quantidade       = $piece(pYFELD,Y,16)
	set precoUnitario    = $piece(YFELD,Y,17)

	set precoTotal = (quantidade * precoUnitario) + custosImportacao

	set $piece(pYFELD,Y,18) = precoTotal

	quit

 
OnAfterSaveHook(pYKEY)
    ;-------------------------------------------------------------------------------
    ; OnAfterSaveHook da classe VARSESCOMPRASPropostaLinha
    ; 
    ; History:
    ; 02-Oct-2009   Gustavo Fiuza       Changed
    ; 28-Aug-2009   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
    if pYKEY = "" quit $$$YES
    
    new strStatus, NoCompra, objCompra, PosicaoProcesso
    
    set strStatus = $$$OK
    set NoCompra  = $$$KEY1(pYKEY)
    set objCompra = $get(^VARSESCOMPRAS(YM,NoCompra,1))         
    
    set PosicaoProcesso = $piece(objCompra,Y,7)
    
    ;PositionHere -> 7 - Aguardando Cadastrar Propostas
    if (PosicaoProcesso = 7) {              
        set strStatus = $$AtualizaAguardandoPropostas(NoCompra,objCompra)
    }
    
    elseif (PosicaoProcesso > 7) {
        quit $$$YES ;Se já passou da posição 8, quit
    }
 
    quit strStatus
 
 
AtualizaAguardandoPropostas(pNoCompra,pobjCompra)
    ;-------------------------------------------------------------------------------
    ; Atualiza a posição do processo caso todos os itens da compra já tenham pelo
    ; menos uma proposta cadastrada para eles.
    ; 
    ; History:
    ; 30-Apr-2011	Gustavo Fiuza		Updated - Lógica de encerramento
    ; 29-Aug-2009   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
    new blnAtualizaProcesso, strStatus, idLinha
    
    set strStatus = $$$OK   
    set blnAtualizaProcesso = $$$NO
    
    $$$Order3(^VARSESCOMPRASLinha,YM,pNoCompra,idLinha)
         
 		//Se o item estiver encerrado, não considera como critério para atualizar o processo
 		continue:($$isItemEncerrado^VARSESCOMPRASPDPASLinha(pNoCompra,idLinha) = $$$YES)
         
        if $data(^VARSESCOMPRASPropostaLinha(YM,pNoCompra,idLinha)) > 0 {
                set blnAtualizaProcesso = $$$YES
            
            } elseif $data(^VARSESCOMPRASPropostaLinha(YM,pNoCompra,idLinha)) = 0 {
                set blnAtualizaProcesso = $$$NO
                quit
            }
    $$$End
        
    if blnAtualizaProcesso = $$$YES {
        ;PositionHere -> 8 - Aguardando Parecer de Propostas
        set $piece(pobjCompra,Y,7) = 8
        set strStatus = $$$Save("VARSESCOMPRAS",pNoCompra,pobjCompra,1)         
    }
 
    quit strStatus
    
GetDataProposta(pobjProposta)
    new dteProposta, dtePropostaShow
    
    set dteProposta = $piece(pobjProposta,Y,2)
        
    if (dteProposta > 0) {
        set dtePropostaShow = $zdate(dteProposta,4)
    } else { 
        set dtePropostaShow = ""
    }
    
    quit dtePropostaShow
    
GetDataValidadeProposta(pobjProposta)
    new dteProposta, qtyValidade, dteValidade, dteValidadeShow
 
    set dteProposta = $piece(pobjProposta,Y,2)
    set qtyValidade = $piece(pobjProposta,Y,3)
    
    set dteValidade = dteProposta + qtyValidade

    //Valores com decimais não são permitidos
    if ($length($piece(dteValidade,".",2)) > 0) {	   
	    quit "Valor inválido"
    }
        
    if (dteValidade > 0) {
        set dteValidadeShow = $zdate(dteValidade,4)
    } else { 
        set dteValidadeShow = ""
    }
 
    quit dteValidadeShow
    
GetPrazoEntregaProposta(pobjProposta)
    new prazoEntrega 
    set prazoEntrega = $piece(pobjProposta,Y,27)
 
    quit prazoEntrega
 
OnBeforeEditAccessHook(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Verifica se o usuário tem permissão ou não para modificar os dados.
    ; 
    ; History:
    ; 30-Oct-2009   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
    if pYKEY = "" quit $$$YES
    
    new isReadOnly      
    set isReadOnly = $$CheckProcessoReadOnly^VARSESCOMPRAS($$$KEY1(pYKEY))      
    if isReadOnly = $$$YES quit $$$NO
 
    quit $$$YES
    
isFornecedorComProposta(pNoProcessoCompra,pidFornecedor)
    ;-------------------------------------------------------------------------------
    ; Verifica se o fornecedor tem proposta cadastrada para algum item do processo de compra
    ; 
    ; History:
    ; 30-Apr-2011   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
    quit:( (pNoProcessoCompra = "") || (pidFornecedor = "") ) ""

	new noPropostas

	&sql( SELECT count(Item)
		    INTO :noPropostas
		    FROM VARSESCOMPRASPropostaLinha
		   WHERE $UPPER(NoProcessodeCompra) = :pNoProcessoCompra
		     AND $UPPER(Fornecedor) = :pidFornecedor
	)
	
	quit:(noPropostas > 0) $$$YES
    quit $$$NO
    
    
ZerarProposta(idProcessoCompra,idItem,idFornecedor)
    ;-------------------------------------------------------------------------------
    ; Zera a proposta do fornecedor (prática que os usuários estão fazendo manualmente)
    ; 
    ; History:
    ; 09-May-2011   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
	quit:(idProcessoCompra = "")
	quit:(idItem = "")
	quit:(idFornecedor = "")
	
	new strStatus, objProposta, chave
	
	set strStatus = $$$OK	
	
	set objProposta = ""
	set $piece(objProposta,Y,5)  = 0
	set $piece(objProposta,Y,11) = $$GetCodigoItem^VARSESCOMPRAS(idProcessoCompra,idItem)
	set $piece(objProposta,Y,12) = $$GetDescricaoCompletaItem^VARSESCOMPRAS(idProcessoCompra,idItem)
	set $piece(objProposta,Y,13) = $$GetUnidadeItem^VARSESCOMPRAS(idProcessoCompra,idItem)
	set $piece(objProposta,Y,14) = $$GetGrupoItem^VARSESCOMPRAS(idProcessoCompra,idItem)
	set $piece(objProposta,Y,15) = $$GetQuantidadeSolicitada^VARSESCOMPRAS(idProcessoCompra,idItem)
	set $piece(objProposta,Y,16) = 0
	set $piece(objProposta,Y,17) = 0
	set $piece(objProposta,Y,18) = 0
	
	set chave = idProcessoCompra_$$$COMMA_idItem_$$$COMMA_idFornecedor
	
	set strStatus = $$$Save("VARSESCOMPRASPropostaLinha",chave,objProposta,1)       
	
	if $$$ISERR(strStatus) {		
		do ReturnError^COMUtilError(strStatus)		
	}	
	
	set YKEY = idProcessoCompra
	do RefreshForm^COMUtilForm()
	
	quit
	
	
isPropostaZerada(pobjProposta)
    ;-------------------------------------------------------------------------------
    ; Verifica se a proposta do fornecedor está zerada. O critério para definir uma
    ; proposta como zerada é aquela em que a quantidade OU o preço forem igual a zero.
    ; 
    ; History:
    ; 09-May-2011   Gustavo Fiuza       Created
    ;-------------------------------------------------------------------------------
	quit:(pobjProposta = "") $$$YES
	
	new blnPropostaZerada, qtde, preco
	set blnPropostaZerada = $$$NO
	
	set qtde  = $piece(objProposta,Y,16)
	set preco = $piece(objProposta,Y,17)
	
	if ( (qtde <= 0) || (preco <= 0) ) {
		set blnPropostaZerada = $$$YES
	}
	
	quit blnPropostaZerada
]]></Routine>
</Export>