<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARManipulacaoMagistral" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARManipulacaoMagistral
	#include MEDConst
	#include VARConst
	#include INConst
	#include COMSYS

IsLessThanZero(&YFELD)
	quit $$$VARManipulacaoMagistralQuantity(YFELD)'>0 ;HEVA-1148.25

ItemsExist(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Do any items exist for this product in the VARProdutoManipulados
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new blnResult
	
	set blnResult=$$$NO

	if $$$VARManipulacaoMagistralManipulatedItem(YFELD) {
		set blnResult= $data(^VARProdutoManipulados(YM,1,$$$VARManipulacaoMagistralManipulatedItem(YFELD)))
	}
	quit blnResult

Exists(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new blnResult
	
	set blnResult=$$$OK
	if $$$VARManipulacaoMagistralItem(YFELD)'="" {
		set blnResult='$data(^INART(YM,$$$VARManipulacaoMagistralItem(YFELD),1))
	}
	quit blnResult
	

Process(YKEY)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 21-Oct-2013	shobby		HEVA-1148.18: Fix problem where it's required to press back button twice.
	; 19-Oct-2013	shobby		HEVA-1148.13: Check if still valid to process.
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus,YFELD
	
 	set YFELD=$get(^MARManipulacaoMagistral(YM,YKEY,1))
 	set strStatus = $$CanProcess()
	set strStatus = $$Transaction^COMTransaction("ProcessTxn^VARManipulacaoMagistral("""_YKEY_""")",$$$YES)
	if $$$ISOK(strStatus) do RefreshForm^COMUtilForm() ;HEVA-1148.18
	
	quit strStatus

ProcessTxn(YKEY)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 28-Nov-2013	shobby		CORE-290:  Call out to CreateAdmissionHistory
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus,objMagistral,idIssue,idReceipt,idHistoryLine
 
 	set strStatus=$$$OK
 	
	set objMagistral=$get(^VARManipulacaoMagistral(YM,YKEY,1))
	if objMagistral'="" {
		if $$$ISOK(strStatus) set strStatus=$$CreateIssue^VARManipulacaoMagistralProcess(YKEY,objMagistral,.idIssue)
		if $$$ISOK(strStatus) set strStatus=$$UpdatePickPrinted^INIssuePrint(idIssue)	
		if $$$ISOK(strStatus) set strStatus=$$PostTxn^INIssueAuto(idIssue)
		if $$$ISOK(strStatus) set strStatus=$$CreateReceiptData^VARManipulacaoMagistralProcess(idIssue,.idReceipt)
		if $$$ISOK(strStatus) set strStatus=$$PostTxn^INMOReceipt(idReceipt)		

		if $$$ISOK(strStatus) set strStatus=$$CreateAdmissionHistory(objMagistral,.idHistoryLine) ;CORE-290

		if $$$ISOK(strStatus) {
			set $$$VARManipulacaoMagistralStatus(objMagistral)=3 ;Processed
			set $$$VARManipulacaoMagistralProcessed(objMagistral)=$horolog
			set $$$VARManipulacaoMagistralIssue(objMagistral)=idIssue
			set $$$VARManipulacaoMagistralReceipt(objMagistral)=idReceipt
			set $$$VARManipulacaoMagistralAdmissionLine(objMagistral)=idHistoryLine
			set strStatus=$$Save^COMUtils("VARManipulacaoMagistral",YKEY,objMagistral,1)
		}
		if $$$ISOK(strStatus) set strStatus=$$UpdateDispenseStatus(objMagistral,2) ;StatusManipulada
	}	
	
	quit strStatus


CreateAdmissionHistory(pobjMagistral,pidHistoryLine="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 28-Nov-2013	shobby		CORE-290: Created
	;-------------------------------------------------------------------------------
	new strStatus,objHistory,idAdmission,objAdmission,objPatient,idPatient
	new idMEDAdmissionHistory
	
	set strStatus=$$$OK
	
	set idAdmission=$$$VARManipulacaoMagistralAdmission(pobjMagistral)
	if idAdmission'="" {
		set objAdmission=$get(^MEDAdmission(YM,idAdmission,1))
		set objHistory=""
		set $$$MEDAdmissionHistoryPatient(objHistory)		= $$$MEDAdmissionPatient(objAdmission)
		set $$$MEDAdmissionHistoryLocation(objHistory)		= $$$MEDAdmissionLocation(objAdmission)
		set $$$MEDAdmissionHistoryRoom(objHistory)			= $$$MEDAdmissionRoom(objAdmission)
		set $$$MEDAdmissionHistoryBed(objHistory)			= $$$MEDAdmissionBed(objAdmission)
		set $$$MEDAdmissionHistoryReason(objHistory)		= $$$MEDAdmissionReason(objAdmission)
		set $$$MEDAdmissionHistoryDateAdmitted(objHistory)	= $$$MEDAdmissionDateAdmitted(objAdmission)
		set $$$MEDAdmissionHistoryTimeAdmitted(objHistory)	= $$$MEDAdmissionTimeAdmitted(objAdmission)
		set $$$MEDAdmissionHistoryDateDischarged(objHistory)= $$$MEDAdmissionDateDischarged(objAdmission)
		set $$$MEDAdmissionHistoryTimeDischarged(objHistory)= $$$MEDAdmissionTimeDischarged(objAdmission)
		
		set idPatient=$$$MEDAdmissionPatient(objAdmission)
		if idPatient'="" {
			set objPatient=$get(^MEDPatient(YM,idPatient,1))
			set $$$MEDAdmissionHistoryDOB(objHistory)			= $$$MEDPatientDOB(objPatient)
			set $$$MEDAdmissionHistoryAllergies(objHistory)		= $$$MEDPatientAllergies(objPatient)
			
			set $$$MEDAdmissionHistoryName(objHistory)			= $$$MEDPatientName(objPatient)
			set $$$MEDAdmissionHistoryOtherNames(objHistory)	= $$$MEDPatientOtherNames(objPatient)
			set $$$MEDAdmissionHistorySurname(objHistory)		= $$$MEDPatientSurname(objPatient)
			set $$$MEDAdmissionHistoryGender(objHistory)		= $$$MEDPatientGender(objPatient)
		}
		set pidHistoryLine=($order(^MEDAdmissionHistory(YM,idAdmission,""),-1)+1)
		set idMEDAdmissionHistory=idAdmission_","_pidHistoryLine
		set strStatus=$$Save^COMUtils("MEDAdmissionHistory",idMEDAdmissionHistory,objHistory,1)

	}
	quit strStatus


UpdateDispenseStatus(pobjMagistral,pintStatus)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus,objDispenseLine,idDispense,idDispenseLine

	set idDispense=$$$VARManipulacaoMagistralDispense(pobjMagistral)
	set idDispenseLine=$$$VARManipulacaoMagistralDispenseLine(pobjMagistral)
	set strStatus=$$$OK
	
	if (idDispense'="")&&(idDispenseLine'="") {
		set objDispenseLine=$get(^MEDDispenseLine(YM,idDispense,idDispenseLine,1))
		set $$$MEDDispenseLineFREE6(objDispenseLine)=pintStatus
		set strStatus=$$Save^COMUtils("MEDDispenseLine",idDispense_","_idDispenseLine,objDispenseLine,1)
	}	
	quit strStatus
	
	
CanProcess()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Oct-2013	shobby		HEVA-1148.13: Check quantities on lines before processing.
	; 18-Oct-2013	shobby		HEVA-1148.10: Check for lines before allowing process.
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$OK
	
	if ($$$VARManipulacaoMagistralStatus(YFELD)'=2)||(YKEY="")||('$data(^VARManipulacaoMagistralLine(YM,YKEY))) {
		set strStatus=$$$MakeStatus("NÃ£o pode processar.")
	}
	if $$$ISOK(strStatus) {
		set strStatus=$$CheckQuantities^VARManipulacaoMagistralLine(YKEY)
	}

	$$$YQHandler(strStatus)
	quit strStatus



CanShowTicket(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	if $$$VARManipulacaoMagistralStatus(YFELD)'=3 { ;Processed
		set strStatus=$$$MakeStatus($$$Text(10118)) ;$$$Text(10118)) ;Process Record before printing.
		$$$YQHandler(strStatus)
	}
	quit
	
GetCostCentre(pYFELD)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new idCC,idLocn
	
	set idCC=""
	set idLocn=$$$VARManipulacaoMagistralDestinationLocation(YFELD)
	if idLocn'="" {
		set idCC=$$$WWW0121FREE3(^WWW0121(0,YM,idLocn,1))
		;set idCC=$$GetCostCentre^INCostCentre(pidLocn)
	}
	quit idCC
	

OnBeforeSave()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-Nov-2014	SCR			ALPHAUP-393: Set authorization for new product
	; 27-Nov-2013	shobby		HEVA-1148.42: Correct a problem with new id's when greater than 10
	; 21-Nov-2013	shobby		HEVA-1148.25: Don't allow saving of quantity less than 0
	; 21-Oct-2013	shobby		HEVA-1148.21: Now a new record is created when field is blank not +
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new idProdutoManipulado,objProdutoManipulado,strStatus,strCode

	set strStatus=$$$OK
	
	if $$$VARManipulacaoMagistralQuantity(YFELD)'>0 {
		set strStatus=$$$MakeStatus("INMOV07")  ;HEVA-1148.25
	}
	
	if $$$ISOK(strStatus) {
		if $$$VARManipulacaoMagistralStatus(YFELD)=1 { ;Open
			set $$$VARManipulacaoMagistralStatus(YFELD)=2 ;In Process
		}
		if YKEY'="" {
			if $$$VARManipulacaoMagistralItem(YFELD)="" { ;HEVA-1148.21
				set strCode=$$$VARManipulacaoMagistralManipulatedItem(YFELD)
				;set strCode=$order(^VARProdutoManipulados(YM,1,strCode,""),-1)					  ;HEVA-1148.42
				set strCode=$order(^VARProdutoManipulado(YM,strCode,""),-1)					      ;HEVA-1148.42
				if strCode="" {
					set strCode=$$$VARManipulacaoMagistralManipulatedItem(YFELD)_"-M1"
				} else {
					set strCode=$$$VARManipulacaoMagistralManipulatedItem(YFELD)_"-M"_(strCode+1) ;HEVA-1148.42
				}
				set $$$VARProdutoManipuladoDescricao(objProdutoManipulado)=$$$VARManipulacaoMagistralItemDescription(YFELD)
				set $$$VARProdutoManipuladoUnidade(objProdutoManipulado)=$$$VARManipulacaoMagistralUnit(YFELD)
				set $$$VARProdutoManipuladoQtyConcentracao(objProdutoManipulado)=$$$VARManipulacaoMagistralConcentrationQuantity(YFELD)
				set $$$VARProdutoManipuladoUnConcentracao(objProdutoManipulado)=$$$VARManipulacaoMagistralConcentrationUnit(YFELD)
				set $$$VARProdutoManipuladoCodigoManipulado(objProdutoManipulado)=strCode
				set idProdutoManipulado=$$$VARManipulacaoMagistralManipulatedItem(YFELD)_","_(($order(^VARProdutoManipulado(YM,$$$VARManipulacaoMagistralManipulatedItem(YFELD),""),-1)+1))
				set strStatus=$$Save^COMUtils("VARProdutoManipulado",idProdutoManipulado,objProdutoManipulado,1)
				if $$$ISOK(strStatus) {
					set $$$VARManipulacaoMagistralItem(YFELD)=strCode
					set strStatus=$$OnBeforeSave1^VARProdutoManipulado(idProdutoManipulado,objProdutoManipulado)
				}
				if $$$ISOK(strStatus) {
					set $$$KEY1(strAuthKey)	= $$$VARManipulacaoMagistralProductionLocation(YFELD)
					set $$$KEY2(strAuthKey)	= strCode
					set strAuthRec			= YBED_Y_$H
					set strStatus=$$Save^COMUtils("VARAutorizacaoProduto",strAuthKey,strAuthRec,1)
				}

				set ^CacheTempSCR($i(^CacheTempSCR))=" strStatus = "_strStatus_" strCode = "_strCode
				$$$YQHandler(strStatus)
			}
		}
	}
	if $$$ISOK(strStatus) {
		if $$$VARManipulacaoMagistralExpiryDate(YFELD)<$h {
			set strStatus	= $$$MakeStatus("VAR8896")  ;Date must be Today or More
		}
	}
	quit strStatus

OnBeforeDeletePossibility()
	if $$$VARManipulacaoMagistralStatus(YFELD)=3 set Q=$$$QDontDelete
	quit Q

OnAfterFormConstruction(YBBN="",YKEY="",YFELD="",strValue="")
	;-------------------------------------------------------------------------------
	; Popup a window to the dispense.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 18-Oct-2013	shobby		HEVA-1148.9: Created
	;-------------------------------------------------------------------------------
	new HREF
	
	if YBBN=4 {
		set strValue=$$$VARManipulacaoMagistralDispense(YFELD)
		
		new YKEY,YPARA
		set YKEY=strValue
		set YPARA=strValue
		set HREF="subWindow(""www.cls?EP=WWWFORM&amp;YFORM=MEDDispense"_$$WWWCGI2^WWWCGI($$$YES)_"""); return false;"
		write "<a HREF='' class=link onclick='"_HREF_"' style='color:blue; font-size:14px;  text-decoration:underline; margin-top:2px;'>"
		write $$$VARManipulacaoMagistralDispense(YFELD)
		write "</a>"
	}
	quit 1

OnBeforeDelete()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$Transaction^COMTransaction("OnBeforeDeleteTxn^VARManipulacaoMagistral("""_YFELD_""")",$$$YES)
	quit strStatus

OnBeforeDeleteTxn(YFELD)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	set strStatus=$$UpdateDispenseStatus(YFELD,1)
	if $$$ISOK(strStatus) set strStatus=$$DeleteIssue($$$VARManipulacaoMagistralIssue(YFELD))
	
	quit strStatus

DeleteIssue(pidIssue="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus,objINIssue
	
	set strStatus=$$$OK
	
	if pidIssue'="" {
		set objINIssue=$get(^INIssue(YM,pidIssue,1))
		if objINIssue'="" {
			if '$$$INIssueStatus(objINIssue)=1 { ;Open / Em aberto
				set strStatus=$$$MakeStatus("Unable to delete Issue!")
			} else {
				set strStatus=$$KILL^COMUtils("INIssue",pidIssue)
			}
		}
	}
	
	quit strStatus




	
OnBlur(YKEY,&YFELD,YBBN,YINHALT="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 21-Oct-2013	shobby		HEVA-1148.21: Changes now that Produto Manipulado is now a select.
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new objManipulado,idManipulado,idINART

	if YBBN=14 {
		set $$$VARManipulacaoMagistralItemSelected(YFELD)=0
		set idINART=$piece(YINHALT,"-",1)
		if idINART'="" {
			if $data(^VARProdutoManipulados(YM,1,idINART,YINHALT)) {
				set idManipulado=$order(^VARProdutoManipulados(YM,1,idINART,YINHALT,idINART,""))
				set objManipulado=$get(^VARProdutoManipulado(YM,idINART,idManipulado,1))
				set $$$VARManipulacaoMagistralItemSelected(YFELD)=1
				set $$$VARManipulacaoMagistralUnit(YFELD)=$$$VARProdutoManipuladoUnidade(objManipulado)
				set $$$VARManipulacaoMagistralConcentrationQuantity(YFELD)=$$$VARProdutoManipuladoQtyConcentracao(objManipulado)
				set $$$VARManipulacaoMagistralConcentrationUnit(YFELD)=$$$VARProdutoManipuladoUnConcentracao(objManipulado)
			}
		}
	} elseif YBBN=20 {
		if $$$VARManipulacaoMagistralExpiryDate(YFELD)<$h {
			;set $$$VARManipulacaoMagistralExpiryDate(YFELD)=""
			$$$Alert($$$Text("VAR8896"))  ;Date must be Today or More
			;set strStatus	= "VAR8896"  ;Date must be Today or More
		}
	}
	quit
	
Load(pidDispense,pidLine)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new arrParams
	
	set arrParams("YDISPENSACAO")=pidDispense
	set arrParams("YMAGISTRALDISPENSELINE")=pidLine

	do GoToForm^COMUtilForm("VARManipulacaoMagistral","","",pidDispense,,,,,.arrParams)
	quit

OnAfterPrimaryKey()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 30-Jun-2014	SCR			HEVA-1509: Remove Quotes & Commas from the Description
	; 23-Jun-2014	SCR			HEVA-1509: populate Item description
	; 17-Oct-2013	shobby		HEVA-1148.2: Corrected a problem where cost centre would not show.
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new idDispense,idPrescription,objDispense,objPrescription,idPatient,idAdmission,objAdmission
	new objLocation,idDispenseLine,objDispenseLine,strDesc
	
	set idDispense=$$$VARManipulacaoMagistralDispense(YFELD)
	set idDispenseLine=$$$VARManipulacaoMagistralDispenseLine(YFELD)
	
	if idDispense="" {
		set idDispense=$get(%("%KEY","YDISPENSACAO"))
		if idDispense'="" {
			set $$$VARManipulacaoMagistralDispense(YFELD)=idDispense
			set objDispense=$get(^MEDDispense(YM,idDispense,1))
			set idPrescription=$$$MEDDispensePrescription(objDispense)
			if idPrescription'="" {
				set objPrescription=$get(^MEDPrescription(YM,idPrescription,1))
				set idPatient=$$$MEDPrescriptionPatientID(objPrescription)
				set idAdmission=$$GetOpenAdmission^MEDAdmission(idPatient)
				if $$$VARManipulacaoMagistralPatient(YFELD)="" {
					set VMAGISTRALPATIENT=idPatient
					;set ^CacheTempMagistral(YUSER,"VMAGISTRALPATIENT")=idPatient
					set $$$VARManipulacaoMagistralPatient(YFELD)=idPatient
				}
				if idAdmission'="" {
					set objAdmission=$get(^MEDAdmission(YM,idAdmission,1))
					set VMAGISTRALLOCAL=$$$MEDAdmissionLocation(objAdmission)
					set $$$VARManipulacaoMagistralAdmission(YFELD)=idAdmission
					set $$$VARManipulacaoMagistralDestinationLocation(YFELD)=VMAGISTRALLOCAL ;HEVA-1148.2
				}
			}
		}		
	}
	if idDispenseLine="" {	
		set idDispenseLine=$get(%("%KEY","YMAGISTRALDISPENSELINE"))
		set $$$VARManipulacaoMagistralDispenseLine(YFELD)=idDispenseLine
	}
	
	if idDispenseLine'="" {
		set VMAGISTRALDISPENSELINE=idDispenseLine
		;set $$$VARManipulacaoMagistralDispenseLine(YFELD)=idDispense	
		set $$$VARManipulacaoMagistralDispenseLine(YFELD)=idDispenseLine	
		set objDispenseLine=$get(^MEDDispenseLine(YM,idDispense,idDispenseLine,1))
		set $piece(YMFELD,Y,8)=$$$MEDDispenseLineItemName(objDispenseLine)_" - "_$get(^INART(YM,$$$MEDDispenseLineItemName(objDispenseLine),1))
		set $$$VARManipulacaoMagistralManipulatedItem(YFELD)=$$$MEDDispenseLineItemName(objDispenseLine)
		set $piece(YMFELD,Y,11)=$$$MEDDispenseLineDoseQuantity(objDispenseLine)_" "_$piece($$$AppEnum("EINHEIT",$$$MEDDispenseLineDoseUOM(objDispenseLine))," - ")
		set $piece(YMFELD,Y,12)=$get(^MEDFrequency(YM,$$$MEDDispenseLineFrequency(objDispenseLine),1))
		set $piece(YMFELD,Y,13)=$$GetParamDesc^INPARA("MEDROUTEADMIN",$$$MEDDispenseLineRoute(objDispenseLine))
		set idItem	= $$$MEDDispenseLineItemName(objDispenseLine)
		if idItem'="" {
			set objItem	= $get(^INART(YM,idItem,1))
			set strDesc	= $$$INARTSearchName(objItem)
			set strDesc	= $tr(strDesc,""",","")
			set $$$VARManipulacaoMagistralItemDescription(YFELD)	= strDesc
		}
		
	}
	set $piece(YMFELD,Y,6)=$$GetCostCentre(YFELD)
	
	if $$$VARManipulacaoMagistralStatus(YFELD)=3 { ;Processed
		set $piece(YVOR,Y,23)=5
	}
	set ^WWWDATEN(YM,+$h,YUSER,YFORM,"D",1)=YFELD  ;This is a bit wrong but is necessary to be available for the Produto Manipulado combobox. OBDA
	quit

OnAfterDataField()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 20-Nov-2013	shobby		HEVA-1148.39: Get details from Admission.
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new idDispense,objDispense,idPrescription,objPrescription
	new objMagistral,objDispenseLine,idDispenseLine,strText
	
	set objMagistral=YFELD
	new YFELD ;Need YFELD to be changed to a Prescription for the Display function to work.
	set YFELD=""
	
	set idDispense=$$$VARManipulacaoMagistralDispense(objMagistral)
	if idDispense'="" {
		set $$$VARManipulacaoMagistralDispense(objMagistral)=idDispense
		set objDispense=$get(^MEDDispense(YM,idDispense,1))
		set idPrescription=$$$MEDDispensePrescription(objDispense)
		if idPrescription'="" {
			set YFELD=$get(^MEDPrescription(YM,idPrescription,1))
		}
		if $$$VARManipulacaoMagistralDispenseLine(objMagistral)'="" {
			set objDispenseLine=$get(^MEDDispenseLine(YM,idDispense,$$$VARManipulacaoMagistralDispenseLine(objMagistral),1))
		}
	}
	;for l=1:1:100 hang 1
	do Display^MEDPatientDetails($$$VARManipulacaoMagistralPatient(objMagistral),1100,$$$VARManipulacaoMagistralAdmission(objMagistral),$$$VARManipulacaoMagistralAdmissionLine(objMagistral)) ;HEVA-1148.39 ;CORE-290
	
	;set strText="Medicamento: "_$$$MEDDispenseLineItemName(objDispenseLine)_" - "_$$$INARTSearchName(^INART(YM,$$$MEDDispenseLineItemName(objDispenseLine),1))
	;&html<
	;	<div style='width:100%; position:relative; font-weight:bold; color:red; font-size:10pt;'>
	;		<div style='left:100px; margin-left:100px;'>#(strText)#
	;		</div>
	;	</div>
	;>

	quit

OnBeforeFormConstruction()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 18-Oct-2013	shobby		HEVA-1148.8: Index needs to include the dispense line. 
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
 	new idDispense,idMagistral,idDispenseLine
	
	set idDispense		= $get(%("%KEY","YDISPENSACAO"))
	set idDispenseLine	= $get(%("%KEY","YMAGISTRALDISPENSELINE"))

	if (idDispense'="")&&(idDispenseLine'="") {
		set idMagistral=$order(^VARManipulacaoMagistrals(YM,1,$$$Index(idDispense),$$$Index(idDispenseLine),""))
		if idMagistral'="" set YKEY=idMagistral
	}
	quit

Editable(pidMagistral="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new objMagistral,blnResult
	
	set blnResult=$$$NO
	set objMagistral=$get(^VARManipulacaoMagistral(YM,pidMagistral,1))
	if objMagistral'="" {
		set blnResult=($$$VARManipulacaoMagistralStatus(objMagistral)'=3) ;Processed
	}
	quit blnResult
]]></Routine>
</Export>