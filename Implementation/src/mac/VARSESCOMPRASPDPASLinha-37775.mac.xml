<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASPDPASLinha" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASPDPASLinha
#include COMSYS
#include COMConst
#include VARSESConst
#include COMGridEdit31

OnBlurProduto(pYKEY,&YFELD,pGROW)
	quit:(YFELD = "")
	
	new idProduto, objProduto, estoquePrincipalCompra, estoquePrincipalProduto, idLocalEntrega, isPadronizado

	set idProduto = $piece(YFELD,Y,2)	
	if (idProduto = "") {
		set $piece(YFELD,Y,3)  = ""
		set $piece(YFELD,Y,4)  = ""
		set $piece(YFELD,Y,21) = ""
	
	} else {			
		set objProduto = $get(^INART(YM,idProduto,1))		
		set statusProduto = $piece(objProduto,Y,66)
		
		;Verificando o status do produto
		if (statusProduto '= 0) { ;0-Ativo		 
		 	w "javascript:alert('O produto "_idProduto_" não está ativo e portanto não é possível utilizá-lo. Favor verificar.');"
			do ClearLineFields
			quit	 
		}
		
		set estoquePrincipalProduto = $piece(objProduto,Y,345)
		set estoquePrincipalCompra  = $$GetEstoquePrincipal^VARSESCOMPRAS($$$KEY1(pYKEY))			
		
		;Verificando o estoque principal
		if (estoquePrincipalCompra '= estoquePrincipalProduto) {

			w "javascript:alert('O estoque principal do produto "_idProduto_" é diferente do estoque principal do local de entrega selecionado para esta compra.\n\n"
			w "Estoque principal do produto: "_estoquePrincipalProduto_"\nEstoque principal do local de entrega: "_estoquePrincipalCompra_"');"
			do ClearLineFields
			quit
		}
		
		//Função está errada no v3, substituindo
		set isPadronizado = $$GetPadrao^VARAlertaLinha(idProduto)
		
		;;Verificando se o produto é padronizado para o caso de Farmácia
		if ( (isPadronizado '= $$$YES) && (estoquePrincipalProduto = "FARMACIA") ) {
			w "javascript:alert('O produto "_idProduto_" não é padronizado. Só é possível inserir produtos de FARMÁCIA padronizados.');"
			do ClearLineFields
			quit
		}
		
		set strProdutos = $$ItensJaIncluidos()

		for idLine = 1:1:$length(strProdutos) {
			
			continue:(idLine = pGROW)

			if ($piece(strProdutos,Y,idLine) = idProduto) {
				w "javascript:alert('O produto "_idProduto_" já foi incluído em outra linha desta compra (linha "_idLine_").');"
				do ClearLineFields
				quit
			}
		}
		
		set idLocalEntrega = $$GetLocaldeEntrega^VARSESCOMPRAS($$$KEY1(pYKEY))
		
		;Verifica se o produto está autorizado para o local de entrega
		if '$data(^VARAutorizacaoProduto(YM,idLocalEntrega,idProduto)) {
			
			w "javascript:alert('O produto "_idProduto_" não está autorizado para o local de entrega definido para esta compra.\nFavor verificar.');"
			do ClearLineFields
			quit
		
		} else {			
			set $piece(YFELD,Y,3) = $$SQLGetGrupoItem^VARSQL(idProduto)
			set $piece(YFELD,Y,4) = $$SQLGetUnitMeasure^VARSQL(idProduto) 
			set $piece(YFELD,Y,21) = $$GetSaldoDisponivel(idProduto)
		}
	}
 
	quit
	
ClearLineFields
	set $piece(YFELD,Y,2)  = ""
	set $piece(YFELD,Y,3)  = ""
	set $piece(YFELD,Y,4)  = ""
	set $piece(YFELD,Y,5)  = ""
	set $piece(YFELD,Y,21) = ""	
	
	quit
	
GetSaldoDisponivel(pidProduto)
	;-------------------------------------------------------------------------------
    ; Verifica o quanto do produto foi comprado no ano corrente, não podendo extrapolar
    ; o valor máximo definido no cadastro de enquadramentos por modalidade.
    ; 
    ; History:
    ; 30-Mar-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pidProduto = "") ""
	
	new Modalidade, lmtMaximo, idCompra, idCompraLinha, criadoEm, anoCompra, anoCorrente, 
		precoCompra, totalComprado, saldoDisponivel
	
	; Modalidade 10-PDPAS
	; Enquadramento Legal 1
	set lmtMaximo = $$GetLimiteMaximoModalidade^VARSESCOMPRAS(10,1)
	
	set totalComprado = 0
	
	$$$Order4(^VARSESCOMPRASLinhas,YM,1,pidProduto,idCompra)
			
			;Verifica Modalidade
			set Modalidade = $$GetModalidade^VARSESCOMPRAS(idCompra)
			continue:(Modalidade '= 10)
			
			;Verifica Posição do Processo
			set statusProcesso = $$GetPosicao^VARSESCOMPRAS(idCompra)
			continue:(statusProcesso < 11) ;11-Processo de Finalizado
			
			;Verifica Unidade de Saúde do Local de compra, se é igual ao local de compra do usuário
			set localDeCompra = $piece($get(^VARSESCOMPRAS(YM,idCompra,1)),Y,35)
			
			;Resgata o código da unidade de saúde para validar
			set codUnidadeSaudeCompra  = $piece($get(^WWW0121(YM,YM,localDeCompra,1)),Y,64)
			set codUnidadeSaudeUsuario = $piece($get(^WWW0121(YM,YM,YLOCATION,1)),Y,64)
						
			continue:(codUnidadeSaudeCompra '= codUnidadeSaudeUsuario)
			
			;Verifica Período, se está dentro do mesmo ano
			set criadoEm = $piece($get(^VARSESCOMPRAS(YM,idCompra,1)),Y,30)
			
			set anoCompra  = $extract($zdate(criadoEm,8),1,4)
			set anoCorrente = $extract($zdate($horolog,8),1,4)
			continue:(anoCompra '= anoCorrente)
			
		$$$Order5(^VARSESCOMPRASLinhas,YM,1,pidProduto,idCompra,idCompraLinha)
			
			set precoCompra = $$GetVencedorPrecoTotal^VARSESCOMPRASParecer(idCompra,idCompraLinha)
			set totalComprado = totalComprado + precoCompra
			
		$$$End
	$$$End
	
	set saldoDisponivel = lmtMaximo - totalComprado
	
	;Envia alerta caso o saldo tenha sido extrapolado
	if (saldoDisponivel <= 0) {
		w "javascript:alert('Atenção, o saldo disponível do produto no ano já excedeu os limites da portaria do PDPAS. "
		w "O limite máximo de compra no ano estabelecido para esta modalidade é de R$ "_$$^WWWTR(0,8,lmtMaximo)_" e o total comprado até o momento é de R$ "_$$^WWWTR(0,8,totalComprado)_".');"
	}
	
	quit saldoDisponivel
	

isItemEncerrado(pNoProcessoCompra,pLinha)
	;-------------------------------------------------------------------------------
    ; Verifica se o item da compra está encerrado.
    ; 
    ; History:
    ; 30-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:( (pNoProcessoCompra = "") || (pLinha = "") ) ""
	
	new objCompraLinha, blnItemEncerrado
	set objCompraLinha = $get(^VARSESCOMPRASLinha(YM,pNoProcessoCompra,pLinha,1))
	
	set blnItemEncerrado = $$$NO
	set blnItemEncerrado = $piece(objCompraLinha,Y,16)
	
	if (blnItemEncerrado = "") set blnItemEncerrado = $$$NO
	
	quit blnItemEncerrado
	
GetSituacaoItem(pNoProcessoCompra,pLinha)
	;-------------------------------------------------------------------------------
    ; Retorna a situação do item, se está encerrado ou não, e qual o motivo.
    ; 
    ; History:
    ; 30-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:( (pNoProcessoCompra = "") || (pLinha = "") ) ""	
	quit:('$data(^VARSESCOMPRASLinha(YM,pNoProcessoCompra,pLinha))) ""
	
	new blnItemEncerrado, objCompraLinha, motivo, strMotivo
		
	set blnItemEncerrado = $$isItemEncerrado(pNoProcessoCompra,pLinha)
	
	if (blnItemEncerrado = $$$YES) {
		set objCompraLinha = $get(^VARSESCOMPRASLinha(YM,pNoProcessoCompra,pLinha,1))	
		set motivo = $piece(objCompraLinha,Y,17)
		set strMotivo = "&nbsp;"_$$GetMotivoDescricao^VARSESCOMPRASEncerrar(motivo)
		
	} else {
		
		set idFornecedorVencedor = $$GetVencedorFornecedor^VARSESCOMPRASParecer(pNoProcessoCompra,pLinha)
		if (idFornecedorVencedor '= "") {				
			set strMotivo = "&nbsp;Vencedor Definido"
		
		} else {
			set strMotivo = "&nbsp;Em Andamento"
		}
	}
	
	quit strMotivo
	
	
GetQuantidadeRecebida(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Retorna a quantidade que já foi recebida do processo / item / fornecedor.
    ;
    ; History:
    ; 01-May-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "") ""
	
	new NoProcessoCompra, Item, Fornecedor, idProduto, fltQuantRecebida

	set NoProcessoCompra = $$$KEY1(pYKEY)
	set Item = $$$KEY2(pYKEY)
	set Fornecedor 	= $$$KEY3(pYKEY)
		
	quit:( (NoProcessoCompra = "") || (Item = "") || (Fornecedor = "") ) ""
	
	set idProduto = $$GetCodigoItem^VARSESCOMPRAS(NoProcessoCompra,Item)
	
	set fltQuantRecebida = ""
	set fltQuantRecebida = $$QuantRecebimento^VARRecRegional(NoProcessoCompra,Fornecedor,idProduto)
	
	quit fltQuantRecebida
	
	
OnDisplayRightClickMenu(pYKEY="",pYFELD="",pYFORM="")
    ;-------------------------------------------------------------------------------
    ; Clique com o botão direito customizado para criar um atalho que leva o usuário
    ; a uma tela de alteração de quantidade na compra.
    ;
    ; Returns:
    ;
    ; History:
    ; 01-May-2011	Gustavo Fiuza		Created (copied from FINAPInvLineEvents)
    ;-------------------------------------------------------------------------------
	#define AddToMenuList(%1) set lstNewMenuItems = lstNewMenuItems_$listbuild(%1)
	
	new idModalidade, objCompra, statusProcesso, blnEnable, idFornecedorVencedor, chave
		
	set blnEnable = $$$YES
	set pYKEY = $translate($get(pYKEY),"""")
	set chave = ""
	
	set idModalidade = $$GetModalidade^VARSESCOMPRAS($$$KEY1(pYKEY))
	quit:(idModalidade '= 10) ""
	
	set objCompra = $get(^VARSESCOMPRAS(YM,$$$KEY1(pYKEY),1))	
	set statusProcesso = $piece(objCompra,Y,7)
	 
	/* Validando o status no cabeçalho
	     PositionHere -> 8 - Aguardando Parecer de Propostas
	     PositionHere -> 11 - Processo de Compra Finalizado  	*/	
	if ( (statusProcesso '= 8) && (statusProcesso '= 11) )  {
		set blnEnable = $$$NO
	}

	/* Validando o status na linha */	
	if ($$isItemEncerrado($$$KEY1(pYKEY),$$$KEY2(pYKEY)) = $$$YES) {
		set blnEnable = $$$NO
	}

	set idFornecedorVencedor = $$GetVencedorFornecedor^VARSESCOMPRASParecer($$$KEY1(pYKEY),$$$KEY2(pYKEY))
	if (idFornecedorVencedor) {
		set chave = pYKEY_$$$COMMA_idFornecedorVencedor	
	} else {
		set blnEnable = $$$NO
	}

	$$$AddToMenuList($lb("a.gif","Alterar quantidade adquirida","CallBack(""redirecionaAlterarQuantidade^VARSESCOMPRASPDPASLinha"","""_chave_""")",,blnEnable))	

	quit lstNewMenuItems
	
redirecionaAlterarQuantidade(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Redireciona o usuário para a tela de alteração de dados na proposta do fornecedor.
    ;
    ; History:
    ; 01-May-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")	
	do GoToForm^COMUtilForm("VARSESCOMPRASPropostaLinhaAlt",pYKEY)
	
	quit
	
ItensJaIncluidos()
    ;-------------------------------------------------------------------------------
    ; Retorna os produtos que já foram inseridos no grid, para evitar duplicações
    ; (utilizando a WWWDATEN)
    ;
    ; History:
    ; 09-Nov-2012	SLV					Verificando todas as linhas do Grid, independente do número de páginas
    ; 01-May-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	new date, idLine, idField, idItem, strProdutos
	
	set strProdutos = ""
	set date = $piece($h,$$$COMMA,1)
 	
 	set intTotalGridLines = $$$GRIDRowCount
 	
	;$$$Order7(^WWWDATEN,YM,date,YUSER,"VARSESCOMPRASPDPASLinha","V","DISPLAYED",idLine) 
	for idLine=1:1:intTotalGridLines {
			
		set idField = "Y"_idLine_"_"_2 ;(2=Coluna Produto)		
		set idItem  = $get(^WWWDATEN(0,date,YUSER,"VARSESCOMPRASPDPASLinha","V",idField,1))
		
		// If grid line was not deleted, include item code in the list of already included itens:
		if ($get(^WWWDATEN(0,date,YUSER,"VARSESCOMPRASPDPASLinha","V","KILLEDRECORD",idLine)) '= 1) {
			set $piece(strProdutos,Y,idLine) = idItem
		}
	}
	;$$$End
	
	quit strProdutos
]]></Routine>
</Export>