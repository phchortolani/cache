<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLJournalLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLJournalLine
	
#include COMSYS
#include FINConst
	
OnBeforeSave(pYFELD)
    ;-------------------------------------------------------------------------------
    ; Make sure an amount is entered
    ;
    ; Params: line object
    ;
    ; Returns: nil
    ;
    ; History:
    ; 27-Jun-2006	JW		SR13755: Created
    ;-------------------------------------------------------------------------------
	if ($$$FINGLJournalLineDebit(pYFELD)="") && ($$$FINGLJournalLineCredit(pYFELD)="") {
		set YRETVAL=$listbuild("Fin00525")  ;The debit or credit column property must be populated.
	}
 	quit
 	
 	
OnLineDeleted(pstrYKEY,pintRowCount)
    ;-------------------------------------------------------------------------------
    ; If this is the last line removed unlock the inter company field
    ; 
    ; History:
    ;  2-Mar-2006	JW		SR13305: Added pstrYKEY parameter
    ; 28-Jun-2005	JW		SR10412: Fixed
    ; 24-Jun-2005	PaulK	Created.
    ;-------------------------------------------------------------------------------
	if pintRowCount=0 {
		do ToggleReadOnly^COMUtilForm($$$FldFINGLJournalInterCompany,$$$NO,"FINGLJournal")
	}
	quit
	
	
OnLineAdded(pintRowCount,pstrKey)
    ;-------------------------------------------------------------------------------
    ; If this is the first line added lock the inter company field
    ;
    ; History:
    ; 28-Jun-2005	JW		SR10412: Fixed
    ; 24-Jun-2005	PaulK	Created.
    ;-------------------------------------------------------------------------------  
    if pintRowCount=1 {
	    do ToggleReadOnly^COMUtilForm($$$FldFINGLJournalInterCompany,$$$YES,"FINGLJournal")
    }
	quit
	
	
OnAfterDataFields(pidJournal)
    ;-------------------------------------------------------------------------------
    ; Turns the Status and Company Columns on and off depending if this is an intercompany journal or not.
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Jun-2005	JW		SR10412: Added $get
    ; 10-May-2005	Paul K	Created SR10412
    ;-------------------------------------------------------------------------------
    if $get(pidJournal)'="" {
    	do ShowInterCompanyColumns^FINGLJournalIC(+$$$FINGLJournalInterCompany($get(^FINGLJournal(YM,pidJournal,1))))
    }
	quit
	
	
ShowTotals(pYKEY) // CURRENTLY NOT IN USE	
    ;-------------------------------------------------------------------------------
    ;
    ; Returns:
    ;
    ; History:
    ; 07-Feb-2005	shobby	YKEY passed as a variable.
    ; 18-Jun-2004	shobby	Foreign Currency
    ;-------------------------------------------------------------------------------
	new curTotalDebit,curTotalCredit,idJournal,objJournal,intJournalLine
	new lstHeader,objJournalLine
	
	set lstHeader=""
	set lstHeader=lstHeader_$listbuild($$^WWWTEXT("Fin00049")) ;Journal
	set lstHeader=lstHeader_$listbuild($$^WWWTEXT("Fin00054"))	;Balance
	set lstHeader=lstHeader_$listbuild($$^WWWTEXT("Fin00051")) ;total debit
	set lstHeader=lstHeader_$listbuild($$^WWWTEXT("Fin00052")) ;total credit
	
	do Start^COMTable(lstHeader)
	set idJournal=$piece($get(pYKEY),",",1)
	if idJournal'="" {
		set objJournal=$get(^FINGLJournal(YM,idJournal,1))
		set intJournalLine=""
		set curTotalCredit=0
		set curTotalDebit=0
		for {
			set intJournalLine=$order(^FINGLJournalLine(YM,idJournal,intJournalLine))
			quit:intJournalLine=""
			set objJournalLine=$get(^FINGLJournalLine(YM,idJournal,intJournalLine,1))
			set curTotalCredit=$$$FCAdd(curTotalCredit,$$$FINGLJournalLineCredit(objJournalLine))
			set curTotalDebit=$$$FCAdd(curTotalDebit,$$$FINGLJournalLineDebit(objJournalLine))
			}
		do NewLine^COMTable()
		do InsertCell^COMTable(idJournal)
		do InsertCurrencyCell^COMTable($$$FINGLJournalBalance(objJournal),$$$YES)
		;do InsertCurrencyCell^COMTable(curTotalCredit-curTotalDebit,$$$YES)
		do InsertCurrencyCell^COMTable(curTotalDebit,$$$YES)
		do InsertCurrencyCell^COMTable(curTotalCredit,$$$YES)
		do EndLine^COMTable()
	}
	do Stop^COMTable()
	quit
	
	
ValidateJournalLine(pobjJournalLine="")
    ;-------------------------------------------------------------------------------
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 18-Apr-2006	JW		SR14557: Corrected number check. Added both populated check.
    ; 28-Jun-2005	JW		SR10412: Added header. Cleaned up
    ; 19-Oct-2004	PK		SR10412: Created
    ;-------------------------------------------------------------------------------
	new strStatus,curDebit,curCredit
	
	set strStatus=$$ValidateAccount^FINGLAccountTran($$$FINGLJournalLineAccount(pobjJournalLine))
	
	if $$$ISOK(strStatus) {
		set curDebit = $$$FINGLJournalLineDebit(pobjJournalLine)
		set curCredit = $$$FINGLJournalLineCredit(pobjJournalLine)
		
		if (curDebit="") && (curCredit="") {
			set strStatus=$listbuild("Fin00525")  ;The debit or credit column property must be populated.
		
		} elseif curDebit'="" && '$isvalidnum(curDebit) {
			set strStatus=$listbuild("Fin00391",$$$StrFINGLJournalLineDebit)  ;Field '%1' must be numeric.
		
		} elseif curCredit'="" && '$isvalidnum(curCredit) {
			set strStatus=$listbuild("Fin00391",$$$StrFINGLJournalLineCredit)  ;Field '%1' must be numeric.
			
		} elseif (+curCredit'=0) && (+curDebit'=0) {
			set strStatus=$listbuild("Fin01012")  ;Only one of either the debit or credit columns can be populated.
		}
		
		/*	SR14557
		set curAmount=$$$FINGLJournalLineDebit(pobjJournalLine)
		if (curAmount'="")&&(+curAmount'=curAmount) {
			set strStatus=($listbuild("Fin00391",$$$StrFINGLJournalLineDebit))  ;Field '%1' must be numeric.
			
		} else {
			set curAmount=$$$FINGLJournalLineCredit(pobjJournalLine)
			if (curAmount'="")&&(+curAmount'=curAmount) {
				set strStatus=($listbuild("Fin00391",$$$StrFINGLJournalLineCredit))  ;Field '%1' must be numeric.
			}
		}
		*/
	}
	quit strStatus
	
	
	
GoToAccount()
    ;-------------------------------------------------------------------------------
    ; Go to the relevant account page
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 29-Jun-2005	JW		SR10412: Created
    ;-------------------------------------------------------------------------------
	new objJournalLine
	
	set objJournalLine = $$GetYFELDEX^COMGridEdit31Interface() // Get current line
	
	if $$$FINGLJournalLineCompany1(objJournalLine)="" {
		do GoToForm^COMGridEdit31Interface("FINGLAccount",$$$FINGLJournalLineAccount(objJournalLine))
	} else {
		do GoToForm^COMGridEdit31Interface("FINICAccount",$$$FINGLJournalLineCompany1(objJournalLine))
	}
	quit
	
]]></Routine>
</Export>