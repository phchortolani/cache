<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPInvChargeIC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPInvChargeIC
 
#include COMSYS
#include FINConst
 
OnAfterDataFields(pidVoucher)
    ;-------------------------------------------------------------------------------
    ; Turns the Status and Company Columns on and off depending if this is an intercompany journal or not.
    ; 
    ; Params:
    ; pidVoucher: The id of the Voucher record.
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jan-2006	RPW		SR13087: Created
    ;-------------------------------------------------------------------------------
 
    if $get(pidVoucher)'="" {
    	do ShowInterCompanyColumns^FINAPVoucherIC(((YSEITE=1)||(YSEITE=6))&&(+$$$FINAPInvInterCompany($get(^FINAPInv(YM,pidVoucher,1)))))
    }
    
	quit
	
OnBlur(pRow,pCol,&pobjVoucherLine,pValue)
    ;--------------------------------------------------------------------------------
    ; Handle the BLUR event for the intercompany fields.
    ;
    ; Params:
    ; pRow           : The row in the grid
    ; pCol           : The cell in the grid
    ; pobjVoucherLine: The data for the grid line
    ; pValue         : The value in this cell.
    ;
    ; ByRefs:
    ; pobjVoucherLine
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jan-2006	RobertW		SR13087: Created
    ;---------------------------------------------------------------------------------
	new idCompany,objICSetup
	
	if pCol=$$$FldFINAPInvChargeCompany1 {
		set idCompany=pValue
		if idCompany'="" {
			set objICSetup=$get(^FINICSetup(YM,idCompany,1))
			set $$$FINAPInvChargeCompanyAccount(pobjVoucherLine)=$$$FINICSetupDefaultDistributionAccoun(objICSetup)
			set $$$FINAPInvChargeAccount(pobjVoucherLine)=$$$FINICSetupToAccount(objICSetup)
		} else {
			set $$$FINAPInvChargeCompanyAccount(pobjVoucherLine)=""
			set $$$FINAPInvChargeAccount(pobjVoucherLine)=""
		}
		do UpdateManualField^COMGridEdit31Interface(pRow,14,$$GetAccountStatus(pobjVoucherLine))
	} elseif pCol=$$$FldFINAPInvChargeCompanyAccount {
		if $$$FINAPInvChargeCompany1(pobjVoucherLine)="" {
			set $$$FINAPInvChargeAccount(pobjVoucherLine)=pValue
		}
		do UpdateManualField^COMGridEdit31Interface(pRow,14,$$GetAccountStatus(pobjVoucherLine))
	}
 
	
	quit
	
GetAccountStatus(pobjLine) 
    /*-------------------------------------------------------------------------------
    ; Get the accounts status for this intercompany amount
    ;
    ; Params:
    ; pobjLine: The grid line to check
    ;
    ; ByRefs:
    ;
    ; Returns:
    ; The account status.
    ;
    ; History:
    ; 25-Jan-2006	RobertW		SR13087: Created
    ;-------------------------------------------------------------------------------*/
 
	quit $$GetAccountStatus^FINICSetup($$$FINAPInvChargeCompany1(pobjLine),$$$FINAPInvChargeCompanyAccount(pobjLine))
	
AccountSearch(pstrLine)
    ;-------------------------------------------------------------------------------
    ; Tailor the Account search to whether there is an intercompany key on this line.
    ;
    ; History:
    ; 25-Jan-2006	RobertW		SR13087: Modified from FINGLJournalIC
    ;-------------------------------------------------------------------------------
	new objJournal,idJournal,idLine,objLine
	
	set idLine=$piece($piece(pstrLine,"Y",2),"_",1)
 
	if idLine'="" {
		set objLine=$$GetYFELD^COMGridEdit31G("FINAPInvCharge",idLine)
		if $$$FINAPInvChargeCompany1(objLine)'="" {
			do Initialise^COMViewCustom("FINICAccount")
			do AddFilter^COMViewCustom("P1","=",$$$FINAPInvChargeCompany1(objLine))
			do AddFilter^COMViewCustom("D"_$$$FldFINICAccountBlockedReason,"=","")
		} else {
			do Initialise^COMViewCustom("FINGLAccount")
			do AddFilter^COMViewCustom("D"_$$$FldFINGLAccountAccountUsage,"=",$$$EnumFINGLACCOUNTUSAGETransaction)
			do AddFilter^COMViewCustom("D"_$$$FldFINGLAccountEnablepostingsecurity,"=","")
		}
		do Start^COMViewCustom("WWW2.Y"_idLine_"_13")
	}
	
	quit
]]></Routine>
</Export>