<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWHELP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWHELP
	
#include COMSYS
#include WWWConst
	
#define TEXTLENGTH	500
#define Store(%row,%col,%fld) set parrFields($select(%row="":" ",$$$YES:%row),$select(%col="":" ",$$$YES:%col),%fld)=""
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Form Help             HILFE ZUM FORMULAR
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
	; 05-Nov-2007	shobby	SRBR014748: Standard routine for password check.
	; 07-May-2007	GRF		SR15511: Doco
	; 08-Dec-2006	PO		SR15276: Changed name of EventBroker to JSLibraries
	; 13-Nov-2006	GM		SRBR014107: Change Customization button to call WWW120D
	; 							and let form name changes with customisation on
	; 							Help Screen  
	; 27-Oct-2006	SS		SR14915: Use some macros
    ; 22-Aug-2006   PJ		SRBR014120: When the user's set language isn't English,
	; 							the help text defaults to English (if English text
	; 							exists in the database, but the user's language 
	; 							doesn't.) German will still appear if there is
	; 							neither the user's own language nor English is
	; 							available.
    ; 26-Jul-2006	JW		SRBR014099: Reverted all changes for build
	; 20-Jul-2006	GM		SRBR014099: Show hidden fields as greyed in Help Text
	; 							and field name like administrator and don't show
	; 							hidden fields in Help Text and field name like user
	;  2-Jun-2006	JW		SR14697: Always use eventbrokeren1.js
	;  9-Dec-2005	JW		SR13195: Removed back button. Look at parent user for grid.
	; 02-Dec-2005 	RPW		SR13940: Get YADMIN from CanCustomize call
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 05-May-2005	Paul K	Normalised Directory
	; 25.01.2000	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	new (%request,%session,%KEY,%,%ZCS,%CGIEVAR,blnEditGridHelp)
	
	do ^WWWVAR
	
	if $get(%(YQUERY,"SPRACHE"))'="" {
		set SPRACHE = $get(%(YQUERY,"SPRACHE"))
	}
	;------------------------------------------------------------------------
	if +$get(YSEITE)=0 set YSEITE = 1
	if YFORM="" set YFORM = $piece(^WWW013(0,YBED,1),Y,12)           ;LETZTES FROMULAR ;last 
	if '$data(^WWW013(0,YBED)) do ^WWWINFO($$^WWWTEXT(5)) quit       ;BERECHTIGUNG
	if '$$CHECK^WWWPWDCHECK($piece(^WWW013(0,YBED,1),Y,2),YPWD) do ^WWWINFO($$^WWWTEXT(5)) quit   ;PASSWORD

	set YADMIN = $$CanCustomize(YBEDBER)
	if YADMIN=0 if '$data(^WWW127(0,YFORM)) do ^WWWINFO($$^WWWTEXT(28)) quit  ;KEINE HILFE ;no help 
	
	set YDATEI = $piece($get(^WWW120(0,YFORM,1)),Y,11)   ;DATEI ;data file 
	do ^WWWFORMX  ;VORGABEN AUS MANDANTEN UND FORM ;out of And shape 
	
	set $$$WWW120FontFace(YVOR)=$$$WWW012FontFace($get(^WWW012(0,YM,1)))
	
	;SR14915: Use Macros here
	set $$$WWW120BackgroundColor(YVOR)	= 137    	// white
	set $$$WWW120FrameBodyColor(YVOR)	= 137     	// white
	set $$$WWW012FrameBodyColor(YVOR1)	= 137  		// white
	set $$$WWW120DisplayFrames(YVOR)	= $$$NO     ;KEIN RAHMEN ;no framework 
	
	set YFOART=$$$WWW120FormType(YVOR)
	
	set KOPF1=$$^WWWUML($$^WWWFORMNAME(YFORM))
	
	;IF $$$WWW120DFormHeaderOrImageFile($GET(^WWW120D(0,YFORM,YM,1)))'="" {      ;BR014107 ;BR014966
	;	SET KOPF1=$$^WWWUML($$$WWW120DFormHeaderOrImageFile($GET(^WWW120D(0,YFORM,YM,1))))
	;}
 
	set KOPF=$$^WWWTEXT(20)_": "_$$$Text(KOPF1) ;BR014966
	
	do ^WWWSTART(KOPF)
	
	do JSLibraries^WWWFORM8()	// SR14697 // SR15276
	
	if $$$WWW012MenuType($get(^WWW012(0,YM,1)))=7 do ^WWWFORM8  ;JAVASCRIPT MIT MENUE ;by means of 
	do ^WWWBODY(4,"NOPRINT")
	write "<FORM NAME=""WWW"" ACTION="""_$piece(YAKTION,"?",1)_""""
	if +$get(YHYPER)=0 write " Method=POST"
	if +$get(YHYPER)=1 write " Method=POST"
	write ">" 
	
	set $piece(YVOR,Y,9)=$piece($get(^WWW012(0,YM,1)),Y,9)
	set $piece(YVOR,Y,5)=$piece($get(^WWW012(0,YM,1)),Y,5)
	if $piece(YVOR,Y,10)=1 write "<CENTER>"
	
	if $piece(YVOR,Y,10)=1 write "</CENTER>"
	write YCR,"<TABLE CELLSPACING=0 BORDER=0 NOWRAP><TR><TD"
	if +$piece(YVOR,Y,45)=1 write " class=""coolButton"""  ;MOUSEEFFECT
	write " NOWRAP>"
	if +$piece(YVOR,Y,45)'=1 write "<FORM>"
	set YQ=0
		
	if +$piece(YVOR,Y,45)'=1 write "</FORM>"
	write YCR,"</TD></TR></TABLE>"
	write YCR
	if $piece(YVOR,Y,13)=1 do ^WWWFRAME(0) write YCR,"<TR><TD>"
	
	set idParentUser = $$$GetParentUser(YUSER)		//SR13195
	if idParentUser'="" && ($data(^CacheTemp(idParentUser,"Grid","Container"))#10)&&(^CacheTemp(idParentUser,"Grid","Container")=$get(YFORM)) { ; SR11141
		set YNOFOOT=1
		set blnIncludeGridHelp=$$$YES
	}
	
	do START
	if $piece(YVOR,Y,13)=1 write YCR,"</TD></TR>" do ^WWWFRAME(1)
	
	do ^WWWSTOP
	
	if $get(blnIncludeGridHelp) { ; SR11141
		kill YNOFOOT
		new strIndex,blnEditGridHelp
		set strIndex = ""
		for {
			set strIndex=$order(%request.Data(strIndex)) quit:strIndex=""
			if strIndex="YFORM" {
				set %request.Data(strIndex,1)=^CacheTemp(idParentUser,"Grid","Name")	//SR13195
			} elseif strIndex="YSEITE" {                               ; SR11348
				set %request.Data(strIndex,1)=1
			}
		}
		set blnEditGridHelp = $$$YES
		do WWWHELP
	}
	
	quit
	
	
START ; EINSPRUNG VON AUSSEN MIT YADMIN UND YFORM ;by means of And 
    ;-------------------------------------------------------------------------------
    ; History:
    ; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
    ; 17-Sep-2007	SCR		SR15600: Added Call to Event Rule
    ; 20-Jun-2007	RPW		SR15534: Show the (!) is either the name or help text is changed.
    ; 18-Jun-2007	RPW		SR15539: Do not show free fields at all when not required,
    ; 						it was showing the last field entry for each free field.
    ; 						Complete rewrite into { syntax.
    ; 11-May-2007	GM		BR014107: Doing fields number in class WWW120D (5, 7, 120
    ; 						and 121) working in WWW120D form
    ; 08-May-2007	GM		BR014107: Customisation Button on Help Screen go to form WWW120D
    ; 21-Dec-2006	JW		SR14235: Added reference to child windows
    ; 27-Oct-2006	Steve S SR15094: Tab customisation support; Encapsulated
    ; 						function calls
    ; 23-Oct-2006	Steve S SR14915: Go to new help text form; Don't use hyperlinks
    ; 						for fields/keys (done via new form)
    ; 						 Use SPRACHE where available, else use WWWLANGU
    ; 28-Aug-2006	HeberB	SRBR014155: Include customized help text for manual input forms
    ; 27-Jul-2006	GM		SRBR014099: Change colour of field name and your help
    ; 						text if it's a hidden field
    ; 11-Nov-2005	JW		SR11904: Child user for popups
    ;  9-Sep-2005	JW		Only call WWWUMLAU once (idxForm)
    ;-------------------------------------------------------------------------------
	new arrFields,idChildUser,idLang,objCust,objCust2,objWWW120D,strHelpText,strName
 
	set idChildUser = $$GetChildUser^WWWUSER(YUSER)
	
	write YCR
	
	set idLang = $select($get(SPRACHE)="":$$^WWWLANGU(YBED),$$$YES:SPRACHE)
	
	write "<HR COLOR="_YBLUE_">"
	
	if YADMIN=1 {
		write "<A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM=COMHelp"
		write "&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
		write "&amp;YUSER="_YUSER
		write "&amp;FORMULAR="_YFORM
		write "&amp;ERFASSUNG=M"
		new BEARB
		set BEARB=1
		if (YFOART=4) || (YFOART=5) if $data(^WWW122(0,YFORM)) set BEARB=0  ;FIS;24611;13.11.03;FREIE FELDER OHNE BUTTON
		if '$data(^WWW122(0,YFORM,1)) if $translate($get(^WWW127(0,YFORM,"M",1,idLang,1)),Y_" ")'="" set BEARB=1
		write "&amp;BEARBEITUNG="_BEARB
		write "&amp;SPRACHE="_idLang
		write "&amp;YFKEY="_YFORM_","_idLang
		write "&amp;YKEY="_YFORM_","_idLang
		write "&amp;YANZ="_YANZ
		write """>"
	} 
 
	;
	;------------------------------------------------------------------------
	new strKOPF1
	set strKOPF1=$zconvert(KOPF1,"U")
	
	if $find(strKOPF1,".GIF") set KOPF1=""
	if $find(strKOPF1,".JPG") set KOPF1=""
 
	write "<H2>"
	write KOPF1
	write "</H2>"
	
	if YADMIN=1 write "</A>"
	
	set objWWW120D  = $get(^WWW120D(0,YFORM,YM,1))
	set strName     = KOPF ;$$$WWW120DFormHeaderOrImageFile(objWWW120D) ;BR014966
	;set strHelpText = $$$WWW120DCustomHelpText(objWWW120D)
	set strHelpText = $$CustomHelpText^WWW120D(objWWW120D) ;BR014966 ;SR16925.2
	
	if +YADMIN'=0 {
		write "<A"
		;IF $PIECE($GET(^WWW121D(0,YFORM,0,YM,1)),Y,5)'="" WRITE " Title="""_$$^WWWTEXT(33417)_""""
		if (strName'="")||(strHelpText'="") write " Title="""_$$^WWWTEXT(33417)_"""" ;BR014107 // SR15534
 
	;	write " HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM=WWW121D"                        ;SR14235
		write " href='' onclick=""subWindow('"_YAKTION_"EP=WWWFORM&amp;YFORM=WWW120D"  ;BR014107
		write "&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
		write "&amp;YUSER="_idChildUser			     ;SR11904
		write "&amp;SPRACHE="_idLang                 ;SR14915
		write "&amp;YFKEY="_YFORM_","_YM             ;BR014107
		write "&amp;YKEY="_YFORM_","_YM              ;BR014107
		write "&amp;YANZ="_YANZ
		write "','TEILEFRAME1'); return false"">"
	;	write """ TARGET=TEILEFRAME1>"	             ;SR14235
		
		if (strName'="")||(strHelpText'="") write "&nbsp;<font color="_YRED_">(!)</FONT>" ;BR014107 // SR15534
		;IF $PIECE($GET(^WWW121D(0,YFORM,0,YM,1)),Y,5)'="" WRITE "&nbsp;<font color="_YRED_">(!)</FONT>"
		write " <IMG SRC="_YGIF_"dflt.gif border=0 TITLE="_$$^WWWTEXT(33400)_">"         ;Customizing ;FAN ;07.07.03;23923
		write "</A>"
	}
 
	write YCR,"<HR COLOR="_YBLUE_">",YCR
	;------------------------------------------------------------------------
	if YADMIN=1 do FormLink() //SR15094
	do FormImage()			  //SR15094
	;------------------------------------------------------------------------
	;  Class / Event Rule Display SR15600
	
	;MANUELLE, PRIMAER, LISTENFELDER
	
	do ShowHelp(idLang,idChildUser)       //SR15539
	do DataFields(idLang)                 //SR15539
	write "</FONT>",YCR
	
	quit
	
	
GetFieldsForPage(pYFORM,pintPage,&parrFields)
	;-------------------------------------------------------------------------------
	; Return an array of fields (YLFN numbers) for this form page
	; (takes customisation into account)
	;
	; Params:	pYFORM		: The form
	; 			pintPage	: The page number (YSEITE)
	;
	; ByRefs:	parrFields	: Array of fields, with their row/col numbers subscripted
	;
	; Returns:
	;
	; History:
	; 08-Nov-2006	Steve S	SR15094: Customisation ordering for row/col/page
	; 27-Oct-2006	Steve S	SR15094: Created
	;-------------------------------------------------------------------------------
	new idxForm,intRow,intCol,intYLFN,objWWW122,objWWW122D,intCustPage
	new intStoreRow,intStoreCol,intCustStoreRow,intCustStoreCol
	
	kill parrFields
 
	set idxForm = $$$Index(pYFORM)
	
	set intRow=""
	for {
		set intRow=$order(^WWW122s(0,3,idxForm,pintPage,intRow))
		quit:(intRow="")
		
		set intCol=""
		for {
			set intCol=$order(^WWW122s(0,3,idxForm,pintPage,intRow,intCol))
			quit:(intCol="")
			
			set intYLFN=""
			for {
				set intYLFN=$order(^WWW122s(0,3,idxForm,pintPage,intRow,intCol,pYFORM,intYLFN))
				quit:(intYLFN="")
				
				set objWWW122 = $get(^WWW122(0,pYFORM,intYLFN,1))
				
				set intStoreRow = $$$WWW122RowPosition(objWWW122)
				set intStoreCol = $$$WWW122ColumnPosition(objWWW122)
				
				if $data(^WWW122D(0,pYFORM,intYLFN,YM)) { // check customisations
				
					set objWWW122D 			= $get(^WWW122D(0,pYFORM,intYLFN,YM,1))
					set intCustPage 		= $$$WWW122DDisplayOnPage(objWWW122D)
					set intCustStoreRow 	= $$$WWW122DLinePosition(objWWW122D)
					set intCustStoreCol 	= $$$WWW122DTabulatorPosition(objWWW122D)
					
					if (intCustPage'="")&&(intCustPage'=pintPage) {
						continue // not on this page
					}
					
					if (intCustStoreRow'="") set intStoreRow=intCustStoreRow
					if (intCustStoreCol'="") set intStoreCol=intCustStoreCol
				}
				$$$Store(intStoreRow,intStoreCol,intYLFN)
			}
		}
	}
 
	// Now check for fields which are normally on other pages but have
	// been customised to be on **THIS** page, and add them in.
	
	set intYLFN=""
	for {
		set intYLFN = $order(^WWW122Ds(0,3,pintPage,pYFORM,intYLFN))
		quit:(intYLFN="")
		
		if $data(^WWW122Ds(0,3,pintPage,pYFORM,intYLFN,YM)) {
			set objWWW122D = $get(^WWW122D(0,pYFORM,intYLFN,YM,1))
			set intStoreRow = $$$WWW122DLinePosition(objWWW122D)
			set intStoreCol = $$$WWW122DTabulatorPosition(objWWW122D)
			$$$Store(intStoreRow,intStoreCol,intYLFN) 
		}
	}	
 
	quit
	
ShowFieldNameNew(pYFORM,pYLFN,pYLFNN,pYDATEI,pidTab)
	;-------------------------------------------------------------------------------
	; Show the field name
	;
	; Params:	pYFORM		: This form
	; 			pYLFN		: This (form) field id
	; 			pYLFNN		: This (class) field id
	; 			pYDATEI		: Corresponding class name.
	; 			pidTab		: Page
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Aug-2007	GM		SRBR014657: Include pidTab as parameter and YM as company
	; 20-Aug-2007	GM		SRBR014657: In Hidden tabs, field names are shown as
	; 						gray for System and Database Administrator users  
	; 15-Aug-2007	shobby	SRBR014669: Created (Rewrite of ShowFieldName)
	;-------------------------------------------------------------------------------
	new objWWW122,strFieldName,strType,intField,objWWW003,blnFreeField,objWWW1203D,blnHide
	
	set objWWW1203D = $get(^WWW1203D(0,pYFORM,SPRACHE,pidTab,YM,1))
	set blnHide     = $$$WWW1203DHide(objWWW1203D)
	
	if (pYDATEI'="") && (pYLFNN'="") {
		set strType   = "D"
		set intField  = pYLFNN
		set objWWW003 = $get(^WWW003(0,pYDATEI,pYLFNN,1))
	} else {
		set strType   = "M"
		set intField  = pYLFN
		set objWWW003 = ""
	}
 	set objWWW122    = $$Get^WWW122(pYFORM,pYLFN)
	set strFieldName = $$^WWWFELDNAME(pYFORM,strType,intField)
	set blnFreeField = $$IsFreeField(strFieldName)
	if ($$$WWW122InputType(objWWW122)=0) || blnFreeField || blnHide {   ; Hidden
		if $$CanCustomize(YBEDBER) {
			if blnFreeField {
				set strFieldName = $$$FREE_" ["_$$$WWW003PropertyName(objWWW003)_"]"
			}
			write "<FONT COLOR=#B6B6B6>"_strFieldName_ "</FONT>"
		}
	} else {
		write strFieldName
	}
	write "</H4>"
	quit
	
	
TextWrapper(pYDATEI="",pYLFNN="",pstrText="",pstrType="D")
	;-------------------------------------------------------------------------------
	; Show the field name
	;
	; Params:	pYFORM		: This form
	; 			pYLFN		: This (form) field id
	; 			pstrText	: The text to display
	; 			pstrType	: Field type
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Aug-2007	shobby	SRBR014669: Improved handling of what to do when a 
	; 							free field has no help text written for it
	;-------------------------------------------------------------------------------
	new objWWW003,strHeading
	
	set pstrText = $$$TRIMWS(pstrText)
	if $translate(pstrText,"|")="" set pstrText = ""
	if pstrText="" {
		if ($get(pYDATEI)'="") && ($get(pYLFNN)'="") && (pstrType="D") {
			set objWWW003  = $get(^WWW003(0,pYDATEI,pYLFNN,1))
			set strHeading = $$$WWW003CaptionInForms(objWWW003)
			if $$IsFreeField(strHeading) {
				set pstrText = "<FONT COLOR=#B6B6B6>"_$$^WWWTEXT("WWW00077")_"</FONT>" ; "A user-configurable data field."
			}
		}
	}
	if pstrText="" set pstrText = $$^WWWTEXT(28)   ; "No help text available"
	quit pstrText
	
IsFreeField(pstrFieldName)
	;-------------------------------------------------------------------------------
	; Determines if this is a free field
	;
	; Params:	pYFORM		: This form
	; 			pYLFN		: This (form) field id
	; 			pYLFNN		: This (class) field id
	; 			pYDATEI		: Corresponding class name.
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Sep-2007	GRF		SRBR014669: "_BLANK" has six letters!
	; 15-Aug-2007	shobby	SRBR014669: Created (Rewrite of ShowFieldName)
	;-------------------------------------------------------------------------------
	set pstrFieldName = $extract($$$TRIMWS(pstrFieldName),1,5)
	quit (pstrFieldName=$$$FREE)||(pstrFieldName="_BLAN")
	
	
ShowFieldName(pYFORM,pYLFN,pidTab)
	;-------------------------------------------------------------------------------
	; *************************   THIS ROUTINE CAN PROBABLY BE REMOVED IF BR014669 IS OK  **********************
	; **** Needs to have ShowFieldNameNew called in place of ShowFieldName noting different argument order *****
	; 
	; NOTE : EXCESSIVE EXIT POINTS - QUITS in IFS (in DISABLED PART OF SUBROUTINE)
	; 
	; Show the field name
	;
	; Params:   pYFORM      : This form
	;           pYLFN       : This field id
	;           pidTab      : Page
	; 			
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Aug-2007	GM		SRBR014657: Include pidTab as parameter
	; 15-Aug-2007	shobby	SRBR014669:	Redirect to ShowFieldNameNew (simplifies and
	; 						improves a few things)
	; 14-Aug-2007	GM		SRBR014669: Condition included if field name was customized in WWW122D1
	; 07-Aug-2007	GM		SRBR014593: Condition included if hidden field in WWW122 was selected
	; 26-Jun-2007	RPW		SR15539: Show the _FREE property name if not customised
	; 27-Oct-2006	Steve S	SR15094: Created (moved code from START^WWWHELP)
	;-------------------------------------------------------------------------------
	new objWWW122D1,strDesc,objWWW122D,objWWW0031,objWWW1221,objWWW122
	new objWWW003,strName
	
 	do ShowFieldNameNew(pYFORM,pYLFN,YLFNN,YDATEI,pidTab)
	quit
	
	
HideField(pstrForm,pidField)
	;-------------------------------------------------------------------------------
	; Check if we should show this field on the help text at all.
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 18-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new blnHide
	
	set blnHide = $$$NO
 
	if (YADMIN'=1) && (YADMIN'=2) && ($extract($$$WWW122ManualCaption($get(^WWW122(0,pstrForm,pidField,1))),1,5)="_FREE") {
		set blnHide=$$$YES
	}
	
	quit blnHide
	
	
ShowCustomiseButton(pYFORM,pYLFN)
	;-------------------------------------------------------------------------------
	; Show customise button
	; 
	; Called By : DataFields
	; 
	; Params:	pYFORM		: This form
	; 			pYLFN		: Form field id
	;
	;
	; Returns:
	;
	; History:
	; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
	; 15-Aug-2007	shobby	BR014669: Don't show the customisation button if it is
	; 							hidden in core.  You can't make a hidden button
	; 							unhidden with customisation.
	; 07-Aug-2007	GM		BR014593: Included a second condition "YADMIN'=2"
	; 27-Oct-2006	Steve S	SR15094: Created (moved code from START^WWWHELP)
	;-------------------------------------------------------------------------------
	new objWWW122,objWWW122D
	
	set objWWW122 = $get(^WWW122(0,pYFORM,pYLFN,1))
	
	;FREIES DATENFELD; free field ;FIS;AUCH FÜR DATA-BASE-ADMIN;13.12.04;26973
	if YADMIN'=1 if YADMIN'=2 if $extract($$$WWW122ManualCaption(objWWW122),1,5)="_FREE" quit    ; *** EARLY EXIT ***   
	
	set YLFNN = $$$WWW122SequenceNumber(objWWW122)    ; Class Field #
	
	;NUR HIDDEN FELD NICHT ANZEIGEN ;TYBD;1.11.01 ;only field Not display ;BR014593 ;BR014669
	;IF YADMIN'=1 IF YADMIN'=2 IF $$$WWW122DataInputType(objWWW122)=15 QUIT

	;+ + + + + + + + + + + + + + + + + + + +
	; DEPRECATED? We see "No help text available" instead
	; FIXME : YI should be "D" since dealing with WWW003 but enters with a value "13" creating
	;         ^WWW127(0,idForm,13,ClassFieldNo,"DE",1) = Class Data Field Description
	;             1. use "D"  instead of YM
	;             2. use "EN" instead of "DE"      <GRF>
	;+ + + + + + + + + + + + + + + + + + + +
	/*
	; Initial population of help text with class field definition
	IF (YLFNN'="") && ($GET(^WWW127(0,pYFORM,YI,YLFNN,"DE",1))="") && (YDATEI'="") {
		;SET ^WWW127(0,pYFORM,YI,YLFNN,"DE",1) = $$$WWW003Description($GET(^WWW003(0,YDATEI,YLFNN,1)))  ;Text from data record
		SET ^WWW127(0,pYFORM,YI,YLFNN,"DE",1) = $$$WWW003Description($GET(^WWW003(0,YDATEI,YLFNN,1)))  ;Text from data record
	}
	*/
	;+ + + + + + + + + + + + + + + + + + + +
	
	write "<BR>",YCR
	write "<H4>"
	if ($$$WWW122InputType(objWWW122)=0) || ($$$WWW122DataInputType(objWWW122)=15) quit    ; *** EARLY EXIT ***   ;BR014669
 
	set objWWW122D = $get(^WWW122D(0,pYFORM,pYLFN,YM,1))
 
	if +YADMIN'=0 {
		write "<A"
		if $$$WWW122DCustomHelpText(objWWW122D)'="" write " Title="""_$$^WWWTEXT(33417)_""""  ; "Location Specific Online-Help"  SR16925
		
		write " href='' onclick=""subWindow('"_YAKTION_"EP=WWWFORM&amp;YFORM=WWW122D"
		write "&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
		write "&amp;YUSER="_idChildUser			//SR11904
		write "&amp;SPRACHE="_idLang
		write "&amp;YFKEY="_pYFORM_","_pYLFN_","_YM
		write "&amp;YKEY="_pYFORM_","_pYLFN_","_YM
		write "&amp;YANZ="_YANZ
		write "','TEILEFRAME1'); return false"">"
		
		if $$$WWW122DCustomHelpText(objWWW122D)'="" write "&nbsp;<font color="_YRED_">(!)</FONT>" ;SR16925.2
		write " <IMG SRC="_YGIF_"dflt.gif border=0 TITLE="_$$^WWWTEXT(33400)_">"         ; "Customizing"
		write "</A>&nbsp;"
	}
	
	; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv EFFECTIVELY DISABLED BLOCK
	//IF YADMIN=1 DO
	if $$$NEVER { //SR14915
		write "<A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM=WWW127"
		write "&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
		write "&amp;YUSER="_YUSER
		write "&amp;FORMULAR="_pYFORM
		
		if YLFNN'="" { 
			write "&amp;ERFASSUNG="_YI
			write "&amp;BEARBEITUNG="_YLFNN
			write "&amp;SPRACHE="_idLang
			write "&amp;YFKEY="_pYFORM_","_YI_","_YLFNN_","_idLang
			write "&amp;YKEY="_pYFORM_","_YI_","_YLFNN_","_idLang
			write "&amp;YANZ="_YANZ
		}
		
		if YLFNN="" { 
			write "&amp;ERFASSUNG=M"
			write "&amp;BEARBEITUNG="_pYLFN
			write "&amp;SPRACHE="_idLang
			write "&amp;YFKEY="_pYFORM_",M,"_pYLFN_","_idLang
			write "&amp;YKEY="_pYFORM_",M,"_pYLFN_","_idLang
			write "&amp;YANZ="_YANZ
		}
		
		write """>"
	}
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ EFFECTIVELY DISABLED BLOCK
	
	quit
	
	
TEXT(pblnColour=$$$NO)  
	;-------------------------------------------------------------------------------
	;	SCHREIBEN TEXT;ACHTUNG AUCH EINSPRUNG AUS WWWHPR ;write Text estimation too out of
	;		SET YTEXT=TEXT ;table-mat 
	;		DO TEXT^WWWHELP()
	; Inputs:
	;	YEDITOR		Boolean
	;	
	; History:
	; 18-Mar-2008	GM		SRBR014883: Make appear the pictures of buttons in "WWWHPR" form
	; 24-Oct-2006	SteveS	SR14915: Display process breaks to show .gifs
	; 28-Jul-2006	GRF		Fix Bug from 27-Jul-2006: pstrColour is not pblnColour
	; 27-Jul-2006	GM		SRBR014099: Change colour of help text field using parameter & macros
	; 16-Jun-2005	RW		Fixed incorrect variable usage which cause images not to
	; 						appear in help text files.
	;-------------------------------------------------------------------------------
	new strYINHALT,strPIECEYINHALT,strText
	
	set YEDITOR = $get(YEDITOR)    ;BEC;23380 KEINE TABELLE BEI EDITOR EINSPRUNG !!! ;no tabulation next to 
	if YEDITOR'=1 { 
		write YCR,"<TABLE BORDER=0 WIDTH=100%>"
		write "<TR>",YCR
		if $data(^WWWSOR(YUSER,"WWW042SP")) {
			write "<TD WIDTH=7%>"   ;WEM;25131;19.02.2004;ANDERES LAYOUT WENN AUFRUF ÜBER WWW0042SP
		} else {
			write "<TD WIDTH=60>"
			write "&nbsp;</TD><TD>"
			if pblnColour {
				write "<FONT SIZE=2 color=#B6B6B6>"
			} else {
				write "<FONT SIZE=2>"
			}
		}
	}
	
 	; Process {YGIF} string
	set YTEXT = $$Replace^COMUtilStr(YTEXT,"src=""{YGIF}","src="""_YGIF)

	set strText = $piece($piece($piece(YTEXT,Y,1),"<body>",2),"</body>",1)
	if strText'="" {
		set $piece(YTEXT,Y,1) = strText
	}
	
	do      
	. for YI1=1:1 quit:$piece(YTEXT,"|",YI1,999)=""  do
	. . set YINHALT = $piece(YTEXT,"|",YI1)
	. . if YINHALT'="" if $find(YINHALT,".GIF") || ($find(YINHALT,".gif")) if $extract($reverse($zconvert($translate(YINHALT," "),"U")),1,4)="FIG." do  quit
	. . . new WIDTH
	. . . set WIDTH = ""
	. . . set strYINHALT      = $zconvert(YINHALT,"L")
	. . . set strPIECEYINHALT = $piece(strYINHALT,".gif/",2)
	. . . if strPIECEYINHALT'="" set WIDTH = strPIECEYINHALT set YINHALT=$piece(strYINHALT,".gif/",1)_".gif"
	. . . if $get(PDF)'=1 write "<CENTER><IMG SRC="""_YGIF_$translate(YINHALT," ")_""""
	. . . if $get(PDF)=1  write "<CENTER><IMG SRC="""_YGIF_$translate(YINHALT," ")_""""  ; FIXME : Same
	. . . if WIDTH'=""    write " width="_WIDTH
	. . . write " TITLE="""_$piece(YINHALT,".",1)_"""></CENTER>"			;BEC;29.07.03;DAMIT BILDER AUF IM PDF RICHTIG ANGEZEIGT WERDEN ;therewith imagery upon rightly will 
	. . ;
	. . if YINHALT'="" if '$find(YINHALT," ") if $find(YINHALT,".AVI") || $find(YINHALT,".avi") do  quit
	. . . write "<CENTER><IMG DYNSRC="""_YGIF_YINHALT_""" START=mouseover></CENTER>"
	. . ;
	. . if YINHALT'="" if '$find(YINHALT," ") set strYINHALT = $zconvert(YINHALT,"U") if $find(YINHALT,"HTTP://") || $find(strYINHALT,"WWW.") do  quit
	. . . if '$find(strYINHALT,"HTTP:") set YINHALT = "http://"_YINHALT
	. . . write "<CENTER><A HREF="_YINHALT_" TARGET=NEUFRAME>"_YINHALT_"</A></CENTER>"
	. . ;
	. . if YINHALT'="" if '$find(YINHALT," ") set strYINHALT=$zconvert(YINHALT,"U") if $find(strYINHALT,"HYPERLINK:") do  quit
	. . . do HYPERLINK
	. . ;
	. . if YINHALT'="" if '$find(YINHALT," ") if $find(YINHALT,".WAV") || ($find(YINHALT,".WAV")) do  quit
	. . . write "<SOUND SRC="""_YGIF_YINHALT_""">"
	. . ;
	. . if $get(YCORR)'=1 write YINHALT
	. . ;
	. . if $get(YCORR)=1 for YI2=1:1 quit:$piece(YINHALT," ",YI2,9999)=""  do
	. . . new PRUEF
	. . . set PRUEF = $piece(YINHALT," ",YI2)
	. . . for PRUEF(4)="B","I","U","H1","H2","H3","H4","H5","H6","H7","LI","CENTER","OL","UL"  do
	. . . . for PRUEF(3)=$zconvert(PRUEF(4),"U"),$zconvert(PRUEF(4),"L") do
	. . . . . if $find(PRUEF,"<"_PRUEF(3)_">")  set PRUEF = $piece(PRUEF,"<"_PRUEF(3)_">",1)_$piece(PRUEF,"<"_PRUEF(3)_">",2)
	. . . . . if $find(PRUEF,"</"_PRUEF(3)_">") set PRUEF = $piece(PRUEF,"</"_PRUEF(3)_">",1)_$piece(PRUEF,"</"_PRUEF(3)_">",2)
	. . . . . ;
	. . . . . if $find(PRUEF,"<"_PRUEF(3)_">")  set PRUEF = $$^WWWTRANSLATE(PRUEF,"<"_PRUEF(3)_">")
	. . . . . if $find(PRUEF,"</"_PRUEF(3)_">") set PRUEF = $$^WWWTRANSLATE(PRUEF,"</"_PRUEF(3)_">")
	. . . ;
	. . . set PRUEF = $translate(PRUEF,"@#$%^®&*_=+<>?-,!'""","                    ")    ;BEC;19.08.04
	. . . set PRUEF = $$^WWWUMLAU(PRUEF,1)
	. . . if $translate(PRUEF," ,()@#$%^&*_=+<>?/-!")'="" for PRUEF(2)=".",";",",",":"," ","!","" do
	. . . . if $extract($reverse(PRUEF))=PRUEF(2) set PRUEF = $reverse($extract($reverse(PRUEF),2,999))
	. . . . if $extract(PRUEF)=PRUEF(2)           set PRUEF = $extract(PRUEF,2,999)
	. . . ;
	. . . set PRUEF(1) = 0
	. . . if $translate(PRUEF," ,()@#$%^&*_=+<>?/-!")'="" do
	. . . . if $length(PRUEF)'>256 if '$data(^WWWDICTIONARY01(0,SPRACHE,PRUEF,1)) do    ; CUST. DIC
	. . . . . if '$data(^WWWDICTIONARY(0,SPRACHE,PRUEF,1)) set PRUEF(1) = 1
	. . . . ;
	. . . . if $length(PRUEF)'>256 if $data(^WWWDICTIONARY(0,SPRACHE,PRUEF_".",1)) || $data(^WWWDICTIONARY(0,SPRACHE,PRUEF_".",1)) set PRUEF(1) = 0     ;FÜR Z.B. , DIE EIN LESTZES "." HAT, DAS GEFILTERT WIRD
	. . . . if $piece(YINHALT," ",YI2)=$zconvert($piece(YINHALT," ",YI2),"U")    set PRUEF(1) = 0   ;KEINE EIGENNAMEN WIE INTRAPREND ;no such as 
	. . . . if +PRUEF=PRUEF set PRUEF(1) = 0        ;KEINE ZAHLEN PRÜFEN ;no check 
	. . . ;
	. . . if PRUEF(1)=1 write "<em style='border-bottom:1px dashed red; font-style:normal;'>"
	. . . ;
	. . . if PRUEF(1)=1 do
	. . . . new YKEY,WORD
	. . . . write YCR,"<A HREF=""#"""
	. . . . set WORD=$translate($piece(YINHALT," ",YI2),",'^()""")
	. . . . for PRUEF(4)="B","I","U","H1","H2","H3","H4","H5","H6","H7","LI","CENTER","OL","UL"  do
	. . . . . for PRUEF(3)=$zconvert(PRUEF(4),"U"),$zconvert(PRUEF(4),"L") do
	. . . . . . if $find(WORD,"<"_PRUEF(3)_">")  set WORD  = $piece(WORD,"<"_PRUEF(3)_">",1)_$piece(WORD,"<"_PRUEF(3)_">",2)
	. . . . . . if $find(WORD,"</"_PRUEF(3)_">") set PRUEF = $piece(WORD,"</"_PRUEF(3)_">",1)_$piece(WORD,"</"_PRUEF(3)_">",2)
	. . . . . . ;
	. . . . . . if $find(WORD,"<"_PRUEF(3)_">")  set WORD = $$^WWWTRANSLATE(WORD,"<"_PRUEF(3)_">")
	. . . . . . if $find(WORD,"</"_PRUEF(3)_">") set WORD = $$^WWWTRANSLATE(WORD,"</"_PRUEF(3)_">")
	. . . . ;
	. . . . if $length(WORD,".")>1 if $extract($reverse(WORD))="." set WORD = $reverse($extract($reverse(WORD),2,999))
	. . . . write " onClick='if (confirm(""\"""_WORD_"\"" "_$$^WWWTEXT(33954)_""")) retval = EventValue("""_YUCI_""","""_YUSER_""","""_YFORM_""",""FIX"",""WWWDICTIONARY2"","""",""6"","""_WORD_""");'"
	. . . . write YCR," TITLE="""_$$^WWWTEXT(33954,,1)_""""   ; "Save Unknown Word in Directory"
	. . . . write YCR,">"
	. . . ;
	. . . write YCR,$$^WWWUML($piece(YINHALT," ",YI2),1)
	. . . if PRUEF(1)=1 write YCR,"</A>"
	. . . ;
	. . . if PRUEF(1)=1 write YCR,"</em>"
	. . . write " "
	. . ;
	. . write "<BR>",YCR
 
	if YEDITOR'=1 {
		write "</TD>",YCR
		write "</TR>",YCR
		write "</TABLE>"
	}
 
	quit
	
HYPERLINK ;EINFÜGEN LINK ;interpolate 
	new YHKEY,YHFORM,YHTEXT,YHTITLE,YHBACK
	
	set YHTEXT  = $piece(YINHALT,":",2)
	set YHFORM  = $piece(YINHALT,":",3)    if $extract(YHFORM)="@"  set YHFORM  = @($extract(YHFORM,2,99))
	set YHKEY   = $piece(YINHALT,":",4)    if $extract(YHKEY)="@"   set YHKEY   = @($extract(YHKEY,2,99))
	set YHTITLE = $piece(YINHALT,":",5)    if $extract(YHTITLE)="@" set YHTITLE = @($extract(YHTITLE,2,99))
	set YHBACK  = YFORM
	
	quit:YHFORM=""
	quit:'$data(^WWW120(0,YHFORM))
	
	do
	. new YKEY,YFORM,YBACK
	. write "<A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM="_YHFORM_"&amp;YKEY="_YHKEY  ;_"&amp;YBACK="_YHBACK_","
	. do ^WWWCGI
	. write """"
	. if YHTITLE'="" write " TITLE="""_YHTITLE_""""
	. write ">"
	
	write "<U>"_$get(YHTEXT)_"</U>"
	write "</A>",YCR
	quit
	
	
BUTTON()
	;-------------------------------------------------------------------------------
	; Print button help text
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 06-Jul-2007	Karine	SRBR014589: Show customized name of buttons if exists.
 	; 20-Jun-2007	RPW		SR15539: Created (rewritten from . syntax)
	;-------------------------------------------------------------------------------
	new intPosition,objButton,idButton,idxForm,idxLang,strPicture,strDesc
	
	; "In addition to the standard-buttons this form contains buttons to link into the following functions:"
	if $data(^WWW124(0,YFORM,SPRACHE)) set YTEXT="|<b>"_$$^WWWTEXT(33862)_"</b>" do TEXT($$$NO)
	
	set idxForm = $$$Index(YFORM)
	set idxLang = $$$Index(SPRACHE)
	
	set intPosition = ""
	for {
		set intPosition = $order(^WWW124s(0,2,idxForm,idxLang,intPosition))
		quit:intPosition=""
		
		set idButton = ""
		for {
			set idButton = $order(^WWW124s(0,2,idxForm,idxLang,intPosition,YFORM,SPRACHE,idButton))
			quit:idButton=""
			
			set objButton  = $get(^WWW124(0,YFORM,SPRACHE,idButton,1))
			set strPicture = $$$WWW124PictureFileOnButton(objButton)
			set strDesc    = $$GetButtonDescription^WWW124(YFORM,SPRACHE,idButton)
			
			if strPicture="" set strPicture = $$$LOWER($extract(strDesc))_".gif"
			set YTEXT = strPicture_"|<U><B>"_$$^WWWUML(strDesc)_"</B></U>|"
			
			do DrawButton(objButton)
		}
	}
	quit
	
	
DrawButton(pobjButton)
	;-------------------------------------------------------------------------------
	; Draw the button info onto the screen 
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 20-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new strHelp,strNewForm,strText
	
	set strText    = ""
	set strNewForm = $$$WWW124NewFormOnClick(pobjButton)
	
	if strNewForm'="" {
		set strText = $$$WWW127HelpText($get(^WWW127(0,strNewForm,"M",0,SPRACHE,1)))
		if strText="" set strText = $$$WWW127HelpText($get(^WWW127(0,strNewForm,"M",1,SPRACHE,1)))
	}
	
	set strText = $piece(strText,"</B>",1)
	set strText = $piece(strText,"</b>",1)
	
	if $length(strText)>$$$TEXTLENGTH set strText=$extract(strText,0,$$$TEXTLENGTH)
	if $translate(strText," ")'="" set YTEXT = YTEXT_strText
	set strHelp = $$$WWW124Hilfe(pobjButton)
	if strHelp'="" set:YTEXT'="" YTEXT = YTEXT_" " set YTEXT = YTEXT_strHelp
	if $length(YTEXT)>10 {
		if '$find(YTEXT,"Assistance Text") && '$find(YTEXT,"Help Text") {
			set YTEXT = "|"_YTEXT
			do TEXT($$$NO)
		}
	}
	quit
	
	
CanCustomize(pintBEDBER,&pblnCanCustomize) 
    ;/*-------------------------------------------------------------------------------
    ; If the user is an Administrator or DB Administrator then they can customize.
    ; 
    ; Let the user know which level they are and the fact they can customize
    ;
    ; Params:
    ; pintBEDBER      : The security level of the user
    ; pblnCanCustomize: Whether this user can customize the form.
    ;
    ; Returns:
    ; 0 - Can not customize
    ; 1 - Can Customize and is the System Administrator
    ; 2 - Can Customize and is the DB Administrator
    ;
    ; History:
    ; 02-Dec-2005	RPW		SR13940: Created
    ;-------------------------------------------------------------------------------*/
	new intAdmin
	
	set intAdmin = 0 
	if +pintBEDBER=1 set intAdmin = 1   ;SYSTEM ADMINISTRATOR
	if +pintBEDBER=2 set intAdmin = 2   ;DATABASE ADMINISTRATOR
	
	set pblnCanCustomize = (intAdmin'=0)
	
	quit intAdmin
	
	
FormLink()
	]]><![CDATA[;-------------------------------------------------------------------------------
	; Write out the class/form link
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Oct-2006	SteveS	SR15094: Created (moved from START^WWWHELP)
	;-------------------------------------------------------------------------------
	new YKEY,YFKEY,idClass
	
	write YCR,"<TABLE CELLSPACING=0 BORDER=0>"
	write "<TR>"
	write "<TD ALIGN=RIGHT width=100></TD>"
	
	write "<TD ALIGN=RIGHT width=200>"
	write "Form-Name: " 
	write "</TD>"
	
	write "<TD><A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM=WWW120&amp;YKEY="_YFORM
	do ^WWWCGI
	write """>"
	write YFORM
	write "</A>",YCR
	write "<BR>"
	write "</TD></TR>"
	
	set idClass = $$$WWW120ClassUsedInForm(YVOR)
	
	if (idClass'="") {
		write "<TR><TD ALIGN=RIGHT width=100></TD><TD ALIGN=RIGHT width=200>" 
		write "Class-Name: " 
		write "</TD>"
		
		write "<TD><A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM=WWW001&amp;YKEY="_idClass
		do ^WWWCGI
		write """>"
		write idClass
		write "</A>",YCR
		write "<BR>"
		write "</TD></TR>"
	}
 
	do ^WWWFRAME(1)
	write "<BR><BR>"
 
	quit
	
	
FormImage()
	;-------------------------------------------------------------------------------
	; Show the form image
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 18-Mar-2008	GM		SRBR014883: Make appear the pictures of screens in "WWWHPR" form
	; 27-Oct-2006	SteveS	SR15094: Created (moved from START^WWWHELP)
	;-------------------------------------------------------------------------------
	new PATH,objCompany
	
	set objCompany = $get(^WWW012(0,0,1))
 
	set FILE2 = $zconvert("FORM_"_YFORM_"_"_SPRACHE_"_"_YSEITE_".HTM","L")  ;HTML
	set PATH  = ##Class(%File).NormalizeDirectory($$$WWW012PhysicalWWWDirectory(objCompany))
	
	quit:$$^WWWFILECHECK(PATH_$piece(FILE2,".",1)_".jpg")'=1
	quit:$get(blnEditGridHelp)=1
	
	write YCR,"<CENTER><IMG SRC="""_YGIF_$piece(FILE2,".",1)_".jpg"" BORDER=1></CENTER>"  ; display image 
	quit
	
	
ShowHelp(pidLang,pidChildUser)
	;-------------------------------------------------------------------------------
	; Show the Help Text
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new strType
	
	for strType="M","P","LP","LD" {
		do Tabs(strType)
		do HelpText(strType,pidLang,pidChildUser)
	}
	quit
	
	
Tabs(pstrType)
	;-------------------------------------------------------------------------------
	; Show the Tabs 
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
 	new strPictureFile
 
	if (YSEITE=1) && (pstrType="P") && ($data(^WWW1203(0,YFORM,SPRACHE,1))) {
		do FF^WWWW()
		write "<HR COLOR="_YBLUE_">"
		set strPictureFile=$$$WWW1203PictureFileForPageTag($get(^WWW1203(0,YFORM,SPRACHE,1,1)))
		if strPictureFile="" {
			if SPRACHE="DE" {
				set strPictureFile="seite1.gif"
			} else {
				set strPictureFile="page1.gif"
			}
		}
		if $find($$$UPPER(strPictureFile),".GIF") {
			write "<img src="""_YGIF_strPictureFile_""">"
		} else {
			write YCR,"<H3>",strPictureFile,"</H3>"
		}
		write "<HR COLOR="_YBLUE_">"
	}
	quit
	
	
HelpText(pstrType,pidLang,pidChildUser)
	;-------------------------------------------------------------------------------
	; Show the actual help text data
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new blnNoPrinting,idField,blnQuit
	
	set blnQuit=$$$NO

	if '$data(^WWW127(0,YFORM,"M",0,SPRACHE)) set ^WWW127(0,YFORM,"M",0,SPRACHE,1) = ""
	set idField = ""
	for {
		set idField = $order(^WWW127(0,YFORM,pstrType,idField))
		quit:idField=""
		quit:$$FieldChecks(pstrType,idField)
		
		set blnNoPrinting = $$$YES
		
		if pstrType="P" {
			if YDATEI'="" {
				if $data(^WWW002(0,YDATEI,idField)) {
					set blnNoPrinting=$$$NO
				}
				
				if +YADMIN=0 && ($translate($$$WWW121FixedInputForHiddenField($get(^WWW121(0,YFORM,idField,1)))," ")'="") {
					set blnQuit=$$$YES
				} else {
					write "<br><h4>"
					if +YADMIN'=0 {
						do LocationOnLineHelp(idField,pidLang,pidChildUser)
					}
					do FieldDescription(idField,.blnQuit)
					if YADMIN=1 write "</A>"
					write "</h4>"
				}
			}
		}
		
		if blnQuit {
			set blnQuit=$$$NO
		} else {
			do:pstrType="M" ManualField(idField,.blnNoPrinting,pidLang,pidChildUser)
			if $extract(pstrType)'="L" {
			;	set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""","""_pstrType_""","""_idField_""","""_idLang_""","""_1_""")")
				set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""","""_pstrType_""","""_idField_""","""_pidLang_""","""_1_""")")
				if YTEXT="" set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""","""_pstrType_""","""_idField_""",""EN"","""_1_""")")
				set $piece(YTEXT,Y,4)=""   ;WEM;25188;02.03.2004;KEINE ANZEIGE DES SCHULUNGSPARAMETERS IN DER ONLINEHILFE
				;SR16925.2 do GetTrainingText(pstrType,idField)
				do ShowText(pstrType,blnNoPrinting,idField)
			}
		}
	}
	quit
	
	
FieldChecks(pstrType,pidField)
	;-------------------------------------------------------------------------------
	; Check whether the field should be shown
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new blnQuit,objWWW122
	
	set blnQuit=$$$NO
 
	if pstrType="M" {
		if pidField=1 {
			set objWWW122 = $get(^WWW122(0,YFORM,1,1))
			if (objWWW122'="") && ($$$WWW122SequenceNumber(objWWW122)="") {
				set blnQuit = $$$YES
			}
		} else {
			if ((pidField'=0) && (pidField'=1)) || ($$$WWW122DataInputType($get(^WWW122(0,YFORM,pidField,1)))=15) {
				set blnQuit = $$$YES
			}
		}
	} elseif pstrType="P" {
		set blnQuit = ($$$WWW121TypeOfAcquisition($get(^WWW121(0,YFORM,pidField,1)))=15)
	}
	quit blnQuit
	
	
FieldDescription(pidField,&pblnQuit)
	;-------------------------------------------------------------------------------
	; Get the field description (probably equiv to WWWFELDNAME)
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	if $translate($$$WWW121FixedInputForHiddenField($get(^WWW121(0,YFORM,pidField,1)))," ")'="" {
		set pblnQuit=$$$YES
		
	} elseif $$$WWW121D1FieldDescription($get(^WWW121D1(0,YFORM,pidField,YM,SPRACHE,1)))'="" {
		write $$$WWW121D1FieldDescription($get(^WWW121D1(0,YFORM,pidField,YM,SPRACHE,1)))
		
	} elseif $$$WWW121DFieldDescription($get(^WWW121D(0,YFORM,pidField,YM,1)))'="" {
		write $$$WWW121DFieldDescription($get(^WWW121D(0,YFORM,pidField,YM,1)))
		
	} elseif $data(^WWW0021(0,YDATEI,pidField,SPRACHE,1)) {
		write $$$WWW0021PropertyDescription(^WWW0021(0,YDATEI,pidField,SPRACHE,1))
		
	} else {
		write $$$WWW002TextInForms($get(^WWW002(0,YDATEI,pidField,1)))
	}
	quit
	
	
LocationOnLineHelp(pidField,pidLang,pidChildUser)
	;-------------------------------------------------------------------------------
	; Show the Location based help text window
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
    ; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new strHelpText
	
	;set strHelpText = $$$WWW121DCustomHelpText($get(^WWW121D(0,YFORM,pidField,YM,1))) ;SR16925.2
	set strHelpText = $$CustomHelpText^WWW121D($get(^WWW121D(0,YFORM,pidField,YM,1)))
	
	write "<A"
	if strHelpText'="" write " Title="""_$$^WWWTEXT(33417)_""""   ; "Location Specific Online-Help"
	
	write " href='' onclick=""subWindow('"_YAKTION_"EP=WWWFORM&amp;YFORM=WWW121D"
	write "&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
	write "&amp;YUSER="_pidChildUser			//SR11904
	write "&amp;SPRACHE="_pidLang
	write "&amp;YFKEY="_YFORM_","_pidField_","_YM
	write "&amp;YKEY="_YFORM_","_pidField_","_YM
	write "&amp;YANZ="_YANZ
	write "','TEILEFRAME1'); return false"">"
	
	if strHelpText'="" write "&nbsp;<font color="_YRED_">(!)</FONT>"
	write " <IMG SRC="_YGIF_"dflt.gif border=0 TITLE="_$$^WWWTEXT(33400)_">"         ; "Customizing"
	write "</A>"
	write "&nbsp;"
 
	quit
	
	
ManualField(pidField,&pblnNoPrinting,pidLang,pidChildUser)
	;-------------------------------------------------------------------------------
	; Show the Manual Fields help text
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 25-May-2011	shobby	SR16925.2: Don't show the button for Manual Field 0
	;							Which is actually form level help.  Already
	;							has one button.
    ; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new objWWW122D,strDesc,strHelpText,strSequence
	
	;quit:'$data(^WWW122(0,YFORM,pidField,1))
	
	set pblnNoPrinting = $$$NO
	set strSequence    = $$$WWW122SequenceNumber($get(^WWW122(0,YFORM,pidField,1)))
	quit:(strSequence'="")  ; data item rather than manual field
	
	write "<BR>"
	write "<H4>"
	
	if (+YADMIN'=0)&&(pidField'=0) {	;SR16925.2
		write "<A"
		
		set objWWW122D=$get(^WWW122D(0,YFORM,pidField,YM,1))
		set strHelpText=$$CustomHelpText^WWW122D(objWWW122D)  ;SR16925.2
		if strHelpText'="" write " Title="""_$$^WWWTEXT(33417)_""""      ; "Location Specific Online-Help"
	
		write " href='' onclick=""subWindow('"_YAKTION_"EP=WWWFORM&amp;YFORM=WWW121D"
		write "&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
		write "&amp;YUSER="_pidChildUser			//SR11904
		write "&amp;SPRACHE="_pidLang
		write "&amp;YFKEY="_YFORM_","_pidField_","_YM
		write "&amp;YKEY="_YFORM_","_pidField_","_YM
		write "&amp;YANZ="_YANZ
		write "','TEILEFRAME1'); return false"">"
	
		if strHelpText'="" write "&nbsp;<font color="_YRED_">(!)</FONT>"
		write " <IMG SRC="_YGIF_"dflt.gif border=0 TITLE="_pidField_"::"_$$^WWWTEXT(33400)_">"   ; "Customizing"
		write "</A>"
		write "&nbsp;"
	}
	
	set strDesc = $$$WWW122D1FieldDescription($get(^WWW122D1(0,YFORM,pidField,YM,SPRACHE,1)))
	if strDesc="" set strDesc = $$$WWW122DFieldDescription($get(^WWW122D(0,YFORM,pidField,YM,1)))
	if strDesc="" set strDesc = $get(^WWW1221(0,YFORM,pidField,SPRACHE,1))
	if strDesc="" set strDesc = $piece($get(^WWW122(0,YFORM,pidField,1)),Y,12)
	
	write strDesc
	if YADMIN=1 write "</A>"
	write "</H4>"
	
	quit
	
	
GetFormText(YTEXT,pblnNoPrint=$$$NO) ;SR16925
	;-------------------------------------------------------------------------------
	; Get the help text for the form
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-May-2011	shobby	SR16925.2: Created
	;-------------------------------------------------------------------------------
	new objWWW120D,strText
	
	set strText = ""
	
	set objWWW120D = $get(^WWW120D(0,YFORM,YM,1))

	if $$$WWW120DHideAlphalincHelpText(objWWW120D) {
		set strText = ""
	} else {
		set strText = $$$WWW127HelpText(YTEXT)
	}

	if '$$$WWW120DHideCustomHelpText(objWWW120D) {
		set strText = strText_"||"_$$CustomHelpText^WWW120D(objWWW120D)
	}
	if '$$$WWW120DHideCustomTrainingText(objWWW120D) {
		set strText = strText_"||"_$$CustomTrainingText^WWW120D(objWWW120D)
	}
	set strText = $zstrip(strText,"<W","|")
	if 'pblnNoPrint if strText="" set strText = $$^WWWTEXT(28) ; "No Help Text Available"
	quit strText


GetPrimaryKeyText(YTEXT,pblnNoPrint=$$$NO,pidField) ;SR16925
	;-------------------------------------------------------------------------------
	; Get the help text for the primary key
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-May-2011	shobby	SR16925.2: Created
	;-------------------------------------------------------------------------------
	new objWWW121D,strText
	
	set strText = ""
	
	set objWWW121D = $get(^WWW121D(0,YFORM,pidField,YM,1))

	if $$$WWW121DHideAlphalincHelpText(objWWW121D) {
		set strText = ""
	} else {
		set strText = $$$WWW127HelpText(YTEXT)
	}

	if '$$$WWW121DHideCustomHelpText(objWWW121D) {
		set strText = strText_"||"_$$CustomHelpText^WWW121D(objWWW121D)
	}
	if '$$$WWW121DHideCustomTrainingText(objWWW121D) {
		set strText = strText_"||"_$$CustomTrainingText^WWW121D(objWWW121D)
	}
	set strText = $zstrip(strText,"<W","|")
	if 'pblnNoPrint if strText="" set strText = $$^WWWTEXT(28) ; "No Help Text Available"
	quit strText
	
 
GetDataFieldText(YTEXT,pblnNoPrint=$$$NO,pidField) ;SR16925
	;-------------------------------------------------------------------------------
	; Get the help text for the data field
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-May-2011	shobby	SR16925.2: Created
	;-------------------------------------------------------------------------------
	new objWWW122D,strText

	set strText = ""
	
	set objWWW122D = $get(^WWW122D(0,YFORM,pidField,YM,1))

	if $$$WWW122DHideAlphalincHelpText(objWWW122D) {
		set strText = ""
	} else {
		set strText = $$$WWW127HelpText(YTEXT)
	}

	if '$$$WWW122DHideCustomHelpText(objWWW122D) {
		set strText = strText_"||"_$$CustomHelpText^WWW122D(objWWW122D)
	}
	if '$$$WWW122DHideCustomTrainingText(objWWW122D) {
		set strText = strText_"||"_$$CustomTrainingText^WWW122D(objWWW122D)
	}
	set strText = $zstrip(strText,"<W","|")
	if 'pblnNoPrint if strText="" set strText = $$^WWWTEXT(28) ; "No Help Text Available"
	quit strText
	
 
ShowText(pstrType,blnNoPrint,pidField)
	;-------------------------------------------------------------------------------
	; Show the actual text
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 27-May-2011	shobby	SR16925.2: Simplified, subroutined and suppress display with
	;							related checkboxes.
    ; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
	; 20-Jun-2007	RPW		SR15534: WWW120D does not have a field number attached,
	;							fix from original code.
	; 19-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new blnFormHelp,objWWW120D,objWWW121D,strText
	
	if pidField=0 {
		set YTEXT = $$GetFormText(YTEXT,blnNoPrint)
	} else {
		set YTEXT = $$GetPrimaryKeyText(YTEXT,blnNoPrint,pidField)
	}
	do TEXT($$$NO)
	set blnFormHelp = ( (pstrType="M") && ((pidField=0) || (pidField=1)) )
	do:blnFormHelp BUTTON()
	
	quit
	
	
DataFields(pidLang)
	;-------------------------------------------------------------------------------
	; Draw the Data Fields for a form
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 27-May-2011	shobby	SR16925.2: Replaced code with call to GetDataFieldText
	; 25-May-2011	shobby	SR16925.2: IndividualTrainingText -> CustomTrainingText
    ; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
	; 06-Sep-2007	GRF		SRBR014657: test (blnHide'=$$$YES) rather than (blnHide="")
	; 28-Aug-2007	GM		SRBR014657: Include pidTab as parameter and YM as company
	; 20-Aug-2007	GM		SRBR014657: Hidden tabs are shown as gray for System
	; 							and Database Administrator users  
	; 15-Aug-2007	shobby	SRBR014669: Call to TextWrapper
	; 07-Aug-2007	GM		BR014593: Condition included if hidden field in WWW122 was selected
	; 24-Jul-2007	GM		BR014593: Data for hidden field will be shown just for
	; 							System Administrator user
	; 20-Jun-2007	RPW		SR15539: Created
	;-------------------------------------------------------------------------------
	new strType,idxForm,idTab,strPictureFile,objWWW122D,blnShowCustomisedOnly,strCustomised,arrFields
	new intRow,intCol,idField,objWWW122,objWWW1203D,blnHide 
	
	set strType = "D"
	set idxForm = $$$Index(YFORM)
	set idTab   = ""
	for {
		set idTab = $order(^WWW122s(0,3,idxForm,idTab))
		quit:idTab=""
		continue:(YSEITE'=1)&&(idTab'=YSEITE)
		
 		set objWWW1203D = $get(^WWW1203D(0,YFORM,SPRACHE,idTab,YM,1))
		set blnHide = $$$WWW1203DHide(objWWW1203D)
 
		if idTab'=1 {
			if $$CanCustomize($$$WWW013UserAccess($get(^WWW013(0,YBED,1)))) || (blnHide'=$$$YES) {
				write "<BR><HR COLOR="_YBLUE_">"
				set strPictureFile = $$$WWW1203PictureFileForPageTag($get(^WWW1203(0,YFORM,SPRACHE,idTab,1)))
				if (strPictureFile="") && (idTab<11) {
					if SPRACHE="DE" {
						set strPictureFile = "seite"_idTab_".gif"
					} else {
						set strPictureFile = "page"_idTab_".gif"
					}
				}
				
				if $find($$$UPPER(strPictureFile),".GIF") {
					write "<IMG SRC="""_YGIF_strPictureFile_""">"
				} else {
					if blnHide {  
						write "<H3><font color=#B6B6B6>"_strPictureFile_"</font></H3>"
					} else {
						write "<H3>"_strPictureFile_"</H3>"
					}
				}
				write "<HR COLOR="_YBLUE_">"
			}
		}
		
		do GetFieldsForPage(YFORM,idTab,.arrFields)
		
		set intRow = ""
		for {
			set intRow = $order(arrFields(intRow))
			quit:intRow=""
			
			set intCol = ""
			for {
				set intCol = $order(arrFields(intRow,intCol))
				quit:intCol=""
				
				set idField = ""
				for {
					set idField = $order(arrFields(intRow,intCol,idField))
					quit:idField=""
					continue:$$HideField(YFORM,idField)
					
		; FIXME : <GRF> should pull <H4> tag from ShowCustomiseButton and put it here since may not get executed
		;               NOTE : Test in ShowCustomiseButton matches $$HideField
					
					do ShowCustomiseButton(YFORM,idField)
					do ShowFieldName(YFORM,idField,idTab)
		;		 	do ShowFieldName(YFORM,idField,YLFNN,YDATEI,idTab)    ; replacement for previous line if ShowFieldNameNew is renamed (BR014669)
					
		; FIXME : <GRF> should pull </H4> tag from ShowFieldName and put it here since it is always executed
					
					if YLFNN'="" {
						set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""","""_strType_""","""_YLFNN_""","""_pidLang_""","""_1_""")")
						if YTEXT="" set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""","""_strType_""","""_YLFNN_""",""EN"","""_1_""")")
						if YTEXT="" set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""","""_strType_""","""_YLFNN_""",""DE"","""_1_""")")
					} else {
						set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""",""M"","""_idField_""","""_pidLang_""","""_1_""")")
						if YTEXT="" set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""",""M"","""_idField_""",""EN"","""_1_""")")
						if YTEXT="" set YTEXT = $$^WWWSETL("^WWW127(0,"""_YFORM_""",""M"","""_idField_""",""DE"","""_1_""")")
					}
					
					set YTEXT     = $$GetDataFieldText(YTEXT,$$$NO,idField)
					set objWWW122 = $$Get^WWW122(YFORM,idField)        ;BR014593  ;BR014669
					set YTEXT = $$TextWrapper(YDATEI,$$$WWW122SequenceNumber(objWWW122),YTEXT,"D")         ;BR014669
				;	if ($$$WWW122DHiddenField(objWWW122D)) || ($$$WWW122DataInputType(objWWW122)=15) {  ;BR014593 	;BR014669
					if ($$$WWW122InputType(objWWW122)=0) || blnHide {                      ;BR014593 ;BR014669 ;BR014657 Hidden
						if $$CanCustomize($$$WWW013UserAccess($get(^WWW013(0,YBED,1)))) {  ;BR014593
							do TEXT($$$YES)
						}
					} else {
						do TEXT($$$NO)
					}
				}
			}
		}
	}
	quit
	
]]></Routine>
</Export>