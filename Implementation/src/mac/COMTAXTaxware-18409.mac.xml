<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMTAXTaxware" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMTAXTaxware
 
#include COMConst
#include COMSYS
 
#define SalesTaxCode			1
#define UseTaxCode				2
 
#define StateSalesTaxType		1
#define CitySalesTaxType		2
#define CountySalesTaxType		3
#define CityTransitTaxType		4
#define CountyTransitTaxType	5
 
LoadVeraTaxFile(filename,EffectDate,LoadCompany,Log)
	;-------------------------------------------------------------------------------
	; routine to load VeraTax tax tables
	; 
	; History:
	; 29-Mar-2006 	SC		SR13942: No longer set environment within routine. 
	; 						Maintained by calling function.
	; 14-Dec-2005	GRF		Boolean Macros
	; 22-Feb-2005	Paul K	Reduce incidence of calling WWWVAR
	;-------------------------------------------------------------------------------
	new prevIO,prevMode,Line,Status,txtlist
 
	set filename   = $get(filename)				; allow for empty parameters
	set EffectDate = $get(EffectDate)			; allow for empty parameters
	set:(EffectDate="") EffectDate=+$horolog	; default to today
	set Log        = $get(Log)					; allow for empty parameters
	
	set Status=$$$OK		; Default to OK
	;if $get(Y)="" do ^WWWVAR ;SR13942 reversed
	;set:($get(LoadCompany)'="") YM=LoadCompany  
 
	do:(Log="") StartLog^COMLog($$^WWWTEXT("Com00077"),.Log)		; Taxware Load
	
	open filename:"RS":0
	if ('$TEST) {
		set txtlist=$listbuild("Com00049",filename) ; Could not retrieve file
		set Status=$$$NO
		do AddMessage^COMLog(Log,$$DecodeError^COMUtils(txtlist))
	}
	
	if ($piece(Status,Y,1)=$$$OK) {
		; Do any initialisation
		set txtlist=$listbuild("Com00051",filename) ; Now attempting to process the file
		do AddMessage^COMLog(Log,$$DecodeError^COMUtils(txtlist))
 
		set Status=$$LoadVeraTaxStart()
		if ('$piece(Status,Y,1)) {
			do AddMessage^COMLog(Log,$piece(Status,Y,2))
		}
	}
	
	if ($piece(Status,Y,1)=$$$OK) {
		; ready to process the file
 
		set prevIO   = $IO
		set prevMode = $ZUTIL(68,40) 
		do $ZUTIL(68,40,1) ; Set to Flag EOF
		use filename
	
		for {
			read Line
			quit:$ZEOF
	
			if ($extract(Line,3,9)="       ") {
				; give the user some feedback
				set txtlist=$listbuild("Com00046","US State "_$extract(Line,1,2)) ; Now attempting to load
				do AddMessage^COMLog(Log,$$DecodeError^COMUtils(txtlist))
 
				set Status=$$LoadVeraTaxState(Line,EffectDate)
			} else {
				set Status=$$LoadVeraTaxLocal(Line,EffectDate)
			}		
			if ('$piece(Status,Y,1)) {
				do AddMessage^COMLog(Log,$piece(Status,Y,2))
				quit
			}
		}
		
		; close the file
		use prevIO
		do $ZUTIL(68,40,prevMode)
		close filename
	}
	
	if ($piece(Status,Y,1)=$$$OK) {
		; Everything OK
		set txtlist=$listbuild("Com00056",filename) ; Processing Complete
		do AddMessage^COMLog(Log,$$DecodeError^COMUtils(txtlist))
	}
	
	quit Status
	
LoadVeraTaxStart()
	;-------------------------------------------------------------------------------
	;-------------------------------------------------------------------------------
	new LocationID,Status
	
	set Status=$$$OK	; Default to OK
	
	; Create a default Tax location for US
	set LocationID="US"
	if ('$data(^COMTAXLocation(YM,LocationID,1))) {
		; Set up New Tax Location information
		set Status=$$CreateTaxLocation(LocationID,"US","US","","","")
	}
	
	quit Status
	
LoadVeraTaxState(Line,EffectDate)
	;-------------------------------------------------------------------------------
	;-------------------------------------------------------------------------------
	new Status,LocationID
	
	set Status=$$$OK	; Default to OK
	set LocationID=$translate("US"_$extract(Line,1,2)," ")
	
	if ('$data(^COMTAXLocation(YM,LocationID,1))) {		; Set up State Tax Location information
		set Status=$$CreateTaxLocation(LocationID,
									$$Trim^COMUtilStr($extract(Line,10,35)),
									"US",
									$$LookupStatePO($extract(Line,1,2)),
									"",
									"US")
	}
	
	if (Status=$$$OK) {		; Save the State Sales Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$SalesTaxCode,$$$StateSalesTaxType,EffectDate,($extract(Line,36,42)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the State Sales Use Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$UseTaxCode,$$$StateSalesTaxType,EffectDate,($extract(Line,50,56)*100),"")
	}
	
	quit Status
	
LoadVeraTaxLocal(Line,EffectDate)
	;-------------------------------------------------------------------------------
	;-------------------------------------------------------------------------------
	new Status,StateLocID,LocationID
	
	set Status=$$$OK	; Default to OK
	set StateLocID=$translate("US"_$extract(Line,1,2)," ")
	set LocationID=$translate("US"_$extract(Line,1,9)," ")
	
	if ('$data(^COMTAXLocation(YM,LocationID,1))) {		; Set up Local City Tax Location information
		set Status=$$CreateTaxLocation(LocationID,
									$$Trim^COMUtilStr($extract(Line,10,35))_" - "_$$Trim^COMUtilStr($extract(Line,64,89)),
									"US",
									$$LookupStatePO($extract(Line,1,2)),
									$$Trim^COMUtilStr($extract(Line,3,7)),
									"US"_$extract(Line,1,2))
	}
 
	if (Status=$$$OK) {		; Save the City Sales Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$SalesTaxCode,$$$CitySalesTaxType,EffectDate,($extract(Line,36,42)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the City Transit Sales Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$SalesTaxCode,$$$CityTransitTaxType,EffectDate,($extract(Line,43,49)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the City Use Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$UseTaxCode,$$$CitySalesTaxType,EffectDate,($extract(Line,50,56)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the City Transit Use Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$UseTaxCode,$$$CityTransitTaxType,EffectDate,($extract(Line,57,63)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the County Sales Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$SalesTaxCode,$$$CountySalesTaxType,EffectDate,($extract(Line,90,96)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the County Transit Sales Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$SalesTaxCode,$$$CountyTransitTaxType,EffectDate,($extract(Line,97,103)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the County Use Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$UseTaxCode,$$$CountySalesTaxType,EffectDate,($extract(Line,104,110)*100),"")
	}
	
	if (Status=$$$OK) {		; Save the County Transit Use Tax Rate
		set Status=$$CreateTaxEntry(LocationID,$$$UseTaxCode,$$$CountyTransitTaxType,EffectDate,($extract(Line,111,117)*100),"")
	}
	
	quit Status
	
LookupStatePO(StateCode)
	;-------------------------------------------------------------------------------
	; Routine to take a StateCode and return a PO
	;-------------------------------------------------------------------------------
	new StatePO
	
	set StatePO=StateCode	; default to code
	
	; TODO: Would it not be better to store data separate from the programming code?
	
	set:(StateCode="01") StatePO="AL"
	set:(StateCode="02") StatePO="AK"
	set:(StateCode="03") StatePO="AZ"
	set:(StateCode="04") StatePO="AR"
	set:(StateCode="05") StatePO="CA"
	set:(StateCode="06") StatePO="CO"
	set:(StateCode="07") StatePO="CT"
	set:(StateCode="08") StatePO="DE"
	set:(StateCode="09") StatePO="DC"
	set:(StateCode="10") StatePO="FL"
	set:(StateCode="11") StatePO="GA"
	set:(StateCode="12") StatePO="HI"
	set:(StateCode="13") StatePO="ID"
	set:(StateCode="14") StatePO="IL"
	set:(StateCode="15") StatePO="IN"
	set:(StateCode="16") StatePO="IA"
	set:(StateCode="17") StatePO="KS"
	set:(StateCode="18") StatePO="KY"
	set:(StateCode="19") StatePO="LA"
	set:(StateCode="20") StatePO="ME"
	set:(StateCode="21") StatePO="MD"
	set:(StateCode="22") StatePO="MA"
	set:(StateCode="23") StatePO="MI"
	set:(StateCode="24") StatePO="MN"
	set:(StateCode="25") StatePO="MS"
	set:(StateCode="26") StatePO="MO"
	set:(StateCode="27") StatePO="MT"
	set:(StateCode="28") StatePO="NE"
	set:(StateCode="29") StatePO="NV"
	set:(StateCode="30") StatePO="NH"
	set:(StateCode="31") StatePO="NJ"
	set:(StateCode="32") StatePO="NM"
	set:(StateCode="33") StatePO="NY"
	set:(StateCode="34") StatePO="NC"
	set:(StateCode="35") StatePO="ND"
	set:(StateCode="36") StatePO="OH"
	set:(StateCode="37") StatePO="OK"
	set:(StateCode="38") StatePO="OR"
	set:(StateCode="39") StatePO="PA"
	set:(StateCode="40") StatePO="RI"
	set:(StateCode="41") StatePO="SC"
	set:(StateCode="42") StatePO="SD"
	set:(StateCode="43") StatePO="TN"
	set:(StateCode="44") StatePO="TX"
	set:(StateCode="45") StatePO="UT"
	set:(StateCode="46") StatePO="VT"
	set:(StateCode="47") StatePO="VA"
	set:(StateCode="48") StatePO="WA"
	set:(StateCode="49") StatePO="WV"
	set:(StateCode="50") StatePO="WI"
	set:(StateCode="51") StatePO="WY"
	set:(StateCode="52") StatePO="CN"
	set:(StateCode="53") StatePO="IT"
	set:(StateCode="54") StatePO="PR"
	set:(StateCode="55") StatePO="VI"
	set:(StateCode="56") StatePO="PI"
	set:(StateCode="57") StatePO="GU"
	set:(StateCode="58") StatePO="AS"
	set:(StateCode="59") StatePO="PW"
	set:(StateCode="60") StatePO="AA"
	set:(StateCode="61") StatePO="AE"
	set:(StateCode="62") StatePO="AP"
 
	quit StatePO
 
CreateTaxLocation(LocationID,Name,Country,State,Zip,Parent)
	;-------------------------------------------------------------------------------
	;
	; History:
	; 15-Mar-2005	Paul K		Added macro usage
	;-------------------------------------------------------------------------------
	new Status,NewRecord,ok,txtlist
	
	set Status    = $$$OK
	set NewRecord = ""
	set $$$COMTAXLocationName(NewRecord)              = Name
	set $$$COMTAXLocationCountry(NewRecord)           = Country
	set $$$COMTAXLocationState(NewRecord)             = State
	set $$$COMTAXLocationZIPCode(NewRecord)           = Zip
	set $$$COMTAXLocationParentTaxLocation(NewRecord) = Parent
 
	set ok=$$^WWWSPEI("COMTAXLocation",LocationID,NewRecord,1)
	
	if (ok=0) {
		set Status = $$$NO_Y_$$^WWWTEXT("Com00072")	; Error returned from WWWSPEI
	} elseif (ok=9) {
		set Status = $$$NO_Y_$$^WWWTEXT("Com00074")	; Unable to save - not owner
	} elseif (ok'=1) {
		set txtlist= $listbuild("Com00075",ok) ; Unknown response(%1) from WWWSPEI
		set Status = $$$NO_Y_$$DecodeError^COMUtils(txtlist)
	}
	
	quit Status
	
CreateTaxEntry(LocationID,TaxCode,TaxType,EffectDate,Rate,GLAccount)
	;-------------------------------------------------------------------------------
	; 
	; History:
	; 17-Nov-2004	GRF		Replace YKOMMA with $$$COMMA
	;-------------------------------------------------------------------------------
	new Status,NewRecord,NewID,ok,txtlist,SaveNeeded,NearestDate
	
	set Status = $$$OK
	set NewID  = LocationID_$$$COMMA_TaxCode_$$$COMMA_TaxType_$$$COMMA_$piece(EffectDate,",",1)
 
	; Check to see if rate is still current
	set SaveNeeded=$$$OK
	set NearestDate=$order(^COMTAXTable(YM,LocationID,TaxCode,TaxType,EffectDate+1),-1)
	if (NearestDate'="") {
		; We have a previous rate
		if ($piece($get(^COMTAXTable(YM,LocationID,TaxCode,TaxType,NearestDate,1)),Y,1)+0=Rate) {
			set SaveNeeded=$$$NO	; It is the same rate
		}
	} else {
		if (Rate=0) {
			set SaveNeeded=$$$NO	; No need to save initial zeros
		}
	}
	
	if (SaveNeeded) {
		; Save an entered Rate
 
		set NewRecord=""
		set $piece(NewRecord,Y,1)=Rate
		set $piece(NewRecord,Y,2)=GLAccount
	
		set ok=$$^WWWSPEI("COMTAXTable",NewID,NewRecord,1)
		
		if (ok=0) {
			set Status = $$$NO_Y_$$^WWWTEXT("Com00072")	; Error returned from WWWSPEI
		} elseif (ok=9) {
			set Status = $$$NO_Y_$$^WWWTEXT("Com00074")	; Unable to save - not owner
		} elseif (ok'=1) {
			set txtlist= $listbuild("Com00075",ok) ; Unknown response(%1) from WWWSPEI
			set Status = $$$NO_Y_$$DecodeError^COMUtils(txtlist)
		}
	}
	
	quit Status
	
	
]]></Routine>
</Export>