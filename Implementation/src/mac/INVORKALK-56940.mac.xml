<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INVORKALK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INVORKALK(AUF,POS)
	;/------------------------------------------------------------------\
	; Description of Function :
	;		VORKALKULATION GUENSTIGSTE UND TEUERSTE HERSTELLKOSTEN
	;
	; Inputs : 
	;	AUF=AUFTRAG- ODER ANGEBOTSNUMMER ;Or 
	;	POS=POSITIONSNUMMER
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; FIS	23.07.2002
	;\------------------------------------------------------------------/
	NEW AUFTRAG,POS1,YEBENE,ARTIKEL
	
	SET AUF=$GET(AUF)     QUIT:AUF=""
	SET POS=$GET(POS)     QUIT:POS=""
	
	SET AUFTRAG=1  ;KALKULATION VON AUFTRAG ;cost estimating order 
	SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	IF POS1="" SET AUFTRAG=0 SET POS1=$GET(^INANGP(YM,AUF,POS,1))  ;KALKULATION VON ANGEBOT ;cost estimating proposition 
	QUIT:POS1=""
	
	IF AUFTRAG=1 QUIT:'$DATA(^INAUFPT(YM,AUF,POS))  ;NUR WENN EIGENFERTIGUNG ;only when 
	IF AUFTRAG=0 QUIT:'$DATA(^INANGPT(YM,AUF,POS))  ;NUR WENN EIGENFERTIGUNG ;only when 
	
	SET ARTIKEL=$PIECE(POS1,Y,4)  ;HAUPTARTIKEL
	KILL ^WWWSOR(YUSER)
	DO EBENE   ;ZERLEGEN UNTERTEILE IN EBENEN ;dissect within 
	DO KALK     ;NEURECHNEN HERSTELLKOSTEN AUS GÜNSTIGSTEN UND TEUERSTEN MATERIALEN ;out of And 
	KILL ^WWWSOR(YUSER)
	QUIT
	
EBENE ;ZERLEGEN UNTERTEILE IN EBENEN UND RECHNEN EK ;dissect within And have faith in Planned Cost 
	NEW SUCH,TEIL,SATZ
	;HAUPTTEIL
	SET ^WWWSOR(YUSER,1,AUF,POS,"1","0")=POS1  ;HAUPTTEIL=EBENE 1, TEIL 0
	;UNTERTEILE
	SET SUCH="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS_")"
	IF AUFTRAG=0 SET SUCH="^INANGPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS_")"
	FOR  DO  QUIT:SUCH=""
	. SET SUCH=$QUERY(@SUCH)
	. IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. ;IF $PIECE(SUCH,",",2)'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; 
	. IF $PIECE(SUCH,",",3)=POS DO
	. . SET SATZ=@SUCH
	. . QUIT:SATZ=""
	. . SET YEBENE=$QLENGTH(SUCH)-2
	. . SET TEIL=$PIECE(SUCH,",",4,99)
	. . SET TEIL=$TRANSLATE(TEIL,",)","..")
	. . IF ($DATA(@SUCH)'=10) && ($DATA(@SUCH)'=11) DO  ;MATERIAL UND LEISTUNG (KEINE WEITEREN UNTERTEILE) ;material And performance 
	. . . SET $PIECE(SATZ,Y,7)=1  ;WH BESTELLUNG ;sales order 
	. . . IF $PIECE(SATZ,Y,26)=2 SET $PIECE(SATZ,Y,7)=4  ;DIENSTLEISTUNG
	. . . IF ($PIECE(SATZ,Y,40)=2) || ($PIECE(SATZ,Y,40)=10) || ($PIECE(SATZ,Y,40)=11) SET $PIECE(SATZ,Y,7)=4  ;DIENSTLEISTUNG
	. . ;
	. . SET ^WWWSOR(YUSER,1,AUF,POS,YEBENE,TEIL)=SATZ
	
	QUIT
	
KALK ;NEURECHNEN HERSTELLKOSTEN HALBFERTIGUNGSTEILE AUS MATERIAL UND LEISTUNG ;out of material And performance 
	NEW TEIL,SATZ,STDKALK,YDATEI,YI,KOST
	SET STDKALK=$PIECE($$^INMASCHKOST(3),Y,2)  ;HOLEN STANDARDKALKULATIONSVORGABE ;send for 
	SET YEBENE="" FOR  SET YEBENE=$ORDER(^WWWSOR(YUSER,1,AUF,POS,YEBENE),-1) QUIT:YEBENE=""  DO
	. SET TEIL="" FOR  SET TEIL=$ORDER(^WWWSOR(YUSER,1,AUF,POS,YEBENE,TEIL)) QUIT:TEIL=""  DO
	. . SET SATZ=$GET(^WWWSOR(YUSER,1,AUF,POS,YEBENE,TEIL))
	. . QUIT:SATZ=""
	. . ;
	. . SET KOST(1)=0  ;NIEDRIGSTE HERSTELLKOSTEN
	. . SET KOST(2)=0  ;TEUERSTE HERSTELLKOSTEN
	. . ;
	. . ;MATERIAL UND LEISTUNGEN (KEINE WEITEREN UNTERTEILE) ;material And LEISTUNGEN 
	. . ;---------------------------------------------------
	. . IF $PIECE(SATZ,Y,7)=1!($PIECE(SATZ,Y,7)=4) DO  QUIT
	. . . ;
	. . . ;SPEICHERN EK UNTERTEILE (MATERIAL UND LEISTUNG) ;Save Planned Cost And 
	. . . SET YDATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . IF AUFTRAG=0 SET YDATEI="^INANGPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . FOR YI=1:1 QUIT:$PIECE(TEIL,".",YI,99)=""  SET YDATEI=YDATEI_","_$PIECE(TEIL,".",YI)
	. . . SET YDATEI=YDATEI_")"
	. . . IF $PIECE(SATZ,Y,7)=1 DO
	. . . . SET $PIECE(@YDATEI,Y,269)=$$^INARTKOSTT($PIECE(SATZ,Y,4),$PIECE(SATZ,Y,5),,,ARTIKEL,TEIL,3)  ;NIEDRIGSTE HK
	. . . . SET $PIECE(@YDATEI,Y,270)=$$^INARTKOSTT($PIECE(SATZ,Y,4),$PIECE(SATZ,Y,5),,,ARTIKEL,TEIL,5)  ;TEUERSTE HK
	. . . . QUIT
	. . . IF $PIECE(SATZ,Y,7)=4 DO
	. . . . SET $PIECE(@YDATEI,Y,269)=$$^INARTKOSTT($PIECE(SATZ,Y,4),0,$PIECE(SATZ,Y,45),,ARTIKEL,TEIL,3)  ;NIEDRIGSTE HK
	. . . . SET $PIECE(@YDATEI,Y,270)=$PIECE(@YDATEI,Y,269)  ;BEI LEISTUNGEN IST TEUERSTE HK = NIEDRIGSTE HK  ;next to LEISTUNGEN 
	. . . ;
	. . . SET $PIECE(@YDATEI,Y,271)=$PIECE(SATZ,Y,7)  ;WARENHERKUNFT
	. . . SET $PIECE(@YDATEI,Y,272)=$PIECE(SATZ,Y,7)  ;WARENHERKUNFT
	. . ;
	. . ;EIGENFERTIGUNG (VS. BESTELLUNG)
	. . ;-------------------------------
	. . IF TEIL=0 DO  ;HAUPTTEIL
	. . . SET TEIL(1)=TEIL
	. . . FOR  SET TEIL(1)=$ORDER(^WWWSOR(YUSER,1,AUF,POS,YEBENE+1,TEIL(1))) QUIT:TEIL(1)=""  IF $PIECE(TEIL(1),".",2)="" DO
	. . . . SET YDATEI(1)="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . . IF AUFTRAG=0 SET YDATEI(1)="^INANGPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . . FOR YI=1:1 QUIT:$PIECE(TEIL(1),".",YI,99)=""  SET YDATEI(1)=YDATEI(1)_","_$PIECE(TEIL(1),".",YI)
	. . . . SET YDATEI(1)=YDATEI(1)_")"
	. . . . SET SATZ(1)=@YDATEI(1)  ;SATZ NEU HOLEN WEGEN ÄNDERUNGEN AUS BEARBEITEN VORHERIGER EBENE ;typesetting recent send for quibble out of plain 
	. . . . IF +$PIECE(SATZ(1),Y,5)=0 SET $PIECE(SATZ(1),Y,5)=1
	. . . . SET KOST(1)=KOST(1)+($PIECE(SATZ(1),Y,269)*$PIECE(SATZ,Y,5))  ;GÜNSTIGSTE HK
	. . . . SET KOST(2)=KOST(2)+($PIECE(SATZ(1),Y,270)*$PIECE(SATZ,Y,5))  ;TEUERSTE HK
	. . . . SET $PIECE(SATZ,Y,271)=3  ;WH=FERTIGUNG
	. . . . SET $PIECE(SATZ,Y,272)=3  ;WH=FERTIGUNG
	. . ;
	. . IF TEIL'=0 DO  ;UNTERTEILE
	. . . SET TEIL(1)=TEIL
	. . . FOR  SET TEIL(1)=$ORDER(^WWWSOR(YUSER,1,AUF,POS,YEBENE+1,TEIL(1))) QUIT:TEIL(1)=""  QUIT:$EXTRACT(TEIL(1),1,$LENGTH(TEIL))'=TEIL  DO
	. . . . SET YDATEI(1)="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . . IF AUFTRAG=0 SET YDATEI(1)="^INANGPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . . FOR YI=1:1 QUIT:$PIECE(TEIL(1),".",YI,99)=""  SET YDATEI(1)=YDATEI(1)_","_$PIECE(TEIL(1),".",YI)
	. . . . SET YDATEI(1)=YDATEI(1)_")"
	. . . . SET SATZ(1)=@YDATEI(1)  ;SATZ NEU HOLEN WEGEN ÄNDERUNGEN AUS BEARBEITEN VORHERIGER EBENE ;typesetting recent send for quibble out of plain 
	. . . . IF +$PIECE(SATZ(1),Y,5)=0 SET $PIECE(SATZ(1),Y,5)=1
	. . . . SET KOST(1)=KOST(1)+($PIECE(SATZ(1),Y,269)*$PIECE(SATZ,Y,5))  ;GÜNSTIGSTE HK
	. . . . SET KOST(2)=KOST(2)+($PIECE(SATZ(1),Y,270)*$PIECE(SATZ,Y,5))  ;TEUERSTE HK
	. . . . SET $PIECE(SATZ,Y,271)=3  ;WH=FERTIGUNG
	. . . . SET $PIECE(SATZ,Y,272)=3  ;WH=FERTIGUNG
	. . ;
	. . ;ALTERNATIVE BESTELLUNG PRÜFEN ;option sales order sift 
	. . DO
	. . . NEW LIEF,BIS,EK
	. . . QUIT:$PIECE(SATZ,Y,4)=""
	. . . QUIT:$PIECE($GET(^INART(YM,$PIECE(SATZ,Y,4),1)),Y,26)=3  ;NUR EIGENFERTIGUNG ;only 
	. . . IF $DATA(^INARTK(YM,$PIECE(SATZ,Y,4))) DO
	. . . . SET LIEF=""
	. . . . FOR  SET LIEF=$ORDER(^INARTK(YM,$PIECE(SATZ,Y,4),LIEF)) QUIT:LIEF=""  DO
	. . . . . SET BIS=$PIECE($GET(^INARTK(YM,$PIECE(SATZ,Y,4),LIEF,1)),Y,14) IF BIS'="" IF BIS<$HOROLOG QUIT  ;NICHT MEHR GÜLTIG ;Not more valuable 
	. . . . . SET EK=$PIECE($GET(^INARTK(YM,$PIECE(SATZ,Y,4),LIEF,1)),Y,13)  ;NETTO EK-PREIS
	. . . . . IF +$PIECE($GET(^INVORG(YM,YM,1)),Y,43)=1 DO  ;OHNE SKONTO ;without 
	. . . . . . SET EK=$JUSTIFY(EK*100/(100-$PIECE($GET(^INARTK(YM,$PIECE(SATZ,Y,4),LIEF,1)),Y,7)),0,2)
	. . . . . ;
	. . . . . IF +$PIECE($GET(^INARTK(YM,$PIECE(SATZ,Y,4),LIEF,1)),Y,41)'=0 DO  ;UMRECHNUNGSFAKTOR
	. . . . . . SET EK=EK*$PIECE($GET(^INARTK(YM,$PIECE(SATZ,Y,4),LIEF,1)),Y,41)
	. . . . . ;
	. . . . . SET EK=EK*$PIECE(SATZ,Y,5)
	. . . . . IF +EK=0 QUIT
	. . . . . IF EK<KOST(1) SET KOST(1)=EK SET $PIECE(SATZ,Y,271)=1    ;WH BESTELLUNG IST NIEDRIGER HK ;sales order 
	. . . . . IF EK>KOST(2) SET KOST(2)=EK SET $PIECE(SATZ,Y,272)=1    ;WH BESTELLUNG IST HÖHERER HK ;sales order 
	. . ;
	. . ;SPEICHERN HERSTELLKOSTEN ;Save 
	. . ;========================
	. . IF (KOST(1)+KOST(2))'=0 DO
	. . . NEW YDATEI
	. . . IF TEIL="0" DO
	. . . . SET YDATEI="^INAUFP("_""""_YM_""""_","_""""_AUF_""""_","_POS_",1)"
	. . . . IF AUFTRAG=0 SET YDATEI="^INANGP("_""""_YM_""""_","_""""_AUF_""""_","_POS_",1)"
	. . . ;
	. . . IF TEIL'="0" DO
	. . . . SET YDATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . . IF AUFTRAG=0 SET YDATEI="^INANGPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . . . FOR YI=1:1 QUIT:$PIECE(TEIL,".",YI,999)=""  SET YDATEI=YDATEI_","_$PIECE(TEIL,".",YI)
	. . . . SET YDATEI=YDATEI_")"
	. . . ;
	. . . QUIT:'$DATA(@YDATEI)
	. . . SET $PIECE(@YDATEI,Y,269)=KOST(1)
	. . . SET $PIECE(@YDATEI,Y,270)=KOST(2)
	. . . SET $PIECE(@YDATEI,Y,271)=$PIECE(SATZ,Y,271)
	. . . SET $PIECE(@YDATEI,Y,272)=$PIECE(SATZ,Y,272)
	
	DO  ;NEUAUFBAU ARTIKELTEILE
	. NEW YAUFTRAG,YPOS,SATZ,YANGEBOT
	. IF AUFTRAG=0 DO  QUIT
	. . SET YANGEBOT=AUF
	. . SET YPOS=POS
	. . SET SATZ=$GET(^INANGP(YM,AUF,POS,1))
	. . DO ^INANGTEILE
	. ;
	. SET YPOS=POS
	. SET YAUFTRAG=AUF
	. SET SATZ=$GET(^INAUFP(YM,AUF,POS,1))
	. DO ^INAUFTEILE(YAUFTRAG,YPOS)
	
	QUIT
]]></Routine>
</Export>