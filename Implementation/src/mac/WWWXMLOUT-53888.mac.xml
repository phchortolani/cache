<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWXMLOUT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWXMLOUT(URL,timeout,%headers,%testing,useformfield,trace,fieldname) ;WWWXMLOUT;DT;XML CLIENT (C) INTERSYSTEMS;07.09.2001  ; Compiled February 9, 2005 10:47:41
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		XML CLIENT (C) INTERSYSTEMS
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;| 30-May-2005		RobertW		SR12056: Attempt at Performance Increase
	;| DT	07.09.2001  
	;|
	;\------------------------------------------------------------------/
	;
	;PARTS ARE (C) INTERSYSTEMS
	;S YOK=$$^WWWXML("http://127.0.0.1/csp/intraprend/www.cls?EP=WWWXMLSERVER&XML=APPLICATION",20)
	;URL = "HTTP:.... DES SERVERS
	;timeout = timeout DER ANFRAGE DES SERVERS ;the 
	;%header = weiterer %header(1-n)
	;%testing wird nicht genutzt ;not 
	;Useformfield wird nicht genutzt ;not 
	;trace wird nicht genutzt ;not 
	;filename wird nur übertragen ;solely transport 
	;DATEN SOLLEN IN ^WWWSOR(YUSER,0,n)=daten  ;= VOLLE XML DATENSÄTZTE
	;SET ^WWWSOR(YUSER,0,1)="<FAIL>" Q 1
	NEW CGI,%z,%x,dev,CRLF,%lineno,%i,httpdomain,port,rawURL,%io,%http,%y,%lastx,%pass
	SET useformfield=+$GET(useformfield)
	SET trace=$GET(trace)
	SET timeout=+$GET(timeout)
	IF timeout=0 SET timeout=10
	SET fieldname=$GET(fieldname)
	SET %io=$I
	SET rawURL=$PIECE(URL,"?",1),%pass=""
	SET CGI=$PIECE(URL,"?",2,99)
	I CGI'="" S CGI="?"_CGI
	S URL=rawURL
	DO
	. ;IF $$^WWWUPER($EXTRACT(URL,1,7))="HTTP://" SET URL=$EXTRACT(URL,8,$LENGTH(URL)) QUIT
	. ;IF $$^WWWUPER($EXTRACT(URL,1,8))="HTTPS://" SET URL=$EXTRACT(URL,9,$LENGTH(URL))
	. IF $zconvert($EXTRACT(URL,1,7),"U")="HTTP://" SET URL=$EXTRACT(URL,8,$LENGTH(URL)) QUIT
	. IF $zconvert($EXTRACT(URL,1,8),"U")="HTTPS://" SET URL=$EXTRACT(URL,9,$LENGTH(URL))
	IF URL["@" DO
	. SET %pass=$PIECE(URL,"@",1),URL=$PIECE(URL,"@",2,255)
	. NEW in
	. SET in(1)=%pass
	. DO encode^WWWBAS64I(.in)  ;   %wldbas64(.in)
	. SET %pass=outlines(1)
	. SET rawURL=$PIECE(rawURL,"@",2,255)
	SET httpdomain=$PIECE(URL,"/",1)
	SET port=80
	IF httpdomain[":" DO
	. SET port=$PIECE(httpdomain,":",2)
	. SET httpdomain=$PIECE(httpdomain,":",1)
	SET URL="/"_$PIECE(URL,"/",2,5000)
	IF $GET(timeout)="" SET timeout=20
	SET CRLF=$CHAR(13,10)
	SET port=$GET(port) IF port="" SET port=80
	SET %lineno=0
	SET dev="|TCP|"_port
	CLOSE dev
	ELSE  OPEN dev:(httpdomain:port:"PST"):timeout ELSE  D FAIL Q 0
	SET URL=$PIECE(rawURL,httpdomain,2,255)
	IF URL="" SET URL="/"
	USE dev 
	;WRITE "POST ",URL_CGI," HTTP/1.1",CRLF,"Host: ",httpdomain,CRLF
	WRITE "GET ",URL_CGI," HTTP/1.1",CRLF,"Host: ",httpdomain,CRLF
	IF %pass'="" WRITE "Authorization: Basic ",%pass,CRLF
	IF $DATA(%headers) DO
	. NEW n,%header
	. FOR  SET n=$ORDER(%headers(n)) QUIT:n=""  DO
	. . SET %header=%headers(n)
	. . WRITE %header,CRLF
	. . QUIT
	. QUIT
	IF $DATA(^WWWSOR(YUSER,0)) DO
	. NEW len,n,xml,mime,Accept
	. SET mime="text/xml"
	. ;SET mime="text/html"
	. IF useformfield SET mime="application/x-www-form-urlencoded"
	. ;NUR TEST; ;only Test 
	. ;I $F($$^WWWUPER(URL),".CLS") s useformfield=1 s:fieldname="" fieldname="FILENAME.TXT" s mime="multipart/form-data"   ;wenn CSP
	. WRITE "Content-Type: ",mime,CRLF
	. ;s Accept="image/gif, image/x-xbitmap, image/jpeg, image/pjpeg, application/msword, application/vnd.ms-powerpoint, application/vnd.ms-excel, */*"
	. ;W "Accept: ",Accept,CRLF   ;test
	. SET len=0,n=""
	. FOR  SET n=$ORDER(^WWWSOR(YUSER,0,n)) QUIT:n=""  DO
	. . SET xml=^WWWSOR(YUSER,0,n)
	. . SET xml=$$^WWWBLANK(xml)
	. . SET len=len+$LENGTH(xml)+2
	. IF useformfield DO
	. . SET len=len+$LENGTH(fieldname_"=")
	. . QUIT
	. ;
	. WRITE "Content-Length: ",len,CRLF
	. WRITE CRLF
	. IF useformfield WRITE fieldname,"="
	. SET n=""
	. FOR  SET n=$ORDER(^WWWSOR(YUSER,0,n)) QUIT:n=""  DO
	. . SET xml=^WWWSOR(YUSER,0,n)
	. . SET xml=$$^WWWBLANK(xml)
	. . WRITE xml,CRLF
	. . QUIT
	. WRITE CRLF
	. QUIT
	WRITE !
	KILL ^WWWSOR(YUSER,0)
	SET $ZT="EOHTML"
	SET %lastx=""
	USE dev FOR %i=1:1 READ %x:timeout QUIT:'$T  DO
	. SET ^WWWSOR(YUSER,0,%i)=%x
	. READ %y:timeout QUIT:'$T
	. QUIT:%y=""
	. SET %i=%i+1
	. SET ^WWWSOR(YUSER,0,%i)=%y
	. QUIT
	
EOHTML ; HTTP server has now disconnected - end of XML
	CLOSE dev
	USE %io
	
EXIT ;
	SET %lineno=$ORDER(^WWWSOR(YUSER,0,""),-1)
	QUIT %lineno
	
FAIL ;
	CLOSE dev
	USE %io
	QUIT
]]></Routine>
</Export>