<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW004" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW004
	
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;	KONTROLLE MENUSPRACHENTEXTE
	;	Remove Language Text Menu sub-class entries if menu entry no longer exists
	;
	; ByRef :
	;	KEY		array - Tests element 2; not otherwise used
	;
	; Returns :
	;	WWWSOR entry : error text (ByRef)
	;
	; History :
	;
	; 02.01.1998	DT		Created
	;-------------------------------------------------------------------------------
	NEW YY
	
	IF $DATA(KEY(2)) IF '$FIND(KEY(2),".") SET ^WWWSOR(YUSER,1)=$$^WWWTEXT(330)  ; "Point (.) Is Missing In Menu Item!"
	
	SET YY=""
	FOR  SET YY=$ORDER(^WWW0041(0,YY)) QUIT:YY=""  DO                 ; Application Name
	. SET YY(1)=""
	. FOR  SET YY(1)=$ORDER(^WWW0041(0,YY,YY(1))) QUIT:YY(1)=""  DO   ; Menu Item No
	. . IF '$DATA(^WWW004(0,YY,YY(1))) KILL ^WWW0041(0,YY,YY(1))
	QUIT
	
	
Get(pidApplicn,pidSection)
	;-------------------------------------------------------------------------------
	; Returns a Menu record with translations done
	; 
	; Called By : OnAfterDataFields^WWWMenuOverview, GetMenu^WWWMenuOverview
	; 
	; ByRef: YBED
	; 
	; Returns : Language Customised Menu entry
	; 
	; History:
	; 27-Apr-2010	GRF		SR16402: use idLang; menu globals ALWAYS 0 not YM
	; 28-Feb-2009	HQN		SR16402: Created
	;-------------------------------------------------------------------------------
	new idLang,objMenu
	
	set objMenu = $get(^WWW004(0,pidApplicn,pidSection,1))
	set idLang  = $$^WWWLANGU(YBED)
	if $data(YBED) && $data(^WWW0041(0,pidApplicn,pidSection,idLang)) {
		set $$$WWW004MenuDescription(objMenu) = $get(^WWW0041(0,pidApplicn,pidSection,idLang,1))
	}
	quit objMenu

OnBeforeDataAccess(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Filters the list of mega menus a user can select from
	; 
	; Returns : Language Customised Menu entry
	; 
	; History:
	; 04-Oct-2013	shobby		CORE-280: quit with 1 if form condition not found
	; 20-Jun-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	if YFORM="WWW013" quit $piece(YKEY,".",2)=""
	quit 1
	
]]></Routine>
</Export>