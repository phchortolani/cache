<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31Links" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31Links
	#include COMSYS
	#def1arg WWWDATEN(%args)	^WWWDATEN(YM,+$horolog,YUSER,$get(^CacheTemp(YUSER,"Grid","Name")," "),"V",%args)

UpdateHeader(YFORM,YROW)
	;-------------------------------------------------------------------------------
	; Updates linked fields in the header based on the contents of a grid.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 25-Jun-2012	SCR			SR18026: Removed Item field check
	; 19-Jun-2012	shobby		SR18026: Created
	;-------------------------------------------------------------------------------
	new YFELD,YMFELD,idField,intField,intPiece,YFORMCONTAINER
	
	set YFELD = $$GetYFELD^COMGridEdit31G(YFORM,YROW)         
	set YMFELD = $$GetYMFELDContainer^COMGridEdit31G()
	set YFORMCONTAINER= $get(^CacheTemp(YUSER,"Grid","Container"))

	set idField="" for { set idField=$order(^CacheTemp(YUSER,"Grid","LinkedTo",idField)) quit:idField=""
		set intField=$extract(idField,2,9999)
		set intPiece=$get(^CacheTemp(YUSER,"Grid","LinkedTo",idField))
		do PopulateDataField^COMUtils(YFORMCONTAINER,intField,$piece(YFELD,Y,intPiece),"M",YFELD,$$$NO)
	}
	quit ""


UpdateGrid(pstrField,YINHALT,pstrOrigInput,pblnForce=0)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 25-Jun-2012	SCR			SR18026: Added force option to use YINHALT
	; 07-Jun-2012	shobby		SR18026: Created
	;-------------------------------------------------------------------------------
	;SR18026
	new strField,intGridField,arrGrid,strGridForm,YKEY,YKEYCONTAINER,YROW,objWWW122
	new YFELDCONTAINER,YMFELDCONTAINER,intHeaderField
	
	set YFELDCONTAINER=$$GetYFELDContainer^COMGridEdit31Interface()
	set YMFELDCONTAINER=$$GetYMFELDContainer^COMGridEdit31Interface(1)
	set strGridField=""
	set YKEY=""
	
	if YFORM'="" {
		set strGridForm=$get(^CacheTemp(YUSER,"Grid","Name"))
		if strGridForm'="" {
			set strField=$piece(pstrField,YFORM,2)
			if strField'="" {
				set intGridField=$get(^CacheTemp(YUSER,"Grid","LinkedTo",strField))
				if intGridField'="" { 
					set intHeaderField=$extract(strField,2,99999)
					set YKEYCONTAINER=$$$GRIDYKEYContainer
					set YROW=$piece($piece($$$WWWDATEN("FOCUSFIELD"),"Y",2),"_",1)
					set YKEY=$piece($$GetYKEY^COMGridEdit31Interface(YROW),",",2)
					if YKEY'="" {
						do GetGridContents^COMGridEdit31G(.arrGrid)  ;SR17779
						set $piece(arrGrid(YKEYCONTAINER,YKEY),Y,intGridField)=$select('pblnForce:$piece(YMFELDCONTAINER,Y,intHeaderField),1:YINHALT)
						do SetGridContents^COMGridEdit31G(.arrGrid)  ;SR17779
					}
				}
			}
		}
	}
	quit

GetLinkedTo()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Apr-2013	SCR			HEVA-877: Call Change Row if 'OnAfterRowChange' is defined
	; 07-Jun-2012	shobby		SR18026: Created
	;-------------------------------------------------------------------------------
	new idField,objWWW122,blnHasLinks
	
	set blnHasLinks=$$$NO
	set idField="" for { set idField=$order(^WWW122(0,YFORM,idField)) quit:idField=""
		set objWWW122=$get(^WWW122(0,YFORM,idField,1))
		if $$$WWW122LinkedTo(objWWW122)'="" {
			if $$$WWW122SequenceNumber(objWWW122)'="" {
				set ^CacheTemp(YUSER,"Grid","LinkedTo",$$$WWW122LinkedTo(objWWW122))=$$$WWW122SequenceNumber(objWWW122)
				set blnHasLinks=$$$YES
			}
		}
	}
	set:$data(^WWW120Hook(0,YFORM,$$$EnumWWWEVENTTYPEOnAfterRowChange)) blnHasLinks=$$$YES ; HEVA-875
	if blnHasLinks {
		$$$StartScript()
		write " function callChangeRow(val) { return ChangeRow(val); }"
		$$$EndScript()
	}
	quit

IsLinkedTo(YBBN)
	;-------------------------------------------------------------------------------
	; Used to determine if a particular selection control has links.  If so then
	; we populate all values, not just the selected value for a readonly control
	; ensuring that when other lines on the grid are selected the control populates
	; appropriately. 
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 07-Jun-2012	shobby		SR18026: Created
	;-------------------------------------------------------------------------------
	quit $data(^CacheTemp(YUSER,"Grid","LinkedTo","M"_YBBN))
	
		
	
	


]]></Routine>
</Export>