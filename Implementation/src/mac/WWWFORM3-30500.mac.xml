<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM3" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORM3
#include COMSYS
#include WWWConst
	
#define LogR(%1,%2) 	;
#define LogR2(%1,%2) 	;
#define LogRx(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORM3("_%2_") : "_$zh $$$JournalOn
#;define LogR2(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORM3("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Form Type 3 : "Grid Form"  (not Edit Grid)
	;		Display Grid Header
	;		
	; Example : Form INARTPACK "Item Units" sub to Item Master Data
	;
	; Called By : ^WWWFORM9, ^WWWFORMP
	;
	; ByRef :
	;	YDATEI			Class
	;
	;
	; History :
	; 09-Nov-2010	GRF		SR17243: Doco; macros; strFontSize
	; 03-Feb-2010	GRF		idxForm rather than id prefix; cleanup
	; 11-Feb-2009	GRF		SR16347: Manual Form Field won't have Class link - 
	; 							subscript error on YLFN
	; 27-Jul-2007	shobby	SRBR014586: Removed YHIDDSE as a parameter of routine
	; 							NoAccess
	; 27-Jul-2007	shobby	SRBR014586: Include YOPTION, YOPTION1, YPARA1 in the
	; 							'access' test.
	; 27-Jul-2007	shobby	SRBR014615:	only call $$^WWWUMLAU once (idForm)
	; 19-Jul-2007	shobby	SRBR014615: Subroutined some repeated code in to a method
	; 19-Jul-2007	shobby	SRBR014615: Subroutined NoAccess call to make sure all
	; 							customisations are considered
	; 08-Jun-2007	GRF		SR15542: Naked Reference; Doco; quits; missed </FONT>
	; 02-08-2004	TYBD	26028;
	; 29-01-2004	TYBD
	; 20.08.1997	DT
	;-------------------------------------------------------------------------------
	
	;-------------------------------------------------------------------------------
	; Initial Primary Keys   |                                                        1. Keys
	;------------------------+                                                               ???
	;
	;-------------------------------------------------------------------------------
	; Last Key   | Field 1 | Field 2 | Field 3 | ...                     | Field n |  2. Headers
	;------------+---------+---------+---------+-------------------------+---------+         ^WWWFORM3
	;            |         |         |         |                         |         |
	; [        ] | [     ] | [     ] | [     ] | [ ...                 ] | [     ] |  3. Input/Edit Fields
	;------------+---------+---------+---------+-------------------------+---------+         ANTWORT^WWWFORM7
	;            |         |         |         |                         |         | \
	;-------------------------------------------------------------------------------
	;            |         |         |         |                         |         |
	;-------------------------------------------------------------------------------  4. Current Data
	;            |         |         |         |                         |         |         ANZEIGE^WWWFORM4
	;-------------------------------------------------------------------------------
	;            |         |         |         |                         |         | /
	;-------------------------------------------------------------------------------
	new YLFN,YTEXT,YSEI,YZEILE,YSPL,YTXT,YHIDDSE,idxForm,strFontSize
	
	;write YCR,YCR,"<!-- ************************* GRIDKOPF (WWWFORM3)************************** -->",YCR,YCR
	
	$$$LogR("",YFORM_"<"_YDATEI)
	
	set YGRIKEY = $order(^WWW002(0,YDATEI,""),-1)   ; Number of keys
	if YGRIKEY>3 do ^WWWFRAME(0)
	if YGRIKEY>1 if YGRIKEY<4 do ^WWWFRAME(2)
	
	set YSORTX   = +$get(%(YQUERY,"YSORT"))
	set YORIENTX = +$get(%(YQUERY,"YORIENT"))
	
	if $$$WWW120FontSize(YVOR)'="" {
		set strFontSize = " SIZE="""_$$$WWW120FontSize(YVOR)_""""    ; D7   ; SR17243
	} else {
		set strFontSize = ""
	}
	;---------------------------------------
	; 2. Build Titles Line
	;---------------------------------------
	write "<TR>"
	
	;--------------------------------------- FIXME : Loop through all keys but only action against last one
	; 2a. i) Last key                                Why not simply test for YGRIKEY'=0 then set YLFN=YGRIKEY
	;---------------------------------------         and process with that?
	set YLFN = ""
	for  set YLFN = $order(^WWW002(0,YDATEI,YLFN)) quit:YLFN=""  do
	. if YGRIKEY'=YLFN quit
	. write "<TH ALIGN=LEFT BGCOLOR="_YDARKGRAY_" NOWRAP>"
	. write "<FONT ",strFontSize,">"
	. set YTEXT = $$^WWWFELDNAME(YFORM,"P",YLFN)
	. set YSORT = +$piece($get(^WWW002(0,YDATEI,YLFN,1)),Y,6)
	. write $$^WWWUML(YTEXT)
	. do Draw(0)
	. do Draw(1)
	. write "</FONT></TH>",YCR
	
	;---------------------------------------
	; 2a. ii) No Keys
	;---------------------------------------
	if +YGRIKEY=0 do
	. write "<TH ALIGN=LEFT BGCOLOR="_YDARKGRAY_">"
	. write "<FONT ",strFontSize,">"
	. write "#"
	. write "</FONT></TH>",YCR
	
	;---------------------------------------
	; 2b. Form Data Fields
	;---------------------------------------
	set idxForm = $$$Index(YFORM)
	set YSEI = ""
	for  set YSEI = $order(^WWW122s(0,3,idxForm,YSEI)) quit:YSEI=""  do
	. set YZEILE = ""
	. for  set YZEILE = $order(^WWW122s(0,3,idxForm,YSEI,YZEILE)) quit:YZEILE=""  do
	. . set YSPL = ""
	. . for  set YSPL = $order(^WWW122s(0,3,idxForm,YSEI,YZEILE,YSPL)) quit:YSPL=""  do
	. . . set YBBN = ""
	. . . for  set YBBN = $order(^WWW122s(0,3,idxForm,YSEI,YZEILE,YSPL,YFORM,YBBN)) quit:YBBN=""  do
	. . . . quit:$$NoAccess^WWWFORMCOMMON(YFORM,YDATEI,YBBN,.YTXT,.YLFN,.YTEXT,$get(YOPTION),$get(YOPTION1),$get(YPARA1))
	. . . . ;
	. . . . set YSORT=0               ; Manual Form Field won't have Class link
	. . . . if YLFN'="" set YSORT = +$piece($get(^WWW003(0,YDATEI,YLFN,1)),Y,6)
	. . . . if YLFN=""  set YTYP  =  $piece($get(^WWW122(0,YFORM,YBBN,1)),Y,5)
	. . . . if YLFN'="" set YTYP  =  $piece($get(^WWW003(0,YDATEI,YLFN,1)),Y,3)
	. . . . if YTYP=""  set YTYP  = 6
	. . . . quit:YTYP=0               ; Hidden Fields
	. . . . ;
	. . . . write "<TH ALIGN=LEFT BGCOLOR="_YDARKGRAY_" NOWRAP>"
	. . . . write "<FONT ",strFontSize,">"
	. . . . write $$^WWWUML(YTEXT)
	. . . . if YSORT'=0 do
	. . . . . do Draw(0)
	. . . . . do Draw(1)
	. . . . ;
	. . . . write "</FONT></TH>",YCR
	
	;------------------------------------------------------------------------
	write "</TR>",YCR
	quit
	
	
Draw(pintOrient=0)
	;-------------------------------------------------------------------------------
	; Subroutine of existing repeated code
	;
	; Params: pintOrient   0:  [^]    1:  [v]  
	;
	; By Ref:  YAKTION, YFORM, YSORT, YGIF
	; Returns: Nothing
	;
	; History:
	; 03-Aug-2007	GRF		SRBR014615: Call twice to see operation rather than
	; 							using a recursive call
	; 18-Jul-2007	shobby	SRBR014615: Created
	;-------------------------------------------------------------------------------
	new strImage
	
	set strImage = $select(pintOrient=0:"scrup.gif",1:"scrdown.gif")
	
	write "<A"
	write " TITLE='"_$$^WWWTEXT(65)_"'"          ; "Sorted By"
	write " HREF='"_YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YSORT="_YSORT_"&amp;YORIENT="_pintOrient
	do ^WWWCGI
	write "'> "
	write "<IMG SRC='"_YGIF_strImage_"' BORDER=0>"
	write "</A>"
	quit
	
]]></Routine>
</Export>