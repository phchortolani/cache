<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRAHMENAUF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRAHMENAUF(TYP)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERSTELLEN EINES AUFTRAGES AUS EINEM RAHMENVERTRAG (KUNDE BZW. LIEFERANT)
	;
	; Inputs : 
	; TYP     = 0=AUFTRAG (SPÄTER) 1=KUNDE 2=LIEFERANT ;guy 
	; VORG(1) = KUNDEN- BZW. LIEFERANTENNUMMER  | ;resp. 
	; VORG(2) = VERTRAGSNUMMER                  |
	; VORG(3) = GÜLTIG VON                      | NUR BEI DEN ;valuable only next to 
	; VORG(4) = GÜLTIG BIS                      | TYPEN 1 U. 2 ;valuable until 
	; VORG(5) = MENGE                           | ;quantity 
	; VORG(6) = ARTIKELNUMMER                   |
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 09-Oct-2007	GRF		Doco
	; 19.01.2004	WEM
	;-------------------------------------------------------------------------------
	NEW ART1,AUF,AUF1,LINK,POS1,YOK,SATZ,SATZ1,SATZP,YI
	
	SET TYP=$GET(TYP)
	IF TYP'=1 IF TYP'=2 SET TYP=0
	
	SET SATZ  = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1))   ; KLASSENFELDER
	SET SATZ1 = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"M",1))   ; MANUELLE FELDER
	SET SATZP = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1))   ; KEYS
	
	IF TYP'=0 DO   ; AUFRUF ERFOLGT NICHT AUS AUFTRAG ;Not out of order 
	. ;
	. ;
	. ; "No Customer Or Supplier No. Available!"   KEINE KUNDEN- BZW. LIEFERANTENNUMMER VORHANDEN
	. SET VORG(1)=$PIECE(SATZP,",",1)
	. IF VORG(1)="" DO ^WWWINFO($$^WWWTEXT(33722)) QUIT
	. ;
	. ;
	. ; "Please Select A Data Record First."   BITTE ZUERST DATENSATZ AUSWÄHLEN ODER SPEICHERN
	. SET VORG(2)=$PIECE(SATZP,",",2)
	. IF (VORG(2)="") || (VORG(2)="+")    DO ^WWWINFO($$^WWWTEXT(32072)) QUIT
	. ;
	. ;
	. ; "This Framework-Contract Is Not Valid At Present!"   DER RAHEMVERTRAG IST NOCH NICHT GÜLTIG
	. SET VORG(3)=$PIECE(SATZ,Y,8)
	. IF VORG(3)'="" IF $HOROLOG<VORG(3) DO ^WWWINFO($$^WWWTEXT(33723)) QUIT
	. ;
	. ;
	. ; "This Framework-Contract Is Not Valid Any Longer!"  ; DER RAHMENVERTRAG IST NICHT MEHR GÜLTIG
	. SET VORG(4)=$PIECE(SATZ,Y,9)
	. IF VORG(4)'="" IF $HOROLOG>VORG(4) DO ^WWWINFO($$^WWWTEXT(33724)) QUIT
	. ;
	. ;
	. ; "Rest No Of Items"  ; BITTE MENGE FESTLEGEN
	. SET VORG(5)=$PIECE(SATZ1,Y,24)
	. IF VORG(5)="" DO ^WWWINFO($$^WWWTEXT(32087)) QUIT
	. ;
	. ;
	. ; "Check Item Number!"   ARTIKELNUMMER PRÜFEN
	. SET VORG(6)=$PIECE(SATZ,Y,25)
	. IF VORG(6)'="" IF '$DATA(^INART(YM,VORG(6))) DO ^WWWINFO($$^WWWTEXT(33444)) QUIT
	. IF VORG(6)=""                                DO ^WWWINFO($$^WWWTEXT(33444)) QUIT
	. ;
	. ;
	. SET AUF=$$^WWWNEXT("INAUF")   ; NEUE AUFTRAGSNUMMER
	. SET AUF1=""                   ; DATENSATZ INAUF ;data record 
	. ;
	. IF TYP=1 SET $PIECE(AUF1,Y,1)=VORG(1)  SET $PIECE(AUF1,Y,2)=0    ; KUNDENNUMMER / -AUFTRAG
	. IF TYP=2 SET $PIECE(AUF1,Y,12)=VORG(1) SET $PIECE(AUF1,Y,2)=2    ; LIEFERANTENNUMMER / -AUFTRAG
	. SET $PIECE(AUF1,Y,4)  = +$HOROLOG                 ; AUFTRAGSDATUM
	. SET $PIECE(AUF1,Y,5)  = $PIECE($HOROLOG,",",2)    ; AUFTRAGSUHRZEIT
	. SET $PIECE(AUF1,Y,6)  = YLOCATION                 ; BETRIEB
	. SET $PIECE(AUF1,Y,14) = YBED                      ; ZUSTÄNDIGER MITARBEITER
	. SET $PIECE(AUF1,Y,41) = +$HOROLOG                 ; ERFASSUNGSDATUM
	. SET $PIECE(AUF1,Y,42) = YBED                      ; ERFASSUNGSMITARBEITER
	. ;
	. SET YOK=$$^WWWSPEI("INAUF",AUF,AUF1,1)
	. ;
	. SET POS1=""   ; DATENSATZ INAUFP ;data record 
	. SET ART1=$GET(^INART(YM,VORG(6),1))
	. ;
	. SET $PIECE(POS1,Y,1)=$PIECE(ART1,Y,1)     ; ARTIKELBEZEICHNUNG
	. SET $PIECE(POS1,Y,4)=VORG(6)              ; ARTIKELNUMMER
	. SET $PIECE(POS1,Y,5)=VORG(5)              ; MENGE ;quantity 
	. IF TYP=2 SET $PIECE(POS1,Y,12)=VORG(1)    ; LIEFERANT BEI BESTELLUNG ;supplier next to sales order 
	. SET $PIECE(POS1,Y,22)=+$HOROLOG           ; ERFASSUNGSDATUM
	. SET $PIECE(POS1,Y,23)=YBED                ; ERFASSUNGSMITARBEITER
	. ;
	. ;  FIXME : <GRF> should the second 60 be removed or changed to 61?
	. ;
	. FOR YI=10,12,14,15,16,17,26,30,31,32,33,36,37,38,40,41,42,43,44,45,47,48,49,50,53,54,55,56,57,58,59,60,60,62,68,70,71,72,73,74,75 DO
	. . SET $PIECE(POS1,Y,YI)=$PIECE(ART1,Y,YI)
	. ;
	. SET $PIECE(POS1,Y,193)=+$HOROLOG          ; AUFTRAGSDATUM
	. SET $PIECE(POS1,Y,320)=VORG(2)            ; RAHMENVERTRAGSNUMMER
	. ;
	. SET YOK=$$^WWWSPEI("INAUFP",AUF_","_1,POS1,1)
	. ;
	. DO  ;LINK
	. . SET YKEY=AUF
	. . NEW YKEY,YLINK,YI,YBUTTON
	. . SET YI=""
	. . SET YLINK=""
	. . SET YI=YAKTION_"EP=WWWFORM&YFORM=INAUF&YKEY="_AUF
	. . DO VAR^WWWCGI
	. . SET YLINK=YI
	. . DO ^WWWINFO($$^WWWTEXT(32680)_" ("_AUF_")",0,YLINK,0)  ; NEUER AUFTRAG ANGELEGT ;order 
	
	IF TYP=0 DO   ; AUFRUF AUS AUFTRAG ;out of order 
	. DO ^WWWINFO($$^WWWTEXT(32310))  ; KEINE AUSWAHL GETROFFEN ;no Selection 
	
	QUIT
	
COPY   ; KOPIEREN DATEN IN ABRUFFELDER (ONBLUR) ;within 
	NEW SATZ,FORM
	
	SET FORM=""
	SET SATZ=""
	IF $GET(YFORM)="INRAHMEN"  SET FORM="INRAHMEN"
	IF $GET(YFORM)="INRAHMENK" SET FORM="INRAHMENK"
	QUIT:FORM=""
	
	SET SATZ=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,FORM,"D",1))
	QUIT:SATZ=""
	
	QUIT:$PIECE(SATZ,Y,1)=""  ;KEIN ARTIKEL GGF SUCHE;20,2,2004;TYBD
	IF '$DATA(^INART(YM,$PIECE(SATZ,Y,1),1)) quit              ;TYBD
	SET %TXT(1)=%TXT(1)_"#Y"_FORM_"D25~"_$PIECE(SATZ,Y,1)
	QUIT
	
]]></Routine>
</Export>