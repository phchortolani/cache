<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINSYSLoad" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINSYSLoad ; File Load
	
#include COMSYS
#include FINConst
	
ARADJ(Company,Number,GLAccount,File)	
	;-------------------------------------------------------------------------------
	; Load Data in a AR Adjustment Lines
	; Company = Complany Number
	; Number = The Adjustment Number, Must be defined and not posted
	; GLAccount = The GL Account to Post to.
	; File = Input File , Tab delimeted , if Null Input is take from the key board ( allows for pasting )
	; Format = 
	; ID #	DATE	NUMBER	 AMOUNT DUE 	 AMOUNT DUE 	 TOTAL DUE 	DISCOUNT	DISCOUNT
	;1060	30-Apr-03	4186	 257.50 	 20.10 	 277.60 	30-May-03	2
	;1000	21-Feb-03	3565	 55.60 	 -   	 55.60 	10-Apr-03	2
	;
	; History:
	; 22-Feb-2005	Paul K	Reduce incidence of calling WWWVAR
	; 25-Jun-2004	shobby	Foreign Currency.
	; 25-Jul-2003	SCR		Remove Quoted from input.
	; 29-Jul-2003	SCR		Changed sign on Credit
	;-------------------------------------------------------------------------------
	new Status,Header,AccountRec,Tab,blnDone,LineNumber,Line,Customer,Date
	new Ref,SaleAmt,FreightAmt,TotalAmt,DueDate,Discount,Rec,DateInt,DueDateInt
	new DiscountAmt,TotalAmount,RecNum
	
	set $ztrap="ERR"
	set Status=$$$OK
	if $get(Company)="" set Status="0~No Company Number"
	if (Status=$$$OK) && ($get(Number)="") set Status="0~No Adjustment Number"
	if $get(Y)="" do ^WWWVAR      ; Set up System vars // TODO: MUST NOT be here, if for dev purposes put in DEV code
	set YM=Company  ; Set Company
	if Status=$$$OK {
		Set Header=$get(^FINARADJ(YM,Number,1))
		if (Header="") set Status="0~Adjustment "_Number_" is not defined"
		if (Status=$$$OK)&&($$$FINARADJPostedDate(Header)'="") set Status="0~Adjustment "_Number_" has been posted"
	}
	if (Status=$$$OK) && ($get(GLAccount)="") set Status="0~No GL Account Specified"
	if (Status=$$$OK) {
		set AccountRec=$get(^FINGLAccount(YM,GLAccount,1))
		if (AccountRec="") set Status="0~GL Account '"_GLAccount_"' not defined"
		if (Status=$$$OK)&&($$$FINGLAccountAccountUsage(AccountRec)'=1) set Status="0~Can not post to account '"_GLAccount_"'"
		
	}
	if (Status=$$$OK) {
		set File=$get(File)
		if File'="" open File:"r":10 else  set Status="0~Can not open file "_File
	}
	set Tab=$char(9)
	if (Status=$$$OK) {
		set blnDone = $$$NO
		for LineNumber=1:1 {
			if File'="" use File
			read Line
			if File'="" use 0
			if File="" if Line="" set blnDone = $$$YES
			quit:blnDone||(Status'=$$$OK)
			
			write !,"Processing Line "_LineNumber_" "
			set Line       = $translate(Line,"""")  ; Remove a Quotes
			set Customer   = $piece(Line,Tab,1)
			set Date       = $piece(Line,Tab,2)
			set Ref        = $piece(Line,Tab,3)
			set SaleAmt    = $translate($piece(Line,Tab,4),", ")
			set FreightAmt = $translate($piece(Line,Tab,5),", ")
			set TotalAmt   = $translate($piece(Line,Tab,6),", ")
			set DueDate    = $piece(Line,Tab,7)
			set Discount   = $piece(Line,Tab,8)
			set Rec = ""
			
			if (Status=$$$OK) && (Customer="") set Status = "No Customer"
			if (Status=$$$OK) {
				set DateInt = $zdh(Date,5,,,,,,,0)
				if 'DateInt set Status = "0~Incorrect Date '"_Date_"'"
			}
			if (Status=$$$OK) {
				set DueDateInt = $zdh(DueDate,5,,,,,,,0)
				if 'DueDateInt set Status = "0~Incorrect Due Date '"_DueDate_"'"
			}
			if (Status=$$$OK) {
				set DiscountAmt = $$Round^COMUtils(SaleAmt*Discount/100)
				set $piece(Rec,Y,1) = Customer
				set $piece(Rec,Y,8) = DateInt
				set $piece(Rec,Y,2) = Ref
				if TotalAmt>0 set $piece(Rec,Y,3) = TotalAmt
				if TotalAmt<0 set $piece(Rec,Y,4) = $$$FCSign(TotalAmt)
				set $piece(Rec,Y,5) = DueDateInt
				set $piece(Rec,Y,6) = DiscountAmt
				set $piece(Rec,Y,7) = GLAccount
				set $piece(Rec,Y,9) = FreightAmt
				set RecNum=$order(^FINARADJLine(YM,Number,""),-1)+1
			}
			if (Status=$$$OK) {
				set Status = $$$Save("FINARADJLine",Number_","_RecNum,Rec,$$$YES)
			}
			write " Done "
		}
	}
	write !,Status
	quit
	
ERR ; Internal Tag
	set $ztrap=""
	if $get(File)'="" Close File
	use 0
	write !,$get(Status)
	write !,$zerror
	quit
	
	
Test ; Test Tag
	;d ARADJ^FINSYSLoad(0,35,"11450.00.00")
	quit
 
 
]]></Routine>
</Export>