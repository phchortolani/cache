<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContratoItem" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContratoItem
#include COMSYS

OnBlurCalculaTotal(pintCol,pYFELD,pYINHALT)
	;-------------------------------------------------------------------------------
    ; Calcula o total da linha, onde total = quantidade * valor unitário.
    ;
    ; History:
    ; 28-Sep-2010	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:(pYFELD = "")
	
	new vlueUnitario, vlueTotal, fltQty
	
	;Se OnBlur no campo Quantidade
	if (pintCol = 4) {
		
		if (pYINHALT = "") {
			set $piece(YFELD,Y,pintCol) = ""
			set $piece(YFELD,Y,6) 		= ""
			quit				
		}
		
		if (pYINHALT <= 0) {			
			$$$Alert("A quantidade não pode ser igual ou menor que zero.")
			set $piece(YFELD,Y,pintCol) = ""
			set $piece(YFELD,Y,6) 		= ""
			quit
		}
		
		set vlueUnitario = $piece(YFELD,Y,5)		
		quit:(vlueUnitario = "")
		
		set vlueTotal = pYINHALT * vlueUnitario
		
		set $piece(YFELD,Y,6) = vlueTotal
	}
	
	;Se OnBlur no campo Valor Unitário
	elseif (pintCol = 5) {

		if (pYINHALT = "") {
			set $piece(YFELD,Y,pintCol) = ""
			set $piece(YFELD,Y,6) 		= ""
			quit				
		}		
		
		if (pYINHALT <= 0) {			
			$$$Alert("O valor unitário não pode ser igual ou menor que zero.")
			set $piece(YFELD,Y,pintCol) = ""
			set $piece(YFELD,Y,6) 		= ""
			quit
		}
		
		set fltQty = $piece(YFELD,Y,4)
		quit:(fltQty = "")
		
		set vlueTotal = pYINHALT * fltQty			
	}
	
	set $piece(YFELD,Y,6) = vlueTotal

	quit
	
	
OnBlurCodigoProduto(pYFELD,pYINHALT)
	;-------------------------------------------------------------------------------
    ; Retorna o campo descrição caso o campo código do produto tenha sido preenchido
    ; e seja um produto válido no cadastro de produtos.
    ;
    ; History:
    ; 28-Sep-2010	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:(pYINHALT = "")
	
	new strDescProduto
	
	if '$data(^INART(YM,pYINHALT)) {
		$$$Alert("O produto "_pYINHALT_" não existe no cadastro de produtos. Favor verificar")
		set $piece(YFELD,Y,3) = ""
		
	} else {	
		set strDescProduto = $$SQLGetDescricaoProduto^VARSQL(pYINHALT)
		set $piece(YFELD,Y,3) = strDescProduto
	}
	
	quit

checkCodigoProdutoObrigatorio(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se a categoria do contrato possui relação com o cadastro de produtos.
    ; Se houver, o campo Cód. Produto fica obrigatório.
    ;
    ; History:
    ; 28-Sep-2010	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "") $$$NO
	
	new idContrato, tipoContrato, categoria, blnRelacaoCadProdutos
	set idContrato = $$$KEY1(pYKEY)	
	
	set tipoContrato = $$GetTipoContrato^VARContrato(idContrato)
	quit:(tipoContrato = "") $$$NO
	
	set categoria = $$GetCategoria^VARContrato(idContrato)
	quit:(categoria = "") $$$NO
	
	set blnRelacaoCadProdutos = $piece($get(^VARContratoTipoCateg(YM,tipoContrato,categoria,1)),Y,6)
	
	if (blnRelacaoCadProdutos = $$$YES) {
		quit $$$YES
	}	
	
	quit $$$NO
	
]]></Routine>
</Export>