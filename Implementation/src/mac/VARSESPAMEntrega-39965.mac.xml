<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPAMEntrega" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMConst
#include INConst
#include WWWConst
#include FIN
#include VARConst
#include COMSYS
 
VARSESPAMEntrega
	quit
	
OnBeforeFormConstruction
	// Andre, 27/02/2008
	// Ao construir a tela, verifica se ela deve ser editável ou não,
	// dependendo da PAM já estar processada ou não.
	
	new PAMStatus, EntregaPadrao
	
	// Se nenhuma PAM escolhida, torna tela não editável:
	if ($get(YKEY) = "") || ($piece($get(YKEY),YKOMMA,1) = "") {
		set $$$WWW120AuthorizationToModifyData(YVOR) = 5
		set $$$WWW120DoNOTDisplayStandardButto(YVOR) = "1;"_$$$WWW120DoNOTDisplayStandardButto(YVOR)
		quit
	}
	
	// Se PAM processada, não é possível editar Entregas:
	// do AlertInScript^COMConst("Status da PAM: "_PAMStatus)
	set PAMStatus = +$piece($get(^VARTRKPAM(YM,$piece(YKEY,YKOMMA,1),1)),Y,14)
	if ( PAMStatus '= 0 ) {
		set $$$WWW120AuthorizationToModifyData(YVOR) = 5
		set $$$WWW120DoNOTDisplayStandardButto(YVOR) = "1;"_$$$WWW120DoNOTDisplayStandardButto(YVOR)
	}
	
	// Se Entrega Padrão definida na PAM, não é possível editar Entregas:
	set EntregaPadrao = $piece($get(^VARTRKPAM(YM,$piece(YKEY,YKOMMA,1),1)),Y,17)
	if ( EntregaPadrao '= "" ) {
		set $$$WWW120AuthorizationToModifyData(YVOR) = 5
		set $$$WWW120DoNOTDisplayStandardButto(YVOR) = "1;"_$$$WWW120DoNOTDisplayStandardButto(YVOR)
		do AlertInScript^COMConst($$$Text("MED01059")_"."_YCR_  ;Não é possível editar entregas, pois foi definida uma Entrega Padrão na PAM ;use text macro;15-Sep-2008
									$$$Text("MED01060")_".")    ;Para editar entregas, deixe o campo 'Entrega Padrão' em branco, na PAM ;use text macro;15-Sep-2008
	}
	
	quit
 
OnBeforeButtonLine
	do CriaListaDeLinhas(YKEY)
	quit
 
OnAfterDataFields
	do LoadGrid^VARSESPAMEntregaLinha(YKEY,YFELD)
	quit
	
OnBeforeSave
	do SaveGrid^VARSESPAMEntregaLinha(YKEY)
	quit
	
ChecarPercentualEhValido()
	// Andre, 27/02/2008
	// Verifica se o percentual é válido.
	
	new percentual,entregaUniforme
	
	set percentual = +$piece(YFELD,Y,4)
	set entregaUniforme = $piece(YFELD,Y,3)
	
	if (entregaUniforme = 1) {
		if ( (percentual <= 0) || (percentual > 100) ) {
			do AlertInScript^COMConst($$$Text("MED01061")_".")  ;Campo de Percentual com valor inválido. Valor deve ser entre 1 e 100 ;use text macro;15-Sep-2008
			quit 0
		}
	}
	quit 1
	
OnAfterSave
	new entregaUniforme
	set entregaUniforme = $piece(YFELD,Y,3)
	if ( entregaUniforme = 1 ) {
		do CriaEntregaUniforme(YKEY,YFELD)
	}
	quit
	
CriaEntregaUniforme(pKeyEntrega,pObjEntrega)
	// Andre, 27/02/2008
	// Após salvar, se o campo "Entrega uniforme" estiver selecionado,
	// cria as linhas da entrega, baseado no percentual informado.
	
	new strStatus, numPAM, numEntrega, percentual, entregaUniforme, numPAMLinha,
		numSeq, objPAMLinha, qtdPAMLinha, itemPAMLinha, qtdEntregaLinha, 
		objEntregaLinha, listaLinhas, result, percCalcLinha
	
	set strStatus = 1
	
	set numPAM 			= $piece(pKeyEntrega,YKOMMA,1)
	set numEntrega 		= $piece(pKeyEntrega,YKOMMA,2)
	set percentual		= $piece(pObjEntrega,Y,4)
	set entregaUniforme = $piece(pObjEntrega,Y,3)
	
	// **** Se campo "Entrega uniforme" NÃO selecionado, sai da rotina:
	if ( entregaUniforme '= 1 ) {
		quit
	}
	
	if ( percentual <= 0 ) || ( percentual > 100 ) {
		do AlertInScript^COMConst($$$Text("MED01061")_".")  ;Campo de Percentual com valor inválido. Valor deve ser entre 1 e 100 ;use text macro;15-Sep-2008
		quit
	}
	
	// Apaga as linhas existentes:
	set numSeq = "",result = 1
	for {
		set numSeq = $order(^VARSESPAMEntregaLinha(YM,numPAM,numEntrega,numSeq))
		quit:(numSeq = "")
		set result = $$KILL^COMUtils("VARSESPAMEntregaLinha", numPAM_YKOMMA_numEntrega_YKOMMA_numSeq)
		quit:('result)
	}
	quit:('result)
	
	set numPAMLinha = "", numSeq = 0
	for {
		set numPAMLinha = $order(^VARTRKPAMLineFake(YM,numPAM,numPAMLinha))
		quit:(numPAMLinha = "") 
		
		set objPAMLinha 	= $get(^VARTRKPAMLineFake(YM,numPAM,numPAMLinha,1))
		set qtdPAMLinha 	= $piece(objPAMLinha,Y,3)
		set itemPAMLinha	= $piece(objPAMLinha,Y,1)
		
		set numSeq = numSeq + 1
		
		set qtdEntregaLinha = $NORMALIZE(qtdPAMLinha * (percentual/100),0) // Arredondando
		if ( qtdEntregaLinha = 0 ) { set qtdEntregaLinha = 1 }
		set qtdOutrasEntregas = $$GetQuantEntr^VARSESPAMEntregaLinha(numPAM, numPAMLinha)
		if ( ( qtdPAMLinha - qtdOutrasEntregas ) < qtdEntregaLinha ) { set qtdEntregaLinha = ( qtdPAMLinha - qtdOutrasEntregas ) }
		if ( qtdEntregaLinha < 0 ) { set qtdEntregaLinha = 0 }
		
		// Percentual calculado da linha, que pode ser diferente do percentual uniforme,
		// devido aos arredondamentos feitos acima:
		set percCalcLinha = qtdEntregaLinha / qtdPAMLinha * 100
		
		set objEntregaLinha = ""
		set $piece(objEntregaLinha,Y,1) = numPAMLinha
		set $piece(objEntregaLinha,Y,2) = qtdEntregaLinha
		set $piece(objEntregaLinha,Y,7)	= itemPAMLinha
		//set $piece(objEntregaLinha,Y,8)	= percCalcLinha
		
		set strStatus = $$ChecarQuant^VARSESPAMEntregaLinha(numPAM_YKOMMA_numEntrega_YKOMMA_numSeq,objEntregaLinha)
		if ( strStatus '= 1) {
			;do AlertInScript^COMConst("Ocorreu um erro ao tentar definir entrega de "_percentual_"% para o produto "_itemPAMLinha_"."_$char(10)_$piece(strStatus,Y,2)) 
			do AlertInScript^COMConst($$$Text($listbuild("MED01062",percentual,itemPAMLinha))_"."_$char(10)_$piece(strStatus,Y,2))   ;Ocorreu um erro ao tentar definir entrega de %1% para o produto %2 ;use text macro;15-Sep-2008
			quit 
		}
 
		set listaLinhas(numSeq) = objEntregaLinha
		
	}
	
	if ( strStatus ) {
 
		// Gera novas linhas:
		set numSeq = ""
		for {
			set numSeq = $order(listaLinhas(numSeq))
			quit:(numSeq = "")
			set objEntregaLinha = $get(listaLinhas(numSeq))
			set qtdEntregaLinha = $piece(objEntregaLinha,Y,2)
			if ( qtdEntregaLinha > 0 ) {
				set result = $$Save^COMUtils("VARSESPAMEntregaLinha",numPAM_YKOMMA_numEntrega_YKOMMA_numSeq,objEntregaLinha)
			}
		}
	}
	
	quit
	
OnExecutePrazoDeEntrega(pYKEY,pYFELD)
	// Andre, 27/02/2008:
	// Valida campo Prazo de Entrega: não permite mais de uma entrega com
	// o mesmo Prazo de Entrega.
	
	new strResult, numPAM, numEntregaCorrente, prazoDeEntregaCorrente, numEntrega, 
		objEntrega, prazoDeEntrega
	
	set strResult = 1
	
	set numPAM = $piece(pYKEY,YKOMMA,1)
	set numEntregaCorrente = $piece(pYKEY,YKOMMA,2)
	set prazoDeEntregaCorrente = $piece(pYFELD,Y,1)
	
	if ( numPAM = "" ) || ( numEntregaCorrente = "") || ( prazoDeEntregaCorrente = "") {
		set strResult = 1
		quit strResult
	}
	
	set numEntrega = "", objEntrega = ""
	for {
		set numEntrega = $order(^VARSESPAMEntrega(YM,numPAM,numEntrega))
		quit:(numEntrega = "")
		continue:(numEntrega = numEntregaCorrente)
		set objEntrega = $get(^VARSESPAMEntrega(YM,numPAM,numEntrega,1))
		continue:(objEntrega = "")
		set prazoDeEntrega = $piece(objEntrega,Y,1)
		if ( prazoDeEntrega = prazoDeEntregaCorrente ) {
			set strResult = 0
			;do AlertInScript^COMConst("Já existe outra entrega (Num. "_numEntrega_") no mesmo prazo de "_prazoDeEntregaCorrente_"dias.")
			do AlertInScript^COMConst($$$Text($listbuild("MED01063",numEntrega,prazoDeEntregaCorrente))_".")  ;Já existe outra entrega (Num. %1) no mesmo prazo de %2 dias ;use text macro;15-Sep-2008
			quit 
		}
	}
	quit strResult
	
CriaListaDeLinhas(pYKEY)
	// Andre, 03/03/2008
	// Cria ^VARSESTempListPAMLinha que é uma lista temporária
	// de linhas da PAM relacionada à entrega corrente.
	
	new strResult, numPAM
	
	kill ^VARSESTempListPAMLinha(YM,YUSER)
	
	quit:( $get(pYKEY) = "") 
	
	set numPAM = $piece(pYKEY,YKOMMA,1)
	
	quit:( numPAM = "" )
	
	set numLinha = ""
	for {
		set numLinha = $order(^VARTRKPAMLineFake(YM,numPAM,numLinha))
		quit:( numLinha = "" )
		
		set objVARTRKPAMLineFake = $get(^VARTRKPAMLineFake(YM,numPAM,numLinha,1))
		continue:( objVARTRKPAMLineFake = "")
		
		set item		= $piece(objVARTRKPAMLineFake,Y,1)
		set itemName 	= $piece(objVARTRKPAMLineFake,Y,54)
		
		set ^VARSESTempListPAMLinha(YM,YUSER,item,1) = itemName_Y_numLinha
	}
	
	quit
	
EntregasValidas(pNumPAM,&listaStatus)
	// Andre, 18/03/2008
	// 
	// Realiza as seguintes validações nas entregas de PAM:
	// 1) Verifica se há ao menos uma entrega definida.
	// 2) Para cada linha de PAM, verifica se a quantidade na PAM é igual à 
	// soma das quantidades das entregas;
	// 3) Verifica se não há nenhuma entrega sem linhas ou sem prazo de entrega;
	// 4) Verifica se não há nenhuma linha de entrega com quantidade vazia.
	
	new strStatus, numPAMLinha, objPAMLinha, qtdPAM, qtdEntregas, numEntrega,
		objEntrega, seqLinhaEntrega, objLinhaEntrega, objPAM, naturezaEntradaPAM
	
	if ( $get(pNumPAM) = "") {
		set strStatus = "0~"_$$$Text("MED01064")  ;Número de PAM não informado ;use text macro;15-Sep-2008
		quit strStatus
	}
	
	// Andre, 30/04/2008: Obter registro da PAM:
	set objPAM = $get(^VARTRKPAM(YM,pNumPAM,1))
	if ( $get(objPAM) = "" ) {
		set strStatus = "0~"_$$$Text($listbuild("MED01065",pNumPAM))  ;Erro ao ler PAM de número '%1'
		quit strStatus
	}
	
	set strStatus = 1
	
	// Andre, 30/04/2008: Se PAM é do tipo 2 - Estoque - Doação, não é exigida a definição de entregas:
	set naturezaEntradaPAM = $piece(objPAM,Y,1)
	if ( +$get(naturezaEntradaPAM) = 2 ) {
		quit 1
	}
	
	// Andre, 05/05/2008: Se PAM é do tipo 6 - Uso Específico, não é exigida a definição de entregas:
	set naturezaEntradaPAM = $piece(objPAM,Y,1)
	if ( +$get(naturezaEntradaPAM) = 6 ) {
		quit 1
	}
	
	// Andre, 08/05/2008: Se PAM é do tipo 3 - Estoque - Ministério da Saúde, não é exigida a definição de entregas:
	set naturezaEntradaPAM = $piece(objPAM,Y,1)
	if ( +$get(naturezaEntradaPAM) = 3 ) {
		quit 1
	}
	
	// 1) Verifica se há ao menos uma entrega definida.
	if ( '$data(^VARSESPAMEntrega(YM,pNumPAM)) ) {
		set strStatus = "0~"_$$$Text($listbuild("MED01066",pNumPAM))  ;Nenhuma entrega definida para a PAM ;use text macro;15-Sep-2008
		quit strStatus
	}
 
 
	// 2) Para cada linha de PAM, verifica se a quantidade na PAM é igual à 
	// soma das quantidades das entregas.
 
	set numPAMLinha = ""
	for {
		set numPAMLinha = $order(^VARTRKPAMLineFake(YM,pNumPAM,numPAMLinha))
		quit:(numPAMLinha = "")
		
		set objPAMLinha = $get(^VARTRKPAMLineFake(YM,pNumPAM,numPAMLinha,1))
		if ( $get(objPAMLinha) = "" ) {
			set strStatus = "0~"_$$$Text($listbuild("MED01067",numPAMLinha,pNumPAM))  ;Erro ao ler linha %1 da PAM '%2' ;use text macro;15-Sep-2008
			quit
		}
		
		set qtdPAM = $piece(objPAMLinha,Y,3)
		
		set qtdEntregas = $$GetQuantEntr^VARSESPAMEntregaLinha(pNumPAM,numPAMLinha)
		
		if ( qtdPAM = qtdEntregas ) {
			set listaStatus(numPAMLinha) = 1
		}
		else {
			set listaStatus(numPAMLinha) = 0_Y_$$$Text($listbuild("MED01068",qtdPAM,qtdEntregas))  ;Qtd. na PAM (%1) diferente da qtd. nas entregas (%2) ;use text macro;15-Sep-2008
			set strStatus = 0
		}
	}
	if (strStatus '= 1) {
		quit strStatus
	}
	
	
	// 3) Verifica se não há nenhuma entrega sem linhas ou sem prazo de entrega:
 
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARSESPAMEntrega(YM,pNumPAM,numEntrega))
		quit:(numEntrega = "")
		
		set objEntrega = $get(^VARSESPAMEntrega(YM,pNumPAM,numEntrega,1))
		if ( $get(objEntrega) = "" ) {
			set strStatus = "0~"_$$$Text($listbuild("MED01069",numEntrega,pNumPAM))  ;Erro ao ler entrega '%1' da PAM '%2' ;use text macro;15-Sep-2008
			quit
		}
		
		// Verifica se há linhas para a entrega:
		if ( '$data(^VARSESPAMEntregaLinha(YM,pNumPAM,numEntrega)) ) {
			set strStatus = "0~"_$$$Text($listbuild("MED01070",numEntrega,pNumPAM))  ;Não há linhas para a entrega '%1' da PAM '%2' ;use text macro;15-Sep-2008
			quit
		}
		
		// Verifica se há prazo de entrega:
		set prazoDeEntrega = $piece(objEntrega,Y,1)
		if ( +prazoDeEntrega <= 0 ) {
			set strStatus = "0~"_$$$Text($listbuild("MED01071",+prazoDeEntrega,numEntrega,pNumPAM))  ;Prazo de entrega inválido (%1 dias) para a entrega '%2' da PAM '%3%' ;use text macro;15-Sep-2008
			quit
		}
		
		// 4) Verifica se não há nenhuma linha de entrega com quantidade vazia.
		
		set seqLinhaEntrega = ""
		for {
			set seqLinhaEntrega = $order(^VARSESPAMEntregaLinha(YM,pNumPAM,numEntrega,seqLinhaEntrega))
			quit:(seqLinhaEntrega = "")
			
			set objLinhaEntrega = $get(^VARSESPAMEntregaLinha(YM,pNumPAM,numEntrega,seqLinhaEntrega,1))
			if ( $get(objLinhaEntrega) = "" ) {
				set strStatus = "0~"_$$$Text($listbuild("MED01072",seqLinhaEntrega,numEntrega,pNumPAM))  ;Erro ao ler linha '%1' da entrega '%2' da PAM '%3' ;use text macro;15-Sep-2008
				quit
			}
			
			set qtdEntrega = $piece(objLinhaEntrega,Y,2)
			if ( +qtdEntrega <= 0 ) {
				set strStatus = "0~"_$$$Text($listbuild("MED01073",+qtdEntrega,seqLinhaEntrega,numEntrega,pNumPAM))  ;Quantidade inválida (%1) da linha '%2' da entrega '%3' da PAM '%4' ;use text macro;15-Sep-2008
				quit
			}
			
		} // Fim - for $order(^VARSESPAMEntregaLinha(YM,pNumPAM,numEntrega,seqLinhaEntrega))
 
		if (strStatus '= 1) {
			quit // Sai do laço da global de entregas da PAM
		}
 
	} // Fim - for $order(^VARSESPAMEntrega(YM,pNumPAM,numEntrega))
	
	if (strStatus '= 1) {
		quit strStatus
	}
 
	
	quit strStatus
	
ListarEntregas(pNumPAM)
	// Andre, 18/03/2008
	// Listar todas as entregas de uma PAM.
	// Esta rotina é chamada por:
	// - Evento OnAfterDataFields^VARTRKPAM (tela de PAM).
	
	new status, linhaPAM, listaStatusEntregas, cabecalhoJaImpresso, 
		lstHeader, numEntrega, objEntrega, PrazoDeEntrega, listaEntregas, 
		i, linhaDaPAM, objLinhaDaPAM, codProduto, qtdPAM, nomeProduto,
		objLinhaEntrega, qtdEntrega, somaQtdEntregas, objPAM, 
		naturezaEntradaPAM
		
	quit:($get(pNumPAM) = "")
	
	write "<BR><b>"_$$$Text("MED01074")_": </b>"  ;Situação das entregas ;use text macro;15-Sep-2008
 	
	set status = $$EntregasValidas^VARSESPAMEntrega(pNumPAM,.listaStatusEntregas)
 
	if ( status '= 1 ) {
		write $piece(status,Y,2) // Mostra mensagem de erro
		if ( $data(listaStatusEntregas) ) {
			set linhaPAM = "", cabecalhoJaImpresso = 0
			for {
				set linhaPAM = $order(listaStatusEntregas(linhaPAM))
				quit:(linhaPAM = "")
				if ( listaStatusEntregas(linhaPAM) '= 1 ) {
					write:(cabecalhoJaImpresso '= 1) "<BR>"_$$$Text("MED01075")_":"  ;Entregas inválidas para as seguintes linhas da PAM ;use text macro;15-Sep-2008
					set cabecalhoJaImpresso = 1
					write "<BR>"_linhaPAM_": "_$piece(listaStatusEntregas(linhaPAM),Y,2)
				}
			}
		}
	}
	else {
		write $$$Text("MED01076")_"."  ;Entregas válidas ;use text macro;15-Sep-2008
	}
 
	// Andre, 05/05/2008: Exibe mensagem informando que entregas não são 
	// obrigatórias, se a Natureza de Entrada da PAM for 2 - Estoque - Doação
	// ou 6 - Uso Específico.
	// Andre, 08/05/2008: idem para Natureza de Entrada 3 - Estoque - Ministério da Saúde.
	set objPAM = $get(^VARTRKPAM(YM,pNumPAM,1))
	if ( $get(objPAM) = "" ) {
		write "<BR>"_$$$Text($listbuild("MED01065",pNumPAM))_"."  ;Erro ao ler PAM de número '%1' ;use text macro;15-Sep-2008
	}
	else {
		set naturezaEntradaPAM = $piece(objPAM,Y,1)
		if ( +$get(naturezaEntradaPAM) = 2 ) {
			write "<BR>"_$$$Text("MED01077")_"."  ;Esta PAM possui Natureza de Entrada 2 - Estoque - Doação, portanto, entregas programadas não são obrigatórias para esta PAM ;use text macro;15-Sep-2008
		}
		if ( +$get(naturezaEntradaPAM) = 6 ) {
			write "<BR>"_$$$Text("MED01078")_"."  ;Esta PAM possui Natureza de Entrada 6 - Uso Específico, portanto, entregas programadas não são obrigatórias para esta PAM ;use text macro;15-Sep-2008
		}
		// Andre, 08/05/2008:
		if ( +$get(naturezaEntradaPAM) = 3 ) {
			write "<BR>"_$$$Text("MED01079")_"."  ;Esta PAM possui Natureza de Entrada 3 - Estoque - Ministério da Saúde, portanto, entregas programadas não são obrigatórias para esta PAM ;use text macro;15-Sep-2008
		}
	}
	// ---------------
	
	set lstHeader=""
	;set lstHeader=lstHeader_$listbuild("Seq","Código/Descrição","Quant.<BR>PAM")
	set lstHeader=lstHeader_$listbuild($$$Text("MED01080"),$$$Text("MED01081"),$$$Text("MED01082")_"<BR>"_$$$Text("MED01083")) ;use text macro;15-Sep-2008
	
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARSESPAMEntrega(YM,pNumPAM,numEntrega))
		quit:(numEntrega = "")
		
		set objEntrega = $get(^VARSESPAMEntrega(YM,pNumPAM,numEntrega,1))
		quit:($get(objEntrega) = "")
		
		set listaEntregas(numEntrega) = objEntrega
		set PrazoDeEntrega = $piece(objEntrega,Y,1)
		set lstHeader=lstHeader_$listbuild($$$Text("MED01085")_" "_numEntrega_"<BR>"_$$$Text($listbuild("MED01086",PrazoDeEntrega)))  ;Entrega<BR>%1 dias ;use text macro;15-Sep-2008
	}
	
	set lstHeader=lstHeader_$listbuild($$$Text("MED01082")_"<BR>"_$$$Text("MED01084"))  ;Quant<BR>Entregas ;use text macro;15-Sep-2008
 
	
	if $$Start^COMTable(lstHeader,"Entregas por linha da PAM:") {
	
		set linhaDaPAM = ""
		for i=1:1 {
			set linhaDaPAM = $order(^VARTRKPAMLineFake(YM,pNumPAM,linhaDaPAM))
			quit:(linhaDaPAM = "")
			
			set objLinhaDaPAM = $get(^VARTRKPAMLineFake(YM,pNumPAM,linhaDaPAM,1))
			continue:(objLinhaDaPAM = "")
			
			set codProduto 	= $piece(objLinhaDaPAM,Y,1)
			set qtdPAM	 	= $piece(objLinhaDaPAM,Y,3)
			
			do NewLine^COMTable()
			do InsertCell^COMTable(linhaDaPAM)
			set nomeProduto = $piece($get(^INART(YM,codProduto,1)),Y,1)
			set nomeProduto = $zconvert(nomeProduto,"O","HTML")
			do InsertCell^COMTable("("_codProduto_") "_"<INPUT SIZE=30 VALUE="""_nomeProduto_""" STYLE=""border:0px solid lightgrey; color:navy; background-color:"_$case(i#2,1:"lightgrey",0:"white")_" ;padding-top:0 "" readonly unselectable>"_$select($length(nomeProduto)>25:" ...",1:""))
			do InsertCell^COMTable(qtdPAM)
			
			set numEntrega = "",somaQtdEntregas = 0
			for {
				set numEntrega = $order(listaEntregas(numEntrega))
				quit:(numEntrega = "")
 
				set linhaEntrega = $order(^VARSESPAMEntregaLinhas(YM,1,$$^WWWUMLAU(pNumPAM,1),linhaDaPAM,numEntrega,""))
				if ( $get(linhaEntrega) '= "" ) {
					set objLinhaEntrega = $get(^VARSESPAMEntregaLinha(YM,pNumPAM,numEntrega,linhaEntrega,1))
					set qtdEntrega = $piece(objLinhaEntrega,Y,2)
					do InsertCell^COMTable(qtdEntrega_" ("_$normalize(qtdEntrega/qtdPAM*100,1)_"%)")
					set somaQtdEntregas = somaQtdEntregas + qtdEntrega
				}
				else {
					do InsertCell^COMTable("")
				}
			}
			
			do InsertCell^COMTable(somaQtdEntregas_" ("_$normalize(somaQtdEntregas/qtdPAM*100,1)_"%)")
			
			do EndLine^COMTable()
	 
		}
 
		do Stop^COMTable()
	
	} // if start^comtable
	
	quit
 
CriaEntregasPadrao(pIdPAM,pObjPAM)
	// Andre, 14/04/2008
	// Se foi definida uma entraga padrão na PAM, cria as diversas entregas uniformes correspondentes.
	new YKEY, YFELD, entrega, pNumEntrega, pObjEntrega, listaDePrazos, listaDePorcentagens, numEntregas, prazo, porcentagem,
		objVARSESPAMEntrega, strStatus
	
	quit:($get(pIdPAM) = "") ""
	quit:($get(pObjPAM) = "") ""
 
	// Se já existem entregas para esta PAM, as exclui:
	set entrega = ""
	for {
		set entrega = $order(^VARSESPAMEntrega(YM,pIdPAM,entrega))
		quit:(entrega = "")
		set strStatus = $$KILL^COMUtils("VARSESPAMEntrega",pIdPAM_","_entrega)
	}
 
	set pNumEntrega = $piece(pObjPAM,Y,17)
	set pObjEntrega = $get(^VARSESEntregaPadrao(YM,pNumEntrega,1))
	
	quit:($get(pNumEntrega) = "") ""
	quit:($get(pObjEntrega) = "") ""
	
	set listaDePrazos 		= $piece(pObjEntrega,Y,1)
	set listaDePorcentagens = $piece(pObjEntrega,Y,2)
	
	set numEntregas = $length(listaDePrazos,";")
	
	for entrega=1:1:numEntregas {
		set porcentagem = $piece(listaDePorcentagens,";",entrega)
		set prazo = $piece(listaDePrazos,";",entrega)
		
		set $$$VARSESPAMEntregaPrazodeEntrega(objVARSESPAMEntrega) = prazo
		set $$$VARSESPAMEntregaEntregaUniforme(objVARSESPAMEntrega) = 1
		set $$$VARSESPAMEntregaPercentual(objVARSESPAMEntrega) = porcentagem
		
		set strStatus = $$Save^COMUtils("VARSESPAMEntrega",pIdPAM_","_entrega,objVARSESPAMEntrega,1)
		
		if ( strStatus = 1 ) {
			do CriaEntregaUniforme(pIdPAM_","_entrega,objVARSESPAMEntrega)
			
			// Andre, 22/04/2008: Se nenhuma linha criada, exclui a entrega:
			if ( '$data(^VARSESPAMEntregaLinha(YM,pIdPAM,entrega)) ) {
				set strStatus = $$KILL^COMUtils("VARSESPAMEntrega",pIdPAM_","_entrega)
				if ('strStatus ) {
					set strStatus = "0~"_$$$Text($listbuild("MED01087",entrega,strStatus))  ;Erro ao excluir entrega '%1' que não possui linhas. Mensagem: %2 ;use text macro;15-Sep-2008
					quit
				}
			}
			
		}
	}
	
	quit
 
CanEditarEntregas
	// Habilita/desabilita botão Entregas
	new EntregaPadrao
	
	set YQ = $$$YQEnable
	
 	if YKEY="" {
	 	set YQ = 1_$$$Text("MED01088")  ;A PAM nao foi salva ;use text macro;15-Sep-2008
	 	quit
 	}
 	
 	set EntregaPadrao = $piece(YFELD,Y,17)
 	if ( $get(EntregaPadrao) '= "" ) {
 	 	set YQ = 1_$$$Text("MED01089")  ;Uma entrega Padrão foi escolhida. Não é possível editar entregas ;use text macro;15-Sep-2008
	 	quit
	}
 	
	quit
	
GetDescEntregaPAM(pListaDePAMs)
	// Andre, 22/04/2008
	new descricao, pIdPAM, objPAM, entrPadraoPAM, resumoEntrPadrao
	
	set descricao = ""
	
	quit:($get(pListaDePAMs) = "") ""
	
	if $length(pListaDePAMs,";") > 1 {
		set descricao = "Múltiplas PAMs relacionadas à esta AFM."
		quit descricao
	}
	
	set pIdPAM = pListaDePAMs
	
	set objPAM = $get(^VARTRKPAM(YM,pIdPAM,1))
	quit:($get(objPAM) = "") ""
	
	set entrPadraoPAM = $piece(objPAM,Y,17)
	
	// Se entrega padrão definida no PAM:
	if ( $get(entrPadraoPAM) '= "" ) {
		set objEntrPadrao = $get(^VARSESEntregaPadrao(YM,entrPadraoPAM,1))
		if $get(objEntrPadrao) = "" {
			set descricao = $$$Text($listbuild("MED01090",entrPadraoPAM))  ;Entrega padrão desconhecida (número %1) ;use text macro;15-Sep-2008
		}
		else {
			set resumoEntrPadrao = $piece(objEntrPadrao,Y,8)
			set descricao = $$$Text("MED01091")_": "_resumoEntrPadrao  ;Padrão ;use text macro;15-Sep-2008
		}
	}
	else {
		if $data(^VARSESPAMEntrega(YM,pIdPAM)) {
			set descricao = $$$Text("MED01092")  ;Entregas customizadas ;use text macro;15-Sep-2008
		}
		else {
			set descricao = $$$Text("MED01093")  ;Nenhuma entrega definida ;use text macro;15-Sep-2008
		}
	}
	
	quit descricao
]]></Routine>
</Export>