<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARTCIPredio" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARTCIPredio

#include COMSYS
#include VARConst
#include VARLog

	quit
	
	
ProcessarAlteracoesPredio(pYKEY)
	;do ProcessarAlteracoesPredio^VARTCIPredio(pYKEY)
	
	quit:(pYKEY="")
	
	new strStatus
	
	set strStatus = $$GravarAutoria(pYKEY)
	
	set:$$$ISOK(strStatus) strStatus = $$IsBloqueadoDesabilitado(pYKEY)
			
	if $$$ISERR(strStatus) {
		$$$Alert("Erro ao gravar dados das alterações do registro.")
		$$$VARLOGERROR("Erro ao gravar dados das alterações do registro.")
	}
	else {
		$$$Alert("Alterações aplicadas com sucesso!")	
	}
		
	do ReloadForm^COMUtilForm()
	
	quit

		
PodeProcessarAlteracoesPredio(pYKEY)
	;do PodeProcessarAlteracoesPredio^VARTCIPredio(pYKEY)
	quit:(pYKEY="")
	
	new strStatus,idLocal,idModulo,idModulo,idRua,idPredio,objPredio
	
	set strStatus = $$$OK
	
	;Separa a chave em seus respectivos valores
	set idLocal  =	$$$KEY1(pYKEY)
	set idModulo =	$$$KEY2(pYKEY)
	set idRua	 =	$$$KEY3(pYKEY)
	set idPredio =	$$$KEY4(pYKEY)
	
	if (idLocal = "") || (idModulo = "") || (idRua = "") || (idPredio = "") {
		set strStatus = "Dados incompletos"		
	}
	
	if $$$ISOK(strStatus) {		
		set objPredio = $get(^VARTCIPredio(YM,idLocal,idModulo,idRua,idPredio,1))
	
		if (objPredio = ""){
			set strStatus = "Registro não encontrado em VARTCIPredio"
		}			
	}
	
	$$$YQHandler(strStatus)
	 
	quit	
	

GetStatusPredio(pLocal,pModulo,pRua,pPredio)
	;-------------------------------------------------------------------------------
	; Retorna o status bloqueado, desabilitado ou ambos para o endereço informado, 
	; sendo que para cada um fora designado os seguintes números. 
	; 
	; strStatus: 1- Desbloqueado e habilitado
	;			 2-	Somente bloqueado
	;			 3-	Somente desabilitado
	;			 4- Bloqueado e desabilitado
	;            
	; History:
	; 18-Apr-2011	SLV		Criado
	;-------------------------------------------------------------------------------
	quit:((pLocal="") || (pModulo="") || (pRua="") || (pPredio=""))
	
	new idStatus,objPredio,stDesabilitado,stBloqueado,stExcluido
	
	set idStatus 	= 1 // Sem restrições
	set objPredio		= $get(^VARTCIPredio(YM,pLocal,pModulo,pRua,pPredio,1))
	
	set stDesabilitado = $$$VARTCIPredioPredioDesabilitado(objPredio)
	set stBloqueado	   = $$$VARTCIPredioPredioBloqueado(objPredio)
	set stExcluido	   = $$$VARTCIPredioPredioExcluido(objPredio)
	
	if (stExcluido '= "") {
		set idStatus = 5
	}
	elseif ((stDesabilitado '= "") && (stBloqueado '= "")){
		set idStatus = 4	//Desabilitado e bloqueado
	}
	elseif (stBloqueado '= "") {
		set idStatus = 2	//Somente bloqueado
	}
	elseif (stDesabilitado '= "") {
		set idStatus = 3	//Somente desabilitado
	}
	
	quit idStatus
	
	
IsBloqueadoDesabilitado(pYKEY)
	;-------------------------------------------------------------------------------
	; Reproduz o Status "Bloqueado" e "Desabilitado" para as classes Filhas           
	; 
	;
	; History:
	; 18-Apr-2011	SLV		Criado
	;-------------------------------------------------------------------------------
	quit:(pYKEY="")
	
	new strStatus,idStatus,idLocal,idModulo,idRua,idPredio,idAndar,idApartamento,
		objAndar,objApartamento,idKeyA,idKeyAp
	
	set strStatus = $$$OK
	
	set idLocal  =	$$$KEY1(pYKEY)
	set idModulo =	$$$KEY2(pYKEY)
	set idRua	 =	$$$KEY3(pYKEY)
	set idPredio =	$$$KEY4(pYKEY)
	
	set idStatus = $$GetStatusPredio(idLocal,idModulo,idRua,idPredio)
	
	if $$$ISOK(idStatus) {
		$$$Order6(^VARTCIAndar,YM,idLocal,idModulo,idRua,idPredio,idAndar)
			set idKeyA = idLocal_$$$COMMA_idModulo_$$$COMMA_idRua_$$$COMMA_idPredio_$$$COMMA_idAndar
			set objAndar = $get(^VARTCIAndar(YM,idLocal,idModulo,idRua,idPredio,idAndar,1))
			if (idStatus = 2) { 	 //Somente bloqueado
				set $$$VARTCIAndarAndarBloqueado(objAndar) 	 = 1
				set $$$VARTCIAndarAndarDesabilitado(objAndar)  = ""		
			}
			elseif (idStatus = 3) { //Somente desabilitado
				set $$$VARTCIAndarAndarBloqueado(objAndar) 	 = ""
				set $$$VARTCIAndarAndarDesabilitado(objAndar)  = 1
			}
			elseif (idStatus = 4) { //Desabilitado e bloqueado
				set $$$VARTCIAndarAndarBloqueado(objAndar) 	 = 1
				set $$$VARTCIAndarAndarDesabilitado(objAndar)  = 1
			}
			elseif (idStatus = 5) { //Excluido
				set $$$VARTCIAndarAndarBloqueado(objAndar) 	 	= 1
				set $$$VARTCIAndarAndarDesabilitado(objAndar)	= 1
				set $$$VARTCIAndarAndarExcluido(objAndar)		= 1
			}
			else { //Liberado
				set $$$VARTCIAndarAndarBloqueado(objAndar) 	 = ""
				set $$$VARTCIAndarAndarDesabilitado(objAndar)  = ""	
			}					
			set strStatus = $$$Save("VARTCIAndar",idKeyA,objAndar,$$$YES)			
			set:$$$ISOK(strStatus) strStatus = $$GravarAutoria^VARTCIAndar(idKeyA)
			
		$$$Order7(^VARTCIApartamento,YM,idLocal,idModulo,idRua,idPredio,idAndar,idApartamento)
				set idKeyAp = idLocal_$$$COMMA_idModulo_$$$COMMA_idRua_$$$COMMA_idPredio_$$$COMMA_idAndar_$$$COMMA_idApartamento
				set objApartamento = $get(^VARTCIApartamento(YM,idLocal,idModulo,idRua,idPredio,idAndar,idApartamento,1))
				if (idStatus = 2) { 	 //Somente bloqueado
					set $$$VARTCIApartamentoAptoBloqueado(objApartamento)		= 1
					set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= ""		
				}
				elseif (idStatus = 3) { //Somente desabilitado
					set $$$VARTCIApartamentoAptoBloqueado(objApartamento)		= ""
					set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= 1
				}
				elseif (idStatus = 4) { //Desabilitado e bloqueado
					set $$$VARTCIApartamentoAptoBloqueado(objApartamento) 	 	= 1
					set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= 1
				}
				elseif (idStatus = 5) { //Excluido
					set $$$VARTCIApartamentoAptoBloqueado(objApartamento) 	 	= 1
					set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= 1
					set $$$VARTCIApartamentoAptoExcluido(objApartamento)		= 1
				}
				else { //Liberado
					set $$$VARTCIApartamentoAptoBloqueado(objApartamento) 	 	= ""
					set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= ""
				}					
				set strStatus = $$$Save("VARTCIApartamento",idKeyAp,objApartamento,$$$YES)
				set:$$$ISOK(strStatus) strStatus = $$GravarAutoria^VARTCIApartamento(idKeyAp)
				
		$$$EndErr(strStatus)	; Apartamento
	$$$EndErr(strStatus)	; Andar
	}

	quit strStatus
	
GravarAutoria(pYKEY)
	quit:(pYKEY="")
	
	new strStatus,idLocal,idModulo,idRua,idPredio,objPredio
	
	set idLocal   = $$$KEY1(pYKEY)
	set idModulo  = $$$KEY2(pYKEY)
	set idRua	  = $$$KEY3(pYKEY)
	set idPredio  = $$$KEY4(pYKEY)
	
	set:(YBED="") YBED = "UNKNOWN"
	
	set objPredio = $get(^VARTCIPredio(YM,idLocal,idModulo,idRua,idPredio,1))
	
	if (objPredio '=""){
		set $$$VARTCIPredioAlteracoesAplicadasEm(objPredio)  = $Horolog
		set $$$VARTCIPredioAlteracoesAplicadasPor(objPredio) = YBED
		
		set strStatus=$$$Save("VARTCIPredio",pYKEY,objPredio,$$$YES)
	}
	
	if $$$ISERR(strStatus) $$$VARLOGERROR("Erro ao gravar dados de autoria do registro.")
	
	quit strStatus		


OnBeforeSave(pYKEY)
	quit:(pYKEY="")
	
	new idLocal,idModulo,idModulo,idRua,idPredio,blnPredio
	
	set idLocal  =	$$$KEY1(pYKEY)
	set idModulo =	$$$KEY2(pYKEY)
	set idRua	 =	$$$KEY3(pYKEY)
	set idPredio =	$$$KEY4(pYKEY)
	
	set blnPredio = $data(^VARTCIPredio(YM,idLocal,idModulo,idRua,idPredio,1))
	
	if ('blnPredio) {
		$$$Alert("Registro inexistente!")
		set $$$KEY4(pYKEY) = ""
		do GoToForm^COMUtils("VARTCIPredio",pYKEY)
	}

	quit	
]]></Routine>
</Export>