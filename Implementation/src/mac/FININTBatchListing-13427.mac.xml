<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FININTBatchListing" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FININTBatchListing
	
#include COMSYS
#include FINConst
#include CST
	/*------------------------------------------------------------------------------
	; FINANCE Interface Batch - List All Lines
	; 
	; History:
	; 19-Jan-2006	GRF		SR14215 : Created
	;-----------------------------------------------------------------------------*/
	
ShowLines
	/*------------------------------------------------------------------------------
	; Show table of all details
	; 
	; History
	; 22-Jul-2011	GRF		-: Order macros
	; 24-Oct-2006	JW		SR15101: Index Batch number! Use Order macros.
	; 19-Jan-2006	GRF		SR14215 : Created
	;-----------------------------------------------------------------------------*/
	new idBatch,idBlock,idBlockMinor,idLine,lstHeader,idxBatch
	
	set idBatch = $get(%KEY("YKEY"))
	quit:$get(idBatch)=""     ; No display if blank form
	quit:$get(YUSER)=""
	quit:'$data(^FININTBatch(YM,idBatch))
	
	set lstHeader = ""
	
	do Start^COMTable(lstHeader,"")
	
	/*-------------------------------------------------------------------------------------------------- +++++
	; Block Number |        | Source Details | Document Date | Posted Date |  User  |    Department    |
	;---------------------------------------------------------------------------------------------------
	;              |        |                |               |             |        |                  |    Block
	;---------------------------------------------------------------------------------------------------
	;   Account    | Amount | Interface Type |   Reference   | Parameters  | Source | Cost Matrix Keys |
	;--------------------------------------------------------------------------------------------------- +++++
	;              |        |                |               |             |        |                  |     Line
	;-----------------------------------------------------------------------------------------------------
	;              |        |                |               |             |        |                  |     Line
	;---------------------------------------------------------------------------------------------------
	;              |        |                |               |             |        |                  |     Line
	;--------------------------------------------------------------------------------------------------- +++ */
	
	set idxBatch = $$$Index(idBatch)
	set idBlock = ""
	for {
		set idBlock = $order(^FININTBatchLines(YM,1,idxBatch,idBlock))
		quit:idBlock=""
		
		do Block(idBatch,idBlock)
		set idBlockMinor = ""
		for {
			set idBlockMinor = $order(^FININTBatchLines(YM,1,idxBatch,idBlock,idBlockMinor))
			quit:idBlockMinor=""
			
			set idLine = ""
			for {
				set idLine = $order(^FININTBatchLines(YM,1,idxBatch,idBlock,idBlockMinor,idBatch,idLine))
				quit:idLine=""

				do Line(idBatch,idBlock,idLine)
			}
		}
	}
	do Stop^COMTable()
	quit
	
	
Block(pidBatch,pidBlock)
	/*------------------------------------------------------------------------------
	; Show block
	; 
	; History
	; 25-Oct-2006	JW		SR15101: Call redirection
	; 19-Jan-2006	GRF		SR14215: Created
	;-----------------------------------------------------------------------------*/
 
	new idKey,LinkForm,LinkPage,LinkPara,objBlock,Span1,Span2,Strong0,Strong1,TextSize
	
	set Strong1  = "<STRONG>"
	set Strong0  = "</STRONG>"
	set Span1    = 7
	set Span2    = 2
	set TextSize = 1
	
	
	if pidBlock>1 {
		do NewLine^COMTable("white")
					
		do InsertCell^COMTable("&nbsp;",,,,,,Span1,TextSize)
		
		do EndLine^COMTable()
	}
	
	;---------------------------------------
	
	do NewLine^COMTable("khaki")
				
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineBlockNumber_"&nbsp;"_Strong0,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchBlockSourceDetails_"&nbsp;"_Strong0,,,,"CENTER",,Span2)
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchBlockDocumentDate_"&nbsp;"_Strong0,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchBlockPostedDate_Strong0,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchBlockUser1_Strong0,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchBlockDepartment_Strong0,,,,"CENTER")
	
	do EndLine^COMTable()
	
	;---------------------------------------
	
	do NewLine^COMTable("khaki")
	
	set idKey    = pidBatch
	set LinkForm = "FININTBatch"
	set LinkPage = 2
	set LinkPara = pidBlock
	
	set objBlock = $get(^FININTBatchBlock(YM,pidBatch,pidBlock,1))
	
	do InsertCell^COMTable(pidBlock                                                                 ,LinkForm,idKey,LinkPara,,LinkPage)
	do InsertCell^COMTable($$Detail^FININTBatchQuery($$$FININTBatchBlockSourceDetails(objBlock))_"&nbsp;",LinkForm,idKey,LinkPara,,LinkPage,Span2)		//SR15101
	do InsertCellDate^COMTable($$$FININTBatchBlockDocumentDate(objBlock)                            ,LinkForm,idKey,LinkPara, LinkPage)
	do InsertCellTime^COMTable($$$FININTBatchBlockPostedDate(objBlock)                              ,LinkForm,idKey,LinkPara, LinkPage)
	do InsertCell^COMTable($$$FININTBatchBlockUser1(objBlock)_"&nbsp;"                              ,LinkForm,idKey,LinkPara,,LinkPage)
	do InsertCell^COMTable($$$FININTBatchBlockDepartment(objBlock)                                  ,LinkForm,idKey,LinkPara,,LinkPage)
	
	do EndLine^COMTable()
	
	;---------------------------------------
	
	do NewLine^COMTable("khaki")
				
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineAccount_"&nbsp;"_Strong0       ,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineAmount_"&nbsp;"_Strong0        ,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineInterfaceType_"&nbsp;"_Strong0 ,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineReference_"&nbsp;"_Strong0     ,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineParameters_"&nbsp;"_Strong0    ,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineSource_"&nbsp;"_Strong0        ,,,,"CENTER")
	do InsertCell^COMTable(Strong1_"&nbsp;"_$$$StrFININTBatchLineCostMatrixKeys_"&nbsp;"_Strong0,,,,"CENTER")
	
	do EndLine^COMTable()
	
	quit
	
Line(pidBatch,pidBlock,pidLine)
	/*------------------------------------------------------------------------------
	; Show batch line
	; 
	; History
	; 19-Jan-2006	GRF		SR14215 : Created
	;-----------------------------------------------------------------------------*/
	new idAccount,objLine
	
	set objLine = $get(^FININTBatchLine(YM,pidBatch,pidLine,1))
	
	;set LinkForm  = "FINGLAccount"
	set idAccount = $$$FININTBatchLineAccount(objLine)
	
	do NewLine^COMTable("")
				
	;do InsertCell^COMTable(idAccount_"&nbsp;",LinkForm,idAccount)    ; Don't link directly due to problems returning <GRF>
	
	do InsertCell^COMTable(idAccount_"&nbsp;")
	do InsertCurrencyCell^COMTable($$$FININTBatchLineAmount(objLine))
	do InsertCell^COMTable($$$FININTBatchLineInterfaceType(objLine))
	do InsertCell^COMTable($$$FININTBatchLineReference(objLine)_"&nbsp;")
	do InsertCell^COMTable($$$FININTBatchLineParameters(objLine)_"&nbsp;")
	do InsertCell^COMTable($$$FININTBatchLineSource(objLine)_"&nbsp;")
	do InsertCell^COMTable($$CSTKeys(objLine)_"&nbsp;")
	
	do EndLine^COMTable()
	
	quit
	
CSTKeys(objLine)
	/*------------------------------------------------------------------------------
	; Show CST Keys if exist
	; 
	; History
	; 19-Jan-2006	GRF		SR14215 : Created
	;-----------------------------------------------------------------------------*/
	new CSTMatKeys,EffectDate
	
	quit:'$$EXIST^%R("CSTCommon.OBJ",$get(YUCI)) ""
	
	set CSTMatKeys   = $$$FININTBatchLineCostMatrixKeys(objLine)
	
	if CSTMatKeys'="" {
		; Display Date as Literal with original key information
		set EffectDate = $piece(CSTMatKeys,$$$COMMA,4)
		if EffectDate'="" {
			set EffectDate = $$^WWWDATE(EffectDate)
			set CSTMatKeys=$piece(CSTMatKeys,$$$COMMA,1,3)_$$$COMMA_EffectDate
		}
	}
	
	quit CSTMatKeys
	
]]></Routine>
</Export>