<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPOTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPOTable	

#include COMSYS
#include INConst
#include %occInclude

ShowItems(pidEvent,pidParameters)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 06-Jan-2010	DWR		SR17074: Only include the line if it has a valid unit
	; 09-Dec-2009	DWR		SR17074: Created
	;-------------------------------------------------------------------------------
	new arrSupply,blnMatch,dteDue,fltQtyReq,idGrid,idItem,idINPO,idINPOLine,idKey
	new idOtherReq,idReq,idSoldTo,idSupplier,idTableLine,idUnit,lstColTypes
	new lstHeaders,objINPOLine,strLocDec
	
	set strStatus = $$$OK
	set idGrid    = $$$DYNTABLEGridLineFocusGrid(pidParameters)
	
	
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dINRequisition")
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dINRequisitionOther")
	kill arrSupply(YUSER)
 
	set idKey = $$ReferenceKey^COMGridEdit31Interface(idGrid,$$$DYNTABLEGridLineFocusRow(pidParameters)) 

	set idINPO	    = $$$KEY1(idKey)
	set idSupplier  = $$$INPOSupplierCode($get(^INPO(0,idINPO,1)))
	set idINPOLine  = $$$KEY2(idKey)
	set objINPOLine = $$$GRIDGetYFELD(idINPOLine)
	
	if objINPOLine '=$$$NULLOREF {
		set idItem = $$$INPOLineItem(objINPOLine)
	}
	if idINPOLine = "" {
		do DrawTable^WWW120DynTable($listbuild($$$Text("IN01164")),"dINRequisition",$listbuild($listbuild("left","display")),pidParameters,"",pidEvent)
	
	} else {
		set idTableLine = ""
		set idReq = ""
		
		;----------------------------
		;  Included on lines
		;----------------------------
		for{
			set idReq = $order(^INRequisitions(0,19,$$$Index(idINPO),YM,idReq))
			quit:idReq=""
		
			set objReq = $get(^INRequisition(0,YM,idReq,1))
			set arrSupply(YUSER,idReq)=""
			
			if idItem = $$$INRequisitionItem(objReq) {
				set idSoldTo = $$$INRequisitionLocation(objReq)
				set strLocDec = $$$INLIEFShortName($get(^INLIEF(0,idSoldTo,1)))
				set fltQtyReq = $$$INRequisitionQuantity(objReq)
				set idUnit    = $$$INRequisitionUnit(objReq)
				if idUnit '="" {
					set strUnit = $$$AppEnum("EINHEIT",idUnit)
				} else {
					set strUnit =  ""
				}
				set dteDue	  = $$^WWWDATE($$$INRequisitionDueDate(objReq))
			
			
				set ^CacheTempDynamicTable(YUCI,YUSER,"dINRequisition",$increment(idTableLine),"data") = idReq
				set ^CacheTempDynamicTable(YUCI,YUSER,"dINRequisition",idTableLine) = idReq_Y_"("_idSoldTo_") "_strLocDec_Y_fltQtyReq_Y_strUnit_Y_dteDue
			}
		}
		;--------------------------
		; Not Included on Lines
		;--------------------------
		set blnMatch=$$$NO
		set idOtherReq = "" 
		for{
			set idOtherReq = $order(^INRequisitions(0,9,$$$Index(idSupplier),YM,idOtherReq))
			quit:idOtherReq=""
			
			if '$data(arrSupply(YUSER,idOtherReq)) {
				set objOtherReq = $get(^INRequisition(0,0,idOtherReq,1))
				if ($$$INRequisitionINPOID(objOtherReq)="") && ($$$INRequisitionStatus(objOtherReq) < 4) {    ; only include ones not already on a line
					set idSoldTo = $$$INRequisitionLocation(objOtherReq)
					set strLocDec = $$$INLIEFShortName($get(^INLIEF(0,idSoldTo,1)))
					set fltQtyReq = $$$INRequisitionQuantity(objOtherReq)
					set idItem    = $$$INRequisitionItem(objOtherReq)
					set strItem   = $$$INARTSearchName($get(^INART(0,idItem,1)))
					set idUnit    = $$$INRequisitionUnit(objOtherReq)
					if idUnit'="" {
						set strUnit   = $$$AppEnum("EINHEIT",idUnit)
					} else {
						set strUnit=""
					}
					set dteDue	  = $$^WWWDATE($$$INRequisitionDueDate(objOtherReq))
					set blnMatch = $$$YES
					
					if strUnit'=""{    ; SR17074 06-Jan-2009
						set ^CacheTempDynamicTable(YUCI,YUSER,"dINRequisitionOther",$increment(idTableLine),"data") = idOtherReq
						set ^CacheTempDynamicTable(YUCI,YUSER,"dINRequisitionOther",idTableLine) = idOtherReq_Y_"("_idSoldTo_") "_strLocDec_Y_strItem_Y_fltQtyReq_Y_strUnit_Y_dteDue
					}
				}
			}
			
		}		
		if blnMatch=$$$YES{	
			set strStatus = $$BuildDynTableHeaderInc(.lstHeaders,.lstColTypes)
					; setup JS Callback
			set strRemoveFunction   = "FillLine"  
			if $$$ISERR(strStatus) {

				write "function "_strRemoveFunction_"() {"
				write "alert('"_$$$JSText($$$Text(strStatus))_"');"
				write "}"
			} else {
				write "function "_strRemoveFunction_"() {"
				write "CallBack(""RemoveSupply^INPOLineSelect"", getRowNum(getFocusField()), this.DiscData[0]);"
				write "}"
			}
			
			do DrawTable^WWW120DynTable(lstHeaders,"dINRequisition",lstColTypes,pidParameters,strRemoveFunction,pidEvent,"RemoveSupply^INPOLineSelect",$$$Text("IN01175"))	 ;"Pegged Supply Records"	
			set strAddFunction   = "AddSupply"  
			if $$$ISERR(strStatus) {

				write "function "_strAddFunction_"() {"
				write "alert('"_$$$JSText($$$Text(strStatus))_"');"
				write "}"
			} else {
				write "function "_strAddFunction_"() {"
				write "CallBack(""AddSupply^INPOLineSelect"", getRowNum(getFocusField()), this.DiscData[0]);"
				write "}"
			}

			set strStatus = $$BuildDynTableHeaderOther(.lstHeaders,.lstColTypes)
			do DrawTable^WWW120DynTable(lstHeaders,"dINRequisitionOther",lstColTypes,pidParameters,strAddFunction,pidEvent,"AddSupply^INPOLineSelect",$$$Text("IN01176"))	 ;"For Possible Inclusion"	

		} else {
			set strAddFunction   = "addSupply"  
			if $$$ISERR(strStatus) {

				write "function "_strAddFunction_"() {"
				write "alert('"_$$$JSText($$$Text(strStatus))_"');"
				write "}"
			} else {
				write "function "_strAddFunction_"() {"
				write "CallBack(""RemoveSupply^INPOLineSelect"", getRowNum(getFocusField()), this.DiscData[0]);"
				write "}"
			}
			set strStatus = $$BuildDynTableHeaderInc(.lstHeaders,.lstColTypes)
			do DrawTable^WWW120DynTable(lstHeaders,"dINRequisition",lstColTypes,pidParameters,strAddFunction,pidEvent,"RemoveSupply^INPOLineSelect",$$$Text("IN01175"))	 ;"Pegged Supply Records"	
		}
	}
	quit strStatus

BuildDynTableHeaderInc(&plstHeaders,&plstColTypes)
	;-------------------------------------------------------------------------------
	; Setup Table Headers
	; receipt Number - Item Number - Received Amount
	;    (1)                 (2)               (3)      (4)	     (5)
	;   Supply Number   "Sold To Location" "Quantity" "Unit" "Due Date"
	;                         
	; 
	; History:
	; 20-Mar-2009	DWR	 SR17074: Created 
	;-------------------------------------------------------------------------------
	
	;---------------------------------------  1
	set plstHeaders  = $listbuild($$$Text(32614))       ; "Supply"
	set plstColTypes = $listbuild($listbuild("left","display"))
	
	;---------------------------------------  2
	set plstHeaders  =  plstHeaders_$listbuild($$$Text("IN01165"))       ; "Sold To Location"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  3
	set plstHeaders  = plstHeaders_$listbuild($$$Text(32025))             ; "Quantity"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  4     
	set plstHeaders  = plstHeaders_$listbuild($$$Text(34128))             ; "Unit"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))

	;---------------------------------------  5     
	set plstHeaders  = plstHeaders_$listbuild($$$Text(33287))             ; "Due Date"
	set plstColTypes = plstColTypes_$listbuild($listbuild("right","display"))
	

	quit $$$OK
	
BuildDynTableHeaderOther(&plstHeaders,&plstColTypes)
	;-------------------------------------------------------------------------------
	; Setup Table Headers
	; receipt Number - Item Number - Received Amount
	;    (1)                 (2)               (3)      (4)	     (5)
	;   Supply Number   "Sold To Location" "Quantity" "Unit" "Due Date"
	;                         
	; 
	; History:
	; 20-Mar-2009	DWR	 SR17074: Created 
	;-------------------------------------------------------------------------------
	
	;---------------------------------------  1
	set plstHeaders  = $listbuild($$$Text(32614))       ; "Supply"
	set plstColTypes = $listbuild($listbuild("left","display"))
	
	;---------------------------------------  2
	set plstHeaders  =  plstHeaders_$listbuild($$$Text("IN01165"))       ; "Sold To Location"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  3
	set plstHeaders  = plstHeaders_$listbuild($$$Text(32024))             ; "Item"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))

	;---------------------------------------  3
	set plstHeaders  = plstHeaders_$listbuild($$$Text(32025))             ; "Quantity"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  4     
	set plstHeaders  = plstHeaders_$listbuild($$$Text(34128))             ; "Unit"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))

	;---------------------------------------  5     
	set plstHeaders  = plstHeaders_$listbuild($$$Text(33287))             ; "Due Date"
	set plstColTypes = plstColTypes_$listbuild($listbuild("right","display"))
	

	quit $$$OK



]]></Routine>
</Export>