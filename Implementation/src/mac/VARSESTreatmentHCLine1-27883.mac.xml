<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESTreatmentHCLine1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include VARConst
#include VARSESFarmaciaAltoCusto
 
VARSESTreatmentHCLine1
	quit
 
OnLineAdded(pintRowCount="",pstrKey="")
	quit $$$OK
 
GetMes(pdatReferencia="")
	if ($length(pdatReferencia) = 0) {
		set pdatReferencia = $$$VARSESTreatmentHCLine1DataReferencia(YFELD)
	}
 
	quit $$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia)
 
UpdateStatus(pidTreatment)
	new strStatus, idLine, idLine1, objLine1
 
	if ($length(pidTreatment) = 0) quit
 
	set strStatus = $$$OK
	set idLine = $order(^VARSESTreatmentHCLine(YM, pidTreatment, ""))
	while ($length(idLine) > 0) {
		set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
			idLine, ""))
 
		while ($length(idLine1) > 0) {
			set objLine1 = $get(^VARSESTreatmentHCLine1(YM, pidTreatment,
				idLine, idLine1, 1))
 
			set datProximoMes = $zdateh($System.SQL.DATEADD("mm", 1,
				$zdate($$$VARSESTreatmentHCLine1DataReferencia(objLine1), 3)), 3)
 
			if ($$GetQuantidadeMensalLinhaDisponivel^VARSESTreatmentHCLine( 
					pidTreatment, idLine, 
					$$$VARSESTreatmentHCLine1DataReferencia(objLine1))){ 
				set $$$VARSESTreatmentHCLine1Atendido(objLine1) = $$$NO 
				set strStatus = $$$Save("VARSESTreatmentHCLine1",pidTreatment_$$$COMMA_idLine_$$$COMMA_idLine1,objLine1,$$$YES) 
				if ('$$$ISOK(strStatus)) quit 
			} 
			if (('$$$VARSESTreatmentHCLine1Atendido(objLine1)) &&
				((datProximoMes <= $$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil()) ||
				 ('$$GetQuantidadeMensalLinhaDisponivel^VARSESTreatmentHCLine(
					pidTreatment, idLine,
					$$$VARSESTreatmentHCLine1DataReferencia(objLine1))))) {
				set $$$VARSESTreatmentHCLine1Atendido(objLine1) = $$$YES
				set strStatus = $$$Save("VARSESTreatmentHCLine1",pidTreatment_$$$COMMA_idLine_$$$COMMA_idLine1,objLine1,$$$YES)
				if ('$$$ISOK(strStatus)) quit
			}
 
			set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
				idLine, idLine1))
		}
		if ('$$$ISOK(strStatus)) quit
 
		set idLine = $order(^VARSESTreatmentHCLine(YM, pidTreatment, idLine))
	}
	quit strStatus
 
SetMotivoSaida(pidTreatment, pdatReferencia, pintMotivoSaida, pstrCBO)
	new strStatus, arrMeses, idLine, idLine1, objLine1

	if (($length(pidTreatment) = 0) || ($length(pdatReferencia) = 0) ||
		($length(pintMotivoSaida) = 0)) {
		quit "0 Parâmetro inválido"
	}
 
	set strStatus = $$$OK
 
	kill arrMeses
 	set bolTudoEstornado = 1
 	if (pintMotivoSaida = $$$MotivoSaidaConsumoNormal) {
		set idLine = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, ""))
		while ($length(idLine) > 0) {
			set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
				idLine, ""))
 
			while ($length(idLine1) > 0) {
				set objLine1 = $get(^VARSESTreatmentHCLine1(YM, pidTreatment,
					idLine, idLine1, 1))
 
				if ($$GetQuantidadeMensalLinhaEmUso^VARSESTreatmentHCLine(
						pidTreatment, idLine,
						$$$VARSESTreatmentHCLine1DataReferencia(objLine1), 0) > 0) {
					set strMesAtual = $$GetMes^VARSESFarmaciaAltoCustoUtil($$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil())
					set strMesLinha = $$GetMes^VARSESFarmaciaAltoCustoUtil(	$$$VARSESTreatmentHCLine1DataReferencia(objLine1))
					set bolComp1 = $piece(strMesAtual,"/",2) '> $piece(strMesLinha,"/",2)
					set bolComp2 = $piece(strMesAtual,"/",1) '> $piece(strMesLinha,"/",1)
					if ( bolComp1 && bolComp2) 
					{
						set bolTudoEstornado = 0
					}
					if ($length($$$VARSESTreatmentHCLine1CBO(objLine1)) = 0) {
						set $$$VARSESTreatmentHCLine1CBO(objLine1) = pstrCBO
						set strStatus = $$$Save("VARSESTreatmentHCLine1",pidTreatment_$$$COMMA_idLine_$$$COMMA_idLine1,objLine1,$$$YES)
					}
					if ('$$$ISOK(strStatus)) quit
 
					set arrMeses(strMesLinha) = ""
				}
 
				set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
					idLine, idLine1))
			}
			if ('$$$ISOK(strStatus)) quit
 
			set idLine = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, idLine))
		}
	}
	else {
		set bolTudoEstornado = 0
		set arrMeses($$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia)) = ""
	}
 
	if ($$$ISOK(strStatus)) {
		set idLine = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, ""))
		while ($length(idLine) > 0) {
			set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
				idLine, ""))
 
			while ($length(idLine1) > 0) {
				set objLine1 = $get(^VARSESTreatmentHCLine1(YM, pidTreatment,
					idLine, idLine1, 1))
 				if ('bolTudoEstornado){
					if ($data(arrMeses($$GetMes^VARSESFarmaciaAltoCustoUtil(
							$$$VARSESTreatmentHCLine1DataReferencia(objLine1))))) {
						
							if ($length($$$VARSESTreatmentHCLine1MotivoSaida(objLine1)) = 0) {
								set $$$VARSESTreatmentHCLine1MotivoSaida(objLine1) = pintMotivoSaida
								set strStatus = $$$Save("VARSESTreatmentHCLine1",pidTreatment_$$$COMMA_idLine_$$$COMMA_idLine1,objLine1,$$$YES)
							}
							elseif ($$$VARSESTreatmentHCLine1MotivoSaida(objLine1) '= pintMotivoSaida) {
								set strStatus = "0 "_idLine_"Motivo de saída já estava definido e com valor diferente do informado"
							}
							if ('$$$ISOK(strStatus)) quit
					}
				} else {
					set strMesAtual = $$GetMes^VARSESFarmaciaAltoCustoUtil($$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil())
					set strMesLinha = $$GetMes^VARSESFarmaciaAltoCustoUtil(	$$$VARSESTreatmentHCLine1DataReferencia(objLine1))
					set bolComp1 = $piece(strMesAtual,"/",2) '> $piece(strMesLinha,"/",2)
					set bolComp2 = $piece(strMesAtual,"/",1) '> $piece(strMesLinha,"/",1)
					if ( bolComp1 && bolComp2) 
					{
						set $$$VARSESTreatmentHCLine1MotivoSaida(objLine1) = ""
						set $$$VARSESTreatmentHCLine1CBO(objLine1) = ""
						set strStatus = $$$Save("VARSESTreatmentHCLine1",pidTreatment_$$$COMMA_idLine_$$$COMMA_idLine1,objLine1,$$$YES)
					}	
				}
				set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
					idLine, idLine1))
			}
			if ('$$$ISOK(strStatus)) quit
 
			set idLine = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, idLine))
		}
	}
 
	quit strStatus
 
GetMotivoSaida(pidTreatment, pidLine, pdatReferencia)
	new idSeq, datRef, intMotivo
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0)) quit ""
 
	set pdatReferencia = +pdatReferencia
	set intMotivo = ""
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 		set datRef = $$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1)
		if ($$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia) =
				$$GetMes^VARSESFarmaciaAltoCustoUtil(datRef)) {
			set intMotivo = $$$VARSESTreatmentHCLine1MotivoSaida(objTreatmentLine1)
			quit
		}
		
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
 	
 	quit intMotivo
 	
GetSeq(pidTreatment, pidTreatmentLine, pstrReferencia)
 	set idSeq = ""
 	if (pstrReferencia = "") {
 		set strRef = $$GetMes^VARSESFarmaciaAltoCustoUtil(
				$$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil())
 	} else {
	 	set strRef = pstrReferencia
	}
	set datMesReferencia = $zdateh($piece(strRef,"/",1)_"/01/"_ $piece(strRef,"/",2))
 
	set idSeq = $order(^VARSESTreatmentHCLine1s(0,1,pidTreatment, pidTreatmentLine,
			datMesReferencia, pidTreatment, pidTreatmentLine, ""))
 	quit idSeq
]]></Routine>
</Export>