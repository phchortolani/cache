<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARCompraEntregaModelo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARCompraEntregaModelo
#include COMSYS

OnBeforeSave
	set $piece(YFELD,Y,3) = $$MontaDescricao(YKEY,YFELD)
	quit
	
MontaDescricao(pNumEntrega,pObjEntrega)
	new strDescricao, listaDePrazos, listaDePorcentagens, numEntregas, primeiraEntregaBreve, textoAte
	
	quit:($get(pNumEntrega) = "") ""
	quit:($get(pObjEntrega) = "") ""
	
	set listaDePrazos 			= $piece(pObjEntrega,Y,1)
	set listaDePorcentagens 	= $piece(pObjEntrega,Y,2)
	set primeiraEntregaBreve	= $piece(pObjEntrega,Y,5) // Primeira entrega o mais breve possível?
	
	set numEntregas = $length(listaDePrazos,";")
	
	set strDescricao = ""
	for i=1:1:numEntregas {
		set:(i '= 1) strDescricao = strDescricao_", "
		if (( i = 1 ) && ( $get(primeiraEntregaBreve) )) {
			set textoAte = "até "
		}
		else {
			set textoAte = ""
		}
		set strDescricao = strDescricao_$piece(listaDePorcentagens,";",i)_"% em "_textoAte_$piece(listaDePrazos,";",i)_" dias"
	}
	set strDescricao = strDescricao_", após o recebimento da NE e OF pelo fornecedor. Os dias são contados corridos."
	
	if (pNumEntrega = 0) {
		set strDescricao = "As entregas são definidas conforme solicitação, diretamente ao fornecedor."
	}
	
	quit strDescricao
	
	
getDescricao(pNumEntrega)
	new strDescricao, objEntrega
	
	set strDescricao = ""
	
	quit:($get(pNumEntrega) = "") ""
	
	set objEntrega = $get(^VARCompraEntregaModelo(YM,pNumEntrega,1))
	quit:($get(objEntrega) = "") ""
	
	set strDescricao = $piece(objEntrega,Y,3)
	
	quit strDescricao
]]></Routine>
</Export>