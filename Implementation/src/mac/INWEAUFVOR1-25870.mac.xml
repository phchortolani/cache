<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEAUFVOR1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEAUFVOR1
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SPEICHERN VORERFASSUNG
	; 
	; Updates : ^INWEAUFVOR - Temporary copy of ^INWEAUF; details are moved across
	;                         on posting
	; 
	; ByRef :
	;	YKEY		idOrder,idLine
	;	YFELD		objINWEAUFVOR?   see FIXME NOTE about resetting
	;	YLOCATION
	;
	; Returns :
	;
	;
	; History :
	; 16-Jul-2007	GRF		Doco; FIX ME
	; 12-Oct-2006	GRF		Doco
	; 29-Sep-2006	GRF		Doco; quits
	; 17.05.2004	FIS
	;-------------------------------------------------------------------------------
	NEW APO,AUF,AUF1,BETR,LAGER
	
	; FIXME : <GRF> Doesn't new DAT, ERLEDIGT, POS1, or SATZ.
	;+++++++++++++++++++++++++++++++++++++++
	;	POS1		objINAUFP
	;	SATZ		objINWEAUF
	;	SATZ		objINWEAUFALT
	;+++++++++++++++++++++++++++++++++++++++
	
	QUIT:$GET(YKEY)=""
	QUIT:$GET(YFELD)=""
	
	;ALLE POSITIONEN DES AUFTRAGES EINLAGERN    ; D30 $$$INWEAUFVORPreliminaryEnterAllLineIt (later $$$INWEAUFAllLineItemsReceived) ?
	IF $PIECE(YFELD,Y,30)=1 DO 
	. ; FIXME : <GRF> The variable YFELD is reset to null and repopulated
	. ;               to save as INWEAUFVOR without being newed so the
	. ;               following two lines are ineffective and the byRef
	. ;               value returned will have no direct connection to the
	. ;               original record.
	. SET $PIECE(YFELD,Y,30)=0    ;WIEDER AUSSCHALTEN  ;re- eliminate 
	. SET $PIECE(YFELD,Y,48)=POS  ;SPEICHERN ERFASSTE POSITION BEI GESAMT-WE ;Save next to 
	. ;
	. SET AUF  = $PIECE(YKEY,",",1)         QUIT:AUF=""
	. SET APO  = $PIECE(YKEY,",",2)         QUIT:APO=""        ; AUFTRAGSPOSITION, DIE ERFASST WURDE
	. SET AUF1 = $GET(^INAUF(YM,AUF,1))     QUIT:AUF1=""
	. SET BETR = $PIECE(AUF1,Y,6)  ;BETRIEB
	. IF BETR="" SET BETR = YLOCATION
	. SET LAGER = $PIECE($GET(^INVORGB(YM,YM,BETR,1)),Y,1)     ; D1  $$$INVORGBOnHandLocation()
	. IF LAGER="" SET LAGER=$ORDER(^INLP(YM,BETR,""))
	. IF LAGER="" SET LAGER=0
	. ;
	. ;----- INFORMATIONEN AUS ERFASSTEN WARENEINGÄNGEN ------
	. SET SATZ=""
	. SET DAT=0
	. IF $DATA(^INWEAUF(YM,AUF)) DO
	. . NEW POS,BET,LAP,WED
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INWEAUF(YM,AUF,POS)) QUIT:POS=""  DO
	. . . SET BET=""
	. . . FOR  SET BET=$ORDER(^INWEAUF(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . . SET LAP=""
	. . . . FOR  SET LAP=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . SET WED=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP,""),-1)               ; Last Date for order line and stock locn
	. . . . . QUIT:WED=""
	. . . . . SET ERLEDIGT(POS)=""
	. . . . . IF WED>DAT IF POS=APO DO
	. . . . . . SET SATZ  = $GET(^INWEAUF(YM,AUF,POS,BET,LAP,WED,1))
	. . . . . . SET BETR  = BET
	. . . . . . SET DAT   = WED
	. . . . . . SET LAGER = LAP
	. ;
	. IF $DATA(^INWEAUFALT(YM,AUF)) DO
	. . NEW POS,BET,LAP,WED
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INWEAUFALT(YM,AUF,POS)) QUIT:POS=""  DO
	. . . SET BET=""
	. . . FOR  SET BET=$ORDER(^INWEAUFALT(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . . SET LAP=""
	. . . . FOR  SET LAP=$ORDER(^INWEAUFALT(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . SET WED=$ORDER(^INWEAUFALT(YM,AUF,POS,BET,LAP,""),-1)               ; Last Date for order line and stock locn
	. . . . . QUIT:WED=""
	. . . . . SET ERLEDIGT(POS)=""
	. . . . . IF WED>DAT IF POS=APO DO
	. . . . . . SET SATZ  = $GET(^INWEAUFALT(YM,AUF,POS,BET,LAP,WED,1))
	. . . . . . SET BETR  = BET
	. . . . . . SET DAT   = WED
	. . . . . . SET LAGER = LAP
	. ;
	. IF $DATA(^INWEAUFVOR(YM,AUF)) DO
	. . NEW POS,BET,LAP,WED
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INWEAUFVOR(YM,AUF,POS)) QUIT:POS=""  DO
	. . . SET BET=""
	. . . FOR  SET BET=$ORDER(^INWEAUFVOR(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . . SET LAP=""
	. . . . FOR  SET LAP=$ORDER(^INWEAUFVOR(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . SET WED=$ORDER(^INWEAUFVOR(YM,AUF,POS,BET,LAP,""),-1)               ; Last Date for order line and stock locn
	. . . . . QUIT:WED=""
	. . . . . SET ERLEDIGT(POS)=""
	. . . . . IF WED>DAT IF POS=APO DO
	. . . . . . SET SATZ  = $GET(^INWEAUFALT(YM,AUF,POS,BET,LAP,WED,1))
	. . . . . . SET BETR  = BET
	. . . . . . SET DAT   = WED
	. . . . . . SET LAGER = LAP
	. ;
	. ;------  SPEICHERN WE 
	. DO  ;WE BUCHEN ;In 
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . . ;
	. . . QUIT:$DATA(ERLEDIGT(POS))  ;POSITION BEREITS EINGELAGERT ;yet 
	. . . QUIT:$PIECE(POS1,Y,4)=""   ;KEIN ARTIKEL     ;no item 
	. . . QUIT:$PIECE(POS1,Y,7)'=1   ;KEINE BESTELLUNG ;no sales order 
	. . . ;
	. . . SET YFELD=""
	. . . SET $PIECE(YFELD,Y,49) = APO                 ;TATSÄCHLICH ERFASSTE POSITION ;really 
	. . . ; FIXME : <GRF> Incoming Goods Flag D10 is based on INWEMERKER
	. . . ;               - a user-defined set of status codes that cannot
	. . . ;               be guaranteed to have a particular set of values.
	. . . SET $PIECE(YFELD,Y,10) = 0                   ;WARENEINGANG OK ;receiving 
	. . . SET $PIECE(YFELD,Y,1)  = $PIECE(SATZ,Y,1)    ;WARENEINGANGSNUMMER
	. . . SET $PIECE(YFELD,Y,4)  = $PIECE(POS1,Y,5)    ;MENGE   ;quantity 
	. . . SET $PIECE(YFELD,Y,18) = $PIECE(POS1,Y,40)   ;UNIT OF MEASURE
	. . . SET $PIECE(YFELD,Y,7)  = $PIECE(AUF1,Y,12)   ;LIEFERANT LF-BESTELLUNG ;supplier 
	. . . IF $PIECE(YFELD,Y,7)="" SET $PIECE(YFELD,Y,7) = $PIECE(POS1,Y,12)  ;LIEFERANT KUNDENAUFTRAG ;supplier customer´s order 
	. . . IF $PIECE(YFELD,Y,7)="" SET $PIECE(YFELD,Y,7) = $PIECE(SATZ,Y,7)   ;LIEFERANT AUS VORGABEN  ;supplier out of 
	. . . IF $PIECE(YFELD,Y,7)=$PIECE(SATZ,Y,7) FOR YI=11,12,13,14,15 SET $PIECE(YFELD,Y,YI)=$PIECE(SATZ,Y,YI)  ;ALTE WE INFO AUFTRAG ;In order 
	. . . IF $PIECE(POS1,Y,47)'="" DO
	. . . . NEW MENGE
	. . . . SET MENGE=$PIECE(POS1,Y,5)
	. . . . IF +MENGE=0 SET MENGE=1
	. . . . SET $PIECE(YFELD,Y,8) = $PIECE(POS1,Y,47)/MENGE*$$^INQTYUNIT(,AUF,POS)  ;EK ;Planned Cost
	. . . ;  
	. . . IF $PIECE(POS1,Y,47)="" IF $PIECE(POS1,Y,4)'="" SET $PIECE(YFELD,Y,8)=$$^INARTKOST($PIECE(POS1,Y,4))  ;EK ;Planned Cost
	. . . SET $PIECE(YFELD,Y,8)=$JUSTIFY($PIECE(YFELD,Y,8),0,2)
	. . . ;
	. . . SET LAGERX=LAGER
	. . . IF $DATA(^INARTLP(YM,$PIECE(POS1,Y,4),BETR)) DO
	. . . . SET LAGERX=$ORDER(^INARTLP(YM,$PIECE(POS1,Y,4),BETR,""))
	. . . . IF '$DATA(^INLP(YM,BETR,LAGERX,1)) SET LAGERX=LAGER
	. . . ;
	. . . DO  ;WE SPEICHERN ;In Save 
	. . . . NEW YKEY
	. . . . SET YKEY=AUF_","_POS_","_BETR_","_LAGERX_","_DAT
	. . . . SET YOK=$$^WWWSPEI("INWEAUFVOR",YKEY,YFELD,1)
	
	QUIT
]]></Routine>
</Export>