<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTIKELME" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTIKELME(ARTIKEL,LIEFER)
#include COMSYS
#include INConst
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGE ME AUS ARTIKELSTAMM
	;
	; Inputs : 
	;	LIEFER		: Supplier
	;	ARTIKEL		: Item No
	;	YINHALT		: Input value (?)
	;	
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 30-Aug-2006	GRF		SR12027: Remove duplicated level of testing (KOND'="")
	; 12-Jul-2005	GRF		SR12027: Macro Conversion
	; 15.05.2002	DT
	;-------------------------------------------------------------------------------
	NEW ConvFactor,DEZ,KOND,ME,MNG
	
	; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; Data Records :
	;	KOND			objINARTK			Supplier Identification of Item
	; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
	SET ME=""					;MENGENEINHEIT
	IF $GET(ARTIKEL)'="" {
		SET ME(1)=$$$INARTUnitofMeasure($get(^INART(YM,ARTIKEL,1)))
		IF ME(1)'="" SET ME=$$$WWW101Text($get(^WWW101(0,"EINHEIT",SPRACHE,ME(1),1)))
	}
	
	;BESTELLEINHEIT UND MENGE ; FIS,16.05.02
	IF ($GET(LIEFER)'="") && ($GET(ARTIKEL)'="") {
		SET KOND=$GET(^INARTK(YM,ARTIKEL,LIEFER,1))
		; 30-Aug-2006
		IF $$$INARTKDeliveryQuantityUnit(KOND)'="" {      ;LIEFEREINHEIT
			SET ME(2)=$$$WWW101Text($get(^WWW101(0,"EINHEIT",SPRACHE,$$$INARTKDeliveryQuantityUnit(KOND))))
			
			SET MNG=0
			set ConvFactor = +$$$INARTKConversionFactor(KOND)
			IF (+$GET(YINHALT)'=0) && (ConvFactor'=0) {
				SET DEZ=$$$WWW012DecimalSigns($get(^WWW012(0,YM,1)))
				IF DEZ'="" SET DEZ=","                 ; FIXME : This uses European format as the default (also company based rather than locale) <GRF>
				IF DEZ="," SET YINHALT=$TRANSLATE($TRANSLATE(YINHALT,"."),",",".")
				IF DEZ="." SET YINHALT=$TRANSLATE(YINHALT,",")
				SET MNG=$$^WWWZAHL((+YINHALT*ConvFactor),0,$LENGTH($PIECE((+YINHALT*ConvFactor),".",2)))
			}
			IF ME(2)'="" {
				SET ME=ME_" ("
				IF MNG'=0 SET ME=ME_MNG_" "
				SET ME=ME_ME(2)
				SET ME=ME_")"
			}
		}
	}
	QUIT ME
	
]]></Routine>
</Export>