<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM7InheritableCheckBox" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORM7InheritableCheckBox
	
#include COMSYS
	
Display()
	;-------------------------------------------------------------------------------
	; This routine will display a tri-state checkbox that can inherit from a parent
	; location.  A reworking of the code in WWWFORM7 for normal checkboxes.
	; NOTE: Only supports location fields.
	;
	; TODO:
	;	If the call to TemporaryStorageLocations is changed to metadata then we
	;		could use this process for other types of data fields rather than just
	; 		Locations.
	; 
	; Called By: ANTWORT^WWWFORM7
	; 
	; ByRef :	YFORM, YART, YLFN, YSTATUS, YHID, YINHALT, YTABX, YJAVA
	; 			YSATZ		objWWW122
	;
	; Returns:
	;
	; History:
	; 09-Mar-2011	shobby	SR17594.1: changed window.event to event to stop an
	;							error with firefox.
	; 11-Nov-2011	shobby	SR17612: change event from onmousedown to onmouseup
	; 08-Nov-2010	shobby	SR17594: Changes to work under Firefox
	; 27-Oct-2010	GRF		SR17564.1: enumValue rather than intValue; boolean macros
	; 12-Oct-2010	shobby	SRBR014837: Made intValue more generic from metadata.
	; 07-Oct-2010	shobby	SR17564: Created
	;-------------------------------------------------------------------------------
	new enumValue,id,strRoutine

	set id         = "Y"_YFORM_YART_YLFN
	set enumValue  = $$$NO
	set strRoutine = $$$WWW122InheritedFrom($$Get^WWW122(YFORM,YBBN))  ;BR014837
	if (strRoutine'="") {
		xecute "set enumValue="_strRoutine
	}
	
	; SR17594.1
	&html<
		<SPAN onmouseup='var xResult=InheritableCheckBoxClick(this,"#(enumValue)#");
					  document.getElementById("#(id)#").value=xResult; 
		              xResult = EventValue("#(YUCI)#","#(YUSER)#","#(YFORM)#","FIX#($translate(YKEY,".","~"))#","#("Y"_YFORM_YART_YLFN)#",xResult,"2","","#(YSEITE)#");
		              event.cancelBubble=true;
		             ' 
		   style='height:20px; float:right; '
		>
	>
	
	if (YHID=2) && (YSTATUS'="") write YCR,"<A" do STAT^WWWFORM7 write ">"
	write YCR,"<INPUT TYPE=""CHECKBOX"""
	write YCR," NAME="""_id_""""
	write YCR," ID="""_id_""""              ;SR17253
	write YCR," VALUE="""_YINHALT_""""      ;SR17612
	write YCR," TABINDEX="
	
	; Tab Index  D61
	;---------------------------------------
	if +$$$WWW122TabIndex(YSATZ)=0 {
	 	if $get(YHID)=2 {
		 	write YCR,"-1"           ;TAB STOP IN READ ONLY FIELD
	 	} else {
		 	write YCR,""""_YTABX_""""
	 	}
 	} else {
	 	write YCR,""""_+$$$WWW122TabIndex(YSATZ)_""""
 	}
 	
 	; Execute on Form Construction (within) D99                       ; *** EXECUTE ***
	if $$$WWW122ExecuteWithinDataField(YSATZ)'="" xecute $$$WWW122ExecuteWithinDataField(YSATZ)
	if $$EXIST^%R("Y"_YFORM_YART_YLFN_"onField.OBJ",$get(YUCI)) write YCR xecute "do ^Y"_YFORM_YART_YLFN_"onField"  ;CUSTOMIZED EXECUTE
	
	; Explicitly or inherited "YES" - mark as checked
	;---------------------------------------
	if (YINHALT=$$$YES) || (YINHALT=$$$INHERIT && enumValue=1) write YCR," CHECKED=""CHECKED"""

	write YCR," style="""
	write "padding-left:1; "_$$$WWW122StyleDefinition(YSATZ)   ; D78
	if YHID=2 do LESEN1^WWWFORM7
	write """"
	
	;DO SAVE
	if (YHID=2) || (YINHALT=$$$INHERIT) if YHTMFORM="WWW2" write " disabled"
	if YHID=2 do LESEN^WWWFORM7
	if (YHID'=2) && (YSTATUS'="") do STAT^WWWFORM7
	
	if $$$WWW122READOnlyFieldsWithFieldEn(YSATZ) do READONLY^WWWFORM7
	if $$$WWW122WriteFieldsWithFieldEntry(YSATZ) do WRITE^WWWFORM7
	if YJAVA=1 do JAVA^WWWFORM8
	write ">"
	if (YHID=2) && (YSTATUS'="") write YCR,"</A>"
	;SR17612 set YINHALT=+YINHALT
	
	
	; Firefox will lose the click event if clicking on a disabled checkbox.  Here, an invisible
	; DIV will accept the click allowing it to bubble up to the SPAN.   ; SR17594
	if YUSERAGENT'="MSIE" write YCR,"<DIV style='position:relative; top:-20px; width:20px; height:20px;'></DIV>"
	
	write YCR,"</SPAN>"
	quit
	
	
Javascript()
	;-------------------------------------------------------------------------------
	; This routine will display a tri-state checkbox that can inherit from a parent
	; location.  A reworking of the code in WWWFORM7 for normal checkboxes.
	; NOTE: Only supports location fields.
	;
	; Called By: ^WWWFORM8
	;
	; Returns:
	;
	; History:
	; 11-Nov-2010	shobby	SR17612: 'Inherit' value is now ""
	; 08-Nov-2010	shobby	SR17594: Changes to work under Firefox
	; 07-Oct-2010	shobby	SR17564: Created
	;-------------------------------------------------------------------------------
	&js<
		function InheritableCheckBoxClick(obj1,inheritedValue) {
			var obj=obj1.children[0];
			
			switch (obj.value) {
				case '0': { obj.value=1;  obj.checked=true;                obj.disabled=false; break;}
				case '1': { obj.value=''; obj.checked=(inheritedValue==1); obj.disabled=true;  break;}
				default:  { obj.value=0;  obj.checked=false;               obj.disabled=false; break;}
			} 
			// to reverse the click on the checkbox.
			if (isIE()&&(obj.value!='')) obj.checked=!obj.checked;
			return obj.value;
		}
	>
	quit
	
]]></Routine>
</Export>