<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMLocking" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMLocking
#include COMSYS
#include WWWConst
 
CanLock(pidForm)
	;-------------------------------------------------------------------------------
	; Determine whether a form should allow locking onto its appropriate
	; data structure.
	;
	; Called By: ^WWWLESE, GetNextLine^COMGridEdit31Add
	; 
	; Params: pidForm		: The form id
	;
	; ByRefs:
	;
	; Returns: blnFlag
	;
	; History:
	; 05-May-2006	SteveS	SR14508: Created
	;-------------------------------------------------------------------------------
	new blnFlag,objWWW120,idClass,objWWW001
	
	set blnFlag = $$$NO
	
	if $get(pidForm)'="" {
		set objWWW120 = $get(^WWW120(0,pidForm,1))
		set idClass   = $$$WWW120ClassUsedInForm(objWWW120)
		
		if (idClass'="") {
			set objWWW001 = $get(^WWW001(0,idClass,1))
			set blnFlag   = '$$$WWW001NoLocking(objWWW001)	
		}
	}
	quit blnFlag
	
	
CreateLock(pstrKey,pidUser,pdtsLocked,pidForm)
	;-------------------------------------------------------------------------------
	; Construct Lock references as a matched pair
	; 
	;  do CreateLock^WWWFORMLocking(%SCHLUESSEL,YUSER,$horolog,YFORM)
	;  Calling routine to ensure all parameters are defined.
	; 
	; Called By: ^WWWLESE
	; 
	; Params:
	;	pstrKey			Formalised Key string e.g. pass in "^WWW003(0,INART,24,1)
	;						which is converted to "^WWW003/0.INART.24.1/" for saving,
	;						thus locking class field 24 for class "INART"
	;	pidUser			YUSER
	;	pdtsLocked		$horolog
	;	pidForm			Form Name
	;
	; History:
	; 30-May-2014	shobby	CORE-385.5: Remove dependency on MED Module
	; 20-Feb-2009	GRF		SR16356: Created as common function
	;-------------------------------------------------------------------------------
	new dteLocked,tmeLocked
	
	; FIXME : If a server is accessed across midnight a lock created just before
	;         midnight will not be found when the date changes.  <GRF>
	set dteLocked = +pdtsLocked
	set tmeLocked = $piece(pdtsLocked,$$$COMMA,2)
	set pstrKey   = $translate(pstrKey,",()"_$$$DBLQUOTE,".//")
	
	set ^WWW006(0,dteLocked,pstrKey,1) = pidUser_Y_tmeLocked
	if pidForm'="" {
		set ^WWWDATEN(YM,dteLocked,pidUser,pidForm,"LOCK",1) = "^WWW006(0,"_dteLocked_","_$$$DBLQUOTE_pstrKey_$$$DBLQUOTE_",1)"
	}
	if ##class(%Library.Routine).Exists("MEDPrescription") {	;CORE-385.5
		do CreateLock^MEDPrescription(dteLocked,pstrKey)		;CORE-385.5
	}															;CORE-385.5
	quit

KillLock(dteToday,pstrKey)
	;-------------------------------------------------------------------------------
	; Common method to remove locks.  Use to track down when locks are being created
	; and destroyed.
	; 
	; Called By: ^WWWSPEI
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 04-Aug-2009	shobby	SR16808: Created
	;-------------------------------------------------------------------------------
	kill ^WWW006(0,dteToday,pstrKey)
	quit
	
]]></Routine>
</Export>