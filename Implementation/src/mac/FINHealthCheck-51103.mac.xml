<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINHealthCheck" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINHealthCheck
	
#include COMSYS
#include FINConst
	
Start()
	;-------------------------------------------------------------------------------
	;
	; History
	; 25-Feb-2003	shobby	WWW001 not shared.
	; ??-Aug-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	quit  ;<TODO>
	new strClass
	;
	set strClass=""
	do StartTree^COMTree($get(YPARA))
	do AddNode^COMTree("",1,"","Classes","","","","","","","")
 
	for {
		set strClass=$order(^WWW001(0,strClass))
		quit:strClass=""
		if $extract(strClass,1,3)="FIN" {
			if $$AddProperties(strClass) {
				do AddNode^COMTree(1,strClass,"",strClass,"","","","","","","")
			}
		}
	}
	do StopTree^COMTree(1)
	quit
 
 
AddProperties(pstrClass)
	;-------------------------------------------------------------------------------
	;
	; History
    ; 03-May-2004   shobby  Replaced YKOMMA with ","
	; ??-Aug-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new strKey,idKey,objKey,strType,strRelation,strSortedKey,blnShow
	
	set blnShow=$$$NO
	for strType="^WWW002","^WWW003" {
		set idKey=""
		for {
			xecute "set idKey=$order("_strType_"(YM,pstrClass,idKey))"
			quit:idKey=""
			
			set objKey=""
			xecute "set objKey=$get("_strType_"(YM,pstrClass,idKey,1))"
			set strKey=$piece(objKey,Y,1)
			;AddNode(pidParent,pidNode,pstrTitle,pstrText,pstrImage,pstrForm="",pstrParam="",pblnHighLight="",pstrExtraData="",pblnLinkStopped="",plngPage="")
			set strRelation=$piece(objKey,Y,8)
			if strRelation'="WWW100" {
				set strSortedKey=pstrClass_":"_strType_":"_$justify(idKey,10)
				if strRelation="WWW101" {
					;w "idKey="_idKey_","_$piece(objKey,Y,9)
					;w $get(^WWW101(YM,$piece($piece(objKey,Y,9),",",1),""""_SPRACHE_"""",idKey,1))
					;w "/"_YM_","_$piece($piece(objKey,Y,9),",",1)_","_""""_SPRACHE_""""_","_idKey_"\"
					;w $piece($piece($piece(objKey,Y,9),"""",2),"""",1)
				} else {
					if strRelation'="" {
						if strType="^WWW002" {
							if $$HasValidLinksToKey(pstrClass,strRelation,idKey,strSortedKey) {
								set $$$Storage("Expand",pstrClass)=""
								set blnShow=$$$YES
								do AddNode^COMTree(pstrClass,strSortedKey,"",strKey,"","","",strRelation'="","","","")
							}
						} else {
							if $$HasValidLinksToData(pstrClass,strRelation,idKey,strSortedKey) {
								set $$$Storage("Expand",pstrClass)=""
								set blnShow=$$$YES
								do AddNode^COMTree(pstrClass,strSortedKey,"",strKey,"","","",strRelation'="","","","")
							}
						}
					}
				}
			}
		}
	}
	quit blnShow
	
	
HasValidLinksToData(pstrClass,pstrRelation,pintKey,pstrParent)
	;-------------------------------------------------------------------------------
	;
	; History
	; 11-Dec-2006	shobby	SRBR014286:Corrected GIF names.
    ; 03-May-2004   shobby  Replaced YKOMMA with ","
	; ??-Aug-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new blnResult,node,strId,node1,blnExpand
	
	set blnExpand=$$$NO
	set node=""
	xecute "set node=$query(^"_pstrClass_")"
	;kill ^mtemp(YUSER,"Links")
	if node'="" {
		for {
			set node=$query(@node)
			quit:node=""
			set strId=$piece(@node,Y,pintKey)
			if strId'="" {
				set ^mtemp(YUSER,"Links",strId)=""
			}
		}
		set node=""
		for {
			set node=$order(^mtemp(YUSER,"Links",node))
			quit:node=""
			set node1=node
			;if node'=+node set node1=""""_node_""""
			if ($extract(node1,1,1)'="""") set node1=""""_node1_""""
			xecute "set blnResult=$data(^"_pstrRelation_"("_YM_","_node1_"))"
			set blnResult=1
			if 'blnResult {
				do AddNode^COMTree(pstrParent,pstrParent_":"_node,"","Data="_node_"-"_pstrRelation_"("_pintKey_")","html.gif","","","","","","")
				set $$$Storage("Expand",pstrParent_":"_node)=""
				set $$$Storage("Expand",pstrParent)=""
				set blnExpand=$$$YES
			}
		}
		;k ^mtemp(YUSER,"Links")	
	}
	quit blnExpand
 
HasValidLinksToKey(pstrClass,pstrRelation,pintKey,pstrParent)
	;-------------------------------------------------------------------------------
	;
	; History
	; 11-Dec-2006	shobby	SRBR014286:Corrected GIF names.
    ; 03-May-2004   shobby	Replaced YKOMMA with ","
	; ??-Aug-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new blnResult,node,strId,node1,blnExpand,i
	
	set blnExpand=$$$NO
	set node=""
	xecute "set node=$query(^"_pstrClass_")"
	;kill ^mtemp(YUSER,"Links")
	if node'="" {
		for {
			set node=$query(@node)
			quit:node=""
			set strId=""
			for i=1:1:pintKey {
				set strId=strId_$$GetIDPart^COMUtils(node,pintKey+1)_","
			}
			if strId'="" set strId=$extract(strId,1,$length(strId)-1)
			;w "strId="_strId
			set ^mtemp(YUSER,"Links",strId)=""
		}
		set node=""
		for {
			set node=$order(^mtemp(YUSER,"Links",node))
			quit:node=""
			set node1=node
			;if node'=+node set node1=""""_node_""""
			if ($extract(node1,1,1)'="""") set node1=""""_node1_""""
			set blnResult=1
			xecute "set blnResult=$data(^"_pstrRelation_"("_YM_","_node1_"))"
			if 'blnResult {
				do AddNode^COMTree(pstrParent,pstrParent_":"_node,"","Key="_node_"("_node1_")","html.gif","","","","","","")
				set $$$Storage("Expand",pstrParent_":"_node)=""
				set $$$Storage("Expand",pstrParent)=""
				set blnExpand=$$$YES
			}
		}
		;k ^mtemp(YUSER,"Links")	
	}
	quit blnExpand
	
 
FindInvoiceErrors(pblnFix=0)
	;-------------------------------------------------------------------------------
	; Finds Invoice/InvoiceTran errors
	; Run from Command Line
	; 
	; **************
	;
	; Returns:
	;
	; History:
	; 13-Nov-2003	shobby	Creation (TR006374)
	;-------------------------------------------------------------------------------
	new objFINARInvoice,objFINARInvoiceTran,arrData,fltAmount
	new idxCustomerNumber,idxInvoiceNumber,idxLine
	
	; Check FINARInvoice
	do ^WWWVAR
	set idxCustomerNumber=""
	for {
		set idxCustomerNumber=$order(^FINARInvoice(YM,idxCustomerNumber))
		quit:idxCustomerNumber=""
		set idxInvoiceNumber=""
		for {
			set idxInvoiceNumber=$order(^FINARInvoice(YM,idxCustomerNumber,idxInvoiceNumber))
			quit:idxInvoiceNumber=""
			set objFINARInvoice=$get(^FINARInvoice(YM,idxCustomerNumber,idxInvoiceNumber,1))
			set arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoice")=$$$FINARInvoiceBalance(objFINARInvoice)
		}
	}
 
	; Check FINARInvoiceTran
	set idxCustomerNumber=""
	for {
		set idxCustomerNumber=$order(^FINARInvoiceTran(YM,idxCustomerNumber))
		quit:idxCustomerNumber=""
		set idxInvoiceNumber=""
		for {
			set idxInvoiceNumber=$order(^FINARInvoiceTran(YM,idxCustomerNumber,idxInvoiceNumber))
			quit:idxInvoiceNumber=""
			set idxLine=""
			set fltAmount=0
			for {
				set idxLine=$order(^FINARInvoiceTran(YM,idxCustomerNumber,idxInvoiceNumber,idxLine))
				quit:idxLine=""
				set objFINARInvoiceTran=$get(^FINARInvoiceTran(YM,idxCustomerNumber,idxInvoiceNumber,idxLine,1))
				set fltAmount=fltAmount+$$$FINARInvoiceTranTransactionAmount(objFINARInvoiceTran)
			}
			set arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoiceTran")=fltAmount			
		}
	}
	
	
	; Check for variances
	set idxCustomerNumber=""
	for {
		set idxCustomerNumber=$order(arrData(idxCustomerNumber))
		quit:idxCustomerNumber=""
		set idxInvoiceNumber=""
		for {
			set idxInvoiceNumber=$order(arrData(idxCustomerNumber,idxInvoiceNumber))
			quit:idxInvoiceNumber=""
			if +$get(arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoice"))'=+$get(arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoiceTran")) {
				write idxCustomerNumber_","_idxInvoiceNumber_"="_$get(arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoice"))_","_$get(arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoiceTran"))
				if pblnFix {
					set objFINARInvoice=$get(^FINARInvoice(YM,idxCustomerNumber,idxInvoiceNumber,1))
					set $$$FINARInvoiceBalance(objFINARInvoice)=$get(arrData(idxCustomerNumber,idxInvoiceNumber,"FINARInvoiceTran"))
					do Save^FINSYS("FINARInvoice",idxCustomerNumber_","_idxInvoiceNumber,objFINARInvoice,1)
					write "   Fixed!"
				}
				write !
			}
			
		}
	}
	quit
	
	
ShowInvalidInvoices(pblnFix=0)
	;-------------------------------------------------------------------------------
	; Finds FINARInvoice that have a balance but no other field
	;
	; Returns:
	;
	; History:
	; 20-Jun-2005	shobby	Use KILL^COMUtils (SR12632)
    ; 03-May-2004   shobby  Replaced YKOMMA with ","
	; 25-Feb-2004	shobby	Variable declaration.
	; 14-Nov-2003	shobby	Creation (TR006374)
	;-------------------------------------------------------------------------------
	new idxCustomerNumber,idxInvoiceNumber,idxLine,objFINARInvoice
	
	; Check FINARInvoice
	do ^WWWVAR
	set idxCustomerNumber=""
	for {
		set idxCustomerNumber=$order(^FINARInvoice(YM,idxCustomerNumber))
		quit:idxCustomerNumber=""
		set idxInvoiceNumber=""
		for {
			set idxInvoiceNumber=$order(^FINARInvoice(YM,idxCustomerNumber,idxInvoiceNumber))
			quit:idxInvoiceNumber=""
			set objFINARInvoice=$get(^FINARInvoice(YM,idxCustomerNumber,idxInvoiceNumber,1))
			if objFINARInvoice'[Y {
				write "^FINARInvoice("_YM_","_idxCustomerNumber_","_idxInvoiceNumber_","_"1) Error!"
				if pblnFix {
					do KILL^COMUtils("FINARInvoice",idxCustomerNumber_","_idxInvoiceNumber_",")
					write "  Fixed."
				}
				write !
			}
		}
	}
	
	quit
	
]]></Routine>
</Export>