<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPHCChequeBook" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPHCChequeBook

	#Include FINHC


Check(StartDate="",EndDate="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; Verifies that all cheques that are used in a payment are contained within
    ; a cheque book.
    ;
    ; Returns:
    ;
    ; History:
    ; 15-Jun-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	;
	new strStatus,idPayment,idLine,objFINAPPaymentLine,strCheque
	new strBankAccount,objFINAPPayment
	
	set strStatus=$$$OK
	if $get(Y)="" do ^WWWVAR
	kill Result
		
	set idPayment=""
	for {
		set idPayment=$order(^FINAPPaymentLine(YM,idPayment))
		quit:idPayment=""
		set idLine=""
		for {
			set idLine=$order(^FINAPPaymentLine(YM,idPayment,idLine))
			quit:idLine=""
			set objFINAPPayment=$get(^FINAPPayment(YM,idPayment,1))
			set objFINAPPaymentLine=$get(^FINAPPaymentLine(YM,idPayment,idLine,1))
			set strCheque=$$$FINAPPaymentLineChequeNumber(objFINAPPaymentLine)
			set strBankAccount=""
			if strCheque'="" {
				set strBankAccount=$$$FINAPPaymentBankAccount(objFINAPPayment)
				if $$$FINAPPaymentStatus(objFINAPPayment)=$$$EnumFINSYSSTATUSPosted {
					if $$$ISERR($$VerifyCheque(strCheque,strBankAccount)) {
						set Result("Hash",$$$ErrorNo(Result("Errors")))=$$$Text($listbuild("Fin00351",strCheque,strBankAccount))  ;Cheque number '%1' was not found in any chequebook for account '%2'.
						set strStatus='$$$OK
					}
				}
			}
		}
	}
	if $$$ISOK(strStatus) {
		set strStatus=2
	}
	quit strStatus


VerifyCheque(pstrCheque,pstrBankAccount)
    ;-------------------------------------------------------------------------------
    ; Verify that the cheque is contained within a chequebook for the specified account.
    ;
    ; Returns:
    ;
    ; History:
    ; 15-Jun-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	;
	new strStatus,idAccount,objFINAPChequeBook,intFirstCheque,intLastCheque
	
	set strStatus='$$$OK
	
	set intFirstCheque=""
	for {
		quit:$$$ISOK(strStatus)
		set intFirstCheque=$order(^FINAPChequeBook(YM,pstrBankAccount,intFirstCheque))
		quit:intFirstCheque=""
		set objFINAPChequeBook=$get(^FINAPChequeBook(YM,pstrBankAccount,intFirstCheque,1))
		set intLastCheque=$$$FINAPChequeBookEndChequeNumber(objFINAPChequeBook)
		if (pstrCheque'<intFirstCheque)&&(pstrCheque'>intLastCheque) {
			set strStatus=$$$OK
		}
	}
	quit strStatus

]]></Routine>
</Export>