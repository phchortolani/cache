<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMEntity" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMEntity
#include COMConst
#include COMSYS
#include WWWConst

OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Clear any entries of this entity in the Location Master Data WWW0121
	; 
	; Called By: OnBeforeDelete field form COMEntity
	;
	; Params: pYKEY - Key for entity being deleted.
	; 
	; History:
	; 03-Jan-2014	shobby	CORE-303: CacheIndex
	; 02-Sep-2009	DWR		SR16866 Created
	;-------------------------------------------------------------------------------
	new idLocn,objLocn,strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pYKEY) {
		set idLocn=""
		for {
			set idLocn = $order(^WWW0121s(0,10,$$Index^COMUtilIndex(pYKEY,"WWW0121"),0,idLocn))  ;CORE-303
			quit:idLocn=""
			
			set objLocn = $get(^WWW0121(0,0,idLocn,1))
			set $$$WWW0121Entity(objLocn)=""
			
			set strStatus=$$$Save("WWW0121",0_$$$COMMA_idLocn,objLocn,$$$YES)
			quit:$$$ISERR(strStatus)
		}
		if $$$ISERR(strStatus){
			set Q = $$$QDontDelete_" "_$$$Text(strStatus)
		}
	}
	
	quit 
	
GetEntity(pidLocn)
	;-------------------------------------------------------------------------------
	; Get the loctions Entity (namespace) if empty go up the tree of locations to
	; get it. If no entity return $$$NO (dont change namespace)
	; 
	; Called By: CheckNamespace (below)
	;
	; Params: idLocn - Location which entity is being sort after for.
	; 
	; History:
	; 02-Sep-2009	DWR		SR16866 Created
	;-------------------------------------------------------------------------------
	new idEntity,idParent,objLocn
	set idEntity = ""
	if pidLocn'="" {
		set objLocn = $get(^WWW0121(0,0,pidLocn,1))
		if $$$WWW0121LocationType(objLocn) = 0 {  ; Entity location
			set idEntity = $$$WWW0121Entity(objLocn)
			if idEntity=""{
				set idEntity = $$$NO
			}
		} elseif $$$WWW0121Entity(objLocn)'="" {
			set idEntity = $$$WWW0121Entity(objLocn)
		}
		
		if idEntity=""{
			set idParent = $$$WWW0121ParentLocn(objLocn)
			if idParent'=""{
				set idEntity = $$GetEntity(idParent)
			}
		}
	}	
	
	quit idEntity

CheckNamespace(pidLocn,&pCurrNS)
	;-------------------------------------------------------------------------------
	; Check if the location requires a namespace change (Entity). Preserve the 
	; current, and return it to enable the changing back to it after the webservice
	; call.
	; 
	; Called By: webservice calls using location in alWEB.WS.Consumption
	;
	; Params: idLocn - Location which entity is being sort after for.
	; 
	; returns byref: original namespace
	; 
	; History:
	; 02-Sep-2009	DWR		SR16866 Created
	;-------------------------------------------------------------------------------
	new idEntity,objEntity
	
	set idEntity = ""
	set pCurrNS= ""
	
	set idEntity = $$GetEntity^COMEntity(pidLocn)
	if idEntity'=""{
		set pCurrNS = $znspace
		set objEntity = $get(^COMEntity(0,idEntity,1))
		set $znspace = $$$COMEntityNamespace(objEntity)
	}

 	quit
]]></Routine>
</Export>