<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRESERVIERT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRESERVIERT
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		RESERVIERUNGEN AUF BESTELLUNGEN OHNE KUNDENAUFTRAG
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 27-Feb-2007	GRF		SR15234: Doco; quits; boolean macros; explicit TYP in
	; 						call to CHECK in SET
	; 04.10.2003	BEC
	;-------------------------------------------------------------------------------
	NEW YRETVAL
	
	SET YRETVAL=0
	
CHECK(ART,QT,SUPORDER,SUPLI,SUP,TYP)
	;-------------------------------------------------------------------------------
	; S YOK=$$CHECK^INRESERVIERT(ART,QT,SUPORDER,SUPLI,SUP,TYP)
	; S YOK=$$CHECK^INRESERVIERT(ART,QT,SUPORDER,SUPLI,,TYP)
	; S YOK=$$CHECK^INRESERVIERT(ART,QT,,,,TYP)
	;
	; Inputs :
	;	ART			ITEM
	;	QT			QUANTITY
	;	SUPORDER	SUPPLIER ORDER
	;	SUPLI		SUPPLIER ORDER LINE ITEM
	;	SUP			SUPPLIER No.
	;	TYP			RETURN TYPE    / RÜCKGABE
	;	
	; Returns
	;	TYP = 0: 1/0  (YES/NO)
	;   TYP = 1: ORDER,ITEMLINE,QUANTITY~...
	;         
	;GIBT NUR 1 ZURÜCK WENN WARE SCHON BESTELLT IST ;only back when wares yet 
	;ACHTUNG EINSPRUNG VON WEBSERVICE ;estimation 
	;-------------------------------------------------------------------------------
	NEW BET,LAP,POK,RESTMENGE,WED,XAUF,XAUF1,XPOS,XPOS1,XXAUF,XXPOS,YRETVAL
	
	SET YRETVAL  = $$$NO
	SET ART      = $GET(ART)           IF ART="" QUIT YRETVAL
	SET QT       = $GET(QT)            IF +QT=0  QUIT YRETVAL
	SET SUPORDER = $GET(SUPORDER)
	SET SUPLI    = $GET(SUPLI)
	SET SUP      = $GET(SUP)
	SET TYP      = $GET(TYP)           IF +TYP=0 SET TYP=0
	
	KILL ^WWWSOR(YUSER)
	DO
	. FOR POK=" ",0 DO
	. . SET XAUF=SUPORDER
	. . DO:XAUF'=""  IF XAUF="" IF ART'="" FOR  SET XAUF=$ORDER(^INAUFPs(YM,5,POK,$$^WWWUMLAU(ART,1),XAUF)) QUIT:XAUF=""  DO   ;OFFENER AUFTRAG ;order 
	. . . SET XAUF1=$GET(^INAUF(YM,XAUF,1))
	. . . QUIT:'$DATA(^INAUF1(YM,XAUF))                  ;NICHT OFFEN      ;Not vulnerable 
	. . . IF +$PIECE(XAUF1,Y,2)'=2               QUIT    ;KEIN BESTELLUNG  ;no sales order 
	. . . IF +$PIECE(XAUF1,Y,38)=1               QUIT    ;GESPERRT         ;DISABLED 
	. . . IF SUP'="" IF +$PIECE(XAUF1,Y,12)'=SUP QUIT    ;NOT SELECTED SUPPLIER 
	. . . ;
	. . . SET XPOS=SUPLI
	. . . DO:XPOS'=""  IF XPOS="" IF ART'="" FOR  SET XPOS=$ORDER(^INAUFPs(YM,5,POK,$$^WWWUMLAU(ART,1),XAUF,XPOS)) QUIT:XPOS=""  DO
	. . . . ;QUIT:$DATA(^INWEAUF(YM,XAUF,XPOS))  ;SCHON WE VORHANDEN
	. . . . SET XPOS1=$GET(^INAUFP(YM,XAUF,XPOS,1))
	. . . . IF ART'="" IF ART'=$PIECE(XPOS1,Y,4) QUIT
	. . . . QUIT:$PIECE(XPOS1,Y,9)=1        ;STORNIERT
	. . . . QUIT:$PIECE(XPOS1,Y,205)=1      ;GESPERRT ;DISABLED 
	. . . . QUIT:$PIECE(XPOS1,Y,83)=""      ;NOT ORDERED YET ;privation 
	. . . . SET RESTMENGE=$PIECE(XPOS1,Y,5)
	. . . . IF $DATA(^INWEAUF(YM,XAUF,XPOS)) DO  ;SCHON WE VORHANDEN ;yet In on hand 
	. . . . . NEW BET,LAP,WED
	. . . . . SET BET=""
	. . . . . FOR  SET BET=$ORDER(^INWEAUF(YM,XAUF,XPOS,BET)) QUIT:BET=""  DO
	. . . . . . SET LAP=""
	. . . . . . FOR  SET LAP=$ORDER(^INWEAUF(YM,XAUF,XPOS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . . . SET WED=""
	. . . . . . . FOR  SET WED=$ORDER(^INWEAUF(YM,XAUF,XPOS,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . . . . . SET RESTMENGE=RESTMENGE-$PIECE($GET(^INWEAUF(YM,XAUF,XPOS,BET,LAP,WED,1)),Y,4)
	. . . . ;
	. . . . SET XXAUF=""
	. . . . FOR  SET XXAUF=$ORDER(^INRESERVIERT(YM,XAUF,XPOS,XXAUF)) QUIT:XXAUF=""  DO  ;SUCHEN RESTMENGE ;seek 
	. . . . . SET XXPOS=""
	. . . . . FOR  SET XXPOS=$ORDER(^INRESERVIERT(YM,XAUF,XPOS,XXAUF,XXPOS)) QUIT:XXPOS=""  DO
	. . . . . . SET RESTMENGE=RESTMENGE-$PIECE($GET(^INRESERVIERT(YM,XAUF,XPOS,XXAUF,XXPOS,1)),Y,1)
	. . . . ;
	. . . . IF RESTMENGE'>0 QUIT
	. . . . IF XAUF'="" IF XPOS'="" SET ^WWWSOR(YUSER,"ORDERS",XAUF,XPOS)=$GET(RESTMENGE)
	
	IF +TYP'=1 DO   ;RÜCKGABE JA/NEIN
	. NEW QTY
	. SET QTY=0
	. DO
	. . SET XAUF=SUPORDER
	. . DO:XAUF'=""  IF XAUF="" FOR  SET XAUF=$ORDER(^WWWSOR(YUSER,"ORDERS",XAUF)) QUIT:XAUF=""  DO
	. . . SET XPOS=SUPLI 
	. . . DO:XPOS'=""  IF XPOS="" FOR  SET XPOS=$ORDER(^WWWSOR(YUSER,"ORDERS",XAUF,XPOS)) QUIT:XPOS=""  DO
	. . . . SET QTY=QTY+$PIECE($GET(^WWWSOR(YUSER,"ORDERS",XAUF,XPOS)),Y,1)
	. ;
	. IF QTY'<QT SET YRETVAL=$$$OK
	
	IF +TYP=1 DO       ;RÜCKGABE AUF,POS,MENGE~...
	. NEW QTY
	. SET YRETVAL=""
	. SET XAUF=""
	. FOR  SET XAUF=$ORDER(^WWWSOR(YUSER,"ORDERS",XAUF)) QUIT:XAUF=""  DO
	. . SET XPOS=""
	. . FOR  SET XPOS=$ORDER(^WWWSOR(YUSER,"ORDERS",XAUF,XPOS)) QUIT:XPOS=""  DO
	. . . SET QTY=$PIECE($GET(^WWWSOR(YUSER,"ORDERS",XAUF,XPOS)),Y,1)
	. . . QUIT:$LENGTH(YRETVAL)>30000
	. . . SET YRETVAL=YRETVAL_XAUF_","_XPOS_","_+QTY_"~"
	
	IF YRETVAL="" SET YRETVAL=$$$NO
	;KILL WWWSOR(YUSER)
	KILL ^WWWSOR(YUSER) ;SR13246
	QUIT YRETVAL
	
SET(SUPORDER,SUPLI,SUP,ART,QT,RENUMB)
	;-------------------------------------------------------------------------------
	;	SPEICHERN RESERVIERUNG ;Save 
	;S YOK=$$SET^INRESERVIERT(SUPORDER,SUPLI,SUP,ART,QT)
	;S YOK=$$SET^INRESERVIERT(SUPORDER,SUPLI,,ART,QT)
	;-------------------------------------------------------------------------------
	NEW YOK,YKEY,YFELD,YRETVAL
	
	SET SUPORDER = $GET(SUPORDER)        ;SUPPLIER ORDER NUMMBER
	SET SUPLI    = $GET(SUPLI)           ;SUPPLIER LINE ITEM
	SET SUP      = $GET(SUP)             ;SUPPLIER NO.
	SET ART      = $GET(ART)             ;ITEM NO.
	SET QT       = $GET(QT)              ;QUANTITY
	SET RENUMB   = $GET(RENUMB)          ;RESERVATION NO. FROM OTHER SYSTEM
	SET YRETVAL  = $$$NO
	
	IF SUPORDER'="" IF SUPLI'="" IF ART'="" IF QT'<0 DO
	. QUIT:$$CHECK(ART,QT,SUPORDER,SUPLI,SUP,0)'=$$$OK
	. ;
	. IF RENUMB="" SET RENUMB=$$^WWWNEXT("INRESERVIERT")
	. SET YKEY=SUPORDER_","_SUPLI_","_" "_","_RENUMB
	. SET YFELD=+QT_Y_Y_ART_Y_RENUMB
	. SET YOK=$$^WWWSPEI("INRESERVIERT",YKEY,YFELD,1)      ;SPEICHERN RESERVIERUNG ;Save 
	. SET YRETVAL=RENUMB                                   ;RESERVATION NUMBER
	
	QUIT YRETVAL 
	
KILL(SUPORDER,SUPLI,RENUMB)
	;-------------------------------------------------------------------------------
	;	KILL RESERVATION
	;	
	; !! ACHTUNG EINSPRUNG VON WEBSERVICE !! ;estimation 
	;S YOK=$$KILL^INRESERVIERT(SUPORDER,SUPLI,RENUMB)
	;S YOK=$$KILL^INRESERVIERT(,,RENUMB)
	;-------------------------------------------------------------------------------
	NEW YRETVAL,YOK
	
	SET SUPORDER = $GET(SUPORDER)        ;SUPPLIER ORDER NUMMBER
	SET SUPLI    = $GET(SUPLI)           ;SUPPLIER LINE ITEM
	SET RENUMB   = $GET(RENUMB)          ;RESERVATION NUMBER
	SET YRETVAL  = $$$NO
	
	IF RENUMB'="" DO
	. DO:SUPORDER'=""  IF SUPORDER="" FOR  SET SUPORDER=$ORDER(^INRESERVIERT(YM,SUPORDER)) QUIT:SUPORDER=""  DO
	. . DO:SUPLI'=""  IF SUPLI="" FOR  SET SUPLI=$ORDER(^INRESERVIERT(YM,SUPORDER,SUPLI)) QUIT:SUPLI=""  DO
	. . . IF $DATA(^INRESERVIERT(YM,SUPORDER,SUPLI," ",RENUMB,1)) DO
	. . . . DO ^WWWSKILL("INRESERVIERT",SUPORDER_","_SUPLI_","_" "_","_RENUMB)     ;KILL RESERVATION
	. . . . SET YRETVAL = $$$OK
	
	QUIT YRETVAL
	
RELOCATE(CUSTORDER,CUSTLI,RENUMB)
	;-------------------------------------------------------------------------------
	;	ALLOCATE SUPPLIER ORDER TO CUSTOMER ORDER
	;  !! ACHTUNG EINSPRUNG VON WEBSERVICE !! ;estimation 
	;SET YOK=$$RELOCATE^INRESERVIERT(CUSTORDER,CUSTLI,RENUMB)
	;-------------------------------------------------------------------------------
	NEW SUPORDER,SUPLI,YFELD,YKEY,YOK,MENGE,ART,WH,YRETVAL
	
	SET CUSTORDER = $GET(CUSTORDER)      ;CUSTOMER ORDER NUMMBER
	SET CUSTLI    = $GET(CUSTLI)         ;CUSTOMER LINE ITEM
	SET RENUMB    = $GET(RENUMB)         ;RESERVATION NUMBER
	SET YRETVAL   = $$$NO                ;RETURN VALUE
	
	SET ART   = $PIECE($GET(^INAUFP(YM,CUSTORDER,CUSTLI,1)),Y,4)     ;ITEM NUMBER CUSTOMER ORDER
	SET MENGE = $PIECE($GET(^INAUFP(YM,CUSTORDER,CUSTLI,1)),Y,5)     ;QTY CUSTOMER ORDER
	SET WH    = $PIECE($GET(^INAUFP(YM,CUSTORDER,CUSTLI,1)),Y,7)     ;GOODS ORIGIN CUSTOMER ORDER
	IF CUSTORDER'="" IF CUSTLI'="" IF RENUMB'="" DO
	. SET SUPORDER=""
	. FOR  SET SUPORDER=$ORDER(^INRESERVIERT(YM,SUPORDER)) QUIT:SUPORDER=""  DO
	. . SET SUPLI=""
	. . FOR  SET SUPLI=$ORDER(^INRESERVIERT(YM,SUPORDER,SUPLI)) QUIT:SUPLI=""  DO
	. . . IF $DATA(^INRESERVIERT(YM,SUPORDER,SUPLI," ",RENUMB)) DO
	. . . . SET YFELD=$GET(^INRESERVIERT(YM,SUPORDER,SUPLI," ",RENUMB,1))
	. . . . IF ART'="" IF ART'=$PIECE(YFELD,Y,3)           QUIT   ;DIFFERENT ITEMS
	. . . . IF MENGE>($PIECE(YFELD,Y,1)-$PIECE(YFELD,Y,2)) QUIT   ;QTY CUSTOMER ORDER > OPEN RESERVED QTY
	. . . . IF WH=5 QUIT                                          ;GOODS ORIGIN ALREADY DEFINED IN CUSTOMER ORDER AS SUPPLIER ORDER 
	. . . . ;
	. . . . SET YKEY=SUPORDER_","_SUPLI_","_CUSTORDER_","_CUSTLI
	. . . . DO  ;ANLEGEN NEUE RESERVIERUNG (MIT AUFTRAG) ;put onto 
	. . . . . NEW YFUNCT,BGJOB
	. . . . . SET (%(YQUERY,"YFUNCT"))=5_"|"_SUPORDER_"|"_SUPLI  ;LAGERMENGE
	. . . . . SET BGJOB = $$$YES
	. . . . . DO ^INAUFWH(CUSTORDER_","_CUSTLI)  ;FESTLEGEN WARENHERKUNFT
	. . . . ;
	. . . . SET YOK=$$KILL^INRESERVIERT(SUPORDER,SUPLI,RENUMB)  ;LÖSCHEN ALTE RESERVIERUNG (OHNE AUFTRAG) ;Delete 
	. . . . ; FIXME : YOK ignored - always returning successful <GRF>
	. . . . SET YRETVAL = $$$OK
	
	QUIT YRETVAL
	
]]></Routine>
</Export>