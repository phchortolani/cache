<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewFilterCombo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewFilterCombo
	;-------------------------------------------------------------------------------
	; creates relation combos for COMView
	;-------------------------------------------------------------------------------
#include WWWConst
#include COMSYS 
#include COMConst
	
#define DefaultRecords 300
	
CreateCombo(pidClass,pidFilter,pobjFld)
    ;-------------------------------------------------------------------------------
    ; Creates a combo box on the screen if less than 200 entries for a relation
    ; otherwise puts in a text box with search button to drill down further.
    ;
    ; Returns:
    ;
    ; History:
	; 21-Apr-2011	GRF		-: Get strSortCodes once
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 05-Jan-2010	GRF		SR15525: Call to $$Convert^WWWTR rather than $$^WWWTR
    ; 17-Sep-2009	shobby	SR16708: Removed changes from 16521
	; 02-Jul-2009	PPP		SR16663: Add the Calculated Relation Display Fields
	; 06-May-2009	PPP		SR16521: Barcodes scanned to get Item 
	; 							Create TextBox with OnBlur event if relation class
	; 							or class is a Quick Search Class
    ; 01-Sep-2008	shobby	SRBR014976: Set the FontFace as per company settings.
    ; 12-Feb-2008	shobby	SRBR014896: Limit the size of a line in a combo box.
    ; 11-Feb-2008	shobby	SRBR014896: Don't use a combo box if property is marked 
    ; 31-Jul-2007	Karine	SR15548: Filter in DataAccess 
    ; 19-Aug-2006	RPW		SR14360: Handle """" not just an empty string
    ; 15-Aug-2006	RPW		SR14807: quit if we are getting null relationships
    ; 15-Mar-2004	PaulK	Added macro usage
    ; 25-Nov-2004	PaulK	Created
    ;-------------------------------------------------------------------------------
    new blnDataAccess,blnInvalid,idClass,idRef,intCount,Loop,objFilter,objRef
    new strCalcRelationItems,strDisplay,strDefault,strGlobal,strJS,strPiece
    new strQuery,strRef,strRelation,strRelationItems,strSortCodes
    
	set blnDataAccess = $$$YES
	set blnInvalid    = $$$NO
	
	set idClass   = $$$WWW002RelationClass(pobjFld)
	set objFilter = $get(^CacheTempView(YUSER,"Filter",pidFilter))
	
	set strQuery  = "^"_idClass
	if $$$WWW002DisplayIfSortKeyEqual(pobjFld) set strQuery = strQuery_"s"
	set strQuery = strQuery_"("_$select($$$WWW001SharedFile($get(^WWW001(0,idClass,1))):0,1:YM) ; FIXME : YM Deprecated
	
	set strSortCodes = $$$WWW002DisplayIfSortKeyEqual(pobjFld)
	if strSortCodes'="" {
		for Loop=1:1:$length(strSortCodes,",") {
			quit:+$piece(strSortCodes,",",Loop)'=$piece(strSortCodes,",",Loop)
			
			set strPiece = $$$AddQuotes($piece(strSortCodes,",",Loop))
			if strPiece="" {
				set blnInvalid = $$$YES
				quit
			}
			set strQuery = strQuery_","_strPiece
		}
	}
	if $$$WWW002RelationalPrimaryKeys(pobjFld)'="" {
		for Loop=1:1:$length($$$WWW002RelationalPrimaryKeys(pobjFld),",") {
			set strRelation = $piece($$$WWW002RelationalPrimaryKeys(pobjFld),",",Loop)
			if (+strRelation'=strRelation) && '$find(strRelation,"""") {
				if '$data(@strRelation) {
					do FindRelation^COMViewUtils(idClass,strRelation,$get(^CacheTempView(YUSER,"RelationKey")))
				}
				set strRelation = $$$AddQuotes($get(@strRelation))
			}
			if (strRelation="") || (strRelation="""""") {
				set blnInvalid = $$$YES
				quit
			}
			set strQuery = strQuery_","_strRelation
		}
	}
	
	quit:blnInvalid
	
	set strGlobal = strQuery_")"
	set intCount  = 0
	set strJS = ""
	set strJS = strJS_"var s = document.createElement('select');"
	set strJS = strJS_"s.attachEvent('onchange',ControlChanged);"
	set strJS = strJS_"s.style.fontFamily='"_$$FontFace^WWW012()_"';" ;BR014976
	set strJS = strJS_"s.id='value"_pidFilter_"';"
	if $$$COMViewFilterComparator(objFilter)=$$$EnumCOMVIEWCOMPARATORWithin {
		set strJS = strJS_"s.multiple=true;"
		set strJS = strJS_"s.size='5';"
	}
	set strJS = strJS_"var o = document.createElement('option');"
	set strJS = strJS_"o.value='';"
	set strJS = strJS_"s.appendChild(o);"
	
	set strRelationItems     = $$$WWW002RelationalDisplayItems(pobjFld)
	set strCalcRelationItems = $$$WWW003CalcRelationalDisplayItems(pobjFld)
 
	for {
		set strGlobal = $query(@strGlobal)
		quit:$extract(strGlobal,1,$length(strQuery))'=strQuery
		
		set idRef = $qsubscript(strGlobal,$qlength(strGlobal)-($$$WWW002DisplayIfSortKeyEqual(pobjFld)=""))
		
		;Check the DataAccess for the field
		if $$$COMViewFilterDataAccess(objFilter)'="" {
			xecute "set blnDataAccess="_$$$COMViewFilterDataAccess(objFilter)
			
		} else {
			set blnDataAccess = $$$YES
		}
		continue:'blnDataAccess
		
		if $$$WWW002DisplayIfSortKeyEqual(pobjFld)'="" {
			set objRef = $get(@$$GetGlobal(strGlobal))
		} else {
			set objRef = $get(@strGlobal)
		}
		set strRef = $$GetLiteral^WWWTR($$$WWW003InputType(pobjFld),idRef)

		if (strCalcRelationItems'="") {   ; SR16663
			for Loop=1:1:$length(strRelationItems,";") {
				if Loop=1 {
					set strRef = strRef_" - "
				} else {
					set strRef = strRef_" "
				}
				set strRef = strRef_" "_$$GetCalculatedValue^WWWFOR71($$$WWW002RelationClass(pobjFld),
				                                                      $piece(strCalcRelationItems,";",Loop),
				                                                      $$$WWW002RelationalPrimaryKeys(pobjFld)_","_idRef,
				                                                      objRef)
			}
		
		} else {
			if (strRelationItems="") set strRelationItems = 1  ;show something from relation class!!
			for Loop=1:1:$length(strRelationItems,";") {
				if Loop=1 {
					set strRef = strRef_" - "
				} else {
					set strRef = strRef_" "
				}
				set strRef = strRef_$piece(objRef,Y,$piece(strRelationItems,";",Loop))
			}
		}
		
		set strJS = strJS_"var o = document.createElement('option');"
		set strJS = strJS_"o.value='"_$zcvt(idRef,"o","JS")_"';"
		if $find(","_$$$COMViewFilterValue1(objFilter)_",",","_idRef_",") {
			set strJS = strJS_"o.selected=true;"
		}
		set strJS = strJS_"o.innerHTML='"_$zcvt($extract(strRef,1,40),"o","JS")_"';"
		if ((idClass="WWW101") || (idClass="INPARA")) && ($$$WWW101ColorCode(objRef)'="") {
			set strJS=strJS_"o.style.backgroundColor='"_$$$Colour($$$WWW101ColorCode(objRef))_"';"
		}
		set strJS=strJS_"s.appendChild(o);"
		set intCount=intCount+1
		quit:intCount>$$$DefaultRecords
		quit:$length(strJS)>32000
	}
 
	;if (intCount<=$$$DefaultRecords) && ($length(strJS)<=32000) {								;BR014896
	;  1 : "Display Relations After Input"
	if (intCount<=$$$DefaultRecords) && ($length(strJS)<=32000) && ($$$WWW002RelationDisplayOptions(pobjFld)'=1) {  ;BR014896
		set strDefault=$$DefaultValue(idClass,$$$WWW002RelationalPrimaryKeys(pobjFld),1,.strDisplay)
		if strDefault'="" {
			set strJS=strJS_"var o = document.createElement('option');"
			set strJS=strJS_"o.value='"_$zcvt(strDefault,"o","JS")_"';"
			if $find(","_$$$COMViewFilterValue1(objFilter)_",",","_strDefault_",") {
				set strJS=strJS_"o.selected='true';"
			}
			set strJS=strJS_"o.innerHTML='"_$zcvt(strDisplay,"o","JS")_"';"
			set strJS=strJS_"s.appendChild(o);"
		}
		write strJS
	;SR17396 write "ctrl"_pidFilter_"_3.appendChild(s);"
		write "document.getElementById('ctrl"_pidFilter_"_3').appendChild(s);"	;SR17396 
	;	write "s.value='"_$$$COMViewFilterValue1(objFilter)_"';"
		write "s.initialValue='"_$$$COMViewFilterValue1(objFilter)_"';"
	;	write "s.options[0].selected=true;"
	
	} else {
		;too many items to successfully fit into a combo. create separate control.
		do CreateText^COMViewFilterControl(pidClass,pidFilter)
		do CreateSearchButton(pidClass,pidFilter)
	}
	quit
	
	
GetGlobal(pstrIndex,pintNumKeys="")
	new idClass,strGlobal,KeyLoop
	
	set idClass = $piece(pstrIndex,"(",1)
	set idClass = $extract(idClass,2,$length(idClass)-1)
	if pintNumKeys="" set pintNumKeys = $order(^WWW002(0,idClass,""),-1)
 
	set strGlobal = "^"_idClass_"("_$qsubscript(pstrIndex,1)
	for KeyLoop=pintNumKeys:-1:1 {
		set strGlobal = strGlobal_","_$$$AddQuotes($qsubscript(pstrIndex,$qlength(pstrIndex)-KeyLoop+1))
	}
	quit strGlobal_",1)"
	
	
CreateSearchButton(pidClass,pidFilter,blnNewNumber=1) 
    ;-------------------------------------------------------------------------------
    ; Creates a search button to go next to the text box.
    ; 
    ; Note. As ^CacheTempView is subscripted by YUSER, a new YUSER number
    ; is required when opening the sub form. 
    ; 
    ; History:
    ; 22-May-2012	shobby	SR18019: Don't try and interpret numerical constants
    ;							in relationships.
    ; 21-Feb-2007	PO		SR15435: Use getElementById for the date field
    ; 31-Aug-2006	HeberB	SRBR014211: add relational keys to search button
    ; 10-Nov-2005	JW		SR11904: Child user for popups
    ;  8-Aug-2005	JW		SR13116: Password is not available. Get it.
    ;  							Y check is redundant. YPARA is set incorrectly.
    ; 23-Mar-2005	Paul K	Made change for related class fields SR11966
    ; 15-Dec-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	new idField,idRelation,idRelationPartial,loopId,objField,objFilter,strLink
	
	set objFilter = $get(^CacheTempView(YUSER,"Filter",pidFilter))
	set idField   = $$$COMViewFilterField(objFilter)
	set objField  = $$GetRelation^COMViewUtils(.pidClass,.idField) ; Should this pass 2nd parm for changing?

	set strLink = YAKTION_"EP=WWWFORM"
	set strLink = strLink_"&YFORM=WWWPARA"
	set strLink = strLink_"&YLFFORM=4"
	set strLink = strLink_"&YTRAKT=1"
	set strLink = strLink_"&YBACK="
	set strLink = strLink_"&YUCI="_YUCI  ; Namespace
	set strLink = strLink_"&YBED="_YBED  ; User
	set strLink = strLink_"&YM="_YM      ; Company
	
	if (blnNewNumber=$$$YES) {
		set strLink = strLink_"&YUSER="_$$GetChildUser^WWWUSER(YUSER)
	} else {
		set strLink = strLink_"&YUSER="_YUSER  ;USER
	}
	
	set strLink = strLink_"&YLFDAT=Y"_$$$WWW002RelationClass(objField)_"D0"
 	
	// Include relational primary keys to search button
	set idRelation = $$$WWW002RelationalPrimaryKeys(objField)
	if (idRelation '= "")  {
		for loopId=1:1 {
			set idRelationPartial = $piece(idRelation, YKOMMA, loopId)
			quit:idRelationPartial=""
 
			if $find(idRelationPartial,"""") {
				set idRelationPartial = $translate(idRelationPartial,"""")	// extract quotes from string
			} elseif (+idRelationPartial=idRelationPartial) {				;SR18019
				; numeric contant entered.  No change required.				;SR18019
			} else {
				set idRelationPartial = @idRelationPartial	// get value from GLOBAL
			}
			if (loopId = 1) {
				set strLink = strLink _ "&YFKEY=" _ idRelationPartial
			} else {
				set strLink = strLink _ YKOMMA _ idRelationPartial
			}
		}
	}
 
	set strLink = strLink_"&YSEC="
	
	&js<
	var src = "search1.gif"
	var icone = ""
	if ('#($$MenuType^WWW013())#'==14){
		var icone = #server(VAR.CSP.VARMenuSideBar.traducaoIcone(src))#
	}
	if (icone!=""){
		var obj=document.createElement('i');
		obj.className=icone;
		obj.style.position='relative';
		obj.style.left='3px';
	}else{
		var obj = document.createElement('img');
		obj.src=FilePath+'search1.gif';
		obj.className='coolButton';
		//SR17583 obj.style.verticalAlign='text-bottom';
		obj.style.verticalAlign='middle';			//SR17583
	}
	obj.attachEvent('onclick',#("SubSearch"_pidFilter)#);
	ctrl#(pidFilter)#_3.appendChild(obj);
	function #("SubSearch"_pidFilter)#() {
		var field=document.getElementById('#("value"_pidFilter)#');  //SR17853
		var left = (screen.width/2)-(570/2);
		var top = (screen.height/2)-(530/2);
		$.post('COM.GridEdit.cls',{method: 'CampoRetorno', YUSER: '#(YUSER)#', yfield: '#("value"_pidFilter)#', pidType: 0, 
		tipoRetorno: 2},function(rs){	
			window.open('#(strLink)#'+(new Date()).getSeconds(),'Parameter','width=570,height=530,resizable=1,scroll=0,help=0,status=0,left='+left+',top='+top);
		});	

	
		/*var result = window.showModalDialog('#(strLink)#'+(new Date()).getSeconds(),'Parameter','DialogWidth: 570px; DialogHeight: 530px; resizable: yes; scroll: no; help:no; status: no;');
		if (result != null) {
			field.value=result;
		}*/
		field.fireEvent('onkeyup');
	}
	
	>
	quit
	
	
DefaultValue(pidClass,pidRelation,pblnDisplay=0,pstrDisplay="")
	;-------------------------------------------------------------------------------
	; Add the variable for a current value - for a filter class
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 02-Nov-2006	JW		SR15166: Use less likely characters for variable indirection.
	;-------------------------------------------------------------------------------
	new strValue
	
	set strValue = ""
	if pidClass="WWW012" {
		set strValue    = "YM"
		set pstrDisplay = $$$StrWWW012Company
	
	} elseif pidClass="WWW0121" {
		set strValue    = "YLOCATION"
		set pstrDisplay = $$$StrWWW0121Location
	
	} elseif pidClass="WWW013" {
		set strValue    = "YBED"
		set pstrDisplay = $$$StrWWW013UserName
	
	} elseif ((pidClass="WWW100") && ($find(pidRelation,"""WAEHRUNG"""))) {
		set strValue    = "YWHR"
		set pstrDisplay = $$$Text("Com00135")  ; "Currency"
	
	} elseif pidClass="WWWUSER" {
		set strValue    = "YUSER"
		set pstrDisplay = $$$StrWWWUSERUserNumber
	
	} elseif ((pidClass="WWW100") && ($find(pidRelation,"""LAND"""))) {
		set strValue    = "YCOUNTRY"
		set pstrDisplay = $$$Text("Com00136")  ; "Country"
	
	} elseif ((pidClass="WWW100") && ($find(pidRelation,"""SPRACHE"""))) {
		set strValue    = "LANGUAGE"
		set pstrDisplay = $$$Text("Com00137")  ; "Language"
	}
	
	if strValue'="" {
		if 'pblnDisplay {
			set strValue = @strValue
		} else {
			set pstrDisplay = $$$Text($listbuild("Com00138",pstrDisplay,@strValue))  ; "Current %1 (%2)"
			set strValue = "»"_strValue_"«"
		}
	}	
	quit strValue
	
]]></Routine>
</Export>