<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSHIPINFOCOSTSCustoms" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSHIPINFOCOSTSCustoms
#include COMSYS
#include INConst
#include FINConst
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		CALCULATE ESTIMATED COSTS FOR IMPORT - Customs
	; 
	; Called by : 
	; 	Routines : INSHIPINFOCOSTS
	; 
	; History :
	; 11-Oct-2006	GRF		SR15107: Moved to separate routine
	;-----------------------------------------------------------------------------*/
	
GetCustoms(YKEY="",pobjINSHIPINFO="")
	/*------------------------------------------------------------------------------
	; Called by : 
	; 	Routines : INSHIPINFO, INSHIPINFOCOSTS
    ;
    ; Returns:
    ;
    ; History:
    ;  7-Jul-2005	JW		SR12745: Need to round customs amount
    ; 16-Jun-2005	shobby	$$$Index idImport  SR12408
    ; 10-Jun-2005	shobby	SR12408: pass in pobjINSHIPINFO to determine the
    ; 						estimated customs based on changes to country of
    ; 						origin and/or Customs Clearance in INSHIPINFO.
    ; 10-Jun-2005	shobby	passed in YKEY.  SR12408
    ; 07-Jun-2005	shobby	Created SR12408
    ;-----------------------------------------------------------------------------*/
	new fltAmount,idOrder,idLine,objINAUFP,objINAUFPK
	new idItem,idSupplier,idCountry,idDate,fltTotalAmount,fltRate
	new objINSHIPINFO,idImport,idImportIdx
	
	set idImport=$piece(YKEY,",",1)
	set fltTotalAmount=0
	if ($piece(YKEY,",",2)="CUSTOMS") && (idImport'="") {
		set idOrder=""
		set idImportIdx=$$$Index(idImport)
		for {
			set idOrder=$order(^INAUFPAs(YM,2,idImportIdx,idOrder))
			quit:idOrder=""
			
			set idLine=""
			for {
				set idLine=$order(^INAUFPAs(YM,2,idImportIdx,idOrder,idLine))
				quit:idLine=""
				set objINAUFP=$get(^INAUFP(YM,idOrder,idLine,1))  ;prices here are what we sell it for ???
				set objINAUFPK=$get(^INAUFPK(YM,idOrder,idLine,1))
					
				set idItem		   =$$$INAUFPItemNumber(objINAUFP)
				set idSupplier	   =$$$INAUFPSupplier(objINAUFP)
				if pobjINSHIPINFO="" {
					set objINSHIPINFO  =$get(^INSHIPINFO(YM,idImport,1))
				} else {
					set objINSHIPINFO=pobjINSHIPINFO
				}
				set idCountry	   =$$$INSHIPINFOCountryOfOrigin(objINSHIPINFO)
				set idDate		   =$$$INSHIPINFOCustomsClearance(objINSHIPINFO)
				set fltRate		   =$$GetRate^INARTKCountryCustom(idItem_","_idSupplier_","_idCountry,idDate)/100
				set fltAmount	   =$$$FCRound($$$FCMult($$$INAUFPKNetOriginalPrice(objINAUFPK),fltRate))	//SR12745
				set fltTotalAmount =$$$FCAdd(fltAmount,fltTotalAmount)   
			}
		}
	}
	
	quit fltTotalAmount
	
ActualCustoms(YKEY="")
    /*------------------------------------------------------------------------------
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Jun-2005	shobby	Created SR12408
    ;-----------------------------------------------------------------------------*/
	new fltAmount
	
	if $piece(YKEY,",",2)="CUSTOMS" {
		set fltAmount=$$GetCustomsActual(YKEY)
		do PopulateDataField^COMUtils("INSHIPINFOCOSTS",2,fltAmount,"D",,)
	}
	quit
	
GetCustomsActual(YKEY="",pidOrder="",pidLine="")
    /*------------------------------------------------------------------------------
    ;
    ; Returns:
    ;
    ; History:
    ;  7-Jul-2005	JW		SR12745: Need to round customs amount
    ; 19-Jun-2005	shobby	optional parameters to only investigate one order/orderline. SR12679
    ; 16-Jun-2005	shobby	$$$Index idImport  SR12408
    ; 10-Jun-2005	shobby	passed in YKEY.  SR12048
    ; 09-Jun-2005	shobby	Allow overriding of country of origin on receive.
    ; 07-Jun-2005	shobby	Created SR12408
    ;-----------------------------------------------------------------------------*/
	new fltAmount,idOrder,idLine,objINAUFP
	new idItem,idSupplier,idCountry,idDate,fltTotalAmount,fltRate
	new idxLocation,idxStockLocation,idxDate,idCountryOverride
	new objINSHIPINFO,objINWEAUFVOR,objINWEAUF
	new idImport,idImportIdx
	
	set idImport=$piece(YKEY,",",1)
	set fltTotalAmount=0
	if ($piece(YKEY,",",2)="CUSTOMS")&&(idImport'="") {
		set idOrder=""
		set idImportIdx=$$$Index(idImport)
		for {
			set idOrder=$order(^INAUFPAs(YM,2,idImportIdx,idOrder))
			quit:idOrder=""
			
			if (idOrder=pidOrder)||(pidOrder="") {
				set idLine=""
				for {
					set idLine=$order(^INAUFPAs(YM,2,idImportIdx,idOrder,idLine))
					quit:idLine=""
					
					if (idLine=pidLine)||(pidLine="") {
						set objINAUFP     = $get(^INAUFP(YM,idOrder,idLine,1))
						set idItem        = $$$INAUFPItemNumber(objINAUFP)
						set idSupplier    = $$$INAUFPSupplier(objINAUFP)
						set objINSHIPINFO = $get(^INSHIPINFO(YM,idImport,1))
						set idCountry     = $$$INSHIPINFOCountryOfOrigin(objINSHIPINFO)
						set idDate        = $$$INSHIPINFOCustomsClearance(objINSHIPINFO)
						;set fltRate=$$GetRate^INARTKCountryCustom(idItem_","_idSupplier_","_idCountry,idDate)/100
						
						set idxLocation=""
						for {
							set idxLocation=$order(^INWEAUFVOR(YM,idOrder,idLine,idxLocation))
							quit:idxLocation=""
							
							set idxStockLocation=""
							for {
								set idxStockLocation=$order(^INWEAUFVOR(YM,idOrder,idLine,idxLocation,idxStockLocation))
								quit:idxStockLocation=""
								
								set idxDate=""
								for {
									set idxDate=$order(^INWEAUFVOR(YM,idOrder,idLine,idxLocation,idxStockLocation,idxDate))
									quit:idxDate=""
									
									set objINWEAUFVOR=$get(^INWEAUFVOR(YM,idOrder,idLine,idxLocation,idxStockLocation,idxDate,1))
									set idCountryOverride=$$$INWEAUFVORCountryOfOrigin(objINWEAUFVOR)
									if idCountryOverride="" set idCountryOverride=idCountry
									set fltRate=$$GetRate^INARTKCountryCustom(idItem_","_idSupplier_","_idCountryOverride,idDate)/100
									
									set fltAmount=$$$FCMult($$$INWEAUFVORCDV(objINWEAUFVOR),$$$INWEAUFVORQuantity(objINWEAUFVOR))
									set fltAmount=$$$FCRound($$$FCMult(fltAmount,fltRate))	//SR12745
									set fltTotalAmount=$$$FCAdd(fltAmount,fltTotalAmount)
								}
							}
						}
						set idxLocation=""
						for {
							set idxLocation=$order(^INWEAUF(YM,idOrder,idLine,idxLocation))
							quit:idxLocation=""
							
							set idxStockLocation=""
							for {
								set idxStockLocation=$order(^INWEAUF(YM,idOrder,idLine,idxLocation,idxStockLocation))
								quit:idxStockLocation=""
								
								set idxDate=""
								for {
									set idxDate=$order(^INWEAUF(YM,idOrder,idLine,idxLocation,idxStockLocation,idxDate))
									quit:idxDate=""
									
									set objINWEAUF=$get(^INWEAUF(YM,idOrder,idLine,idxLocation,idxStockLocation,idxDate,1))
									set idCountryOverride=$$$INWEAUFCountryOfOrigin(objINWEAUF)
									if idCountryOverride="" set idCountryOverride=idCountry
									set fltRate		   = $$GetRate^INARTKCountryCustom(idItem_","_idSupplier_","_idCountryOverride,idDate)/100
									set fltAmount	   = $$$FCMult($$$INWEAUFCDV(objINWEAUF),$$$INWEAUFIncomingGoodsQuantity(objINWEAUF))
									set fltAmount	   = $$$FCRound($$$FCMult(fltAmount,fltRate))	//SR12745
									set fltTotalAmount = $$$FCAdd(fltAmount,fltTotalAmount)
								}
							}
						}
					}
				}
			}
		}
	}
	quit fltTotalAmount
	
]]></Routine>
</Export>