<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW120A" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW120A
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN FORMULARFELDER
	;		Display table of keys and fields for a form
	;
	; Inputs : 
	;
	; ByRef :
	;	DATEI		
	;	YFELD		
	;	YKEY		
	;	YSEITE		Tab number - don't display if not on first tab
	;
	; Returns :
	;
	;
	; History :
	; 27-Mar-2007	GRF		SR15487: Doco; if/else rather than repeated testing; grid
	; 						background colours assigned through <TR> rather than <TD>
	; 						throughout.
	; 24-Jun-2005	GRF		SR12777: Enhancement to class listing for relations so
	; 						don't need to hover over property to see relation name.
	; 28.06.1998	DT
	;-------------------------------------------------------------------------------
	
	;==============================================================================================
	; (Form Key)         Form Name [*******                   ] *********               
	;-----------------------------------------------------------------------------------------------+  gen    list    gantt
	; |# |Description in Forms     |Input Type   |Input Size|Index Key|Input In Variable|Customizing|  KOPF / KOPF1 / KOPF2
	;-----------------------------------------------------------------------------------------------+
	; |  | No Primary Key          |             |          |         |                 |           |  ANKEY                 "WWW121"
	;-----------------------------------------------------------------------------------------------+
	; |  |                         |             |          |         |                 |           |         ANLIS1         "WWW131"
	;-----------------------------------------------------------------------------------------------+
	; |  | No Data Items           |             |          |         |                 |           |  ANDAT  ANDAT          "WWW122"
	;-----------------------------------------------------------------------------------------------+
	; |  | No List Items           |             |          |         |                 |           | (ANLIS)                "WWW131"
	;-----------------------------------------------------------------------------------------------+
	; |  | No Gantt Chart Defaults |             |          |         |                 |           | (ANGANT)        ANGANT "WWWDRAGDROP"
	;-----------------------------------------------------------------------------------------------+
	; (Form Data)
	;================================================================================================
	
	IF $GET(DATEI)="" SET DATEI=$PIECE($GET(YFELD),Y,11)
	IF $PIECE($GET(YKEY),",",1)'="" SET DATEI=$PIECE($GET(^WWW120(0,$PIECE(YKEY,",",1),1)),Y,11)  ;23861;TYBD;30,06,2003
	QUIT:YSEITE>1
	
	SET YDDSATZ=0
	SET YNKEY=$TRANSLATE($PIECE(YKEY,",",1),"""")
	QUIT:YNKEY=""
	SET YNDATEI=$PIECE($GET(^WWW120(0,YNKEY,1)),Y,11)
	
	NEW YFKEY,YDATEI,YSUCH,YSUCH1,YFORM,YI,YBACK,YKEY,YSATZ,FORMULAR
	
	SET YBACK="WWW120,"
	SET YKEY=YNKEY
	SET FORMULAR=YNKEY

	IF YNDATEI="" SET YNDATEI=YNKEY
	
	SET YFKEY=YKEY
	
	IF $PIECE(YVOR,Y,44)>0 WRITE YCR,"</TABLE>"
	do ^WWWFRAME(0)
	
	; vvv SR15487
	if $data(^WWWDRAGDROP(0,YKEY)) {
	;	GANTT CHARTS
		do KOPF2                                                             ; 7 columns
		do ANGANT
	} else {
	;	LIST GENERATOR
		if $data(^WWW131(0,YKEY)) {
			do KOPF1
			do ANLIS1
			do ANDAT
		
		} else {
	;	OTHER FORM TYPES	
			do KOPF                              ; heading
			set YDATEI=YNDATEI
			if YDATEI'="" {
				do ANKEY                         ; keys
				do ANDAT                         ; data fields
				if '$data(^WWW121(0,YKEY)) {
					do ANLIS                                                 ; "No List Items"
					do ANGANT                                                ; "No Gantt Chart Defaults"
				}
			} else {
				do ANDAT                         ; DRUCKEN MANUELLE    ; can never get here! <GRF>
			}
		}
	}
 
	DO ^WWWFRAME(1)
	IF $PIECE(YVOR,Y,44)>0 WRITE YCR,"<TABLE CELLSPACING=0 BORDER=0>"
	QUIT
	
KOPF
	;-------------------------------------------------------------------------------
	;	UEBERSCHRIFT
	;
	; History:
	; 27-Mar-2007	GRF		SR15487: Convert to brace format; close <FONT>; doco
	;-------------------------------------------------------------------------------
	SET YDATEI="WWW002"
 
	WRITE YCR,"<TR BGCOLOR="_YDARKGRAY_">",YCR  ; SR15487
	
	WRITE "<TH NOWRAP ALIGN=LEFT>"
	WRITE "<FONT SIZE=2>"
	WRITE "&nbsp;"
	WRITE "</FONT></TH>",YCR
 
	WRITE "<TH NOWRAP ALIGN=LEFT>"
	WRITE "<FONT SIZE=2>"
	WRITE "#"
	WRITE "</FONT></TH>",YCR
	
	FOR YLFN=2,3,4,6 {                                                              ; SR15487
		WRITE "<TH NOWRAP ALIGN=LEFT>"
		WRITE "<FONT SIZE=2>"
		IF $DATA(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)) {
			WRITE $$^WWWUML($PIECE($GET(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)),Y,1),1)
		} else {
			WRITE $$^WWWUML($PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,2),1)
		}
		WRITE "</FONT></TH>",YCR
	}
	
	WRITE "<TH NOWRAP ALIGN=LEFT>"
	WRITE "<FONT SIZE=2>"
	IF $DATA(^WWW0031(0,"WWW122",21,SPRACHE,1)) {                                   ; SR15487
		WRITE $$^WWWUML($PIECE($GET(^WWW0031(0,"WWW122",21,SPRACHE,1)),Y,1),1)
	} else {
		WRITE $$^WWWUML($PIECE($GET(^WWW003(0,"WWW122",21,1)),Y,2),1)       ; "Input In Variable"
	}
	WRITE "</FONT></TH>",YCR
 
	WRITE "<TH NOWRAP ALIGN=LEFT>"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(33400)           ; "Customizing"   ;SR14275
	WRITE "</FONT></TH>",YCR
	
	WRITE "</TR>",YCR
	QUIT
	
KOPF1
	;-------------------------------------------------------------------------------
	;	UEBERSCHRIFT LISTGENERATOR
	;
	; History:
	; 28-Mar-2007	GRF		SR15487: Always show 8 columns
	; 27-Mar-2007	GRF		SR15487: Convert to brace format; close <FONT>; doco
	;-------------------------------------------------------------------------------
	
	;-------------------------------------+
	;  |# |List : Database |  |  |  |  |  |
	;-------------------------------------+
	
	SET YDATEI="WWW131"
	WRITE YCR,"<TR BGCOLOR="_YDARKGRAY_">",YCR ; SR15487
	
	WRITE "<TH NOWRAP>"
	WRITE "<FONT SIZE=2>"
	WRITE "&nbsp;"
	WRITE "</FONT></TH>",YCR
	
	WRITE "<TH NOWRAP>"
	WRITE "<FONT SIZE=2>"
	WRITE "#"
	WRITE "</FONT></TH>",YCR
	
	;FOR YLFN=1,0,0,0,0 {                                ; SR15487
	FOR YLFN=1,0,0,0,0,0 {
		WRITE "<TH NOWRAP>"
		WRITE "<FONT SIZE=2>"
		IF YLFN=1 WRITE $$^WWWTEXT(90)_": "              ; "List"
	
		IF $DATA(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)) {                               ; SR15487
			WRITE $$^WWWUML($PIECE($GET(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)),Y,1),1)
		} else {
			WRITE $$^WWWUML($PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,2),1),"&nbsp;"
		}
		WRITE "</FONT></TH>",YCR
	}
	
	WRITE "</TR>",YCR
	QUIT
	
KOPF2
	;-------------------------------------------------------------------------------
	;	UEBERSCHRIFT GANTT
	;
	; History:
	; 29-Mar-2007	RPW		SR15487: Removed hard coded english
	; 28-Mar-2007	GRF		SR15487: Always show 8 columns
	; 27-Mar-2007	GRF		SR15487: Convert to brace format; close <FONT>; doco
	;-------------------------------------------------------------------------------
	
	;--------------------------------------------+
	;  |# |Gantt-Chart : Database |  |  |  |  |  |
	;--------------------------------------------+
	
	SET YDATEI="WWW131"
	WRITE YCR,"<TR BGCOLOR="_YDARKGRAY_">",YCR	; SR15487
	
	WRITE "<TH NOWRAP>"
	WRITE "<FONT SIZE=2>"
	WRITE "&nbsp;"
	WRITE "</FONT></TH>",YCR
	
	WRITE "<TH NOWRAP>"
	WRITE "<FONT SIZE=2>"
	WRITE "#"
	WRITE "</FONT></TH>",YCR
	
	;FOR YLFN=1,0,0,0,0 {                             ; SR15487
	FOR YLFN=1,0,0,0,0,0 {
		WRITE "<TH NOWRAP>"
		WRITE "<FONT SIZE=2>"
		IF YLFN=1 WRITE $$^WWWTEXT("WWW00068")_": "  ;Gantt-Chart // SR15487
		
		IF $DATA(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)) {                               ; SR15487
			WRITE $$^WWWUML($PIECE($GET(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)),Y,1),1)
		} else {
			WRITE $$^WWWUML($PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,2),1),"&nbsp;"
		}
		WRITE "</FONT></TH>",YCR
	}
	
	WRITE "</TR>",YCR
	QUIT
	
	
ANKEY 
    /*-------------------------------------------------------------------------------
    ;	PRIMÄRSCHLÜSSEL
    ;	Keys
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Mar-2007	GRF		SR15487: Replace kill commands to ensure indices are updated.
    ; 27-Mar-2007	GRF		SR15487: Call AddLink; close <FONT>;set strBGColor once
    ; 						instead of repeated checking; brace format; use idFld and
    ; 						pass to DATEN because new of YLFN is different under brace
    ; 						format; simplify "No Primary Key" output.
    ; 08-Feb-2006	shobby	SR14275: Call out to DisplayCustomising
    ;-------------------------------------------------------------------------------*/
	NEW idFld,intCol,strBGColor,YKEY
	
	SET YFORM="WWW121"
	SET YDATEI=YNKEY
	WRITE YCR
	
	;---------------------------------------
	; If any of the class' data fields are marked as being a unique key, kill all
	; the standard keys for the form.           (Key 2 : based on D22 Unique Key)
	; 
	; NOTE: the "Unique Key" facility has been disabled because accidental use can
	;       wipe out the true keys for a class.
	;---------------------------------------
	
	;IF $DATA(^WWW003s(0,2,$$$YES,YNDATEI)) KILL ^WWW121(0,YNKEY)          ;SONDER PRIMÄR
	;---------------------------------------
	IF $DATA(^WWW003s(0,2,$$$YES,YNDATEI)) do ^WWWSKILL("WWW121",YNKEY)    ; kills all keys for form
	
	;---------------------------------------
	; If no "Unique Key" field in class (all classes now) and
	; No actual keys for form
	;---------------------------------------
	IF '$DATA(^WWW003s(0,2,$$$YES,YNDATEI)) && '$DATA(^WWW121(0,YNKEY)) {
		set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)    ; SR15487
		write "<TR"_strBGColor_">"
				
	;--------------------------------------- Col 1-8         ; SR15487 - simplify
		for intCol=1:1:8 {
			write "<TD NOWRAP>"                              ;  SR15487
			write "<FONT SIZE=2>"
			if intCol=3 {
				do AddLink(YNKEY,YNKEY,YAKTION,YFORM,"")
				write $$^WWWTEXT(278)                        ; "No Primary Key"
				write "</A>"
			} else {
				write "&nbsp;"
			}
			write "</FONT></TD>",YCR
		}
		
		write YCR,"</TR>"
		
	} else {
		;-----------------------------------
		; Form has actual keys
		;-----------------------------------
	
		SET YLFN=""
		FOR  {
			SET YLFN=$ORDER(^WWW121(0,YNKEY,YLFN))
			quit:YLFN=""
			
			;*******************************
			; If a particular form key doesn't exist for the class, remove
			; it from the form together with any customization.
			;-------------------------------
			SET YSATZ=$GET(^WWW002(0,YNDATEI,YLFN,1))
			IF YSATZ="" {
				set strStatus = $$$Kill("WWW121",YNKEY_$$$COMMA_YLFN)
				do ^WWWSKILL("WWW121D",YNKEY_$$$COMMA_YLFN)               ; kills for all companies
				quit
			}
			;*******************************
			
			SET YKEY=YNKEY_","_YLFN
			set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)    ; SR15487
			write "<TR"_strBGColor_">"
			
	;--------------------------------------- Col 1
			write "<TD NOWRAP>"                            ; SR15487
			WRITE "<FONT SIZE=2>"
			do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"    ; SR15487
			SET YINHALT="primaer.gif"
			DO THUMP
			IF $PIECE(YSATZ,Y,3)=16   SET YINHALT="1:n"  ;1-n Beziehung
			IF $PIECE(YSATZ,Y,35)'="" SET YINHALT=$PIECE($GET(^WWW100(0,"RELATIONSHIP",SPRACHE,$PIECE(YSATZ,Y,35),1)),Y,1)
			WRITE "</FONT></TD>",YCR
			
	;--------------------------------------- Col 2
			write "<TD NOWRAP>"                            ; SR15487
			WRITE "<FONT SIZE=2>"
			do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"    ; SR15487
			
			WRITE YLFN
			WRITE "</A>"
			WRITE "</FONT></TD>",YCR
			
	;--------------------------------------- Col 3-7
			;FOR YI=2,3,4,6,8,21 SET YINHALT=$PIECE(YSATZ,Y,YI) DO  ;TYBD;14,2,2005
			FOR YI=2,3,4,6,21 {
				SET YINHALT=$PIECE(YSATZ,Y,YI)
				IF YI=2 {
					IF YNDATEI'="" IF YLFN'="" IF $DATA(^WWW0021(0,YNDATEI,YLFN,SPRACHE,1)) SET YINHALT=$PIECE(^WWW0021(0,YNDATEI,YLFN,SPRACHE,1),Y,1)
					SET YINHALT="("_YLFN_") "_YINHALT
				} elseIF YI=21 {
					SET YINHALT=$PIECE($GET(^WWW121(0,YNKEY,YLFN,1)),Y,21)
				}
				
			;	NEW YLFN                                             ; SR15487
			;	SET YLFN=YI
				set idFld = YI
				
				write "<TD NOWRAP>"                                  ; SR15487
				WRITE "<FONT SIZE=2>"
				; vvv   SR15487
				set strTitle = ""
				if (YI=3) {
					if ($piece(YSATZ,Y,26)'="") {
						set strTitle = $piece(YSATZ,Y,26)
					} elseif $piece(YSATZ,Y,8)'="" {
						set strTitle = $piece(YSATZ,Y,8)_"("_$translate($piece(YSATZ,Y,9),"""")_")"
					} else {
						set strTitle = $$^WWWTEXT(374)
					}
				} else {
					set strTitle = $$^WWWTEXT(374)
				}
				
				do AddLink(YKEY,YNKEY,YAKTION,YFORM,strTitle)        ; SR15487
				
				SET YDATEI="WWW002"
			;	SR15487 vvv
				IF YI=3 {
					IF YINHALT=16 && ($PIECE(YSATZ,Y,26)'="") {
						SET YINHALT=$PIECE(YSATZ,Y,26)
						SET idFld=8 ;ALTERNATIVE DATATYPE
					} elseIF $PIECE(YSATZ,Y,8)'="" {
						SET YINHALT=$PIECE(YSATZ,Y,8)
						SET idFld=8 ;TYBD;14,2,2005
					}
				}
				DO DATEN(idFld)
			;	SR15487 ^^^
				IF YI=4 {
					if ($PIECE(YSATZ,Y,26)'="") && ($PIECE(YSATZ,Y,3)=16) SET YINHALT=$$^WWWTEXT("WWW00045")  ;OID
				} elseif YI=3 {
					write $$^WWWTEXT("WWW00069")_" "  ;as
				}
				
				WRITE $$^WWWUML(YINHALT,1)
				;IF YLFN=8 IF $GET(SPRACHE)="DE" IF $PIECE(YSATZ,Y,9)'="" WRITE " (",$PIECE(YSATZ,Y,9),")"   ; SR12777;GRF
				if (idFld=8) && ($piece(YSATZ,Y,9)'="") {                                                    ; SR15487
					write "<FONT SIZE=1> (",$translate($piece($piece(YSATZ,Y,9),","),$$$DBLQUOTE),")</FONT>"
				}
				IF YINHALT="" WRITE "&nbsp;"
				;DO DATEN
				;WRITE $$^WWWUML(YINHALT,1)
				;IF YINHALT="" WRITE "&nbsp;"
				WRITE "</A>"
				WRITE "</FONT></TD>",YCR
			}
			
	;--------------------------------------- Col 8
			do DisplayCustomizing(YKEY)               ;SR14275
			
			WRITE "</TR>",YCR
		}
	}
	QUIT
	
ANDAT  
    /*-------------------------------------------------------------------------------
    ;	DATENFELDER
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 26-Jun-2007	RPW		SR15539: Show Free property number after _FREE fields
	; 28-Mar-2007	GRF		SR15487: Always show 8 columns
    ; 27-Mar-2007	GRF		SR15487: Call AddLink; close <FONT>; set strBGColor once
    ; 						instead of repeated checking; brace format; use idFld and
    ; 						pass to DATEN because new of YLFN is different under brace
    ; 						format; simplify "No Data Items" output.
    ; 08-Feb-2006	shobby	SR14275: Call out to DisplayCustomising
    ;-------------------------------------------------------------------------------*/
	NEW idFld,intCol,strBGColor,YKEY,YSATZ1,objWWW003
	
	SET YFORM="WWW122"
	SET YDATEI=YNKEY
	WRITE YCR
	
	IF '$DATA(^WWW122(0,YNKEY)) {
		set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)    ; SR15487
		write "<TR"_strBGColor_">"
				
	;--------------------------------------- Col 1-8
		for intCol=1:1:8 {
			write "<TD NOWRAP>"                            ; SR15487
			write "<FONT SIZE=2>"
			if intCol=3 {
				do AddLink(YNKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"    ; SR15487
				write $$^WWWTEXT(279)                                        ; "No Data Items"
				write "</A>"
			} else {
				write "&nbsp;"
			}
			write "</FONT></TD>",YCR
		}
		
		write YCR,"</TR>"
		
	} else {
	
		SET YBBN=""
		FOR  {
			SET YBBN=$ORDER(^WWW122(0,YNKEY,YBBN))
			QUIT:YBBN=""
			
			SET YLFN  = $PIECE($GET(^WWW122(0,YNKEY,YBBN,1)),Y,1)
			SET YSATZ = ""
			IF YLFN'="" SET YSATZ = $GET(^WWW003(0,YNDATEI,YLFN,1))
			
			SET YSATZ1=$GET(^WWW122(0,YNKEY,YBBN,1))
			IF YSATZ="" {
				SET $PIECE(YSATZ,Y,2) = $PIECE(YSATZ1,Y,12)
				IF $DATA(^WWW1221(0,YNKEY,YBBN,SPRACHE,1)) SET $PIECE(YSATZ,Y,2) = $PIECE(^WWW1221(0,YNKEY,YBBN,SPRACHE,1),Y,1)
				SET $PIECE(YSATZ,Y,3) = $PIECE(YSATZ1,Y,5)
				SET $PIECE(YSATZ,Y,4) = $PIECE(YSATZ1,Y,6)
				SET $PIECE(YSATZ,Y,8) = $PIECE(YSATZ1,Y,32)
			}
			
			set YKEY=YNKEY_","_YBBN
			set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)    ; SR15487
			write "<TR"_strBGColor_">"
			
	;--------------------------------------- Col 1
			write "<TD NOWRAP>"                                    ; SR15487
			write "<FONT SIZE=2>"
			do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"    ; SR15487
			set YINHALT="daten.gif"
			IF $PIECE(YSATZ,Y,22)=1   set YINHALT="primaer.gif"   ; FIXME : $$$WWW003UniqueKey() [D22] is deprecated <GRF>
			IF $PIECE(YSATZ,Y,3)=16   set YINHALT="1:n"  ;1-n Beziehung
			IF $PIECE(YSATZ,Y,35)'="" set YINHALT=$PIECE($GET(^WWW100(0,"RELATIONSHIP",SPRACHE,$PIECE(YSATZ,Y,35),1)),Y,1)
			do THUMP
			write "</FONT></TD>",YCR
			
	;--------------------------------------- Col 2
			write "<TD NOWRAP>"                                    ; SR15487
			WRITE "<FONT SIZE=2>"
			do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"    ; SR15487
			WRITE YBBN
			WRITE "</A>"
			WRITE "</FONT></TD>",YCR
			
	;--------------------------------------- Col 3-7
			FOR YI=2,3,4,6,21 {
				SET YINHALT=$PIECE(YSATZ,Y,YI)
				IF (YI=2) && (YLFN'="") {   ;WENN TEXTANZEIGE ;when 
					// SR15539: vvv
					IF YNDATEI'="" {
						IF $DATA(^WWW0031(0,YNDATEI,YLFN,SPRACHE,1)) SET YINHALT=$PIECE(^WWW0031(0,YNDATEI,YLFN,SPRACHE,1),Y,1)
						set objWWW003=$get(^WWW003(0,YNDATEI,YLFN,1))
						if $$$TRIMWS(YINHALT)=$$$FREE set YINHALT=$$$FREE_" ["_$$$WWW003PropertyName(objWWW003)_"]"
					}
					// SR15539 ^^^
					SET YINHALT="("_YLFN_") "_YINHALT
					
				}
				IF YI=21 SET YINHALT=$PIECE(YSATZ1,Y,21)_"("_$PIECE(YSATZ1,Y,60)_"-"_$PIECE(YSATZ1,Y,3)_"."_$PIECE(YSATZ1,Y,4)_")"
				
			;	NEW YLFN                                             ; SR15487
			;	SET YLFN=YI
				set idFld = YI
				
				write "<TD NOWRAP>"                                    ; SR15487
				WRITE "<FONT SIZE=2>"
				
				; vvv   SR15487
				set strTitle = ""
				if (YI=3) {
					if ($piece(YSATZ,Y,26)'="") {
						set strTitle = $piece(YSATZ,Y,26)
					} elseif $piece(YSATZ,Y,8)'="" {
						set strTitle = $piece(YSATZ,Y,8)_"("_$translate($piece(YSATZ,Y,9),"""")_")"
					} else {
						set strTitle = $$^WWWTEXT(374)
					}
				} else {
					set strTitle = $$^WWWTEXT(374)
				}
				
				do AddLink(YKEY,YNKEY,YAKTION,YFORM,strTitle)        ; SR15487
				
				SET YDATEI="WWW003"
			;	SR15487 vvv
			;	IF YI=3 IF YINHALT=16 IF $PIECE(YSATZ,Y,26)'="" SET YINHALT=$PIECE(YSATZ,Y,26) SET YLFN=8 ;ALTERNATIVE DATATYPE
			;	if YI=3 IF $PIECE(YSATZ,Y,8)'=""                SET YINHALT=$PIECE(YSATZ,Y,8) SET YLFN=8  ;TYBD;14,2,2005
			;	DO DATEN                                ; SR15487
				IF YI=3 {
					IF YINHALT=16 && ($PIECE(YSATZ,Y,26)'="") {
						SET YINHALT=$PIECE(YSATZ,Y,26)
						SET idFld=8 ;ALTERNATIVE DATATYPE
					} elseIF $PIECE(YSATZ,Y,8)'="" {
					    SET YINHALT=$PIECE(YSATZ,Y,8)
					    SET idFld=8  ;TYBD;14,2,2005
					}
				}
				
				DO DATEN(idFld)
			;	SR15487 ^^^
				IF YI=4  {
					IF $PIECE(YSATZ,Y,26)'="" && ($PIECE(YSATZ,Y,3)=16) {
						SET YINHALT=$$^WWWTEXT("WWW00045")  ;OID
					}
				} elseif YI=3 {
					write $$^WWWTEXT("WWW00069")_" "  ;as
				}
				
				WRITE $$^WWWUML(YINHALT,1)
				;IF YLFN=8 IF $GET(SPRACHE)="DE" IF $PIECE(YSATZ,Y,9)'="" WRITE " (",$PIECE(YSATZ,Y,9),")"   ; SR12777;GRF
				if (idFld=8) && ($piece(YSATZ,Y,9)'="") {                                                    ; SR15487
					write "<FONT SIZE=1> (",$translate($piece($piece(YSATZ,Y,9),","),$$$DBLQUOTE),")</FONT>"
				}
				IF YINHALT="" WRITE "&nbsp;"
				;DO DATEN
				;WRITE $$^WWWUML(YINHALT,1)
				;IF YINHALT="" WRITE "&nbsp;"
				WRITE "</A>"
				WRITE "</FONT></TD>",YCR
			}
			
	;--------------------------------------- Col 8
			do DisplayCustomizing(YKEY)              ;SR14275
			
			WRITE "</TR>",YCR
		}
	}
	QUIT
 
DisplayCustomizing(pYKEY="") 
    ;-------------------------------------------------------------------------------
    ; Displays an additional column on the 'Create Forms and Listings' screen WWW120
    ; to display links to customisations for each primary key and each field for
    ; each company.
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 02-Jul-2009	shobby	SR16710: Add visual indicators to the fields to link to 'Core Rules' or field
    ; 							customising.
    ; 08-Aug-2007	shobby	SR?????: Put the link back to the customizing form.
	; 29-Mar-2007	RPW		SR15487: &nbsp requires a ; on the end
    ; 27-Mar-2007	GRF		SR15487: Close <TD> and <FONT>; call AddLink
    ; 08-Feb-2006	shobby	SR14275: Created
    ;-------------------------------------------------------------------------------*/
	new id1,id2,strLink,idCompany,YDATEI,YKEY
	
	set id1=$piece(pYKEY,",",1)
	set id2=$piece(pYKEY,",",2)
	set strLink=""
	
	write "<TD NOWRAP>"                                    ; SR15487

	write "<FONT SIZE=2>"
	write "&nbsp"
	
	set blnCoreRule=$$$NO
	if (id1'="")&&(YFORM="WWW122") {
		if $data(^WWW122C2(0,id1,id2)) {
			do AddLink(pYKEY,YNKEY,YAKTION,YFORM_"C2",$$$Text(34455),"ball_blue.gif")	;Core rules are defined for this field.			;SR16710
		} else {
			do AddLink(pYKEY,YNKEY,YAKTION,YFORM_"C2",$$$Text(34456),"ball_grey.gif") 	;No Core rules are defined for this field.		;SR16710
		}
		write "</A>"
	}
	
	if (id1'="")&&(id2'="") {
		set idCompany=""
		for {
			xecute "set idCompany=$order(^"_YFORM_"D(0,id1,id2,idCompany))"
			quit:idCompany=""
			
			set YKEY=pYKEY_","_idCompany
			do AddLink(YKEY,YNKEY,YAKTION,YFORM_"D",$$$Text($lb(34457,idCompany)),"ball_green.gif")        ; Customisation is defined for company %1 on this field.    ; SR15487 ;SR????? ;SR16710
			write "</A>"
		}
	}
	write "</FONT></TD>",YCR             ; SR15487
	quit
	
ANLIS
    /*-------------------------------------------------------------------------------
    ;	LISTGENERATORVORGABEN
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Mar-2007	GRF		SR15487: ^WWW131D (customization of ^WWW131) does *NOT*
    ; 						exist - commented call to DisplayCustomizing but ensure
    ; 						8 columns.   CLEANED UP
    ; 27-Mar-2007	GRF		SR15487: set strBGColor once instead of repeated checking;
    ;						call AddLink; close <FONT>; brace format
    ; 08-Feb-2006	shobby	SR14275: Call out to DisplayCustomising
    ;-------------------------------------------------------------------------------*/
	new intCol,strBGColor,YKEY
	
	set YFORM="WWW131"
	set YDATEI=YNKEY
	write YCR
	
	;  vvv SR15487
	if '$data(^WWW131(0,YNKEY)) {
		set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)
		write "<TR "_strBGColor_">"
		
	;--------------------------------------- Col 1-8
		for intCol=1:1:8 {
			write "<TD NOWRAP>"
			write "<FONT SIZE=2>"
			if intCol=3 {
				do AddLink(YNKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"
				write $$^WWWTEXT(334)                                        ; "No List Items"
				write "</A>"
			} else {
				write "&nbsp;"
			}
			write "</FONT></TD>",YCR
		}
		write "</TR>",YCR
	}
	
	quit
	
ANGANT
    /*-------------------------------------------------------------------------------
    ;	GANTCHART VORGAGEN
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Mar-2007	GRF		SR15487: ^WWWDRAGDROPD is *NOT* customization of
    ; 						^WWWDRAGDROP - commented call to DisplayCustomizing but
    ; 						ensure 8 columns.   CLEANED UP
    ; 27-Mar-2007	GRF		SR15487: Call AddLink; close <FONT>; set strBGColor once
    ; 						instead of repeated checking; brace format
    ; 08-Feb-2006	shobby	SR14275: Call out to DisplayCustomising
    ;-------------------------------------------------------------------------------*/
	new intCol,strBGColor,YKEY
	
	set YFORM  = "WWWDRAGDROP"
	set YDATEI = YNKEY
	write YCR
	
	;  vvv SR15487
	set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)
	write "<TR "_strBGColor_">"
	
	;--------------------------------------- Col 1-8
	for intCol=1:1:8 {
		write "<TD NOWRAP>"
		write "<FONT SIZE=2>"
		if intCol=3 {
			do AddLink(YNKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"
			if $data(^WWWDRAGDROP(0,YNKEY)) {
				write $$^WWWTEXT(405)                                    ; "Gantt Chart Layout Default"   ; "Gantt Chart AUSWÄHLEN"
			} else {
				write $$^WWWTEXT(404)                                    ; "No Gantt Chart Defaults"      ; "KEIN Gantt Chart"
			}
			write "</A>"
		} else {
			write "&nbsp;"
		}
		write "</FONT></TD>",YCR
	}
	
	write "</TR>",YCR
	
	quit
	
ANLIS1
	;-------------------------------------------------------------------------------
	;	LISTGENERATOR
	;	
	; History:
	; 28-Mar-2007	GRF		SR15487: Always show 8 columns; remove ineffective anchors
	; 						in cols 4-7 and combine with col 8.   CLEANED UP
    ; 27-Mar-2007	GRF		SR15487: Call AddLink; close <FONT>; set strBGColor once
    ; 						instead of repeated checking; brace format; pass YI to
    ; 						DATEN because new of YLFN is different under brace format
	;-------------------------------------------------------------------------------
	new strBGColor,YKEY
	
	set YFORM="WWW131"
	set YDATEI=YNKEY
	write YCR
	
	;  vvv SR15487
	set YLFN=""
	$$$Order3(^WWW131,0,YNKEY,YLFN)
		
		set YSATZ=$GET(^WWW131(0,YNKEY,YLFN,1))
		set YKEY=YNKEY_","_YLFN
		set strBGColor = " BGCOLOR="_$select($increment(YDDSATZ)#2:YWHITE,1:YGRAY)
		write "<TR"_strBGColor_">"
		
	;--------------------------------------- Col 1
		write "<TD NOWRAP>"
		write "<FONT SIZE=2>"
		do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"
		write "&nbsp;"
		write "</FONT></TD>",YCR
		
	;--------------------------------------- Col 2
		write "<TD NOWRAP>"
		write "<FONT SIZE=2>"
		do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"
		write YLFN
		write "</A>"
		write "</FONT></TD>",YCR
		
	;--------------------------------------- Col 3
		set YINHALT = $piece(YSATZ,Y,1)
		WRITE "<TD NOWRAP>"
		WRITE "<FONT SIZE=2>"
		do AddLink(YKEY,YNKEY,YAKTION,YFORM,$$^WWWTEXT(374))        ; "Select Data Record"
		set YDATEI="WWW131"
		do DATEN(1)
		write $$^WWWUML(YINHALT,1)
		IF YINHALT="" write "&nbsp;"
		write "</A>"
		write "</FONT></TD>",YCR
		
	;--------------------------------------- Col 4-8
		for intCol=4:1:8 {
			WRITE "<TD NOWRAP>"
			WRITE "<FONT SIZE=2>"
			write "&nbsp;"
			write "</FONT></TD>",YCR
		}
		
		write "</TR>",YCR
	$$$End
	
	quit
	
DATEN(YLFN)
	;-------------------------------------------------------------------------------
	;	FORMAT DATEN
	;	Convert *Input Type* codes to text and replace references to relation classes
	;	with their class names (e.g. WWW100 becomes System Parameter)
	; 
	; 
	; History:
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 28-Mar-2007	GRF		SR15487: use objClassData and macros; convert to brace
	; 						format; doco
    ; 27-Mar-2007	GRF		SR15487: Pass YLFN in to cope with different new
    ; 						operation under brace format.
	;-------------------------------------------------------------------------------
	new blnProcess,objClassData,YDAT,YDATA,YFE,YKE,YSAT        ; vvv SR15487
	
	set objClassData = $get(^WWW003(0,YDATEI,YLFN,1))
	set YQ=0
	set YTYP = $$$WWW003InputType(objClassData)
	set YDAT = $$$WWW003RelationDatabase(objClassData)
	
	if (YINHALT'="") && (YDAT'="") {
		set blnProcess = $$$YES
		set YKE  = $$$WWW003RelationalPrimaryKeys(objClassData)
		if (YKE'="") && ($extract(YKE)'="""") {
			set blnProcess=(YKEY[",") || ('$data(@YKE)) || ($get(@YKE)="")
		}
		if blnProcess {
			set YFE  = $$$WWW003RelationalDisplayItems(objClassData)
			if +YFE=0 SET YFE=1
			if (YDAT="WWW001") && $data(^WWW0011(0,YINHALT,SPRACHE,1)) {
				set YINHALT = $piece(^WWW0011(0,YINHALT,SPRACHE,1),Y,1)
			} else {
				set YSAT  = "^"_YDAT_"("_$$^WWWYM(YDAT,1)
				set YDATA = $get(^WWW001(0,YDAT,1))
				if (YKE'="") && ($extract(YKE)'=",") set YSAT = YSAT_YKE_","
				if $$$WWW001AltSaveProcedure(YDATA)=4 {
					set YSAT = YSAT_""""_$translate(YINHALT,"""")_""""_")"
				} else {
					set YSAT = YSAT_""""_$translate(YINHALT,"""")_""""_",1)"
				}
				set YSAT(1) = $piece($$^WWWSETL(YSAT),Y,YFE)
				if YSAT(1)'="" set YINHALT = $extract($translate(YSAT(1),"|"," "),1,30) set YQ=1
			}
		}
	} else {
		do FORMAT
	}
	
	quit
		
FORMAT ;DATENFORMAT FELDER ;data format 
	
	QUIT:YINHALT=""
	SET YINHALT=$$GetLiteral^WWWTR(YTYP,YINHALT)
	
	;I YTYP=8!(YTYP=12) S YINHALT=$E("              ",1,(12-$L(YINHALT)))_YINHALT Q
	IF YTYP=5 SET YINHALT=$EXTRACT("*****************",1,$LENGTH(YINHALT)) QUIT
	IF YTYP=3 SET YINHALT=$EXTRACT($PIECE(YINHALT,"|",1),1,50) SET:YINHALT'="" YINHALT=YINHALT_"..." QUIT
	QUIT
	
THUMP ;BILDANZEIGE
	QUIT:YINHALT=""
	
	IF $FIND(YINHALT,":") WRITE YINHALT QUIT   ; 1:1, 1:n , m:n ; tybd;14,2,2005 
	IF '$FIND(YINHALT,".GIF") IF '$FIND(YINHALT,".gif") QUIT
	IF '$FIND(YINHALT,"/") SET YINHALT=YGIF_YINHALT
	WRITE YCR,"<IMG SRC="_""""_YINHALT_""""_" BORDER=0 ALIGN=RIGHT TITLE="_""""_$PIECE(YINHALT,".",1)_""""_" border=0>"
	QUIT
	
AddLink(YKEY,YDATEI,YAKTION,YFORM,pstrTitle="",pstrImage="")
	;-------------------------------------------------------------------------------
	; Build opening structure for link with optional title
	; 
	; History :
	; 02-Jul-2009	shobby	SR16710: Add images.
	; 27-Mar-2007	GRF		SR15487: created from lines in other subroutines as common
	; 						wrapper for WWWCGI with implied NEW.
	;-------------------------------------------------------------------------------
	write "<A"
	if pstrTitle'="" write " TITLE="""_pstrTitle_""""
	write " HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM
	do ^WWWCGI
	write """>"
	if (pstrImage'="") write "<IMG border=0 SRC="""_YGIF_pstrImage_""">"   ;SR16710
	quit
	
]]></Routine>
</Export>