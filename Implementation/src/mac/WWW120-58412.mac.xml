<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW120" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW120
#include COMSYS
#include WWWConst
#include COMConst

 	;15-Jul-2011	shobby	SR17584:   Class/Form changed (Do Not remove comment)
 	;								   InnerFrameHeight
 
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUFBAU AUTOMATISCHE MASKEN
	;	NOTE : The saved form record is updated directly without further reference
	;	       to WWWSPEI.
	;	
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 10-Sep-2008	shobby	BR014980: Call to HelpFile No longer necessary
	; 08-May-2007	GRF		SRBR014310: Doco; some braces; boolean macros
	; 13-Nov-2006	Steve S	SR14915: Call to HelpFile
	; 20-Jul-2005	GRF		SR12996: review when Multi-select not saving; ! => ||
	; 27.05.1998	DT
	;-------------------------------------------------------------------------------
	new YFORM
	
	QUIT:'$DATA(YKEY)
	QUIT:YKEY=""
	
	do Update(YKEY)
	quit
	
	
Update(YKEY)
	SET YFORM  = YKEY
	SET YFOART = $$$WWW120FormType($GET(^WWW120(0,YFORM,1)))
	
	IF YFOART=3 SET $$$WWW120FormFormatting(^WWW120(0,YFORM,1)) = 1   ;grid form => dann feldlänge automatisch ;automatic 
	
	;-------------------------------------------------------------------------------
	;AUTOMATIK FUER NORMALE FORMULARE
	;-------------------------------------------------------------------------------
	
	// FIXME: After the build change this to use all macros.
	
	IF (YFOART=2) || (YFOART=6) || (YFOART=8) {      ; SRBR014310
		SET $PIECE(^WWW120(0,YFORM,1),Y,123)="" ;NO FAST SAVE ;next 
		SET $PIECE(^WWW120(0,YFORM,1),Y,124)="" ;NO SUPER FAST SAVE ;way-out next 
	}
	
	IF $DATA(^WWW124(0,YFORM))                       SET $PIECE(^WWW120(0,YFORM,1),Y,124) = ""       ;NO SUPER FAST SAVE WHEN BUTTONS ;way-out next 
	IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,123)=$$$YES SET $PIECE(^WWW120(0,YFORM,1),Y,82)  = $$$YES   ;FAST SAVE
	IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,124)=$$$YES SET $PIECE(^WWW120(0,YFORM,1),Y,123) = $$$YES SET $PIECE(^WWW120(0,YFORM,1),Y,82) = $$$YES ;FAST SAVE
	
	;-------------------------------------------------------------------------------
	; D30 Create form automatically
	;-------------------------------------------------------------------------------
	IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,30)=$$$YES DO  ;FORMULAR AUTOMATISCH ERSTELLEN
	. QUIT:YFOART=2  ;Listen
	. ;
	. IF YFOART'=6 IF YFOART'=8 SET $PIECE(^WWW120(0,YFORM,1),Y,123) = $$$YES   ;SAVE WWWDATEN
	. SET $PIECE(^WWW120(0,YFORM,1),Y,82) = $$$YES     ;STÄNDIGE FELDVALIDIERUNG
	. SET $PIECE(^WWW120(0,YFORM,1),Y,29) = $$$YES     ;STÄNDIGE PROTOKOLLIERUNG
	. SET $PIECE(^WWW120(0,YFORM,1),Y,30) = $$$NO      ;AUFBEREITET
	. SET $PIECE(^WWW120(0,YFORM,1),Y,13) = $$$YES     ;GERAHMT =JA
	. SET $PIECE(^WWW120(0,YFORM,1),Y,44) = 2          ;FORMAT
	. SET $PIECE(^WWW120(0,YFORM,1),Y,32) = 200        ;FORMATLÄNGE
	. ;
	. ;IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)'=1 IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)'=3 QUIT
	. IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)'=1 IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)'=3 IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)'=11 QUIT  ;FIS;EDIT TABLE;25029;04.02.04
	. ;
	. SET YI(1) = $PIECE($GET(^WWW120(0,YFORM,1)),Y,11)
	. IF YI(1)'="" IF $PIECE(^WWW120(0,YFORM,1),Y,1)="" SET $PIECE(^WWW120(0,YFORM,1),Y,1)=$PIECE($GET(^WWW001(0,YI(1),1)),Y,1)  ;NAME AUS KLASSE ÜBERNEHMEN;FIS;03.02.05
	. IF YI(1)'="" DO
	. . ;
	. . ; Copy Class Keys to Form Keys
	. . ;
	. . SET YI(2)=""
	. . FOR  SET YI(2)=$ORDER(^WWW002(0,YI(1),YI(2))) QUIT:YI(2)=""  DO
	. . . IF '$DATA(^WWW121(0,YFORM,YI(2),1)) SET ^WWW121(0,YFORM,YI(2),1)=Y_Y_YI(2)
	. . . IF $PIECE($GET(^WWW002(0,YI(1),YI(2),1)),Y,3)'=0 IF '$DATA(^WWW127(0,YFORM,"P",YI(2),SPRACHE,1)) SET ^WWW127(0,YFORM,"P",YI(2),SPRACHE,1)=""
	. . ;
	. . SET YLFP=0,YLFSEI=1
	. . ;
	. . ; Copy Class Fields to Form Fields
	. . ;
	. . SET YI(22)=""
	. . SET YI(2)=""
	. . FOR  SET YI(2)=$ORDER(^WWW003(0,YI(1),YI(2))) QUIT:YI(2)=""  DO
	. . . QUIT:$PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,3)=16  ;EMBEDED NICHT EINBAUEN;TYBD;27338;14,2,2005
	. . . SET YI(22)=YI(22)+1
	. . . IF '$DATA(^WWW122(0,YFORM,YI(22),1)) DO
	. . . . ;
	. . . . ;------ Form Name
	. . . . SET YI(9)=$PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,2)
	. . . . IF $FIND($PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,25),".") SET YI(9)=$PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,25)  ;EMBEDDED MIT PROP.PROP...
	. . . . ;
	. . . . ;------ Alternate Input Type
	. . . . IF $PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,26)'="" DO  QUIT  ;NICHT FÜR EMBEDED FELD, ABER TEILE IN FELD AUFBAUEN
	. . . . . NEW PROP
	. . . . . SET YI(10) = $PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,26)
	. . . . . SET PROP   = $PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,25)
	. . . . . SET YI(20)=""
	. . . . . FOR  SET YI(20)=$ORDER(^WWW003(0,YI(10),YI(20))) QUIT:YI(20)=""  DO
	. . . . . . SET YI(22)=YI(22)+1
	. . . . . . IF '$DATA(^WWW122(0,YFORM,YI(22),1)) DO
	. . . . . . . SET YI(9)=PROP_"."_$PIECE($GET(^WWW003(0,YI(10),YI(20),1)),Y,25)  ;DATENFELD ;data item 
	. . . . . . . SET YMUSS=$PIECE($GET(^WWW003(0,YI(10),YI(20),1)),Y,18)           ;MUSSFELD
	. . . . . . . SET YLFP=YLFP+1 
	. . . . . . . SET ^WWW122(0,YFORM,YI(22),1)=Y_0_Y_+$GET(YLFP)_Y_1_Y_16_Y_Y_Y_Y_Y_Y_Y_YI(9)_Y_YMUSS
	. . . . . . . SET $PIECE(^WWW122(0,YFORM,YI(22),1),Y,60)=YLFSEI  ;SEITE ;side 
	. . . . . . . IF YI(22)=1 SET $PIECE(^WWW122(0,YFORM,YI(22),1),Y,38)=1
	. . . . . . . DO ^WWWSSORT("^WWW122(0,"_""""_YFORM_""""_","_YI(22)_",1)")
	. . . . ;
	. . . . ;----- Mandatory Field
	. . . . SET YMUSS=$PIECE($GET(^WWW003(0,YI(1),YI(2),1)),Y,18)      ;MUSSFELD
	. . . . ;
	. . . . ;----- On automatic build - start new tab after each 40 fields
	. . . . ;SET YLFP=YLFP+1 IF YLFP>2000 SET YLFP=1,YLFSEI=YLFSEI+1   ;KEIN SEITENWECHSEL
	. . . . SET YLFP=YLFP+1 
	. . . . IF YLFP>40 SET YLFP=1,YLFSEI=YLFSEI+1                      ;NACH 20 SEITE+1 
	. . . . ;
	. . . . ;----- Build and save record
	. . . . ; ClassFld# : AutomaticDataType : Row# : Col1 ... : Caption : blnMandatory
	. . . . SET ^WWW122(0,YFORM,YI(22),1)=YI(2)_Y_0_Y_+$GET(YLFP)_Y_1_Y_Y_Y_Y_Y_Y_Y_Y_YI(9)_Y_YMUSS
	. . . . SET $PIECE(^WWW122(0,YFORM,YI(22),1),Y,60)=YLFSEI                   ; tab        ;SEITE
	. . . . IF YI(22)=1 set $PIECE(^WWW122(0,YFORM,YI(22),1),Y,38)=1            ; new group
	. . . . DO ^WWWSSORT("^WWW122(0,"_""""_YFORM_""""_","_YI(22)_",1)")
	. . . ;
	. . . IF '$DATA(^WWW127(0,YFORM,"D",YI(2),SPRACHE,1)) SET ^WWW127(0,YFORM,"D",YI(2),SPRACHE,1)=""
	
	;-------------------------------------------------------------------------------
	;AUTOMATIK FUER LISTEN		D44		$$$WWW120FormFormatting
	;-------------------------------------------------------------------------------
	IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)=2 {      ; SRBR014310
		IF $PIECE(^WWW120(0,YFORM,1),Y,44)="" SET $PIECE(^WWW120(0,YFORM,1),Y,44)=1
	}
	
	;-------------------------------------------------------------------------------
	; D30		$$$WWW120CreateFormAutomatically (Reset for once-only execution)
	;-------------------------------------------------------------------------------
	IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,30)=$$$YES DO
	. SET $PIECE(^WWW120(0,YFORM,1),Y,30) = $$$NO
	. KILL ^WWW131(0,YFORM),^WWW132(0,YFORM),^WWW133(0,YFORM)
	. IF '$DATA(^WWW131(0,YFORM)) SET ^WWW131(0,YFORM,1,1)=$PIECE(^WWW120(0,YFORM,1),Y,11)_Y_Y_Y_Y_1_Y_Y_Y_1_Y_Y_Y_Y_Y_Y_Y_"-"_Y_1
	. SET YI(3)=""
	. FOR  SET YI(3)=$ORDER(^WWW131(0,YFORM,YI(3))) QUIT:YI(3)=""  DO
	. . SET YI(1)=$PIECE($GET(^WWW131(0,YFORM,YI(3),1)),Y,1)
	. . IF YI(1)'="" DO
	. . . SET YI(2)=""
	. . . FOR  SET YI(2)=$ORDER(^WWW002(0,YI(1),YI(2))) QUIT:YI(2)=""  DO
	. . . . IF '$DATA(^WWW132(0,YFORM,YI(3),YI(2),1)) SET ^WWW132(0,YFORM,YI(3),YI(2),1)=YI(2)_Y_Y_1_Y_1_Y_Y_Y_1_Y
	. . . ;
	. . . SET YI(2)=""
	. . . FOR  SET YI(2)=$ORDER(^WWW003(0,YI(1),YI(2))) QUIT:YI(2)=""  DO
	. . . . IF '$DATA(^WWW133(0,YFORM,YI(3),YI(2),1)) SET ^WWW133(0,YFORM,YI(3),YI(2),1)=YI(2)_Y_Y_Y_1_Y_Y_Y_1_Y
	
	;-------------------------------------------------------------------------------
	;VORGABEN AUS MANDANTENVORGABE ;default from company defaults
	;-------------------------------------------------------------------------------
	IF +$$$WWW120InheritCompanyProperties($get(^WWW120(0,YFORM,1)))=$$$NO DO 
	. NEW YVOR1,YQ,YI,YVOR
	. SET YQ=0
	. FOR YI=3:1:9 IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,YI)'="" SET YQ=1 QUIT
	. QUIT:YQ=1
	. SET $PIECE(^WWW120(0,YFORM,1),Y,31)=1
	. IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,2)=3 SET $PIECE(^WWW120(0,YFORM,1),Y,36)=1   ;WENN GRID, DANN LEFT
	. ;
	. DO ^WWWFORMX                 ; sets YVOR and YVOR1 from WWW120 and WWW012 respectively + YFIXHEADER, YI
	. ;
	. SET $PIECE(YVOR,Y,31)=0
	. SET ^WWW120(0,YFORM,1)=YVOR
	
	QUIT
	
	
OnAfterDataFields(pidForm,&pobjForm)
	;-------------------------------------------------------------------------------
	; Load Edit Grid
	;
	; Params:
	; pidForm - Form Id
	; pobjForm - Form Record
	;
	; ByRefs: This is not really needed as we use & for ByRef parameters now.
	;
	; Returns: Nothing
	;
	; History:
	; 05-Aug-2008	GRF		SR15826: Added call to ShowCustom on Tab 1
	; 18-Dec-2006	PO		SR15351: Created
	;-------------------------------------------------------------------------------
	;if YSEITE = 5 {         ; SR15826 vvv
	if YSEITE=1 {
		do ShowCustom(YKEY)
	} elseif YSEITE = 5 {    ; SR15826 ^^^
		do LoadGrid(pidForm,pobjForm)
	//	do SwitchBetweenClassAndForm(.pobjForm)
	}
	quit
	
	
ShowCustom(pidForm)
	;-------------------------------------------------------------------------------
	; Displays customisation of form
	;
	; Params: pidForm
	;
	; Returns: Nothing
	;
	; History:
	; 05-Aug-2008	GRF		SR15826: Created
	;-------------------------------------------------------------------------------
	new dteChanged,dteCreated,idCoy,idKey,objCustom,strFormName
	
	if (pidForm'="") && $data(^WWW120D(0,pidForm)) {
		do Start^COMTable($listbuild($$$StrWWW120DCompany,
		                             $$$StrWWW120DCreatedBy,
		                             $$$StrWWW120DCreatedOn,
		                             $$$StrWWW120DModifiedBy,
		                             $$$StrWWW120DModifiedOn),
		                  $$^WWWFORMNAME("WWW120D")_":",$$$NO,$$$YES,$$$NO)
		set idCoy = ""
		for {
			set idCoy = $order(^WWW120D(0,pidForm,idCoy))
			quit:idCoy=""
			
			set objCustom = $get(^WWW120D(0,pidForm,idCoy,1))
			set idKey = pidForm_","_idCoy
			
			if $$$WWW120DCreatedOn(objCustom) {
				set dteCreated = $$^WWWDATE(+$$$WWW120DCreatedOn(objCustom))
			} else {
				set dteCreated = "&nbsp;"
			}
			if $$$WWW120DModifiedOn(objCustom) {
				set dteChanged = $$^WWWDATE(+$$$WWW120DModifiedOn(objCustom))
			} else {
				set dteChanged = "&nbsp;"
			}
			
			do NewLine^COMTable()
			do InsertCell^COMTable(idCoy,"WWW120D",idKey)		
			do InsertCell^COMTable($$$WWW120DCreatedBy(objCustom),"WWW120D",idKey)		
			do InsertCell^COMTable(dteCreated,"WWW120D",idKey)		
			do InsertCell^COMTable($$$WWW120DModifiedBy(objCustom),"WWW120D",idKey)		
			do InsertCell^COMTable(dteChanged,"WWW120D",idKey)		
			do EndLine^COMTable()
		}
		do Stop^COMTable()
	}
	quit
	
	
LoadGrid(pidForm,pobjForm)
	;-------------------------------------------------------------------------------
	; Load the Edit Grid
	;
	; Params:
	; 	pidForm - Form Id
	; 	pobjForm - Form Record
	;
	; ByRefs:
	; 	YVOR		objForm        ^WWW120
	;
	; Returns: Nothing
	;
	; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
	; 18-Dec-2006	PO		SR15351: Created
	;-------------------------------------------------------------------------------
	new YAUSWAHL,YFELD,YFORM,YKEY
	
	set YFORM = "WWW120DynTable"
	set YFELD = ""
	set YKEY = $get(pidForm)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 300
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "WWW120"
 
	do ^COMGridEdit31
	quit

 
OnBeforeSave(pidForm,pobjForm)
	;-------------------------------------------------------------------------------
	; Ensure save is allowed and save Edit Grid content.
	;
	; Params:
	; pidForm - Form Id
	; pobjForm - Form Record
 	;
	; Returns: Nothing
	;
	; History:
	; 12-Apr-2010	GRF		SR17177: arrDynTable rather than objDynamicTable
	; 23-Mar-2010	shobby	SR17245: Removed redundant code. 
	; 18-Dec-2006	PO		SR15351: Created
	;-------------------------------------------------------------------------------
	new arrDynTable,arrPagesUsed,idTable,intPage,strStatus
 
	set strStatus = $$$OK
	$$$GRIDGetContents(.arrDynTable)
	kill arrPagesUsed
	;SR17245 if $get(idTable)'="" {  //due to UNDEF error 
	set idTable = ""
	for {
		set idTable = $order(arrDynTable(pidForm,idTable))
		quit:idTable=""
		
		set intPage = $$$WWW120DynTablePages1(arrDynTable(pidForm,idTable))
		if intPage '= "" {
			if $data(arrPagesUsed(intPage)) {
				set strStatus = "WWW00059"  ; "Currently there can only be one Dynamic Table per page of a form."
				quit
			} else {
				set arrPagesUsed(intPage) = ""
			}
		}
	}
	;SR17245 }
 
	;SR17245;if ($$$WWW120DynamicTableBasis(pobjForm) = $$$EnumWWWBASEDONClass) || ($$$WWW120DynamicTableBasis(pobjForm) = $$$EnumWWWBASEDONForm) {
	;SR17245;	if $$$WWW120DynTableClassOrForm(pobjForm) = "" {
	;SR17245;		set strStatus = "Class / Form field must be filled out."
	;SR17245;	}
	;SR17245;}
 
	;SR17245 if $$$ISOK(strStatus) set strStatus = $$ValidateLines^WWW120DynTable(pidForm,pobjForm)
 
	;SR17245if $$$ISOK(strStatus) {
	;SR17245	set strStatus = $$$GRIDSave(pidForm)
	;SR17245}
 
	if $$$ISERR(strStatus) {
		$$$Alert(strStatus)
		set Q = $$$QDontSave
	}
	quit
	
	
DoNotDisplay(pidForm)
	;-------------------------------------------------------------------------------
	; Check characters in Form and Customisation to don't display standard buttons
	;
	; Params: 	pidForm			: The form name
	;			pstrSeparator	: Separator character
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 13-Dec-2007	shobby	SRBR014601: Call to this routine has been removed from
	; 							all locations and only put in WWWFORMX.  As such it
	; 							is no longer necessary to consider that the list may
	; 							be comma separated, as in WWWFORMF.
	; 							Removed parameter pstrSeparator.
	; 11-Dez-2007	GM		SRBR014601: Created
	;-------------------------------------------------------------------------------
	new objWWW120,objWWW120D,strResult
	
	set objWWW120  = $get(^WWW120(0,pidForm,1))
	set objWWW120D = $get(^WWW120D(0,pidForm,YM,1))
 
	set strResult = $$$WWW120DoNOTDisplayStandardButto(objWWW120)_";"_$$$WWW120DDoNOTDisplayStandardButto(objWWW120D)
	quit strResult
	
	
ClassUsedInForm(pidForm="")
	;-------------------------------------------------------------------------------
	; Find the class used by the form
	;
	; Params: 	pidForm : The form name
	;
	; ByRefs:
	;
	; Returns:	strClass : The name of the class
	;
	; History:
	; 25-Jan-2008	shobby	SRBR014754: Created
	;-------------------------------------------------------------------------------
	new objWWW120,strClass
	
	set strClass = ""
	if pidForm'="" {
		set objWWW120 = $get(^WWW120(0,pidForm,1))
		set strClass  = $$$WWW120ClassUsedInForm(objWWW120)
	}
	quit strClass
	
	
AutoResizeRows(pidForm="")
	;-------------------------------------------------------------------------------
	; Determines whether the lines on a grid can Auto Expand based on the contents
	; of the cell.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 28-Feb-2008	shobby	SRBR014904: Created
	;-------------------------------------------------------------------------------
	new blnAutoResizeRows,objWWW120D
	
	set blnAutoResizeRows = $$$NO
	if (pidForm'="") {
		set blnAutoResizeRows = +$$$WWW120AutoResizeRows($get(^WWW120(0,pidForm,1)))
		set objWWW120D = $get(^WWW120D(0,pidForm,0,1))
		if $$$WWW120DAutoResizeRows(objWWW120D)'="" {
			set blnAutoResizeRows = +$$$WWW120DAutoResizeRows(objWWW120D)
		}
	}
	quit blnAutoResizeRows
	
	
OnBeforeDataAccess(YKEY="",YFORM="")
	;-------------------------------------------------------------------------------
	; Restricts the list of forms to show
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Jul-2008	shobby	SRBR014965: Defaults for parameters.
	; 03-Jul-2008	shobby	SRBR014965: Created
	;-------------------------------------------------------------------------------
	new idClass,strStatus
	
	set strStatus = $$$OK
	if '$$$NoKey(YKEY) {
		if $get(YFORM)="WWW128Form" {
			set strStatus = ($$$WWW120ClassUsedInForm($get(^WWW120(0,YKEY,1))) = $$$KEY1(YKEYCONTAINER))
			if $$$ISOK(strStatus) set strStatus = $$HasCounter(YKEY)
		} elseif ($get(YFORM)="WWW128") {
			set strStatus = $$$NO
		}
	}
	quit strStatus
	
	
OnFilter()
	quit $$$OK
	
	
HasCounter(pYKEY)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 26-Feb-2008	shobby	SRBR014965: Extended test to look at the Automatic
	; 							Sequence Counter field on the form definition.
	; 09-Jul-2008	shobby	SRBR014965: Created
	;-------------------------------------------------------------------------------
	new blnCounter,objWWW002,idLast,idClass,objWWW120,objWWW121,idLastKey
	
	set blnCounter = $$$NO
	if pYKEY'="" {
		set objWWW120 = $get(^WWW120(0,pYKEY,1))
		set idLastKey = $order(^WWW121(0,pYKEY,""),-1)
		if idLastKey'="" {
			set objWWW121 = $get(^WWW121(0,pYKEY,idLastKey,1))
			if $$$WWW121AutomaticSequenceCounter(objWWW121) {
				set blnCounter = $$$YES
			}
		}
		if 'blnCounter {
			set idClass = $$$WWW120ClassUsedInForm(objWWW120)
			if idClass'="" {
				set idLastKey = $order(^WWW002(0,idClass,""),-1)
				if idLastKey'="" {
					set objWWW002 = $get(^WWW002(0,idClass,idLastKey,1))
					if $$$WWW002InputType(objWWW002)=9 { ;Counter
						set blnCounter = $$$YES
					}
				}
			}
		}
	}
	quit blnCounter
	
	
FormHeaderOrImageFile(pYKEY="")
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 16-Sep-2008	shobby	SRBR014983: Created
	;-------------------------------------------------------------------------------
	new objWWW120,objWWW120D,strHeader
	
	set strHeader = ""
	
	if pYKEY'="" {
		set objWWW120  = $get(^WWW120(0,pYKEY,1))
		set strHeader  = $$$WWW120FormHeaderOrImageFile(objWWW120)
		set objWWW120D = $get(^WWW120D(0,pYKEY,YM,1))
		if $$$WWW120DFormHeaderOrImageFile(objWWW120D) '="" {
			set strHeader = $$$WWW120DFormHeaderOrImageFile(objWWW120D)
		}
	}
	quit strHeader

DoNotDisplayFormHeader(YFORM)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
    ; 15-Jul-2014	shobby	HEVA-1565: MenuType should also consider WWW012
	; 24-Apr-2011	shobby	SR17998: Don't put Mega menu of WWWPARA
	; 23-Apr-2011	shobby	SR17998: Created
	;-------------------------------------------------------------------------------
	new blnResult
	
	set blnResult=$$$NO

	if $get(YFORM)'="" {
		if $$MenuType^WWW013()'=13 {	;HEVA-1565
			;HEVA-1565 if ($$$WWW013MenuType($get(^WWW013(0,YBED,1)))'=13) {
		 	set objWWW120 = $get(^WWW120(0,YFORM,1))
		 	if $$$WWW120DoNOTDisplayFormHeader(objWWW120) {
		 		set blnResult=$$$YES
		 	} elseif $$$WWW120InheritCompanyProperties(objWWW120) {
		 		if $$$WWW012DoNotDisplayHeader(YVOR1) set blnResult=$$$YES
			}
		} else {
			; TODO: Need to find a more generic way of doing this with popups only
			if YFORM="WWWPARA" set blnResult=$$$YES
			if YFORM="COMViewSearch" set blnResult=$$$YES
		}
	}
	quit blnResult

FixedColumns()
	;-------------------------------------------------------------------------------
	; Determine the default value for the checkbox 
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Sep-2013	shobby	CORE-254: Created
	;-------------------------------------------------------------------------------
	new strFixedColumns
	set strFixedColumns=""
	if YFORM'="" {
		set strFixedColumns=$$$WWW120FixedColumns($get(^WWW120(0,YFORM,1)))
		if strFixedColumns="" set strFixedColumns=$$$WWW012FixedColumns($get(^WWW120(0,YM,1)))
	}
	quit strFixedColumns 


	
]]></Routine>
</Export>