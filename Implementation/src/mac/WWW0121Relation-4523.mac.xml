<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW0121Relation" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW0121Relation
	;-------------------------------------------------------------------------------
	;     DEVELOPMENT CODE ONLY - DO NOT USE
	;-------------------------------------------------------------------------------
#include COMSYS
 
ByType(pidCompany,pintLocnType,parrLocs)
	;-------------------------------------------------------------------------------
	;     DEVELOPMENT CODE ONLY - DO NOT USE AS IS
	;-------------------------------------------------------------------------------
	; Filter Locations by Type
	;
	; Inputs: 
	; 	pidCompany		Active company - often YM
	; 	pintLocType		Type of Location
	;	parrLocs		By Reference Array or Global (.XYZ or ^XYZ)
	; 
	; Returns : Array Of Locations
	;
	; History :
	; 07-Aug-2007	FIS		created
	;-------------------------------------------------------------------------------
	new strLoc,idxType,loop,count,strGlobalNode
	
	if $get(pidCompany)="" set pidCompany=YM
	if $get(pintLocType)="" quit
	
	set count=0
	for loop=1:1 {
		set idxType=$piece(pintLocnType,",",loop)
		quit:idxType=""
		
		set strLoc=""
		for {
			set strLoc=$order(^WWW0121s(0,4,idxType,pidCompany,strLoc))
			quit:strLoc=""
			
			set count=count+1
			if $extract($get(parrLocs))="^" {               //Save in Global
				set strGlobalNode=parrLocs_"("_count_")"
				set @strGlobalNode=strLoc
			} else {                                        //Save in Variable
				set parrLocs(count)=strLoc
			}
		}
	}
	quit
	
Inventory(pidCompany,parrLocs)
	;-------------------------------------------------------------------------------
	;     DEVELOPMENT CODE ONLY - DO NOT USE AS IS
	;-------------------------------------------------------------------------------
	if $extract($get(parrLocs))="^" {    //Save in Global
		do ByUsage(pidCompany,1,0,0,parrLocs)
	} else {                             //Save in Variable
		do ByUsage(pidCompany,1,0,0,.parrLocs)
	}
	quit
	
Planning(pidCompany,parrLocs)
	;-------------------------------------------------------------------------------
	;     DEVELOPMENT CODE ONLY - DO NOT USE AS IS
	;-------------------------------------------------------------------------------
	if $extract($get(parrLocs))="^" {
		do ByUsage(pidCompany,0,1,0,parrLocs)
	} else {
		do ByUsage(pidCompany,0,1,0,.parrLocs)
	}
	quit
	
Sales(pidCompany,parrLocs)
	;-------------------------------------------------------------------------------
	;     DEVELOPMENT CODE ONLY - DO NOT USE AS IS
	;-------------------------------------------------------------------------------
	if $extract($get(parrLocs))="^" {
		do ByUsage(pidCompany,0,0,1,parrLocs)
	} else {
		do ByUsage(pidCompany,0,0,1,.parrLocs)
	}
	quit
	
	
ByUsage(pidCompany,pblnInv,pblnPlan,pblnSale,parrLocns)
	;-------------------------------------------------------------------------------
	;     DEVELOPMENT CODE ONLY - DO NOT USE AS IS
	;-------------------------------------------------------------------------------
	; Filter Locations by Usage
	;
	; Inputs:
	; 	pidCompany		Active company - often YM
	; 	pblnInv			This is a Inventory Location
	;	pblnPlan		This is a Planning Location
	;	pblnSale		This is a Sales Location
	;	parrLocs		By Reference Array or Global (.XYZ or ^XYZ)
	; 
	; Returns : Array Of Locations
	;
	; History :
	; 07-Aug-2007	FIS		created
	;-------------------------------------------------------------------------------
	new idLocn,objLocn,count,strGlobalNode
	
	set pblnInv   = +$get(pblnInv)
	set pblnPlan  = +$get(pblnPlan)
	set pblnSale  = +$get(pblnSale)
	
	set count=0
	set idLocn=""
	for {
		set idLocn=$order(^WWW0121(0,pidCompany,idLocn))
		quit:idLocn=""
		
		; FIXME - If ask for multiple (e.g. Sales and Planning) will only get locations that are both.
		;         If want all sales and all planning and all both must run, save, re-run, combine. <GRF>
		;         May not be needed. If indexed can build lists from them.
		
		set objLocn=$get(^WWW0121(0,pidCompany,idLocn,1))
		if pblnInv   = $$$YES  continue:$$$WWW0121StorageLocn(objLocn)'=$$$YES
		if pblnPlan  = $$$YES  continue:$$$WWW0121PlanningLocn(objLocn)'=$$$YES
	;	if pblnAvail = $$$YES  continue:$$$WWW0121InventoryAvailForParent(objLocn)'=$$$YES
		if pblnSale  = $$$YES  continue:$$$WWW0121SalesLocn(objLocn)'=$$$YES
		
		set count=count+1
		if $extract($get(parrLocns))="^" {              //Save in Global
			set strGlobalNode=parrLocns_"("_count_")"
			set @strGlobalNode = idLocn
		} else {                                        //Save in Variable
			set parrLocns(count) = idLocn
		}
	}
	quit
	
]]></Routine>
</Export>