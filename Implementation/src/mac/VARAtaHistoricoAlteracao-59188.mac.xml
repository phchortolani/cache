<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAtaHistoricoAlteracao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include INConst
#include VARConst
#include VARLog
 
VARAtaHistoricoAlteracao
	quit
 
OnAfterPrimaryKey()
	set VORG(1)=$piece(YKEY, YKOMMA, 1)
	quit
	
GetInformacaoAfetada()
	quit $case($$$VARAtaHistoricoAlteracaoProduto(YFELD), "":"", 0:"Documento",:"Produto")
 
GetCodigo()
	new strCodigo, objAtaLinha, objINART
	
	set strCodigo = ""
	if ($$$VARAtaHistoricoAlteracaoProduto(YFELD) = 0) {
		set strCodigo = "("_$$$VARAtaHistoricoAlteracaoProduto(YFELD)_")&nbsp;"_"Ata"
	}
	elseif ($length($$$VARAtaHistoricoAlteracaoProduto(YFELD)) > 0) && ($$$VARAtaHistoricoAlteracaoProduto(YFELD) '= 0) && ($length(VORG(1)) > 0){
		set objINART = $get(^INART(0, $$$VARAtaHistoricoAlteracaoProduto(YFELD), 1))
		set strCodigo = "("_$$$VARAtaHistoricoAlteracaoProduto(YFELD)_")&nbsp;"_$$$INARTSearchName(objINART)
	}
 
	quit strCodigo
 
RegistrarAlteracao(pidAta, pintAcao, pidItem, pobjAtaLinha)
	quit $$RegistrarAlteracaoIdentificada($horolog, $get(YBED), pidAta, pintAcao, pidItem, pobjAtaLinha)
 
RegistrarAlteracaoIdentificada(pdatAlt, pstrUsuario,  pidAta, pintAcao, pidItem, pobjAtaLinha)
	new objAtaHistAlter, idHistAlt, strStatus, strLog
 
	kill objAtaHistAlter
	
	set $$$VARAtaHistoricoAlteracaoDataHoraAlteracao(objAtaHistAlter)	= pdatAlt
	set $$$VARAtaHistoricoAlteracaoUsuario(objAtaHistAlter) 			= pstrUsuario
	set $$$VARAtaHistoricoAlteracaoAcao(objAtaHistAlter) 				= pintAcao
	set $$$VARAtaHistoricoAlteracaoProduto(objAtaHistAlter) 			= pidItem
	set $$$VARAtaHistoricoAlteracaoQuantidade(objAtaHistAlter) 			= $$$VARAtaLinhaQuantidade(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoValorUnitario(objAtaHistAlter) 		= $$$VARAtaLinhaValorUnitario(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoFornecedor(objAtaHistAlter) 			= $$$VARAtaLinhaFornecedor(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoEmbalagem(objAtaHistAlter) 			= $$$VARAtaLinhaEmbalagem(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoMarca(objAtaHistAlter) 				= $$$VARAtaLinhaMarca(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoDataHomologacao(objAtaHistAlter) 	= $$$VARAtaLinhaDataHomologacao(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoDataValidade(objAtaHistAlter) 		= $$$VARAtaLinhaDatadeValidade(pobjAtaLinha)
	set $$$VARAtaHistoricoAlteracaoStatusItem(objAtaHistAlter) 			= $$$VARAtaLinhaStatusItem(pobjAtaLinha)
 
	set idHistAlt = $$^WWWNEXT1("VARAtaHistoricoAlteracao", pidAta, 2)
	set strStatus = $$$Save("VARAtaHistoricoAlteracao",pidAta_$$$COMMA_idHistAlt,objAtaHistAlter,$$$YES)
 
	set strLog = "Data/hora='"_$zdatetime(pdatAlt, 3, 1)_"', "_
		"usuário="_pstrUsuario_", "_
		"ação="_$$GetDescricaoAcao(pintAcao)_", "_
		"produto="_pidItem_", "_
		"quantidade="_$$$VARAtaLinhaQuantidade(pobjAtaLinha)_", "_
		"valor unitário"_$$$VARAtaLinhaValorUnitario(pobjAtaLinha)_", "_
		"fornecedor="_$$$VARAtaLinhaFornecedor(pobjAtaLinha)_", "_
		"embalagem='"_$$$VARAtaLinhaEmbalagem(pobjAtaLinha)_"', "_
		"marca='"_$$$VARAtaLinhaMarca(pobjAtaLinha)_"', "_
		"data homologação='"_$$$VARAtaLinhaDataHomologacao(pobjAtaLinha)_"', "_
		"data validade='"_$$$VARAtaLinhaDatadeValidade(pobjAtaLinha)_"', "_
		"status'"_$$$VARAtaLinhaStatusItem(pobjAtaLinha)_"'"
	$$$VARLOGINFO(strLog)
 
	quit strStatus
 
GetDescricaoAcao(pintAcao)
	if ('$data(^INPARA(YM, "VARSESHISTALTACAO", SPRACHE, pintAcao, 1))) {
		quit "Desconhecida"
	}
	
	quit $$$INPARAText(^INPARA(YM, "VARSESHISTALTACAO", SPRACHE, pintAcao, 1))
 
]]></Routine>
</Export>