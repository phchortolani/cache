<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMCombo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMCombo
    ;-------------------------------------------------------------------------------
    ; populates @Net Combos
    ;-------------------------------------------------------------------------------
#include COMSYS
	
StartCombo(pstrComboName,pstrValue="",pblnNoScriptTag=$$$NO,pblnMandatory=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Starts the combo process off. Either writes at screen paint time
    ; or hyperevent time.
    ; 
    ; Note:
    ; 30-Mar-2005	JW		TODO: if pstrValue is set, it selects that option, but 
    ; 						does not update the current data (YFELD).
    ; 
    ; History:
    ; 24-Mar-2008	shobby	SRBR014916: Improve performance when loading a large no.
    ; 							of options. Included YUCI as key on CacheTempCombo
    ; 03-Apr-2007	JW		SR15384: Catch exception: eg field has been moved/hidden
    ; 							via customisation
    ; 22-Nov-2004	PaulK	Created
    ;-------------------------------------------------------------------------------
    new strHTML
    
    set strHTML = ""
 	kill ^CacheTempCombo(YUCI,YUSER,"HTML") ;BR014916
	if $get(%KEY("HYPEREVENT"))'=1 if 'pblnNoScriptTag $$$Append(strHTML,$$$CRLF_"<script language=javascript type='text/javascript'>")
	$$$Append(strHTML,"try {")		// SR15384
	$$$Append(strHTML,"var s = typeof("_pstrComboName_")=='object'?"_pstrComboName_":document.getElementById('"_pstrComboName_"');"_$$$CRLF)
	$$$Append(strHTML,"s.innerHTML='';"_$$$CRLF)
	set ^CacheTempCombo(YUCI,YUSER,"Value")  = pstrValue ;BR014916
	set ^CacheTempCombo(YUCI,YUSER,"Buffer") = strHTML	 ;BR014916
	set ^CacheTempCombo(YUCI,YUSER,"Hyper")  = ($get(%KEY("HYPEREVENT"))=1)||(pblnNoScriptTag)  ;BR014916
	
	if 'pblnMandatory do AddOption("","")
	
	quit
	
	
StopCombo(pblnUseArray=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Clears the write buffer and send the rest of combo creation to the screen.
    ; 
    ; History:
    ; 01-Sep-2008	shobby	SRBR014976: Set the FontFace as per company settings.
    ; 24-Mar-2008	shobby	SRBR014916: Improve performance when loading a large no.
    ; 							of options. Included YUCI as key on CacheTempCombo
    ; 03-Apr-2007	JW		SR15384: Catch exception: eg field has been moved/hidden
    ; 							via customisation
    ; 22-Nov-2004	PaulK	Created
    ;-------------------------------------------------------------------------------
	new strHTML,idxHTML
	
	set strHTML=$get(^CacheTempCombo(YUCI,YUSER,"Buffer"))  ;BR014916
	$$$Append(strHTML,"} catch(e) {}")		// SR15384
	write strHTML
	if pblnUseArray {
		write "s.outerHTML=s.outerHTML.split('>')[0]+'>'"
		set idxHTML=""
		for {
			set idxHTML=$order(^CacheTempCombo(YUCI,YUSER,"HTML",idxHTML))  ;BR014916
			quit:idxHTML=""
			write "+'"_$get(^CacheTempCombo(YUCI,YUSER,"HTML",idxHTML))_"'"  ;BR014916
		}
		write "+'</select>';"_$$$CRLF
	}
	write "s.style.fontFamily='"_$$FontFace^WWW012()_"';"  //BR014976
	if '^CacheTempCombo(YUCI,YUSER,"Hyper") write "</script>"  ;BR014916 ;BR014976
	kill ^CacheTempCombo(YUCI,YUSER)  ;BR014916
	quit
	
	
AddOption(pidKey,pstrName,pblnUseArray=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Adds an option to the combo, if keys match, selects that option.
    ;
    ; History:
    ; 24-Mar-2008	shobby	SRBR014916: Improve performance when loading a large number of options.
    ; 									Included YUCI as a key on CacheTempCombo
    ; 22-Nov-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	new strHTML,strHTML1
	
	if pblnUseArray {
		set strHTML=$get(^CacheTempCombo(YUCI,YUSER,"HTML",99999)) ;BR014916
		set strHTML1="<OPTION value="_pidKey
		if (pidKey=$get(^CacheTempCombo(YUCI,YUSER,"Value")))&&(pidKey'="") { ;BR014916
			set strHTML1=strHTML1_" selected"
		}
		set strHTML1=strHTML1_">"_pstrName_"</OPTION>"
		if ($length(strHTML)+$length(strHTML1))>32000 {
			merge ^CacheTempCombo(YUCI,YUSER,"HTML",$order(^CacheTempCombo(YUCI,YUSER,"HTML",99999),-1)+1)=^CacheTempCombo(YUCI,YUSER,"HTML",99999) ;BR014916
			set strHTML=""
		}
		set strHTML=strHTML_strHTML1
		set ^CacheTempCombo(YUCI,YUSER,"HTML",99999)=strHTML ;BR014916
	} else {
		set strHTML=$get(^CacheTempCombo(YUCI,YUSER,"Buffer")) ;BR014916
	 
		set strHTML=strHTML_"var o = document.createElement('option');"_$$$CRLF
		set strHTML=strHTML_"o.value='"_pidKey_"';"_$$$CRLF
		set strHTML=strHTML_"o.innerHTML='"_pstrName_"';"_$$$CRLF
		
		if (pidKey=$get(^CacheTempCombo(YUCI,YUSER,"Value")))&&(pidKey'="") { ;BR014916
			set strHTML=strHTML_"o.selected=true;"_$$$CRLF
		}
		set strHTML=strHTML_"s.appendChild(o);"_$$$CRLF
		if $length(strHTML)>32000 {
			write strHTML
			set strHTML=""
		}
		
		set ^CacheTempCombo(YUCI,YUSER,"Buffer")=strHTML ;BR014916
	}	
	quit
	
	
AddGroup(pstrName="")
    ;-------------------------------------------------------------------------------
    ; Adds unselectable group separators to combo boxes.
    ; Clears the buffer if too big.
    ;
    ; History:
    ; 24-Mar-2008	shobby	SRBR014916:	Included YUCI as a key on CacheTempCombo
    ; 22-Nov-2004	PaulK	Created
    ;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML = $get(^CacheTempCombo(YUCI,YUSER,"Buffer")) ;BR014916
	set strHTML = strHTML_"var g = document.createElement('optgroup');"_$$$CRLF
	set strHTML = strHTML_"g.label='"_pstrName_"';"_$$$CRLF
	set strHTML = strHTML_"s.appendChild(g);"_$$$CRLF
	
	if $length(strHTML)>32000 {
		write strHTML
		set strHTML = ""
	}
	set ^CacheTempCombo(YUCI,YUSER,"Buffer")=strHTML ;BR014916
	quit 
	
 
 
	/* UNFINISHED - update a combo field based on another field... get relations automatically
	 
		Would be great if this could be called automatically on blur for an fields based on another
	
UpdateCombo(pidForm,pidType,pidField)
	;-------------------------------------------------------------------------------
	; Update a combo list based on another field changing... very generic - look at field defs.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 04-Oct-2006	JW		SR15084: Created
	;-------------------------------------------------------------------------------
	new strGlobal,strQuery,idKey
	
	do StartCombo("document.WWW2.Y"_pidForm_pidType_pidField,"",$$$YES)
	
	 	Get relation details:
		 	GetValidationInformation^WWWFieldValidation
		
		Define keys:
			DefineKeys^WWWFieldValidation
		
	 
	
	set strGlobal = $$$GlobalString(pidClass,pstrRelKeys)
	$$$Query(strGlobal,strQuery)
		set idKey = $$$DEQUOTE($$$QueryANid(strQuery))
		
		//do AddOption^COMCombo(idKey,idKey_" - REL")		// encapsulate fn to get REL here
		
	$$$End
	
	do StopCombo^COMCombo()
	
	quit
	*/
]]></Routine>
</Export>