<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvLine
	;--------------------------------------------------------------------------------------
	; SR: 11837 - Created as a replacement for FINARRecurringInvoiceLine.mac
	;--------------------------------------------------------------------------------------
	#Include FIN
	#Include FINConst
	#Include COMConst
	#Include COMSYS
	#include WWWConst
 
OnAfterDataFields()
    ;-------------------------------------------------------------------------------
    ; Update tax fields
    ;
    ; Returns:
    ;
    ; History:
    ; 02-Nov-2006		Steve S	SR14243: Intercompany support
    ; 31-Mar-2005		JW		SR11937: Created
    ;-------------------------------------------------------------------------------
	new objInvoice
	
	set objInvoice = $$$GRIDYFELDContainer
 	do UpdateTaxFields^FINARInvTax(objInvoice)
 	
 	//SR14243
 	do ShowInterCompanyColumns^FINARInvIC(+$$$FINARInvInterCompany(objInvoice))
 	
	quit
 
 
OnBlur(pYFELD="")
    ;-------------------------------------------------------------------------------
    ; Calculate tax
    ; 
    ; Params: pYFELD - line data
    ;
    ; Returns: nil
    ;
    ; History:
    ; 31-Mar-2005	JW		SR11937: Cleaned up.
    ;  4-Mar-2005	JW		FC
	; 18-Feb-2005	JW		SR11667: Created
    ;-------------------------------------------------------------------------------
	new objInv
	
	set objInv = $$$GRIDYFELDContainer
	
	do CalculateTax(.pYFELD,objInv,$$$FINARInvLinesIncludeTax(objInv))
	
	quit
 
 
UpdateContainer(pidField,parrGrid,pHeadYFELD,pHeadYMFELD)
    ;-------------------------------------------------------------------------------
    ; Calculates Header Totals
    ;
    ; Returns:
    ;
    ; History:
	; 10-Apr-2010	GRF		SR17177: parrGrid rather than pobjGrid
    ; 30-Sep-2005	JW		SR11573: Param is now class field not form col
    ; 03-Jun-2005   SH/PK/SS/JW		Changed to FINARInvLine from FINAPInvCharge.
    ; 16-Mar-2005	JW		SR11937: Created (copied from FINAPInvCharge)
    ;-------------------------------------------------------------------------------
	new idFINARInv,idFINARInvLine,objFINARInvLine, total,tax
	
		// Update if not on blur, or if blurred from specified cells.
	if (pidField="") || ($listfind($lb($$$FldFINARInvLineTaxCode,$$$FldFINARInvLineAmount,$$$FldFINARInvLineAmountTaxEx),pidField)) {
	
		set total = 0
		set tax = 0
		
		// Sum values
		
		set idFINARInv = $order(parrGrid(""))
		if idFINARInv'="" {
			set idFINARInvLine = ""
			for {
				set idFINARInvLine = $order(parrGrid(idFINARInv,idFINARInvLine))
				quit:idFINARInvLine=""
				
				set objFINARInvLine = $get(parrGrid(idFINARInv,idFINARInvLine))
				
				set total = $$$FCAdd(total,$$$FINARInvLineAmountTaxEx(objFINARInvLine))
				set tax   = $$$FCAdd(tax,$$$FINARInvLineTax(objFINARInvLine))
			}
		}
		
		// Update Headers
		if $$$FINARInvLinesIncludeTax(pHeadYFELD) {
			set $$$FINARInvTax(pHeadYFELD) = tax
		} else {
			set tax = $$$FINARInvTax(pHeadYFELD)
		}
		
		set $piece(pHeadYMFELD,Y,22) = total
		set $piece(pHeadYMFELD,Y,23) = $$$FCAdd(tax,total)
	}
	
	quit
	
	
	;OnBlurAmount(pYFELD)
UpdateHeaders(lineTaxDiff=0) // No longer in use
    ;-------------------------------------------------------------------------------
    ; Update header fields
    ; 
    ; Params:	lineTaxDiff - difference between current line's prev & updated tax
    ; 
    ; Returns:
    ;
    ; History:
	; 10-Apr-2010	GRF		SR17177: arrGrid rather than objGrid; GRID Macros
    ; 31-Mar-2005	JW		Currently not in use.
    ; 04-Mar-2005	JW		FC
    ; 18-Feb-2005	JW		SR11667: Changed name. Added Tax.
    ; 16-Feb-2005	JW/SS	SR11742: Use cache addition instead of JS.
    ; 20-Jan-2005	SteveS	Created
    ;-------------------------------------------------------------------------------
    new arrGrid,idInvoice,idLine,objInvLine,subtotal,objInv,tax
	
	$$$GRIDGetContents(.arrGrid)
	set tax = lineTaxDiff
	set subtotal=0
	
	set idInvoice=$order(arrGrid(""))
	if idInvoice'="" {
		set idLine=""
		for {
			set idLine=$order(arrGrid(idInvoice,idLine))
			quit:idLine=""
			
			set objInvLine=$get(arrGrid(idInvoice,idLine))
			set subtotal = $$$FCAdd(subtotal,$$$FINARInvLineAmount(objInvLine))
			set tax = $$$FCAdd(tax,$$$FINARInvLineTax(objInvLine))
		}
	}
	
	do UpdateHeaderField^COMGridEdit31Interface("M22",subtotal)
	do UpdateHeaderField^COMGridEdit31Interface("D11",tax)
	do UpdateHeaderField^COMGridEdit31Interface("M23",$$$FCAdd(subtotal,tax))
	
	quit
 
 
LinesAreValid(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Checks that all invoice lines have the required data.
    ;
    ; Returns:
    ;
    ; History:
    ; 30-Mar-2005	Steve S		SR11937: Rolled back Tax Code check
    ; 21-Feb-2005	JW			SR11667: Added Tax Code. Cleaned up Status usage.
    ; 03-Feb-2005	Steve S		Created
    ;-------------------------------------------------------------------------------
	new idInvoice,strStatus,idLine,objInvoiceLine,fltLineAmount
	new idGLAccount,strDescription
	
	set idInvoice=$get(pidInvoice)
	set strStatus=$$$OK
	
	if '$$$NoKey(idInvoice) {
		set idLine=""
		for {
			set idLine=$order(^FINARInvLine(YM,idInvoice,idLine))
			quit:(idLine="")
			
			set objInvoiceLine =$get(^FINARInvLine(YM,idInvoice,idLine,1))
			set fltLineAmount  =$$$FINARInvLineAmountTaxEx(objInvoiceLine)
			set idGLAccount	   =$$$FINARInvLineGLAccount(objInvoiceLine)
			set strDescription =$$$FINARInvLineDescription(objInvoiceLine)
			
			if fltLineAmount="" {
				set strStatus=$listbuild("Fin00643")  ;All invoice lines must have an Amount.	
			} elseif idGLAccount="" {
				set strStatus=$listbuild("Fin00644")  ;All invoice lines require a GL Account.
			} elseif '$data(^FINGLAccount(YM,idGLAccount,1)) {
				set strStatus=$listbuild("Fin00681",idGLAccount,idLine)  ;GL Account '%1' is invalid on line %2.	
			} elseif strDescription="" {
				set strStatus=$listbuild("Fin00645")  ;All invoice lines must have a Description.	
			} elseif $$$FINARInvLineTaxCode(objInvoiceLine)="" {
				;set strStatus=$lb("Fin00711")  ;All invoice lines require a Tax Code.
			}
			
			quit:$$$ISERR(strStatus)
		}	
	}
 
	quit strStatus
	
	
CalculateTax(pobjLine="",pobjInvoice="",pblnIncTax=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Calculate the tax amount for a given line and customer.
    ; 
    ; Params:	pblnIncTax - whether to calculate from TaxInc or TaxEx amount
    ; 			pobjLine - line data (is updated with amounts)
    ; 			pobjInvoice - invoice data
    ;
    ; Returns: pobjLine as parameter
    ; 		   tax amount
    ; 
    ; History:
    ;  8-Jul-2005	JW		SR12939: Added '+'
    ; 13-May-2005	Steve S	SR11907: Return no tax if line has no tax code
    ;  8-Apr-2005	JW		SR12098: Get tax location from YFELD
    ; 31-Mar-2005	JW		SR11937: Replaced with copy from FINAPInvCharge
    ; 30-Mar-2005	Steve S	SR11937: Use idTaxLocation to compute tax
    ;  4-Mar-2005	JW		FC
    ; 18-Feb-2005	JW		SR11667: Created
    ;-------------------------------------------------------------------------------
	new itemTaxCode,customer,tax,location,transDate,taxRate,taxCode
	
	set itemTaxCode = $$$FINARInvLineTaxCode(pobjLine)
	set location = $$$FINARInvTaxLocation(pobjInvoice)
	
	set taxCode = $$IsTaxable^COMTAX(itemTaxCode,+$$$FINARInvTaxInvoice(pobjInvoice),,location)	//SR12939
	
	if (taxCode = 0) || (location = "") || (itemTaxCode="") {
		set tax = ""
	} else {
		set transDate = $$GetTransactionDate^FINARInv(pobjInvoice)
		set taxRate = $$CombinedTaxRate^COMTAX(taxCode,location,transDate)
		
		if 'pblnIncTax {
			set tax = $$$FCMult($$$FINARInvLineAmountTaxEx(pobjLine),taxRate/100)
		} else {
			if taxRate {
				set tax = $$$FCDivd($$$FINARInvLineAmount(pobjLine),(100/taxRate)+1)
			} else {
				set tax = 0
			}
		}
		set tax = $$$FCRound(tax)
	}
	
	if pblnIncTax {
		set $$$FINARInvLineAmountTaxEx(pobjLine) = $$$FCSub($$$FINARInvLineAmount(pobjLine),tax)
	} else {
		set $$$FINARInvLineAmount(pobjLine) = $$$FCAdd($$$FINARInvLineAmountTaxEx(pobjLine),tax)
	}
	set $$$FINARInvLineTax(pobjLine) = tax
	
	quit tax
	
	
 
GetDefaultTaxCode()
    ;-------------------------------------------------------------------------------
    ; Get the default Tax Code for an invoice line.
    ;
    ; Returns: a tax code
    ;
    ; History:
    ; 17-Mar-2005	Steve S		Created (SR11907)
    ;-------------------------------------------------------------------------------
 
	new strInvoiceData
	
	set strInvoiceData = $$$GRIDYFELDContainer
	quit $$$FINARInvDefaultTaxCode(strInvoiceData)
	
	
GetDefaultAccount()
    ;-------------------------------------------------------------------------------
    ; Get the default expense account for the invoice customer
    ;
    ; Returns:
    ;
    ; History:
    ; 27-Apr-2005	JW		Created SR12144
    ;-------------------------------------------------------------------------------
	new idAccount,idCustomer,strInvoiceData
	
	set idAccount = ""
	set strInvoiceData = $$$GRIDYFELDContainer
	
	set idCustomer = $$$FINARInvCustomer(strInvoiceData)
	if idCustomer'="" {
		set idAccount = $$$FINARCustomerDefaultExpenseAccount($get(^FINARCustomer(YM,idCustomer,1)))
	}
	
	quit idAccount
	
	
OnLineAdded(pintRowCount,pstrKey)
	;-------------------------------------------------------------------------------
    ; If this is the first line added lock the inter company field
    ; 
    ; Parameters:
    ; pintRowCount: The number of rows added
    ; pstrKey     : The key to the line added
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 02-Nov-2006	Steve S		SR14243: Created
	;-------------------------------------------------------------------------------
    if pintRowCount=1 {
	    do ToggleReadOnly^COMUtilForm($$$FldFINARInvInterCompany,$$$YES,"FINARInv")
    }
    
	quit
	
	
OnLineDeleted(pstrYKEY,pintRowCount)
	;-------------------------------------------------------------------------------
	; If this is the last line removed unlock the inter company field
	;
	; Params: pintRowCount	: The number of rows added
	; 		  pstrYKEY		: The YKEY
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 02-Nov-2006	Steve S		SR14243: Created
	;-------------------------------------------------------------------------------
	if pintRowCount=0 {
		do ToggleReadOnly^COMUtilForm($$$FldFINARInvInterCompany,$$$NO,"FINARInv")
	}
	
	quit
	
]]></Routine>
</Export>