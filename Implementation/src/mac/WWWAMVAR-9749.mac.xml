<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWAMVAR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWAMVAR ;WWWAMVAR;FIS;LOAD VARIABLES;26.11.2002  ; Compiled March 4, 2005 13:32:31
	;
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		LOAD VARIABLES
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Aug-2010	shobby	SR17488: call to SET^WWWUSERAGENT
	; 23-Nov-2005	GRF		SR13171 : Doco; dot alignment
	; 26.11.2002	FIS
	;-----------------------------------------------------------------------------*/
	
	SET YAMCANCEL = ""                    ;CANCEL INFORMATION
	SET YHYPER    = +$GET(YHYPER)
	
	;SYSTEM VARIALBE
	SET Y="~"
	SET YCR=$CHAR(13,10)
	;SET YBED="UNKNOWN"
	SET YUSER=""
	IF $GET(YAMUSER)'="" SET YUSER=YAMUSER
	SET YTIMEOUT=0
	
	/* Umlauts for Conversion         ;UMLAUTE FÜR UMSETZUNG
	;-------------------------------------*/
	DO
	. SET YUMLAU=""
	. NEW CHAR
	. SET CHAR=""
	. FOR  SET CHAR=$ORDER(^WWWUMLAU(0,CHAR)) QUIT:CHAR=""  SET YUMLAU=YUMLAU_CHAR_","
	. IF YUMLAU'="" SET YUMLAU=$EXTRACT(YUMLAU,1,$LENGTH(YUMLAU)-1)  ;OHNE , AM ENDE
	. QUIT
	
	IF YHYPER=0 DO WEBLINK   ;= WEBLINK
	IF YHYPER=1 DO CSP       ;= CSP
	;IF YHYPER=2             ;= FUTURE USE.....
	
	IF $GET(YAMUSER)'="" DO  QUIT  ;IF CALL BY WWWAMEVENT
	. DO
	. . ;SYSTEM VARIABLES (FIX)
	. . SET YUCI	   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YUCI"))
	. . SET YBED	   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YBED"))
	. . SET YAMAPPLIC  =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMAPPLIC"))
	. . SET SPRACHE	   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","SPRACHE"))
	. . SET YM		   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YM"))
	. . IF YM="" SET YM=0
	. . SET YAMURL	   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMURL"))
	. . SET YAMSYSURL  =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMSYSURL"))
	. . SET YAMGIF	   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMGIF"))
	. . SET YAMREMIP   =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMREMIP"))
	. . ;SR17488 SET YUSERAGENT =$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YUSERAGENT"))
	. . do SET^WWWUSERAGENT($GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YUSERAGENT"))) ;SR17488
	. . ;SR17488 IF YUSERAGENT="" SET YUSERAGENT="MSIE"
	. . ;
	. . ;CHECK LOGIN
	. . IF $GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YLOGINOK"))'=1 DO
	. . . IF YAMAPPLIC'="" SET YAMPAGE=$PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,5)  ;NO ACCESS PAGE
	. . . IF YAMPAGE="" IF YAMAPPLIC'="" SET YAMPAGE=$PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,2)  ;START PAGE ;take-off page 
	. . ;
	. . ;APPLICATION VARIABLES (VARIABLE)
	. . IF $GET(YAMPAGE)="" SET YAMPAGE=$GET(^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMPAGE"))
	. . IF YAMPAGE=""!(YAMAPPLIC="") SET YAMCANCEL="NO ACCESS"
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMPAGE")=YAMPAGE
	. . ;
	. . ; @NET MANAGER VARIABLES
	. . SET YFORM=YAMPAGE
	. . SET YGIF1=YAMGIF
	. . SET YGIF=$PIECE($GET(^WWW012(0,YM,1)),Y,47)
	. . SET YWHR=$PIECE($GET(^WWW012(0,YM,1)),Y,75)
	
	DO
	. ;NAMESPACE
	. SET YUCI=$GET(YUSI)
	. IF YUCI="" SET YUCI=$GET(YQUERY("YUCI"))  ;NAMESPACE
	. IF YUCI="" SET YUCI="INTRAPREND"
	. ;
	. ;COMPANY
	. SET YM=$GET(YM)
	. IF YM="" SET YM=$GET(YQUERY("YM"))
	. IF YM="" SET YM=0
	. ;
	. ;LANGUAGE
	. SET SPRACHE=$GET(YQUERY("SPRACHE"))
	. IF SPRACHE="" SET SPRACHE="EN"
	. ;IF SPRACHE="" SET SPRACHE="DE"
	. ;
	. ;LOGIN AS USER
	. SET YBED="UNKNOWN"
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,78)'="" SET YBED=$PIECE($GET(^INVORG(YM,YM,1)),Y,78)
	. ;
	. ;APPLICATION AND STARTPAGE
	. SET YAMAPPLIC=$GET(YQUERY("YAMAPPLIC"))  ;APPLICATION
	. IF YAMAPPLIC="" SET YAMCANCEL="application not defined"
	. IF YAMAPPLIC'="" DO
	. . IF '$DATA(^WWWAMDEFSYS(0,YAMAPPLIC)) SET YAMCANCEL="application does not exists"
	. . ;;;SET YAMSYSURL=$PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,11)  ;VORGABE FÜR URL
	. . SET YAMSYSURL=$PIECE($GET(^WWW012(0,YM,1)),Y,44)                 ;VORGABE FÜR URL ;default to URL
	. . IF $EXTRACT($$^WWWUPER(YAMSYSURL),1,4)'="HTTP" SET YAMSYSURL=""  ;FIS;04-Jul-2006
	. . IF YAMSYSURL'="" DO
	. . . IF $EXTRACT($REVERSE(YAMSYSURL))="/" SET YAMSYSURL=$REVERSE($EXTRACT($REVERSE(YAMSYSURL),2,99))
	. . .;IF YHYPER=1 SET YAMURL=YAMSYSURL_YAMURL ;table-mat 
	. . . SET YAMURL=YAMSYSURL_YAMURL
	. . . QUIT
	. . ;SET YAMGIF  = $PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,13)   ;WWW GIF SYSTEM
	. . SET YAMGIF  = $PIECE($GET(^WWW012(0,YM,1)),Y,48)               ;WWW GIF USER
	. . SET YGIF1   = YAMGIF 
	. . SET YGIF    = $PIECE($GET(^WWW012(0,YM,1)),Y,47)               ;WWW GIFSYSTEM
	. . ;SET YAMFILE = $PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,4)    ;WWW GIF SYSTEM
	. . SET YAMFILE = $PIECE($GET(^WWW012(0,YM,1)),Y,45)               ;FTP SYSTEM
	. . ;IF YAMFILE="" SET YAMFILE="temp"
	. . SET YWHR    = $PIECE($GET(^WWW012(0,YM,1)),Y,75)               ;STANDARD Currency   ;WÄHRUNGSKZ
	. ;
	. SET YAMERROR=$GET(YQUERY("YAMERROR"))  ;ERROR INFORMATION
	. ;SET YAMPAGE=$GET(YQUERY("YAMPAGE"))  ;PAGE
	. IF YAMERROR'="" QUIT
	. IF YAMCANCEL'="" QUIT
	. ;IF YAMPAGE="" SET YAMPAGE=$PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,2)  ;STARTPAGE
	. SET YAMPAGE=$PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,2)  ;ALWAYS STARTPAGE IF YAMUSER=""
	. IF YAMPAGE="" SET YAMCANCEL="page not defined" QUIT  ;NO PAGE
	. IF '$DATA(^WWWAMDEFPAGE(0,YAMPAGE)) SET YAMCANCEL="page does not exists" QUIT  ;NO PAGE
	. ;
	. ;EXPLORER INFO
	. ;SR17488 SET YUSERAGENT=$$^WWWUSERAGENT()
	. do SET^WWWUSERAGENT($$^WWWUSERAGENT())	;SR17488
	. ;
	. ;USER INFORMATION
	. ;SET YAMUSER=$GET(YQUERY("YAMUSER"))
	. SET YAMUSER=$GET(YUSER)
	. IF YAMUSER="" DO  ;INACTIVE
	. . IF '$DATA(^CacheTempWWWAM) set ^CacheTempWWWAM(0)=""
	. . ;LOCK +^CacheTempWWWAM:2 IF '$TEST SET YAMCANCEL="Unable to lock user table" QUIT
	. . ;SET YAMUSER=$ORDER(^CacheTempWWWAM(""),-1)+1
	. . ;LOCK -^CacheTempWWWAM
	. . LOCK +^WWWUSER(0):2
	. . FOR  SET YAMUSER=$RANDOM(10000000) QUIT:'$DATA(^WWWUSER(0,YAMUSER))
	. . SET ^WWWUSER(0,YAMUSER,1)=Y_YBED_Y_+$HOROLOG_Y_$PIECE($HOROLOG,",",2)
	. . SET $PIECE(^WWWUSER(0,YAMUSER,1),Y,35)=YUSERAGENT
	. . LOCK -^WWWUSER(0)
	. . SET YUSER=YAMUSER
	. . ;
	. . KILL ^CacheTempWWWAM(YAMUSER)
	. . SET ^CacheTempWWWAM(YAMUSER)						  = $$^WWWDATE($HOROLOG)_"/"_$$^WWWTIME($HOROLOG)
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMURL")	  = YAMURL
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMSYSURL")  = YAMSYSURL
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YUCI")		  = YUCI
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YM")		  = YM
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YBED")		  = YBED
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMAPPLIC")  = YAMAPPLIC
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","SPRACHE")	  = SPRACHE
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMPAGE")	  = YAMPAGE
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMREMIP")   = YAMREMIP
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMGIF")	  = YAMGIF
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMFILE")	  = YAMFILE
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAMERROR")   = YAMERROR
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YLOGINOK")   = 0
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YUSERAGENT") = YUSERAGENT
	. . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YTIMEOUT")   = YTIMEOUT
	. .;SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YLASTACTION") = $PIECE($HOROLOG,",",2)
	. . IF $GET(YAMPARA)'="" IF $FIND(YAMPARA,",") DO
	. . . SET ^CacheTempWWWAM(YAMUSER,"YAMCGI","YAUTOLOGIN")=$GET(YAMPARA)
	. . . SET YAMPARA=$PIECE(YAMPARA,",",1)
	. . . QUIT
	. . SET YUSER=YAMUSER
	. . SET YFORM=YAMPAGE
	. . QUIT
	. IF YAMCANCEL'="" QUIT
	. SET YQUERY("YAMUSER")=YAMUSER
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;SET ^WWWAMUSER(YM,+$HOROLOG,YAMUSER,1)=""
	. ;IF $ORDER(^WWWAMUSER(YM,+$HOROLOG-1),-1)'="" DO  ;KILL OLD DATA
	. . SET DAT=+$HOROLOG-1 FOR  SET DAT=$ORDER(^WWWAMUSER(YM,DAT),-1) QUIT:DAT=""  KILL ^(1)
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	
	QUIT
	;
	
CSP   ;INTERSYSTEMS CACHE CSP
	SET YQUERY=""
	SET YAMCGIVAR=""
	IF $GET(YUCI)'=""        SET YQUERY("YUCI")       = YUCI
	IF $GET(YM)'=""          SET YQUERY("YM")         = YM
	IF $GET(YXMLHTTPREQ)'="" SET YQUERY("XMLHTTPREQ") = YXMLHTTPREQ  ;TYBD;24,11,2004
	DO
	. NEW VAR
	. SET VAR=""
	. FOR  SET VAR=$ORDER(%request.Data(VAR)) QUIT:VAR=""  DO
	. . IF $GET(%request.Data(VAR))'=""    SET YQUERY(VAR)=$GET(%request.Data(VAR))   QUIT  ;NUR EIN TEIL ;only uni- part 
	. . IF $ORDER(%request.Data(VAR,1))="" SET YQUERY(VAR)=$GET(%request.Data(VAR,1)) QUIT  ;NUR EIN TEIL ;only uni- part 
	. . SET VAR(1)=""
	. . FOR  SET VAR(1)=$ORDER(%request.Data(VAR,VAR(1))) QUIT:VAR(1)=""  DO
	. . . SET YQUERY(VAR,VAR(1))=%request.Data(VAR,VAR(1))
	. ;
	. SET VAR=""
	. FOR  SET VAR=$ORDER(%request.CgiEnvs(VAR)) QUIT:VAR=""  DO
	. . IF $GET(%request.CgiEnvs(VAR))'=""    SET YAMCGIVAR(VAR)=$GET(%request.CgiEnvs(VAR))   QUIT  ;NUR EIN TEIL ;only uni- part 
	. . IF $ORDER(%request.CgiEnvs(VAR,1))="" SET YAMCGIVAR(VAR)=$GET(%request.CgiEnvs(VAR,1)) QUIT  ;NUR EIN TEIL ;only uni- part 
	. . SET VAR(1)=""
	. . FOR  SET VAR(1)=$ORDER(%request.CgiEnvs(VAR,VAR(1))) QUIT:VAR(1)=""  DO
	. . . SET YAMCGIVAR(VAR,VAR(1))=%request.CgiEnvs(VAR,VAR(1))
	
	IF $FIND($GET(YAMCGIVAR("HTTP_REFERER")),".htm") SET YAMCGIVAR("HTTP_REFERER")=$PIECE($PIECE($GET(YAMCGIVAR("HTTP_REFERER")),"?",1),".htm",1)_".cls"
	SET YAMREMIP=$$^WWWIP1($GET(YAMCGIVAR("REMOTE_ADDR")))
	
	SET YTIMEOUT=%session.AppTimeout
	IF YTIMEOUT<900 SET YTIMEOUT=900
	;SET YTIMEOUT=10
	
	SET YAMURL=""
	IF $GET(YQUERY("YUCI"))="" SET YQUERY("YUCI")="INTRAPREND"  ;DFLT. NAMESPACE
	IF $GET(YQUERY("YM"))=""   SET YQUERY("YM")=0               ;DFLT. CUSTOMER
	// FIXME<15532>: Can we removed the /csp/namespace component
	SET YAMURL="/csp/"_$TRANSLATE($GET(YQUERY("YUCI")),"ABCDEFGHIJKLMNOPQRSTUVWXYZÄÖÜ","abcdefghijklmnopqrstuvwxyzäöü")_"/wwwam.cls?"
	
	KILL %CGIEVAR
	MERGE %CGIEVAR=YAMCGIVAR
	QUIT
	
WEBLINK   ;INTERSYSTEMS CACHE WEBLINK
	;NEW MGWLPN,MGWLIB
	SET YQUERY=""
	MERGE YQUERY=%KEY
	
	SET YAMCGIVAR=""
	MERGE YAMCGIVAR=%CGIEVAR
	
	SET MGWLPN   = $GET(YQUERY("MGWLPN"))
	SET MGWLIB   = $GET(YQUERY("MGWLIB"))
	SET YAMREMIP = $$^WWWIP1($GET(YAMCGIVAR("REMOTE_ADDR")))
	IF $GET(YAMUSER)="" SET YAMUSER = $GET(YQUERY("YAMUSER"))
	SET YAMPAGE  = $GET(YQUERY("YAMPAGE"))
	
	SET YAMURL=""
	IF MGWLIB'="" IF MGWLPN'="" SET YAMURL=MGWLIB_"?MGWLPN="_MGWLPN_"&EP=WWWAMMASTER&"
	
	QUIT
	
PROPGET(USER,PROPNAME)
	NEW RESULT
	SET RESULT=""
	IF $GET(USER)'="" IF $GET(PROPNAME)'="" DO
	. SET RESULT=$GET(^CacheTempWWWAM(USER,"YAMCGI",PROPNAME))
	
	QUIT RESULT
	
PROPSET(USER,PROPNAME,VALUE)
	QUIT:$GET(USER)="" 0
	QUIT:$GET(PROPNAME)="" 0
	SET ^CacheTempWWWAM(USER,"YAMCGI",PROPNAME)=$get(VALUE)
	
	QUIT 1
	
VARGET(USER,PROPNAME)
	NEW RESULT
	
	SET RESULT=""
	IF $GET(USER)'="" IF $GET(PROPNAME)'="" DO
	. SET RESULT=$GET(^CacheTempWWWAM(USER,"YAMVAR",PROPNAME))
	
	QUIT RESULT
	
VARSET(USER,PROPNAME,VALUE)
	QUIT:$GET(USER)="" 0
	QUIT:$GET(PROPNAME)="" 0
	SET ^CacheTempWWWAM(USER,"YAMVAR",PROPNAME)=$get(VALUE)
	QUIT 1
	
]]></Routine>
</Export>