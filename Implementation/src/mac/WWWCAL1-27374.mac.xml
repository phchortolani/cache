<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWCAL1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWCAL1
	
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FEIERTAGSKALENDER / ANZEIGEN KALENDER INNERHALB EINES FORMULARS
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	;
	; 25.06.2002	FIS
	;-------------------------------------------------------------------------------
	NEW KALENDER,YI
 
	SET KALENDER = $PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1)),",",1)  ;AKTUELLER KALENDER ;calendar 
	SET YDATE    = $PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1)),",",2)  ;AKTUELLER TAG
	WRITE "<hr>"
	;LINK FÃœR NEU LADEN KALENDER ;to recent charge calendar 
	SET YI="<A HREF="_""""_YAKTION_"EP=WWWMANU&amp;YEXEC=D|GOTO^WWWCAL1()&amp;YFORM=WWWCAL1"
	DO VAR^WWWCGI
	SET YI=YI_""""_" TITLE="_""""_$$^WWWTEXT(33057)_""""_">"  ; "Edit Calendar"    ;KALENDER NEU LADEN ;calendar recent charge 
	IF KALENDER=""  DO FS^WWWTAB(YI_"<FONT COLOR=BLUE>"_$$^WWWTEXT(33057)_"</FONT></A>",2)  ;AUSWAHL ;Selection 
	IF KALENDER'="" DO FS^WWWTAB(YI_$PIECE($GET(^WWWKALENDER(0,KALENDER,1)),Y,1)_"</A>",2)
	WRITE "<TABLE BORDER=0 CELLSPACING=0 ALIGN=CENTER VALIGN=TOP>"
	WRITE "<TR><TD>"
	DO KALENDER
	WRITE "</TD></TR>"
	WRITE "</TABLE>"
	DO FSE^WWWTAB
	QUIT
	
	
GOTO(YDATE)   ;DATUM WECHSEL ;Date vicissitude 
	SET YDATE=$GET(YDATE)
	SET:+YDATE=0 YDATE=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1)),",",2)
	SET %(YQUERY,"YDATE")=YDATE
	SET:+YDATE=0 YDATE=+$HOROLOG
	SET %(YQUERY,"YAUSWAHL")=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1)),",",1)
	DO ^WWWFORM
	QUIT
	
	
KALENDER 
	;-------------------------------------------------------------------------------
	; Display calendar                  ANZEIGEN KALENDER
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Jun-2010	GRF		SR17146: call "DD.MM.YYYY" wrapper for WWWDATE1
	; 06-Aug-2007 	Frank	SRBR014598: Changing minimum year limit from 80 to 130
	; 							under today's year.
	; 16-May-2007	HeberB	BR014457: Month forward was subtracting; Change gif to
	; 							button; Reuse of WWWCAL2 code
	;-------------------------------------------------------------------------------
	NEW A,GO,TX,TAGEX,TAGXE,TAGXX
	
	SET GO = 0
	SET YDATE=$GET(YDATE)
	SET:+YDATE=0 YDATE=+$HOROLOG
	WRITE "<NOBR>"
	WRITE "<CENTER>"
	WRITE YCR,"<TABLE CELLSPACING=0 BORDER=0>"
	WRITE "<TR>"
	
	;---------------------------------------
	WRITE "<TD NOWRAP>"
	do START^WWWBUTTON(90)
	new YI,YTXT,YTIP
	set YI=YAKTION_"EP=WWWMANU&amp;YEXEC=D|GOTO^WWWCAL1("_(YDATE-$$^WWWMONTHDAYS(YDATE,-1))_")&amp;YFORM=WWWCAL1"
	set YTXT="-"_$$^WWWTEXT(16)                            ; "Month"
	do VAR^WWWCGI
	do BUTTON^WWWBUTTON($get(YTXT),YI,$$^WWWTEXT(33600))   ; "Month back"
	write YCR,"</TD>"
	
	;---------------------------------------
	write "<TD NOWRAP>"
	set YI=YAKTION_"EP=WWWMANU&amp;YEXEC=D|GOTO^WWWCAL1("_(YDATE+$$^WWWMONTHDAYS(YDATE,0))_")&amp;YFORM=WWWCAL1"
	set YTXT="+"_$$^WWWTEXT(16)                            ; "Month"
	do VAR^WWWCGI
	do BUTTON^WWWBUTTON($get(YTXT),YI,$$^WWWTEXT(33599))   ; "Month forward"
	do END^WWWBUTTON
	WRITE YCR,"</TD>"
	
	;---------------------------------------
	WRITE YCR,"<TD>"
	WRITE "<select NAME=MONAT onChange="_""""_"javascript:window.location='"_YAKTION_"EP=WWWMANU&amp;YEXEC=D|GOTO^WWWCAL1(' + document.WWW.MONAT.value + ')&amp;YFORM=WWWCAL1"
	DO ^WWWCGI
	WRITE "'"_""""_">"
	SET MONATS=""
	FOR  SET MONATS=$ORDER(^WWW101(0,"MONAT",SPRACHE,MONATS)) QUIT:MONATS=""  DO
	. WRITE YCR,"<option value="_$$DMY^WWWDATE1("01."_$EXTRACT(MONATS+100,2,3)_"."_$$^WWWYEAR(YDATE))  ; SR17146
	. IF $$^WWWMONTH(YDATE)=MONATS WRITE " SELECTED=SELECTED"
	. WRITE ">"_$PIECE($GET(^WWW101(0,"MONAT",SPRACHE,MONATS,1)),Y,1)_"</option>"
	WRITE YCR,"</select>"
	WRITE YCR,"</TD>"
	
	;---------------------------------------
	WRITE YCR,"<TD>"
	WRITE "<select NAME=JAHR onChange="_""""_"javascript:window.location='"_YAKTION_"EP=WWWMANU&amp;YEXEC=D|GOTO^WWWCAL1(' + document.WWW.JAHR.value + ')&amp;YFORM=WWWCAL1"
	DO ^WWWCGI
	WRITE "'"_""""_">"
	FOR JAHR=($$^WWWYEAR()-130):1:($$^WWWYEAR()+20) DO
	. WRITE YCR,"<option value="_$$DMY^WWWDATE1("01."_$EXTRACT(100+$$^WWWMONTH(YDATE),2,3)_"."_JAHR)  ; SR17146
	. IF $$^WWWYEAR(YDATE)=JAHR WRITE " SELECTED=SELECTED"
	. WRITE ">"_JAHR_"</option>"
	WRITE YCR,"</select>"
	WRITE YCR,"</TD>"
	
	;---------------------------------------
	WRITE YCR,"</TR>"
	WRITE YCR,"</TABLE>"
	
	DO
	. NEW DATE
	. SET DATE=$$^WWWDATE(YDATE)
	. SET YLFDAT="YWWWCAL1P2"
	. DO TERMIN1^WWWCAL2(DATE,1,1)
	
	WRITE "</CENTER>"
	QUIT
	
]]></Routine>
</Export>