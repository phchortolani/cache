<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRPAUF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRPAUF
	#include COMSYS
	#include INConst
 
LineCost(pidCompany,pidOrder,pidLine,pblnUnit=$$$NO,pblnFC=$$$NO)
	;-------------------------------------------------------------------------------
	; Retrieves the cost of an order line (uses the item terms)
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidOrder		: The order number
	; 			pidLine			: The order line
	; 			pblnUnit		: Whether to return the unit cost, or total cost
	;
	; ByRefs:
	;
	; Returns: fltClost
	;
	; History:
	; 17-Aug-2006	GRF		SR14856: Doco
	; 09-Aug-2006	FIS		SR14856: return Foreign Currency Costs
	; 05-Apr-2006	Steve S	SR14416: Created
	;-------------------------------------------------------------------------------
	new YM,fltClost,objINAUFPK
 
	set YM = $get(pidCompany)
	set fltClost=""
	
	if (YM'="") && ($get(pidOrder)'="") && ($get(pidLine)'="") {
		set objINAUFPK = $get(^INAUFPK(YM,pidOrder,pidLine,1))
		if 'pblnFC {                                              //09-Aug-2006:FIS:SR14856 add if block
			if pblnUnit {
				set fltClost = $$$INAUFPKUnitPrice(objINAUFPK)
			} else {
				set fltClost = $$$INAUFPKNetOriginalPrice(objINAUFPK)
			}
		} else {
			set fltClost=$$$INAUFPKCostFC(objINAUFPK)
			if pblnUnit set fltClost=$justify(fltClost/$$$INAUFPKPurchaseQuantity(objINAUFPK),0,2)
		}
	}
 
	quit fltClost
	
	
SupplierDetail(pidCompany,pidSupplier,pintField)
	;-------------------------------------------------------------------------------
	; Retrieve a piece of supplier detail
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidSupplier		: The supplier number
	; 			pintField		: The field number
	;
	; ByRefs:
	;
	; Returns: strData
	;
	; History:
	; 06-Apr-2006	Steve S	SR14416: Created
	;-------------------------------------------------------------------------------
	new YM,strData,objINLIEF,Y
 
	set YM = $get(pidCompany)
	set Y  = "~"
	set strData = ""
	
	if (YM'="") && ($get(pidSupplier)'="") {
		set objINLIEF = $get(^INLIEF(YM,pidSupplier,1))
		if +$get(pintField)'=0 {
			set strData = $piece(objINLIEF,Y,pintField)
		}
	}
 
	quit strData
	
	
CustomerDetail(pidCompany,pidCustomer,pintField)
	;-------------------------------------------------------------------------------
	; Retrieve a piece of customer detail
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidCustomer		: The customer number
	; 			pintField		: The field number
	;
	; ByRefs:
	;
	; Returns: strDetail
	;
	; History:
	; 12-Sep-2006	Steve S		Created
	;-------------------------------------------------------------------------------
	new YM,Y,strDetail,objINKUNDE
 
	set YM			=$get(pidCompany)
	set Y			="~"
	set strDetail	=""
 
	if (YM'="")&&($get(pidCustomer)'="") {
		set objINKUNDE=$get(^INKUNDE(YM,pidCustomer,1))
		if +$get(pintField)'=0 {
			set strDetail=$piece(objINKUNDE,Y,pintField)
		}
	}
 
	quit strDetail
	
	
SupplierBanking(pidCompany,pidSupplier,pblnNumber=$$$NO)
	;-------------------------------------------------------------------------------
	; Retrieve a supplier's banking detail
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidSupplier		: The supplier number
	; 			pblnNumber		: Whether to return the acc number, or just the name
	;
	; ByRefs:
	;
	; Returns: strBank	(the banking detail)
	;
	; History:
	; 06-Apr-2006	Steve S	SR14416: Created
	;-------------------------------------------------------------------------------
	new YM,strBank,objINLIEF,strAccount
 
	set YM=$get(pidCompany)
	
	set strBank=""
 
	if (YM'="") && ($get(pidSupplier)'="") {
		set objINLIEF  = $get(^INLIEF(YM,pidSupplier,1))
		set strAccount = $$$INLIEFCurrentAccount(objINLIEF)
		
		if strAccount=0 { 		;Acc 1
			if pblnNumber {
				set strBank = $$$INLIEFBankAccountNumber1(objINLIEF)		
			} else {
				set strBank = $$$INLIEFBankName1(objINLIEF)
			}		
		} elseif strAccount=1 { ;Acc 2
			if pblnNumber {
				set strBank = $$$INLIEFBankAccountNumber2(objINLIEF)
			} else { 
				set strBank = $$$INLIEFBankName2(objINLIEF)
			}
		}
	}
 
	quit strBank
]]></Routine>
</Export>