<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLJournalPrePost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
    ;-------------------------------------------------------------------------------
    ; FINGLJournalPrePost - Pre Posting information on journals.
    ;-------------------------------------------------------------------------------
	#include COMSYS
	#include FINConst

PrePost(pidJournal)
    ;-------------------------------------------------------------------------------
    ; Show List of account and amounts for a journal. Include Intercompany journals.
    ;
    ; History:
    ; 01-Jul-2005	shobby		Code Check
    ; 24-Jun-2005	Paul K		Created
    ;-------------------------------------------------------------------------------
	new lstHeader,idLine,objLine,arrJournals,idCompany,idAccount,blnInter,objCompany,objAccount
	new curAmount
	
	if pidJournal'="" {
		do GetJournalArray^FINGLJournalIC(pidJournal,.arrJournals)
		set blnInter=$data(arrJournals)
		set lstHeader=""
		set lstHeader=lstHeader_$listbuild($$$StrFINGLJournalLineAccount)
		set lstHeader=lstHeader_$listbuild($$$StrFINGLJournalLineReference)
		set lstHeader=lstHeader_$listbuild($$$StrFINGLJournalLineDebit)
		set lstHeader=lstHeader_$listbuild($$$StrFINGLJournalLineCredit)
		if blnInter {
			set lstHeader=lstHeader_$listbuild($$$Text("Fin00814"))  ;Account Source
		}
		
		do Start^COMTable(lstHeader,$$$Text("Fin00811"))  ;Journal Entries
		do NewLine^COMTable("white")
		do InsertCell^COMTable("<B>"_$$$Text("Fin00813")_"</B>",,,,,,$select(blnInter:5,1:4),3) ;Journal To Be Created Here.
		
		do EndLine^COMTable()
		set idLine = ""
		for {
			set idLine = $order(^FINGLJournalLine(YM,pidJournal,idLine))
			quit:idLine=""
			
			set objLine=$get(^FINGLJournalLine(YM,pidJournal,idLine,1))
			do NewLine^COMTable()
			do InsertCell^COMTable($$$FINGLJournalLineAccount(objLine)_" ("_$$$FINGLAccountDescription($get(^FINGLAccount(YM,$$$FINGLJournalLineAccount(objLine),1)))_")")
			do InsertCell^COMTable($$$FINGLJournalLineReference(objLine))
			do InsertCurrencyCell^COMTable($$$FINGLJournalLineDebit(objLine))
			do InsertCurrencyCell^COMTable($$$FINGLJournalLineCredit(objLine))
			if blnInter {
				do InsertCell^COMTable($select($$$FINGLJournalLineAccount(objLine)=$$$FINGLJournalLineCompanyAccount(objLine):$$$Text("Fin00815"),1:$$$Text("Fin00816")))  ;Manually Selected  ;Inter Company Setup
			}
			do EndLine^COMTable()
		}
		
		set idCompany=""
		for {
			set idCompany=$order(arrJournals(idCompany))
			quit:idCompany=""
			
			set objCompany=$get(^FINICSetup(YM,idCompany,1))
			do NewLine^COMTable("white")
			do InsertCell^COMTable("<B>"_$$$Text($listbuild("Fin00812",idCompany_" ("_$$$FINICSetupDescription(objCompany)_")"))_"</B>",,,,,,$select(blnInter:5,1:4),3) ;Journal to Be Created for Inter Company %1
			do EndLine^COMTable()
			set idLine=""
			for {
				set idLine=$order(arrJournals(idCompany,idLine))
				quit:idLine=""
				set idAccount=""
				for {
					set idAccount=$order(arrJournals(idCompany,idLine,idAccount))
					quit:idAccount=""
					
					set objAccount=$get(^FINICAccount(YM,idCompany,idAccount,1))
					do NewLine^COMTable()
					do InsertCell^COMTable(idAccount_" ("_$$$FINICAccountDescription(objAccount)_")")
					do InsertCell^COMTable($get(arrJournals(idCompany,idLine)))
					set curAmount=$get(arrJournals(idCompany,idLine,idAccount))
					do InsertCurrencyCell^COMTable($select(curAmount>0:curAmount,1:""))
					do InsertCurrencyCell^COMTable($select(curAmount<0:-curAmount,1:""))
					if blnInter {
						do InsertCell^COMTable($select(idAccount=$$$FINGLJournalLineCompanyAccount($get(^FINGLJournalLine(YM,pidJournal,idLine,1))):$$$Text("Fin00815"),1:$$$Text("Fin00816")))  ;Manually Selected  ;Inter Company Setup
					}
					do EndLine^COMTable()
				}				
			}
		}
		do Stop^COMTable()
	}
	quit

]]></Routine>
</Export>