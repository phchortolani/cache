<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMegaMenuFullMenu" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMegaMenuFullMenu
	#include WWWConst

Javascript()
	;-------------------------------------------------------------------------------
	; Javascript functions to support menu operation
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Apr-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	;
	&js<
	  function menuexpand() {
		  if (event.srcElement.tagName.toLowerCase()=='a') {
			  window.location=event.srcElement.href;
		  } else {
			  var strMenu=getParent(event.srcElement,'div');
			 // if (strMenu.id=='MM^@net_Manager^SM^01.01.') return false;
			  var obj=document.getElementById(strMenu.id+'^SM');
			  if (obj.innerHTML.replace(/\s/g, '')=='') {
				  var idSM=strMenu.id.split('^')[3];
				  if (idSM==undefined) idSM='';
				  var id=strMenu.id.split('^')[1];
				  var retval = EventValue('#(YUCI)#','#(YUSER)#','#(YFORM)#','FIX','EVENT^WWWMegaMenu','GETSUBMENU','6',id+":"+idSM);
		  		  obj.innerHTML=retval;
			  }
			  menuChangeState(strMenu.id);
			  setMenuHeight(8);
			  //if (e) {
				  //e.stopPropagation();
				  //e.preventDefault();
			  //} else {
				  event.cancelBubble=true;
				  event.returnValue=false;
			  //}
		  }
		  
	  }
	  function menuChangeState(pid) {
		  var img1=document.getElementById(pid+'IMG1');
		  var img2=document.getElementById(pid+'IMG2');
		  var obj=document.getElementById(pid+'^SM');
		  if (obj.innerHTML.replace(/ /g,'')!='') {
			  if (obj.style.display=='none') {
				  obj.style.display='block';
				  img1.src=img1.src.replace('plus','minus');
				  img2.src='#(YGIF)#oopen.gif';
			  	  var retval = EventValue('#(YUCI)#','#(YUSER)#','#(YFORM)#','FIX','EVENT^WWWMegaMenu','OPENMENU','6',pid);
			  } else {
				  obj.style.display='none';
				  img1.src=img1.src.replace('minus','plus');
				  img2.src='#(YGIF)#oclose.gif';
			  	  var retval = EventValue('#(YUCI)#','#(YUSER)#','#(YFORM)#','FIX','EVENT^WWWMegaMenu','CLOSEMENU','6',pid);
			  }
		  }
	  }
	  var TIMEOUTScroll;
	  function SetScrollPosition() {
		  window.clearTimeout(TIMEOUTScroll);
		  document.getElementById('menucontents_8').setAttribute('_ScrollTop',document.getElementById('menucontents_8').scrollTop);
		  TIMEOUTScroll=window.setTimeout(SetScrollPosition2,200);
	  }
	  function SetScrollPosition2() {
		  var retval = EventValue('#(YUCI)#','#(YUSER)#','#(YFORM)#','FIX','EVENT^WWWMegaMenu',"SETSCROLLPOSITION",'6',document.getElementById('menucontents_8').scrollTop); //CORE-112
	  }
	>
	quit

SetScrollPosition(pintPosition=0)
	;CORE-112
	set ^WWWMegaMenuUser(YUCI,YBED,"ScrollPosition")=pintPosition
	quit $$$YES
	
GetScrollPosition()
	;CORE-112
	quit +$get(^WWWMegaMenuUser(YUCI,YBED,"ScrollPosition"))

	
GetFullMenu()
	;-------------------------------------------------------------------------------
	; Full Alphalinc Menu
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Apr-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	;
	new HTML
	
	set HTML=""
	set HTML=HTML_"<li id='menuLi_8' class='li'  hclass='lihover' zclass='li' "_$$Events^WWWMegaMenu(1)_"><a id='menuA_8' href='#' class='drop'>Alphalinc</a>"
    set HTML=HTML_"  <div id='menuDiv_8' class='dropdown_1column' zclass='dropdown_1column' _CallBack='GETFULL_INTERNAL'>"
	set HTML=HTML_"  </div>"
	set HTML=HTML_"</li>"
	quit HTML
	

GetFullMenuInternal()
	;-------------------------------------------------------------------------------
	; Full Alphalinc Menu
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 31-Mar-2014	shobby		CORE-112: SetScrollPosition
	; 02-Jan-2014	shobby		CORE-302.2: onmousemove
	; 20-Jun-2013	shobby		CORE-113: Corrected width of Alphalinc menu in Firefox.
	; 01-May-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	;
	new HTML,idMenu1,idMenu2,objWWW004,strMenu,id,strMenuId,blnOpen,YKEY
	
	set HTML=""
    set HTML=HTML_"  <div id='menuDiv_8' onmousemove='menucloseClearTimeout();' style='width:"_$select(YUSERAGENT="MSIE":"140px",1:"auto")_"' class='dropdown_1column' zclass='dropdown_1column' _CallBack='GETFULL_INTERNAL'>"
    set HTML=HTML_"    <div>"
	set HTML=HTML_"      <div class='col_3'>"
	set HTML=HTML_"        <h2>Alphalinc Menu</h2>"
	set HTML=HTML_"      </div>"
	set HTML=HTML_"      <div id='menucontents_8' class='col_3' style='overflow-y:auto;' _ScrollTop="_$$GetScrollPosition()_" onscroll='SetScrollPosition();'>"
	set id="" for { set id=$order(^WWW004(0,id)) quit:id=""
		set strMenuId="MM^"_id
		set blnOpen=$data(^WWWMegaMenuUser(YUCI,YBED,"MenuState",strMenuId)) ;CORE-112
		
		set HTML=HTML_"    <div id='"_strMenuId_"' onclick='menuexpand();'>"
		set HTML=HTML_"      <p style='color:red; margin:0px; cursor:pointer;'>"
		set HTML=HTML_"        <img src='"_YGIF_$$GetImage(id,"",blnOpen)_"'   id='MM^"_id_"IMG1' style='vertical-align:bottom;'>"
		set HTML=HTML_"        <img class='menuimg' "
		if blnOpen {
			set HTML=HTML_"             src='"_YGIF_"oopen.gif' "
		} else {
			set HTML=HTML_"             src='"_YGIF_"oclose.gif' "
		}
		set HTML=HTML_"             id='MM^"_id_"IMG2' title=Main Menu align=top border=0 vspace=0>"
		set HTML=HTML_id
		set HTML=HTML_"      </p>"
		set HTML=HTML_"      <div id='MM^"_id_"^SM' "
		if blnOpen {
			set HTML=HTML_"style='display:block;'>"
			set HTML=HTML_$$GetSubMenu(id,"")
		} else {
			set HTML=HTML_"style='display:none;'>"
		}
		set HTML=HTML_"      </div>"
		set HTML=HTML_"    </div>"
	}

	set HTML=HTML_"      </div>"
	set HTML=HTML_"    </div>"
	set HTML=HTML_"  </div>"

	quit HTML 


GetSubMenu(pid,pidSubMenu)
	;-------------------------------------------------------------------------------
	; Get sub menus.  Not all submenus are loaded initially, only those that were
	; open from the previous time the form was loaded.  Other sub menus are loaded
	; as required.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Apr-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	;
	new HTML,id,objWWW004,intLevels,strImage,strMenuId,blnOpen,id1,arrUserMenu
	
	set HTML=""

	do GetMenu^WWWMenuOverview(YBED,pid,.arrUserMenu)
	set id=pidSubMenu for { set id=$order(^WWW004(0,pid,id)) quit:id=""
		set id1=id
		if $extract(id,$length(id))'="." set id1=id_"."
		quit:($extract(id1,1,$length(pidSubMenu))'=pidSubMenu)
		continue:($length(id1,".")>($length(pidSubMenu,".")+1))
		;continue:'$data(^WWW004(0,pid,$piece(id,".",$length(id,".")-1)_"."))
		set strMenuId="MM^"_pid_"^SM^"_id
		set blnOpen=$data(^WWWMegaMenuUser(YUCI,YBED,"MenuState",strMenuId)) ;CORE-112

		set HTML=HTML_"  <div id='"_strMenuId_"'>"
		set HTML=HTML_"    <p class='AlphalincP' >"
		if $order(^WWW004(0,pid))="" {
			for intLevels=1:1:$length(id1,".")-1 {
				set HTML=HTML_"  <img src='"_YGIF_"bplus.gif'>"
			}
		} else {
			for intLevels=1:1:$length(id1,".")-1 {
				set HTML=HTML_"  <img src='"_YGIF_"iplus.gif'>"
			}
		}
		set HTML=HTML_"      <img src='"_YGIF_$$GetImage(pid,id,blnOpen)_"' id='"_strMenuId_"IMG1'>"
		
		set strImage=$$GetCloseImage(pid,id,blnOpen)
		set HTML=HTML_"      <IMG class='menuimg' src='"_YGIF_strImage_"' id='"_strMenuId_"IMG2' TITLE=Main Menu>"
		set HTML=HTML_$$GetHREF(pid,id)
		set HTML=HTML_"    </p>"
		set HTML=HTML_"    <div id='MM^"_pid_"^SM^"_id_"^SM' "
		if blnOpen {
			set HTML=HTML_"style='display:block;'>"
			set HTML=HTML_$$GetSubMenu(pid,id)
		} else {
			set HTML=HTML_"style='display:none;'>"
		}
		set HTML=HTML_"    </div>"
		set HTML=HTML_"  </div>"
	}
	quit HTML


GetHREF(pid,pidSubMenu)
	;-------------------------------------------------------------------------------
	; GetHREF
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 26-Dec-2013	shobby		CORE-291.2: Don't UMLAU the YNAME.
	; 18-Dec-2013	shobby		CORE-291: YUSENAME
	; 06-May-2013	shobby		CORE-81.1: Pass in YPARA, YNAME and YANZ
	; 19-Apr-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	;
	new HTML,YA,YPARA,YANZ,YNAME,YUSENAME ;CORE-81.1 ;CORE-291
	new YASTART,YTRAKT ;CORE-108
	
	set YASTART=1 ;CORE-108 YLOCKKILL
	
	set YA=$get(^WWW004(0,pid,pidSubMenu,1))
	set YPARA= $$$WWW004TransferTriggerForForm(YA)_Y_ $$$WWW004AccessForChange(YA)	;CORE-81.1
	set YNAME= $$$WWW004MenuDescription(YA)							;CORE-81.1
	if +$$$WWW004UseMenuDescriptionForForm(YA) set YUSENAME=$$$WWW004UseMenuDescriptionForForm(YA)		;CORE-291
	;CORE-108set YANZ=pid_","_pidSubMenu											;CORE-81.1
	set YANZ=pid_","_$extract(pidSubMenu,1,$length(pidSubMenu,".")-1)_"."	    	;CORE-108
	
	set HTML=""
	if $$$WWW004FormName(YA)'="" {
		set HTML=HTML_"        <a class='menuitem' "
		if $$$WWW004StartFormWithSearchFuncti(YA) {
			set HTML=HTML_"HREF='"_YAKTION_"&EP=WWWSEAR&YFORM="_$$$WWW004FormName(YA)
		} else {
			set HTML=HTML_"HREF='"_YAKTION_"&EP=WWWFORM&YFORM="_$$$WWW004FormName(YA)
		}
		;set HTML=HTML_$$^WWWTRANSLATE($PIECE(YA,Y,12),"/YUCI/","/"_$GET(YUCI)_"/")_$$$WWW004FormName(YA)
		set HTML=HTML_$$WWWCGI2^WWWCGI()
		set HTML=HTML_"'>"
	}

	set HTML=HTML_$$$WWW004MenuDescription(($$Get^WWW004(pid,pidSubMenu)))

	if $$$WWW004FormName(YA)'="" {
		set HTML=HTML_"</a>"
	}
	
	;;set HTML=HTML_$$Get^WWW004(pid,pidSubMenu)
 	new YKEY
	set YKEY=pid_","_pidSubMenu
	set HTML=HTML_"<B style='cursor:default;'>&nbsp;&nbsp;&nbsp;&nbsp;</B>"
	set HTML=HTML_"<span onclick='window.location="" "
	set HTML=HTML_YAKTION_"&EP=WWWFORM&YFORM=WWW004&YBACK="_YBACK_YFORM_","
	set HTML=HTML_$$WWWCGI2^WWWCGI()
	set HTML=HTML_"""; return false; event.cancelBubble=true; event.returnValue=false;'>"
	set HTML=HTML_$zcvt("&#x25cf;","i","HTML") ;Black Circle
	set HTML=HTML_"</span>"
	quit HTML
	
GetImage(pid,pidSubMenu,blnOpen)
	quit $$GetPrefix(pid,pidSubMenu)_$select(blnOpen:"minus",1:"plus")_".gif"
		
GetPlusImage(pid,pidSubMenu)
	quit $$GetPrefix(pid,pidSubMenu)_"plus.gif"
	
GetPrefix(pid,pidSubMenu)
	;-------------------------------------------------------------------------------
	; Gets the prefix of the images used to build the trees
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Apr-2012	shobby		SR17998: Created
	;-------------------------------------------------------------------------------
	;
	new idNext,strImage,idLast
	
	set idNext=$order(^WWW004(0,pid,pidSubMenu))
	if pidSubMenu="" {
		if $order(^WWW004(0,pid))="" {
			set strImage="e"
		} else {
			set strImage=""
		}
	} else {
		if $extract(idNext,1,$length(pidSubMenu))'=pidSubMenu {
			if (idNext="")||($piece(idNext,".",$length(pidSubMenu,".")-2)'=$piece(pidSubMenu,".",$length(pidSubMenu,".")-2)) {
				set strImage="l"
			} else {
				set strImage="t"
			}
		} else {
			set idLast=$order(^WWW004(0,pid,""),-1)
			if $extract(idLast,1,$length(pidSubMenu))=pidSubMenu {
				set strImage="e"
			} else {
				set strImage=""
			}
		}
	}
	quit strImage

GetCloseImage(pid,pidSubMenu,pblnOpen)
	new idNext,strImage,objWWW004
	
	if pblnOpen {
		set strImage="oopen.gif"
	} else {
		set idNext=$order(^WWW004(0,pid,pidSubMenu))
		if $extract(idNext,1,$length(pidSubMenu))'=pidSubMenu {
			set objWWW004=$get(^WWW004(0,pid,pidSubMenu,1))
			set strImage=$$$WWW004ImageFilegif(objWWW004)
			if strImage="" set strImage="html.gif"
		} else {
			set strImage="oclose.gif"
		}
	}
	quit strImage

GetDescription(pid,pidSubMenu)
	quit $$$WWW004MenuDescription($get(^WWW004(0,pid,pidSubMenu,1)))

OpenMenu(pid)
	set ^WWWMegaMenuUser(YUCI,YBED,"MenuState",pid)=1 ;CORE-112
	quit 1

CloseMenu(pid)
	kill ^WWWMegaMenuUser(YUCI,YBED,"MenuState",pid)  ;CORE-112
	quit 1
]]></Routine>
</Export>