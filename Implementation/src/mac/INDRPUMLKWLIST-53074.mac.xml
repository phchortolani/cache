<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPUMLKWLIST" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include INConst
#include %occInclude

INDRPUMLKWLIST
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		DRUCKEN VERSANDART LISTE
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 30-May-2011	GRF		Naked References; Quits
	; 18.12.2001	FAN
	;-------------------------------------------------------------------------------
	new ANZM,YDDSATZ,YFELD,VERSNR,BET,TOUR1,BETV,TOUR,SORT
	
	set BET = $get(YLOCATION)
	if BET="" set BET = $order(^WWW0121(0,YM,""))
	do ^WWWBACK
	set ANZM=0
	do TH
	do TR
	quit
	
TH   ;heading 
	write "<BR><FONT SIZE=2><B>",YCR
	write $$^WWWTEXT(388)_": "_BET_" "_$piece($get(^WWW0121(0,YM,BET,1)),Y,1)  ; "Location"
	write "</FONT></B>"
	write YCR
	do ^WWWFRAME(0)  ;STARTEN DER ÃœBERSCHRIFT ;launching the superscription 
	write "<THEAD>"
	set YDDSATZ=0
	write YCR
	;kopf der anzeigetabelle ;who 
	
	new TEXT
	
	do    ;VERSAND ART ;shipping Type 
	. if $data(^WWW0021(0,"INDRPUMLKW",1,SPRACHE,1)) set $piece(TEXT,Y,1) = $piece(^WWW0021(0,"INDRPUMLKW",1,SPRACHE,1),Y,1) quit
	. set $piece(TEXT,Y,1) = $piece($get(^WWW002(0,"INDRPUMLKW",1,1)),Y,1)
	
	do    ;BEZEICHNUNG ;notation 
	. if $data(^WWW0031(0,"INDRPUMLKW",2,SPRACHE,1)) set $piece(TEXT,Y,2) = $piece(^WWW0031(0,"INDRPUMLKW",2,SPRACHE,1),Y,1) quit
	. set $piece(TEXT,Y,2) = $piece($get(^WWW003(0,"INDRPUMLKW",2,1)),Y,1)
	
	do    ;KENNZEICHEN ;characteristic
	. if $data(^WWW0031(0,"INDRPUMLKW",1,SPRACHE,1)) set $piece(TEXT,Y,3) = $piece(^WWW0031(0,"INDRPUMLKW",1,SPRACHE,1),Y,1) quit
	. set $piece(TEXT,Y,3) = $piece($get(^WWW003(0,"INDRPUMLKW",1,1)),Y,1)
	
	set $piece(TEXT,Y,4)=" "
	
	do    ; Storage
	. if $data(^WWW0031(0,"INDRPUMLKW",5,SPRACHE,1)) set $piece(TEXT,Y,5) = $piece(^WWW0031(0,"INDRPUMLKW",5,SPRACHE,1),Y,1) quit
	. set $piece(TEXT,Y,5) = $piece($get(^WWW003(0,"INDRPUMLKW",5,1)),Y,1)
	
	write "<TR>"
	for YI=1:1:5 do
	. write "<TH VALIGN=TOP NOWRAP VALIGN=TOP ALIGN=LEFT"
	. write " BGCOLOR="_YDARKGRAY
	. write ">"
	. write "<FONT SIZE=2>"
	. if TEXT'="" write $piece(TEXT,Y,YI)
	. write "&nbsp;"
	. write YCR,"</TH>"
	
	write YCR,"</TR>"
	write "</THEAD>"
	quit
	
TR ;
	set VERART=""
	for  set VERART=$order(^INDRPUMLKWs(YM,1,$$^WWWUMLAU(BET,1),VERART)) quit:VERART=""  do      ;NOCH KEINE PICKLIST GEDRUCKT ;yet no 
	. set VERART1=$get(^INDRPUMLKW(YM,VERART,1))
	. quit:VERART1=""
	. do ANZ
	
	if ANZM=0 do ZEILE
	do ^WWWFRAME(1)  ;TABLE AUS ;out of 
	quit
	
ZEILE ;LEERZEILE ;blank line
	new YI
	
	write "<TR>"
	write "<TD VALIGN=TOP NOWRAP ALIGN=LEFT"
	if YDDSATZ#2'=1 write " BGCOLOR="_YWHITE
	write ">"
	write "<FONT SIZE=2>"
	write $$^WWWTEXT(46)   ; "No Data Available"
	write "</TD>"
	write YCR
	for YI=2:1:5 do
	. write "<TD VALIGN=TOP NOWRAP ALIGN=LEFT"
	. if YDDSATZ#2'=1 write " BGCOLOR="_YWHITE
	. write ">"
	. write "<FONT SIZE=2>"
	. write "&nbsp;"
	. write "</TD>"
	. write YCR
	
	write "</TR>"
	quit
	
ANZ ;einzelne ANZs
	set ANZM=1+ANZM
	write YCR
	do NL^WWWTAB    
	do NF^WWWTAB
	write YCR,YCR,"<FONT SIZE=2>"         
	write VERART                     ;VERSANDART
	write "&nbsp;"
	write YCR,"</FONT>"
	
	do ZW^WWWTAB
	write YCR,"<FONT SIZE=2>"    ;BEZEICHNUNG ;notation 
	write $piece(VERART1,Y,2)
	write "&nbsp;"
	write YCR,"</FONT>"
	
	do ZW^WWWTAB
	write YCR,"<FONT SIZE=2>"    ;KENNZEICHEN ;characteristic 
	write $piece(VERART1,Y,1)
	write "&nbsp;"
	write YCR,"</FONT>"
	
	do ZW^WWWTAB
	write YCR,"<FONT SIZE=2>"    ;VERSANDNUMMER
	if $piece(VERART1,Y,5)'="" do
	. new YFORM
	. write "<A"
	. write " HREF="_""""
	. write YAKTION_"EP=WWWMANU&YEXEC=D|LKW^INBESTANZL1&YFORM=INBESTANZL&YPARA="_$piece(VERART1,Y,5)
	. do ^WWWCGI
	. write """"
	. write " TARGET=TEILEFRAME2"
	. write ">"
	. write "<img SRC="_""""_YGIF_"platz.gif"_""""_" "_YWIDTH_" "_YHEIGHT_" TITLE="_""""_$$^WWWTEXT(32031)_""""_"border=0></A>"   ;LAGERMENGE
	. write YCR
	
	write "&nbsp;"
	write YCR,"</FONT>"
	
	do ZW^WWWTAB
	write YCR,"<FONT SIZE=2>"   ;VERSANDART
	write $piece(VERART1,Y,5)_" "
	if $piece(VERART1,Y,5)'="" write $piece($get(^INLP(YM,BET,$piece(VERART1,Y,5),1)),Y,10)
	write "&nbsp;"
	write YCR,"</FONT>"
	do EL^WWWTAB
	quit


ListStorageInTransit(pidLocation,&parrInTransit)
	;-------------------------------------------------------------------------------
	; Get a list of all the INTRANSIT Storages defined for a given Location
	;
	; Inputs: 
	;	pidLocation
	;	
	; Returns as parameter:
	;	parrInTransit : array of Intransit Locations
	;
	; History:
	; 06-May-2011	PPP	SR17693: Created
	;-------------------------------------------------------------------------------
	new idMode,idStorage,idxLocn,objINDRPUMLKW,strStatus
	
	kill parrInTransit
	set strStatus = $$$OK
	
	set idxLocn = $$$Index(pidLocation)
	
	set idMode = ""	
	for {
		set idMode = $order(^INDRPUMLKWs(0,1,idxLocn,idMode))
		quit:idMode=""

		set objINDRPUMLKW = $get(^INDRPUMLKW(0,idMode,1))
		continue:$$$INDRPUMLKWLocation(objINDRPUMLKW)'=pidLocation
	
		set idStorage = $$$INDRPUMLKWStockLocation(objINDRPUMLKW)
		
		if idStorage'="" {
			if $data(^INLP(0,pidLocation,idStorage)) {
				set parrInTransit(idStorage)=""
			}
		}
	}
	quit strStatus
	
	
IsStorageInTransit(pidLocation, pidStorage)
	;-------------------------------------------------------------------------------
	; Is the Given Storage for a Location defined as an INTRANSIT Storage
	;
	; Inputs: 
	;	pidLocation
	;	pidStorage
	;	
	; Returns as parameter:
	;	Boolean
	;
	; History:
	; 06-May-2011	PPP	SR17693: Created
	;-------------------------------------------------------------------------------
	new arrStorage

	do ListStorageInTransit(pidLocation, .arrStorage)
	
	quit $data(arrStorage(pidStorage))
	
]]></Routine>
</Export>