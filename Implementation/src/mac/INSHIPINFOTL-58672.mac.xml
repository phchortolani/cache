<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSHIPINFOTL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include INConst
INSHIPINFOTL(blnPutAway=$$$NO)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN DER DATEN FÜR DIE TRANSPORTNUMMER UND ALLE ZUGEWIESENEN AUFTRÄGE
	;		INDICATE THE DATA FOR THE TRANSPORTATION NUMBER AND ALL ASSIGNED ORDERS
	; 
	; Called by:
	; 	Form INSHIPINFO
	; 		Button 4		($$$NO)  Print Container Manifest
	; 		Button 6		($$$YES) Print PutAway Manifest
	; 	DO ^INSHIPINFOTL    AUFRUF ÜBER BUTTON IN INSHIPINFO
	;
	; Inputs : 
	;	blnPutAway		swtich between Container or PutAway Manifest
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 28-Aug-2006	GRF		SR12027: Boolean Macro; Product Description; Doco
	; 11-Apr-2005	Steve S	SR12093 Print line items using a COMTable
	; 23-Mar-2005	RobertW	SR10969 Add a new cell for the foreign currency if there is one
	; 						Get the foreign currency and the specifier to display in the new cell
	; 30.03.2004	WEM
	;-------------------------------------------------------------------------------
	NEW ANZ,AUF,POS,LIEF,UES,GEWG,GEW,YI,HK
	
	SET YKEY=$GET(YKEY)
	DO ^WWWPRINT   ; AUSDRUCK DER INFORMATIONEN ZUR TRANSPORTNUMMER ;term the 
	do PrintLineItems(YKEY,blnPutAway) ;SR12093
	QUIT
	
	/*vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv Unused Code Start
	SET LIEF=""
	SET LIEF=$PIECE($GET(^INSHIPINFO(YM,YKEY,1)),Y,2)
	IF LIEF="" SET LIEF=" "
	SET ANZ=0
	SET GEW=0
	SET GEWG=0
	SET HK=0
	WRITE YCR
	WRITE "&nbsp;<b>"_$$^WWWTEXT(33816)_":</b>"  ;IMPORT NUMMER POSITION
	WRITE "<br>"
	WRITE YCR
	SET YDDSATZ=1
	DO START100^WWWTAB
	DO UEB
	NEW YAUF1,blnIsFC,strCurrency
	SET AUF=""
	FOR  SET AUF=$ORDER(^INAUFPAs(YM,2,$$^WWWUMLAU(YKEY,1),AUF)) QUIT:AUF=""  DO
	.  ;23-Mar-2005	RobertW		SR10969 Get the foreign currency and the specifier to display in the new cell
	. set YAUF1=$get(^INAUF(YM,AUF,1))
	. set blnIsFC=0
	. IF $PIECE(YAUF1,Y,2)=2 IF $PIECE(YAUF1,Y,51)'="" IF $PIECE(YAUF1,Y,51)'=YWHR SET blnIsFC=1 set strCurrency=$PIECE(YAUF1,Y,51)
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFPAs(YM,2,$$^WWWUMLAU(YKEY,1),AUF,POS)) QUIT:POS=""  DO
	. . DO ANZ^INSHIPINFOALL(2)
	
	;ANZ = Number of Shipment Line Items
	;
	IF ANZ>0 DO   ; GESAMTGEWICHT ;gross load weight 
	. DO NL^WWWTAB
	. ; 23-Mar-2005	RobertW		SR10969: If this is not putaway, there are 3 columns back, otherwise 2.
	. FOR YI=1:1:($LENGTH(UES,Y)-$select($get(blnPutAway):2,1:3))  DO
	. . WRITE "<TD BGCOLOR="_YDARKGRAY_">"
	. . WRITE "&nbsp;"
	. . DO EF^WWWTAB
	. ;
	. WRITE "<TD ALIGN=RIGHT BGCOLOR="_YDARKGRAY_">"
	. WRITE "<FONT SIZE=2>"
	. WRITE "&nbsp;"
	. if $get(blnPutAway)  do ;SR10951
	. . WRITE "</FONT>"
	. . DO EF^WWWTAB
	. . WRITE "<TD ALIGN=RIGHT BGCOLOR="_YDARKGRAY_">"
	. . WRITE "<FONT SIZE=2>"
	. . WRITE "&nbsp;"
	. ;
	. IF (+GEWG'=0) WRITE $$^WWWZAHL(GEWG,0,$LENGTH($PIECE(GEWG,".",2)))
	. WRITE "</FONT>"
	. DO EF^WWWTAB
	. if '$get(blnPutAway)  do ;SR10951
	. . WRITE "<TD ALIGN=RIGHT BGCOLOR="_YDARKGRAY_">"
	. . WRITE "<FONT SIZE=2>"
	. . WRITE "&nbsp;"
	. . IF (+HK'=0) WRITE $$^WWWZAHL(HK,0,2) 
	. . WRITE "</FONT>"
	. . DO EF^WWWTAB
	. . ; 23-Mar-2005		RobertW		SR10969 - Show the foreign currency total is there is some.
	. . WRITE "<TD ALIGN=RIGHT BGCOLOR="_YDARKGRAY_">"
	. . WRITE "<FONT SIZE=2>"
	. . WRITE "&nbsp;"
	. . if $get(blnIsFC) do
	. . .IF (+FCHK'=0) WRITE $$FormatCurrency^COMTable(FCHK,,strCurrency)
	. . WRITE "</FONT>"
	. . DO EF^WWWTAB
	. ;
	. DO EL^WWWTAB
	
	IF ANZ=0 DO LEER^WWWTAB(20,$LENGTH(UES,Y))
	DO STOP^WWWTAB
	QUIT
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ Unused Code End */
	
UEB
	;-------------------------------------------------------------------------------
	; ERSTELLEN DER ÜBERSCHRIFT ;the superscription
	; 
	; NOTE : This is called by the disabled code above - not known if any external calls
	; 
	; History:
	; 23-Mar-2005	RobertW	SR10969: Add the foreign currency for a container manifest.
	; 						Leaving the foreign currency column here even if it's
	; 						not a foreign currency, so that we can modify this later
	; 						for multiple currencies
	; 23-Mar-2005	Steve S	SR 10950 - Added Received Quantity to Header
	; 21-Mar-2005	Steve S	SR 10951 - If Putaway Manifest, exclude cost info
	;-------------------------------------------------------------------------------
	if $get(blnPutAway) {
		set UES=$$^WWWTEXT(32047)_Y_$$^WWWTEXT(216)_Y_$$^WWWFELDNAME("INART","P",1)_Y_$$^WWWFELDNAME("INAUFP","D",1)_Y_$$^WWWTEXT(34003)_Y_$$^WWWTEXT(34153)_Y_$$^WWWFELDNAME("INAUFP","D",40)_Y_$$^WWWFELDNAME("INAUFP","D",2)_Y_$$^WWWTEXT(32324)
	} else {
		set UES=$$^WWWTEXT(32047)_Y_$$^WWWTEXT(216)_Y_$$^WWWFELDNAME("INART","P",1)_Y_$$^WWWFELDNAME("INAUFP","D",1)_Y_$$^WWWTEXT(34003)_Y_$$^WWWTEXT(34153)_Y_$$^WWWFELDNAME("INAUFP","D",40)_Y_$$^WWWFELDNAME("INAUFP","D",2)_Y_$$^WWWTEXT(32324)_Y_$$^WWWFELDNAME("INAUFPK","D",12)
		set UES=UES_Y_$$^WWWFELDNAME("INAUFPK","D",68)
	}
	;                 AUFTRAG           POSITION               ARTIKELNUMMER               ARTIKELBEZEICHNUNG                   					MENGE                            MENGENEINHEIT                      EAN-CODE                   GEWICHT ;order 
	do HEADER^WWWTAB(UES,2)
	quit
	
	
	
OnAfterSave
	;-------------------------------------------------------------------------------
	; After saving the Import, go through all the associated lines and apply the 
	; Due Date field to them all as the Required Delivery Date (piece 19 of INAUFP)
	;
	; Called by:
	; 	Form INSHIPINFO  :  OnAfterSave
	; 
	; Inputs : 	YKEY, YFELD
	; 	
	; Returns: nothing
	;
	; History:
	; 30-Mar-2005	RobertW	Created SR10565
	;-------------------------------------------------------------------------------
	new dteDueDate,idOrder,idOrderLine,idShipment,objINAUFP
	
	set dteDueDate=$piece(YFELD,Y,9)
	set idShipment=$$$Index(YKEY)
	
	if dteDueDate'="" {
		set idOrder="" 
		for {
			set idOrder=$order(^INAUFPAs(YM,2,idShipment,idOrder)) 
			quit:idOrder=""
			
			set idOrderLine=""
			
			for {
				set idOrderLine=$order(^INAUFPAs(YM,2,idShipment,idOrder,idOrderLine))
				quit:idOrderLine=""
				
				set objINAUFP=$get(^INAUFP(YM,idOrder,idOrderLine,1))
				if objINAUFP'="" {
					set $$$INAUFPRequiredDeliveryDate(objINAUFP)=dteDueDate
					do Save^COMUtils("INAUFP",idOrder_","_idOrderLine,objINAUFP,1)
				}
			}
		}
	}
	
	quit
	
	
	
PrintLineItems(pidImport="",pblnPutAway=$$$NO)
	;-------------------------------------------------------------------------------
	; Print assigned line items on import container manifest
	;
	; Returns:
	;
	; History:
	; 28-Aug-2006	GRF		SR12027: Supplier No replaces Product Description; shorter
	;                       variable names; remove hard-coded "test" EAN-Code left in place.
	; 10-Apr-2006 	SC		SR14500: Wrap fields on manifest.
	; 03-Apr-2006	SC		SR14500: Add Product Description to COMTable. 
	; 18-Jan-2006	Steve S	SR14179: Don't display a total qty value
	; 13-Jan-2006	Steve S	SR14165: Only one currency allowed, only keep one total
	; 10-Jan-2006	Steve S	SR14165: Compute FC Properly
	; 28-Jul-2005	RPW 	All Below: Fixed the very first check of INAUFPAs, this
	; 						would fail for alphanumeric with lower case. Made the
	; 						location left justified.
	; 20-Jul-2005	Steve S	SR12551: Show default bin loc's
	; 18-May-2005 	Steve S	SR12378: Move EAN next to Item Number; Remove Rec Qty;
	; 								 Make linespacing 3X; Add Bin Location
	; 17-May-2005	Steve S	SR12345: Add Bin Location to putaway manifest
	; 								 Add blank lines to putaway manifest
	; 						SR12377: Move EAN next to Item Number
	; 								 Delete Item Name/Rec Qty
	; 11-Apr-2005	Steve S	Created  SR:12093
	;-------------------------------------------------------------------------------
	new arrTotals,fltAmount,fltFCAmt,fltLocalAmt,fltRate,fltRecQty,fltRunningRecQty,fltTotal
	new idConfirmation,idCurrency,idLine,idOrder,idSupplier,idx,idxImport
	new lstHeader,lstStockLocns,objImport,objOrderLine,objRecdLine,objSupConds,objSupLine
	new strBinOutput,strBreakString,strCosts,strSupProduct
	//new fltRunningOrderQty  ;SR14179
	
	; TODO: Should really be using a JReport for this, not a COMTable, as the table
	; 		will grow quite large for large imports.
	
	set strBreakString=""
	if pblnPutAway set strBreakString="<BR>&nbsp;<BR>&nbsp;<BR>&nbsp;"
	
	set idxImport=$$$Index(pidImport)
	
	;if (pidImport'="") && ($data(^INAUFPAs(YM,2,pidImport))) { ; 28-Jul-2005 RPW All SR's listed above!
	if (pidImport'="") && ($data(^INAUFPAs(YM,2,idxImport))) {
		set objImport=$get(^INSHIPINFO(YM,pidImport,1))
		
		set lstHeader=""
		set lstHeader=lstHeader_$lb($$$Text(33441)) 						;Order Item
		set lstHeader=lstHeader_$lb($$$StrINARTItemNumber)					;Item
		set lstHeader=lstHeader_$lb($$$StrINAUFPEANCode) 					;EAN-Code
		if pblnPutAway set lstHeader=lstHeader_$lb($$$StrINAUFPItemName)	;Item Name
		set lstHeader=lstHeader_$lb($$$Text("IN00312"))						;Order Qty
	
		if 'pblnPutAway {
			set lstHeader=lstHeader_$lb($$$Text(32993))					;Price
		}
		
		if pblnPutAway {
			set lstHeader=lstHeader_$lb($$$StrINWEAUFStockLocation)	;Stock Location
		}
	
		;Import Shipment Line Items
		do Start^COMTable(lstHeader,$$$Text(33816),,,,0) 					 
			
		set idOrder  = ""
		set idLine   = ""
		set fltTotal = 0
		//set fltRunningOrderQty=0 ;SR14179
		set fltRunningRecQty=0
			
		for {
			set idOrder=$order(^INAUFPAs(YM,2,idxImport,idOrder))
			quit:idOrder=""
			
			set idCurrency=$$$INAUFOrderCurrency($get(^INAUF(YM,idOrder,1)))
			if idCurrency="" set idCurrency=YWHR
				
			set idLine=""
			for {
				set idLine=$order(^INAUFPAs(YM,2,idxImport,idOrder,idLine))
				quit:idLine=""
				
				set strBinOutput=""	
				set idSupplier=$order(^INAUFPAs(YM,2,idxImport,idOrder,idLine,""))
				set idConfirmation=$order(^INAUFPAs(YM,2,idxImport,idOrder,idLine,idSupplier,""))
					
				set objRecdLine  = $get(^INAUFPA(YM,idOrder,idLine,idSupplier,idConfirmation,1))    ; FIXME : not used <GRF>
				set objOrderLine = $get(^INAUFP(YM,idOrder,idLine,1))
				set objSupLine   = $get(^INAUFPK(YM,idOrder,idLine,1))
				set objSupConds  = $get(^INARTK(YM,$$$INAUFPItemNumber(objOrderLine),idSupplier,1))
 
				do NewLine^COMTable()
				do InsertCell^COMTable(idOrder_"-"_idLine_strBreakString,,,,,,,1) ;Order Item	
				
				; Item Number
				;---------------------------
				;SR14500
			;	do InsertCell^COMTable($$$INAUFPItemNumber(objOrderLine),,,,,,,1)
			;	set strSupProduct = $$$INARTKProductDescription(objSupConds)   ; SR12027
				set strSupProduct = $$$INARTKSupplierItemNo(objSupConds)
				if strSupProduct '= "" {
					do InsertCell^COMTable($$$INAUFPItemNumber(objOrderLine)_"<br> ("_$piece(strSupProduct,"|",1)_")",,,,,,,1,,$$$YES)
				} else {
					do InsertCell^COMTable($$$INAUFPItemNumber(objOrderLine),,,,,,,1)
				}
				
				; EAN-code/Item Name/Order Qty   ;SR14500
				;---------------------------
				do InsertCell^COMTable($$$INAUFPEANCode(objOrderLine),,,,"Right",,,1,,$$$YES)
				if pblnPutAway do InsertCell^COMTable($$$INAUFPItemName(objOrderLine),,,,,,,1,,$$$YES)
				do InsertCell^COMTable($$$INAUFPQuantity(objOrderLine)_" "_$$$AppEnum("EINHEIT",$$$INAUFPQuantityUnit(objOrderLine)),,,,"Right",,,1,,$$$YES)
			; FIXME : Verify don't want INAUFP D449 instead of D40
				//set fltRunningOrderQty=fltRunningOrderQty+$$$INAUFPQuantity(objOrderLine) ;SR14179
				set fltRecQty=$$GetReceivedQuantity^INSHIPINFOALL(idOrder,idLine)
				set fltRunningRecQty=fltRunningRecQty+fltRecQty
				
				;SR14165
				if 'pblnPutAway {
					if (idCurrency=YWHR) { ;Local
						set fltAmount=$$$INAUFPKNetOriginalPrice(objSupLine)
					} else { ;FC
						set fltAmount=$$$INAUFPKPurchaseQuantity(objSupLine)*$$$INAUFPKCostFC(objSupLine)
					}
					
					set strCosts=$$FormatCurrency^COMTable(fltAmount,$$$NO,idCurrency)
					set fltTotal=fltTotal+fltAmount
					do InsertCell^COMTable(strCosts,,,,"Right",,,1)
					
				} else {
					set lstStockLocns = $$GetOrderLineBin(pidImport,idOrder,idLine)
					for idx=1:1:$ll(lstStockLocns) {
						set strBinOutput=strBinOutput_$lg(lstStockLocns,idx)_"<BR>&nbsp;"
					}
			
					;SR12551
					if strBinOutput="" set strBinOutput=$$DefaultBinLoc($$$INAUFPItemNumber(objOrderLine),$$$INSHIPINFODestinationLocation(objImport))
					do InsertCell^COMTable(strBinOutput,,,,"Left",,,1,,$$$YES) ;SR14500
				}
					
				do EndLine^COMTable()
			}
		}	
		
		; ****************** FOOTER OF TABLE ******************
			
		do NewLine^COMTable("DarkGray")
		do InsertCell^COMTable("") 
		do InsertCell^COMTable("")
		do InsertCell^COMTable("")
		
		if pblnPutAway do InsertCell^COMTable("")
		
		do InsertCell^COMTable("") ;SR14179
		;do InsertCell^COMTable(fltRunningOrderQty,,,,"Right",,,1) ;SR14179
	
		if 'pblnPutAway {
			do InsertCell^COMTable($$FormatCurrency^COMTable(fltTotal,$$$NO,idCurrency),,,,"Right",,,1)
		} else {
			do InsertCell^COMTable("")
		}	
			
		do EndLine^COMTable()
		
		; **************** END FOOTER OF TABLE ****************
		
		do Stop^COMTable()	
	}
	
	quit
	
	;SR14165: Obsolete print pricing code
	;VVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVV
	
	/*
	if (+$$$INAUFPKCostFC(objSupLine)=0) {
		set fltLocalAmt=$$$INAUFPKNetOriginalPrice(objSupLine)
		set strCosts=$$FormatCurrency^COMTable(fltLocalAmt,,idCurrency)
		set fltTotalLocalAmt=fltTotalLocalAmt+fltLocalAmt
	} else {
		set fltFCAmt=
		set strCosts=$$FormatCurrency^COMTable(fltFCAmt,,idCurrency)
		set fltTotalFCAmt=fltTotalFCAmt+fltFCAmt
	}
	
	if $$$INAUFPKCostFC(objSupLine)'="" { ;FC Exists, use this
		set idCurrency=$$$INAUFOrderCurrency($get(^INAUF(YM,idOrder,1)))
		set fltFCAmt=$$$INAUFPKCostFC(objSupLine)*$$$INAUFPQuantity(objOrderLine)
		set strCosts=$$FormatCurrency^COMTable(fltFCAmt,,idCurrency)
		set fltTotalFCAmt=fltTotalFCAmt+fltFCAmt
	} else { ;No FC, use local instead
		set fltLocalAmt=$$$INAUFPKUnitPrice(objSupLine)*$$$INAUFPQuantity(objOrderLine)
		set strCosts=$$FormatCurrency^COMTable(fltLocalAmt)
		set fltTotalLocalAmt=fltTotalLocalAmt+fltLocalAmt
	}
	*/
	
DefaultBinLoc(pidItem="",pidLocation="")
	;-------------------------------------------------------------------------------
	; Get default bin location(s) for an import line when it hasn't been
	; received yet
	;
	; Returns: string
	;
	; History:
	; 20-Jul-2005	Steve S	SR12551: Created
	;-------------------------------------------------------------------------------
	new strOutput,idStockLoc,objBinLocation,strBinDesc
 
	set strOutput=""
	
	if (pidItem'="")&&(pidLocation'="") {
		set idStockLoc=""
		for {
			set idStockLoc=$order(^INARTLP(YM,pidItem,pidLocation,idStockLoc))
			quit:idStockLoc=""
			
			set objBinLocation=$get(^INLP(YM,pidLocation,idStockLoc,1))
			set strBinDesc=$$$INLPRemarks(objBinLocation)
			
			if strBinDesc'="" {
				set strOutput=strOutput_idStockLoc_" ("_strBinDesc_")<BR>"
			} else {
				set strOutput=strOutput_idStockLoc_"<BR>"
			}
		}
		
		if strOutput="" set strOutput=$$$Text($lb("IN00356",pidItem)) ;No default stock locations defined for item `%1`
	}
 
	quit strOutput
	
	
GetOrderLineBin(pidImport,pidOrder,pidLine)
	;-------------------------------------------------------------------------------
	; For an order line, return a list of bin locations where it has been received.
	;
	; Returns:
	;
	; History:
	; 17-May-2005	Steve S		Created SR:12345
	;-------------------------------------------------------------------------------
	
	new idBinLocation,objImport,idDestLoc,lstBinLocation,objBinLocation,strBinDesc
	
	set lstBinLocation=""
	
	if ($get(pidImport)'="")&&($get(pidOrder)'="")&&($get(pidLine)'="") {
		set objImport=$get(^INSHIPINFO(YM,pidImport,1))
		
		set idDestLoc=$$$INSHIPINFODestinationLocation(objImport)
		if idDestLoc="" set idDestLoc=YLOCATION
		
		set idBinLocation=""
		for {
			set idBinLocation=$order(^INWEAUF(YM,pidOrder,pidLine,idDestLoc,idBinLocation))
			quit:idBinLocation=""
			
			set objBinLocation=$get(^INLP(YM,idDestLoc,idBinLocation,1))
			set strBinDesc=$$$INLPRemarks(objBinLocation)
			
			if strBinDesc'="" {
				set lstBinLocation=lstBinLocation_$lb(idBinLocation_" ("_strBinDesc_")")
			} else {
				set lstBinLocation=lstBinLocation_$lb(idBinLocation)
			}
		}	
	}
	
	quit lstBinLocation
]]></Routine>
</Export>