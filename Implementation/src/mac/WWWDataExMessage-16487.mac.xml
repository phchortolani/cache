<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWDataExMessage" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWDataExMessage
	
#include COMSYS
#include WWWConst
	
OnAfterDataFields
	;-------------------------------------------------------------------------------
	; Code to check that the Primary Key Property is available as Message Property
	;
	; History:
	; 29-Apr-2008	CHINMAY
	;-------------------------------------------------------------------------------
	new strSource,strKeyProp,strKeyAllProps,blnOK,intCount,strMessage
	
	set strMessage = $get(YKEY)
	set strSource = $piece($get(YFELD),Y,2)
	if (strSource'="") && (strMessage'="") {
		set strKeyAllProps = ""
		set strKeyProp="" for {
			set strKeyProp=$order(^WWW002(0,strSource,strKeyProp))
			quit:strKeyProp=""
			set strKeyAllProps = strKeyAllProps_$$$WWW002PropertyName($get(^WWW002(0,strSource,strKeyProp,1)))_";" 
		}
		set blnOK=1
		for intCount=1:1:$length(strKeyAllProps,";")-1 {
			if '$data(^WWWDataExMessageProps(0,1,$$$Index($piece(strKeyAllProps,";",intCount)),strMessage)) set blnOK=0
		}
		if blnOK'=1 {
			write "<b><font color="_YRED_">Attention</font></b><br>" //FIXME: use language texta
		}
	}
	quit
	
	
OnAfterSaveDataRecords
 	;-------------------------------------------------------------------------------
	; Auto populate the Message Properties class with primary key records from the Data Class
	;
	; History:
	; 29-Apr-2008	CHINMAY
	;-------------------------------------------------------------------------------
	new idPrimaryKey,objPrimaryKey,strDataClass
	new objMsgProp,strStatus
 
	//if the data class field is not empty
	if ($piece(YFELD,Y,2)'="") {
		set objMsgProp=""
		set $$$WWWDataExMessagePropSourcePropertyName(objMsgProp) = YKEY
		set $$$WWWDataExMessagePropSourceClass(objMsgProp)        = $piece(YFELD,Y,2)
		set $$$WWWDataExMessagePropTargetPropertyName(objMsgProp) = YKEY
	
		set strStatus = $$$Save("WWWDataExMessageProp",YKEY_","_"0",objMsgProp,$$$YES)
		if (strStatus=1) {
			set count=1
			set idPrimaryKey="" for {
				set idPrimaryKey = $order(^WWW002(0,$piece(YFELD,Y,2),idPrimaryKey))
				quit:idPrimaryKey=""
				
				set objPrimaryKey = $get(^WWW002(0,$piece(YFELD,Y,2),idPrimaryKey,1))
				set objMsgProp    = ""
				set $$$WWWDataExMessagePropSourcePropertyName(objMsgProp) = $$$WWW002PropertyName(objPrimaryKey)
				set $$$WWWDataExMessagePropSourceClass(objMsgProp)        = $piece(YFELD,Y,2)
				set $$$WWWDataExMessagePropTargetPropertyName(objMsgProp) = $$$WWW002PropertyName(objPrimaryKey)
			
				set strStatus = $$$Save("WWWDataExMessageProp",YKEY_","_count,objMsgProp)
				set count=count+1
			}
		}
	}
	quit 
	
]]></Routine>
</Export>