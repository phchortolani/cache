<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31Globals" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31Globals
	
#include WWWConst
#include COMGridEdit31
	
#define LogR(%1,%2)	;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMGridEdit31Globals("_%2_") : "_$zh $$$JournalOn
	
REFERENCECLASS(YFORM="") 
    ;-------------------------------------------------------------------------------
    ; Gets (and sets a global of) Class of form YFORM
    ; 
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 18-Oct-2005	JW		SR11573: Called from macro $$$GRIDCLASS
    ;-------------------------------------------------------------------------------
	new YSATZ,YDATEI
	
	$$$LogR("REFERENCECLASS",pYFORM)
	
	set YDATEI = ""
	if YFORM'="" {
		set YSATZ  = $get(^WWW120(0,YFORM,1))
		set YDATEI = $$$WWW120ClassUsedInForm(YSATZ)
		set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1) = YDATEI
	}
	quit YDATEI
	
	
GetYVOR(pstrName)
    ;-------------------------------------------------------------------------------
    ; Gets the value of YVOR for the grid's class.  Can't use the global when
    ; multiple forms on the screen at the same time.
    ;
    ; Returns:
    ;
    ; History:
    ; 18-Oct-2005	JW		SR11573: Called from macro $$$GRIDYVOR
    ; 16-Mar-2005	shobby	Support multiple form names to allow additional charge
    ; 							lines to mix with FINAPInvLines.
    ; 02-Feb-2005	shobby	CodeCheck.
    ; 25-Jan-2005	RobertW	Only use 1 call to ^CacheTemp to get YVOR, not 2
    ; 21-Jan-2005	JW		Temporarily store YVOR
    ; 25-Nov-2004	Shobby	Created (SR10468)
    ;-------------------------------------------------------------------------------
	new YFORM,YVOR,YVOR1,YFIXHEADER,YI,strYVOR  ;Scope variables before call to ^WWWFORMX
	
	$$$LogR("GetYVOR",pstrName)
	
	if $get(pstrName)=""  set pstrName = $get(^CacheTemp(YUSER,"Grid","Name"))
	
	set YFORM = pstrName
	do ^WWWFORMX
	set ^CacheTemp(YUSER,"Grid",pstrName,"YVOR") = $get(YVOR)
	quit $get(YVOR)
	
	
KILLEDRECORDGet(YFORM,YROW)
	$$$LogR("KILLEDRECORDGet",YFORM_","_YROW)
	
	quit:$get(YROW)="" ""
	quit $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",$get(YROW)))
	
	
KILLEDRECORDSet(YFORM,YROW)
	$$$LogR("KILLEDRECORDSet",YFORM_","_YROW)
	
	quit:$get(YROW)=""
	set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW) = $$$YES
	quit
	
	
KILLEDRECORDKill(YFORM,YROW)
	$$$LogR("KILLEDRECORDKill",YFORM_","_YROW)
	
	quit:$get(YROW)=""
	kill ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)
	quit
	
	
RowCount(YFORM,pidKey) 
	;-------------------------------------------------------------------------------
	; Called from GRIDGetRowCount macro (COMGridEdit31Interface)
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 19-Oct-2005	JW		SR13659: Created
	;-------------------------------------------------------------------------------
	new intCount
	
	$$$LogR("RowCount",YFORM_";"_pidKey)
	
	set intCount = 0
	
	if ($get(pidKey)="") || ($get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY"))=pidKey) {
		set intCount = +$get($$$GRIDRowCount)
	} else {
		set intCount = $$DiskCount^COMGridEdit31Globals(YFORM,pidKey)
	}
	quit intCount
	
	
DiskCount(YFORM,YKEY)
    ;-------------------------------------------------------------------------------
    ; Count how many records are currently on disk.
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 12-Oct-2005	JW		SR13659: Created
    ;-------------------------------------------------------------------------------
	new idClass,intCount,intKeys,idParent,query
	
	$$$LogR("DiskCount",YFORM_";"_YKEY)
	
	set idClass  = $$$WWW120ClassUsedInForm($get(^WWW120(0,YFORM,1)))
	set intKeys  = $order(^WWW002(0,idClass,""),-1)
	set idParent = "^"_idClass_"("_$$^WWWKEYBUILD($$$WWWYM(idClass)_","_$piece(YKEY,",",1,intKeys-1))
	
	; JW TODO: Use macros $$$GlobalString and $$$Query
	
	set intCount = 0
	set query = idParent_")"
	for {
		set query = $query(@query)
		quit:query=""
		quit:$piece(query,",",1,intKeys)'=idParent
		
		set intCount = intCount + 1
	}
	quit intCount
	
]]></Routine>
</Export>