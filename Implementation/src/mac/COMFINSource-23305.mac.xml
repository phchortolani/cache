<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMFINSource" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMFINSource
	
#include COMSYS
#include WWWConst
 
Update(pidClass,pidKeys,pidSourceForm,pidSourceKeys)
	;-------------------------------------------------------------------------------
	; Update a source record with its link's details
	; 
	; Params: 	pidClass		- link class (currently only FININTBatchBlock)
	; 			pidKeys			- link keys
	; 			pidSourceForm	- source record's form
	; 			pidSourceKeys	- source record's keys
	;
	; ByRefs:	nil
	;
	; Returns:	status
	;
	; History:
	; 12-May-2006	JW		SR14615: Created
	;-------------------------------------------------------------------------------
	new idSourceClass,objData,strStatus,objOld
	
	set strStatus = $$$OK
	
	set idSourceClass = $$$WWW120ClassUsedInForm($get(^WWW120(0,pidSourceForm,1)))
	if idSourceClass'="" {
		
		// TODO JW - use better indirection here? WWWSETL ?
		
		set objData = @("^"_idSourceClass_"(YM,"_$$^WWWKEYBUILD(pidSourceKeys)_",1)")
		set objOld = objData
		
		if pidClass = "FININTBatchBlock" {
			set strStatus = $$UpdateField(.objData,$$GetBatchFld(idSourceClass),$$$KEY1(pidKeys))
			if $$$ISOK(strStatus) {
				set strStatus = $$UpdateField(.objData,$$GetBlockFld(idSourceClass),$$$KEY2(pidKeys))
			}
		}
		
		if $$$ISOK(strStatus) && (objData'=objOld) {
			set strStatus = $$$Save(idSourceClass,pidSourceKeys,objData,$$$YES)
		}
	}
	
	quit strStatus
	
 
UpdateField(&pobjData,pidFld,pstrData)
	;-------------------------------------------------------------------------------
	; Update a field of the source record. Return msg if already done.
	;
	; Params:	pidFld		- field id
	; 			pstrData	- new field data
	;
	; ByRefs:	pobjData	- source record
	;
	; Returns:	status
	;
	; History:
	; 15-May-2006	JW		SR14615: Created
	;-------------------------------------------------------------------------------
	new strStatus,strOldVal
	
	set strStatus = $$$OK
	
	if pidFld'="" {
		set strOldVal = $piece(pobjData,Y,pidFld)
		
		if strOldVal'="" {
			
			// Be very careful here - should this be done with status returned, or just log?
			
			if strOldVal'=pstrData {
				set strStatus = $listbuild("Com00246")  ;Details already exist. Check for duplicate processing.
			}
		} else {
			set $piece(pobjData,Y,pidFld) = pstrData
		}
	} else {
		do ##class(Common.Dump).DumpFile("Not defined","SR14615")
	}
	
	quit strStatus
 
GetBatchFld(pidClass)
	;-------------------------------------------------------------------------------
	; Get the batch (FININTBatch) field number from a class
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 12-May-2006	JW		SR14615: Created
	;-------------------------------------------------------------------------------
 	
	
	quit $case(pidClass,	"INERECH"	:134,
							"INREC"		:24,
							"INRECLine"	:24,
										:"")
	
	
GetBlockFld(pidClass)
	;-------------------------------------------------------------------------------
	; Get the block (FININTBatchBlock) field number from a class
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 12-May-2006	JW		SR14615: Created
	;-------------------------------------------------------------------------------
	
	quit $case(pidClass,	"INERECH"	:135,
							"INREC"		:25,
							"INRECLine"	:25,
										:"")
	
	
DisplayLink(pidForm,pidSourceClass,pobjSource)
	;-------------------------------------------------------------------------------
	; Display a class' link
	;
	; Params:	pidForm - linking form
	; 			pidSourceClass - source class id
	; 			pobjSource - source record
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-May-2006	JW		SR14615: Created
	;-------------------------------------------------------------------------------
	new strText,idBatch,YKEY
	
	set strText=""
	/*	TODO JW - reverted for build
	set YKEY = ""
	
	if pidForm = "FININTBatch" {
		set idBatch = $piece(pobjSource,Y,$$GetBatchFld(pidSourceClass))
		if idBatch'="" {
			set YKEY = idBatch
			set YPARA = $piece(pobjSource,Y,$$GetBlockFld(pidSourceClass))
			set YSEITE = 2
		}
	}
	
	if YKEY'="" {
		set strText = "<A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM="_pidForm
					_$$WWWCGI2^WWWCGI()_""">"_$$^WWWFORMNAME(pidForm)_"</a>"
	}
	*/
	quit strText
	
	
GoToLink(pidForm,pidSourceClass,pstrSourceKeys)
	;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 16-May-2006	JW		SR14615: Created
	;-------------------------------------------------------------------------------
	new objSource,strKeys,strPage,strPara,idBatch
	
	set objSource = @("^"_pidSourceClass_"(YM,"_$$^WWWKEYBUILD(pstrSourceKeys)_",1)")
	
	set strKeys = ""
	set strPage = ""
	set strPara = ""
	
	if pidForm = "FININTBatch" {
		set idBatch = $piece(objSource,Y,$$GetBatchFld(pidSourceClass))
		if idBatch'="" {
			set strKeys = idBatch
			set strPara = $piece(objSource,Y,$$GetBlockFld(pidSourceClass))
			set strPage = 2
		}
	}
	
	if strKeys'="" {
		do GoToForm^COMUtilForm(pidForm,strKeys,strPage,strPara)
	}
	
	quit
	
]]></Routine>
</Export>