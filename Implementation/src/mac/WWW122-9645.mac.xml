<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW122" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW122
	
#include COMSYS
#include WWWConst
#include COMConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PRUEFEN DATENFELDER
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Jun-2008	GRF		SR15668: new idField, move strType to FixField
	; 03-Apr-2007	RPW		SRBR014368: Rewrote and killed incorrect WWW127 entries.
	; 02.01.1998	DT
	;-------------------------------------------------------------------------------
	new YY,YKEY1,idField,idSeq,objWWW003,strCaption
	
	quit:$get(YKEY)=""
	set YKEY1 = $$$KEY1(YKEY)
	quit:YKEY1=""
	
	set YY = $piece($get(^WWW120(0,YKEY1,1)),Y,11)  ; $$$WWW120ClassUsedInForm()
	
	set idField = ""
	for {
		set idField = $order(^WWW122(0,YKEY1,idField))
		quit:idField=""
		
		do FixField(YKEY1,idField)
	}
	quit
	
	
OnAfterButtonLine(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Display a message if the field has no help text.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 09-Sep-2008	shobby	BR014980: COMHelp* is temporary data now.
	; 09-Jan-2008	shobby	SRAdhoc: Created
	;-------------------------------------------------------------------------------
	new idField,idForm
	
	if $$$DEVMODE {
		set idForm  = $$$KEY1(pYKEY)
		set idField = $$$WWW122SequenceNumber(pYFELD)
		if (idForm'="") && (idField'="") && ($get(SPRACHE)'="") {
			if $$$WWW127HelpText($get(^WWW127(0,idForm,"D",idField,SPRACHE,1)))="" {
				write "<B><FONT color=red>"_$$$Text("WWW00082")_"</FONT></B>"
			}  ; "Warning: This field has no help text."
		}
	}
	quit

FlatControls(pYKEY)
	;-------------------------------------------------------------------------------
	; This routine determines what the 'Inherited' value of the 'Flat Controls' checkbox
	; will be when viewed from the Form Field screen.
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Oct-2013	shobby	CORE-281: Some rework to include WWW120D
	; 12-Oct-2010	shobby	CORE-281: Created
	;-------------------------------------------------------------------------------
	new idForm,objWWW120,blnFlatControls,objWWW120D
	
	
	set blnFlatControls=""
	set idForm=$$$KEY1(pYKEY)
	if idForm'="" {
		set objWWW120D=$get(^WWW120D(YM,idForm,YM,1))
		set blnFlatControls = $$$WWW120DFlatControls(objWWW120D)	
		if blnFlatControls="" set blnFlatControls=$$FlatControls^WWW120D(idForm)
	}
	quit blnFlatControls
	
RelationWithSearchFunction(pYKEY)
	;-------------------------------------------------------------------------------
	; This routine determines what the 'Inherited' value of the 'Relation With Search
	;   Function' checkbox will be when viewed from the Form Field screen.
	; 
	; Called By: RelationWithSearchFunction^WWW122D, Form WWW122 F76 - Inherited From
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 12-Oct-2010	shobby	SRBR014837: Created
	;-------------------------------------------------------------------------------
	new idClass,idClassField,idField,idForm,intValue,objWWW003,objWWW120,objWWW122
	
	set idForm   = $$$KEY1(pYKEY)
	set idField  = $$$KEY2(pYKEY)
	set intValue = 0
	
	if (idForm'="") && (idField'="") {
		set objWWW120 = $get(^WWW120(0,idForm,1))
		set idClass   = $$$WWW120ClassUsedInForm(objWWW120)
		if idClass'="" {
			set objWWW122    = $get(^WWW122(0,idForm,idField,1))
			set idClassField = $$$WWW122SequenceNumber(objWWW122)
			if idClassField'="" {
				set objWWW003 = $get(^WWW003(0,idClass,idClassField,1))
				set intValue  = +$$$WWW003ShowRelationinSearchfunct(objWWW003)
			}
		}
	}
	quit intValue

		
Get(pidForm="",pidField="")  
	;-------------------------------------------------------------------------------
	; Replacement for $get(^WWW122(0,<form name>,<field>,1))
	; This routine should be the only point of access when getting the properties of a
	; form as it will consider all customisations.
	; TODO language texts as well.
	; First implementation will be just to support COMGridEdit
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-Jun-2014	shobby	CORE-385.15: Removed $$$IN Macro.
	; 31-Oct-2013	shobby	SESDF-683: Removed MemoFieldWithFormatButton
	; 28-Oct-2013	shobby	HEVA-1279: Return a blank record if no WWW122 is found.
	; 08-Oct-2013	shobby	CORE-281: Some rework to include WWW120D
	; 14-May-2013	shobby	CORE-79.3: ListWidth changed to MaxListWidth
	; 04-Dec-2012	shobby	HEVA-679: CalcRelationalDataField
	; 23-Apr-2011	shobby	SR18000: FlatControls
	; 09-MAy-2011	GRF		SR17250: Dec Places for Inventory Costs
	; 29-Apr-2011	shobby	SR17714: Pass in YFORM to OnBeforeDisplay.
	; 11-Apr-2011	shobby	SR17701: Reinstated mask functionality
	; 11-Nov-2010	shobby	SR17612: $$$INHERIT
	; 12-Oct-2010	shobby	SRBR014837: The 'Relation With Search Function' field now
	;							uses an 'Inherited Checkbox' control.
	; 06-Nov-2009	shobby	SR16981: If RelationDataField is empty default to first field.
	; 22-Oct-2009	shobby	SR16969: FixAnswerLengthInPixel
	; 29-Jun-2009	PPP		SR16663: Add the Calculated Relation Display Fields
	; 24-Mar-2009	shobby	SR16436: Check the class definition as to whether the
	; 							field should be mandatory.
	; 20-Oct-2008	GRF		SR12505: "Don't Allow Characters" over-ride
	; 04-Jul-2008	GRF		$$$YES boolean macro
	; 26-Jun-2008	GM		SRBR014802: Incl new field "PermanentButton" in WWW122/D
	; 28-Apr-2008	GM		SRBR014867: Incl new field "HyperlinktoForm" in WWW122/D
	; 09-Apr-2008	FIS		SR15668: Field "Relational Display Items" added for
	; 							default setting
	; 28-Feb-2008	shobby	SRBR014894: DoNotShowPrimaryKey
	; 08-Jan-2008	heber	SRBR014794: Mask fields removed from class WWW122D
	; 04-Jan-2008	shobby	SRAdHoc   : NoRelationValidation defaults from class.
	; 29-Nov-2007	GM		SRBR014805: New field "HideRelationClassIDs" in WWW122/D 
	; 16-Nov-2007	shobby	- : Relation with search function defaults from class
	; 13-Nov-2007	shobby	SRBR014513: MemoFieldsWithFormatButtons
	; 12-Nov-2007	shobby	SRBR014786: Get relationships from the class as default.
	; 31-Oct-2007	shobby	SRBR014752: New property for 'Hyperlink to related class
	; 							for ReadOnly Field in grids'
	; 23-Oct-2007	shobby	SRBR014742: Got some relation information from the class
	; 							if it isn't specified in the form.
	; 18-Oct-2007	shobby	SRBR014566: ButtonBehindInputField
	; 12-Oct-2007	shobby	SRBR014625: New customisation field 'Modified Field Length'
	; 10-Oct-2007	shobby	SRBR014516: Included DataInputType
	; 10-Oct-2007	GRF		SR15563: Macro change
	; 03-Oct-2007	shobby	SRBR014592: Included RelationWithSearchFuncti
	; 27-Sep-2007	GRF		SR15603: Macro change
	; 05-Sep-2007	shobby	SRBR014638: Field is mandatory if UniqueKey is set on the class.
	; 04-Aug-2007	shobby	SRBR014614: Completed mapping of all customisations
	; 11-Aug-2007	shobby	SRBR014668: Make sure fields are hidden if either the 
	;						InputType or DataInputType mark them as hidden.
	; 12-Jul-2007	RPW		SRBR014553: set the objWWW122 so we don't get an
	;						undefined if we do not get in here.
	; 10-Jul-2007	shobby	SRBR014553: Added some properties removed call to 'Copy'
	;						subroutine.  (This way is faster)
	; 05-Jul-2007	shobby	SRBR014553: Created
	;-------------------------------------------------------------------------------
	; Decimal Place Rules :\
	;	1.  Number of decimal places is specified on the form.
	;	2.  If Form Decimal Places is not set, use Class Decimal Places.
	;	3.  If Custom Decimal Places is set, override with that.
	;	4a. If the field is an Inventory Cost and there is an INVORG setting then override with that.
	;	4b. If a quantity field has an association to a unit or item then use unit-based decimal places. [EXTERNAL SETTING]
	;-------------------------------------------------------------------------------
	new blnInventCost,idClassField,idClassUsedInForm,intDP
	new objWWW003,objWWW120,objWWW122,objWWW122D,objWWW120D ;SR18000
	
	set objWWW122 = ""
	
	if (pidForm'="") && (pidField'="") {
		set intDP         = ""                                  ; SR17250
		set blnInventCost = $$$NO                               ; SR17250
		set objWWW122     = $get(^WWW122(0,pidForm,pidField,1))
		if objWWW122'="" {
			; Get the input type
			set idClassField = $$$GetClassField(pidForm,pidField)
	
			set objWWW120         = $get(^WWW120(0,pidForm,1))
			set objWWW120D		  = $get(^WWW120D(0,pidForm,YM,1)) ;SR18000
			if idClassField'="" {
				set idClassUsedInForm = $$$WWW120ClassUsedInForm(objWWW120)
				if idClassUsedInForm'="" {
					set objWWW003 = $get(^WWW003(0,idClassUsedInForm,idClassField,1))
					if objWWW003'="" { ; FIXME : $$$WWW003UniqueKey() [D22] is deprecated - canot be set on form and code run to clear any earlier settings <GRF>
						if '$$$WWW122MandatoryInputItem(objWWW122) set $$$WWW122MandatoryInputItem(objWWW122) = $$$WWW003UniqueKey(objWWW003)  ;BR014638
						if '$$$WWW122MandatoryInputItem(objWWW122) set $$$WWW122MandatoryInputItem(objWWW122) = $$$WWW003MandatoryField(objWWW003)  ;SR16436
						if $$$WWW122InputType(objWWW122)="" {
							set $$$WWW122InputType(objWWW122) = $$$WWW003InputType(objWWW003)
						}
						if $$$WWW122RelationDisplayOptions(objWWW122)= "" set $$$WWW122RelationDisplayOptions(objWWW122)= $$$WWW003RelationDisplayOptions(objWWW003)    ;SRBR014742
						if $$$WWW122RelationalPrimaryKey(objWWW122)  = "" set $$$WWW122RelationalPrimaryKey(objWWW122)  = $$$WWW003RelationalPrimaryKeys(objWWW003)     ;SRBR014742
						if $$$WWW122DisplayIfSortCodes(objWWW122)    = "" set $$$WWW122DisplayIfSortCodes(objWWW122)    = $$$WWW003DisplayIfSortKeyEqual(objWWW003)     ;SRBR014742
						if $$$WWW122RelationClass(objWWW122)         = "" set $$$WWW122RelationClass(objWWW122)         = $$$WWW003RelationDatabase(objWWW003)          ;SRBR014742
						if $$$WWW122RelationalDataField(objWWW122)   = "" set $$$WWW122RelationalDataField(objWWW122)   = $$$WWW003RelationalDisplayItems(objWWW003)    ;SR15668
					
						if $$$WWW122CalcRelationalDataField(objWWW122)   = "" set $$$WWW122CalcRelationalDataField(objWWW122)   = $$$WWW003CalcRelationalDisplayItems(objWWW003)    ;SR16663

						if $$OnBeforeDisplay($$$WWW122RelationClass(objWWW122),pidForm) {	; SR17714
							set $$$WWW122CalcRelationalDataField(objWWW122)=1				; SR17714
						}																	; SR17714
						set blnInventCost = ''$$$WWW003InventoryCost(objWWW003)             ; SR17250
						if $$$WWW122Decimals(objWWW122)                  = "" set $$$WWW122Decimals(objWWW122)                 = $$$WWW003NoOfDecimals(objWWW003) ; SR17250
						if $$$WWW122SortTheRelation(objWWW122)           = "" set $$$WWW122SortTheRelation(objWWW122)          = $$$WWW003SortTheRelation(objWWW003)           ;SRBR014786
						if $$$WWW122RelationalDataField(objWWW122)       = "" set $$$WWW122RelationalDataField(objWWW122)      = $$$WWW003RelationalDisplayItems(objWWW003)    ;SRBR014786
						if $$$WWW122DisplayRelationsDownwards(objWWW122) = "" set $$$WWW122DisplayRelationsDownwards(objWWW122)= $$$WWW003DisplayRelationsDownwards(objWWW003) ;SRBR014786
						if $$$WWW122NoRelationValidation(objWWW122)      = "" set $$$WWW122NoRelationValidation(objWWW122)     = $$$WWW003NoRelationValidation(objWWW003)
						if $$$WWW122RelationWithSearchFuncti(objWWW122)  = $$$INHERIT  set $$$WWW122RelationWithSearchFuncti(objWWW122) = +$$$WWW003ShowRelationinSearchfunct(objWWW003) ;SRBR014837 ;SR17612
					}
				}
			}
			if $data(^WWW122D(0,pidForm,pidField,0,1)) {
				set objWWW122D = $get(^WWW122D(0,pidForm,pidField,0,1))
			
				if $$$WWW122DDefaultVariableInput(objWWW122D)     '="" set $$$WWW122DefaultVariableInput(objWWW122)	     = $$$WWW122DDefaultVariableInput(objWWW122D)
				if '$$$WWW122MandatoryInputItem(objWWW122) {  ; Things can not be made 'not mandatory' with customisation  ; BR014638
					if $$$WWW122DMandatoryField(objWWW122D)       '="" set $$$WWW122MandatoryInputItem(objWWW122)        = $$$WWW122DMandatoryField(objWWW122D)
				}
				if $$$WWW122DFieldLength(objWWW122D)              '="" set $$$WWW122FieldSize(objWWW122)                 = $$$WWW122DFieldLength(objWWW122D)
				if $$$WWW122DModule1(objWWW122D)                  '="" set $$$WWW122Module1(objWWW122)                   = $$$WWW122DModule1(objWWW122D)
			
				; FIXME : Doesn't consider WWW122D1 and possible WWW1221 as language over-rides <GRF>
			
				if $$$WWW122DFieldDescription(objWWW122D)         '="" set $$$WWW122ManualCaption(objWWW122)             = $$$WWW122DFieldDescription(objWWW122D)
			
				;Input Type (order is important, hidden must be last)
				if $$$WWW122DInputType(objWWW122D)                '="" set $$$WWW122InputType(objWWW122)                 = $$$WWW122DInputType(objWWW122D)
				if $$$WWW122DComboBox(objWWW122D)                 '="" set $$$WWW122InputType(objWWW122)                 = 4
				if $$$WWW122DHiddenField(objWWW122D)          = $$$YES set $$$WWW122InputType(objWWW122)                 = 0
			
				if $$$WWW122DDontAllowCharacters(objWWW122D)      '="" set $$$WWW122DontAllowChars(objWWW122)            = $$$WWW122DDontAllowCharacters(objWWW122D)   ; SR12505
				if $$$WWW122DPatternMatch(objWWW122D)             '="" set $$$WWW122PatternMatch(objWWW122)              = $$$WWW122DPatternMatch(objWWW122D)
				if $$$WWW122DRelationClass(objWWW122D)            '="" {
					set $$$WWW122RelationClass(objWWW122) 		= $$$WWW122DRelationClass(objWWW122D)
					set $$$WWW122DisplayIfSortCodes(objWWW122) 	= ""
				}
				if $$$WWW122DRelationalPrimaryKey(objWWW122D)     '="" set $$$WWW122RelationalPrimaryKey(objWWW122)      = $$$WWW122DRelationalPrimaryKey(objWWW122D)
			
				if $$$WWW122DRelationalDataField(objWWW122D)      '="" set $$$WWW122RelationalDataField(objWWW122)       = $$$WWW122DRelationalDataField(objWWW122D)
				if ($$$WWW122RelationalDataField(objWWW122)="") && ($$$WWW122RelationClass(objWWW122)'="") {
					set $$$WWW122RelationalDataField(objWWW122) = $order(^WWW003(0,$$$WWW122RelationClass(objWWW122),""))
				}
			
				if $$$WWW122DRelationDisplayOptions(objWWW122D)   '="" set $$$WWW122RelationDisplayOptions(objWWW122)    = $$$WWW122DRelationDisplayOptions(objWWW122D)
				if $$$WWW122DNewGroup(objWWW122D)                 '="" set $$$WWW122NewGroup(objWWW122)                  = $$$WWW122DNewGroup(objWWW122D)
				if $$$WWW122DGroupName(objWWW122D)                '="" set $$$WWW122GroupName(objWWW122)                 = $$$WWW122DGroupName(objWWW122D)
				if $$$WWW122DUsersAccess(objWWW122D)              '="" set $$$WWW122UserAccess(objWWW122)                = $$$WWW122DUsersAccess(objWWW122D)
				if $$$WWW122DDisplayOnPage(objWWW122D)            '="" set $$$WWW122DisplayOnPageNumber(objWWW122)       = $$$WWW122DDisplayOnPage(objWWW122D)
				if $$$WWW122DLinePosition(objWWW122D)             '="" set $$$WWW122RowPosition(objWWW122)               = $$$WWW122DLinePosition(objWWW122D)
				if $$$WWW122DTabulatorPosition(objWWW122D)        '="" set $$$WWW122ColumnPosition(objWWW122)		     = $$$WWW122DTabulatorPosition(objWWW122D)
				if $$$WWW122DModifyOnlyRelationFields(objWWW122D) '="" set $$$WWW122RelationFieldSequence(objWWW122)     = $$$WWW122DModifyOnlyRelationFields(objWWW122D)       ; SR15563
				if $$$WWW122DAddNoOfSpaces(objWWW122D)            '="" set $$$WWW122AddNoOfSpaces(objWWW122)             = $$$WWW122DAddNoOfSpaces(objWWW122D)
		;---------------------------------------
		; FIXME : <GRF> This may not be a problem but we have possibly compulsory functionality
		;               being replaced by customised functionality that may not perform ALL the
		;               REQUIRED operations.
		;               If the *** EXECUTE *** hooks examine each entry separately, this particular
		;               code may not matter.
		;               We need clear guidelines of how they are supposed to interact.
		;               (WWWFORMD also overwrites these fields in a similar block of code.)
		;---------------------------------------
				if $$$WWW122DExecuteBeforeDataField(objWWW122D)   '="" set $$$WWW122ExecuteBeforeDataField(objWWW122)   = $$$WWW122DExecuteBeforeDataField(objWWW122D)   ; SR15603
				if $$$WWW122DExecuteAfterDataField(objWWW122D)    '="" set $$$WWW122ExecuteAfterDataField(objWWW122)    = $$$WWW122DExecuteAfterDataField(objWWW122D)    ; SR15603
				if $$$WWW122DExecuteWithinDataField(objWWW122D)   '="" set $$$WWW122ExecuteWithinDataField(objWWW122)   = $$$WWW122DExecuteWithinDataField(objWWW122D)   ; SR15603
				if $$$WWW122DComboBox(objWWW122D)                 '="" set $$$WWW122ComboboxFunction(objWWW122)         = $$$WWW122DComboBox(objWWW122D)
				if $$$WWW122DRelationTestOnEdit(objWWW122D)       '="" set $$$WWW122NoRelationValidation(objWWW122)     = $$$WWW122DRelationTestOnEdit(objWWW122D)
				if $$$WWW122DMask(objWWW122D)                     '="" set $$$WWW122Mask(objWWW122)                     = $$$WWW122DMask(objWWW122D)					; SRBR014794 ;SR17701
		;SR17701 ;if $$$WWW122DApplyMaskIfDataField(objWWW122D)    '="" set $$$WWW122ApplyMaskIfDataField(objWWW122)     = $$$WWW122DApplyMaskIfDataField(objWWW122D)	; SRBR014794
		;SR17701 ;if $$$WWW122DContains(objWWW122D)                '="" set $$$WWW122Contains(objWWW122)                 = $$$WWW122DContains(objWWW122D)				; SRBR014794
		;SR17701 ;if $$$WWW122DMask2(objWWW122D)                   '="" set $$$WWW122Mask2(objWWW122)                    = $$$WWW122DMask2(objWWW122D)					; SRBR014794
			
				if $$$WWW122DRelationWithSearchFuncti(objWWW122D) '=$$$INHERIT  set $$$WWW122RelationWithSearchFuncti(objWWW122) = $$$WWW122DRelationWithSearchFuncti(objWWW122D) ;BR014592 ;BR014837 ;SR17612
				if $$$WWW122DDataInputType(objWWW122D)			  '="" set $$$WWW122DataInputType(objWWW122)		    = $$$WWW122DDataInputType(objWWW122D) ;BR014516
				if $$$WWW122DModifiedFieldLength(objWWW122D)	  '="" set $$$WWW122ModifiedFieldLength(objWWW122)	    = $$$WWW122DModifiedFieldLength(objWWW122D) ;BR014265
				if $$$WWW122DButtonBehindInputField(objWWW122D)   '="" set $$$WWW122ButtonBehindInputField(objWWW122)   = $$$WWW122ButtonBehindInputField(objWWW122)_";***CUSTOM***;"_$$$WWW122DButtonBehindInputField(objWWW122D)
				if $$$WWW122DHyperlinkToRelatedClass(objWWW122D)  '="" set $$$WWW122HyperlinkToRelatedClass(objWWW122)  = $$$WWW122DHyperlinkToRelatedClass(objWWW122D)  ;SRBR014752
				if $$$WWW122DHideRelationClassIDs(objWWW122D)     '="" set $$$WWW122HideRelationClassIDs(objWWW122)     = $$$WWW122DHideRelationClassIDs(objWWW122D)  ;SRBR014805
				if $$$WWW122DDoNotShowPrimaryKey(objWWW122D)      '="" set $$$WWW122DoNotShowPrimaryKey(objWWW122)      = $$$WWW122DDoNotShowPrimaryKey(objWWW122D)  ;SRBR014894
				if $$$WWW122DHyperlinktoForm(objWWW122D)          '="" set $$$WWW122HyperlinktoForm(objWWW122)          = $$$WWW122DHyperlinktoForm(objWWW122D)  ;SRBR014867
				if $$$WWW122DPermanentButton(objWWW122D)          '="" set $$$WWW122PermanentButton(objWWW122)          = $$$WWW122DPermanentButton(objWWW122D)  ;SRBR014802
				if $$$WWW122DFixAnswerLengthInPixel(objWWW122D)   '="" set $$$WWW122FixAnswerLengthInPixel(objWWW122)   = $$$WWW122DFixAnswerLengthInPixel(objWWW122D)  ;SR16969  
			
			;	if $$$WWW122DManualCaption(objWWW122D)			  '="" set $$$WWW122ManualCaption(objWWW122)		  =$$$WWW122DManualCaption(objWWW122D)
			;	   $$$FldWWW122SequenceNumber    - ignore not in customisation
			
				if $$$WWW122DDecimals(objWWW122D)     '="" set $$$WWW122Decimals(objWWW122)     = $$$WWW122DDecimals(objWWW122D) ; SR17250
				if $$$WWW122DMaxListWidth(objWWW122D) '="" set $$$WWW122MaxListWidth(objWWW122) = $$$WWW122DMaxListWidth(objWWW122D)  ;SR17725 ;CORE-79.3
				set $$$WWW122FlatControls(objWWW122)     = $$$WWW122DFlatControls(objWWW122D)  	;SR18000 ;CORE-281
				if $$$WWW122DCalcRelationalDataField(objWWW122D) '="" set $$$WWW122CalcRelationalDataField(objWWW122)  = $$$WWW122DCalcRelationalDataField(objWWW122D) ;HEVA-679
				if $$$WWW122FlatControls(objWWW122) = "" set $$$WWW122FlatControls(objWWW122)=$$FlatControls^WWW122D(pidForm_","_pidField) ;CORE-281
				if $$$WWW122DExecuteOnBlur(objWWW122D)   '="" set $$$WWW122ExecuteOnBlur(objWWW122)   = $$$WWW122DExecuteOnBlur(objWWW122D)
			}
		
			if $$$WWW122InputType(objWWW122)=0      set $$$WWW122DataInputType(objWWW122) = 15   ;BR014668
			if $$$WWW122DataInputType(objWWW122)=15 set $$$WWW122InputType(objWWW122)     = 0    ;BR014668
			if blnInventCost {        ; SR17250 vvv
				set intDP = $piece($get(^INVORG(0,0,1)),Y,1) ;$$$INVORGDecimalpointsforinventory($get(^INVORG(0,0,1))) ;CORE-385.15
				if intDP'=""  set $$$WWW122Decimals(objWWW122) = intDP
			}
		}		
	}
	quit objWWW122	

OnBeforeDisplay(pidClass,YFORM="")
	;-------------------------------------------------------------------------------
	; Determines if this has a special format for display in grids.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Jul-2011	shobby		SR17791: Test YFORM
	; 29-Apr-2011	shobby		SR17714: 2nd Parameter YFORM
	; 27-Apr-2011	shobby		SR17714: Created
	;-------------------------------------------------------------------------------
	new blnResult,objWWW120
	
	set blnResult=$$$NO
	if YFORM'="" {			;SR17791
		set objWWW120=$get(^WWW120(0,YFORM,1))
		if $$$WWW120FormType(objWWW120)=12 {	;Grid Edit Only	
			if pidClass'="" {
				set blnResult=($data(^WWW001Hook(YM,pidClass,10)))||($data(^WWW001VARHooks(YM,pidClass,10)))
			}
		}
	}
	quit blnResult

HasOnBeforeDisplay(YKEY="",YFELD="")
	;-------------------------------------------------------------------------------
	; Determines whether to lock some formatting fields because a class hook is
	; already taking care of it..
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 29-Apr-2011	shobby		SR17714: Pass in YFORM as 2nd parameter.
	; 27-Apr-2011	shobby		SR17714: Created
	;-------------------------------------------------------------------------------
	quit $$OnBeforeDisplay($$$WWW122RelationClass(YFELD),$$$KEY1(YKEY))

 
FixField(pidForm="",pidField="")
	;-------------------------------------------------------------------------------
	; Update the help fields and the captions.
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 14-Mar-2014	SCR		CORE-93: Remove Rules
	; 23-Nov-2009	shobby	SR17053: Only remove help texts on Manual Fields.  Help Texts
	; 							created against Regular Form fields may still belong to another
	; 							Regular Form Field, or may want to recreate a form field
	; 							that points to the class field without losing this help text.
	; 06-Jun-2008	GRF		SR15668: new strType
	; 11-Apr-2007	RPW		SRBR014368: Created
	;-------------------------------------------------------------------------------
	quit:$get(YKEY)=""
	
	new YY,YKEY1,idField,idSeq,objWWW003,strCaption,strType
	
	do KillChildren^COMUtils("WWW122C2",pidForm_","_pidField)
	do KillChildren^COMUtils("WWW122D2",pidForm_","_pidField)
	if pidForm="" {
		set YKEY1   = $$$KEY1(YKEY)    quit:YKEY1=""            ; *** EARLY EXIT ***
		set idField = $$$KEY2(YKEY)
	} else {
		set YKEY1   = pidForm          quit:YKEY1=""            ; *** EARLY EXIT ***
		set idField = pidField
	}
	
	set YY = $piece($get(^WWW120(0,YKEY1,1)),Y,11)  ;DATEINAME ;file name 
	;S DATEI=YY
	
	set idSeq = $$$WWW122SequenceNumber($get(^WWW122(0,YKEY1,idField,1)))
	if idSeq="" { // Manual Field now
		; Field May have been deleted.
		if ($get(^WWW122(0,YKEY1,idField,1))'="") && '$data(^WWW127(0,YKEY1,"M",idField,SPRACHE,1)) set ^WWW127(0,YKEY1,"M",idField,SPRACHE,1)="" ;17053
		; SR17053 vvvv  Don't remove data field help text.  It may belong to another form field or may want to assign
		; a new form field in the future without losing the help text.
		;if $data(^WWW127(0,YKEY1,"D",idField)) {
		;		do KillChildren^COMUtils("WWW127",YKEY1_",D,"_idField)
		;}
		; SR17053 ^^^^
	} else {
		if '$data(^WWW127(0,YKEY1,"D",idField,SPRACHE,1)) set ^WWW127(0,YKEY1,"D",idField,SPRACHE,1) = ""
		if $data(^WWW127(0,YKEY1,"M",idField)) {
			do KillChildren^COMUtils("WWW127",YKEY1_",M,"_idField)
		}
		
		if YY'="" {
			if '$data(^WWW003(0,YY,idSeq)) {
				set $piece(^WWW122(0,YKEY1,idField,1),Y,1) = ""
			} else {
				set objWWW003 = $get(^WWW003(0,YY,idSeq,1))
				if objWWW003'="" {
					set strCaption = $$$WWW003CaptionInForms(objWWW003)
					if $find($$$WWW003PropertyName(objWWW003),".") set strCaption = $$$WWW003PropertyName(objWWW003)
					set $piece(^WWW122(0,YKEY1,idField,1),Y,12) = strCaption  ; $$$WWW122ManualCaption
				
				} elseif $$$WWW003InputType(objWWW003)=0 {           ; enumDataHidden
					set $piece(^WWW122(0,YKEY1,idField,1),Y,2) = 15  ; $$$WWW122DataInputType = enumFormHidden
				}
			}
		} else {
			set $piece(^WWW122(0,YKEY1,idField,1),Y,1) = ""          ; $$$WWW122SequenceNumber (manual field)  ;LÃ–SCHEN, WENN KEINE DATEI
		}
	}
	
	; SR17053 vvvv  Don't remove data field help text.  It may belong to another form field or may want to assign
	; a new form field in the future without losing the help text.
	;for strType="M","D" {
	;	if $data(^WWW127(0,YKEY1,strType,idField)) {
	;		set idSeq = $$$WWW122SequenceNumber($get(^WWW122(0,YKEY1,idField,1)))
	;		if ((strType="D") && (idSeq="")) || ((strType="M") && (idSeq'="")) {
	;			do KillChildren^COMUtils("WWW127",YKEY1_","_strType_","_idField)
	;		}
	;	}
	;}
	
	if $data(^WWW127(0,YKEY1,"M",idField)) {
		set idSeq = $$$WWW122SequenceNumber($get(^WWW122(0,YKEY1,idField,1)))
		if (idSeq'="")||($get(^WWW122(0,YKEY1,idField,1))="") {
			do KillChildren^COMUtils("WWW127",YKEY1_",M,"_idField)
		}
	}
	; SR17053 ^^^^^
			
	quit
	
	
OnAfterDelete(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Routine to run after deleting a form field
	;
	; Params: pYKEY		: The YKEY
	;		  pYFELD	: The YFELD
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 10-Sep-2008	shobby	BR014980	COMHelp* no longer necessary (Temp data now)
	; 03-Apr-2007	RPW		SRBR014368: Rebuild the HelpText for the form due to
	;							bad data structures
	; 08-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idForm,idField
	
	do FixField($$$KEY1(pYKEY),$$$KEY2(pYKEY))
	quit
	
	
OnAfterSave(pYKEY)
	;-------------------------------------------------------------------------------
	; Routine to run after saving a data field
	;
	; Params: pYKEY		: The YKEY
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 10-Sep-2008	shobby	BR014980	COMHelp* no longer necessary. (Temporary data now)
	; 03-Apr-2007	RPW		SRBR014368: Rebuild the help text for the form due to
	;							bad data structure
	; 08-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idForm,idField,strStatus
	
	set strStatus = $$$OK
	do FixField($$$KEY1(pYKEY),$$$KEY2(pYKEY))
	quit
	
	
FixAllHelpText()
	;-------------------------------------------------------------------------------
	; Remove WWW127 entries that should not exist.
	; 
	; This should be used manually to removed extra entries.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 23-Nov-2009	shobby	SR17053:	Include Spanish
	; 10-Sep-2008	shobby	BR014980	COMHelp* no longer necessary. (Temporary data now)
	; 03-May-2007	RPW		SRBR014368: Convert Class Field # to Form Field #
	; 03-Apr-2007	RPW		SRBR014368: Created
	;-------------------------------------------------------------------------------
	;	^WWW127(0,"WWW123","D",22,"EN",1)="..."
	;	^COMHelpField(0,"WWW123","EN",19,1)="...~D22"
	;	^WWW122(0,"WWW123",17,1)="22~~..."
	;	^WWW122s(0,4,22,"WWW123",17)=""
	;	
	;	^WWW122(0,"INWEFREI",7,1)="~~..."   Manual Fields
	;-------------------------------------------------------------------------------
	new idField,idForm,idFormField,SPRACHE,YKEY
	
	set idForm = ""
	for {
		set idForm = $order(^WWW120(0,idForm))
		quit:idForm=""
		
		write !,"Fixing form : "_idForm
		
		set idField = ""
		for {
			set idField = $order(^WWW122(0,idForm,idField))
			quit:idField=""
			
			for SPRACHE="EN","PT","DE","ES" {
				set YKEY=idForm_$$$COMMA_idField
				do FixField()
			}
		}
		set idField = ""
		for {
			set idField = $order(^WWW127(0,idForm,"D",idField))
			quit:idField=""
			
			set idFormField = $order(^WWW122s(0,4,idField,idForm,""))
			for SPRACHE="EN","PT","DE","ES" {
				set YKEY=idForm_$$$COMMA_idFormField
				do FixField()
			}
		}
	}
	quit
	
	
OnBeforeSave(pidWWW122,pobjWWW122)
	;-------------------------------------------------------------------------------
	; Verify whether entered masks are valid
	; 
	; Params:
	;
	; Returns:
	;
	; History:
	; 15-Apr-2011	shobby	SR17701:	Only one mask to check.
	; 04-Jan-2008	heber	SRBR014794:	Changed VerifyMask parameters.
	; 28-Aug-2007	Karine	SR15592:	Created a mask validation for WWW122D.
	; 27-Jul-2007	HeberB	SRBR014502: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set Q = $$$QSave
	;SR17701 set strStatus = $$VerifyMask^WWWFORM7($$$WWW122Mask(pobjWWW122),$$$WWW122Mask2(pobjWWW122))
	set strStatus = $$VerifyMask^WWWFORM7($$$WWW122Mask(pobjWWW122)) ;SR17701
	if $$$ISERR(strStatus) {
		$$$Alert(strStatus)
		set Q = $$$QDontSave
	}
	quit

Renumber(YFORM,pintPage)
	;-------------------------------------------------------------------------------
	; Tool to renumber the fields on a form to make it possible to put a new field
	; in to 2nd or subsequent column without having to rearrange the entire form
	; because many fields have the same row number.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-Jun-2012	shobby		SR18037: Created
	;-------------------------------------------------------------------------------
	new idField,arrFields,intCounter,objWWW122,intRow,intCol,strStatus
	 
	kill arrFields
	$$$VAR
	tstart 
	set intCounter=0
	
	set idField="" for { set idField=$order(^WWW122(0,YFORM,idField)) quit:idField=""
		set objWWW122=$get(^WWW122(0,YFORM,idField,1))
		if $$$WWW122DisplayOnPageNumber(objWWW122)=pintPage {
			set intRow=$$$WWW122RowPosition(objWWW122)
			set intCol=$$$WWW122ColumnPosition(objWWW122)
			set arrFields(intRow,intCol,idField)=""
			if $$$WWW122ColumnPosition(objWWW122)=1 {
				set intCounter=intCounter+1
			}
		}
	}
	write !,intCounter
	set intRow="" for { set intRow=$order(arrFields(intRow),-1) quit:intRow=""
		set intCol="" for { set intCol=$order(arrFields(intRow,intCol),-1) quit:intCol=""
			set idField="" for { set idField=$order(arrFields(intRow,intCol,idField),-1) quit:idField=""
				set arrFields(intRow,intCol,idField)=intCounter
				set objWWW122=$get(^WWW122(0,YFORM,idField,1))
				set $$$WWW122RowPosition(objWWW122)=intCounter
				set strStatus=$$Save^COMUtils("WWW122",YFORM_","_idField,objWWW122,1)
				break '$$$ISOK(strStatus)
				if intCol=1 set intCounter=intCounter-1

			}
		}
	}
	zw arrFields
	quit
	
]]></Routine>
</Export>