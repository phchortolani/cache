<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWDASHBOARD" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWDASHBOARD
	
#include WWWConst
#include COMSYS
#include COMConst
	
	;-------------------------------------------------------------------------------
	; ( ) Standard Currency                       AUD   1                     * 1 *
	; ( ) Entity Setup       THIS NAMESPACE       XXX   XXX                   * 2 *
	; ( ) Entity Setup       LiveNamespace        XXX   XXX                   * 2 *
	; ( ) Entity Setup       TestNamespace        XXX   XXX                   * 2 *
	;-------------------------------------------------------------------------------
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 21-Sep-2011	shobby	SR17835: Internationalised some text.
	; 19-Jul-2011	shobby	SR17835: CheckBudget
	; 08-Feb-2011	shobby	SR17657: Created
	;-------------------------------------------------------------------------------
	new idCurrency,strImage,strText,YKEY,YTOOLTIP
	
	kill ^WWWDASHBOARD
	set idCurrency = $$StandardCurrency()
	
	write "<TABLE>"
	
	;---------------------------------------
	; 34458		"'%1' field in the '%2' screen must be '%3'."
	; WWW00148	"Must have at least one 'Transaction Type' of type 'Budget'."
	; WWW00151  "Transaction Types"
	; WWW00149  "Session IDs should have a regularly scheduled tasks to clear out old records.||do ##class(alWEB.WS.Session).ClearSessions()"
	; WWW00150  "SessionIDs"
	;---------------------------------------
	;  * 1 * Currency Test
	;---------------------------------------
	do MessageLine^WWWDASHBOARDEntity($$CheckStandardCurrency(),$$$Text($listbuild(34358,$$^WWWFELDNAME("WWWWAE","D",5),$$^WWWFORMNAME("WWWWAE"),1)),$$^WWWFELDNAME("WWW012","D",75),"",idCurrency,+$$UnitPrice(idCurrency),"WWW012") ;SR17835
	
	;---------------------------------------
	;  * 2 * Dataset Test - may be multiple lines
	;---------------------------------------
	do Message^WWWDASHBOARDEntity()
	do MessageLine^WWWDASHBOARDEntity($$CheckBudget()    ,$$$Text("WWW00148"),$$$Text("WWW00151"),"+","*","","FINGLTranType") ;SR17835 		
	do MessageLine^WWWDASHBOARDEntity($$CheckSessionIDs(),$$$Text("WWW00149"),$$$Text("WWW00150"),"+","*","","COMSchedule")   ;SR17835
	write "</TABLE>"
	quit
	
	
	; ******************* Checks ********************************
CheckSessionIDs()
	;SR17778
	new blnFound,idSchedule,objSchedule
	
	set blnFound = $$$NO
	
	if $data(^alWEB.WS.SessionD) {
		set idSchedule = ""
		for {
			set idSchedule = $order(^COMSchedule(0,idSchedule))
			quit:idSchedule=""
			
			set objSchedule = $get(^COMSchedule(0,idSchedule,1))
			if $$$COMScheduleCode(objSchedule)="##class(alWEB.WS.Session).ClearSessions()" {
				set blnFound = $$$YES
				quit
			}
		}
	} else {
		set blnFound=$$$YES
	}
	quit blnFound
	
	
CheckBudget()
	;SR17835 - check if a budget type is defined.
	quit $data(^FINGLTranTypes(0,1,1))
	
	
CheckStandardCurrency()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Feb-2011	shobby	SR17657: Created
	;-------------------------------------------------------------------------------
	new fltUnitPrice,idCurrency
	
	set fltUnitPrice = 0
	set idCurrency   = $$StandardCurrency()
	if idCurrency'="" {
		set fltUnitPrice = $$UnitPrice(idCurrency)
	}
	quit (fltUnitPrice=1)
	
	
StandardCurrency()
	quit $$$WWW012StandardCurrency($get(^WWW012(0,0,1)))
	
	
UnitPrice(pidCurrency)
	quit $$$WWWWAEUnitPrice($get(^WWWWAE(0,pidCurrency,1)))
	
	
AllChecks(&pintWarnings)
	;-------------------------------------------------------------------------------
	; See if there are any warnings.  Will run once every 60 seconds.  So as not
	; to slow down the loading of every form for a test that you never really fail.
	;
	; Called By : WarningIndicator^WWWDASHBOARD
	;
	; Returns:
	;
	; History:
	; 02-May-2013	shobby	CORE-81: Include SessionID check. Count number of warnings.
	; 12-Jul-2012	shobby	SR18040: CheckMidnight.
	; 19-Jul-2011	shobby	SR17835: Check to see whether a 'Budget' transaction type
	;								 has been setup.
	; 03-Mar-2011	shobby	SR17661: WWWDASHBOARD now exists as a class and can
	;							be managed in a standard @netManager. 
	; 17-Feb-2011	shobby	SR17661: When there are a lot of tests this will start
	;							to get slow, so only check once a minute.
	; 17-Feb-2011	shobby	SR17661: Check the COMEntity setups.
	; 08-Feb-2011	shobby	SR17657: Created
	;-------------------------------------------------------------------------------
	new blnResult,objWWWDASHBOARD,strStatus
	
	set objWWWDASHBOARD = $get(^WWWDASHBOARD(0,0,1))
	
	if $zabs($piece($horolog,",",2)-$piece($$$WWWDASHBOARDDate1(objWWWDASHBOARD),",",2))>60 {  ; $zabs allows for different date
		set $$$WWWDASHBOARDDate1(objWWWDASHBOARD) = $horolog
		set blnResult = $$$OK
		set pintWarnings = 0
		if '$$Check^WWWDASHBOARDEntity() set blnResult=$$$NO set pintWarnings=pintWarnings+1
		if '$$CheckStandardCurrency() set blnResult=$$$NO set pintWarnings=pintWarnings+1
		if '$$CheckBudget() set blnResult=$$$NO set pintWarnings=pintWarnings+1
		if '$$CheckMidnight() set blnResult=$$$NO set pintWarnings=pintWarnings+1
		if '$$CheckSessionIDs()  set blnResult=$$$NO set pintWarnings=pintWarnings+1 ;CORE-81
		set $$$WWWDASHBOARDResult(objWWWDASHBOARD) = blnResult
		set strStatus = $$$Save("WWWDASHBOARD",pintWarnings,objWWWDASHBOARD,$$$YES)
		
	}
	quit $$$WWWDASHBOARDResult(objWWWDASHBOARD)
	
	
WarningIndicator(pstrColorRHS)
	;-------------------------------------------------------------------------------
	; Puts a warning light in the top right of the screen if there are any incorrect
	; or missing critical setups.
	;
	; Called By : V2^WWWKOPF (draws form title line)
	; 
	; Inputs: pstrColorRHS - colour for Right Hand Side of form title line
	; Returns:
	;
	; History:
	; 20-Apr-2011	GRF		SR17657/17661: YBACK is not necessarily defined - add $get
    ; 25-Feb-2011	GRF		SR17661: Pass ColorCodeForHeaderRight to Warning Light
	; 08-Feb-2011	shobby	SR17657: Created
	;-------------------------------------------------------------------------------
	new strBack
	
	set strBack = $get(YBACK)
	new YBACK
	
	if '$$AllChecks() {
		write "<TD padding=0 bgcolor="_pstrColorRHS_" align=center width=24>"
		set YBACK = strBack_YFORM_","
		write "<A border=none class=link onclick=""window.location='"_YAKTION_"EP=WWWFORM&amp;YFORM=WWWDASHBOARD"_$$WWWCGI2^WWWCGI()_"'"">"
		write "<IMG style='margin:0px; border:0px; padding:0px; vertical-align:middle;' align=right SRC="_YGIF_"ball.gif"
		write ">"
		write "&nbsp;</TD>",YCR
	}
	quit
	
	
CheckMidnight()
	;-------------------------------------------------------------------------------
	; Check whether the change of day, WWWDATEN protection is set up as a schedule task.
	; If not create it.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Jul-2012	shobby		SR18040: Created
	;-------------------------------------------------------------------------------
	new idSchedule,idType,blnFound,objCOMSchedule,strStatus,YKEY
	
	set blnFound=$$$NO
	set strStatus=$$$OK
	
	set idType="" for { set idType=$order(^COMSchedules(0,1,"WWWTAG",idType)) quit:idType=""
		set idSchedule="" for { set idSchedule=$order(^COMSchedules(0,1,"WWWTAG",idType,idSchedule)) quit:idSchedule=""
			set objCOMSchedule=$get(^COMSchedule(0,idSchedule,1))
			if $$$COMScheduleCode(objCOMSchedule)="Midnight^WWWDATEN()" {
				set blnFound=$$$YES
				quit
			}
		}
	}
	if 'blnFound { 
		;"WWWTAG~1~4~~1;2;3;4;5;6;7~~~86340~~62651,86340~~Midnight~Midnight^WWWDATEN()~~"
		set objCOMSchedule=""
		set $$$COMScheduleClass(objCOMSchedule)="WWWTAG"
		set $$$COMScheduleTypeReference(objCOMSchedule)=1
		set $$$COMScheduleTimeIncrement(objCOMSchedule)=4
		set $$$COMScheduleTimeToRun(objCOMSchedule)=86340
		set $$$COMScheduleDaysOfWeek(objCOMSchedule)="1;2;3;4;5;6;7"
		set $$$COMScheduleDescription(objCOMSchedule)="Midnight"
		set $$$COMScheduleCode(objCOMSchedule)="Midnight^WWWDATEN()"
		set YKEY=$$^WWWNEXT("COMSchedule")
		set strStatus=$$Save^COMUtils("COMSchedule",YKEY,objCOMSchedule,1)
	}
	quit strStatus
	
]]></Routine>
</Export>