VARDashboardFracionamento
#include COMSYS

PrepareTempVariables(YLOCATION, YBED)		
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Seta a variável YLOCATION na temporária utilizada na navegação.
    ;
    ; Chamado por:
    ;	Form VARDashboardFracionamento
    ; 
    ; Histórico:
    ; 	01-Mar-2012		Criado
    ;-------------------------------------------------------------------------------
	kill ^VARTempDashboard(YM, YBED)
	set $piece(^VARTempDashboard(YM,YBED),Y,1) = YLOCATION
	
	quit

PrintFracionamentosPendentes(pidLocal)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe a tabela de fracionamentos pendentes.
    ;
    ; Chamado por:
    ;	VARDashboardFracionamento.csp
    ; 
    ; Histórico:
    ; 	22-Fev-2012		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidLocal = "")
	
	new objLocation, EndUnitPadrao, arrayResponse, arrayWorkList, idProduto, descProduto, image

	set objLocation = $get(^WWW0121(YM,YM,pidLocal,1)) //Getting location data
	quit:(objLocation = "")
	
	set EndUnitPadrao = $$$WWW0121FREE14(objLocation) //Getting standard storage for unitarization
	quit:(EndUnitPadrao = "")

	set arrayResponse = ""
	set arrayWorkList = ""
	
	do GetListaTrabalhoUnit^VARListaTrabalhoUnit(pidLocal,EndUnitPadrao,.arrayWorkList)
	do ReOrderResult^VARListaTrabalhoUnit(.arrayWorkList,.arrayResponse,"P",pidLocal,2)

	set count = ""
	set count = $order(arrayResponse(count))
	
	write "<table width='100%' cellspacing='1'>"
	write "<tr style='background-color:#2c6c7f;color:white'>"
	write "	<th width='50'>Prioridade</th>"
	write " <th width='60'>Ordem</th>"
	write "	<th width='110'>Status</th>"
	write " <th>Produto</th>"
	write " <th width='60'>Lote</th>"	
	write "	<th width='50'>Quant.</th>"
	write "</tr>"	
	
	while (count '= "") {
		
		quit:(count > 5)
		
		set idProduto   = $piece(arrayResponse(count),Y,1)
		set descProduto = $piece(arrayResponse(count),Y,2)
		
		set Manual = $piece(arrayResponse(count),Y,3)
		set Quantity 	= $piece(arrayResponse(count),Y,8)
		set LotNumber 	= $piece(arrayResponse(count),Y,12)	
		set WorkID	 	= $piece(arrayResponse(count),Y,17)
		set WorkStatus 	= $piece(arrayResponse(count),Y,18)
	  	
		set param = "STATUSUNITARIZACAO"_Y_WorkStatus
	
		if (WorkStatus '= "") {
			set descStatus = $$SQLGetUserParamDescVAR^VARSQL(param)
		} else {
			set descStatus = ""
		}
		
		if (WorkStatus = 0) {
			set image = YGIF_"alertRed.gif"
		} elseif (WorkStatus = 1) {
			set image = YGIF_"alertYellow.gif"
		} elseif (WorkStatus = 2)  {
			set image = YGIF_"alertBlue.gif"
		} else {
			set image = ""	
		}
	  	
	  	if (count#2=0) set bkgColor = "#F0F0F0"
	  	if (count#2=1) set bkgColor = "#E0E0E0"
	  	
	  	if (WorkID = "") {
		  	set WorkID = "Aguard."
	  	} else {	  	
			set redirectLink = YAKTION_"EP=WWWFORM&YFORM=VAROrdemUnitarizacao&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YKEY="_WorkID
			set WorkID = "<a href='"_redirectLink_"' title='Clique para ver os detalhes da ordem.'>"_WorkID_"</a>"
	  	}
	  	
		write "<tr style='background-color:"_bkgColor_"'>"
		write "	<td style='background-image: url("_image_");background-repeat:no-repeat;background-position:center'>&nbsp;"_count_"</td>"
		write " <td>"_WorkID_"</td>"
		write " <td>"_descStatus_"</td>"
		write " <td>"_descProduto_"</td>"
		write " <td>"_LotNumber_"</td>"		
		write " <td align='right'>"_$$^WWWTR(0,12,Quantity)_"</td>"
		write "</tr>"
		
		set count = $order(arrayResponse(count))
	}
	
	write "</table>"
	
	quit
	
PrintMontagemKitsPendentes(pidLocal)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe a tabela de montagem de kits pendentes.
    ;
    ; Chamado por:
    ;	VARDashboardFracionamento.csp
    ; 
    ; Histórico:
    ; 	22-Fev-2012		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidLocal = "")
	
	new objLocation, EndUnitPadrao, arrayResponse, arrayWorkList, idProduto, descProduto, image

	set objLocation = $get(^WWW0121(YM,YM,pidLocal,1)) //Getting location data
	quit:(objLocation = "")
	
	set EndUnitPadrao = $$$WWW0121FREE14(objLocation) //Getting standard storage for unitarization
	quit:(EndUnitPadrao = "")

	set arrayResponse = ""
	set arrayWorkList = ""
	
	do GetListaTrabalhoKit^VARListaTrabalhoKit(pidLocal,EndUnitPadrao,.arrayWorkList)
	do ReOrderResult^VARListaTrabalhoKit(.arrayWorkList,.arrayResponse,"P",pidLocal,2)

	set count = ""
	set count = $order(arrayResponse(count))

	write "<table width='100%' cellspacing='1'>"
	write "<tr style='background-color:#2c6c7f;color:white'>"
	write "	<th width='50'>Prioridade</th>"
	write " <th width='60'>Ordem</th>"
	write "	<th width='110'>Status</th>"
	write " <th>Kit</th>"	
	write "	<th width='50'>Quant.</th>"
	write "</tr>"
	
	while (count '= "") {
		
		quit:(count > 5)
		
		set idProduto   = $piece(arrayResponse(count),Y,1)
		set descProduto = $piece(arrayResponse(count),Y,2)
		set Quantity 	= $piece(arrayResponse(count),Y,4)		
		set WorkID	 	= $piece(arrayResponse(count),Y,7)
		set WorkStatus 	= $piece(arrayResponse(count),Y,8)	
	  	
	  	set descStatus = WorkStatus
	
		if (WorkStatus '= "") {
			set descStatus = $piece($get(^WWW101(YM,"INMOSTATUS","PT",WorkStatus,1)),Y,1)
		} else {
			set descStatus = ""
		}

		if (WorkStatus < 2) {
			set image = YGIF_"alertRed.gif"
		} elseif (WorkStatus = 2) {
			set image = YGIF_"alertYellow.gif"
		} elseif (WorkStatus > 2)  {
			set image = YGIF_"alertBlue.gif"
		} else {
			set image = ""
		}	  	
	  	
	  	if (count#2=0) set bkgColor = "#F0F0F0"
	  	if (count#2=1) set bkgColor = "#E0E0E0"
	  	
	  	if (WorkID = "") {
		  	set WorkID = "Aguard."
	  	} else {	  	
			set redirectLink = YAKTION_"EP=WWWFORM&YFORM=INMO&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YKEY="_WorkID
			set WorkID = "<a href='"_redirectLink_"' title='Clique para ver os detalhes da ordem.'>"_WorkID_"</a>"
	  	}	  	
	  	
		write "<tr style='background-color:"_bkgColor_"'>"
		write "	<td style='background-image: url("_image_");background-repeat:no-repeat;background-position:center'>&nbsp;"_count_"</td>"
		write " <td>"_WorkID_"</td>"
		write " <td>"_descStatus_"</td>"
		write " <td>"_descProduto_"</td>"		
		write " <td align='right'>"_$$^WWWTR(0,12,Quantity)_"</td>"
		write "</tr>"

		set count = $order(arrayResponse(count))
	}

	write "</table>"
	
	quit
	
	
PrintActivityStream(pidLocal)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe a tabela de atividades recentes.
    ;
    ; Chamado por:
    ;	VARDashboardFracionamento.csp
    ; 
    ; Histórico:
    ; 	22-Fev-2012		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidLocal = "")
	
	new rsActivityScream, SQLQuery, rsActivityScream, dataHora, idProduto, tipo, data, 
		hora, lastData, usuario
	
    set rsActivityScream = ##class(%ResultSet).%New()  ;Create Result Set Object
	
	set SQLQuery = ""
	
	set SQLQuery = SQLQuery_" SELECT TOP 10 ConcluidoEm, ConcluidoPor, Produto, 'Unit' as Tipo"
	set SQLQuery = SQLQuery_" FROM VAROrdemUnitarizacao"
	set SQLQuery = SQLQuery_" WHERE Status = 4"
	set SQLQuery = SQLQuery_" AND Local1 = "_pidLocal
	
	set SQLQuery = SQLQuery_" UNION ALL"
	
	set SQLQuery = SQLQuery_" SELECT TOP 10 ReceiptFirmedDate, ReceiptFirmedBy, ItemCode, 'Kit' as Tipo"
	set SQLQuery = SQLQuery_" FROM INMO"
	set SQLQuery = SQLQuery_" WHERE Status = 5"
	set SQLQuery = SQLQuery_" AND ProductionLocation = "_pidLocal	
	
	set SQLQuery = SQLQuery_" ORDER BY 1 DESC"

    do rsActivityScream.Prepare(SQLQuery)  ;Prepare Query
    do rsActivityScream.Execute()  		   ;Execute Query
	
	write "<table width='100%' cellspacing='0'>"
	
	set lastData = ""
	set count 	 = 0

    while (rsActivityScream.Next()) {

        set dataHora  = rsActivityScream.GetData(1)
        set usuario   = rsActivityScream.GetData(2)
        set idProduto = rsActivityScream.GetData(3)
        set tipo      = rsActivityScream.GetData(4)
        
        set data = $piece(dataHora,",",1)
        set hora = $piece(dataHora,",",2)
        
        if (usuario '= "") {
	        set nomeUsuario = " por "_$$SQLGetUserName^VARSQL(usuario)
        } else {
	        set nomeUsuario = ""
        }
        
        //Não repetir os dias
        if ( (lastData = "") || (data '= lastData) ) {
				write "<tr style='background-color:#2c6c7f;color:white;font-weight:bold'>"
				write " <td class='padder'>"_$$FormatDiaPT(data)_"</td>"
				write "</tr>"
    	}
    	
    	set count = $increment(count)
    		  	
	  	if (count#2=0) set bkgColor = "#dfe9c9"
	  	if (count#2=1) set bkgColor = "white"
    	
		write "<tr style='background-color:"_bkgColor_"'>"
		write "	<td class='padder'><strong>"_$ztime(hora,2)_"</strong> - "
		
		if (tipo = "Unit") {			
			write "Concluído o fracionamento do produto "_$$SQLGetDescricaoProduto^VARSQL(idProduto)_nomeUsuario
		} elseif (tipo = "Kit") {
			write "Concluída a montagem do kit "_$$SQLGetDescricaoProduto^VARSQL(idProduto)_nomeUsuario
		}			

		write " </td>"
		write "</tr>"
		
		set lastData = data
    }
    
    do rsActivityScream.Close()
    
	write "</table>"
	
	quit
	
PrintTabelaResumo(pTipo,pidLocal)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe a tabela resumo abaixo dos gráficos.
    ;
    ; Chamado por:
    ;	VARDashboardFracionamento.csp
    ; 
    ; Histórico:
    ; 	22-Fev-2012		Criado
    ;-------------------------------------------------------------------------------
	quit:( (pTipo = "") || (pidLocal = "") )
	
	new currentDate, competencia, mediaMensal12Meses, mediaDiaria12Meses, quantAcumuladaDia, 
		quantAcumuladaMes, percDiario, percMensal, colorDia, colorMes
	
	set currentDate = $zdate($horolog,4)
	set competencia = $extract(currentDate,7,11)_$extract(currentDate,4,5)

	set mediaMensal12Meses = +$$GetMediaMensal^VARDWProducaoMensal(pTipo,pidLocal)
	set mediaDiaria12Meses = +$$GetMediaDiaria^VARDWProducaoMensal(pTipo,pidLocal)
	set quantAcumuladaDia  = +$$GetQuantProducaoDiaria^VARDWProducaoDiaria(pTipo,pidLocal,$piece($horolog,",",1))
	set quantAcumuladaMes  = +$$GetQuantProducaoMensal^VARDWProducaoMensal(pTipo,pidLocal,competencia)	
	
	if (quantAcumuladaDia = 0) {
		set colorDia = "black"
	} elseif (quantAcumuladaDia >= mediaDiaria12Meses) {
		set colorDia = "blue"
	} else {
		set colorDia = "red"
	}
	
	if (quantAcumuladaMes = 0) {
		set colorMes = "black"
	} elseif (quantAcumuladaMes >= mediaMensal12Meses) {
		set colorMes = "blue"
	} else {
		set colorMes = "red"
	}		
	
	if ( (mediaDiaria12Meses > 0) && (quantAcumuladaDia > 0) ) {
		set percDiario = (quantAcumuladaDia * 100) / mediaDiaria12Meses
		set percDiario = "<font color='"_colorDia_"'>"_$$^WWWTR(0,12,percDiario,1)_"%</font>"
	} else {
		set percDiario = "0,0%"
	}
	
	if ( (mediaMensal12Meses > 0) && (quantAcumuladaMes > 0) ) {
		set percMensal = (quantAcumuladaMes * 100) / mediaMensal12Meses
		set percMensal = "<font color='"_colorMes_"'>"_$$^WWWTR(0,12,percMensal,1)_"%</font>"
	} else {
		set percMensal = "0,0%"
	}
	
	if (pTipo = 1) { 
		set descTipo = "Fracionamento"
	} elseif (pTipo = 2) {
		set descTipo = "Montagem de Kits"	
	}
	
	write "<table class='tabelaResumo'>"
	write "	<tr>"
	write "		<td colspan='6' style='padding-bottom:4px;'><strong>Resumo - "_descTipo_"</strong></td>"
	write "	</tr><tr>"
	write "		<td nowrap='nowrap'>Média diária (12 meses):</td>"
	write "		<td align='right' style='padding-right:30px;padding-left:4px;font-weight:bold'>"_$$^WWWTR(0,12,mediaDiaria12Meses)_"</td>"
	write "		<td>Acumulado dia:</td>"
	write "     <td align='right' style='padding-right:30px;padding-left:4px;font-weight:bold'>"_$$^WWWTR(0,12,quantAcumuladaDia)_"</td>"
	write "		<td>Desempenho:</td>"
	write "		<td align='right' style='padding-left:4px;font-weight:bold'>"_percDiario_"</td>"	
	write "	</tr><tr>"
	write "		<td nowrap='nowrap'>Média mensal (12 meses):</td>"
	write "		<td align='right' style='padding-right:30px;padding-left:4px;font-weight:bold'>"_$$^WWWTR(0,12,mediaMensal12Meses,)_"</td>"
	write "		<td nowrap='nowrap'>Acumulado mês:</td>"
	write "		<td align='right' style='padding-right:30px;padding-left:4px;font-weight:bold'>"_$$^WWWTR(0,12,quantAcumuladaMes)_"</td>"
	write "		<td>Desempenho:</td>"
	write "		<td align='right' style='padding-left:4px;font-weight:bold'>"_percMensal_"</td>"
	write "	</tr>"
	write "</table>"
	
	
	quit
	
getSeries(pTipo,pidLocal,pAno)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna a série no formato [n1,n2,n3] para um ano.
    ;
    ; Chamado por:
    ;	VARDashboardFracionamento.csp
    ; 
    ; Histórico:
    ; 	22-Fev-2012		Criado
    ;-------------------------------------------------------------------------------
	quit:( (pTipo = "") || (pidLocal = "") || (pAno = "") )

	new strSeries, strCompetencia, mes, mesRef, competencia, qtdeMensal

	set strSeries = ""	
	set strCompetencia = ""
    
    for mes = 1:1:12 {
	    
	    //Formatando 01, 02, 03 ao invés de 1, 2, 3.
	 	if (mes < 10) {
		 	set mesRef = "0"_mes
	 	} else {
		 	set mesRef = mes	
	 	}
	 	
	 	set competencia = pAno_mesRef	 	
	 	set qtdeMensal = +$piece($get(^VARDWProducaoMensal(YM,pTipo,pidLocal,competencia,1)),Y,1)

		if (strSeries = "") {
			set strSeries = "["_qtdeMensal
		} else {
			set strSeries = strSeries_","_qtdeMensal
		}
		
		set competencia = $$RetornaMesCompetencia($extract(competencia,5,6),$$$YES)
		if (strCompetencia = "") {			
			set strCompetencia = "['"_competencia_"'"
		} else {
			set strCompetencia = strCompetencia_",'"_competencia_"'"
		}
		
    }
    
	set strSeries = strSeries_"]"
	set strCompetencia = strCompetencia_"]"
	
	quit strSeries_"||"_strCompetencia

	
FormatDiaPT(pDay)
	;-------------------------------------------------------------------------------
    ; Descrição: retorna a data em português no formato '01 Janeiro 2011'.
    ; 
    ; Histórico:
    ; 	28-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	new dte, month, descMesPT, diaPT, diaPT2
	
	set dte = $zdate(pDay,4)
	set month = $extract(dte,4,5)
	
	set descMesPT = $$RetornaMesCompetencia(month)
 	
 	set diaPT  = $$Replace^COMUtilStr($extract(dte,4,5),month,descMesPT)
	set diaPT2 = $$Replace^COMUtilStr($extract(dte,1,2)_"&nbsp;"_diaPT_$extract(dte,6,10),"/","&nbsp;")	
 
	quit diaPT2	
	
	
RetornaMesCompetencia(month,abreviado=$$$NO)
	quit:(month = "") ""
	
	new descMesPT
	set descMesPT = $case(month,"01":"Janeiro", "02":"Fevereiro", "03":"Março",
								"04":"Abril",   "05":"Maio",      "06":"Junho",
								"07":"Julho",   "08":"Agosto",    "09":"Setembro",
								"10":"Outubro",  "11":"Novembro", "12":"Dezembro")
								
	if (abreviado = $$$YES) { 
		quit $extract(descMesPT,1,3)
	}
	
	quit descMesPT