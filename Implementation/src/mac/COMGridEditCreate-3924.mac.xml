<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEditCreate" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEditCreate ; PROOF OF CONCEPT - WORK IN PROGRESS - DO NOT USE
	
	; Attempt to consolidate multiple tables for grid into one.
	
#include WWWConst
#include COMConst
#include COMGridEdit31
	
#def1arg WWWDATEN(%args) ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",%args)
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^CGECreate("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	;  *1  COMGridEdit31Head  (Followed by tag creating that part of the grid)
	;  *2  COMGridEdit31
	;  *3  COMGridEdit31Body
	;  *4  COMGridEdit31Body2
	;  *5  WWW120DynTable
	;  
	;  *6  COMGridEditCreate
	;-------------------------------------------------------------------------------
	
	; PREVIOUS
	; =============
	
	;gridDIV			;-----------------------------------------------------------------------------
	; |					; Key Name : Key Value       [optional - ^CacheTemp(YUSER,"Grid","ShowKeys") ]  *1 HEAD
	; |					;  Per Page [  ]   Pages: 1  2  3                           [ ] [ ] [ ] [ ]  |  *1 Pages / Button
	; |					;-----------------------------------------------------------------------------
	; |  | gridheadDIV	;    |  P1  |  P2  |       |       |       |       |       |                 |  *1 HeadStart
	; |  |  |			;Line| Name | Name | xxxxx | xxxxx | xxxxx | xxxxx | xxxxx |                 |  *3 Head
	; |  |  +-			;-----------------------------------------------------------                 |
	; |  | gridbodyDIV	;    |      |      |       |       |       |       |       |                 |  *4 CreateData
	; |  |  |			;-----------------------------------------------------------                 |  *3 AddLines
	; |  |  |			;    |      |      |       |       |       |       |       |                 |  *3 DISPLAYLINE
	; |  |  |			;-----------------------------------------------------------                 |  *3 BodyFieldCell
	; |  |  |			;    |      |      |       |       |       |       |       |                 |
	; +- +- +-			;-----------------------------------------------------------                 |
	;DynamicArea		;                                                                            |
	; | DYNAMIC_div		;-----------------------------------------------------------------------------
	; |	 |				; Dyna Table                                                                 |  *5 CreateDynTableCall
	; |	 |				;                                                                            |
	; +- +-				;-----------------------------------------------------------------------------
	;					; Developer Info                                                             |
	;					; AF: [       ]  FF: [      ] T: [        ]                                  |
	;---				;-----------------------------------------------------------------------------
	
	
	; PROPOSED
	; =============
	
	;gridDIV			;-----------------------------------------------------------------------------
	; |    table		; Key Name : Key Value       [optional - ^CacheTemp(YUSER,"Grid","ShowKeys") ]  *6 Header
	; |					;  Per Page [  ]   Pages: 1  2  3                           [ ] [ ] [ ] [ ]  |    *1 Pages / Button
	; | gridtableDIV	;-----------------------------------------------------------------------------  *6 GridBase
	; |  | table		;    |  P1  |  P2  |       |       |       |       |       |                 |    
	; |  |  thead		;Line| Name | Name | xxxxx | xxxxx | xxxxx | xxxxx | xxxxx |                 |    *3 Head
	; |  |  +-			;-----------------------------------------------------------                 |
	; |  |  tbody		;    |      |      |       |       |       |       |       |                 |    *4 CreateData
	; |  |  |			;-----------------------------------------------------------                 |    *3 AddLines
	; |  |  |			;    |      |      |       |       |       |       |       |                 |    *3 DISPLAYLINE
	; |  |  |			;-----------------------------------------------------------                 |    *3 BodyFieldCell
	; |  |  |			;    |      |      |       |       |       |       |       |                 |
	; +- +- +-			;-----------------------------------------------------------                 |
	;DynamicArea		;                                                                            |
	; | DYNAMIC_div		;-----------------------------------------------------------------------------
	; |	 |				; Dyna Table                                                                 |  *5 CreateDynTableCall
	; |	 |				;                                                                            |
	; +- +-				;-----------------------------------------------------------------------------
	;					; Developer Info                                                             |
	;					; AF: [       ]  FF: [      ] T: [        ]                                  |
	;---				;-----------------------------------------------------------------------------
	
	
CreateGrid(YDATEI,YKEY,YMAXKEY,pblnTabOnly)
	;-------------------------------------------------------------------------------
	; Creates the header and body of the grid
	; 
	; Called By: Start^COMGridEdit31
	; 
	; Params:	YDATEI		- parent class
	; 			YKEY		- parent key
	; 			YMAXKEY		- number of grid keys
	; 			blnTabOnly 	- whether just changing tabs
	;
	; Returns:
	;
	; History:
	; 14-Feb-2012	shobby	SR17724: Changes to grid width management.
	; 27-Sep-2011	shobby	SR17853: Reverted doCancelEvent
	; 24-Mar-2011	shobby	SR17687: Removed unnecessary cancelBubble
	;-------------------------------------------------------------------------------
	new blnFocusGrid,blnSharedForm,intDisplay,intFormFocus,intLeft
	new intMaxHeight,intMaxWidth,intTop,strFocus,idRow,intPage
 	
	set intLeft       = +$get(^CacheTemp(YUSER,"Grid","Left"))
	set intTop        = +$get(^CacheTemp(YUSER,"Grid","Top"))
	set blnSharedForm = +$get(^CacheTemp(YUSER,"Grid","SharedForm"))
	set intMaxWidth   =  $get(^CacheTemp(YUSER,"Grid","MaxWidth"),"document.body.clientWidth-gridDIV.offsetLeft-16")
		
	set intMaxHeight = +$get(^CacheTemp(YUSER,"Grid","MaxHeight"))
	if intMaxHeight<1 set intMaxHeight = 9999999999999
	set intMaxHeight = "getGridHeight("_intMaxHeight_","_+$$$WWW012IgnoreMaximumHeight($get(^WWW012(0,YM,1)))_")"
	
	write YCR,"<input type=""hidden"" id=""nextactivefield"" value="""">"
	write YCR,"<input type=""hidden"" id=""activegrid"" value="""">"
	write YCR,"<input type=""hidden"" id=""sharedform"" value="""_blnSharedForm_""">"
	
	;---------------------------------------
	
	; *** COMView External Filter generated somewhere here
	;     or generated later and ends up displayed in the DIV <GRF>
	
	; SR17416 : expression() is a deprecated MSIE-only feature
	
	if blnSharedForm {
		write YCR,"<DIV TABINDEX=10000 id='gridDIV' class=gDIVsh " 
	;	write "style=' left:"_intLeft_"px; top:"_intTop_"px; height:expression("_intMaxHeight_"); width:expression("_intMaxWidth_"); ' "
		if '$$GridVersion^COMGridEdit31V2() {
			write "style=' table-layout:fixed; left:"_intLeft_"px; top:"_intTop_"px; height:300px; width:1000px; height:expression("_intMaxHeight_"); width:expression("_intMaxWidth_"); ' "
		} else {
		    write "style=' table-layout:fixed; left:"_intLeft_"px; top:"_intTop_"px; height:300px; width:100%; height:expression("_intMaxHeight_"); ' "
		}
	} else {
		write YCR,"<DIV TABINDEX=10000 id='gridDIV' class=gDIVex "
	;	write "style=' left:"_intLeft_"px; top:"_intTop_"px; height:expression("_intMaxHeight_"); width:expression("_intMaxWidth_"); ' "
		if '$$GridVersion^COMGridEdit31V2() {
			write "style=' table-layout:fixed; left:"_intLeft_"px; top:"_intTop_"px; height:300px; width:1000px; height:expression("_intMaxHeight_"); width:expression("_intMaxWidth_"); ' "
		} else {
			write "style=' table-layout:fixed; left:"_intLeft_"px; top:"_intTop_"px; height:300px; width:100%; height:expression("_intMaxHeight_");' " ;SR17724
		}
	}
	
	; FF: onfocusin  IE: onfocus
	write YCR," "_$$Event^WWWFORMCrossBrowserSupport("onfocusin")_"='var retval=SetFocus(""Grid""); window.event.returnValue = false; window.event.cancelBubble = true;'"  //SR17253 ;SR17853
	write YCR," oncontextmenu='cgeRightClick(event); window.event.returnValue = false; window.event.cancelBubble = true;'"  //SR17253 SR17413 ;SR17853
 
	//SR17687 write YCR," onclick =' cgeTest2(event); event.cancelBubble=true; ' " ;  //SR17253
	write YCR," onclick ='cgeTest2(event)'" ;  //SR17253 //SR17687
	write YCR,">"
	
	write YCR,"<a name='grid'>"
	;write "</a>"
	
	;do HEAD^COMGridEdit31Head(YDATEI,YKEY,YMAXKEY)
	;do BODY^COMGridEdit31
	
	do Header(YDATEI,YKEY,YMAXKEY)
	do GridBase()
	
	write YCR,"</DIV>"
	
	;---------------------------------------
	
 	if $data(^WWW120DynTable(0,$$$GRIDContainer)) {
	 	write "<div id='DynamicArea'>"
	 	do CreateDynTableCall^WWW120DynTable($$$GRIDContainer,YFORM,YSEITE)
		write YCR,"</div>"
 	}
	
	;---------------------------------------
	
	if $$$DEVMODE {
		write YCR,"<b>AF: <input id=""activefield"" value="""" onclick=""window.open('/global/dv.html');"">"
		write YCR,"FF: <input id=""focusfield"" value="""_$get(YFOCUSID)_""">"
		write YCR,"T:<input id=""test"" value="""_$get(YFOCUSID)_"""></b>"
	} else {
		write YCR,"<input type=""hidden"" id=""activefield"" value="""">"
		write YCR,"<input type=""hidden"" id=""focusfield"" value="""_$get(YFOCUSID)_""">"
		write YCR,"<input type=""hidden"" id=""test"" value="""_$get(YFOCUSID)_""">"
	}
	write YCR,YCR
	$$$StartScript()
	write YCR,"InitGlobals('"_YUSER_"','"_YUCI_"','"_YFORM_"','"_$$GetDateFormat^COMGridEdit31()_"','"_$zconvert(YGIF,"o","JS")_"','"_$get(YBED)_"');"
	write YCR,"  focusfield=document.getElementById('focusfield').value;"
	write YCR,"  if (focusfield != '') { "
	write YCR,"     var thefield=document.getElementById(focusfield);"
	write YCR,"	    if (thefield!=null) {"
	write YCR,"		   thefield.parentNode.firstChild.style.fontWeight='bold';"
	write YCR,"     } "
	write YCR,"  } "
	
	do StoreColumnOrder^COMGridEdit31(YFORM)
	do Head^COMGridEdit31Body(YKEY,YFORM,YMAXKEY,"gridheadRow")
	
	if 'pblnTabOnly {
		if '$get(^CacheTemp(YUSER,"Grid","COMView")) {
			do CreateData^COMGridEdit31Body2(YKEY,YFORM)
		}
		do AllCallBacks^COMGridEdit31S()
	
	} else {
		if '$get(^CacheTemp(YUSER,"Grid","COMView")) {
			do AddLines^COMGridEdit31Body(YKEY,YFORM)
		}
	}
	do OnAfterDataFields^COMGridEdit31Events()
	
	set intPage      = $get($$$GRIDPage,1)
	// If we are in form where the focus has been specifically set, do not focus the grid.
	set intFormFocus = $$$WWW120FirstFocusToDataFieldNumb($get(^WWW120(0,$$$GRIDContainer,1)))
	set blnFocusGrid = $$$YES
	
	if intFormFocus'="" {
		set intDisplay = $$$WWW122DisplayOnPageNumber($$Get^WWW122(YFORM,intFormFocus))
		if YSEITE=intDisplay {
			set blnFocusGrid = $$$NO
			set strFocus     = ""
		}
	}

 	if blnFocusGrid {
		set strFocus="T"                         //Default Focus = Top
		if $get(^CacheTemp(YUSER,"GridLine"))'="" {
			set idRow = $get($$$WWWDATEN("REFERENCEROW",$get(^CacheTemp(YUSER,"GridLine")),1))
			if idRow'="" {
				set intPage  = $$GetPage^COMGridEdit31R(idRow)
				set strFocus = $$$GRIDGetFirstCell(idRow)
				kill ^CacheTemp(YUSER,"GridLine")
			}
		} else {
			if '$$IsScanReq^WWW120Scan(YFORM) {
				do AddDefaultLine^COMGridEdit31()
			}
		}
	}
	if intPage > $$$GRIDNumPages set intPage = $$$GRIDNumPages
	
	write YCR,$$$GRIDGoToPage(intPage,$$$GRIDNumPages,strFocus,$$$NO)
	
	$$$EndScript()
	
	write YCR
	quit
	
	
Header(YDATEI,YKEY,YMAXKEY)	  ;TABLE HEADER
    ;-------------------------------------------------------------------------------
    ; Displays primary keys for this class along top of grid area.
    ; 
    ; Params:	YDATEI	- parent class
    ;			YKEY	- parent key
    ;			YMAXKEY	- number of grid keys
    ; 
    ; Returns:
    ;
    ; History:
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 05-Jan-2010	GRF		SR15525: Call to $$Convert^WWWTR rather than $$^WWWTR
    ; 09-Aug-2009	GRF		Doco
    ; 25-May-2009	PPP		SR16565: Scanning Prompt for Grid, Only if grid is not read-only
    ; 28-Nov-2008	HQN		SR16194: Added support for OnBeforeButtonLine for 
    ; 							GridEdit forms
    ; 31-Oct-2006	JW		BR014279: Added parameters
    ; 31-Aug-2006	JW		SR14907: Switch for key display. Removed excess TH elements
    ; 19-Oct-2005	JW		SR11573: Paging
    ; 16-Dec-2004	shobby	Change parameter PARA to @PARA in $$KEYBUILD. (To evaluate
    ;							YM for example, rather than use "YM")
    ; 15-Dec-2004	Shobby	Put all primary keys on one line to save space.
    ;-------------------------------------------------------------------------------
	new YTEXT,YSEI,YZEILE,YSPL,YBBN,YTXT,YKLFN,YWIDTH,YINHALT,YTYP,REL,PARA,RELF,SUCH
	new YHEIGHT,YFONT,YFONTSIZE,YV
	
	$$$LogR("Header",YDATEI_";"_YKEY_";"_YMAXKEY)
	
	write YCR,"<table cellspacing=0 cellpadding=0>"

	set YHEIGHT   = 14
	set YFONT     = "arial"
	set YFONTSIZE = 8
	
	;---------------------------------------
	
	if $get(^CacheTemp(YUSER,"Grid","ShowKeys")) {
		write YCR,"<tr>"
		write YCR,"<th nowrap class='TH' style=' font:"_YFONTSIZE_"pt arial; height:"_YHEIGHT_"pt;'>"
		for YKLFN=1:1:YMAXKEY {
			quit:$piece(YKEY,",",YKLFN)=""
			
			write "<B>"_$$^WWWFELDNAME(YFORM,"P",YKLFN)_" : </B>"
			
			set YINHALT = $piece(YKEY,",",YKLFN)
			
			set YDAT  = $get(^WWW002(0,YDATEI,YKLFN,1))  ;DATENFELDDEFINITION
			set YTYP  = $piece(YDAT,Y,3)                 ; D3		$$$WWW002InputType()
			set REL   = $piece(YDAT,Y,8)                 ; D8		$$$WWW002RelationClass()
			set PARA  = $piece(YDAT,Y,9)                 ; D9		$$$WWW002RelationalPrimaryKeys()
			if PARA'="" if '$find(PARA,",") if $extract(PARA)'="""" if $get(@(PARA))="" write YTEXT quit
			if PARA'="" if $find(PARA,",") for YV=1:1  quit:$piece(PARA,",",YV,99)=""  if $extract($piece(PARA,",",YV))'="""" set:$get(@($piece(PARA,",",YV)))="" PARA=""  if PARA="" write YTEXT quit  ;KEY VORDATEI MIT MEHREREN VARIABLEN
			set RELF  = $piece(YDAT,Y,10)                ; D10		$$$WWW002RelationalDisplayItems()
			set YTEXT = $$GetLiteral^WWWTR(YTYP,YINHALT)
			
			if (REL="") || (+RELF=0) { ; don't show if no relation
				write YTEXT
			
			} else {
				set SUCH = "^"_REL_"("""_YM_""""  ;ZUSAMMENBAU DER GLOBALREFERENZ
				if PARA'="" set SUCH = SUCH_","_$$^WWWKEYBUILD(@PARA)
				set SUCH = SUCH_","""_$translate(YTEXT,"""")_""",1)"
				if ($data(@SUCH)#2)=0 {
					write YTEXT
				} else {
					write YTEXT_"&nbsp;("_$piece(@SUCH,Y,RELF)_")"
				}
			}
			write YCR," &nbsp; &nbsp; &nbsp; &nbsp;"
		}
		write YCR,"</th></tr>"
	}
	
	;---------------------------------------
	
	write YCR,"<tr>"
 
	// Only if grid is not read-only
	if $get(^CacheTemp(YUSER,"Grid","Enabled")) {
		if $$IsScanReq^WWW120Scan(YFORM) {
			do ScanData^COMGridEdit31Head(YFONT,YFONTSIZE)
		}
	}
	
	do Pages^COMGridEdit31Head(YFONT,YFONTSIZE)
	do OnBeforeButtonLine^COMGridEdit31Events()
	do Buttons^COMGridEdit31Head(YKEY,YFORM,YFONTSIZE)
	write YCR,"</tr>"
	
	;---------------------------------------
	
	write YCR,"</TABLE>"
	 
	//write $$HeadStart^COMGridEdit31Head()
	quit
	
	
GridBase()
	;-------------------------------------------------------------------------------
	; Start table
	;
	; DIV			gridtableDIV	gDIVhd
	;   TABLE		gridtable		TABLEhd
	;     THEAD	
	;     TBODY 
	; 		
	; // 		TABLE:  gridhead2		- additional charges
	;
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Apr-2011	shobby	SR17724: Removed old commented code.
	; 02-Mar-2011	shobby	SR17673: Removed unused nonexistent property 'resizing'
	; 30-Jun-2010	SHOBBY	SR17325.2: changed DIV to "table-layout:auto" with
	;							"display:-moz-box;"
	; 29-Jun-2010	GRF		SR17377: Apply "table-layout:fixed" to div and table
	; 06-Sep-2006	JW		SR14484: Moved 2nd header from Head^COMGridEdit31Body
	;-------------------------------------------------------------------------------
	new strHTML
	
	$$$LogR("GridBase","")
	
	set strHTML = "<DIV id='gridtableDIV' class='gDIVhd'"
	set strHTML = strHTML_" style='display:-moz-box; table-layout:auto; position:relative;"
	set strHTML = strHTML_"  overflow-x:hidden; overflow-y:hidden; width:gridbodyDIV.clientWidth; z-index:0;'>"
	set strHTML = strHTML_"<TABLE cellspacing=0 cellpadding=0 id='gridhead' class='TABLEhd' style='table-layout:fixed;'>" ;SR17673

	set strHTML = strHTML_"<thead>"
	set strHTML = strHTML_"<tr id='gridheadRow'>"
	set strHTML = strHTML_"</tr></thead>"
	
	set strHTML = strHTML_"<tbody id='TBODY'>"
	set strHTML = strHTML_"</tbody>"
	
	set strHTML = strHTML_"</TABLE></DIV>"
	quit strHTML

PopulateGridHead(YKEY,YFORM,YMAXKEY,pstrRow,pblnExpandLinesAllowed=$$$YES) // Head^COMGridEdit31Body
	;-------------------------------------------------------------------------------
	; Wrapper for two header functions. Create additional header if needed.
	; 
	; Called By: CreateGrid^COMGridEdit31; self-reference (commented)
	; 
	; Params:
	;	pstrRow			e.g. "gridheadRow"
	;
	; Returns:
	;
	; History:
	; 06-Dec-2010	GRF		SR17597: pass flag as argument to HeadFields
	; 08-Jul-2010	CB		SR17403: relocate syncGridWidths to CGE31J4
	; 04-Jun-2010	GRF/CB	SR17325: call syncGridWidths
	; 06-Sep-2006	JW		SR14484: Moved 2nd header to HeadStart^COMGridEdit31Head
	; 28-Sep-2005	JW		SR11573: Created
	;-------------------------------------------------------------------------------
	new strLink
	
	write YCR,$$HeadPrimaryKey(YKEY,YFORM,YMAXKEY,pstrRow,pblnExpandLinesAllowed)
	write YCR,$$HeadFields(YFORM,pstrRow,pblnExpandLinesAllowed)
	
	/*
	if pblnExpandLinesAllowed {
		set strLink=$get($$$WWWDATEN("LINKS"))
		if strLink'="" {
			do PopulateGridHead(YKEY,strLink,YMAXKEY,"gridheadRow2",$$$NO)
		}
	}
	*/
	quit
	
	
HeadPrimaryKey(YKEY,YFORM,YMAXKEY,pstrRow,pblnExpandLinesAllowed=$$$YES)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;	pstrRow			e.g. "gridheadRow"
	;
	; Returns:
	;
	; History:
	; 07-Feb-2012	shobby	SR178724: Changed some images.
	; 06-Dec-2010	GRF		SR17597: blnAttach is used to disable the attachEvent
	;							operation for OnMouseDown and OnMouseMove when
	;							called for expanded grids (based on multiple forms)
	;							This is a work around since the column compression
	;							problem only occurs in FF & only on expanded grids.
	;							Put [+] button in centre of column with width=50
	;							rather than on RHS with width from first key.
	; 05-Nov-2010	shobby	SR17605: Call out to cgeHeadFields wrapper
	; 18-May-2010	GRF		SR17253.1: use .getElementById
	; 14-Apr-2010	shobby	SR17253: Firefox compatibility
	; 01-May-2009	shobby	SR16507: Show the grid created date on the perpageheader
	; 							object. (Less intrusive but still available)
	; 30-Jul-2007	GRF		SR15571: strParams for repeated values
	; 06-Mar-2007	PO		SR15466: Pass YDAT by reference
	; 27-Jun-2006	JW		SR13755: Store key names, get them properly.
	; 09-Mar-2005	JW		SR11848: Added $$$NO - hidden paramater.
	; 17-Dec-2004	Shobby	Use 'text in forms' as the caption (SR10061)
	;-------------------------------------------------------------------------------
	new blnAttach,blnHidden,strHTML
	new YTXT,YLFN,YTYP,REL,PARA,RELF,SUCH,YFIELDNAME,YLENGTH,YWIDTH,YCOLON,YCOLOFF,YVALIGN,YPADDING
	new YHEIGHT,YFONT,YFONTSIZE,YFELDNAME,YKLFN,YTEXT ;SR17605 ,strParams
	
	set strHTML = ""
	write !,"document.getElementById('perpageheader').title=document.getElementById('perpageheader').title+cgeGridDate();"  ;SR17253
 
	for YKLFN=1:1:YMAXKEY {
		if $piece(YKEY,",",YKLFN)="" {     ; Ignore populated keys - usually leaves only YMAXKEY
			do PARA^COMGridEdit31Body("P",YKLFN,.YWIDTH,.YVALIGN,.YHEIGHT,.YFONT,.YFONTSIZE,.YTYP,.YFORM,.YLFN,.YTXT,.YDAT)  ;SET PARAMETER // SR15466
			set blnHidden = $select($piece(YTXT,Y,104)=15:1,1:0)   ; D104 $$$WWW121TypeOfAcquisition : WWW100("EINGABE") = Hidden
			
			if (pblnExpandLinesAllowed) && ($get(^CacheTemp(YUSER,"Grid","ExpandLines"))) {     ; SR17597
				set blnAttach = $$$NO
			} else {
				set blnAttach = $$$YES
			}
			
			set YFELDNAME = $$^WWWFELDNAME(YFORM,"P",YKLFN)
			set ^CacheTemp(YUSER,"Grid","KeyName",YKLFN) = YFELDNAME
			
	;SR17605set strParams = YFONT_"','"_YFONTSIZE_"','"_YWIDTH_"','"_YHEIGHT_"','"_YDARKGRAY_"','"_YTYP_"','"_blnHidden_"','"_YFORM
	;SR17605;                #5            #6             #7           #8             #9            #10          #11           #12
			set strHTML = strHTML_YCR_$$cgeHeadFields^COMGridEdit31JSFunctionWrappers(pstrRow,"Key",YKLFN,YFELDNAME,YFONT,YFONTSIZE,YWIDTH,YHEIGHT,YDARKGRAY,YTYP,blnHidden,YFORM,blnAttach) ;SR17605
			if (pblnExpandLinesAllowed) && ($get(^CacheTemp(YUSER,"Grid","ExpandLines"))) {
				set YTEXT   = "<IMG style='margin-left:4px;' id=Expand align=left SRC="""_YGIF_"cplus_small.gif"" onclick=""cgeExpandAll;"">"
				
				set strHTML = strHTML_YCR_$$cgeHeadFields^COMGridEdit31JSFunctionWrappers(pstrRow,"Key","Expand",YTEXT,YFONT,YFONTSIZE,30,YHEIGHT,YDARKGRAY,YTYP,blnHidden,YFORM,$$$NO) ;SR17605
				set strHTML = strHTML_YCR_"THkeyExpand.detachEvent('onclick',cgeonclick);"
				set strHTML = strHTML_YCR_"THkeyExpand.attachEvent('onclick',cgeExpandAll);"
			}
		}
	}
	quit strHTML
	
	
HeadFields(YFORM,pstrRow,pblnExpandLinesAllowed)
	;-------------------------------------------------------------------------------
	;
	; Returns:
	;
	; History:
	; 06-Dec-2010	GRF		SR17597: pass flag as argument; pass blnAttach to
	;							cgeHeadFields
	; 05-Nov-2010	shobby	SR17605: Call out to cgeHeadFields wrapper
	; 30-Jul-2007	GRF		SR15571: loop instead of idx
	; 10-Jul-2007	shobby	SRBR014553: Form field definition is obtained from standard method.
	;						No longer necessary to reference customisation values.
	; 05-Apr-2007	GRF		SRBR014310: EnumFType macros
	; 02-Mar-2007	PO		SR15466: New and pass YDAT by ref to PARA
	; 25-Jan-2007	PO&JW	SR15280: Removed redundant cache.
	; 26-Jun-2006	JW		SR12775: Check and store mandatory property here.
	; 12-Sep-2005	JW		SR13099: Call GetColumnOrder directly.
	;  9-Mar-2005	JW		SR11848: Added isHidden property
	; 11-Feb-2005	Paul K	Pass in the type of column (for sorting purposes SR#11717)
	; 10-Feb-2005	shobby	Moved most of the code into Javascript.
	; 10-Dec-2004	RobertW	Allow YOPTION to show/hide the fields. (SR10061)
	; 20-Dec-2004	PO		SR11286 Incorrectly identifying currency
	;-------------------------------------------------------------------------------
	new blnAttach,blnHidden,loop,strDataType,strFieldType,strHTML,strSort,YBBN
	
	set strHTML = ""
	set strSort = $$GetColumnOrder^COMGridEdit31Body(YFORM)     ; comma-delimited string
	
	if ($get($$$WWWDATEN("LINKS"))'="") || 'pblnExpandLinesAllowed {    ; SR17597
		set blnAttach = $$$NO
	
	} else {
		set blnAttach = $$$YES
	}
	
	for loop=1:1:$length(strSort,",") {
		set YBBN = $piece(strSort,",",loop)
		new YTXT,YLFN,YTYP,REL,PARA,RELF,SUCH,YFIELDNAME,YLENGTH,YWIDTH,YCOLON,YCOLOFF,YVALIGN,YPADDING,YHEIGHT,YFONT,YFONTSIZE,YFELDNAME
		
		do PARA^COMGridEdit31Body("D",YBBN,.YWIDTH,.YVALIGN,.YHEIGHT,.YFONT,.YFONTSIZE,.YTYP,.YFORM,.YLFN,.YTXT,.YDAT)
		if $piece(YTXT,Y,1)="" {
			set strFieldType = "M"
		} else {
			set strFieldType = "D"					
		}
		
		set YFELDNAME = $$^WWWFELDNAME(YFORM,strFieldType,YLFN)
		
		if $find(",1,14,",","_YTYP_",") {                           ; Date, Timestamp
			set strDataType = "Date"
		
		} elseif $find(",4,8,12,",","_YTYP_",") {                   ; Num, Cur, Float
			set strDataType = "Number"
		
		} else {
			set strDataType = ""
		}
		
		set blnHidden = (($piece(YTXT,Y,2)=15) || '$$ShowCell^COMGridEdit31Body(YFORM,YBBN))
		
		if blnHidden {
			set YWIDTH    = 0
			set YFELDNAME = ""
		}
		if YFELDNAME="" set YFELDNAME = "&nbsp;"
	
		set strHTML = strHTML_YCR_$$cgeHeadFields^COMGridEdit31JSFunctionWrappers(pstrRow,"Field",YBBN,YFELDNAME,YFONT,YFONTSIZE,YWIDTH,,YDARKGRAY,strDataType,blnHidden,YFORM,blnAttach) ;SR17605 ; SR17597

		if $$$WWW003MandatoryField(YDAT) || $$$WWW122MandatoryInputItem(YTXT) {
			set $$$FLDMandatory(YBBN) = $$$YES
		}
	}
	quit strHTML
	
]]></Routine>
</Export>