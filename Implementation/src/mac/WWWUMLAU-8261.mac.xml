<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWUMLAU" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
WWWUMLAU(X,Z="",COMMA=$$$NO)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Convert Umlauts
	;
	;	+++++++++++++++++++++++++++++++++++++++++++++++++++++
	;	+ WARNING : It is possible for the results for some +
	;	+           values of Z to be a null string.        +
	;	+++++++++++++++++++++++++++++++++++++++++++++++++++++
	;
	;	S TEXT=$$^WWWUMLAU(TEXT,1)
	;	
	; Inputs : 
	;	X=TEXT
	;	
	;	Z="" NUR UMLAUTE										  ;Only umlauts
	;	Z=1 UMLAUTE GROSS UND SONDERZEICHEN ZU LEER  (SORTIERUNG) ;Umlauts, large, and remove special characters
	;	Z=2 UMLAUTE GROSS SONDERZEICHEN NICHT VERÄNDERN           ;Umlauts, large
	;	Z=3 UMLAUTE GROSS UND SONDERZEICHEN LÖSCHEN               ;& Delete special character  
	;	Z=4 UMLAUTE 1. ZEICHEN GROSS SONDERZEICHEN LÖSCHEN        ;sign special character Delete 
	;	Z=5 UMLAUTE KEINE LEER SONDERZEICHEN LÖSCHEN KEINE ZAHLEN AN 1.STELLE ;no void special character Delete no upon 
	;	Z=6 SONDERZEICHEN ZU LEER UMLAUTE                         ;special character within void
	;	
	;	COMMA=$$$YES	DO NOT DELETE "," COMMAS
	;
	; ByRef :
	;
	;
	; Returns : Modified X
	;
	;
	; History :
	; 10-Dec-2010	GRF		Code cleanup - old commented code removed
	; 28-Mar-2007	HeberB	SRBR014367: moved code upwards
	; 10-Oct-2005	JW		SR13641: Corrected, sped up.
	; 19-Jul-2005	JW		SR12934: Changed 255 to 254
	; 17-Jun-2005	Paul K	Fixed subscript error if length of "strOriginal" > 255
	; 16-Jun-2005	RPW		SR12056: Attempt at Performance Increase
	; 08-Jun-2005	GRF		Clarify
	; 01-Jun-2005	RPW		SR12056: Attempt at Performance Increase
	; 27-May-2005	GRF		Replace $Data test for Z with default argument to try to get
	; 						around 'eventQueueManager^COMDCMControlModule()' problem in
	; 						scheduler.
	; 24.11.1994	DT		Created - UMSETZEN UMLAUTE
	;-------------------------------------------------------------------------------
	if Z'=1 quit:X="" X
	new K,TRX,YQ,YI,strOriginal,strUMLAU
	new LW,UP,LW2,UP2,LW0,UP0,UP6
	
	set strOriginal = $select($get(X)="":" ",1:X)
	
	if $get(YUCI)="" set YUCI = $zutil(5)
	
	if ($length(strOriginal)<254) && ($length(Z)<254) {
		set strUMLAU = $get(^CacheTempWWWUMLAUCACHE(YUCI,strOriginal,$select(Z="":" ",1:Z),$select(COMMA="":0,1:COMMA)))
		if strUMLAU'="" {
			quit strUMLAU              ; *** EARLY EXIT ***
		}
	}
	
	if $get(YUMLAU)'="" {    ;UMLAU SPEED UP;TYBD;5,10,2004;PARAMETER UMLAUTUMSETZUNG ;KEINE UMLAUTE;TYBD;6,10,2004;26526
		set strUMLAU = ""
		for YI=1:1:$length(YUMLAU,",") {
			set strUMLAU = strUMLAU_$char($piece(YUMLAU,",",YI))
		}
		set YQ = (X=$translate(X,strUMLAU))
		
		if YQ'=$$$YES {
			if '$data(^CacheTempWWWUMLAU) {
				merge ^CacheTempWWWUMLAU=^WWWUMLAU
			}
			
			set CHAR=""
			for  set CHAR = $order(^CacheTempWWWUMLAU(0,CHAR)) quit:CHAR=""  do CHAR
		}
	}
	
	;f Z=1 set LW  = "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?"_$CHAR(128)_""""_" ",UP="UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ                   " ; SR17225
	if Z=1 set LW  = "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$char(128)_""""_" ",UP="UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	if Z=2 set LW2 = "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz",UP2="UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ"
	if Z=5 set LW5 = "ÜÄÖüäöß][\}{|~ ,()@#$%^&*_=+<>?/"_$char(128)_"""",UP5="UAOUAOSUAOUAOS"
	if Z=6 set UP6 = "UAOUAOSUAOUAOSabcdefghijklmnopqrstuvwxyz"
	set LW0 = "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz ,()@#$%^&*_=+<>?/"_$char(128)_""""
	set UP0 = "UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ"
	
	if (COMMA=$$$YES) && $find(X,",") {   ;NO COMMA ON COMMA=$$$YES
		set LW  = $translate($get(LW),",")
		set LW2 = $translate($get(LW2),",")
		set LW5 = $translate($get(LW5),",")
		set LW0 = $translate($get(LW0),",")
		set UP  = $translate($get(UP),",")  ;FIS;03.03.05;27426;SONST FALSCHER TRANSLATE
	}
	
	; ***** EARLY EXITS ON EACH TEST *****
	if Z=1 if $length(X)=1 if $find("+-/=",X) set:($length(strOriginal)<254) ^CacheTempWWWUMLAUCACHE(YUCI,strOriginal,Z,$select(COMMA="":0,1:COMMA))=X quit X   ;TYBD;7.3.2003;23432;SONDERUMSETZUNG WENN NUR EIN ZEICHEN
	if Z=1 {
		set X=$translate(X,LW,UP) 
		set:X="" X=" " 
		set:($length(strOriginal)<254) ^CacheTempWWWUMLAUCACHE(YUCI,strOriginal,Z,$select(COMMA="":0,1:COMMA))=X
		quit X
	}
	if Z=2 quit $translate(X,LW2,UP2)
	if Z=3 quit $translate(X,LW0,UP0)
	if Z=4 quit $extract($translate(X,LW0,UP0))_$extract($zconvert($translate(X,LW0,UP0),"L"),2,99)
	if Z=5 for  quit:X=""  quit:'$find("1234567890",$extract(X))  set X = $extract(X,2,99999) 
	if Z=5 quit $translate(X,LW5,UP5)
	if Z=6 quit $translate(X,LW0,UP6)
	
	if ($length(strOriginal)<254) && ($length(Z)<254) {
		set ^CacheTempWWWUMLAUCACHE(YUCI,strOriginal,$select(Z="":" ",1:Z),$select(COMMA="":0,1:COMMA)) = X
	}
	quit X
	
	
CHAR
	;-------------------------------------------------------------------------------
	;	umsevtzen der umlaute und sonderzeichen 
	;	convert the umlauts and special characters
	;-------------------------------------------------------------------------------
	new UML,K
	
	set UML = $piece($get(^CacheTempWWWUMLAU(0,CHAR,1)),Y,2)
	quit:UML=""
	for  quit:'$find(X,$char(CHAR))  set K = $find(X,$char(CHAR)) if K set X = $extract(X,1,K-2)_UML_$extract(X,K,$length(X))
	quit
	
	
LW()
	quit "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$char(128)_""""_" "
	
UP()
	quit "UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	
LW0()
	quit "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz ,()@#$%^&*_=+<>?/"_$char(128)_""""
	
UP0()
	quit "UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ"
	
LW2()
	quit "ÜÄÖüäöß][\}{|~abcdefghijklmnopqrstuvwxyz"
	
UP2()
	quit "UAOUAOSUAOUAOSABCDEFGHIJKLMNOPQRSTUVWXYZ"
	
LW5()
	quit "ÜÄÖüäöß][\}{|~ ,()@#$%^&*_=+<>?/"_$char(128)_""""
	
UP5()
	quit "UAOUAOSUAOUAOS"
	
UP6()
	quit "UAOUAOSUAOUAOSabcdefghijklmnopqrstuvwxyz"
	
]]></Routine>
</Export>