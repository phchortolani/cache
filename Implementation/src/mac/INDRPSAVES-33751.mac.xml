<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPSAVES" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPSAVES(KEY,FELD)
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SPEICHERN/BERICHTIGEN DEMAND AUS SUPPLY
	;
	;DATEN DES SUPPLY AUS DEM BETRIEB, DER DIE ARTIKEL BENÖTIGT WERDEN WIEDER AND DEN BETRIEB GESANDT
	;DER DIE ARTIKEL VERSENDET. DATEN WERDEN ALS DEMAND GESPEICHERT. ;the who item will when 
	;
	; Called By :
	; 	Form - INDRPSUPPLY
	; 	Routines -
	; 		INDRPRUN13
	;		FREI^INDRPSUSPERR
	;		KILL^INDRPSUSPERR
	;		WWWXMLSAVE
	;		
	; Inputs : 
	;	KEY		- Location,SupplyNo
	;	FELD	- objINDRPDEMAND ?
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 07-Aug-2008	PP		SR15812 : If Transfer Order Type and SHipped Date exists do not recreate the DEMAND
	; 13-Nov-2007	GRF		SR15598 : Use macros to clarify code
	; 12-Nov-2007	GRF		SR15598 : merge changes; idx rather than repeated WWWUMLAU
	; 16-Oct-2007	HQN		SR15598 : Add requisition as a valid type to create demands
	; 03.12.2001	DT
	;-------------------------------------------------------------------------------
	new BETRIEBN,BETRIEBV,DEMAND,idItemNo,idxLocn,idxSupply,NEU,SDART,SUPPLY,XKEY,YFELD,YI,YKEY
	
	;---------------------------------------
	;	YFELD		objINDRPDEMAND
	;---------------------------------------
	
	;IF $PIECE($GET(^INVORG(YM,YM,1)),Y,80)'=2 QUIT  ;KEINE DIREKTE DATENÜBERTRAGUNG ;no data transmission ; SR15598
	IF $$$INVORGDRPMRPDataExchange($GET(^INVORG(YM,YM,1)))'=2 quit  ; not "Direct Exchange" ;no data transmission 
	quit:$GET(KEY)=""
	quit:$GET(FELD)=""
	
	SET YFELD=FELD
	SET KEY=$TRANSLATE(KEY,"""")
	SET KEY(1)=$PIECE(KEY,",",1)   QUIT:KEY(1)=""    ;VON BETRIEB ;location 
	SET KEY(2)=$PIECE(KEY,",",2)   QUIT:KEY(2)=""    ;VON SUPPLYNUMMER
	set idxLocn   = $$^WWWUMLAU(KEY(1),1)                  ; 12-Nov-2007
	set idxSupply = $$^WWWUMLAU(KEY(2),1)
	
	IF YFELD="KILL" DO  QUIT  ;LÖSCHEN DATENSATZ ;Delete data record ; early exit
	. SET YKEY(2)=""
	. SET YKEY(1)=""
	. IF KEY(1)'="" IF KEY(2)'="" DO
	. . SET YKEY(1)=$ORDER(^INDRPDEMANDs(YM,6,idxLocn,idxSupply,YKEY(1)))
	. . IF YKEY(1)'="" SET YKEY(2)=$ORDER(^INDRPDEMANDs(YM,6,idxLocn,idxSupply,YKEY(1),""))
	. ;
	. IF YKEY(2)'="" IF YKEY(1)'="" DO
	. . SET KEY="^INDRPDEMAND("_""""_YM_""""_","_""""_YKEY(1)_""""_","_""""_YKEY(2)_""""_",1)"
	. . SET YOK=$$^WWWSETK(KEY)
	. . DO ^WWWSKILL(KEY,,1)
	
	;-------------------------------------------------------------------------------
	
	;IF $PIECE(YFELD,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=$$$YES SET ^INDRPNETCHANGE(YM,$PIECE(YFELD,Y,4),1)=""  ;FIS;29.04.05;SR12200 ;SR15598
	set idItemNo = $piece(YFELD,Y,4)
	IF idItemNo'="" IF $$$INVORGDRPOnlyForNetChange($GET(^INVORG(YM,YM,1)))=$$$YES SET ^INDRPNETCHANGE(YM,idItemNo,1)=""
	
	; SR15598 vvv
	;SET SDART = $PIECE(YFELD,Y,70)
	;IF SDART'=0 IF SDART'=3      QUIT  ;NUR MANUELLE UND TRANSFERAUFTRÄGE
	;IF SDART'=3                  QUIT  ;NUR TRANSFERAUFTRÄGE
	;IF $PIECE(YFELD,Y,19)=$$$YES QUIT  ;ONLY WHEN RELEASED;22.04.05;FIS;SR12200
	
	SET SDART = $$$INDRPDEMANDDemandType(YFELD)
	IF (SDART'=3) && (SDART'=5)             QUIT  ;NUR TRANSFERAUFTRÄGE; SR15598 : Requisition is a type of transfer
	IF $$$INDRPDEMANDBlocked(YFELD)=$$$YES  QUIT  ;ONLY WHEN RELEASED
	; SR15598 ^^^
	
	// SR15812 If Transfer Order Type and SHipped Date exists do not recreate the DEMAND  ; 
	IF (SDART=3)&&(($$$INDRPSUPPLYDateShipped(YFELD)'="")!($$$INDRPSUPPLYPickListPrintDate(YFELD)'="")) QUIT
	
	SET YKEY(1) = $PIECE(FELD,Y,1)   ;AN BETRIEB ;upon location 
	SET YKEY(2) = $PIECE(YFELD,Y,2)  ;DEMANDNUMMER
	SET $PIECE(YFELD,Y,1) = KEY(1)   ;ALTER SCHLUESSEL  ;VON BETRIEB ;location 
	SET $PIECE(YFELD,Y,2) = KEY(2)   ;ALTER SCHLUESSEL  ;SUPPLYNUMMER
	
	IF YKEY(2)="" IF YKEY(1)'="" IF KEY(1)'="" IF KEY(2)'="" DO
	. IF $DATA(^INDRPDEMANDs(YM,6,idxLocn,idxSupply,YKEY(1))) SET YKEY(2)=$ORDER(^INDRPDEMANDs(YM,6,idxLocn,idxSupply,YKEY(1),""))
	
	IF YKEY(2)="" SET YKEY(2)=$$^WWWNEXT("INDRPDEMAND")  ;NEUE NUMMER ;numeral 
	;FOR NEU=6,9,72,101 SET $PIECE(YFELD,Y,NEU)=""  ;FIS;25.04.05;SR12200
	FOR NEU=6,9,72,101,120 SET $PIECE(YFELD,Y,NEU)=""
	
	NEW YFORM,YVOR,YOK
	
	SET YKEY=YKEY(1)_","_YKEY(2)
	SET YFELD(2)=""
	SET YFELD(1)=YFELD
	SET XKEY="^INDRPDEMAND("_""""_YM_""""_","_""""_YKEY(1)_""""_","_""""_YKEY(2)_""""_",1)"
	IF $DATA(@XKEY) SET YFELD(1)=@XKEY SET YFELD(2)=YFELD(1)
	SET $PIECE(YFELD(1),Y,9)="D"
	IF $PIECE(YFELD(1),Y,6)=""  SET $PIECE(YFELD(1),Y,6)  = +$HOROLOG                ;Date
	IF $PIECE(YFELD(1),Y,17)="" SET $PIECE(YFELD(1),Y,17) = +$PIECE($HOROLOG,",",2)  ;UHR ;time 
	;IF $PIECE(YFELD(1),Y,7)="" SET $PIECE(YFELD(1),Y,7)=+$HOROLOG                   ;Due Date   ;SR12200
	
	FOR NEU=19,39,98,99 {                                  ; boolean $$$NO changed to null
		SET $PIECE(YFELD(1),Y,NEU) = +$PIECE(YFELD,Y,NEU)
		IF $PIECE(YFELD(1),Y,NEU)=0 SET $PIECE(YFELD(1),Y,NEU) = ""  ;GESPERRT ;DISABLED 
	;	set temp = +$PIECE(YFELD,Y,NEU),$PIECE(YFELD(1),Y,NEU) = $select(temp=0:"",1:temp)
	;	set temp = +$PIECE(YFELD,Y,19),$PIECE(YFELD(1),Y,NEU) = $select(temp=0:"",1:temp)
	;	set temp = +$PIECE(YFELD,Y,39),$PIECE(YFELD(1),Y,NEU) = $select(temp=0:"",1:temp)
	;	set temp = +$PIECE(YFELD,Y,98),$PIECE(YFELD(1),Y,NEU) = $select(temp=0:"",1:temp)
	;	set temp = +$PIECE(YFELD,Y,99),$PIECE(YFELD(1),Y,NEU) = $select(temp=0:"",1:temp)
	}
	
	FOR NEU=3,4,5,8,12,17,64,66,67,71,100 {
		IF $PIECE(YFELD,Y,NEU)'="" SET $PIECE(YFELD(1),Y,NEU)=$PIECE(YFELD,Y,NEU)  ;DATEN ÜBERNEHMEN ;assume 
	}
	IF YFELD(1)'=YFELD(2) SET YOK=$$^WWWSPEI("INDRPDEMAND",YKEY,YFELD(1),1)
	
	QUIT
	
]]></Routine>
</Export>