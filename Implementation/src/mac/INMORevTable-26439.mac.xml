<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INMORevTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INMORevTable
	
#include COMSYS
#include INConst
#include %occInclude
 
ShowItems(pidEvent,pidParameters)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; Params:
	;
	; Returns:
	;
	; History: 
	; 11-May-2010	PPP		SR16573: Created
	; -------------------------------------------------------------------------------
	new idKey,lstColTypes,lstHeaders,objItem,objItemPolicy,objLocn
	new strFunction,strGridLineYFORM,strIssue,strIssueLine,strStatus
	new idRev
	new idRevLine
	new objRev
	new objRevLine
	new idItem
	new idIssue
	
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dBundle")
	
	set strGridLineYFORM = $$$DYNTABLEGridLineFocusGrid(pidParameters)
	set idKey		= $$ReferenceKey^COMGridEdit31Interface(strGridLineYFORM,$$$DYNTABLEGridLineFocusRow(pidParameters))
	set idRev     	= $$$KEY1(idKey)
	set idRevLine 	= $$$KEY2(idKey)
	
	if '$$Editable^INMORev(idRev) quit                       // *** EARLY EXIT ***
	
	set strStatus    = $$GetLock^WWWMultiLock("INMORev",strGridLineYFORM,idRev_$$$COMMA_idRevLine)
	; FIXME : strStatus is not checked <GRF>
	
 	set objRev	 = $$$GRIDYFELDContainer
 	set objRevLine = $$$GRIDGetYFELD(idRevLine)
	
	; Load Lookup objects
	;set objIssueLine = ##class(alISS.dUIssueLine).%OpenId("0||"_idIssue_"||"_idIssueLine,0)
	//set objLocn = ##class(alLOC.dLocation).%OpenId($$$INIssueFromLocn(strIssue))
	
	set idItem 	= $$$INMORevLineItem(objRevLine)
	set objItem = ##class(alINV.dItem).%OpenId(idItem,0)
	
	if objItem'=$$$NULLOREF {
		set objItemPolicy = objItem.ItemPolicy
		set strFunction = "FillLine"
		set strStatus = $$BuildDynTableHeader(objItemPolicy,.lstHeaders,.lstColTypes)
		
		;HEVA-1686.2 set idIssue=$order(^INIssues(YM,6,$$$Index(idRev),""))
		set idIssue=$order(^INIssues(YM,6,idRev,"")) ;HEVA-1686.2

		do BuildDynTableRows(idItem,idIssue)
		
		; setup JS Callback
		if $$$ISERR(strStatus) {
			write "function "_strFunction_"() {"
			write "alert('"_$$$JSText($$$Text(strStatus))_"');"
			write "}"
		} else {
			write "function "_strFunction_"() {"
			write "CallBack(""SelectBundle^INMORevTable"", getRowNum(getFocusField()), this.DiscData[0]);"
			write "}"
		}
		do DrawTable^WWW120DynTable(lstHeaders,"dBundle",lstColTypes,pidParameters,strFunction,pidEvent,"SelectBundle^INMORevTable")
	}
	
	quit $$$OK
	
	
BuildDynTableHeader(pobjItemPolicy,&plstHeaders,&plstColTypes)
	;-------------------------------------------------------------------------------
	; Setup Table Headers
	;         (1)               (2)                      (3)
	;     "Storage"     Attribute Headings       "Available Quantity"
	;                   (e.g. "Lot Number")       
	; 
	; History:
	; 11-May-2010	PPP		SR16573: Created
	;-------------------------------------------------------------------------------
	new idAttrKey,objAttr,strAttrName
	
	;---------------------------------------  1
	
	if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
		set plstHeaders  = $listbuild("EndereÃ§o de armazenagem")
	}else{
		set plstHeaders  = $listbuild($$$Text("alISS0004"))     ; "Stock Bundle"
	}
	set plstColTypes = $listbuild($listbuild("left","link"))
	
	;---------------------------------------  2
	
	set idAttrKey = ""
	for {
		set objAttr = pobjItemPolicy.Attributes.GetNext(.idAttrKey)
		quit:(idAttrKey = "")
		continue:(objAttr.Attribute.Name="ReceiptNo")
		
		set strAttrName  = ##class(alINV.uiPolicyAttrTrans).GetText(objAttr.Attribute.Name,SPRACHE) //SR15698
		set plstHeaders  = plstHeaders_$listbuild(strAttrName)
		set plstColTypes = plstColTypes_$listbuild($listbuild("left","link"))
	}
	
	;---------------------------------------  3
		
	set plstHeaders  = plstHeaders_$listbuild($$$Text(32665))          ; "Available Quantity "
 	set plstColTypes = plstColTypes_$listbuild($listbuild("right","link"))
 	
	quit $$$OK
	
	
BuildDynTableRows(pidItem,pidIssue)
	;-------------------------------------------------------------------------------
	; Build an array "^CacheTempDynamicTable" which contains the lines corresponding
	; to the headings in BuildDynTableHeader
	; 
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-May-2010	PPP		SR16573: Created
	;-------------------------------------------------------------------------------
	new idAttrKey,idLine,idPolicyAttr,loopRS,objAttr,objBundleStock,objItem
	new objItemPolicy,objLocn,objPolicyAttr,objResultSet,strAttrValues
	new idIssLine
	new objIssueLine
	new idBundle
	new objBundle
	
	
	set objItem    = ##class(alINV.iUINV).GetItem(pidItem)
	//set objLocn    = ##class(alLOC.iULOC).GetLocation(pidLocn)
	
	//if (objItem '= $$$NULLOREF) && (objLocn '= $$$NULLOREF) {
	if (objItem '= $$$NULLOREF) {
		kill ^CacheTempDynamicTable(YUCI,YUSER,"dBundle")
		set objItemPolicy = objItem.ItemPolicy
 
		set idIssLine=""
		for {
			set idIssLine = $order(^INIssueLine(YM,pidIssue,idIssLine))
			quit:idIssLine=""
			
			set objIssueLine = $get(^INIssueLine(YM,pidIssue,idIssLine,1))
			continue:$$$INIssueLineItem(objIssueLine)'=pidItem

			set idBundle = $$$INIssueLineBundle(objIssueLine)		
			set ^CacheTempDynamicTable(YUCI,YUSER,"dBundle",$increment(idLine),"data") = idBundle
				
			set strAttrValues = ""	//objBundleStock.Storage.Code_Y
			if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
				set strAttrValues = $$SQLGetEnderecoArmaz^VARSQL(idBundle)_Y
			}else{
				set strAttrValues = idBundle_Y
			}
			set objBundle = ##class(alSOH.dBundle).%OpenId(idBundle,0)
	
			set idAttrKey = ""
			for {
				set objAttr = objBundle.Attributes.GetNext(.idAttrKey)
				quit:(idAttrKey  = "")
				
				set idPolicyAttr = objBundle.ItemPolicy.Attributes.FindObjectId(objBundle.ItemPolicy.%Id()_"||"_objAttr.AttributeName)
				set objPolicyAttr = $$$NULLOREF
				set:(idPolicyAttr '= "") objPolicyAttr = objBundle.ItemPolicy.Attributes.GetAt(idPolicyAttr)
				continue:(objAttr.AttributeName="ReceiptNo")
								
				if ((objPolicyAttr '= $$$NULLOREF)) && (objPolicyAttr.Attribute.DataType = "%Date") {
					set strAttrValues = strAttrValues_$$^WWWDATE(objAttr.AttributeValue)_Y
				} else {
					set strAttrValues = strAttrValues_objAttr.AttributeValue_Y
				}
			}
			
			;-------------------------------  3
			
			set strAttrValues = strAttrValues_$$$INIssueLineQtyIssued(objIssueLine)
			set ^CacheTempDynamicTable(YUCI,YUSER,"dBundle",idLine) = strAttrValues
		}
	}
	quit $$$OK
	
	
SelectBundle(pidRow,pidKey)
	;---------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-May-2010	PPP		SR16573: Created
	;---------------------------------------------------------------------------
	new objBundleStock,sc,strIssue,strIssueLine,YFORM
	
	set strIssue     = $$$GRIDYFELDContainer
	set strIssueLine = $$GetYFELDEX^COMGridEdit31Interface("INMOIssueLine",$$$GRIDYKEYContainer_","_pidRow)
	do SetRecord^COMGridEdit31S1("INMOIssueLine",pidRow,$$$FldINMORevLineBundle_$$$COMMA_pidKey)
		
	quit
	
 
]]></Routine>
</Export>