<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31S1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31S1
 
#include COMSYS
#include WWWConst
 
	;
	; NOTE: There is a similar procedure at ScreenUpdate^COMGridEdit31S, these may be merged or
	; at least made to use common components at a later stage when the Dynamic Table settles.
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^CGE31S1("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	
SetRecord(pidClass,pidRow,pstrData,pstrReferences="",pblnUpdateScreen=$$$YES)
	;-------------------------------------------------------------------------------
	; Set multiple fields of a record in the Edit Grid
	;
	; Params:
	; pstrClass				Class Id
	; pidRow				Grid Row Id
	; pstrData				Y delimited list of field change pairs :
	;							ClassFldNo_","_NewValue [_Y_ClassFldNo_","_NewValue [...]]
	; pstrPreferences:		Y delimited list of references fields.
	;							ClassFldNo [_Y_ClassFldNo [...]]
	; 
	; Returns: Nothing [Creates ^WWWDATEN(..."V","REFERENCEVALUE") and ^WWWDATEN(..."V",Yr_c)]
	;
	; History:
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 05-Jan-2010	GRF		SR15525: Call to $$Convert^WWWTR rather than $$^WWWTR
	; 18-Aug-2009	DWR		SR16797: After further discussions, changes reinstated
	; 14-Aug-2009	DWR		SR16797: reverted changes, kept for info re Core Rules
	; 30-Jul-2009	DWR		SR16797: Add parameter update screen, to allow forms with
	; 							core rules only (no OnBeforeFormat) to be formatted 
	; 							correctly by passing in $$$NO.
	; 18-Jan-2007	RPW		SR15340: Make the below use a new parameter
	; 18-Jan-2007	RPW		SR15340: If there is no data, just add this as a referenced field.
	; 17-Jan-2007	RPW		SR15340: Fixed class usage and variables, added references and call ScreenUpdateStyle.
	; 03-Jan-2007	PO		SR15351: Created
	;-------------------------------------------------------------------------------
	new blnFirst,dteToday,idClassField,idFirst,idFormField,idLastKey,idType,loop,objField
	new strData,strDisplay,strFieldName,YFORM,YKEY
	
	$$$LogR("SetRecord",pidClass_"<"_pidRow_"<"_pblnUpdateScreen)

	set YFORM=$$$GRIDName
 
	set dteToday = +$horolog
	set YKEY = $$ReferenceKey^COMGridEdit31Interface(YFORM,pidRow)   ; FIXME : appears YKEY is not used except to get unused idLastLine <GRF>
	set idLastKey = $piece(YKEY,$$$COMMA,$length(YKEY,$$$COMMA))     ; equiv. idDataLine (2.01 rather than 2x01)   FIXME : appears not used <GRF>
	
	set idFirst=""
	set blnFirst=$$$YES
	
	for loop=1:1:$length(pstrData,Y) {
		set idClassField = $$$KEY1($piece(pstrData,Y,loop))
		set objField     = $get(^WWW003(0,pidClass,idClassField,1))
		set idFormField  = $$$GetFormField(YFORM,idClassField)
		if blnFirst {
			set idFirst  = idFormField
			set blnFirst = $$$NO
		}
		set strFieldName = "Y"_pidRow_"_"_idFormField
		set strData      = $piece($piece(pstrData,Y,loop),$$$COMMA,2)
		set idType       = $$$WWW003InputType(objField)
		set strDisplay   = $$GetLiteral^WWWTR(idType,strData)
		set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"V","REFERENCEVALUE",strFieldName,1) = strDisplay
		set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"V",strFieldName,1) = strData
		
		;SR17673.2 $$$LogRx("cgeUV:'td"_strFieldName_"','"_$$$JSText($$DisplayData^COMGridEdit31F1(strData,YFORM,idFormField))_"'")
	$$$LogRx("cgeUV:getFieldId('"_strFieldName_"','"_YFORM_"'),'"_$$$JSText($$DisplayData^COMGridEdit31F1(strData,YFORM,idFormField))_"'") ;SR17673.2 
	
		;SR17673.2 write "cgeUpdateValue('td"_strFieldName_"','"_$$$JSText($$DisplayData^COMGridEdit31F1(strData,YFORM,idFormField))_"');"
		write "cgeUpdateValue(getFieldId('"_strFieldName_"','"_YFORM_"'),'"_$$$JSText($$DisplayData^COMGridEdit31F1(strData,YFORM,idFormField))_"');" ;SR17673.2 
		do MadeChange^COMGridEdit31S()
		do AddReference^COMGridEdit31Events(idClassField)
	}
	
	for loop=1:1:$length(pstrReferences,Y) {
		do AddReference^COMGridEdit31Events($piece(pstrReferences,Y,loop))
	}
	
	;do:idFirst'="" ScreenUpdateStyle^COMGridEdit31S("Y"_pidRow_"_"_idFirst,$$$YES)    ;SR16797
	do:idFirst'="" ScreenUpdateStyle^COMGridEdit31S("Y"_pidRow_"_"_idFirst,pblnUpdateScreen)
	quit
	
]]></Routine>
</Export>