<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESProdutosFavoritos" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESProdutosFavoritos
#include COMSYS
#include COMConst
#include INConst
#include WWWConst
#include FIN
#include VARConst
#include VARSESConst
 
 
GoToFavoritos(pYKEY,pSource)
 	;-------------------------------------------------------------------------------
    ; Go to the favourites form. This routine is called by clicking the "Favoritos"
    ; button in VARSESSolicTransfLinha or VARSESINTFRLine.
    ;
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	new idDocument
 
	kill ^FavouriteItem(YBED)
	
	set ^FavouriteItem(YBED) = pYKEY_"~"_pSource_"~"_YPARA	
	
	set ^LoadDefaultFavourite(YBED) = 1
	
	do GoToForm^COMUtilForm("VARSESProdutosFavoritos",YBED)
	
	quit
 
 
OnAfterDataFields(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Loads the grid and the favourites table.
    ;
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	do LoadGrid(pYKEY)
	do MostraFavoritos()
	
	kill ^LoadDefaultFavourite(YBED)
 
 	quit
 	
 
LoadGrid(pid="")
 	;-------------------------------------------------------------------------------
    ; Loads the grid.
    ;
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	new objInvoice,YFORM,YKEY,YAUSWAHL,YFELD,objVARTRKATARequest,strData
	set YFORM="VARSESProdutosFavoritosLinha"
	set YFELD=""
	set YKEY=$get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)=1
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)=310
	set $$$COMGridEditParameterGridName(YAUSWAHL)=YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)='($$$WWW120AuthorizationToModifyData(YVOR)=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)="VARSESProdutosFavoritos"
	
	$$$GRIDStart(YFORM,YKEY)
	
	quit
 
OnBeforeSave(pidFavoritos)
 	;-------------------------------------------------------------------------------
    ; Saves the grid and set the favourite as padrão if that's the first one to
    ; be created by this user.
    ;
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	
	new strStatus
	set strStatus = $$$GRIDSave(pidFavoritos)
	
	if $data(^VARSESProdutosFavoritos(YM,YBED)) = 0 {	
		set $piece(YFELD,Y,2) = 1		
	}
			 
	quit
 
	
OnAfterSave(pidFavoritos)
 	;-------------------------------------------------------------------------------
    ; When the user sets one favourite as the default, the code looks for any
    ; other favourite that's set as default and make it non-default.   
    ;
    ; History:
    ; 19-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
		
	if $piece(YFELD,Y,2) = 1 {
		
		new idFavorito, Usuario, Padrao, idFavoritoAberto, statusSave, 
			objFavorito, ChaveFavorito
		
		set idFavoritoAberto = $piece(pidFavoritos,",",2)
		
		set idFavorito = ""
		set Usuario    = YBED
		
		for {
			set idFavorito = $order(^VARSESProdutosFavoritos(YM,Usuario,idFavorito))
			quit:(idFavorito = "")
			
			set objFavorito = $get(^VARSESProdutosFavoritos(YM,Usuario,idFavorito,1))
			set Padrao = $piece(objFavorito,Y,2)
									
			if ((Padrao = 1) && (idFavorito '= idFavoritoAberto)) {
				
				set ChaveFavorito = Usuario_","_idFavorito
				
				set $piece(objFavorito,Y,2) = ""
								
				set statusSave = $$Save^COMUtils("VARSESProdutosFavoritos", ChaveFavorito, objFavorito, 1)
	
				if $$$ISERR(statusSave) {  
					do StartScript^COMUtiljavascript()
					write "alert('"_$$$Text("MED01109")_".'); "  ;Erro na alteração do favorito padrão ;use text macro;15-Sep-2008
					do EndScript^COMUtiljavascript()				
				}				
			}
		}					
	}
			 
	quit
 
			
MostraFavoritos(pYKEY,pYFELD)
 	;-------------------------------------------------------------------------------
    ; Display the Favourites table.
    ;
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	new NoFavorito, NomeFavorito, lstHeader, Padrao
	
	set NoFavorito = ""
 
 	set lstHeader=""
  	set lstHeader=lstHeader_$listbuild($$$Text("MED01110"),$$$Text("MED01111"),"")  ;Seq.,Nome do Favorito ;use text macro;15-Sep-2008   			 			    			    							 					
 
	if $$Start^COMTable(lstHeader) {
 
		&html< <font size="2" color="#000000"><b> > write $$$Text("MED01112")_":" &html< </font> >    	  ;Meus Favoritos ;use text macro;15-Sep-2008   		
	
		for {
   			set NoFavorito = $order(^VARSESProdutosFavoritos(YM,YBED,NoFavorito))
   			quit:(NoFavorito = "")	
   			
   			set Chave = YBED_","_NoFavorito
   			
   			set NomeFavorito = $piece($get(^VARSESProdutosFavoritos(YM,YBED,NoFavorito,1)),Y,1)
   			set Padrao		 = $piece($get(^VARSESProdutosFavoritos(YM,YBED,NoFavorito,1)),Y,2)
			
			if Padrao = 1  { set Padrao = $$$Text("MED01113") }  ;Favorito Padrão ;use text macro;15-Sep-2008
			if Padrao = "" { set Padrao = "" }			
        			
    		do NewLine^COMTable()
     		do InsertCell^COMTable(NoFavorito,"",,"")
     		do InsertCell^COMTable(NomeFavorito,"VARSESProdutosFavoritos",Chave,"")
     		do InsertCell^COMTable(Padrao,"VARSESProdutosFavoritos",Chave,"")
    		do EndLine^COMTable()	
		}
		
	}
	
	quit
	
 
InsereFavoritos(pYKEY)
 	;-------------------------------------------------------------------------------
    ; Choose if the items will be inserted in VARSESSolicTransfLinha or 
    ; VARSESINTFRLine.
    ;
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	new idDocument, Source, idFavorito, TipoMovimentacao 
		
	set idDocument       = $piece(^FavouriteItem(YBED),Y,1)
	set Source     	     = $piece(^FavouriteItem(YBED),Y,2)
	set TipoMovimentacao = $piece(^FavouriteItem(YBED),Y,3)
	
	set idFavorito = $piece(pYKEY,",",2)
	
	if Source = "VARSESSolicTransfLinha" { 
		do InsereFavoritosNaSolic(idFavorito,idDocument,TipoMovimentacao)
	}
	
	if Source = "VARSESINTFRLine" {
		do InsereFavoritosNoConsumo(idFavorito,idDocument,TipoMovimentacao)
	}
	
	quit
 
	
InsereFavoritosNaSolic(idFavorito,pidDocument,pTipoMovimentacao)
 	;-------------------------------------------------------------------------------
    ; Go through all checked items in the selected favourite and save them in
    ; VARSESSolicTransfLinha. It should NEVER be saved repeated items in VARSESSolicTransfLinha.
    ;    
    ; History:
    ; 18-Apr-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	new idSolic, idLinhaFavoritos, Inserir, Item, ItemFind, LinhaNovaSolic,
		objRegistroItemFavorito, ChaveItem, statusSave, ItemFind, blnItemExisteNaSolic
 
	set idSolic = pidDocument
		
	do MontaArrayItensNaSolic(idSolic)
 
	set idLinhaFavoritos     = ""
	set LinhaNovaSolic       = 0
	set blnItemExisteNaSolic = 0
 
	for {
		set idLinhaFavoritos = $order(^VARSESProdutosFavoritosLinha(YM,YBED,idFavorito,idLinhaFavoritos))
		quit:(idLinhaFavoritos = "")
						
		set Inserir = $piece($get(^VARSESProdutosFavoritosLinha(YM,YBED,idFavorito,idLinhaFavoritos,1)),"~",2)
		
		if (Inserir = 1) { //caso o checkbox de "Usar" esteja marcado, vai inserir na solic
			
			set Item = $piece($get(^VARSESProdutosFavoritosLinha(YM,YBED,idFavorito,idLinhaFavoritos,1)),"~",1)			
			
			if ArrayItens = "" {
				
				set LinhaNovaSolic = LinhaNovaSolic + 1
				set ChaveItem = idSolic_$$$COMMA_LinhaNovaSolic								
				
				set objRegistroItemFavorito = $get(^VARSESSolicTransfLinha(YM, ChaveItem, 1))
 
				set $piece(objRegistroItemFavorito,Y,1)  = Item
				set $piece(objRegistroItemFavorito,Y,3)  = $piece($get(^INART(0,Item,1)),Y,40)
				set $piece(objRegistroItemFavorito,Y,8)  = $$getSOHLocal(Item,YLOCATION)
				set $piece(objRegistroItemFavorito,Y,9)  = $$getSOHLocal(Item,$piece($get(^VARSESSolicTransf(YM,idSolic,1)),"~",3))																											
				set $piece(objRegistroItemFavorito,Y,14) = $piece($get(^INART(0,Item,1)),Y,30)
			
				set statusSave = $$Save^COMUtils("VARSESSolicTransfLinha", ChaveItem, objRegistroItemFavorito, 1)
	
				if $$$ISERR(statusSave) {  
					do StartScript^COMUtiljavascript()
					write "alert('"_$$$Text("MED01114")_".'); "  ;Não foi possível salvar os itens na Solicitação ;use text macro;15-Sep-2008
					do EndScript^COMUtiljavascript()
				}
			}
			
			elseif ArrayItens '= "" {
				
				set ItemFind = ";"_Item_";"
				
				if $find(ArrayItens,ItemFind) {
					set blnItemExisteNaSolic = 1
				}
				
				elseif '$find(ArrayItens,ItemFind) {
					
					set LinhaNovaSolic = $order(^VARSESSolicTransfLinha(0,idSolic,""),-1) + 1
					
					set ChaveItem = idSolic_$$$COMMA_LinhaNovaSolic								
				
					set objRegistroItemFavorito = $get(^VARSESSolicTransfLinha(YM, ChaveItem, 1))
 
					set $piece(objRegistroItemFavorito,Y,1)  = Item
					set $piece(objRegistroItemFavorito,Y,3)  = $piece($get(^INART(0,Item,1)),Y,40)
					set $piece(objRegistroItemFavorito,Y,8)  = $$getSOHLocal(Item,YLOCATION)
					set $piece(objRegistroItemFavorito,Y,9)  = $$getSOHLocal(Item,$piece($get(^VARSESSolicTransf(YM,idSolic,1)),"~",3))																											
					set $piece(objRegistroItemFavorito,Y,14) = $piece($get(^INART(0,Item,1)),Y,30)
				
					set statusSave = $$Save^COMUtils("VARSESSolicTransfLinha", ChaveItem, objRegistroItemFavorito, 1)
	
					if $$$ISERR(statusSave) {  
						do StartScript^COMUtiljavascript()
						write "alert('"_$$$Text("MED01114")_".'); "  ;Não foi possível salvar os itens na Solicitação ;use text macro;15-Sep-2008
						do EndScript^COMUtiljavascript()
					}
				}								
			}															
		}
	}								
 
	if (blnItemExisteNaSolic = 1) {
		do StartScript^COMUtiljavascript()
		write "alert('"_$$$Text("MED01115")_".'); "  ;Alguns dos produtos selecionados já existem na solicitação e não serão inseridos novamente ;use text macro;15-Sep-2008
		do EndScript^COMUtiljavascript()
	}
			
	do GoToForm^COMUtilForm("VARSESSolicTransf", idSolic,,pTipoMovimentacao)	
	
	quit
 
InsereFavoritosNoConsumo(idFavorito,pidDocument,pTipoMovimentacao)
 	;-------------------------------------------------------------------------------
    ; Go through all checked items in the selected favourite and save them in
    ; INTFRLine It should NEVER be saved repeated items in INTFRLine.
    ;    
    ; History:
    ; 13-May-2008	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 
	new idConsumo, idLinhaFavoritos, Inserir, Item, ItemFind, LinhaNovaConsumo,
		objRegistroItemFavorito, ChaveItem, statusSave, ItemFind, blnItemExisteNoConsumo
 
	set idConsumo = pidDocument
		
	do MontaArrayItensNoConsumo(idConsumo)
 
	set idLinhaFavoritos       = ""
	set LinhaNovaConsumo       = 0
	set blnItemExisteNoConsumo = 0
 
	for {
		set idLinhaFavoritos = $order(^VARSESProdutosFavoritosLinha(YM,YBED,idFavorito,idLinhaFavoritos))
		quit:(idLinhaFavoritos = "")
						
		set Inserir = $piece($get(^VARSESProdutosFavoritosLinha(YM,YBED,idFavorito,idLinhaFavoritos,1)),"~",2)
		
		if (Inserir = 1) { //caso o checkbox de "Usar" esteja marcado, vai inserir na solic
			
			set Item = $piece($get(^VARSESProdutosFavoritosLinha(YM,YBED,idFavorito,idLinhaFavoritos,1)),"~",1)			
			
			if ArrayItens = "" {
				
				set LinhaNovaConsumo = LinhaNovaConsumo + 1
				set ChaveItem = idConsumo_$$$COMMA_LinhaNovaConsumo								
				
				set objRegistroItemFavorito = $get(^(YM, ChaveItem, 1))
 
				set $piece(objRegistroItemFavorito,Y,1)  = Item
				set $piece(objRegistroItemFavorito,Y,4)  = $piece($get(^INART(0,Item,1)),Y,40)
				set $piece(objRegistroItemFavorito,Y,14) = $$getSOHLocal(Item,YLOCATION)
			
				set statusSave = $$Save^COMUtils("INTFRLine", ChaveItem, objRegistroItemFavorito, 1)
	
				if $$$ISERR(statusSave) {  
					do StartScript^COMUtiljavascript()
					write "alert('"_$$$Text("MED01116")_"'); "  ;Não foi possível salvar os itens no consumo ;use text macro;15-Sep-2008
					do EndScript^COMUtiljavascript()
				}
			}
			
			elseif ArrayItens '= "" {
				
				set ItemFind = ";"_Item_";"
				
				if $find(ArrayItens,ItemFind) {
					set blnItemExisteNoConsumo = 1
				}
				
				elseif '$find(ArrayItens,ItemFind) {
					
					set LinhaNovaConsumo = $order(^INTFRLine(0,idConsumo,""),-1) + 1
					
					set ChaveItem = idConsumo_$$$COMMA_LinhaNovaConsumo								
				
					set objRegistroItemFavorito = $get(^INTFRLine(YM, ChaveItem, 1))
 
					set $piece(objRegistroItemFavorito,Y,1)  = Item
					set $piece(objRegistroItemFavorito,Y,4)  = $piece($get(^INART(0,Item,1)),Y,40)
					set $piece(objRegistroItemFavorito,Y,14) = $$getSOHLocal(Item,YLOCATION)
				
					set statusSave = $$Save^COMUtils("INTFRLine", ChaveItem, objRegistroItemFavorito, 1)
	
					if $$$ISERR(statusSave) {  
						do StartScript^COMUtiljavascript()
						write "alert('"_$$$Text("MED01116")_"'); "  ;Não foi possível salvar os itens no consumo ;use text macro;15-Sep-2008
						do EndScript^COMUtiljavascript()
					}
				}								
			}															
		}
	}								
 
	if (blnItemExisteNoConsumo = 1) {
		do StartScript^COMUtiljavascript()
		write "alert('"_$$$Text("MED01117")_".'); "  ;Alguns dos produtos selecionados já existem no consumo e não serão inseridos novamente ;use text macro;15-Sep-2008
		do EndScript^COMUtiljavascript()
	}
			
	do GoToForm^COMUtilForm("VARSESINTFR", idConsumo,,pTipoMovimentacao)	
	
	quit
 
 
getSOHLocal(pItem,pLocation)
	quit:($get(pItem) = "") $$$RecordNotSelected
	quit $$^INARTMENGE(pItem,,pLocation,,1,0,1)
	
 
MontaArrayItensNaSolic(pidSolic)
 
	new idLinhaSolic, ArrayItensNaSolic, Item
		
	set ArrayItens   = ""		
	set idLinhaSolic = ""
		
	for {
		set idLinhaSolic = $order(^VARSESSolicTransfLinha(YM,pidSolic,idLinhaSolic))
		quit:(idLinhaSolic = "")
	
		set Item = $piece($get(^VARSESSolicTransfLinha(YM,pidSolic,idLinhaSolic,1)),"~",1)
		
		set ArrayItens = ArrayItens_";"_Item						
	}
	
	if ArrayItens '= "" { set ArrayItens = ArrayItens_";" }
	
	quit ArrayItens
 
 
MontaArrayItensNoConsumo(pidConsumo)
 
	new idLinhaConsumo, Item
		
	set ArrayItens     = ""		
	set idLinhaConsumo = ""
		
	for {
		set idLinhaConsumo = $order(^INTFRLine(YM,pidConsumo,idLinhaConsumo))
		quit:(idLinhaConsumo = "")
	
		set Item = $piece($get(^INTFRLine(YM,pidConsumo,idLinhaConsumo,1)),"~",1)
		
		set ArrayItens = ArrayItens_";"_Item
	}
	
	if ArrayItens '= "" { set ArrayItens = ArrayItens_";" }
	
	quit ArrayItens
 
CarregaPadrao()
	
	new Usuario, idFavorito, Padrao
   
	set Usuario    = YBED
	set idFavorito = ""
 
	if $data(^LoadDefaultFavourite(YBED)) = 0 {	
		quit
	}
 
	elseif ^LoadDefaultFavourite(YBED) = 1 {
 
		for {
			set idFavorito = $order(^VARSESProdutosFavoritos(YM,Usuario,idFavorito))
			quit:(idFavorito = "")
			
			set Padrao = $piece($get(^VARSESProdutosFavoritos(YM,Usuario,idFavorito,1)),Y,2)
		
			if Padrao = 1 {			
				set YKEY = Usuario_","_idFavorito						
			}		
		}
	}
	
			
	quit 1
	
	
CanPost(pYKEY)	
 
 	if $piece(pYKEY,",",2)'= "" {
		do VerificaProdutosRepetidos(pYKEY)
 	}
 	
	quit
	
	
VerificaProdutosRepetidos(pYKEY)
	;-------------------------------------------------------------------------------
    ; Validate if there are any repeated items in the grid.
    ;
    ; History:
    ; 19-Apr-2008	Gustavo Fiuza	Created
    ;------------------------------------------------------------------------------- 	
 	
	new LinhaFavorito, objLinhaFavorito, CodProduto, ArrayProdutos, Count,
		ExisteRepeticao, ArrayRepetidos, ArrayMensagens, cnt, ArrayLinhas, 
		Count1, LinhaFavorito1, Rec, Count2, Count3, CodFavorito,
		ArrayProdutosFind, CodProdutoFind
	
	set LinhaFavorito = ""
	set ArrayProdutos = ""
	set ArrayProdutosFind = ""
	set Count = 0
	set ArrayRepetidos = ""
	set ExisteRepeticao = 0
	set Count3 = 0
	
	set CodFavorito = $piece(pYKEY,",",2)
 
	for {
   		set LinhaFavorito = $order(^VARSESProdutosFavoritosLinha(YM,YBED,CodFavorito,LinhaFavorito))            
   		quit:(LinhaFavorito = "")
   	
   		set Count = Count + 1
   		
    	set objLinhaFavorito = $get(^VARSESProdutosFavoritosLinha(YM,YBED,CodFavorito,LinhaFavorito,1))
    				    			
    	set CodProduto     = $piece(objLinhaFavorito,Y,1)
    	set CodProdutoFind = ";"_CodProduto_";"	    	    	
 
		if ($find(ArrayProdutosFind,CodProdutoFind) && (Count > 1)) {
			
			set ExisteRepeticao = 1
			
			if '$find(ArrayRepetidos,CodProduto) {
				if Count3 = 0 {
					set ArrayRepetidos = CodProduto
				}
				else { 
					set ArrayRepetidos = ArrayRepetidos_";"_CodProduto
				}
				set Count3 = Count3 + 1
			}											
		}  
		
		set ArrayProdutosFind = ArrayProdutosFind_";"_CodProduto_";"		
		set ArrayProdutos 	  = ArrayProdutos_";"_CodProduto_"("_Count_")"
		 		
	}
	
	set ArrayMensagens = ""
	
 	for cnt=1:1:$length(ArrayRepetidos) {
		
		set Rec = $piece(ArrayRepetidos,";",cnt)
		quit:(Rec = "")
			
		set Count1 = 0
		set LinhaFavorito1 = ""
		set ArrayLinhas = ""
		set Count2 = 0
		
		for {
   			set LinhaFavorito1 = $order(^VARSESProdutosFavoritosLinha(YM,YBED,CodFavorito,LinhaFavorito1))            
   			quit:(LinhaFavorito1 = "")
   			
   			set Count1 = Count1 + 1
   		
    		set objLinhaFavorito = $get(^VARSESProdutosFavoritosLinha(YM,YBED,CodFavorito,LinhaFavorito1,1))
    				    			
    		set CodProduto = $piece(objLinhaFavorito,Y,1)
    	
    		if $piece(ArrayRepetidos,";",cnt) = CodProduto {
	  		  	if Count2 = 0 {	
	  		  		set ArrayLinhas = LinhaFavorito1
	  		  	}
	  		  	else { 
	  		  		set ArrayLinhas = ArrayLinhas_", "_LinhaFavorito1 
	  		  	}
	  		  	set Count2 = Count2 + 1
    		}    			
		 	
		}
		
		;set Mensagem = "Produto "_Rec_" -> Seq. "_ArrayLinhas
		set Mensagem = $$$Text("MED01009")_" "_Rec_" -> "_$$$Text("MED01110")_" "_ArrayLinhas  ;use text macro;15-Sep-2008
		set ArrayMensagens = ArrayMensagens_$char(10)_Mensagem
		 		
 	}
	
	if ExisteRepeticao = 1 {		
		set YQ = 1_$$$Text("MED01118")_". "_$char(10)_""_ArrayMensagens_""  ;Não é possível inserir porque existem produtos repetidos nos favoritos ;use text macro;15-Sep-2008
		quit			
	}  		
	
	quit
 
	
Voltar(pYKEY)
	;-------------------------------------------------------------------------------
    ; VAR back button. Depending on the screen where the user came from (Transfer
    ; Request or Transfer Order), the button will lead him to the right one, 
    ; with the correct YKEY and YPARA.
    ;
    ; History:
    ; 07-May-2008	Gustavo Fiuza	Created
    ;------------------------------------------------------------------------------- 	
 
	new idDocument, Source, BackSource, TipoMovimentacao
	
	set idDocument       = $piece(^FavouriteItem(YBED),Y,1)
	set Source     	     = $piece(^FavouriteItem(YBED),Y,2)
	set TipoMovimentacao = $piece(^FavouriteItem(YBED),Y,3)
	
	if Source = "VARSESSolicTransfLinha" { set BackSource = "VARSESSolicTransf" }
	elseif Source = "VARSESINTFRLine" { set BackSource = "VARSESINTFR" }
	
	do GoToForm^COMUtilForm(BackSource, idDocument,,TipoMovimentacao)
 
	quit
	
	
OnBlurItem(pYFELD)
	;-------------------------------------------------------------------------------
    ; Validates if the item exist and if it is authorized for the location.
    ;
    ; History:
    ; 07-May-2008	Gustavo Fiuza	Created
    ;------------------------------------------------------------------------------- 	
 	
 	new Produto
 
 	set Produto = $piece(pYFELD,Y,1)
	quit:(Produto = "")
	
	if '$data(^INART(YM, Produto, 1)) {
		set $piece(YFELD,Y,1) = ""
		$$$Alert($$$Text($listbuild("MED01119",Produto)))  ;O produto '%1' não existe ;use text macro;15-Sep-2008
	}
	
	elseif '$data(^INDRPITEM(YM,YLOCATION,Produto,1)) {
		set $piece(YFELD,Y,1) = ""
		$$$Alert($$$Text("MED01120"))  ;O produto não está autorizado para o seu local ;use text macro;15-Sep-2008
	}		
	
 	quit
]]></Routine>
</Export>