<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPAPROC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPAPROC(YAUF,YPOS,PLUS,MINUS) ;INWEAUFPROC;FIS;AUFRUF WARENEINGANGS-FORMULAR;18.04.02
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		AUFRUF WARENEINGANGS-FORMULAR
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	18.04.02
	;|
	;\------------------------------------------------------------------/
	;
	;YAUF=AUFTRAGSNUMMER
	;YPOS=POSITION, AUS DER AUFGERUFEN WIRD
	;PLUS=EINE AUFTRAGSPOSITION VOR ;pre- 
	;MINUS=EINE AUFTRAGSPOSITION ZURÜCK ;back 
	NEW YYKEY,YI,POSX,SORT
	SET YAUF=$GET(YAUF)
	SET YPOS=$GET(YPOS)
	IF YAUF=""!(YPOS="") DO ^WWWINFO($$^WWWTEXT(32292)) QUIT  ;BITTE PRÜFEN ;please sift 
	SET POSX=YPOS
	IF $GET(PLUS)'="" SET YPOS=YPOS+(+PLUS)  ;NACHFOLGENDE POSITION
	IF $GET(MINUS)'="" SET YPOS=YPOS-(+MINUS)  ;VORHERIGE POSITION
	IF +YPOS<1 SET YPOS=1  ;ERSTE POSITION ;premier 
	IF '$DATA(^INAUFP(YM,YAUF,YPOS)) SET YPOS=$ORDER(^INAUFP(YM,YAUF,""),-1)  ;LETZTE POSITION ;last 
	;SUCHEN NÄCHSTE BESTELLPOSITION ;seek next 
	DO
	. IF $PIECE($GET(^INAUFP(YM,YAUF,YPOS,1)),Y,7)=1 IF $PIECE(^(1),Y,12)'="" IF $PIECE($GET(^INLIEF(YM,$PIECE(^(1),Y,12),1)),Y,34)=1 QUIT  ;NÄCHSTE POSITION IST BEREITS BESTELLPOSITION MIT AB ;next yet by means of Confirm. 
	. SET SORT=-1
	. IF $GET(PLUS)'="" SET SORT=1
	. FOR  SET YPOS=$ORDER(^INAUFP(YM,YAUF,YPOS),SORT)  QUIT:YPOS=""  IF $PIECE($GET(^INAUFP(YM,YAUF,YPOS,1)),Y,7)=1 IF $PIECE(^(1),Y,12)'="" IF $PIECE($GET(^INLIEF(YM,$PIECE(^(1),Y,12),1)),Y,34)=1 QUIT
	. IF YPOS="" SET YPOS=POSX  ;KEINE WEITERE BESTELLPOSITION ;no 
	. QUIT
	SET YYKEY=""
	SET YFORM="INAUFPA"
	DO
	. IF $DATA(^INAUFPA(YM,YAUF,YPOS)) DO  QUIT  ;ERFASSTE AB ;Confirm. 
	. . NEW LFNR,ABNR,DAT
	. . SET LFNR="" FOR  SET LFNR=$ORDER(^INAUFPA(YM,YAUF,YPOS,LFNR)) QUIT:LFNR=""  DO  QUIT:YYKEY'=""
	. . . SET ABNR=$ORDER(^INAUFPA(YM,YAUF,YPOS,LFNR,""),-1)
	. . . QUIT:ABNR=""
	. . . SET YYKEY=YAUF_","_YPOS_","_LFNR_","_ABNR
	. . . QUIT
	. . QUIT
	. SET YYKEY=YAUF_","_YPOS_",,"                        ;KEINE AB ERFASST ;no Confirm. 
	. QUIT
	SET YBACK="INAUFBANZ,"
	SET %("VAR","YFORM")=YFORM
	SET %("VAR","YKEY")=YYKEY
	SET %("VAR","YBACK")=YBACK
	DO ^WWWFORM
	QUIT
]]></Routine>
</Export>