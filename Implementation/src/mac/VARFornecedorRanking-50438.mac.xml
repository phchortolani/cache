<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARFornecedorRanking" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARFornecedorRanking
#include COMSYS
#include VARConst

OnAfterDataFields()	

	new totalFornecCadastro, totalFornecAtivos, totalFornecInativos, totalFornecComAvaliacao,
		totalFornecSemAvaliacao, totalFornecComOcorrencia, totalFornecSemOcorrencia

	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	new tipoRanking
	set tipoRanking = $get(VORG(1))
	
	if (tipoRanking = 1) {
		
		do PrintCriterioAvaliacao()
		
	} elseif (tipoRanking = 2) {
		
		do PrintCriterioOcorrencia()
		
	} else {
		
		$$$Alert("É necessário escolher o tipo de ranking.")
		do GoToForm^COMUtilForm(YFORM,"")
	}
	quit
	
PrintCriterioOcorrencia()

	do PrintDadosFornecedores()
	do ExibeRankingOcorrencia()
	
	write "<div style='padding-top:10px;padding-left:2px;padding-bottom:10px'>Observação: fornecedores com o mesmo índice de ocorrências são qualificados pelo menor número de ocorrências graves, depois moderadas e então leves.</div>"
	

	quit	
	
PrintCriterioAvaliacao()

	do PrintDadosFornecedores()
	do ExibeRankingAvaliacao()
	
	write "<div style='padding-top:10px;padding-left:2px;padding-bottom:10px'>Observação: fornecedores com o mesmo índice de desempenho são qualificados pelo menor número de ocorrências.</div>"

	quit
	
PrintDadosFornecedores()

	set totalFornecInativos = $$getTotalFornecedores(0)
	set totalFornecAtivos   = $$getTotalFornecedores(1)	
	set totalFornecCadastro = totalFornecAtivos + totalFornecInativos
	
	set totalFornecComAvaliacao = $$getTotalFornecComAvaliacao(1)
	set totalFornecSemAvaliacao = totalFornecAtivos - totalFornecComAvaliacao
	
	set totalFornecComOcorrencia = $$getTotalFornecComOcorrencia(1)
	set totalFornecSemOcorrencia = totalFornecAtivos - totalFornecComOcorrencia	

	write "<div style='margin-left:2px;line-height:19px;border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:8px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px'>"
	write "	<table style='font-size:13px;'>"
	write "		<tr>"
	write "			<td colspan='4'><strong>Cadastro de Fornecedores</strong></td>"
	write "		</tr>"
	write "		<tr>"
	write "			<td colspan='4'>Total de Fornecedores Cadastrados: "_$$^WWWTR(0,12,totalFornecCadastro)_"&nbsp;&nbsp;"
	write "							Ativos: <font color='blue'>"_$$^WWWTR(0,12,totalFornecAtivos)_"</font>&nbsp;&nbsp;"
	write "							Inativos: <font color='red'>"_$$^WWWTR(0,12,totalFornecInativos)_"</font></td>"
	write "		</tr>"
	write "		<tr>"
	write "			<td colspan='4' style='padding-top:12px'><strong>Sobre os Fornecedores <font color='blue'>Ativos</font></strong></td>"
	write "		</tr>"
	write "		<tr>"
	write "			<td>Com Avaliação: </td>"
	write "			<td>"_$$^WWWTR(0,12,totalFornecComAvaliacao)_"</td>"	
	write "			<td style='padding-left:35px'>Sem Avaliação: </td>"
	write "			<td>"_$$^WWWTR(0,12,totalFornecSemAvaliacao)_"</td>"	
	write "		</tr>"
	write "		<tr>"
	write "			<td>Com Ocorrência: </td>"
	write "			<td>"_$$^WWWTR(0,12,totalFornecComOcorrencia)_"</td>"	
	write "			<td style='padding-left:35px'>Sem Ocorrência: </td>"
	write "			<td>"_$$^WWWTR(0,12,totalFornecSemOcorrencia)_"</td>"	
	write "		</tr>"
	write " </table>"
	write "</div>"
	
	quit	
	
ExibeRankingOcorrencia()

 	new lstHeader, idFornecedor, nomeFornecedor, totalRecebimentos, intNaoInformado, intGrave, intModerada, intLeve,
 		intTotalOcorrencias, fltIndice, count
		
	&sql(
		DECLARE C1 CURSOR FOR 
		
		SELECT R.SupplierNumber, Name1, R.Recebimentos, ISNULL(N.NaoInformado,0), ISNULL(G.Grave,0), ISNULL(M.Moderada,0), ISNULL(L.Leve,0), 
			   (ISNULL(N.NaoInformado,0) + ISNULL(G.Grave,0) + ISNULL(M.Moderada,0) + ISNULL(L.Leve,0)) as TotalOcorrencias, 
			  ((ISNULL(N.NaoInformado,0) + ISNULL(G.Grave,0) + ISNULL(M.Moderada,0) + ISNULL(L.Leve,0))/R.Recebimentos) as Indice
			  
		INTO :idFornecedor, :nomeFornecedor, :totalRecebimentos, :intNaoInformado, :intGrave, :intModerada, :intLeve, :intTotalOcorrencias, :fltIndice

		FROM
		 
		(select SupplierNumber, count(*) as Recebimentos from INREC a INNER JOIN INLIEF b ON b.SupplierNumber = a.Supplier where a.type != 5 group by SupplierNumber) as R 

		LEFT OUTER JOIN (select CodFornecedor, count(*) as NaoInformado from VARFornecedorOcorrencia where Classificacao is null group by CodFornecedor) as N ON R.SupplierNumber = N.CodFornecedor

		INNER JOIN (select distinct CodFornecedor from VARFornecedorOcorrencia) as T ON R.SupplierNumber = T.CodFornecedor

		LEFT OUTER JOIN (select CodFornecedor, count(*) as Grave from VARFornecedorOcorrencia where Classificacao = 1 group by CodFornecedor) as G ON R.SupplierNumber = G.CodFornecedor

		LEFT OUTER JOIN (select CodFornecedor, count(*) as Moderada from VARFornecedorOcorrencia where Classificacao = 2 group by CodFornecedor) as M ON R.SupplierNumber = M.CodFornecedor

		LEFT OUTER JOIN (select CodFornecedor, count(*) as Leve from VARFornecedorOcorrencia where Classificacao = 3 group by CodFornecedor) as L ON R.SupplierNumber = L.CodFornecedor

		INNER JOIN INLIEF ON R.SupplierNumber = INLIEF.SupplierNumber

		order by Indice desc, Grave desc, Moderada desc, Leve desc
	)

	set lstHeader = ""
 	set lstHeader = lstHeader_$listbuild("Posição&nbsp;<br />&nbsp;&nbsp;no Ranking","Código","Fornecedor","Qtde.&nbsp;<br />&nbsp;&nbsp;Entregas","Ocorrências&nbsp;<br />&nbsp;&nbsp;Graves", "Ocorrências&nbsp;<br />&nbsp;&nbsp;Moderadas",
 										 "Ocorrências&nbsp;<br />&nbsp;&nbsp;Leves", "Ocorrências&nbsp;<br />&nbsp;&nbsp;Não Classificadas", "Total&nbsp;<br />&nbsp;&nbsp;de Ocorrências","Índice de&nbsp;<br />&nbsp;&nbsp;Ocorrências") 

	write "<div style='margin-left:2px;line-height:21px'>"

	if $$Start^COMTable(lstHeader,"&nbsp;Ranking (somente fornecedores com ocorrências)",,,$$$NO) {		
		
		&sql(OPEN C1)
		&sql(FETCH C1)
		while (SQLCODE = 0) {
		
			do NewLine^COMTable()
		
			do InsertCell^COMTable("<strong>"_$increment(count)_"</strong>",,,,"center")
			do InsertCell^COMTable(idFornecedor,"INLIEF",idFornecedor)
			do InsertCell^COMTable(nomeFornecedor,,,,,,,,,$$$YES)							
			do InsertCell^COMTable(totalRecebimentos,,,,"right")
			do InsertCell^COMTable(intGrave,,,,"right")							
			do InsertCell^COMTable(intModerada,,,,"right")							
			do InsertCell^COMTable(intLeve,,,,"right")							
			do InsertCell^COMTable(intNaoInformado,,,,"right")							
			do InsertCell^COMTable(intTotalOcorrencias,,,,"right")
			do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,(fltIndice*100),2)_"%</strong>",,,,"right")					
		
			do EndLine^COMTable() 		
		
			&sql(FETCH C1)
		}
		&sql(CLOSE C1)
	}
	do Stop^COMTable()
	
	write "</div>"

	quit	
	
ExibeRankingAvaliacao()

 	new SQLQuery, lstHeader, count, idFornecedor, indiceDesemp, totalAvaliacoesFornec,
 		totalOcorrenciasFornecedor

	set SQLQuery = "SELECT SupplierNumber,"
	set SQLQuery = SQLQuery_" $$CalculaDesempenhoFornecedor^VARFornecedorAvaliacao(SupplierNumber), "
	set SQLQuery = SQLQuery_" $$getTotalFornecComOcorrencia^VARFornecedorRanking(SupplierNumber)"
	set SQLQuery = SQLQuery_" FROM INLIEF"
	set SQLQuery = SQLQuery_" WHERE upper(Status) = 1"
	set SQLQuery = SQLQuery_" AND $$CalculaDesempenhoFornecedor^VARFornecedorAvaliacao(SupplierNumber) is not null"
	set SQLQuery = SQLQuery_" ORDER BY $$CalculaDesempenhoFornecedor^VARFornecedorAvaliacao(SupplierNumber) desc, "
	set SQLQuery = SQLQuery_" $$getTotalOcorrenciasFornecedor^VARFornecedorRanking(SupplierNumber) asc"
	
	set formulaData = ##class(%ResultSet).%New()  ;Create Result Set Object
	do formulaData.Prepare(SQLQuery)  ;Prepare Query
	do formulaData.Execute()  		  ;Execute Query

	set lstHeader = ""
 	set lstHeader = lstHeader_$listbuild("Posição&nbsp;<br />&nbsp;&nbsp;no Ranking","Código","Fornecedor","Qtde. Avaliações","Qtde. Ocorrências","Índice de Desempenho") 

	write "<div style='margin-left:2px;line-height:21px'>"

	if $$Start^COMTable(lstHeader,"&nbsp;Ranking (somente fornecedores com avaliação)",,,$$$NO) {		
		
		set count = 0
	
		while (formulaData.Next()) {
							
 			set count = $increment(count)
 			
 			set idFornecedor = formulaData.GetData(1)
 			set indiceDesemp = formulaData.GetData(2)		
 			
 			set totalAvaliacoesFornec = $$getTotalAvaliacoesFornecedor(idFornecedor)
 			set totalOcorrenciasFornecedor = $$getTotalOcorrenciasFornecedor(idFornecedor)
 			
			do NewLine^COMTable()
			do InsertCell^COMTable("<strong>"_count_"</strong>",,,,"center")
			do InsertCell^COMTable(idFornecedor,"INLIEF",idFornecedor)
			do InsertCell^COMTable($$SQLGetDescFornecedor^VARSQL(idFornecedor),"INLIEF",idFornecedor,,,,,,,$$$YES)							
			do InsertCell^COMTable($$^WWWTR(0,12,totalAvaliacoesFornec),"VARFornecedorAvaliacao",idFornecedor,,"right")
			do InsertCell^COMTable($$^WWWTR(0,12,totalOcorrenciasFornecedor),"VARFornecedorOcorrencia",idFornecedor,,"right")
			
			if (indiceDesemp >= 60) {
				do InsertCell^COMTable($$^WWWTR(0,12,indiceDesemp),,,,"right")
			} else {
				do InsertCell^COMTable("<font color='red'>"_$$^WWWTR(0,12,indiceDesemp)_"</font>",,,,"right")
			}
			
			do EndLine^COMTable() 		
		}
		
		if (count = 0)  {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum fornecedor com avaliações para posição no ranking.",,,,,,6)
			do EndLine^COMTable()
		}
 	
 		do Stop^COMTable()
 		do formulaData.Close()
	}
	
	write "</div>"

	quit

OnAfterSave
	
	do VARLogAfterSave^VARUtil(YBED,YFORM)
	do GoToForm^COMUtilForm(YFORM,"")
	
	quit

getTotalFornecedores(supplierStatus=1)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna a quantidade de fornecedores no cadastro.
	;
    ; Histórico:
    ; 	04-Set-2012  Gustavo	Criado
    ;-------------------------------------------------------------------------------
	new totalFornecedores
	
	&sql(SELECT count(SupplierNumber)
		   INTO totalFornecedores
    	   FROM INLIEF
       	  WHERE UPPER(Status) = :supplierStatus
    )
    
	if (SQLCODE '= 0) {
		quit ""
	}
    
    quit totalFornecedores
    
    
getTotalFornecComAvaliacao(supplierStatus=1)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna o total de fornecedores com avaliação, ativos ou inativos.
	;
    ; Histórico:
    ; 	04-Set-2012  Gustavo	Criado
    ;-------------------------------------------------------------------------------
    new idFornecedor, totalFornecComAvaliacao
    set totalFornecComAvaliacao = 0
    
    $$$Order4(^INLIEFs,YM,2,supplierStatus,idFornecedor)    
    	set:($data(^VARFornecedorAvaliacao(YM,idFornecedor))) totalFornecComAvaliacao = $increment(totalFornecComAvaliacao)    
    $$$End
    
    quit totalFornecComAvaliacao
    

getTotalFornecComOcorrencia(supplierStatus=1)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna o total de fornecedores com ocorrência, ativos ou inativos.
	;
    ; Histórico:
    ; 	04-Set-2012  Gustavo	Criado
    ;-------------------------------------------------------------------------------
    new idFornecedor, totalFornecComOcorrencia
    set totalFornecComOcorrencia = 0
    
    $$$Order4(^INLIEFs,YM,2,supplierStatus,idFornecedor)    
    	set:($data(^VARFornecedorOcorrencia(YM,idFornecedor))) totalFornecComOcorrencia = $increment(totalFornecComOcorrencia)    
    $$$End
    
    quit totalFornecComOcorrencia


getTotalAvaliacoesFornecedor(pidSupplier)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna o total de avaliações para um determinado fornecedor.
	;
    ; Histórico:
    ; 	04-Set-2012  Gustavo	Criado
    ;-------------------------------------------------------------------------------
	quit:(pidSupplier = "") ""
	
	new totalAvaliacoesFornec, idAvaliacao
	set totalAvaliacoesFornec = 0
	
	$$$Order3(^VARFornecedorAvaliacao,YM,pidSupplier,idAvaliacao)		
		set totalAvaliacoesFornec = $increment(totalAvaliacoesFornec)	
	$$$End
	
	quit totalAvaliacoesFornec
	
getTotalOcorrenciasFornecedor(pidSupplier)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna o total de avaliações para um determinado fornecedor.
	;
    ; Histórico:
    ; 	04-Set-2012  Gustavo	Criado
    ;-------------------------------------------------------------------------------
	quit:(pidSupplier = "") ""
	
	new totalOcorrenciasFornec, idOcorrencia
	set totalOcorrenciasFornec = 0
	
	$$$Order3(^VARFornecedorOcorrencia,YM,pidSupplier,idOcorrencia)		
		set totalOcorrenciasFornec = $increment(totalOcorrenciasFornec)
	$$$End
	
	quit totalOcorrenciasFornec
]]></Routine>
</Export>