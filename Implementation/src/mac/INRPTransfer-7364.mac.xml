<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRPTransfer" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRPTransfer
	#include COMSYS
	#include INConst	
 
QOH(pidCompany,pidTransfer,pidLine,pblnSending=$$$YES)
	;-------------------------------------------------------------------------------
	; Return QOH for a transfer line, for the sending/receiving location.
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidTransfer		: The transfer number
	; 			pidLine			: The transfer line number
	; 			pblnSending		: Whether to check the sending loc, or receive loc.
	;
	; ByRefs:
	;
	; Returns: fltQty (The QOH)
	;
	; History:
	; 20-Apr-2006	Steve S		SR14429: Created
	;-------------------------------------------------------------------------------
	new fltQty,objINTFRLine,idItem,objINTFR,idLoc,YM,YLOCATION
 
	set fltQty=0
	
	if ($get(pidCompany)'="")&&($get(pidTransfer)'="")&&($get(pidLine)'="") {
		set objINTFRLine=$get(^INTFRLine(pidCompany,pidTransfer,pidLine,1))
		set idItem=$$$INTFRLineItem(objINTFRLine)
		
		if (idItem'="") {
			set objINTFR=$get(^INTFR(pidCompany,pidTransfer,1))
			
			if pblnSending {
				set idLoc=$$$INTFRSendingLocation(objINTFR)	
			} else {
				set idLoc=$$$INTFRReceivingLocation(objINTFR)
			}
			
			do ^WWWVAR
			set YM=$get(pidCompany)
			set YLOCATION=idLoc
			
			set fltQty=$$^INARTMENGE(idItem,,idLoc)
			
		}
	}
 
	quit fltQty
	
	
AverageConsumption(pidCompany,pidTransfer,pidLine)
	;-------------------------------------------------------------------------------
	; Return average consumption for a transfer line
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidTransfer		: The item number
	; 			pidLine			: For this location
	;
	; ByRefs:
	;
	; Returns: fltConsumption
	;
	; History:
	; 20-Apr-2006	Steve S		SR14424: Created
	;-------------------------------------------------------------------------------
	new YM,fltConsumption
 
	set fltConsumption=0
	
	set YM=$get(pidCompany)
	
	if (YM'="")&&($get(pidTransfer)'="")&&($get(pidLine)'="") {
		// TODO: Compute the consumption		
	}
 
	quit fltConsumption
	
	
PickedQuantity(pidCompany,pidTransfer,pidLine)
	;-------------------------------------------------------------------------------
	; For a transfer line, return the quantity that has been picked.
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidTransfer		: The transfer number
	; 			pidLine			: The transfer line number
	;
	; ByRefs:
	;
	; Returns: fltQty	(The picked qty)
	;
	; History:
	; 19-Jun-2006	PO			SR14669: Index on INTFRPickLine changed
	; 20-Apr-2006	Steve S		SR14418: Created
	;-------------------------------------------------------------------------------
	new YM,fltQty,idxLine,idPickLine,objINTFRPickLine,idxTransfer
 
	set idxTransfer = $$$Index(pidTransfer)
 
	set fltQty=0
	
	set YM=$get(pidCompany)
	
	if (YM'="")&&($get(pidTransfer)'="")&&($get(pidLine)'="") {
		set idxLine=$$$Index(pidLine)
		
		set idPickLine=""
		for {
			set idPickLine=$order(^INTFRPickLines(YM,1,idxTransfer,idxLine,pidTransfer,idPickLine))
			quit:(idPickLine="")
			
			set objINTFRPickLine=$get(^INTFRPickLine(YM,pidTransfer,idPickLine,1))
			
			set fltQty=fltQty+$$$INTFRPickLinePickedQuantity(objINTFRPickLine)
		}
	}
 
	quit fltQty
	
	
ReceiptNumber(pidCompany,pidTransfer)
	;-------------------------------------------------------------------------------
	; For a transfer line, get its receipt number.
	;
	; Params:	pidCompany		: The company number (YM)
	; 			pidTransfer		: The transfer number
	;
	; ByRefs:
	;
	; Returns: strNumber (receipt number)
	;
	; History:
	; 20-Apr-2006	Steve S		SR14418: Created
	;-------------------------------------------------------------------------------
	new YM,strNumber,idxTransfer,idREC
	
	set strNumber=""
	set YM=$get(pidCompany)
	
	if (YM'="")&&($get(pidTransfer)'="") {
		set idxTransfer=$$$Index(pidTransfer)
		set idREC=""
		for {
			set idREC=$order(^INRECs(YM,1,idxTransfer,idREC))
			quit:(idREC="")
			
			if strNumber="" {
				set strNumber=idREC	
			} else {
				set strNumber=strNumber_","_idREC
			}
		}
	}
	
	quit strNumber
]]></Routine>
</Export>