<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPatientConsumptionHC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESPatientConsumptionHC
	#include WWWConst
	#include COMSYS
	#include COMConst
	#include MEDConst
	#include COMGridEdit31
	#include INConst
	#include VARConst
	#include VARSESFarmaciaAltoCusto
	quit

 //okv3
OnBeforeButtonLine()
	if ('$$IsEditavel(YKEY)) {
		set $$$WWW120AuthorizationToModifyData(YVOR) = 5 // Read Only
	}
	quit
 //okv3
OnAfterPrimaryKey()
	new objTreatment, objProfSaude
 	if (YSEITE=3) {
		if ($length(YKEY)=0) quit
		set strChaveSemBarra = $translate(YKEY,"/"," ")
		if ($length(strChaveSemBarra)=0) quit
		set idHCReverse = $order(^INIssues(0,9,strChaveSemBarra,""))
		if ($length(idHCReverse)=0) quit
		set objEstorno = ^INIssue(0,idHCReverse,1)
		if ($length(objEstorno)=0) quit
		set objDispensacao= $get(^INIssue(0,YKEY,1))
		set strMotivoEstorno=$$$INIssueFREE19(objDispensacao) 
		set strObservacaoEstorno=$$$INIssueFREE15(objDispensacao) 
		set strEstornadoPor=$$$INIssueFREE14(objEstorno) 
		set datEstornadoEm=$$$INIssueFREE13(objEstorno) 
	} else {
		if ($$$INIssueReference(YFELD) '= "") {
			set bolPrint = 0
			if ($$IsReverted($get(YKEY))) {
				set strChaveSemBarra = $translate(YKEY,"/"," ")
				set idHCReverse = $order(^INIssues(0,9,strChaveSemBarra,""))
				set objEstorno = $get(^INIssue(0,idHCReverse,1))
				set datEstornadoEm=+$$$INIssueFREE14(objEstorno) 
				set bolPrint = 1
			}
			
			set objTreatment = $get(^VARSESTreatmentHC(0, $$$INIssueReference(YFELD), 1))
			if ('$$$VARSESTreatmentHCTemFaturamento(objTreatment)) {
				set bolPrint = 1
			}
			if (bolPrint) {
				w !, "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:6px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
				if ('$$$VARSESTreatmentHCTemFaturamento(objTreatment)) {
					w !, "<font color=blue size=2>O tratamento é uma AEPAC-SES</font>"
				}
				if ($$IsReverted($get(YKEY))) {
					w !, "<font color=red size=2>Esta dispensação foi estornada em "_$zdate(+datEstornadoEm,4)_".</font>"
				}
				w !, "</div><BR>"
			}
			
			set GblNumeroAPAC = $$$VARSESTreatmentHCNumeroAPAC(objTreatment)
			set GblDataInicioTratamento = $$$VARSESTreatmentHCDataInicioTratamento(objTreatment)
			set GblDataTerminoTratamento = $$$VARSESTreatmentHCDataTerminoTratamento(objTreatment)
			set GblCodigoPaciente = $$$INIssueCustomerName(YFELD)
			if (GblCodigoPaciente '= "") {
				set GblObservacoesPaciente = $$$MEDPatientFREE13($get(^MEDPatient(0,GblCodigoPaciente,1)))
			} else {
				set GblObservacoesPaciente = ""
			}
			set GblObservacao = $$$VARSESTreatmentHCObservacao(objTreatment)
			set GblObservacaoAprovador = $$$VARSESTreatmentHCObservacaoAprovador(objTreatment)
			set GblIdProfSaude = $$$VARSESTreatmentHCNumeroDocumentoProfSaude(objTreatment)
			if (GblIdProfSaude '= "") {
				set GblMedicoConselho = $$$VARSESTreatmentHCTipoDocumentoProfSaude(objTreatment)
			}
		}
		else {
			set GblNumeroAPAC = ""
			set GblDataInicioTratamento = ""
			set GblDataTerminoTratamento = ""
			set GblCodigoPaciente = ""
			set GblObservacao = ""
			set GblObservacaoAprovador = ""
			set GblIdProfSaude = ""
			set GblMedicoConselho = ""
			set GblObservacoesPaciente = ""
		}
	}
	
	quit
 //okv3
OnAfterDataFields()
 
	if (($get(^VARSESPatientConsumptionHCFirstSave(YBED))=1) && ($get(^VARSESPatientConsumptionHCAfterSave(YBED))=1)) {
		kill ^VARSESPatientConsumptionHCFirstSave(YBED)
		kill ^VARSESPatientConsumptionHCAfterSave(YBED)
		
		if ((+$piece($zdate(GblDataTerminoTratamento,4),"/",2)=+$piece($zdate($h,4),"/",2)) && (+$piece($zdate(GblDataTerminoTratamento,4),"/",3)=+$piece($zdate($h,4),"/",3))) {
			do StartScript^COMUtiljavascript()
			
			write "if (confirm('Último mês do tratamento. Deseja imprimir a LME?')) {"
			write YCR
			write "window.location='"_YAKTION_"EP=WWWMANU&amp;YFORM="_YFORM_"&YEXEC=do|RunReportLME^VARSESJasperRunReport(YKEY)"
			do ^WWWCGI
			write "'"
			write YCR
			write "}"
			
			do EndScript^COMUtiljavascript()
		}
	}
	
 	new strNumeroPedidoAutorizacaoNome
 
	set strNumeroPedidoAutorizacaoNome = "Y"_YFORM_"D19"
 
	$$$StartScript()
	&js<
		function codigoDispensacaoOnFocus() {
			document.#(YHTMFORM)#.#(strNumeroPedidoAutorizacaoNome)#.focus();
		}
	>
	$$$EndScript()
 
	if ($$$NoKey(YKEY)) quit
 
	$$$StartScript()
	&js<
		function processar() {
			CallBackNow('ProcessOnClick^VARSESPatientConsumptionHC', '#(YKEY)#');
		}		
	>
	$$$EndScript()
 	set GblCBO = $$GetCBO()
	do LoadGrid(YKEY)
	quit
	
 //okv3
OnBeforeSave()
	new objTreatment, objPaciente, idProfSaude, strStatus
	set GblCBO = $$GetCBO()
	if ((YKEY = "") || (YKEY = "+")) {
 		set ^VARSESPatientConsumptionHCFirstSave(YBED) = 1
 	} else {
	 	kill ^VARSESPatientConsumptionHCFirstSave(YBED)
 	}
 
	if ($length($$$INIssueFREE30(YFELD)) = 0) {
		set $$$INIssueFREE30(YFELD) = 0
	}
	if ($length($$$INIssueFromLocn(YFELD)) = 0) {
		set $$$INIssueFromLocn(YFELD) = YLOCATION
	}
  
	set strStatus = $$$OK 
	if ($length($$$INIssueReference(YFELD)) = 0) {
		set strStatus = "0 Número do Pedido/Autorização não preenchido ou inválido"
	}
 
	if ($$$ISOK(strStatus) && ($$$NoKey(YKEY) ||
		($$$INIssueReference(YFELD) '= $$$INIssueReference($get(^INIssue(0,YKEY,1)))))) {
		set objTreatment = $get(^VARSESTreatmentHC(0, $$$INIssueReference(YFELD), 1))
 
		set strStatus = $$VerificarPrescricao(objTreatment)
		if ($$$ISOK(strStatus)) {
			set $$$INIssueCustomerName(YFELD) = $$$VARSESTreatmentHCCodigoPaciente(objTreatment)
			set $$$INIssueFREE16(YFELD) 	  = $$$VARSESTIPOPACIENTEUSERAltaComplexidade
			set objPaciente = $get(^MEDPatient(0, $$$INIssueCustomerName(YFELD), 1))
			set $$$INIssueFREE20(YFELD) = $$$MEDPatientExternalCode(objPaciente)
			set $$$INIssueFREE8(YFELD) = $$$MEDPatientName(objPaciente)
			if ($length($$$MEDPatientDOB(objPaciente)) > 0) {
				set $$$INIssueFREE7(YFELD) =
					$zdate($$$MEDPatientDOB(objPaciente), 4)
			}
			else {
				set $$$INIssueFREE7(YFELD) = ""
			}			
			set $$$INIssueReferrerName(YFELD) = $$$VARSESTreatmentHCUsuarioAutorizador(objTreatment)
			set $$$INIssueFREE4(YFELD) 		  = $$$VARSESTreatmentHCTipoDocumentoProfSaude(objTreatment)
			set $$$INIssueReferrerID(YFELD)   = $$$VARSESTreatmentHCNumeroDocumentoProfSaude(objTreatment) 
			set GblIdProfSaude = $$$VARSESTreatmentHCNumeroDocumentoProfSaude(objTreatment)

			do RegistraUsuarioResponsavel(.YFELD)
 
			set $$$CacheTempConf($$$CONFRECREATELINES) = 1
		}
	}
 
	if ($$$ISOK(strStatus)) {
		set strStatus = $$IsEditavel(YKEY)
		if ($$$ISOK(strStatus)) set strStatus = $$$GRIDSave(YKEY)
	}
 
	if ($$$ISERR(strStatus)) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	quit
 //okv3
OnAfterSave()
	if ($get($$$CacheTempConf($$$CONFRECREATELINES))) {
		do CreateLinesFromTreatment^VARSESPatientConsumptionHCLine(
			$$$INIssueReference(YFELD), YKEY)
		kill $$$CacheTempConf($$$CONFRECREATELINES)
	} else {
		set idPaciente = $$$INIssueCustomerName(YFELD)
		if (($length(idPaciente)>0) && ($data(^MEDPatient(0,idPaciente,1)))) {
			set objPaciente = ^MEDPatient(0,idPaciente,1)
			set $$$MEDPatientFREE13(objPaciente)=$get(strObservacoesPaciente)
			set strStatus=$$$Save("MEDPatient",idPaciente,objPaciente,$$$YES)
			if ('$$$ISOK(strStatus)){$$$Alert("Erro na mudanca da observação do paciente "_idPaciente)}
		}
	}
	set status =  $$updateLines^VARSESPatientConsumptionHCLine(YKEY)
	if (status '= 1) && ($get(^VARSESPatientConsumptionHCAfterSave(YBED,YKEY))){
		$$$Alert(status)
	}
	set ^VARSESPatientConsumptionHCAfterSave(YBED,YKEY) = 1
	set ^VARSESPatientConsumptionHCAfterSave(YBED) = 1
	
	quit
 //okv3
VerificarPrescricao(pobjTreatment)
	new strStatus
 
	set strStatus = $$$YES
	if ($$$VARSESTreatmentHCStatus(pobjTreatment) = $$$TreatmentStatusEmEdicao) {
		set strStatus = "0 Esta prescrição está em situação de edição e não está liberada para uso"
	}
	if ($$$VARSESTreatmentHCStatus(pobjTreatment) = $$$TreatmentStatusCancelado) {
		set strStatus = "0 Esta prescrição está cancelada e não está liberada para uso"
	}
	if ($$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil() >
			$$$VARSESTreatmentHCDataTerminoTratamento(pobjTreatment)) {
		set strStatus = "0 A APAC / AEPAC está vencida. Favor renovar!"
	}
 
	quit strStatus
 //okv3
IsEditavel(pidINIssue)
	new strStatus, objINIssue
 
	set strStatus = $$$OK
	if ($$$NoKey(pidINIssue)) quit strStatus
 	set objINIssue = $get(^INIssue(YM, pidINIssue, 1))
	if ($Length(objINIssue) > 0) { 
		if ($$$INIssueStatus(objINIssue) '= $$$EnumINSTATUSUnprocessed) {
			set strStatus = $listbuild("IN00377") // Registro já processado
		}

		set objTreatmentHC = $get(^VARSESTreatmentHC(YM,
			$$$INIssueReference(^INIssue(YM, pidINIssue, 1)), 1))
		if ($length($$$VARSESTreatmentHCStatusTRAK(objTreatmentHC))>0) {
			if (($$$VARSESTreatmentHCStatusTRAK(objTreatmentHC)'=2) &&
				($$$VARSESTreatmentHCStatusTRAK(objTreatmentHC)'=3)) {
				set strStatus = "0 Status do TRAK não é Avaliado ou Aprovado"
			}
		}
	}
	quit strStatus
 //okv3
LoadGrid(pidINIssue)
 	new YFORM, YKEY, YAUSWAHL, YFELD
 
	set YFORM = "VARSESPatientConsumptionHCLine"
	set YFELD = ""
	set YKEY = $get(pidINIssue)
 
	set $$$COMGridEditParameterSharedForm(YAUSWAHL) = 1
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 320
	set $$$COMGridEditParameterGridName(YAUSWAHL) = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL) = ($piece(YVOR, Y, 23) '= 5)
	set $$$COMGridEditParameterContainer(YAUSWAHL) = "VARSESPatientConsumptionHC"
 	$$$GRIDStart(YFORM,YKEY)
	quit
  //okv3
SearchOnClick()
	set link=YAKTION_"EP=WWWFORM&YFORM="_"VARSESPatientConsumptionHCPesquisa"_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YPARA="_""
    $$$StartScript()
	write "window.location = '"_link_"'"
	$$$EndScript()
	quit
	
  //okv3
RegistraUsuarioResponsavel(pYFELD)
	set $$$INIssueFREE10(pYFELD) = YBED
	set $$$INIssueFREE25(pYFELD) = $$$WWW013FREE10($get(^WWW013(0,YBED,1)))
 	quit
 
  //okv3
GetNumeroAPAC(pFREE15)
	new objTreatment
 	if ($length(pFREE15)>""){
	set objTreatment = $get(^VARSESTreatmentHC(0, pFREE15, 1))
 	} else {
		quit ""
	}
 
	quit $$$VARSESTreatmentHCNumeroAPAC(objTreatment)
 
  //okv3
GetMesCompetencia(pobjINIssue="")
	if ($length(pobjINIssue) = 0) set pobjINIssue = YFELD
 
	quit $$GetMes^VARSESFarmaciaAltoCustoUtil($$$INIssueCreatedOn(pobjINIssue))
  //okv3
GetDataRetorno()
	new intDay, intDayWeek
 
	set intDay = +$$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil()
	set intDay = intDay + 28
	set intDayWeek = $zdate(intDay, 10)
	if (intDayWeek = 0) set intDay = intDay - 3
	if (intDayWeek = 6) set intDay = intDay - 2
	if (intDayWeek = 5) set intDay = intDay - 4
 
	quit $zdate(intDay, 4)
 //okv3
CanProcess(YKEY)
	new blnTemQuantidadeDiferenteZero, idINIssueLine, objINIssueLine
 
	if ('$$$NoKey(YKEY)) {
		set blnTemQuantidadeDiferenteZero = 0
		set idINIssueLine = $order(^INIssueLine(YM, YKEY, ""))
		while ($length(idINIssueLine) > 0) {
			set objINIssueLine = $get(^INIssueLine(YM, YKEY, idINIssueLine, 1))
	
			if ($$$INIssueLineQtyPicked(objINIssueLine) > 0) {
				set blnTemQuantidadeDiferenteZero = 1
				quit
			}
	
			set idINIssueLine = $order(^INIssueLine(YM, YKEY, idINIssueLine))
		}
		if ('blnTemQuantidadeDiferenteZero) {
			set YQ = $$$YQDisable("Pelo menos uma quantidade dispensada deve estar diferente de 0.")
			quit
		}
	}
 
	do ValidaBotaoProcessar^VARSESINIssue(YKEY)
	quit
 //okv3
ProcessOnClick(pidINIssue)
	new strStatus
 
	if ($length(pidINIssue) = 0) quit
 
	if ($$$WWW013FREE4(^WWW013(0,YBED,1)) = "" ) {
		$$$Alert("É necessário ter um CBO cadastrado para executar esta funcionalidade")
		quit
	}
	set objINIssue = $get(^INIssue(YM, pidINIssue, 1))
	if ($length(objINIssue)>0) {
		if ($$GetMesCompetencia(objINIssue) '=
				$$GetMes^VARSESFarmaciaAltoCustoUtil(
					$$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil())) {
			$$$Alert("O mês de competência desta dispensação já terminou e ela não pode mais ser utilizada")
			quit
		}
		set objTreatmentHC = ^VARSESTreatmentHC(YM,$$$INIssueReference(objINIssue),1)
		if ($$$VARSESTreatmentHCStatus(objTreatmentHC)=$$$TreatmentStatusCancelado) {
			$$$Alert("O Número de Autorização já existe e está cancelado.")
			quit
		}
	}
 
 	set strStatus = $$VerifyAll(pidINIssue)
	if ($$$ISOK(strStatus)) {
		set strStatus = $$Transaction^COMTransaction("Process^VARSESPatientConsumptionHC("""_pidINIssue_""")")
	}
	if ($$$ISERR(strStatus)) {
		$$$Alert("Erro ao processar dispensação: "_$$DecodeError^COMUtilError(strStatus))
	}
 
	do GoToForm^COMUtilForm("VARSESPatientConsumptionHC", pidINIssue)
	quit
  //okv3
VerifyAll(pidINIssue)
	new strStatus, objINIssue, idTreatment, objTreatment, idINIssueLine,
		objINIssueLine
 
	set strStatus = $$$YES
 
	set objINIssue = $get(^INIssue(YM, pidINIssue, 1))
	set idTreatment = $$$INIssueReference(objINIssue)
	set objTreatment = $get(^VARSESTreatmentHC(0, idTreatment, 1))
 
	set strStatus = $$VerificarPrescricao(objTreatment)
	if ('$$$ISOK(strStatus)) quit strStatus
 
	if ($$$ISOK(strStatus)) {
		set idINIssueLine = $order(^INIssueLine(YM, pidINIssue, ""))
		while ($length(idINIssueLine) > 0) {
			set objINIssueLine = $get(^INIssueLine(YM, pidINIssue, idINIssueLine, 1))
 
			set YKEY = pidINIssue_","_idINIssueLine
			set YFELD = objINIssueLine
			set $$$INIssueLineQtyPicked(YFELD) =
				$$GetSOHLocal^VARSESPatientConsumptionHCLine(
					$$$INIssueLineItem(YFELD), YLOCATION)
			if ('$$ValidarQuantidade^VARSESPatientConsumptionHCLine()) {
				set strStatus = "0 Quantidade inválida"
				quit
			}
			if (('$$IsEditavel^VARSESPatientConsumptionHCLine()) &&
				($$$INIssueLineQtyPicked(objINIssueLine) > 0)) {
				set strStatus = "0 Linha "_idINIssueLine_" não tem quantidade disponível para retirada"
				quit
			}
 
			set idINIssueLine = $order(^INIssueLine(YM, pidINIssue, idINIssueLine))
		}
	}
 
	quit strStatus
 //okv3
Process(pidINIssue)
	new strStatus, objINIssue
	set strStatus = $$Transaction^COMTransaction("FirmTxn^INIssue("""_pidINIssue_""")",$$$YES)	
	if ($$$ISOK(strStatus)) {
		set objINIssue = $get(^INIssue(YM, pidINIssue, 1))
		
		if ('$$$ISOK(strStatus)) {
			set $zerror = "Erro no ajuste de estoque. => "_$$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
 
		set strStatus = $$UpdateStatus^VARSESTreatmentHC($$$INIssueReference(objINIssue))
		if ('$$$ISOK(strStatus)) {
			set $zerror = "Erro ao alterar status da prescrição. => "_$$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
 
		set strStatus = $$ExecutaPosProcessamento^VARSESPatientConsumptionHCLine(
			$$$INIssueReference(objINIssue), pidINIssue, $$$INIssueCreatedOn(objINIssue))
		if ('$$$ISOK(strStatus)) {
			set $zerror = "Erro ao atualizar dados relacionados com os produtos na linha de detalhamento da dispensação. => "_$$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
	}
	else {
		set $zerror = $$$Text(334018)_" => "_$$DecodeError^COMUtilError(strStatus)
		zquit 1 GOTO @$ZTRAP
	}
 
	quit $$$OK 
  //okv3
ValidaBotaoRelatorio(pYKEY)
	;-------------------------------------------------------------------------------
    ; History:
    ; 14-Sep-2008	heber		Do not process if qty field empty
    ; 21-May-2008	GustavoF	Alert user which lines does not have stock anymore from
    ; 							the time he created the line to the time he tries to
    ; 							click on process.
    ; 
    ; 15-Sep-2007	GustavoF	Não deixa o usuário processar o registro se não for
    ; 							um registro do seu local.
    ;-------------------------------------------------------------------------------
 
	new LocalUsuario, LocalDonoDoRegistro, idINIssueLine, objINIssue, CodProduto,
		Quantidade, EstoqueAtual, strMensagem, blnDisable
 
	if ($length(pYKEY) = 0) {
		set YQ = 1_"Nenhuma dispensação selecionada"
		quit
	}
	
	set objINIssue = $get(^INIssue(YM, pYKEY, 1))
	if ($$$INIssueStatus(objINIssue) '= 9) { ; 9 processado todo modificar para um Enum ou status returno
		set YQ = 1_"Dispensação ainda não foi processada"
		quit
	}
 
	quit
 //okv3
ValidaBotaoEstorno(pYKEY)
	new LocalUsuario, LocalDonoDoRegistro, objINIssue, strChaveSemBarra
	
	set LocalUsuario = YLOCATION
	set LocalDonoDoRegistro = $$$INIssueFromLocn(YFELD)
	
	if LocalUsuario '= LocalDonoDoRegistro {  // verifica se o usuário é do mesmo local do documento
		set YQ = $$$YQDisable()
		quit
	}
	
	set objINIssue = $get(^INIssue(YM, pYKEY, 1))
	if ($$$INIssueStatus(objINIssue) '= 9) { // verifica se a dispensação ja foi processada
		set YQ = 1_"Dispensação ainda não foi processada"
		quit
	}
 
    set strChaveSemBarra = $translate(pYKEY,"/"," ")
	if (($$$INIssueStatus(objINIssue) = $$$EnumINSTATUSReverted) || ($order(^INIssues(YM, 9, strChaveSemBarra, "")) '= "")
	        ) { // verifica se a dispensação ja foi estornada
		set YQ = 1_"Dispensação já foi estornada"
		quit
	}
	
	if ($$GetMes^VARSESFarmaciaAltoCustoUtil($$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil()) '= $$GetMesCompetencia(objINIssue)){ // verifica se o mes da dispensação ja acabou
		set YQ = 1_"O mês de competência desta dispensação já terminou e ela não pode mais ser utilizada"
		quit
	}
	
	quit $$$OK
	//okv3
ValidaBotaoRelatorioLME(pYKEY)
 
	new strStatus
	
	set strStatus = $$$OK
	
	if $$$NoKey(pYKEY) {
		set strStatus = $$$MakeStatus(32072)  ; "Please Select A Data Record First."
	}
	
	$$$YQHandler(strStatus)
	
	quit
	//okv3
IsReverted(idPatientConsumptionHC)
	set strSemBarra = $translate(idPatientConsumptionHC,"/"," ")
	if ($length(strSemBarra)=0) {
		quit ""
	} else {
		quit $order(^INIssues(0,9,strSemBarra,""))'=""
	}
 	//okv3
ValidaBotaoPaciente(pYKEY)
	new strStatus
	
	set strStatus = $$$OK
	
	if $$$NoKey(pYKEY) {
		set strStatus = $$$MakeStatus(32072)  ; "Please Select A Data Record First."
	}
	
	$$$YQHandler(strStatus)
	quit
 	//okv3
OpenPatient(pYKEY)
	new objINIssue, codPaciente 
	set objINIssue = ^INIssue(0,pYKEY,1)
	set codPaciente = $$$INIssueCustomerName(objINIssue)
	do GoToForm^COMUtilForm("MEDPatient",codPaciente)
	quit
 	//okv3
Historico(pYKEY)
 	new objConsulta, objINIssue
	set objINIssue = ^INIssue(0,pYKEY,1)
	
	set $piece(objConsulta,Y,3) = $$$INIssueCustomerName(objINIssue)	;CodPaciente
	set $piece(objConsulta,Y,4) = 2							;Alta Complexidade
 
	set ^VARSESHistAtendimentoPacTEMP(YBED,YUSER)= objConsulta
	do GoToForm^COMUtilForm("VARSESHistAtendimentoPaciente",YKEY)

	quit
	
GetCBO()
	quit $$$WWW013FREE4($Get(^WWW013(0,YBED,1)))
]]></Routine>
</Export>