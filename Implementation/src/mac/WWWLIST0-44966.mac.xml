<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWLIST0" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWLIST0
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LISTGENERATORSTART
	;  FORMATIERUNG DER EINGABEFELDER ;formatting the 
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 14-Nov-2006	GRF		Doco; Dots; quits
	; 05.09.1997	DT		(C) BY DITMAR TYBUSSEK 1997
	;-------------------------------------------------------------------------------
	quit:$get(YFORM)=""
	
	set YLFN=""
	for  set YLFN=$order(^WWW131(0,YFORM,YLFN)) quit:YLFN=""  do
	. set YLF=$get(^WWW131(0,YFORM,YLFN,1))  ;DATEISATZ
	. set YDATEI=$piece(YLF,Y,1)  ;DATEI, DIE GEDRUCKT WIRD
	. quit:YDATEI=""
	. set YI=""
	. for  set YI=$order(^WWW002(0,YDATEI,YI)) quit:YI=""  do
	. . set YTYP=$piece($get(^WWW002(0,YDATEI,YI,1)),Y,3)
	. . set YFELD=$get(YLP(YLFN,$extract(100+YI,2,3)))
	. . if YFELD'="" set YLP(YLFN,$extract(100+YI,2,3))=$$GetInternal^WWWTR(YTYP,YFELD)
	. . set YFELD=$get(YLP1(YLFN,$extract(100+YI,2,3)))
	. . if YFELD'="" set YLP1(YLFN,$extract(100+YI,2,3))=$$GetInternal^WWWTR(YTYP,YFELD)
	. ;
	. set YI=""
	. for  set YI=$order(^WWW003(0,YDATEI,YI)) quit:YI=""  quit:YI>99  do
	. . set YTYP=$piece($get(^WWW003(0,YDATEI,YI,1)),Y,3)
	. . set YFELD=$get(YLD(YLFN,$extract(100+YI,2,3)))
	. . if YFELD'="" set YLD(YLFN,$extract(100+YI,2,3))=$$GetInternal^WWWTR(YTYP,YFELD)
	. . set YFELD=$get(YLD1(YLFN,$extract(100+YI,2,3)))
	. . if YFELD'="" set YLD1(YLFN,$extract(100+YI,2,3))=$$GetInternal^WWWTR(YTYP,YFELD)
	
	set YI=""
	for  set YI=$order(^WWW122(0,YFORM,YI)) quit:YI=""  do
	. quit:$piece($get(^WWW122(0,YFORM,YI,1)),Y,1)'=""  ;KEIN MANUELLES FELD ;no field 
	. set YTYP=$piece($get(^WWW122(0,YFORM,YI,1)),Y,5)
	. if $get(YM(YI))'="" set YM(YI)=$$GetInternal^WWWTR(YTYP,YM(YI))
	
	;+++++++++++++++++++++++++++++++++++++++
	;  IMPLICIT DROP THROUGH
	;+++++++++++++++++++++++++++++++++++++++
	
START ;STARTEN DES FORMULARS ;launching 
	set KOPF=$$^WWWTEXT(6)
	do ^WWWSTART(KOPF)
	set YVOR1=$get(^WWW012(0,YM,1))  ;LAYOUTVORGABEN
	do:YUSER=""  if YUSER'="" if $piece($get(^WWWUSER(0,YUSER,1)),Y,6)=0 do
	. if YUSER'="" set $piece(^WWWUSER(0,YUSER,1),Y,6)=1
	. write "<BODY"
	. if $piece(YVOR,Y,3)'="" write " BACKGROUND="_""""_YGIF_$piece(YVOR,Y,3)_""""
	. if $piece(YVOR,Y,4)=1   write " BGPROPERTIES=fixed"
	. if $piece(YVOR,Y,5)'=""  do
	. . if $length($piece(YVOR,Y,5))=6  write " BGCOLOR="_""""_"#"_$piece(YVOR,Y,5)_""""
	. . if $length($piece(YVOR,Y,5))'=6 write " BGCOLOR="_""""_$piece($get(^WWW100(0,"FARBE",SPRACHE,$piece(YVOR,Y,5),1)),Y,1)_""""
	. ;
	. if $piece(YVOR1,Y,12)'=""  do
	. . if $length($piece(YVOR1,Y,12))=6  write " LINK="_""""_"#"_$piece(YVOR1,Y,12)_"""" write " VLINK="_""""_"#"_$piece(YVOR1,Y,12)_""""
	. . if $length($piece(YVOR1,Y,12))'=6 write " LINK="_""""_$piece($get(^WWW100(0,"FARBE",SPRACHE,$piece(YVOR1,Y,12),1)),Y,1)_"""" write " VLINK="_""""_$piece($get(^WWW100(0,"FARBE",SPRACHE,$piece(YVOR1,Y,12),1)),Y,1)_""""
	. ;
	. write " topmargin=1 leftmargin=1"
	. if $get(YFORM)'="" do   ;BEC;11.10.04;26562
	. . quit:$piece($get(^WWW120(0,YFORM,1)),Y,85)'=1
	. . write " onLoad="_""""_"window.print();"_""""
	. ;
	. write ">"
	
	write YCR
	write "<FONT"
	if $piece(YVOR,Y,9)'="" write " FACE="_""""_$piece($get(^WWW100(0,"SCHRIFTART",SPRACHE,$piece(YVOR,Y,9),1)),Y,1)_""""
	if $piece(YVOR,Y,7)'="" write " SIZE="_""""_$piece(YVOR,Y,7)_""""
	if $piece(YVOR,Y,6)'=""  do
	. if $length($piece(YVOR,Y,6))=6  write " COLOR="_""""_"#"_$piece(YVOR,Y,6)_""""
	. if $length($piece(YVOR,Y,6))'=6 write " COLOR="_""""_$piece($get(^WWW100(0,"FARBE",SPRACHE,$piece(YVOR,Y,6),1)),Y,1)_""""
	
	write ">"
	
	;WRITE YCR,YCR,"<!-- ************************* LISTENÜBERSCHRIFT (WWWLIST0)************************** -->",YCR,YCR
	
	do ^WWWBACK  ;ZURÜCK-BUTTON; FIS,13.12.01
	set YKOPF=$piece(YVOR,Y,1)
	if $data(^WWW1201(0,YFORM,SPRACHE,1)) set YKOPF=$piece(^(1),Y,1)
	write "<B>"
	write YCR,YKOPF
	write "</B><BR>"
	set YDATEI=""
	if YFORM'="" set YDATEI=$piece($get(^WWW131(0,YFORM,1,1)),Y,1)
	if $length($get(YLP(1,"01")))>3 do
	. if YDATEI'="" if $length(+YLP(1,"01"))=5 if $piece($get(^WWW002(0,YDATEI,1,1)),Y,3)=1 write $$^WWWDATE(+YLP(1,"01")) quit  ;DATUM ALS SELEKTION 1 ;Date when 
	. write YLP(1,"01")
	. write "<BR>"
	
	write YCR
	if $find($piece(YVOR,Y,8),1) write "<STRONG><B>"
	if $find($piece(YVOR,Y,8),2) write "<U>"
	if $find($piece(YVOR,Y,8),3) write "<I>"
	if $find($piece(YVOR,Y,8),4) write "<STRIKE>"
	;I $P(YVOR,Y,21)=1 W "<PRE>"
	;I $P(YVOR,Y,10)=1 W "<CENTER>" W YCR,"<TABLE CELLSPACING=0 BORDER="_+$P(YVOR,Y,13)_">",YCR
	do ^WWWLIST  ;SELEKTION
	do ^WWWLISTT  ;DRUCKEN ;print 
	;I $P(YVOR,Y,10)=1 W YCR,"</TABLE>" W "</CENTER>"
	;I $P(YVOR,Y,21)=1 W "</PRE>"
	if $find($piece(YVOR,Y,8),1) write "</STRONG></B>"
	if $find($piece(YVOR,Y,8),2) write "</U>"
	if $find($piece(YVOR,Y,8),3) write "</I>"
	if $find($piece(YVOR,Y,8),4) write "</STRIKE>"
	write "</FONT>"
	write YCR
	do ^WWWSTOP
	quit
]]></Routine>
</Export>