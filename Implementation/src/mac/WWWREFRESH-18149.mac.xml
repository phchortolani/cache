<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWREFRESH" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWREFRESH(YUSER,YFORM)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FORMUALR REFRESH DER ÄNDERUNGEN
	;	SET %TXT(1)=$$^WWWREFRESH(YUSER,YFORM)
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 07-Nov-2007	GRF		Doco; quits
	; 19.10.2000	DT		Created
	;-------------------------------------------------------------------------------
	NEW YI,YEART,YDATEN1,YDATEN2,YDATEI,RETURN,VALUE,YEART2,VALUE1
	
	SET RETURN=""
	QUIT:$GET(YUSER)="" RETURN 
	QUIT:$GET(YFORM)="" RETURN
	QUIT:YM="" RETURN
	SET YDATEI=$PIECE($GET(^WWW120(0,YFORM,1)),Y,11)
	
	;---------------------------------------
	;PRIMÄRSCHLÜSSEL
	;---------------------------------------
	SET YDATEN1=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1))
	SET YDATEN2=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",2))
	
	IF YDATEI'="" FOR YI=1:1:$LENGTH(YDATEN1,",") SET YI(1)=$PIECE(YDATEN1,",",YI)  DO
	. QUIT:YI(1)=$PIECE(YDATEN2,",",YI)
	. SET YEART=$PIECE($GET(^WWW002(0,YDATEI,YI,1)),Y,3)
	. SET VALUE=$$GetLiteral^WWWTR(YEART,YI(1))
	. IF VALUE="" SET VALUE=" "
	. SET RETURN=RETURN_"#Y"_YFORM_"P"_YI_"~"_VALUE
	
	;---------------------------------------
	;DATENFELDER
	;---------------------------------------
	SET YDATEN1=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1))
	SET YDATEN2=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",2))
	
	IF YDATEI'="" SET YI="" FOR  SET YI=$ORDER(^WWW003(0,YDATEI,YI)) QUIT:YI=""  DO
	. ;IF YDATEI'="" FOR YI=1:1:$LENGTH(YDATEN1,Y) SET YI(1)=$PIECE(YDATEN1,Y,YI) D
	. SET YI(1)=$PIECE(YDATEN1,Y,YI)
	. IF YDATEN2'="" QUIT:YI(1)=$PIECE(YDATEN2,Y,YI)  ;TYBD 15.09.2002
	. SET YI(2)=$ORDER(^WWW122s(0,4,YI,YFORM,""))  ;FIS;19.07.2004;DATENFELDNUMMER !
	. SET YEART2 = $PIECE($GET(^WWW122(0,YFORM,+YI(2),1)),Y,2)
	. SET YEART  = $PIECE($GET(^WWW003(0,YDATEI,YI,1)),Y,3)
	. SET VALUE  = $$GetLiteral^WWWTR(YEART,YI(1))
	. IF YEART=2 DO
	. . IF +VALUE=0 SET VALUE="UNCHECKED"
	. . IF +VALUE=1 SET VALUE="CHECKED"
	. ;
	. SET VALUE1=""
	. IF (YEART2=2) || (YEART2=11) DO   ;RADIO
	. . ;SET VALUE1="("_VALUE_")"  ;TYBD;25.03.2003;22842;RADIO
	. . ;SET VALUE="CHECKED"
	. . NEW REL,PARA
	. . SET REL  = $PIECE($GET(^WWW003(0,YDATEI,YI,1)),Y,8)    ;RELATIONSDATEI (PARAMETER)
	. . SET PARA = $PIECE($GET(^WWW003(0,YDATEI,YI,1)),Y,9)    ;KEY FÜR RELATIONSDATEI (Z.B. SPRACHE) ;KEY to 
	. . DO RADIO
	. ;
	. IF YDATEN2'="" IF VALUE="" SET VALUE=" "
	. IF YEART=3 SET VALUE=$TRANSLATE(VALUE,"|","'")   ;TYBD
	. SET RETURN=RETURN_"#Y"_YFORM_"D"_YI_VALUE1_"~"_VALUE  ;TYBD;25.03.2003;22842;RADIO
	
	;---------------------------------------
	;MANUELLE FELDER
	;---------------------------------------
	SET YDATEN1=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"M",1))
	SET YDATEN2=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"M",2))
	
	FOR YI=1:1:$LENGTH(YDATEN1,Y) SET YI(1)=$PIECE(YDATEN1,Y,YI)  DO
	. QUIT:YI(1)=$PIECE(YDATEN2,Y,YI)
	. QUIT:'$DATA(^WWW122(0,YFORM,YI,1))
	. ;
	. SET YEART2 = $PIECE($GET(^WWW122(0,YFORM,YI,1)),Y,2)   ;TYBD;25.03.2003;22842;RADIO
	. SET YEART  = $PIECE($GET(^WWW122(0,YFORM,YI,1)),Y,5)
	. SET VALUE  = $$GetLiteral^WWWTR(YEART,YI(1))
	. IF (YEART=2) || (YEART2=3) DO
	. . IF +VALUE=0 SET VALUE="UNCHECKED"
	. . IF +VALUE=1 SET VALUE="CHECKED"
	. ;
	. SET VALUE1=""
	. IF (YEART2=2) || (YEART2=11) DO   ;TYBD;25.03.2003;22842;RADIO
	. . ;SET VALUE1="("_VALUE_")"       ;TYBD;25.03.2003;22842;RADIO
	. . ;SET VALUE="CHECKED"
	. . NEW REL,PARA
	. . SET REL  = $PIECE($GET(^WWW122(0,YFORM,YI,1)),Y,32)     ;RELATIONSDATEI (PARAMETER)
	. . SET PARA = $PIECE($GET(^WWW122(0,YFORM,YI,1)),Y,33)     ;KEY FÜR RELATIONSDATEI (Z.B. SPRACHE) ;KEY to 
	. . DO RADIO
	. ;
	. IF VALUE="" SET VALUE=" "
	. IF YEART=3  SET VALUE=$TRANSLATE(VALUE,"|","'")       ;TYBD
	. SET RETURN=RETURN_"#Y"_YFORM_"M"_YI_VALUE1_"~"_VALUE  ;TYBD;25.03.2003;22842;RADIO
	
	QUIT RETURN
	
RADIO	;RADIO BUTTON FOR ALPHANUMERIC VALUES;FIS;13.07.04;26092
	NEW SUCH,KEY,LFN,YII
	
	SET SUCH="^"_REL_"("_""""_$$^WWWYM(REL)_""""  ;ZUSAMMENBAU DER GLOBALREFERENZ  ;FIS;14.06.04;25866
	IF PARA'="" SET SUCH=SUCH_","_PARA
	SET SUCH=SUCH_")"
	SET LFN=0
	FOR  DO  QUIT:SUCH=""
	. SET SUCH=$QUERY(@SUCH)
	. IF $PIECE($PIECE(SUCH,"^",2),"(",1)'=REL           SET SUCH=""  QUIT
	. IF $PIECE($PIECE(SUCH,"(",2),",",1)'=$$^WWWYM(REL) SET SUCH=""  QUIT
	. IF PARA'="" FOR YII=2:1:1+$LENGTH(PARA,",") DO
	. . IF $FIND($PIECE(PARA,",",YII-1),"""") IF $PIECE(SUCH,",",YII)'=$PIECE(PARA,",",YII-1) SET SUCH=""
	. . IF '$FIND($PIECE(PARA,",",YII-1),"""") IF $TRANSLATE($PIECE(SUCH,",",YII),"""")'=$TRANSLATE(@$PIECE(PARA,",",YII-1),"""") SET SUCH=""
	. ;
	. QUIT:SUCH=""
	. IF PARA'="" SET KEY=$PIECE(SUCH,",",2+$LENGTH(PARA,","))
	. IF PARA=""  SET KEY=$PIECE(SUCH,",",2)
	. IF $TRANSLATE(KEY,"""")=$TRANSLATE(YI(1),"""") DO
	. . SET VALUE1="("_LFN_")"
	. . SET VALUE="CHECKED"
	. ;
	. SET LFN=LFN+1
	
	QUIT
]]></Routine>
</Export>