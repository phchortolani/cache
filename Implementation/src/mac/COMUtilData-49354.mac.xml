<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilData" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilData
    ;-------------------------------------------------------------------------------
	; Data Anlisys
    ; 
    ; 
	; History:
	; 10-Sep-2007	SCR		Created
    ;-------------------------------------------------------------------------------
 
 
Start()
    ;-------------------------------------------------------------------------------
	; Look at all user classes (Globals)
    ; 
    ; 
	; History:
	; 10-Sep-2008	GRF		Better starting point
	; 10-Sep-2007	SCR		Created
    ;-------------------------------------------------------------------------------
	new strGlo,strTMP
	
	set strTMP="^CacheTempData($job)"  ; Tempory data
	kill @strTMP
	;set strGlo="^A"
	set strGlo="^%zzzzz"
	
	for {
		set strGlo=$order(^$Global(strGlo))
		quit:strGlo=""
		set ok=$$ClassOK(strGlo)  ; Check to see if the class contains user data
		if ok {
			do Class(strGlo,strTMP)
		}
	}
	do File(strTMP,"c:\Temp\ClassUsage.html")
	quit
	
File(pstrTMP,pstrFile)
    ;-------------------------------------------------------------------------------
	; Generate htlm file
    ; 
    ; 
	; History:
	; 10-Sep-2007	SCR		Created
    ;-------------------------------------------------------------------------------
	new strClass,strDesc,intCount,intField,strField
	
	open pstrFile:"NSW":4
	else  write "can not open file ",pstrFile quit 
	use pstrFile
	&html<
	<html>
	<title>User Data</title>
	<body>
	<table border>
	<caption>Class with client data</caption>
	<tr>
	<td>Class</dt><dt>Description</dt><dt>Count</td>
	</tr>
	>
	set strClass="" 
	for {
		set strClass=$order(@pstrTMP@("Class",strClass))
		quit:strClass=""
		set intCount=$get(@pstrTMP@("Class",strClass,"RecCnt"))
		set strDesc=$piece($get(^WWW001(0,strClass,1)),"~",1)
		&html<
		<tr><td><a href="#ClassDetail#(strClass)#" name="ClassSummary#(strClass)#">#(strClass)#</a></td>
		<td>#(strDesc)#</td>
		<td ALIGN=right>#($fn(intCount,","))#</td><tr>
		>	
	}
	&html<
	</table>
	>
	set strClass="" 
	for {
		set strClass=$order(@pstrTMP@("Class",strClass))
		quit:strClass=""
		set intCount=$get(@pstrTMP@("Class",strClass,"RecCnt"))
		set strDesc=$piece($get(^WWW001(0,strClass,1)),"~",1)
		&html<
		<a name="ClassDetail#(strClass)#" href="#ClassSummary#(strClass)#">Back to Summary</a>
		<table border>
		<caption>Field Detatils for Class <b>#(strClass)#(#(strDesc)#) Count = #(intCount)#</caption>
		<tr><td>Field</td><td>Useage Count</td></tr>
		>
		set intField=""
		for {
			set intField=$order(^WWW003(0,strClass,intField))
			quit:'intField
			set strDesc=$piece($get(^WWW003(0,strClass,intField,1)),"~",1)
			set intCount=+$get(@pstrTMP@("Class",strClass,"FldCnt",intField))
			&html<
			<tr><td>#(strDesc)#</td><td ALIGN=right>#($fn(intCount,","))#</td></tr>
			>
		}
		&html<
		</table>
		>
			
	}
	
	&html<
	</body>
	</html>
	>
	use 0
	close pstrFile
	quit
 
Class(pstrGlo,pstrTMP)
    ;-------------------------------------------------------------------------------
	; Check Class
    ; 
    ; 
	; History:
	; 10-Sep-2007	SCR		Created
    ;-------------------------------------------------------------------------------
	new intCount,strGlo,arrFldCount,strData,intFld,intFldCnt,strClass
	set intCount=0
	set strGlo=pstrGlo
	set strClass=$extract(pstrGlo,2,99)
	for {
		set strGlo=$query(@strGlo)
		quit:strGlo=""
		set intCount=intCount+1
		set strData=$get(@strGlo)  ; get the data
		set intFldCnt=$length(strData,"~")  ; get list of fields
		for intFld=1:1:intFldCnt {
			if $piece(strData,"~",intFld)'=""  {
				set arrFldCount(intFld)=$get(arrFldCount(intFld))+1  ; increment field count array if field is not used.
			}
		}
	}
	write !,strClass," = ",intCount
	set @pstrTMP@("Class",strClass,"RecCnt")=intCount
	merge @pstrTMP@("Class",strClass,"FldCnt")=arrFldCount
	quit
 
ClassOK(pstrGlo)
	;-------------------------------------------------------------------------------
	; Check a Global to see if it contains user data
	; History:
	; 10-Sep-2007	SCR		Created
	;-------------------------------------------------------------------------------
 
	new ok,strClass
	set ok = 0
	
	if pstrGlo["^COMTAX" set ok = 1
	
	if pstrGlo["^WWW0121" set ok=1 ; Location
	if pstrGlo["^WWWWAE" set ok=1 ; Currencies
	
	if (pstrGlo["^AEP")||(pstrGlo["^CST")||(pstrGlo["^FIN")||(pstrGlo["^IN")||
	   (pstrGlo["^REP")||(pstrGlo["^SAL")||(pstrGlo["^STK")||(pstrGlo["^V") {
		set ok = 1
	}
	set strClass=$extract(pstrGlo,2,99)
	if strClass="" {
		set ok=0
	}
	if ok=1 {
		if '$data(^WWW001(0,strClass)) {
			set ok=0  ; not a class
		}
	}
	
	quit ok
]]></Routine>
</Export>