<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM91" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORM91       ;WWWFORM91;DT;MULTI SCHLUESSELVORGABE;26.08.2000
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		MULTI SCHLUESSELVORGABE
	;	"Internal Message Display" Form Button - dropdown with "Open" and MRU list
	;
	;	Called by WWWFORMF
	;	
	; Inputs : 
	;
	;
	; ByRef :
	;	YFORART		Form Type
	;	YVOR		WWW120-D88		No of items in MRU List
	; 
	; Returns :
	;
	;
	; History :
	; 17-Oct-2011	GRF		-:  field macros
	; 23-Aug-2011	shobby	SR17861: Fixed the height of the input box so that it 
	;							doesn't appear too small using FF and Windows 7.
	; 22-Dec-2005	JW		SR13195: Edited SAVENOW call.
	; 11-Oct-2005	GRF		Doco (SR13195 research); eliminate long space strings
	; 07-Jul-2005	shobby	SR12892: WWW1262 is no longer shared.
	; 19-Mar-2003	PK		alteration
	; 26-Aug-2000	DT		Created
	;-------------------------------------------------------------------------------
	new YI,YA,YK,YLSTKEY
	
	set YLSTKEY=""
	quit:$get(YFOART)'=1   ;NUR STANDARD FORMULARE ;only 
	
	write YCR,"<select class=""inputCab"" NAME=""YOPEN1"""
	write " size=1 onChange=""javascript:SAVENOW(document.WWW.YOPEN1.options[document.WWW.YOPEN1.selectedIndex].value);"""
	
	;SR17861 WRITE " style=""padding-top:0 ;padding-bottom:0; """   ;style
	write " style=""padding-top:0px ;padding-bottom:0px; height:20px;width:150px; """   ;style ;SR17861
	;WRITE " style="""
	;WRITE "font-face: 'Courier New'"
	;WRITE "; font-family: 'Courier New'"
	;WRITE """"
	write ">"
	
	;"Open"   ;ÖFFNEN
	;---------------------------------------
	write YCR,"<option value=""1"">"_$$^WWWNBSP($extract($$^WWWTEXT(101)_$justify("",34),1,34))_"</option>"
	; FIXME : Why is this 34 chars wide when MRU list is 14? <GRF>  Can we be wider?
	;         NOTE : Reversal of MRU entries before truncation to 14 characters leads to weird appearance
	
	; MRU List
	;---------------------------------------
	set YSORT=""
	for YI=1:1 set YSORT=$order(^WWW1262(0,YFORM,YBED,YSORT),-1) quit:YSORT=""  do
	. if YI>$$$WWW120NumberofMRURecordListItem(YVOR) kill ^WWW1262(0,YFORM,YBED,YSORT) quit  ;ZU VIELE  ;within much  D88
	. if '$$Exists(YFORM,YBED,YSORT,YDATEI)          kill ^WWW1262(0,YFORM,YBED,YSORT) quit             ;sun shobby
	. set YA=""
	. set YK=""
	. for  set YK=$order(^WWW1262(0,YFORM,YBED,YSORT,YK)) quit:YK=""  do       ;SUCHEN DER KEY'S ;seek the 
	. . set YA=YA_", "_$extract($get(^WWW1262(0,YFORM,YBED,YSORT,YK,1)),1,30)  ;DATENSATZ ;data record 
	. . quit
	. ;
	. ;ggf wieder rausnehmen, da nicht funktioniert, wenn user an anderen Rechner eingelogt ist:
	. if YLSTKEY'="" if $listfind(YLSTKEY,YA) kill ^WWW1262(0,YFORM,YBED,YSORT) set YI=YI-1 quit
	. set YLSTKEY=YLSTKEY_$listbuild(YA)
	. ;
	. if $extract(YA)="," set YA=$extract(YA,2,200)
	. write YCR,"<option value="""_1_YSORT_""""
	. set YA=$reverse($extract($reverse(YA),1,14))
	. write ">"_$$^WWWNBSP($extract(YA_$justify("",14),1,14))_"</option>"
	
	write "</select>"
	quit
 
 
Exists(YFORM="",YBED="",YSORT="",YDATEI="")
    ;-------------------------------------------------------------------------------
    ; If the data record does not exist it can be removed from the MRU
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 04-Nov-2008	shobby	SRAdhoc: Can crash if very long key. (probably from corrupt data)
	; 07-Jul-2005	shobby	SR12892: WWW1262 is no longer shared.
    ; 27-Jun-2005	shobby	SR12442
    ;-------------------------------------------------------------------------------
	new blnExists,objWWW1262,idKey,idIndex,strGlobal,strKey,intLoop,blnInvalidKey
	
	set blnExists = $$$YES
	if (YDATEI'="") && (YFORM'="") && (YBED'="") && (YSORT'="") {
		set blnExists = $$$NO
		set idKey     = ""
		set idIndex   = ""
		for {
			set idIndex = $order(^WWW1262(0,YFORM,YBED,YSORT,idIndex))
			quit:(idIndex="")
			
			set objWWW1262 = $get(^WWW1262(0,YFORM,YBED,YSORT,idIndex,1))
			if (objWWW1262="") || (objWWW1262="+") set idKey="" quit
			quit:objWWW1262=""
			
			set idKey = idKey_objWWW1262_","
		}
		
		if idKey'="" {
			set blnInvalidKey = $$$NO
			set idKey  = $extract(idKey,1,$length(idKey)-1)
			set strKey = $$^WWWKEYBUILD(idKey)
			for intLoop=1:1:$length(strKey,",") {
				if $length($piece(strKey,",",intLoop))>255 {
					set blnInvalidKey=$$$YES   ;SRAdhoc
				}
			}
			if 'blnInvalidKey {
				set strGlobal="^"_YDATEI_"("""_$$^WWWYM(YDATEI)_""","_$$^WWWKEYBUILD(strKey)_",1)"
				set blnExists=$data(@strGlobal) ;SRAdhoc
			}
		}
	}
	quit blnExists
	
	
BUTTON
	;-------------------------------------------------------------------------------
	;	BUTTONEFFECT -> NICHT EINGEBAUT, DA OPTISCH NICHT SO GUT. 
	;	ERFORDERT MENU.HTC IN WWWSTART
    ;
    ; History:
	; 27-Sep-2011	shobby	SR17853: Reverted doCancelEvent
    ; 22-Dec-2005	JW		SR13195: Edited SAVENOW call.
	; 07-Jul-2005	shobby	SR12892: WWW1262 is no longer shared.
	; 19-Mar-2003	PK		alteration
	;-------------------------------------------------------------------------------
	new YI,YA,YK,YLSTKEY,LNK,LINKS,YDISABLED
	
	set YLSTKEY=""
	quit:$get(YFOART)'=1   ;NUR STANDARD FORMULARE ;only 
	
	;SUCHEN HISTORY ;seek 
	;ANZEIEN MENU
	;WRITE YCR,"<div id=""cmdPanelMain"" style=""border:1px solid black; padding:0px; WIDTH: 100px;"">"
	write YCR,"<table style=""padding-top:1px; border:none;"" cellspacing=0 cellpadding=0>"
	write YCR,"<tr><td>"
	write YCR,"<table style=""table-layout:fixed; border-left:1px solid ButtonHighLight; border-top:1px solid ButtonHighLight; border-right:2px solid ButtonShadow; border-bottom:2px solid ButtonShadow;"" cellspacing=0 cellpadding=0>"
	write YCR,"<tr><td height=17 width=135 valign=top>"
	write YCR,"<myMenu:menu id=""MyPage"" access=""enabled"""
	write " onsubmenu_click=""var x = event.result; SAVENOW(x); window.event.returnValue = false; window.event.cancelBubble = true;"""	//SR17253 //SR17853
	write " style=""cursor:pointer; text-align:middle; text-align:center; font-family:arial; width:135px;"">"		;SR17253
	
	write $$^WWWTEXT(101)  ; "Open" 
	
	set YSORT=""
	for YI=1:1 set YSORT=$order(^WWW1262(0,YFORM,YBED,YSORT),-1) quit:YSORT=""  do
	. if YI>$$$WWW120NumberofMRURecordListItem(YVOR) kill ^WWW1262(0,YFORM,YBED,YSORT) quit  ;ZU VIELE  ;within much   ; D88
	. set YA=""
	. set YK=""
	. for  set YK=$order(^WWW1262(0,YFORM,YBED,YSORT,YK)) quit:YK=""  do       ;SUCHEN DER KEY'S ;seek the 
	. . set YA=YA_", "_$extract($get(^WWW1262(0,YFORM,YBED,YSORT,YK,1)),1,30)  ;DATENSATZ ;data record 
	. ;
	. if YLSTKEY'="" if $listfind(YLSTKEY,YA) kill ^WWW1262(0,YFORM,YBED,YSORT) set YI=YI-1 quit  ;ÄNDERUNG VON DISC/PAUL KALENJUK 19.03.03 ;alteration 
	. set YLSTKEY=YLSTKEY_$listbuild(YA)
	. if $extract(YA)="," set YA=$extract(YA,2,200)
	. set YA=$reverse($extract($reverse(YA),1,14))
	. ;
	. set YDISABLED="enabled"
	. write YCR,"   <myMenu:menu access="""_YDISABLED_""" id=""1"_YSORT_""">"_YA_"</myMenu:menu>"
	
	write YCR,"</myMenu:menu>"
	write YCR,"</tr></td>"
	write YCR,"</table>"
	write YCR,"</tr></td>"
	write YCR,"</table>"
	;WRITE YCR,"</div>"
	quit
	
]]></Routine>
</Export>