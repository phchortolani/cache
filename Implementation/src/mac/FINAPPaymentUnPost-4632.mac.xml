<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPPaymentUnPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPPaymentUnPost
#include COMSYS
#include FINConst
#include COMConst
#include WWWConst
	;-------------------------------------------------------------------------------
	;	Supplier Payments UnPost
	; 
	; History:
	; 19-Sep-2005	GRF		SR13481 : include - FIN changed to COMSYS
	;-------------------------------------------------------------------------------
 
UnPostNEW(pidFINAPPayment,pblnCancelCheque=$$$NO) 
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Nov-2005	SCR		SR13578: Added Drill Down Detail to Allocate
    ; 26-Sep-2005	PO		SR13568: Created
    ;-------------------------------------------------------------------------------
    ;
    ; !!!!!!!!!!!!!!!!!! NOT IN USE !!!!!!!!!!!!!!!!!!!
    ;
	new objFINAPPayment,strStatus,blnUnPost,strBatchIDPosted,idBatchPosted,idBlockPosted
	new idBatchLinePosted,objFININTBatchLine,idBlockNumberPosted,idBatch,idBlock,dtePaymentDate
	new idAccount,curAmount,strInterfaceType,strParam,strSource,strReference,idFINAPPaymentLine
	new intChequeNumber,objFINAPPaymentLine
	new strForm,strDetail
 
	tstart
 
	set strStatus = $$$OK
	set blnUnPost = $$$YES
	set strStatus = $$GetPaymentObject^FINAPPaymentPost(pidFINAPPayment,.objFINAPPayment)
 
	if $$$ISOK(strStatus) set strStatus=$$UnPostable^FINSYSReview("FINAPPayment",$$$FINAPPaymentStatus(objFINAPPayment),.blnUnPost)
 
	if blnUnPost {
		if $$$ISOK(strStatus) set dtePaymentDate=$$$FINAPPaymentPaymentDate(objFINAPPayment)
		; SR13578 - Source Details for Drill Down - Start
		set strForm		= $$CheckForm^FINUtils($get(YFORM),"FINAPPayment")	; Get the form using YFORM, with validation
		set strDetail 	= "Fin00932,"_pidFINAPPayment  ;Supplier Payment Number %1
		if $$$ISOK(strStatus) set strStatus=$$Allocate^FININTBatch(.idBatch,.idBlock,"APPAY",$get(YLOCATION),dtePaymentDate,,,,strForm,pidFINAPPayment,strDetail)
		;if $$$ISOK(strStatus) set strStatus=$$Allocate^FININTBatch(.idBatch,.idBlock,"APPAY",$get(YLOCATION),dtePaymentDate)
		; SR13578 - Source Details for Drill Down - End
 
		if $$$ISOK(strStatus) {
			set strBatchIDPosted = $$$FINAPPaymentInterfaceBatchID(objFINAPPayment)
 
			if strBatchIDPosted '= "" {
				set idBatchPosted = $piece(strBatchIDPosted,",",1)
				set idBlockPosted = $piece(strBatchIDPosted,",",2)
 
				do SubBlockNumber^FININTBatch(.idBlock) ; Increment Sub Block Number
 
				set idBatchLinePosted = ""
				for {
					set idBatchLinePosted = $order(^FININTBatchLine(YM,idBatchPosted,idBatchLinePosted))
					quit:idBatchLinePosted=""
					set objFININTBatchLine = $get(^FININTBatchLine(YM,idBatchPosted,idBatchLinePosted,1))
					set idBlockNumberPosted = $$$FININTBatchLineBlockNumber(objFININTBatchLine)
 
					continue:idBlockPosted>$piece(idBlockNumberPosted,".",1) ; Skip prior blocks
					quit:idBlockPosted<$piece(idBlockNumberPosted,".",1) ; Stop when finished block
 
					set idAccount		 = $$$FININTBatchLineAccount(objFININTBatchLine)
					set curAmount		 = $$$FININTBatchLineAmount(objFININTBatchLine)
					set strInterfaceType = $$$FININTBatchLineInterfaceType(objFININTBatchLine)
					set strParam		 = $$$FININTBatchLineParameters(objFININTBatchLine)
					set strSource		 = $$$FININTBatchLineSource(objFININTBatchLine)
					set strReference	 = $$$FININTBatchLineReference(objFININTBatchLine)
 
					set strStatus=$$AddLine^FININTBatch(idBatch,idBlock,idAccount,curAmount,strInterfaceType,strParam,strSource,strReference,$$$YES)
 
					quit:$$$ISERR(strStatus)
				}
			} else {
				set strStatus = $listbuild("Fin00898")  ;This record can not be unposted, record does not include interface batch details.
			}
		}
		if $$$ISOK(strStatus) {
			set idFINAPPaymentLine = ""
			for {
				set idFINAPPaymentLine = $order(^FINAPPaymentLine(YM,pidFINAPPayment,idFINAPPaymentLine))
				quit:idFINAPPaymentLine=""
 
				set objFINAPPaymentLine = $get(^FINAPPaymentLine(YM,pidFINAPPayment,idFINAPPaymentLine,1))
 
				set intChequeNumber = $$$FINAPPaymentLineChequeNumber(objFINAPPaymentLine)
				set $$$FINAPPaymentLineChequeNumber(objFINAPPaymentLine)=""
				set strStatus=$$Save^FINSYS("FINAPPaymentLine",pidFINAPPayment_","_idFINAPPaymentLine,objFINAPPaymentLine,1)
				if $$$ISOK(strStatus) if pblnCancelCheque set strStatus=$$CancelCheque^FINAPChequeBookCancelled(pidFINAPPayment,intChequeNumber)
			}
		}
		if $$$ISOK(strStatus) set strStatus=$$PostBlock^FININTBatch(idBatch,idBlock)
	}
 
	set $$$FINAPPaymentPostedDate(objFINAPPayment)=""	; Set Posted Date
	set $$$FINAPPaymentStatus(objFINAPPayment)=$$$EnumFINSYSSTATUSUnposted
	do TransactionComplete^FINSYS(strStatus,pidFINAPPayment,objFINAPPayment,$$$YES)
 
	;} else {
	;	set strStatus = $$UnPostOLD(pidFINAPPayment,pblnCancelCheque)
	;}
	
    ;
    ; !!!!!!!!!!!!!!!!!! NOT IN USE !!!!!!!!!!!!!!!!!!!
    ;
	quit strStatus
 
UnPost(pidFINAPPayment,pblnCancelCheque=$$$NO,pidLine="")
	;-------------------------------------------------------------------------------
	; Unpost the payment
	;
	; Returns:
	;
	; History:
	; 17-May-2006	PO/SS	SR14004: Use InHyperEvent macro instead of always assuming hyperevent
	; 28-Feb-2006	PO		SR14225: Added Drill Down Detail to Allocate
	; 12-Jan-2006	Steve S	SR14114: Call TransactionComplete correctly for lines
	; 19-Sep-2005	GRF		SR13481: Boolean macro
	; 06-Jul-2005	PO		SR12881: Changed 0 to $$$EnumFINSYSSTATUSUnposted,
	; 						$$$ISOK is now used & TransactionComplete now in FINSYS
	; 01-Oct-2004	Paul K	Added Unpostable call and status.
	; 18-Aug-2004	shobby	Call to Post Block to create the Invoice Transaction (SR10262)
	; 25-Feb-2004	shobby	Created
	;-------------------------------------------------------------------------------
	new strStatus,idBatch,idBlock,dtePaymentDate,objFINAPPayment,blnUnPost,strForm,strDetail
	
	; Validity Checks
	; 
	;--------------------------------------- 
	tstart
	;---------------------------------------
	set strStatus = $$$OK
	set blnUnPost = $$$YES
	set strStatus=$$GetPaymentObject^FINAPPaymentPost(pidFINAPPayment,.objFINAPPayment)
	
	if $$$ISOK(strStatus) set strStatus=$$UnPostable^FINSYSReview("FINAPPayment",$$$FINAPPaymentStatus(objFINAPPayment),.blnUnPost)
	
	if blnUnPost {
		if $$$ISOK(strStatus) set dtePaymentDate=$$$FINAPPaymentPaymentDate(objFINAPPayment)
		if $$$ISOK(strStatus) {
			; SR14225 - Source Details for Drill Down - Start
			set strForm   = $$CheckForm^FINUtils($get(YFORM),"FINAPPayment")  ; Get the form using YFORM, with validation
			set strDetail = "Fin00932,"_pidFINAPPayment  ;Supplier Payment Reversal %1
			set strStatus = $$Allocate^FININTBatch(.idBatch,.idBlock,"APPAY",$get(YLOCATION),dtePaymentDate,,,,strForm,pidFINAPPayment,strDetail)
		}
		;if $$$ISOK(strStatus) set strStatus=$$Allocate^FININTBatch(.idBatch,.idBlock,"APPAY",$get(YLOCATION),dtePaymentDate) ; SR14225
		if $$$ISOK(strStatus) set strStatus=$$UnPostLines(pidFINAPPayment,idBatch,idBlock,pblnCancelCheque,pidLine)
		if $$$ISOK(strStatus) set strStatus=$$PostBlock^FININTBatch(idBatch,idBlock)
	}
	
	;set $$$FINAPPaymentPostedDate(objFINAPPayment)=""	; Set Posted Date
	
	if pidLine="" { ;SR14114
		set $$$FINAPPaymentStatus(objFINAPPayment)=$$$EnumFINSYSSTATUSCancelled
		do TransactionComplete^FINSYS(strStatus,pidFINAPPayment,objFINAPPayment,$$$InHyperEvent)
	} else {
		do TransactionComplete^FINSYS(strStatus,"","",$$$InHyperEvent,$$$NO)
	}
	
	quit strStatus
	
UnPostLines(pidFINAPPayment,pidBatch,pidBlock,pblnCancelCheque=$$$NO,pidLine="")
	;-------------------------------------------------------------------------------
	; Looks through the payments and posts a line with the -ve value effectively
	; undoing the previous transaction but leaving an audit trail.
	;
	; Returns:
	;
	; History:
	; 16-Jan-2005	SC		SR14026 Keep Cheque# when cancelling cheque.
	; 14-Oct-2005	Steve S	SR13216 Update lines when unposted
	; 11-Jul-2005	shobby	SR12678 Cancel Cheque.
	; 26-Feb-2004	shobby	Created
	;-------------------------------------------------------------------------------
 	new strStatus,idLineNumber,intChequeNumber,objFINAPPaymentLine,intStatus
    
    set strStatus=$$$OK
	
	set idLineNumber=""
	;set ChequeNumber=$$$FINAPPaymentNextChequeNumber(objFINAPPayment)-1  ;24-Feb-2004
	for {
		set idLineNumber=$order(^FINAPPaymentLine(YM,pidFINAPPayment,idLineNumber))
		quit:(idLineNumber="")||($$$ISERR(strStatus))
		continue:((pidLine'="")&&(idLineNumber'=pidLine)) ;SR13216
		
		set objFINAPPaymentLine=$get(^FINAPPaymentLine(YM,pidFINAPPayment,idLineNumber,1))
		set intStatus=$$$FINAPPaymentLineStatus(objFINAPPaymentLine)
		
		
		if (objFINAPPaymentLine'="")&&(intStatus'=$$$EnumFINSYSSTATUSCancelled) {
			set intChequeNumber=$$$FINAPPaymentLineChequeNumber(objFINAPPaymentLine)
			set strStatus=$$PostLine^FINAPPaymentPost(pidFINAPPayment,idLineNumber,pidBatch,.pidBlock,.intChequeNumber,$$$YES)
			if $$$ISOK(strStatus) {
			
				;set $$$FINAPPaymentLineChequeNumber(objFINAPPaymentLine)="" ;set Cheque No. = "" ;SR14026.
				;set strStatus=$$Save^FINSYS("FINAPPaymentLine",pidFINAPPayment_","_idLineNumber,objFINAPPaymentLine,1)
				;if $$$ISOK(strStatus) {
				
				if pblnCancelCheque {
					set strStatus=$$CancelCheque^FINAPChequeBookCancelled(pidFINAPPayment,intChequeNumber)
				}
				if $$$ISOK(strStatus) {
					set strStatus=$$UpdateLine^FINAPPaymentPost(pidFINAPPayment,idLineNumber,$$$EnumFINSYSSTATUSCancelled,pidBatch,pidBlock) ;SR13216
				}
				
				;}
			}
		}
	}
	
	quit strStatus
	
	
CheckClearances(pidPayment)
    ;-------------------------------------------------------------------------------
    ; Check if any payment lines have been cleared in bank rec. If so,
    ; warn the user before unposting.
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Oct-2005	Steve S		SR13216: Created
    ;-------------------------------------------------------------------------------
	new strLines,idPayLine,strKey,strMessage,objButton
 
	set strLines=""
	
	if '$$$NoKey(pidPayment) {
		set idPayLine=""
		for {
			set idPayLine=$order(^FINAPPaymentLine(YM,pidPayment,idPayLine))
			quit:idPayLine=""
			
			set strKey=pidPayment_","_idPayLine
			
			if $$ClearanceCheck^FINAPPaymentLineUnPost(strKey,$$$NO) {
				set strLines=strLines_idPayLine_","
			}
		}
		set strLines=$extract(strLines,1,$length(strLines)-1)
	}
	
	if strLines'="" {
		set strMessage=$$$Text($listbuild("Fin00913",strLines))
		;This payment run contains lines (%1) which have been cleared in Bank Reconciliaion. Proceed with unpost?
	} else {
		set strMessage=""	
	}
	
	set objButton=$get(^WWW124(0,"FINAPPayment",SPRACHE,7,1))
	set $$$WWW124AcknowledgementTextBefore(objButton)=strMessage
	do Save^COMUtils("WWW124","FINAPPayment,"_SPRACHE_",7",objButton,1)
 	
	quit
]]></Routine>
</Export>