<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEINVCYCD" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEINVCYCD ;INWEINVCYCD;FIS;DRUCKEN INVENTUR ZÄHLLISTE CYCLE-COUNTING;09.05.2003
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		DRUCKEN INVENTUR ZÄHLLISTE CYCLE-COUNTING
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	09.05.2003
	;|
	;\------------------------------------------------------------------/
	;
	NEW LFN,LFN1,BET,ART,ART1,WG,ME,LAP,ART,AUF,POS
	SET VORG(1)=$GET(VORG(1))  ;VORGABE INVENTUR ;default stocktaking 
	IF +$GET(VORG(1))=0 DO ^WWWINFO($$^WWWTEXT(32292)) QUIT  ;KEIN Datum ;no Date 
	KILL ^WWWSOR(YUSER)  ;LÖSCHEN ZWISCHENDATEI ;Delete 
	;BEARBEITEN ÜBERSCHRIFT ;superscription 
	SET $P(UES(1),Y,1)="<BR>"_$$^WWWDATE(VORG(1))_"<BR><BR>"_$$^WWWTEXT(32053)    ;NR
	SET $P(UES(1),Y,2)="<BR>"_$$^WWWTEXT(32056)_":"_"_____________"_"<BR><BR>"_$$^WWWTEXT(32054)    ;LAP
	SET $P(UES(1),Y,3)="<BR>"_$$^WWWTEXT(32057)_":"_"_____________"_"<BR><BR>"_$$^WWWTEXT(32024)    ;ARTIKEL ;item 
	SET $P(UES(1),Y,4)="<BR>"_$$^WWWTEXT(32058)_":"_"_____________"_"<BR><BR>"_$$^WWWTEXT(32026)    ;BEZ
	SET $P(UES(1),Y,5)="<BR>"_"<BR>"_"<BR>"_"<BR>"_$$^WWWTEXT(32030)    ;ME
	SET $P(UES(1),Y,6)="<BR>"_"<BR>"_"<BR>"_"<BR>"_$$^WWWTEXT(32025)
	DO ^WWWBACK
	DO DRUCK   ;WERTE DRUCKEN ;print 
	DO STOP^WWWTAB  ;ENDE TABELLE ;termination tabulation 
	KILL ^WWWSOR(YUSER)  ;LÖSCHEN ZWISCHENDATEI ;Delete 
	QUIT
	
DRUCK ;
	;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Nov-2006	JW		BR014285: Item group change. Lang text.
	;-------------------------------------------------------------------------------
	SET SUM=0  ;SUMME ;sum 
	DO UPUEB  ;DRUCKEN ÜBERSCHRIFT ;print superscription 
	SET YDDSATZ=0
	;AUSWERTUNG DRUCKEN ;print 
	SET LFN="" FOR  SET LFN=$ORDER(^INWEINVCYC(YM,VORG(1),LFN)) QUIT:LFN=""  DO 
	. SET LFN1=$GET(^INWEINVCYC(YM,VORG(1),LFN,1))  ;BESTANDSDATEN
	. QUIT:LFN1=""
	. SET BET=$PIECE(LFN1,Y,2)  ;BETRIEB
	. SET ART=$PIECE(LFN1,Y,1)  ;ARTIKEL ;item 
	. QUIT:ART=""
	. SET ART1=""
	. IF ART'="" SET ART1=$GET(^INART(YM,ART,1))
	. SET WG=$PIECE(ART1,Y,30) ;WARENGRUPPE
	. SET ME=""   ;MENGENEINHEIT
	. IF $PIECE(ART1,Y,40)'="" SET ME=$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(ART1,Y,40),1)),Y,1)
	. SET LAP=$PIECE(LFN1,Y,3) 
	. SET WED=$PIECE(LFN1,Y,6) 
	. SET AUF=$PIECE(LFN1,Y,32) 
	. SET POS=$PIECE(LFN1,Y,33) 
	. ;
	. WRITE !
	. WRITE YCR
	. DO NL^WWWTAB
	. DO NFR^WWWTAB
	. WRITE LFN  ;ZÄHLNUMMER
	. WRITE "&nbsp;"
	. DO ZW^WWWTAB
	. WRITE BET," / ",LAP  ;BETRIEB UND LAGERPLATZ ;location And stock location 
	. IF $PIECE(LFN1,Y,9)="HISTORY" WRITE " "_$$^WWWTEXT(32859)	; History	//BR014285
	. IF $PIECE(LFN1,Y,6)'="" W "<BR>("_$$^WWWDATE($PIECE(LFN1,Y,6))_")"
	. WRITE "&nbsp;"
	. DO ZW^WWWTAB
	. WRITE ART_"<BR>"
	. //IF WG'="" WRITE "("_WG_") "_$PIECE($GET(^INPARA(YM,"WARENGRUPPE",SPRACHE,WG,1)),Y,1)
	. IF WG'="" WRITE "("_WG_") "_$$GetDescription^INDItemGroup(WG)	//BR014285
	. WRITE "&nbsp;"
	. DO ZW^WWWTAB
	. IF $PIECE(LFN1,Y,32)'="" DO
	. . WRITE "<B>"
	. . WRITE $$^WWWTEXT(32047)_": "_$PIECE(LFN1,Y,32)
	. . IF $PIECE(LFN1,Y,33)'="" WRITE "-"_$PIECE(LFN1,Y,33)
	. . WRITE "<BR>"
	. . WRITE "</b>"
	. . QUIT
	. SET YINHALT=$PIECE(ART1,Y,1)
	. WRITE $EXTRACT(YINHALT,1,40)
	. SET YINHALT=$PIECE(ART1,Y,14)
	. IF $TRANSLATE(YINHALT,"| ")=$TRANSLATE($PIECE(ART1,Y,1),"| ") SET YINHALT=""
	. IF YINHALT'="" WRITE "<BR>"
	. ;LANGER TEXT 
	. FOR YIZ(1)=1:1 QUIT:$PIECE(YINHALT,"|",YIZ(1),999)=""  DO
	. . SET YIZ(2)=$TRANSLATE($$^WWWUML($PIECE(YINHALT,"|",YIZ(1)),1),"|"," ")
	. . QUIT:$TRANSLATE(YIZ(2)," ")=""
	. . WRITE $EXTRACT(YIZ(2),1,40)
	. . IF $EXTRACT(YIZ(2),41,80)'="" WRITE "<BR>",$EXTRACT(YIZ(2),41,80)
	. . IF $EXTRACT(YIZ(2),81,120)'="" WRITE "<BR>",$EXTRACT(YIZ(2),81,120)
	. . IF $EXTRACT(YIZ(2),121,160)'="" WRITE "<BR>",$EXTRACT(YIZ(2),121,160)
	. . IF $PIECE(YINHALT,"|",YIZ(1)+1,999)'="" WRITE "<BR>",YCR
	. . QUIT
	. WRITE "&nbsp;"
	. DO ZWR^WWWTAB
	. WRITE ME  ;MENGENEINHEIT
	. WRITE "&nbsp;"
	. DO ZWR^WWWTAB
	. IF $PIECE(LFN1,Y,32)'="" WRITE $$^WWWZAHL($PIECE(LFN1,Y,4),0,2)_"<BR>"
	. FOR YI=1:1:30 WRITE "&nbsp;"
	. ;ENDE ;termination 
	. DO EF^WWWTAB
	. DO EL^WWWTAB
	. QUIT
	QUIT
	
GESAMT ;GESAMT ;total  ;total whole 
	QUIT
	
UPUEB ;ÜBERSCHRIFT ;superscription 
	WRITE "<CENTER>"
	DO START00^WWWTAB
	DO NL^WWWTAB
	DO NF^WWWTAB
	WRITE "<FONT SIZE=3>"
	WRITE "<B>"
	WRITE "<NOBR>"
	WRITE YKOPF
	WRITE "</NOBR>"
	DO EF^WWWTAB
	DO EL^WWWTAB
	DO STOP^WWWTAB
	DO START100^WWWTAB
	WRITE "</CENTER>"
	WRITE "<THEAD>" 
	DO NL^WWWTAB
	FOR I=1:1 QUIT:$PIECE(UES(1),Y,I,99)=""  DO NHW^WWWTAB WRITE $PIECE(UES(1),Y,I),"&nbsp;" DO EH^WWWTAB
	DO EL^WWWTAB
	WRITE "</THEAD>"
	QUIT
	QUIT
]]></Routine>
</Export>