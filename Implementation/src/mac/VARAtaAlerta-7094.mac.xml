<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAtaAlerta" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARAtaAlerta
#include COMSYS
#include COMConst

OnBeforeFormConstruction	
	if $get(^VARTempAtaAlerta(YM,YBED,1)) = "" {
		set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,4) = 30 //default, 30 days
	}
	
	quit
	
	
OnAfterSave
	;-------------------------------------------------------------------------------
    ; Prepara as variáveis para a consulta do usuário.
	;
    ; History:
    ; 21-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new idFornecedor, idProduto, idAta, intDaysToExpire, intDaysExpired, dteFromPeriodExpiry, 
		dteToPeriodExpiry, blnComSaldo, comissao, contaContabil, percUtilizacao
	
	//Filter
	set idFornecedor    	= $get(VORG(1))
	set idProduto       	= $get(VORG(2))
	set idAta           	= $get(VORG(3))
	set intDaysToExpire 	= $get(VORG(4))
	set intDaysExpired  	= $get(VORG(5))
	set dteFromPeriodExpiry = $get(VORG(6))
	set dteToPeriodExpiry   = $get(VORG(7))
	set blnComSaldo			= $get(VORG(8))
	set comissao			= $get(VORG(9))
	set contaContabil	    = $get(VORG(10))
	set percUtilizacao		= $get(VORG(11))

	// If the user has not set any filtering
	if ((intDaysToExpire = "") && (intDaysExpired = "") &&
		(dteFromPeriodExpiry = "") && (dteToPeriodExpiry = "")) {
		set intDaysToExpire = ""
	}

	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,1) = idFornecedor
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,2) = idProduto
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,3) = idAta
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,4) = intDaysToExpire
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,5) = intDaysExpired
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,6) = dteFromPeriodExpiry
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,7) = dteToPeriodExpiry
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,8) = blnComSaldo
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,9) = comissao
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,10) = contaContabil
	set $piece(^VARTempAtaAlerta(YM,YBED,1),Y,11) = percUtilizacao
	
	do VARLogAfterSave^VARUtil(YBED,YFORM)	
	do ReloadForm^COMUtilForm("VARAtaAlerta")
	
	quit
	

OnAfterDataFields
	;-------------------------------------------------------------------------------
    ; Monta a tabela de consulta conforme filtrado pelo usuários das atas com vencimento
    ; próximo.
	;
    ; History:
	; 07-Jun-2012	Gustavo	  Incluída opção de validade na linha	
	; 03-Mai-2012	Gustavo	  Incluído VARLogAfterSave
    ; 21-Sep-2011	Gustavo   Created
    ;-------------------------------------------------------------------------------	
	;do PrintJS
	
	new flterFornecedor, flterProduto, flterAta, flterDaysToExpire, flterDaysExpired,
		flterFromPeriodExpiry, flterToPeriodExpiry, dteStart, dteEnd, object, mySQL, count,
		flterComSaldo, fltQuantAditivo, fltQuantAditivo, fltQuantTotal, hintQuant, comissao,
		percUtilizacao, contaContabil, flterComissao, flterContaContabil, flterPercUtilizacao
		
	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)		
	
	set flterFornecedor   	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,1)
	set flterProduto      	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,2)
	set flterAta         	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,3)	
	set flterDaysToExpire 	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,4)
	set flterDaysExpired  	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,5)
	set flterFromPeriodExpiry = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,6)
	set flterToPeriodExpiry   = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,7)
	set flterComSaldo		  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,8)
	set flterComissao		  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,9)
	set flterContaContabil	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,10)
	set flterPercUtilizacao	  = $piece($get(^VARTempAtaAlerta(YM,YBED,1)),Y,11)
	
	if flterDaysExpired = "" {
		set dteStart = $piece($horolog,",",1)
	
	} else {
		set dteStart = $piece($horolog,",",1) - flterDaysExpired
	}
	
	if (flterDaysToExpire = "") {		
		if (flterDaysExpired = "") {
			set flterDaysToExpire = ""
		} else {
			set flterDaysToExpire = 0
		}
	}
	
	set dteEnd = $piece($horolog,",",1) + flterDaysToExpire
	
	if ((flterDaysToExpire = "") && (flterDaysExpired = "")){
		set dteStart = ""
		set dteEnd   = ""	
	}
	
	if ((flterFromPeriodExpiry '= "") || (flterToPeriodExpiry '= "")) {
		if (flterFromPeriodExpiry '= "") {
			set dteStart = flterFromPeriodExpiry - 1
		}
		else {
			set dteStart = ""
		}
		set dteEnd = flterToPeriodExpiry

		set flterDaysToExpire = ""
	}
	
	set object = ##class(%ResultSet).%New()  ;Create Result Set Object

	set mySQL = "SELECT Ata.CodATA,"
	set mySQL = mySQL_" Ata.NumeroProcesso,"
	set mySQL = mySQL_" Ata.Status,"
	set mySQL = mySQL_" $$GetValidadeAta^VARAta(Ata.CodAta,Linha.LinhadaAta) as DataValidade,"
	set mySQL = mySQL_" Linha.LinhadaAta,"
	set mySQL = mySQL_" Linha.Produto,"
	set mySQL = mySQL_" Linha.Fornecedor,"
	set mySQL = mySQL_" Linha.Quantidade,"
	set mySQL = mySQL_" Linha.QuantidadeAditivada,"	
	set mySQL = mySQL_" Linha.Unidade,"
	set mySQL = mySQL_" Linha.ValorUnitario,"
	set mySQL = mySQL_" $$SQLGetSaldoItemQuant^VARAtaLinha(Ata.CodAta,Linha.LinhadaAta) as Saldo,"
	set mySQL = mySQL_" Ata.Comissao,"
	set mySQL = mySQL_" Item.FREE3"
	set mySQL = mySQL_" FROM VARAta Ata"
	set mySQL = mySQL_" LEFT JOIN VARAtaLinha Linha ON Linha.CodAta = Ata.CodAta"
	set mySQL = mySQL_" JOIN INART Item ON (Item.ItemNumber = Linha.Produto)"
	set mySQL = mySQL_" WHERE UPPER(Ata.Status) = 1"
	set mySQL = mySQL_"  AND $$GetSituacaoItem^VARAtaLinha(Ata.CodAta,Linha.LinhadaAta,2) >= 3"		
	
	if (flterComSaldo = $$$YES){
		set mySQL = mySQL_"  AND $$SQLGetSaldoItemQuant^VARAtaLinha(Ata.CodAta,Linha.LinhadaAta) > 0"
	}
	
	if flterFornecedor '= "" {
		set mySQL = mySQL_" AND UPPER(Linha.Fornecedor) = "_flterFornecedor
	}
		
	if flterProduto '= "" {
		set mySQL = mySQL_" AND UPPER(Linha.Produto) = "_flterProduto
	}
		
	if flterAta '= "" {
		set mySQL = mySQL_" AND Ata.CodATA = '"_flterAta_"'"
	}
	
	if flterComissao '= "" {
		set mySQL = mySQL_" AND Ata.Comissao = '"_flterComissao_"'"	
	}
	
	if flterContaContabil '= "" {
		set mySQL = mySQL_" AND Item.FREE3 = '"_flterContaContabil_"'"	
	}
	
	if flterPercUtilizacao '= "" {
		set mySQL = mySQL_" AND $$GetPercUtilizacao^VARAtaAlerta($$SQLGetSaldoItemQuant^VARAtaLinha(Ata.CodAta,Linha.LinhadaAta),$$GetQuantTotal^VARAtaAlerta(Linha.Quantidade,Linha.QuantidadeAditivada)) < "_flterPercUtilizacao
	}

	if (dteStart '= "") {
		set mySQL = mySQL_" AND $$GetValidadeAta^VARAta(Ata.CodAta,Linha.LinhadaAta) >= "_dteStart
	}
	
	if (dteEnd '= "") {
		set mySQL = mySQL_" AND  $$GetValidadeAta^VARAta(Ata.CodAta,Linha.LinhadaAta) < "_dteEnd
	}
	
	set mySQL = mySQL_" ORDER BY $$GetValidadeAta^VARAta(Ata.CodAta,Linha.LinhadaAta)"
    
	do object.Prepare(mySQL) ;Prepare Query
	do object.Execute()  	 ;Execute Query
	
	//Tabela
   	new lstHeader, idAta, NumeroAta, Status, dteValidade, idLinhaAta, idProduto, idFornecedor,
   		fltQuantidade, idUnidade
   	
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("","Nº. da Ata","N°. Processo","Fornecedor","Produto","Quantidade","Saldo","UN","Preço","Validade","Prazo Restante")

	set count = 0
	  	
	if $$Start^COMTable(lstHeader,"Atas:") {

		while (object.Next()) {
				
			set idAta	        = object.GetData(1)
			set NumeroAta       = object.GetData(2)
			set Status          = object.GetData(3)
			set dteValidade     = object.GetData(4)
			set idLinhaAta      = object.GetData(5)
			set idProduto	    = object.GetData(6)
			set idFornecedor    = object.GetData(7)
			set fltQuantidade   = object.GetData(8)
			set fltQuantAditivo = object.GetData(9)
			set idUnidade       = object.GetData(10)
			set vlueUnitario    = object.GetData(11)
			set fltSaldoItem    = object.GetData(12)
			set comissao		= object.GetData(13)
			set contaContabil   = object.GetData(14)
			
			set count = $increment(count)
		
			set intNoDaysAVencer = 0
			set intNoDaysAVencer = dteValidade - $piece($horolog,",",1)
			
			set fltQuantTotal = fltQuantidade + fltQuantAditivo				
			
			if (fltQuantAditivo > 0) {
				set hintQuant = "Quant.: "_$$^WWWTR(0,12,fltQuantidade)_" + Quant. Aditivo: "_$$^WWWTR(0,12,fltQuantAditivo)_""			
			} else {
				set hintQuant = "Quant.: "_$$^WWWTR(0,12,fltQuantidade)	
			}					
			
			set percUtilizacao = $$GetPercUtilizacao(fltSaldoItem,fltQuantTotal)
			
			set strImg = $$GetImg(intNoDaysAVencer)
			
			if intNoDaysAVencer >= 0 {
				do NewLine^COMTable()
			} else {
				do NewLine^COMTable("navajowhite")
			}				

			do InsertCell^COMTable(strImg)
	    	do InsertCell^COMTable(idAta,"VARAta",idAta)
	    	do InsertCell^COMTable(NumeroAta,"VARAta",idAta)
	    	do InsertCell^COMTable("("_idFornecedor_") "_$$SQLGetDescFornecedor^VARTRKSQL(idFornecedor),,,,,,,,,$$$YES)	    	
	    	do InsertCell^COMTable("("_idProduto_") "_$$SQLGetDescricaoProduto^VARTRKSQL(idProduto),,,,,,,,,$$$YES)
	    	do InsertCell^COMTable("<font title='"_hintQuant_"'>"_$$^WWWTR(0,12,fltQuantTotal)_"</font>",,,,"RIGHT")
	    	do InsertCell^COMTable("<font color=blue>"_$$^WWWTR(0,12,fltSaldoItem)_"</font>",,,,"RIGHT")		    	
			do InsertCell^COMTable($$SQLGetSiglaUnit^VARSQL(idUnidade))
	    	;do InsertCell^COMTable($$^WWWTR(0,12,percUtilizacao,2)_"%",,,,"right")
	    	do InsertCurrencyCell^COMTable(vlueUnitario)
	    	do InsertCell^COMTable($zdate(dteValidade,4))

    		if intNoDaysAVencer >= 0 {
    			do InsertCell^COMTable($$^WWWTR(0,12,intNoDaysAVencer)_" dia(s)",,,,"RIGHT")	    		
    		} else {
	    		do InsertCell^COMTable("<font color=red><strong>Vencido há "_$$^WWWTR(0,12,intNoDaysAVencer*-1)_" dia(s)</strong></font>",,,,"RIGHT")		    	
    		}

    		do EndLine^COMTable()
		}
		
		if (count = 0) {
			do NewLine^COMTable()
	    	do InsertCell^COMTable("")
		   	do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,9)	    	
    		do EndLine^COMTable()
		}
	
		do Stop^COMTable()
		do object.Close()
	}
  	
	quit

OnFromExpiryBlur(YFELD)
	if (YINHALT '= "") {
		set %TXT(1) = "#Y"_YFORM_"M4~#Y"_YFORM_"M5~"
	}
	quit
	
OnToExpiryBlur()
	do OnFromExpiryBlur("")
	quit	
	
GetImg(pintNoDaysAVencer)
	new alertHint, img, strImg

	set alertHint = "Atenção: resta(m) "_intNoDaysAVencer_" dia(s) para esta ata vencer."

	if (pintNoDaysAVencer < 0) {
		set strImg = ""
	
	} elseif ((pintNoDaysAVencer >= 0) && (pintNoDaysAVencer <= 30)) {
		set img = "alertRed.gif"
		set strImg = "<IMG SRC="_YGIF_img_" TITLE='"_alertHint_"' border=0 align=""center"" >"
	
	} elseif ((pintNoDaysAVencer > 30) && (pintNoDaysAVencer <= 60)) {
		set img = "alertYellow.gif"
		set strImg = "<IMG SRC="_YGIF_img_" TITLE='"_alertHint_"' border=0 align=""center"" >"
	
	} elseif ((pintNoDaysAVencer > 60) && (pintNoDaysAVencer <= 90)) {
		set img = "alertBlue.gif"
		set strImg = "<IMG SRC="_YGIF_img_" TITLE='"_alertHint_"' border=0 align=""center"" >"
	
	} else {
		set strImg = ""
	}
	
	quit strImg	
	
Imprimir()
	do RunReportAtaAlerta^VARJasperRunReport()
	quit
	
	
GetPercUtilizacao(pfltSaldo,pfltQuantTotal)
	new saldo, quantTotal, quantRestante, percUtiliz
	
	set saldo       = +pfltSaldo
	set quantTotal  = +pfltQuantTotal
	quit:(quantTotal = 0) 0	
	
	set quantRestante = quantTotal - saldo	
	quit:(saldo = 0) 100
	quit:(saldo = quantTotal) 0
	
	set percUtiliz = (quantRestante * 100) / quantTotal
	quit percUtiliz
	
GetQuantTotal(pfltQuant,pfltQuantAditivada="")
	new fltQuantTotal
	set fltQuantTotal = pfltQuant + pfltQuantAditivada	
	quit fltQuantTotal
]]></Routine>
</Export>