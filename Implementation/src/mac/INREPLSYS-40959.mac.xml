<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INREPLSYS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INREPLSYS(NAME) ;INREPLSYS;FIS;GETTING VALUE FOR CALCULATION;19.05.2003
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		GETTING VALUE FOR CALCULATION
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	19.05.2003
	;|
	;\------------------------------------------------------------------/
	;
	NEW RETVAL
	KILL ^WWWSOR(YUSER,"PROCESSED")
	SET RETVAL=$$CALC($G(NAME))
	KILL ^WWWSOR(YUSER,"PROCESSED")
	QUIT RETVAL
	
CALC(NAME)
	NEW VALUE,FUNCT,PARA,SAVFUNCT
	SET VALUE=""
	SET NAME=$G(NAME)
	QUIT:NAME="" VALUE
	SET SAVFUNCT="SET VALUE="
	QUIT:$D(^WWWSOR(YUSER,"PROCESSED",NAME)) VALUE  ;NO LOOP !
	SET ^WWWSOR(YUSER,"PROCESSED",NAME)=""
	IF $D(^INREPLSYS(YM,NAME)) DO
	. SET CALC=$GET(^INREPLSYS(YM,NAME,1))  ;CALCULATION OF VALUE
	. ;
	. IF $PIECE(CALC,Y,1)'="" SET VALUE=$PIECE(CALC,Y,1) SET SAVFUNCT=SAVFUNCT_""""_$PIECE(CALC,Y,1)_"""" QUIT  ;FIX VALUE ;skillful VALUE 
	. IF $PIECE(CALC,Y,2)'="" IF $DATA(@$PIECE(CALC,Y,2)) SET VALUE=@$PIECE(CALC,Y,2) SET SAVFUNCT=SAVFUNCT_$PIECE(CALC,Y,2) QUIT  ;EXISTING VARIABLE
	. ;
	. IF $PIECE(CALC,Y,3)'="" DO  QUIT  ;FUNCTION CALL
	. . ;IF '$DATA(^ROUTINE($P(CALC,Y,3))) QUIT  ;FUNCTION DOES NOT EXISTS
	. . IF '$FIND($PIECE(CALC,Y,3),"^") IF '$$EXIST^%R($PIECE(CALC,Y,3)_".OBJ",$GET(YUCI)) QUIT      ;Bec;05.12.03;24774;CHECK IF COMPLED ROUTINE EXIST.
	. . IF $FIND($PIECE(CALC,Y,3),"^") IF '$$EXIST^%R($PIECE($PIECE(CALC,Y,3),"^",2)_".OBJ",$GET(YUCI)) QUIT      ;Bec;05.12.03;24774;CHECK IF COMPLED ROUTINE EXIST.
	. . ;
	. . IF '$FIND($PIECE(CALC,Y,3),"^") SET FUNCT="SET VALUE=$$^"_$PIECE(CALC,Y,3)
	. . IF $FIND($PIECE(CALC,Y,3),"^") SET FUNCT="SET VALUE=$$"_$PIECE(CALC,Y,3)
	. . SET FUNCT=FUNCT_"("
	. . IF $DATA(^INREPLSYSPARA(YM,NAME)) DO
	. . . SET PARA="" FOR  SET PARA=$ORDER(^INREPLSYSPARA(YM,NAME,PARA)) QUIT:PARA=""  DO
	. . . . SET PARA1=$GET(^INREPLSYSPARA(YM,NAME,PARA,1))
	. . . . DO
	. . . . . IF $PIECE(PARA1,Y,2)=1 QUIT  ;NO VALUE
	. . . . . IF $PIECE(PARA1,Y,3)'="" SET FUNCT=FUNCT_""""_$PIECE(PARA1,Y,3)_"""" QUIT  ;FIX VALUE ;skillful VALUE 
	. . . . . SET FUNCT=FUNCT_""""_$$CALC($PIECE(PARA1,Y,1))_""""
	. . . . . QUIT
	. . . . IF $ORDER(^INREPLSYSPARA(YM,NAME,PARA))'="" SET FUNCT=FUNCT_","
	. . . . QUIT
	. . . QUIT
	. . SET FUNCT=FUNCT_")"
	. . SET SAVFUNCT=FUNCT
	. . XECUTE FUNCT
	. . QUIT
	. ;
	. IF $P(CALC,Y,4)'="" SET VALUE=$$CALC($P(CALC,Y,4)) QUIT  ;OTHER CALCULATION
	. ;
	. IF $P(CALC,Y,5)'="" DO  QUIT  ;CACHE OBJECT SCRIPT
	. . SET FUNCT="SET VALUE="_$P(CALC,Y,5)
	. . SET SAVFUNCT=FUNCT
	. . XECUTE FUNCT
	. . QUIT
	. QUIT
	IF $G(TYPE)'="" IF $G(YRLOCATION)'="" IF $G(YRITEM)'="" DO  ;CALL OUT OF ^INREPLRULESUB WITH TYPE
	. ;SET TYPE=$G(TYPE) ;table-mat 
	. ;IF TYPE="" SET TYPE=" "
	. SET ^INREPLDATA(YM,TYPE,YRLOCATION,YRITEM,NAME,1)=VALUE_Y_SAVFUNCT_Y_+$H  ;SAVE
	. IF $P(CALC,Y,11)=1 SET ^INREPLPARAM(YM,NAME,1)=VALUE  ;VALID FOR ALL RULES
	. IF $G(YBGJOB)=0 WRITE "("_$$^WWWTEXT(388)_": "_YRLOCATION_" "_$$^WWWTEXT(32024)_": "_YRITEM_") "_NAME_": "_VALUE,"<BR>",!  ;LOCATION,ITEM,PARAMETER,VALUE
	. QUIT
	QUIT VALUE
]]></Routine>
</Export>