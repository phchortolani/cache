<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilMail" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilMail
    ;-------------------------------------------------------------------------------
    ; Common Utilities for Mail
    ; 
	; 
	; History:
	; 11-Mar-2009	PPP		Created
    ;-------------------------------------------------------------------------------
#include COMConst
#include WWWConst
#include COMSYS
	
SendEmail(pstrEmailList="", pstrDisplayFrom="", pstrSubject="", parrMessage="") 
	;-------------------------------------------------------------------------------
	; Send an email to Dev that a Build was completed 
	;
	; Params:
	;		pidResponsible	: Who this message should be sent to
	;		parrMessage: 
	;		
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 24-May-2011	PPP		SR17767: Email to Multiple Addresses
	; 28-Jan-2011	PPP		SRAdhoc:1. Added strStatus as Return
	;							2. Do not add <BR> for attachment lines
	; 13-Jul-2009	PPP		If explicitely required on N/S other that Development, add
	;							^SysSetup("AllowEmail")=1 
	; 11-Mar-2009	PPP		Created
	;-------------------------------------------------------------------------------
	new idMessage,intCnt,intLine,objWWW013,strStatus
	new idx
 	new strEmailAddr
 	
	//quit:'$get(^Development)		// Only in dev. Not in core application
	quit:'$get(^Development)&&('$get(^SysSetup("AllowEmail")))		// Only in dev. Not in core application and if explicitely required
 	
	set strStatus = $$$OK
	set idMessage = $$Allocate^WWWMAILS()
 
	set intLine = 0
	set intCnt  = 0
	for {
		set intCnt = $increment(intCnt)
		quit:'$data(parrMessage(intCnt))
		
		//SRAdhoc
		if $get(parrMessage(intCnt))["#attach" {
			set ^WWWSOR(idMessage,$increment(intLine)) = $get(parrMessage(intCnt))
		} else {
			set ^WWWSOR(idMessage,$increment(intLine)) = $get(parrMessage(intCnt))_"<BR>"		
		}
	}
 
	for idx=1:1:$length(pstrEmailList,";") {

		set strEmailAddr = $piece(pstrEmailList,";",idx)

		if (strEmailAddr'="") && (strEmailAddr["@") {
			set strStatus = $$PackageMail^WWWMAILS(idMessage,pstrSubject,strEmailAddr,"","",pstrDisplayFrom)
		}
	}
	
	do Deallocate^WWWMAILS(idMessage)
	
	//SRAdhoc
	quit strStatus
	
]]></Routine>
</Export>