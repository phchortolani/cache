<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW122D" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include WWWConst
#include COMSYS
	
	;-------------------------------------------------------------------------------
	; 22-Oct-2009	shobby	SR16969 Class/Form changed (Do Not remove comment)
	;-------------------------------------------------------------------------------
	
WWW122D(YKEY)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LÖSCHEN DER CUSTOMIZINGDATEN
	;	D ^WWW122D("FORMNAME,4")
	;
	; Inputs : YKEY   : Form,FormField
	;
	; Returns : Nothing
	;
	; History :
	; 07-Jul-2011	GRF		SR16925: get keys once
	; 20.06.2003	TYBD
	; 27.09.2002	DT
	;-------------------------------------------------------------------------------
	new idForm,idFormField
	
	quit:$get(YKEY)=""
	set idForm      = $$$KEY1(YKEY)
	set idFormField = $$$KEY2(YKEY)
	quit:idForm=""
	quit:idFormField=""
	
	if $data(^WWW122D(0,idForm,idFormField))    kill ^WWW122D(0,idForm,idFormField)
	if $data(^WWW127(0,idForm,"D",idFormField)) kill ^WWW127(0,idForm,"D",idFormField)  ;LÖSCHEN HILFETEXT
	if $data(^WWW127(0,idForm,"M",idFormField)) kill ^WWW127(0,idForm,"M",idFormField)  ;LÖSCHEN HILFETEXT
	quit
	
	
ListUsedFreeFields(pidForm)
	;-------------------------------------------------------------------------------
	; Create a COMTempList of customised free fields for a given form.
	;
	; Params:	pidForm - the id of the form with the free fields
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 04-Nov-2010	GRF		SRBR0414837: Replace Order macro
	; 01-Nov-2006	shobby	SRBR014196: Prepend the word Form_ as part of the key
	;							on COMTempList
	; 17-Jul-2006	JW		SR14832: Created (modified SetResultFields^INForeLine)
	;-------------------------------------------------------------------------------
	new idField,objField,strDesc,idClassField
	
	quit:$get(pidForm)=""
	
	kill ^COMTempList(0,YUSER,pidForm)
	
	set idField = ""
	for {               	// Loop through all customised fields
		set idField = $order(^WWW122D(0,pidForm,idField))
		quit:idField=""
		
		set objField = $get(^WWW122(0,pidForm,idField,1))
		if $extract($$$WWW122ManualCaption(objField),1,5) = "_FREE" {		// Which are free fields
			set strDesc = $$$WWW122DFieldDescription($get(^WWW122D(0,pidForm,idField,0,1)))
			if strDesc'="" {
				set idClassField = +$$$WWW122SequenceNumber(objField)		// Which have a description
				set ^COMTempList(0,YUSER,"Form_"_pidForm,idClassField,1) = strDesc
			}
		}
	}
	quit
	
	
CustomHelpText(pobjWWW122D)
	;-------------------------------------------------------------------------------
	; If Help Text is a WWW009 reference, return that Text string rather than
	; the Text ID.
	; 
	; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText	
	;-------------------------------------------------------------------------------
	quit $$PreProcessText^WWWFORMCOMMON(pobjWWW122D,$$$FldWWW122DCustomHelpText)	 ;SR16925.2
	
	
CustomTrainingText(pobjWWW122D)
	;-------------------------------------------------------------------------------
	; If Training Text is a WWW009 reference, return that Text string rather than
	; the Text ID.
	; 
	; 25-May-2011	shobby	SR16925.2: IndividualTrainingText->CustomTrainingText	
	;-------------------------------------------------------------------------------
	quit $$PreProcessText^WWWFORMCOMMON(pobjWWW122D,$$$FldWWW122DCustomTrainingText) ;SR16925.2
	
	
RelationWithSearchFunction(YKEY)
	;-------------------------------------------------------------------------------
	; This routine determines what the 'Inherited' value of the 'Relation With Search
	;   Function' checkbox will be when viewed from the Form Field customisation screen.
	;
	; Called By: Form WWW122D F127 - Inherited From
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Nov-2010	shobby	SR17612:	$$$INHERIT is now ""
	; 12-Oct-2010	shobby	SRBR014837: Created
	;-------------------------------------------------------------------------------
	new idField,idForm,intValue,objWWW122
	
	set idForm   = $$$KEY1(YKEY)
	set idField  = $$$KEY2(YKEY)
	set intValue = 0
	
	if (idForm'="") && (idField'="") {
		set objWWW122 = $get(^WWW122(0,idForm,idField,1))
		if objWWW122'="" {
			set intValue = $$$WWW122RelationWithSearchFuncti(objWWW122)								;SR17612
			if intValue="" set intValue = $$RelationWithSearchFunction^WWW122(idForm_","_idField)	;SR17612
		}
	}
	quit intValue

FlatControls(pYKEY)
	;-------------------------------------------------------------------------------
	; This routine determines what the 'Inherited' value of the 'Flat Controls' checkbox
	; will be when viewed from the Form Field screen.
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Oct-2013	shobby	CORE-281: New'd objWWW122
	; 12-Oct-2010	shobby	CORE-281: Created
	;-------------------------------------------------------------------------------
	new idForm,idField,objWWW122,intValue
	
	
	set intValue = 0

	set idForm=$$$KEY1(pYKEY)
	set idField=$$$KEY2(pYKEY)
	if (idForm'="") && (idField'="") {
		set objWWW122 = $get(^WWW122(0,idForm,idField,1))
		if objWWW122'="" {
			set intValue = $$$WWW122FlatControls(objWWW122)								;SR17612
			if intValue="" set intValue = $$FlatControls^WWW122(idForm_","_idField)	;SR17612
		}
	}
	quit intValue
	
]]></Routine>
</Export>