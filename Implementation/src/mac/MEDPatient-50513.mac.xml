<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPatient" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPatient
	
#include MEDConst
	
GetPatientDetails(pidPatient)
	;-------------------------------------------------------------------------------
	; Get Patient Details (Name,DOB,ROOM,etc.) as a String
	;
	; Inputs: Patient
	;
	; Returns:
	;
	; History:
	; 15-Jun-2009	SCR		SR16546: Created
	;-------------------------------------------------------------------------------
	new objPatient,strText
	
	set strText = ""
	if pidPatient'="" {
		set objPatient = $get(^MEDPatient(0,pidPatient,1))
		if objPatient'="" {
			set strText = strText_$$$MEDPatientSurname(objPatient)_", "_$$$MEDPatientOtherNames(objPatient)
		}
	}
	quit strText


Admission(YM,pidPatient,pintField)	
	;-------------------------------------------------------------------------------
	; Get Patient Admission Details for calculated fields
	;
	; Inputs: Patient
	;
	; Returns:
	;
	; History:
	; 21-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
#dim idAdmission,objAdmission,strData
	new idAdmission,objAdmission,strData 
	set strData=""
	if $get(pidPatient)'="" {
		set idAdmission=$$GetAdmission^MEDAdmission(pidPatient)
		if idAdmission'="" {
			set objAdmission=$get(^MEDAdmission(YM,idAdmission,1))
			set strData=$piece(objAdmission,Y,+$get(pintField))
		}
	}
	quit strData 
	
IsAdmitted(pidPatient="")
	;-------------------------------------------------------------------------------
	; Check if the patient is currently admitted.
	;
	; Inputs: Patient
	;
	; Returns:
	;
	; History:
	; 18-Oct-2012	shobby		SR18130.2 Created
	;-------------------------------------------------------------------------------
	new objPatient,blnAdmitted

	set blnAdmitted=$$$NO
	
	if pidPatient'="" {
		set objPatient=$get(^MEDPatient(YM,pidPatient,1))
		set blnAdmitted=$$$MEDPatientCurrentlyAdmitted(objPatient)
	}
	quit blnAdmitted	


CheckAdmitted(pidDispense="",pidPrescription="",pidPatient="")
	;-------------------------------------------------------------------------------
	; Check if the patient is currently admitted.
	;
	; Inputs: Patient
	;
	; Returns:
	;
	; History:
	; 26-Oct-2012	shobby		SR18130.2 Created
	;-------------------------------------------------------------------------------
	new idPatient,strStatus
	
	set strStatus=$$$OK
	
	if pidDispense'="" {
		set pidPrescription=$$$MEDDispensePrescription($get(^MEDDispense(YM,pidDispense,1)))
	}
	if pidPrescription'="" {
		set pidPatient=$$$MEDPrescriptionPatientID($get(^MEDPrescription(YM,pidPrescription,1)))
	}
	if pidPatient'="" {
		if '$$IsAdmitted(pidPatient) set strStatus=$$$MakeStatus("MED01310") ;"Patient does not have an active admission"
	}
	
	quit strStatus


GetDOB(pstrDOB="",pdteAt="")
	;-------------------------------------------------------------------------------
	; Get Date Of Birth string.
	;
	; Inputs: Patient
	;
	; Returns:
	;
	; History:
	; 29-Nov-2013	shobby	CORE-290: Created
	;-------------------------------------------------------------------------------
	new strAge,intAge
	
	set strAge=""
	if pstrDOB {
		if pdteAt="" set pdteAt=$horolog
		set intAge=pdteAt-pstrDOB/365.25\1
		if (intAge = 1) {
			set intAge = intAge_" "_$$DecodeError^COMUtilError("MED01302") ; " ano"
		}else{
			set intAge = intAge_" "_$$DecodeError^COMUtilError("MED01301") ;" anos"
		}

		set strAge=$$^WWWTR(0,1,pstrDOB)_" ("_intAge_")"  ;Years Old
	}
	quit strAge
	

]]></Routine>
</Export>