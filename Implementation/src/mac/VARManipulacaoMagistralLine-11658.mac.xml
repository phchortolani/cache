<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARManipulacaoMagistralLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARManipulacaoMagistralLine
	#include VARConst
	#include INConst
	#include MEDConst

OnBeforeDisplayCombo(pstrKeys,pobj,pintCol,&pSUCH)
	;-------------------------------------------------------------------------------
	; Populates the Unit with the UOM as defined in INARTPACK only, excludes
	; units that the item does not come in.
	; 
	; set pSUCH as the items to be used when you override the list (set via blnOverride)
	;
	; Called By: COMGridEdit executable
	; 
	; Inputs:
	;	pstrKeys		YKEY
	;	pobjSummary		YFELD
	; 
	; Returns: $$$YES or $$$NO (override the data) 
	;
	; History:
	; 21-Oct-2013	shobby	HEVA-1148.20	Created.
	;-------------------------------------------------------------------------------
	new blnOverride
	
	set blnOverride = $$$NO
	
	if pintCol = 2 {      ; Unit
		set blnOverride = $$LoadCombo^INARTPACK($$$VARManipulacaoMagistralLineItem(pobj),blnOverride,.pSUCH)
	}
	quit blnOverride
	
QuantityAvailable(YFELD="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 11-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new YFELDCONTAINER,blnResult
	
	set blnResult=$$$YES
	set YFELDCONTAINER=$$GetYFELDContainer^COMGridEdit31Interface()
	if $$$VARManipulacaoMagistralStatus(YFELDCONTAINER)'=3 {
		set blnResult=$$QuantityAvailableEx(YFELD,YFELDCONTAINER)
	}
	quit blnResult

QuantityAvailableEx(YFELD="",YFELDCONTAINER="",pidLine="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 21-Oct-2013	shobby		HEVA-1148.19: Don't quantity check an empty cell.
	; 21-Oct-2013	shobby		HEVA-1148.16: Also check for 0 quantities and prepare the message here.
	; 19-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new objBundleStock,idBundleStock,fltQtyAvailable,strStatus
	
	set strStatus=$$$OK
	
	if (($$$VARManipulacaoMagistralLineQuantity(YFELD)'>0)&&($$$VARManipulacaoMagistralLineQuantity(YFELD)'="")) { ;HEVA-1148.19
		set strStatus=$$$MakeStatus("Quantidade Inválido.")
	}
	
	if $$$ISOK(strStatus) {
		set idBundleStock=$$$VARManipulacaoMagistralLineItem(YFELD)_"||"_$$$VARManipulacaoMagistralProductionLocation(YFELDCONTAINER)_"||"_$$$VARManipulacaoMagistralLineStockLocation(YFELD)_"||"_$$$VARManipulacaoMagistralLineBundle(YFELD)

		set fltQtyAvailable=0

		if idBundleStock'="" {
			set objBundleStock = ##class(alSOH.dBundleStock).%OpenId(idBundleStock)
			if objBundleStock'="" {
				set fltQtyAvailable = objBundleStock.QtyOnHand-##class(alSOH.iSOHBalance).GetQtyPlanned(objBundleStock.Item.%Id(),objBundleStock.Storage.Location.%Id(),objBundleStock.Storage.%Id(),objBundleStock.Bundle.%Id())	
			}
		}
		if fltQtyAvailable<$$$VARManipulacaoMagistralLineQuantity(YFELD) {
			set strStatus=$$$MakeStatus("Não há saldo suficiente em estoque para o produto selecionado na linha "_pidLine)
		}
	}

	quit strStatus
	
CheckQuantities(pidMagistral="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 21-Oct-2013	shobby		HEVA-1148.16: Move the message creation into CheckQuantitiesEx
	; 19-Oct-2013	shobby		HEVA-1148: Created
	;-------------------------------------------------------------------------------
	new strStatus,idLine,objMagistralLine,objMagistral
	
	set strStatus=$$$OK
	
	if pidMagistral'="" {
		set objMagistral=$get(^VARManipulacaoMagistral(YM,pidMagistral,1))
		set idLine="" for { set idLine=$order(^VARManipulacaoMagistralLine(YM,pidMagistral,idLine)) quit:idLine=""
			set objMagistralLine=$get(^VARManipulacaoMagistralLine(YM,pidMagistral,idLine,1))
			set strStatus=$$QuantityAvailableEx(objMagistralLine,objMagistral,idLine)
			quit:$$$ISERR(strStatus)
		}
	}
	quit strStatus

OnBlur(YBBN,YKEY,&YFELD, pidGridRow)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 16-Sep-2014	SCR			HEVA-1607: Update Table
	; 28-Aug-2014	SCR			HEVA-1591: Item Check
	; 18-Oct-2013	shobby		HEVA-1148.7: Created
	;-------------------------------------------------------------------------------
	new strStatus,idItem
	set strStatus	= $$$OK
	set idItem		= $$$VARManipulacaoMagistralLineItem(YFELD)

	if $$$VARManipulacaoMagistralLineUnit(YFELD)="" {
		if idItem'="" {
			set $$$VARManipulacaoMagistralLineUnit(YFELD)=$$$INARTUnitofMeasure($get(^INART(YM,idItem,1)))
		}
	}
	if idItem'="" {
		set strStatus	= $$GridItemOK(idItem)
		if $$$ISERR(strStatus) {
			$$$Alert(strStatus)
			set $$$VARManipulacaoMagistralLineItem(YFELD)	= ""
			set $$$VARManipulacaoMagistralLineUnit(YFELD)	= ""
		}
	}
	write "DrawDynTable_GridFieldFocus("_pidGridRow_");"
	quit

GridItemOK(pidItem)
	;-------------------------------------------------------------------------------
	; Check that the item is ok from the grid
	;
	; Inputs: 
	;
	; Returns: Status
	;
	; History:
	; 28-Aug-2014	SCR		HEVA-1591: Created
	;-------------------------------------------------------------------------------
	new YFELDCONTAINER,idDisp,idDispLine,idRx,idRxItem,idRxLine,objDisp,objRxLine
	new strStatus

	set strStatus	= $$$OK
	set YFELDCONTAINER=$$GetYFELDContainer^COMGridEdit31Interface()
	
	set idDisp		= $$$VARManipulacaoMagistralDispense(YFELDCONTAINER)
	set idDispLine	= $$$VARManipulacaoMagistralDispenseLine(YFELDCONTAINER)
	
	if (idDisp="") || (idDispLine="") {
		set strStatus =  $$$MakeStatus("VAR8898")  ;Problem
		quit strStatus								; EARLY EXIT
	}
	set idRxLine	= idDispLine
	set objDisp		= $get(^MEDDispense(YM,idDisp,1))
	set idRx		= $$$MEDDispensePrescription(objDisp)
	set objRxLine	= $get(^MEDPrescriptionLine(YM,idRx,idRxLine,1))
	set idRxItem	= $$$MEDPrescriptionLineItem(objRxLine)
	
	if $$$ISOK(strStatus) {
		set strStatus	= $$ItemOK(pidItem,idRxItem)
	}

	quit strStatus

ItemOK(pidItem,pidRxItem)
	;-------------------------------------------------------------------------------
	; Check that the item is ok
	;
	; Inputs: 
	;
	; Returns: Status
	;
	; History:
	; 28-Aug-2014	SCR		HEVA-1591: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus	= $$$OK
	if pidItem=pidRxItem {
		quit strStatus								; EARLY EXIT
	}
	if '$data(^INARTA(YM,pidRxItem,pidItem)) {
		set strStatus	= "VAR8899"  ;The selected item does not belong to the prescribed item manufacture list.
	}
	quit strStatus
]]></Routine>
</Export>