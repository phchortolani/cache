<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARVolumeEstoqueProduto" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include VARConst
#include COMSYS
#include INConst

VARVolumeEstoqueProduto
	quit
	
OnAfterSave
	set local 				= $get(VORG(1))
	set produto 			= $get(VORG(2))
	set unidade 			= $get(VORG(3))
	set enderecoArmazenagem = $get(VORG(4))
	set programa 			= $get(VORG(5))
	set mes 				= $get(VORG(6))
	set ano 				= $get(VORG(7))
	set formato 			= $get(VORG(8))
	
	kill ^VARTempVolumeEstoqueProduto(YM,YBED)
	
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,1) = local
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,2) = produto
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,3) = unidade
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,4) = enderecoArmazenagem
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,5) = programa
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,6) = mes
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,7) = ano
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,8) = formato
	
	if (unidade'=""){
		set unidade = $get(^COMTempList(YM,YBED,"VARVolumeEstoqueUnidade",""_unidade_"",1))
	}
	
	if ((produto="")||(mes="")||(ano="")||(formato="")){
		$$$Alert("É necessário preencher todos os filtros obrigatórios.")
		do GoToForm^COMUtilForm("VARVolumeEstoqueProduto","",,,,,)
		quit
	} elseif ('$$ExisteVolumeCadastrado^VARVolumeEstoque(produto,unidade)){
		if (unidade=""){
			$$$Alert("Não há volume cadastrado para a unidade de estoque deste produto.")
			do GoToForm^COMUtilForm("VARVolumeEstoqueProduto","",,,,,)
			quit
		} elseif (unidade'=""){
			$$$Alert("Não há volume cadastrado para a unidade selecionada deste produto.")
			do GoToForm^COMUtilForm("VARVolumeEstoqueProduto","",,,,,)
			quit
		}
	}
	
	do MontaTabela(local,produto,enderecoArmazenagem,programa,mes,ano)
	
	do RunReportVolumeEstoqueProduto^VARJasperRunReport(local,produto,unidade,enderecoArmazenagem,programa,mes,ano,formato)
	
	quit
	
OnBeforeButtonLine
	do LoadListaUnidadesProduto
	quit
	
OnBlurProduto
	set Local	 = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",1)
	set Produto  = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",2)
	set Endereco = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",4)
	set Programa = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",5)
	set Mes 	 = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",6)
	set Ano 	 = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",7)
	set Formato  = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",8)
	
	quit:(Produto="")
	
	if ($piece($get(^INART(0,Produto,1)),"~",1) = ""){
		$$$Alert("Código de produto inválido.")
		quit
	}
	
	k ^VARTempVolumeEstoqueProduto(YM,YBED)
	
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,1) = Local
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,2) = Produto
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,4) = Endereco
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,5) = Programa
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,6) = Mes
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,7) = Ano
	set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,8) = Formato
	
	do GoToForm^COMUtilForm("VARVolumeEstoqueProduto","",,,,,)
	quit

LoadListaUnidadesProduto
	new pProduto,Unidade, objLista, count
	
	kill ^COMTempList(YM,YBED,"VARVolumeEstoqueUnidade")
	set pProduto = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,2)
	quit:(pProduto="")

	&sql(
		DECLARE CursorUnidade CURSOR FOR 
		SELECT OuterUnit, OuterName
		INTO :Unidade, :UnidadeFormatada
		FROM INARTPACK
 		WHERE COMPANY = :YM
 		AND ItemNo = :pProduto)
 		
	
	&sql(OPEN CursorUnidade)
    IF (SQLCODE) {
		QUIT
    }
    
 	FOR { &sql(FETCH CursorUnidade)
		QUIT:SQLCODE
		set ^COMTempList(YM,YBED,"VARVolumeEstoqueUnidade",""_UnidadeFormatada_"",1) = ""_Unidade_""
 	}
 
   	&sql(CLOSE CursorUnidade)
	
	quit
	

GetEstoqueNaData(produto,data,local="",endereco="",programa="")
	new quantidade
	set quantidade=0

	&sql(SELECT SUM(QtyMoved),Item->UnitOfMeasure
		INTO :quantidade,:unidadeEstoque
		FROM alSOH.dStockHistory
		WHERE Item=:produto 
		AND Location=:local
		AND DateConfirmed<=:data
		AND NonStock=0
		AND Storage->Code != 'X'
		AND ((:endereco IS NULL) OR (Storage->Code like :endereco_"%") OR (Storage->Physical->Code like :endereco_"%"))
		AND ((:programa IS NULL) OR (Bundle->Program = :programa))
		)
	
	if (SQLCODE=0) {
		set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,9) = unidadeEstoque

	} else {
	
		//Caso a consulta retorne vazia, seta uma unidade padrão 
		//para retornar 0 nos cálculos do relatório
		set objProduto = $get(^INART(0,produto,1))
		set UnidadeMinima = $$$INARTUnitofMeasure(objProduto)
		set $piece(^VARTempVolumeEstoqueProduto(YM,YBED,1),Y,9) = UnidadeMinima
	}	
		
	quit quantidade

GetEstoqueNaDataRede(produto,data,Location="",endereco="",programa="")
	set quantidadeTotal=0
	
	if (Location = ""){
		for{
			set Location = $order(^WWW0121(0,0,Location))
			quit:Location=""
			set quantidadeTotal=quantidadeTotal+$$GetEstoqueNaData(produto,data,Location,endereco,programa)
		}
		quit quantidadeTotal
	} else {
		set quantidadeTotal=quantidadeTotal+$$GetEstoqueNaData(produto,data,Location,endereco,programa)
		quit quantidadeTotal
	}

	quit quantidadeTotal

MontaTabela(Local,Produto,EnderecoArmazenagem,Programa,Mes,Ano)
	new strStatus,quantidadeMes,count,objReport,DataDia,stKey
	set strStatus 	  = $$$OK
	set quantidadeMes = 0
	set count 	 	  = 0
	
	set Periodo 	= $$GetPeriodo^VARVolumeEstoque(Mes,Ano)
	set DataInicial = $ZDH($piece(Periodo,Y,1),3)
	set DataFinal   = $ZDH($piece(Periodo,Y,2),3)

	kill ^VARTempVolEstoqueProdReport(YM,YBED)
	
	for DataDia=DataInicial:1:DataFinal {
	
		continue:DataDia>+$horolog // Não mostrar o estoque com data no futuro.
	
		set objReport = ""
		set count 	  = $i(count)
		set stKey	  = YBED_","_count
		
		set $piece(objReport,Y,1) = Produto
		set $piece(objReport,Y,2) = $$SQLGetDescricaoProduto^VARSQL(Produto)
		set $piece(objReport,Y,3) = $$SQLGetDataFormatada^VARSQL(DataDia)
		set $piece(objReport,Y,4) = $$GetEstoqueNaDataRede(Produto,DataDia,Local,EnderecoArmazenagem,Programa)
		set $piece(objReport,Y,5) = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,9)
		set $piece(objReport,Y,6) = "" ;nao utilizado nesta tela
		
		set strStatus = $$$Save("VARTempVolEstoqueProdReport",stKey,objReport,1)
		
		if $$$ISERR(strStatus) {
			set $zerror = $$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
	}
	quit
	
GetValorPadraoLocal()
	new Local
	set Local = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,1)
	set Produto = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,2)
	
	if ((Local = "")&&(Produto = "")){
		set Local = YLOCATION
	}
	
	quit Local
	
GetValorPadraoUnidade()
	new Unidade
	set Unidade = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,3)
	set Produto = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,2)
	
	if ((Unidade = "")&&(Produto '= "")){
		set objProduto = $get(^INART(0,Produto,1))
		set Unidade = $$SQLGetDescCompletaUnit^VARSQL($$$INARTUnitofMeasure(objProduto))
	}
	
	quit Unidade
	
GetValorPadraoMes()
	new Mes
	set Mes = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,6)
	
	if (Mes = ""){
		set Mes = $number($piece($zdate($h,3),"-",2))
	}
	
	quit Mes
	
GetValorPadraoAno()
	new Ano
	set Ano = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,7)
	
	if (Ano = ""){
		set Ano = $piece($zdate($h,3),"-",1)
	}
	
	quit Ano
	
GetValorPadraoVisualizacao()
	new Visualizacao
	set Visualizacao = $piece($get(^VARTempVolumeEstoqueProduto(YM,YBED,1)),Y,8)
	
	if (Visualizacao = ""){
		set Visualizacao = 1 ;tabela
	}
	
	quit Visualizacao
]]></Routine>
</Export>