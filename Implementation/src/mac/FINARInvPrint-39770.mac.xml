<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvPrint" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvPrint
	;--------------------------------------------------------------------------------------
	; SR: 11837 - Created as a replacement for FINARRecurringInvoicePrint.mac
	;--------------------------------------------------------------------------------------
	#Include FIN
	#Include FINConst
	#Include COMConst
	#Include COMSYS
	#include REPConst
 
GetContactInfo(pidInvoice,pstrType="Email")
    ;--------------------------------------------------------------------------------
    ; Gets contact information (fax/email) for an invoice.
    ;
    ; Params: pidInvoice:	The invoice id
    ; 		  pstrType:		The type of contact info to get (email/fax)
    ;
    ; ByRefs:
    ; 
    ; Returns: string
    ;
    ; History:
    ; 26-Jun-2006	Steve S		SR14514: Check email for invoices first
    ; 19-Dec-2005	RPW			SR12974: Only zconvert once.
    ; 25-Nov-2005	Steve S		SR12974: Created
    ;---------------------------------------------------------------------------------
	new strContact,objFINARInv,idCustomer,objFINARCustomer
 
	set strContact=""
 
	if '$$$NoKey(pidInvoice) {
		set objFINARInv=$get(^FINARInv(YM,pidInvoice,1))
		set idCustomer=$$$FINARInvCustomer(objFINARInv)
		if (idCustomer'="") {
			set objFINARCustomer=$get(^FINARCustomer(YM,idCustomer,1))
			set pstrType=$zconvert(pstrType,"U")
			if pstrType="EMAIL" {
				//SR14514
				set strContact=$$$FINARCustomerEmailforInvoices(objFINARCustomer)
				if strContact="" set strContact=$$$FINARCustomerEmail(objFINARCustomer)
			} elseif pstrType="FAX" {
				set strContact=$$$FINARCustomerFax(objFINARCustomer)
			}
		}
	}
 
	quit strContact
	
	
CanPrint(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Determines whether an invoice record can be printed
    ;
    ; Returns:
    ;
    ; History:
    ; 11-Sep-2006	JW			SR14771: Cleaned up. Removed redundant checks.
    ; 06-Jan-2006 	SC			SR13798: Decode wrapper removed. Decode in COMSYS.INC
    ; 19-Dec-2005	RPW			SR12482: Added a check for unposted invoices.
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strStatus,objInvoice,idCustomer,objCustomer
 
	set strStatus=$$$OK
	
	if '$$$NoKey(pidInvoice) {
		/* SR14771 - unnecessary, check in postable.
		if '$data(^FINARInvLine(YM,pidInvoice)) {
			set strStatus=$listbuild("Fin00542",pidInvoice) ;Invoice Number ´%1´ must have lines.	
		} else {
		*/
		set objInvoice=$get(^FINARInv(YM,pidInvoice,1))
		
		if $$$FINARInvStatus(objInvoice)'=$$$EnumFINSYSSTATUSPosted {
			set strStatus=$listbuild("Fin00946",pidInvoice)  ;Invoice Number '%1' must be posted.
		} else {
			set idCustomer=$$$FINARInvCustomer(objInvoice)
			
			//if (idCustomer'="") {
			set objCustomer=$get(^FINARCustomer(YM,idCustomer,1))
			//	if (objCustomer'="") {
			if '$$$FINARCustomerPrintInvoices(objCustomer) {
				set strStatus=$listbuild("Fin00934",idCustomer)  ;Can not print invoices for customer `%1`.	
			}	
			/* SR14771 - unnecessary, check in postable.
				} else {
					set strStatus=$listbuild("Fin00079",idCustomer) ;Customer number ´%1´ not Found	
				}
			} else {
					set strStatus=$listbuild("Fin00159")  ;Customer Number Required	
				}
			}
			*/
		}
	} else {
		set strStatus=$listbuild("Fin00089")  ;No Record Selected.	
	}
 
	if $$$ISOK(strStatus) {
		set YQ=$$$YQEnable		
	} else {
		set YQ=$$$YQDisable(strStatus)	;SR13798
	}
 
	quit strStatus
	
	
DefaultOutput(pidInvoice)
	;-------------------------------------------------------------------------------
	; For an invoice, return how the customer likes their invoices sent.
	;
	; Params: pidInvoice	: The invoice number
	;
	; ByRefs:
	;
	; Returns: strOutput
	;
	; History:
	; 26-Jun-2006	Steve S		SR14514: Created
	;-------------------------------------------------------------------------------
	new strOutput,objFINARInv,idCustomer,objFINARCustomer
 
	set strOutput=""
	
	if '$$$NoKey(pidInvoice) {
		set objFINARInv=$get(^FINARInv(YM,pidInvoice,1))
		set idCustomer=$$$FINARInvCustomer(objFINARInv)
		
		if (idCustomer'="") {
			set objFINARCustomer=$get(^FINARCustomer(YM,idCustomer,1))
			set strOutput=$$$FINARCustomerSendInvoicesVia(objFINARCustomer)
			
			if strOutput="" set strOutput=$$$EnumREPOUTPUTPrinter
		}
	}
 
	quit strOutput
]]></Routine>
</Export>