<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHCLines" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHCLines
	
#include WWWConst
#include COMSYS
	
Test(pblnFix=$$$NO)
	;-------------------------------------------------------------------------------
	; Looked for orphaned grid line data and optionally remove.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-Apr-2010	GRF		SR17228: idForm not idx
	; 19-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new idForm,objForm,intErrors,idClass,idForm,idRecord,idParent,idClassX,objParent
	new strStatus,YKEY
	
	$$$VAR
	
	set intErrors = 0
	set idForm = ""
	for {
		set idForm = $order(^WWW120(0,idForm))
		quit:idForm=""
		
		set objForm = $get(^WWW120(0,idForm,1))
		
		if ($$$WWW120FormType(objForm)=12) || $$OtherRelated(idForm) {
			set idClass = $$$WWW120ClassUsedInForm(objForm)
			continue:idClass="COMViewLang"
			continue:idClass="INForeRestSet"
			continue:idForm="INPurOrderCutLine"
			continue:idForm="INPurOrderPlaceLine"
			continue:idForm="INReqFirmLine"
			continue:idForm="INReqIssueAsReq"
			continue:idForm="INReqRW"
			continue:idForm="INReqRWLine"
			continue:idForm="INReqRWLine"
			continue:idForm="INReqReleaseLine"
			continue:idForm="INReqSourceLine"
			continue:idForm="MEDPresAsIssue"
			continue:idClass="VARSESAlertaLinha"
			continue:$extract(idForm,1,3)="VAR"
		;	continue:idClass'="INIssueLine"
			
			if idClass="" {
				write !,"No class defined for form "_idForm_"."
				set intErrors = intErrors+1
			
			} else {
				set idClassX = "^"_idClass
				if $data(@idClassX) {
					set idRecord = idClassX_"("
					write !,idClass_" "_$data(@idClassX)
					set idParent = $$GetParent(idClass)
					if '$data(^WWW001(0,idParent)) {
						set intErrors = intErrors+1
						write !,"Parent missing: "_idClass
					
					} else {
						set idRecord = idClassX
						for {
							set idRecord = $query(@idRecord) quit:idRecord=""
							;if $qlength(idRecord)'=4 {
							;	write !,"Length incorrect:"_idRecord
							;}
							set objParent = $$BuildParent(idParent,idRecord)
							if '$data(@objParent) {
								write !,"   Parent data missing: "_idRecord
								write !,"                      : "_objParent
								set YKEY=$translate($piece(idRecord,",",2,$length(idRecord,",")-1),"""")
								set intErrors = intErrors+1
								if pblnFix set strStatus = $$KILL^COMUtils(idClass,YKEY)
							}
						}
						;write !,idForm
					}
				}
			}
		}
	}
	write !,!, "Errors: "_intErrors
	quit
	
	
OtherRelated(pidForm)
	quit (";INReqIssue;REPViewField;")[(";"_pidForm_";")
	
	
GetParent(pidClass)
	;-------------------------------------------------------------------------------
	; Parent class of this class
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new objWWW002,idClass
	
	if pidClass="INReqIssue" {              set idClass = "INIssue"
	} elseif pidClass="WWW120DynTable" {    set idClass = "WWW120"
	} elseif pidClass="REPViewField" {      set idClass = "REPView"
	} else {
		set idClass = $piece(pidClass,"Line",1)
		if idClass'="" {
			if '$data(^WWW001(0,idClass)) {
				set idClass=""
			}
		}
		if idClass="" {
			set objWWW002 = $get(^WWW002(0,pidClass,1,1))
			set idClass   = $$$WWW002RelationClass(objWWW002)
		}
	}
	quit idClass
	
	
BuildParent(pidParent,pidRecord)
	;-------------------------------------------------------------------------------
	; Create the parent global reference
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new strParent,loop
	
	set strParent = ""
	for loop = 1:1:$qlength(pidRecord)-2 {
		set strParent = strParent_$qsubscript(pidRecord,loop)_","
	}
	set strParent = $$^WWWKEYBUILD(strParent)
	set strParent = "^"_pidParent_"("_strParent_",1)"
	
	quit strParent
	
]]></Routine>
</Export>