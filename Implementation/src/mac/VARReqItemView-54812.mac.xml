<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARReqItemView" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARReqItemView

#include COMSYS
#include COMConst
#include INConst
#include COMView
#include %occInclude

OnBeforeButtonLine(YKEY)
	;-------------------------------------------------------------------------------
	;			
	; Called By : Form INReqItem
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 31-Oct-2013	SCR		SESDF-681:	Created
	;-------------------------------------------------------------------------------
	new idMonth,idStatus,idxMonth,intMonth,strDate,strMonths
	
	kill ^VARSESSolicTransfVigenciaOrd(YM,YUSER)
	set strMonths=$Listbuild("JAN","FEV","MAR","ABR","MAI","JUN","JUL","AGO","SET","OUT","NOV","DEZ")
	for idStatus=1:1:3 {
		set idxMonth	= ""
		for {
			set idxMonth	= $order(^INReqs(YM,6,idStatus,idxMonth))
			quit:idxMonth=""
			if (($length(idxMonth)>5) && (idxMonth '= "-100000000000000")) {
				;set idMonth		= $translate(idxMonth," ","/")
				set intMonth	= $listfind(strMonths,$piece(idxMonth,"/",1))
				set strDate		= $piece(idxMonth,"/",2)_$justify(intMonth,3)
				set ^VARSESSolicTransfVigenciaOrd(YM,YUSER,strDate,1)=idxMonth		
			}
		}
	}
	quit

LoadLineSQL(pobjReqItem,pblnViewMode,pstrSQL)

	;-------------------------------------------------------------------------------
	; Custom SQL for Priority and Month			
	; Called By : Form INReqItem
	;		
	; History :
	; 31-Oct-2013	SCR		SESDF-681:	Created
	;-------------------------------------------------------------------------------
	new idMonth,strDate
	
	set strDate		= $$$INReqItemFREE2(pobjReqItem)
	if strDate'="" {
		set idMonth	= $get(^VARSESSolicTransfVigenciaOrd(YM,YUSER,strDate,1))
		set pstrSQL	= pstrSQL _"and (INReq.FREE16 = '"_idMonth_"') "
	}
	quit pstrSQL
	
UpdateReqItemLine(pobjReqItem,pobjReq,pobjReqLine)

	;-------------------------------------------------------------------------------
	; Update ReqItemLine ( add VigÃªncia )		
	;		
	; History :
	; 01-Nov-2013	SCR		SESDF-681:	Created
	;-------------------------------------------------------------------------------
	set $$$INReqItemLineFREE1(pobjReqItem)	= $$$INReqFREE16(pobjReq)

	quit pobjReqItem
	
OnBeforeSave(YKEY,&YFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By : Form INReqItem
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 02-Nov-2013	SCR		SESDF-681:	Created
	;-------------------------------------------------------------------------------
	new idItem,objItem
	
	set idItem	= $$$INReqItemItem(YFELD)
	quit:idItem=""
	set objItem	= $get(^INART(YM,idItem,0))
	
	set $$$INReqItemFREE3(YFELD)	= $$$INARTFREE35(objItem) ; Tipo
	set $$$INReqItemFREE4(YFELD)	= $$GetConsumoMedioMensalRede^VARSESAlertaRegionaisLinha(idItem)
	quit
]]></Routine>
</Export>