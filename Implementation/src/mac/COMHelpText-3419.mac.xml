<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHelpText" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHelpText
	
ShowHelpText(menu,pblnRegeneratePictures=0,pblnPDF=0)
    ;-------------------------------------------------------------------------------
    ; Shows help to screen for a menu
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Mar-2005	shobby	Gave up trying to fight with WWWHPR it would only compile Help images for
    ; 						screens that appear in the menu structure unless a list of screens was passed
    ; 						in.  This method now calls the GetScreens method which produces a list of
    ; 						screens to create images for. (SR11190)
    ; 28-Sep-2004	Paul K	Added param to generate picture files.
    ; 19-Aug-2004	Shobby	Modified to prevent recursive calls to ShowHelpText
    ; 						When generating the Help Screens.
    ;-------------------------------------------------------------------------------
    
    ; FIXME : <GRF> This approach is flawed.
    ;   Job 1 - Finds ShowHelpText=""; sets to "1"; starts processing
    ;   Job 2 - Finds ShowHelpText=1; bypasses if block; kills ShowHelpText
    ;   Job 3 - Finds ShowHelpText=""; sets to "1"; starts processing
    ;   
    ;   Admittedly unlikely - what is reasoning to stop processing twice?
    ;   
    ;   Consider CacheTemp* rather than ShowHelpText as it is transient anyway
    
	new VORG,YPARA,savYFORM,wait,lstScreens
	
	if $get(^ShowHelpText(menu))="" { ; Spoke to Shobby and YUSER needs to be used in flag ShowHelpText
		set ^ShowHelpText(menu) = 1   ; Have not implemented YUSER as SR10852 is still be looked into.
		set savYFORM = YFORM	
		if menu="" {
			set VORG(1) = ""		                 ; list of forms
		} else {
			set VORG(1) = $$GetScreens(menu)
		}
		set VORG(2) = 1		                     ; print all forms
		set VORG(3) = pblnRegeneratePictures	 ; re-generate new pictures
		set VORG(3) = 1		                     ; re-generate new pictures
		
		set VORG(4) = pblnPDF	                 ; print PDF file
		set VORG(5) = 1		                     ; display Menu overview
		set YPARA   = menu		                 ; menu option
	
		do ^WWWHPR
		set YFORM = savYFORM
	}
	kill ^ShowHelpText(menu)
	
	;do ReloadForm^COMUtilForm() ; causes header to be displayed twice
	;do RedirectForm^COMUtilForm(YFORM,"","","","") ; This does work but unfortunately you will see form load twice, first without help then with.
	quit
	
	
GetScreens(pstrMenu)
    ;-------------------------------------------------------------------------------
    ; Creates a list of screens associated with the specified module.
    ;
    ; Returns:
    ;
    ; History:
    ; 10-Sep-2008	GRF		Layout
	; 24-Jun-2005	RPW		SR12498: Changed Reporting to Reports
    ; 31-May-2005	RPW		SR12498: Added REP
    ; 01-Apr-2005	shobby	SR11190: Should have looped through WWW120 not WWW001
    ; 17-Mar-2005	Shobby	SR11190: Created
    ;-------------------------------------------------------------------------------
	new strPrefix,strPrefix1,idx,idxPrefix,lstScreens
	
	set strPrefix(0) = ""
	set strPrefix(1) = ""
	set lstScreens   = ""
	
	if pstrMenu="Finance"              set strPrefix(0) = "FIN" 
	if pstrMenu="Reports"              set strPrefix(0) = "REP"
	if pstrMenu="System"               set strPrefix(0) = "COM"
	if pstrMenu="Sale"                 set strPrefix(0) = "SAL"
	if pstrMenu="@net_Manager"         set strPrefix(0) = "WWW"
	if pstrMenu="AlphaLinc"            set strPrefix(0) = "IN"
	if pstrMenu="Time_Manager"         set strPrefix(0) = "TE"
	if pstrMenu="Advanced_Procurement" set strPrefix(0) = "AEP"
	if pstrMenu="Local_Customisation"  set strPrefix(0) = "VAR"
	if pstrMenu="Workflow"             set strPrefix(0) = "WF"
	if pstrMenu="Cost"                 set strPrefix(0) = "CST"
	if pstrMenu="Stock_Control"        set strPrefix(0) = "STK"
	
	set idx = ""
	for {
		set idx = $order(^WWW120(0,idx))
		quit:idx=""
		
		set idxPrefix = ""
		for {
			set idxPrefix = $order(strPrefix(idxPrefix))
			quit:idxPrefix=""
			
			if strPrefix(idxPrefix)'="" {
				if ($extract(idx,1,$length(strPrefix(idxPrefix)))=strPrefix(idxPrefix)) {
					if (idx'="FINAPVoucherPrePost") &&
					   (idx'="INDRP")               &&
					   (idx'="INNACHKALK2")         &&
					   (idx'="WWWDIR")              &&
					   (idx'="WWWGEDT")             &&
					   (idx'="WWWCAL1")             &&
					   (idx'="WWWEXPLORER")         &&
					   (idx'="WWWFLD")                 {
						
						set lstScreens = lstScreens_idx_";"
					}
				}
			}
		}
	}
	if lstScreens'="" {
		set lstScreens = $extract(lstScreens,1,$length(lstScreens)-1)
	}
	quit lstScreens
	
]]></Routine>
</Export>