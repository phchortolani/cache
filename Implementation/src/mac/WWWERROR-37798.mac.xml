<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWERROR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWERROR
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERRORHANDLING
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 20-Jul-2009	GRF		Data & boolean macros; doco
	; 27-Mar-2008	GRF		SR15647: Add a second's delay to ensure not being over-written
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 11.10.2000	DT		Created
	;-------------------------------------------------------------------------------
	NEW INFO,ERRNUM
	
	; D113	$$$WWW012ErrorTrappingWithVariable()
	; D114	$$$WWW012ExecuteErrorTrapping()
	
	SET YERROR=$ZERROR
	SET $ZTRAP=""  ;TYBD;25,11,2004;BEWIDATA REQUEST
	
	;---------------------------------------
	; *** EXECUTE # ***
	;---------------------------------------
	
	IF '$FIND(YERROR,"<WRITE>") IF $GET(YM)'="" IF $GET(Y)'="" IF $$$WWW012ExecuteErrorTrapping($GET(^WWW012(0,YM,1)))'="" DO  QUIT  ; MANDANTENSPEZIFISCHE ERROR TRAPPING
	. IF $$$WWW012ErrorTrappingWithVariable(^WWW012(0,YM,1))=$$$YES DO BACK^%ETN  ;ERRORTRAP CACHE
	. XECUTE $$$WWW012ExecuteErrorTrapping(^WWW012(0,YM,1))
	
	;LOOP  SICHERUNG WEGEN ERROR IN ERROR ;TYBD;19.05.2003 ;quibble within 
	IF $GET(YUSER)'="" IF +$GET(YTRAKT)'=0 DO
	. IF YTRAKT>10000                                           DO CLOSE^WWWSTART HALT  ;ENDE ;termination 
	. IF $GET(^WWWZWS(0,+$HOROLOG,YUSER,"X","YTRAKT",1))=YTRAKT DO CLOSE^WWWSTART HALT  ;ENDE ;termination 
	;. SET ^WWWZWS(0,+$H,YUSER,"X","YTRAKT",1)=YTRAKT
	
	SET ERRNUM=""
	IF ($GET(YM)'="") && ($GET(Y)'="") && ($$$WWW012ErrorTrappingWithVariable($GET(^WWW012(0,YM,1)))=$$$YES) {
		SET ERRNUM = " / Error #"_(+$GET(^ERRORS(+$HOROLOG))+1) ;FIS;21.05.04;25690;NUMMER IN ^ERRORS
	}
	LOCK -^WWWSOR(+$GET(YUSER))       ;FREE JOB
	DO OPEN^WWWSTART                  ;GGF DEVICE FALSCH ;wrong 
	IF '$FIND(YERROR,"<WRITE>") {
		SET ^WWWERROR(0,+$HOROLOG,$PIECE($HOROLOG,",",2),+$GET(YUSER),1)=YERROR_" / http:"_ERRNUM_" / "_$GET(YBED)
		hang 1       ; SR15647
	}
	SET INFO = $$^WWWTEXT(387)_"|Info : "_$PIECE($PIECE(YERROR,"<",2),">",1)_"|Prog.: "_$PIECE(YERROR,">",2,9)
	; "An Internal ERROR Has Occurred In Your Application."
	
	if ($get(YBED)'="")                  &&
	   (+$$^WWWBEDBER(YBED)=1)           &&
	   ($get(YMANDANT)="Development")    &&
	   ($$^WWWACCESS(,"WWW")=1)          &&
	   $find($piece(YERROR,">",2,9),"^")    {
		   
		SET INFO=INFO_"|Code: "_$TEXT(@$PIECE(YERROR,">",2,9))
	}
	
	IF $FIND(YERROR,"<EDITED>") SET INFO = "Update Is In Process."  ;tybd;edited;30,9,2004;26492    ; FIXME : WWW009 text <GRF>
	
	DO ^WWWINFO(INFO)
	IF $GET(YUSER)'="" DO
	. QUIT:$GET(Y)=""
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,5)  = $$$NO    ;HTML AUS ;HTML out of     ; $$$WWWUSERHTMLStarted
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,6)  = $$$NO    ;BODY AUS ;out of 
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,9)  = $$$NO    ;FORMULAR AUS ;form out of 
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,10) = $$$NO    ;KOPF AUS ;pate out of 
	. LOCK +^WWWSOR(YUSER,"PAGE"):2                   ;CHECK AUF SEITE;TYBD;19,12,2003
	. LOCK -^WWWSOR(YUSER,"PAGE")
	. KILL ^WWWSOR(YUSER)
	
	IF $GET(YM)'="" IF $GET(Y)'="" IF $$$WWW012ErrorTrappingWithVariable($GET(^WWW012(0,YM,1)))=$$$YES DO BACK^%ETN  ;ERRORTRAP CACHE
	QUIT
	
XML ;XMLERROR
	;SET $ZE="XML^WWWERROR"
	SET YERROR=$ZERROR
	SET $ZTRAP=""
	LOCK -^WWWSOR(+$GET(YUSER))  ;FREE JOB
	DO OPEN^WWWSTART             ;GGF DEVICE FALSCH ;wrong 
	SET ^WWWERROR(0,+$HOROLOG,$PIECE($HOROLOG,",",2),+$GET(YUSER),1) = YERROR_" / xml / "_$GET(YBED)
	hang 1       ; SR15647
	
	DO ^WWWXMLMSG(387,$TRANSLATE(YERROR,"><","  "))
	IF $GET(YUSER)'="" DO
	. LOCK +^WWWSOR(YUSER,"PAGE"):2   ;CHECK AUF SEITE;TYBD;19,12,2003
	. LOCK -^WWWSOR(YUSER,"PAGE")
	. KILL ^WWWSOR(YUSER)
	
	IF $GET(YM)'="" IF $GET(Y)'="" IF $$$WWW012ErrorTrappingWithVariable($GET(^WWW012(0,YM,1)))=$$$YES DO BACK^%ETN  ;ERRORTRAP CACHE
	QUIT
	
BACKGROUND ;BACKGROUND
	;SET $ZE="BACKGROUND^WWWERROR"
 
	$$$JournalOff
 
	set YERROR=$ZERROR
	set $ZTRAP=""
	LOCK -^WWWSOR(+$GET(YUSER))  ;FREE JOB
	set ^WWWERROR(0,+$HOROLOG,$PIECE($HOROLOG,",",2),+$GET(YUSER),1) = YERROR_" / Background / "_$GET(YBED)
	hang 1       ; SR15647
	
	IF $GET(YM)'="" IF $GET(Y)'="" IF $$$WWW012ErrorTrappingWithVariable($GET(^WWW012(0,YM,1)))=$$$YES DO BACK^%ETN  ;ERRORTRAP CACHE
	
	$$$JournalOn
	
	QUIT
	
SetError(pstrErrorText)	;create manual error entry
	;-------------------------------------------------------------------------------
	; Description of Function : create manual error entry
	; 
	; History :
	; 27-Mar-2008	GRF		SR15647: Add a second's delay to ensure not being over-written
	; 15-Jan-2008	FIS		SRBR014564: Created
	;-------------------------------------------------------------------------------
	set ^WWWERROR(0,+$HOROLOG,$PIECE($HOROLOG,",",2),+$GET(YUSER),1) = pstrErrorText_" / MANUAL ENTRY / "_$GET(YBED)
	hang 1       ; SR15647
	quit
	
	
]]></Routine>
</Export>