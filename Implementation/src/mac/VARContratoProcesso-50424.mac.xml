<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContratoProcesso" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContratoProcesso
#include COMSYS

OnBeforeSave(pYKEY)
	quit:(pYKEY = "")
	
	new strStatus, CodContrato, processoSelecionado, NoProcesso, objContratoProcesso, isProcessoPrincipal,
		chave
	
	set strStatus = $$$OK
	
	set CodContrato = $$$KEY1(pYKEY)
	set processoSelecionado = $$$KEY2(pYKEY)
	
	if $piece(YFELD,Y,1) = 1 {
		
		//Verifica se hÃ¡ outro processo como contrato principal e altera-o		
		$$$Order3(^VARContratoProcesso,YM,CodContrato,NoProcesso)
			
			continue:(NoProcesso = processoSelecionado)
			
			set objContratoProcesso = $get(^VARContratoProcesso(YM,CodContrato,NoProcesso,1))
			set isProcessoPrincipal = $piece(objContratoProcesso,Y,1)
			
			if (isProcessoPrincipal = 1) {
				set $piece(objContratoProcesso,Y,1) = 0	
				set chave = CodContrato_","_NoProcesso
				set strStatus = $$$Save("VARContratoProcesso",chave,objContratoProcesso,1)
			}
						
		$$$End		
	}	
	
	if ($piece(YFELD,Y,1) '= 1) && ($data(^VARContratoProcesso(YM,CodContrato)) = 0) {
		set $piece(YFELD,Y,1) = 1	
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		quit
	}
	
	quit
	
OnAfterDataFields(pYKEY)
	quit:((pYKEY = "") || (pYKEY = "+"))
	
	new CodContrato, NoProcesso, isProcessoPrincipal, count, assunto
	set CodContrato = $$$KEY1(pYKEY)
	quit:(CodContrato = "")
	
	set count = 0
	
	//Tabela
   	new lstHeader
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","No. Processo","Assunto","Detentor Atual","Processo Principal")
 
  	if $$Start^COMTable(lstHeader,"&nbsp;Processos Vinculados a este Contrato",,,$$$NO) { 	
	
		$$$Order3(^VARContratoProcesso,YM,CodContrato,NoProcesso)
			
			set isProcessoPrincipal = $piece($get(^VARContratoProcesso(YM,CodContrato,NoProcesso,1)),Y,1)
			
			set count = $i(count)
				
			set chave = CodContrato_","_NoProcesso			
			set detentorAtual = $$getDetentor^VARSESProcesso(NoProcesso)
			set assunto = $piece($get(^VARSESProcesso(YM,NoProcesso,1)),Y,1)
			
	 		do NewLine^COMTable()
	 		do InsertCell^COMTable(count)
	 		do InsertCell^COMTable(NoProcesso,"VARContratoProcesso",chave)
	 		do InsertCell^COMTable(assunto,"VARContratoProcesso",chave)
	 		do InsertCell^COMTable(detentorAtual,"VARContratoProcesso",chave)
	 		do InsertCell^COMTable($$SQLGetYesOrNo^VARSQL(isProcessoPrincipal),"VARContratoProcesso",chave,,,"CENTER")
	 		do EndLine^COMTable()
 
		$$$End
		
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum processo associado a este contrato.","VARContratoProcesso",CodContrato,,,,5)
			do EndLine^COMTable()		
		}
		
		do Stop^COMTable()
  	}
	
	quit
	
	
OnBlurNoProcesso(pYKEY)
	quit:(pYKEY = "")
	
	new NoProcesso, DetentorAtual
	set NoProcesso = $$$KEY2(pYKEY)
 
	if (NoProcesso = "") {
		set %TXT(1) = "#YVARContratoProcessoM4~"	
	} else {		
		set %TXT(1) = "#YVARContratoProcessoM4~"_$$getDetentor^VARSESProcesso(NoProcesso)
	}
	
	quit
	
	
OnBeforeButtonLine(pYKEY)
	quit:(pYKEY = "")
	set ^COMTempList(YM,YBED,"VARLastContrato",1) = $$$KEY1(pYKEY)
	quit
]]></Routine>
</Export>