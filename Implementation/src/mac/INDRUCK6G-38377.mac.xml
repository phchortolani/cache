<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRUCK6G" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRUCK6G ;INDRUCK6G;DT;DRUCKEN POSITIONSGRUPPEN;19.02.2002  ; Compiled March 7, 2005 13:14:34
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		DRUCKEN POSITIONSGRUPPEN
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| DT	19.02.2002  
	;|
	;\------------------------------------------------------------------/
	;
	;
	;
	;VON^INDRUCK6
	
POSUEB ;DRUCKEN POSITIONSGRUPPEN-ÜBERSCHRIFT ;print 
	;
	;
	QUIT:$PIECE(YBELEG1,Y,187)=1  ;POSITIONEN NICHT DRUCKEN ;Not print 
	IF $PIECE($G(POS1),Y,309)=1 QUIT:YBELEG=6  ;KEIN LIEFERSCHEIN DRUCKEN ;no packing slip print 
	;
	SET POSGRUP=$PIECE(POS1,Y,247)  ;POSITIONSGRUPPE
	IF YBELEG=1!(YBELEG=2)!(YBELEG=10) DO
	. NEW POSX,YQ
	. SET YQ(1)=0  ;ABBRUCH ÜBERSCHRIFT ;discontinuation superscription 
	. IF YBELEG=1!(YBELEG=10) DO  ;ANGEBOT ;proposition 
	. . SET POSX(1)=$ORDER(^INANGP(YM,YAUFTRAG,POS),-1)  ;VORHERIGE POSITION
	. . IF POSX(1)'="" IF $PIECE($GET(^INANGP(YM,YAUFTRAG,POSX(1),1)),Y,247)=POSGRUP SET YQ(1)=1  ;BEREITS GEDRUCKT ;yet 
	. . QUIT
	. IF YBELEG=2 DO  ;AB ;Confirm. 
	. . SET POSX(1)=$ORDER(^INAUFP(YM,YAUFTRAG,POS),-1)
	. . IF POSX(1)'="" IF $PIECE($GET(^INAUFP(YM,YAUFTRAG,POSX(1),1)),Y,247)=POSGRUP SET YQ(1)=1  ;BEREITS GEDRUCKT ;yet 
	. . QUIT
	. QUIT:YQ(1)=1
	. QUIT:POSGRUP=""
	. ;
	. SET YA(1)=$PIECE(YBELEG1,Y,182)  ;DRUCK 0/1 ;printing 
	. QUIT:YA(1)'=1
	. ;
	. SET A=YSPE
	. DO SPE^INDRUCK
	. SET YA(2)=$PIECE(YBELEG1,Y,183)   ;SPALTE ;rift 
	. IF +YA(2)=0 SET YA(2)=22
	. IF YA(2)="" SET YA(2)=14
	. SET YA(3)=""
	. IF POSGRUP="" SET POSGRUP=" "
	. IF YBELEG=1!(YBELEG=10) IF $DATA(^INANGPARA(YM,YAUFTRAG,POSGRUP,1)) SET YA(3)=$PIECE(^(1),Y,1)
	. IF YBELEG=2 IF $DATA(^INAUFPARA(YM,YAUFTRAG,POSGRUP,1)) SET YA(3)=$PIECE(^(1),Y,1)
	. IF YA(3)="" SET YA(3)=$PIECE(POS1,Y,247)
	. IF YA(3)="" IF YBELEG=1 SET YA(3)=$$^WWWTEXT(32180)   ;ALTERNATIV BIETEN WIR AN ;afford we upon 
	. IF YA(3)="" IF YBELEG=10 SET YA(3)=$$^WWWTEXT(32202)   ;BIETEN SIE UNS ALTERNATIV AN ;afford they we upon 
	. SET YA(3)="<B>"_YA(3)_"</B>"
	. SET A=$EXTRACT(A,1,YA(2))_YA(3)_$EXTRACT(A,YA(2)+1+$LENGTH(YA(3)),132)   ;EINSETZEN ;stow 
	. DO SPE^INDRUCK
	. SET A=YSPE
	. DO SPE^INDRUCK
	. QUIT
	QUIT
	;
	;
	
POSZWS ;DRUCKEN POSITIONSGRUPPEN-ZWISCHENSUMME ;print 
	;
	QUIT:$PIECE(YBELEG1,Y,187)=1  ;POSITIONEN NICHT DRUCKEN ;Not print 
	IF $PIECE(POS1,Y,309)=1 QUIT:YBELEG=6  ;KEIN LIEFERSCHEIN DRUCKEN ;no packing slip print 
	;
	NEW GRUPPE
	SET POSGRUP=$PIECE(POS1,Y,247)  ;POSITIONSGRUPPE
	IF YBELEG=1!(YBELEG=2)!(YBELEG=10) DO
	. NEW POSX,YQ
	. SET YQ(2)=0  ;ABBRUCH ZWISCHENSUMME ;discontinuation subtotal 
	. IF YBELEG=1!(YBELEG=10) DO  ;ANGEBOT ;proposition 
	. . SET POSX(2)=$ORDER(^INANGP(YM,YAUFTRAG,POS),1)  ;FOLGENDE POSITION
	. . IF POSX(2)'="" IF $PIECE($GET(^INANGP(YM,YAUFTRAG,POSX(2),1)),Y,247)=POSGRUP SET YQ(2)=1  ;NICHT LETZTE POSITION ;Not last 
	. . QUIT
	. IF YBELEG=2 DO  ;AB ;Confirm. 
	. . SET POSX(2)=$ORDER(^INAUFP(YM,YAUFTRAG,POS),1)  ;FOLGENDE POSITION
	. . IF POSX(2)'="" IF $PIECE($GET(^INAUFP(YM,YAUFTRAG,POSX(2),1)),Y,247)=POSGRUP SET YQ(2)=1  ;NICHT LETZTE POSITION ;Not last 
	. . QUIT
	. QUIT:YQ(2)=1
	. QUIT:POSGRUP=""
	. SET A=YSPE
	. ;SET YA(1)=$PIECE(YBELEG1,Y,182)  ;DRUCK POSITIONSGRUPPEN 0/1
	. SET YA(1)=$PIECE(YBELEG1,Y,184)  ;DRUCK SUMMENAUFLISTUNG 0/1 ;printing 
	. SET YA(2)=$PIECE(YBELEG1,Y,183)   ;SPALTE ;rift 
	. IF YA(2)="" SET YA(2)=14
	. IF YA(1)=1 DO  ;ANZEIGEN ;give notice  ;display 
	. . DO SPE^INDRUCK
	. . ;
	. . SET A=YSPE
	. . SET YA(3)=""
	. . DO  ;GRUPPENNAME
	. . . SET GRUPPE=""
	. . . IF POSGRUP="" SET GRUPPE=" " QUIT
	. . . IF YBELEG=1!(YBELEG=10) SET GRUPPE=$PIECE($GET(^INANGPARA(YM,YAUFTRAG,POSGRUP,1)),Y,1)
	. . . IF YBELEG=2 SET GRUPPE=$PIECE($GET(^INAUFPARA(YM,YAUFTRAG,POSGRUP,1)),Y,1)
	. . . IF GRUPPE="" SET GRUPPE=POSGRUP  ;MANUELLER TEXT
	. . . QUIT
	. . SET $PIECE(YA(3)," ",30)=" "
	. . SET YA(3)=GRUPPE_$EXTRACT(YA(3),1+$LENGTH(GRUPPE),30)
	. . SET YA(3)=YA(3)_$$^WWWZAHL(POSSUM,,2,YWHR)
	. . SET A=$EXTRACT(A,1,YA(2))_YA(3)_$EXTRACT(A,YA(2)+1+$LENGTH(YA(3)),132)   ;ZWISCHENSUMMEN
	. . SET YI(4)=+$GET(YI(4))+1
	. . SET POSZWS(YI(4))=A
	. . SET POSSUM=0
	. . SET A="<U>"_A_"</U>"
	. . DO SPE^INDRUCK
	. . ;SET A=YSPE ;table-mat 
	. . ;DO SPE^INDRUCK
	. . QUIT
	. QUIT
	QUIT
	;
	;
	
POSGRUP ;DRUCKEN POSITIONSGRUPPEN-AUFSTELLUNG ;print 
	;
	QUIT:$PIECE(YBELEG1,Y,187)=1  ;POSITIONEN NICHT DRUCKEN ;Not print 
	;
	;IF $PIECE(POS1,Y,309)=1 QUIT:YBELEG=6   ;KEIN LIEFERSCHEIN DRUCKEN
	IF YBELEG'=1 IF YBELEG'=2 IF YBELEG'=10 QUIT  ;FALSCHER BELEG ;proof 
	NEW POSZWSNR
	SET YA(1)=$PIECE(YBELEG1,Y,184)  ;DRUCK 0/1 ;printing 
	IF YA(1)=1 DO  ;ANZEIGEN ;give notice  ;display 
	. SET YA(2)=$PIECE(YBELEG1,Y,49)   ;SPALTE ;rift 
	. IF YA(2)="" SET YA(2)=14
	. SET A=YSPE
	. QUIT:'$DATA(POSZWS)
	. ;
	. IF $PIECE(YBELEG1,Y,186)=1 SET LFN=999  ;AUF EXTRA SEITE DRUCKEN ;upon extra side print 
	. ;
	. DO SPE^INDRUCK
	. SET YA(3)=$$^WWWTEXT(32750)  ;POSITIONSGRUPPEN
	. IF $TRANSLATE($PIECE(YBELEG1,Y,185)," ")'="" SET YA(3)=$PIECE(YBELEG1,Y,185)
	. SET YA(3)="<B><U>"_YA(3)_"</B></U>"  ;POSITIONSGRUPPEN
	. SET A=$EXTRACT(A,1,YA(2))_YA(3)_$EXTRACT(A,YA(2)+1+$LENGTH(YA(3)),132)   ;ÜBERSCHRIFT ;superscription 
	. DO SPE^INDRUCK
	. IF $PIECE(YBELEG1,Y,186)=1 DO  ;ABSTAND ;offset 
	. . SET A=YSPE
	. . DO SPE^INDRUCK
	. . QUIT
	. ;
	. SET POSZWSNR="" FOR  SET POSZWSNR=$ORDER(POSZWS(POSZWSNR)) QUIT:POSZWSNR=""  DO
	. . SET A=$GET(POSZWS(POSZWSNR))
	. . DO SPE^INDRUCK
	. . QUIT
	. SET A=$TRANSLATE(YSPE," ","=")
	. DO SPE^INDRUCK
	. QUIT
	QUIT
]]></Routine>
</Export>