<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INKUNDED52" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INKUNDED52   ; Tax Location field
	
OnBlur(pLocation="")
	;-------------------------------------------------------------------------------
	; Validate the tax location and populate Default Tax Code
	;
	; Returns:
	;
	; History:
	; 19-Mar-2010	shobby	SR15753: Removed redundant code.
	; 02-May-2005	JW		Created SR12253 - copied from INLIEFD53
	;-------------------------------------------------------------------------------
	new idTaxCode,codesArray,default
	
	set default = $$DefaultTaxCode^COMTAX(pLocation)
	do StartCombo^COMCombo("document.WWW2.Y"_YFORM_"D59",default,1)
	
	if pLocation'="" {
		if $$IsValidLocation^COMTAXLocation(pLocation) {
			
			do CreateTaxCodesList^COMTAXLocation(pLocation,.codesArray)
			set idTaxCode = ""
			for {
				set idTaxCode = $order(codesArray(idTaxCode))
				quit:idTaxCode=""
				
				do AddOption^COMCombo(idTaxCode,$zcvt(idTaxCode_" - "_$get(codesArray(idTaxCode)),"o","JS"))
			}
			
		} else {
			SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D52~"
			SET %TXT(1) = %TXT(1)_"#!"_$$^WWWTEXT(34155,,1)  ; "Tax Location does not exist"
		}
	}
	do StopCombo^COMCombo()
	quit
	
]]></Routine>
</Export>