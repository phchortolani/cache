<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INTRNPickList" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INTRNPickList
	
#include INConst
#include COMSYS
#include %occInclude
	
PrintPickList(pidTRN)
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the Transfer
    ;
    ; Params: None
    ;
    ; Returns: None
    ;
    ;
    ; History:
    ; 28-Oct-2008	HQN		SR16088: Direct usage of screenwriting and ^WWWBACK,
    ; 							YBACK not handled correctly, workaround
    ; 12-Dec-2007	PPP		SR15597: Created  (this is a temporary print format)
    ; 							Will be superceded by a report.
    ; 							Currently Views exist for the Transfer Pick Header
    ; 							and Lines and CS can write their own reports uisng
    ; 							these views if  necessary.
    ;-------------------------------------------------------------------------------
	new ANZM,strStatus,YDDSATZ
	
	set YBACK = ""
	kill ^WWWSOR(YUSER)
	set ANZM = 0
	
	set strStatus = $$$OK
	
	if $$$ISOK(strStatus) {
		set YNOFOOT = 1
		do List(pidTRN)
		if ANZM=0 do ^WWWINFO($$^WWWTEXT(46),1,,1)    ; "No Data Available"
	}
	kill ^WWWSOR(YUSER)
	quit strStatus
	
	;  List : constructs Headers as "UES" and managers report
	;
	;====+==========================================================================
	; <= |                                                                            Title
	;----+            T r a n s f e r   O r d e r   P i c k l i s t                   
	;                       Issued By Location:  xx (xxxxxxx)
	;For Location: xx (xxxxxxx)                           Transfer Number: xxxxxxxxx  
	;Reference: xxxxxxx               Status: x           Delivery Date : xx/xx/xxxx  
	;-------------------------------------------------------------------------------
	;Number|Sending Storage|Item|Description|Quantity| Unit |Use By Date|Lot Number|  Header - from "UES"
	;------+---------------+----+-----------+--------+------+-----------+----------+
	;      |               |    |           |        |      |           |          |  ANZ
	;------+---------------+----+-----------+--------+------+-----------+----------+
	;      |               |    |           |        |      |           |          |
	;------+---------------+----+-----------+--------+------+-----------+----------+
	;      |               |    |           |        |      |           |          |
	;===============================================================================
	;  P2         D2         D1  D1=>INART D1   D11     D4       D8           D7
	
	
List(pidTRN) ;
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the Transfer
    ;
    ; Params: None
    ;
    ; Returns: None
    ;
    ;
    ; History:
 	; 25-Jun-2009	PPP		SR16682: Get the Bundle details directly from the Bundle Properties	
    ; 15-Dec-2008   DavidR  SR16302: Added two more columns for lot number and expiry
    ; 							date. Also modified the naming of the columns to be
    ; 							reliant on the form, to allow for any customisation.
    ; 12-Dec-2007	PPP		SR15597: Created
    ;-------------------------------------------------------------------------------
	new ANZSEIT,intLn,LFN,objVORGLANG,TEXT,UES,VORG1,YFELD
	
	set YNOFOOT = 1
	do ^WWWBACK
	
	set VORG1       = $get(^INVORG(0,0,1))
	set objVORGLANG = $get(^INVORGLANG(0,0,SPRACHE,1))
	set TEXT=$$$INVORGLANGTextAfterPickList(objVORGLANG)
	
	set ANZSEIT=$piece(VORG1,Y,82)    ; Number within side 
	if ANZSEIT="" set ANZSEIT=30
	
	set idForm = "INTFRPickLine"
	set UES = $$^WWWTEXT(207)                                                        ;1 Number
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINTFRPickLineSendingStockLocation) ;2
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINTFRPickLineItem)                 ;3
	set UES=UES_Y_$$^WWWTEXT(32026)                                                  ;4 Item Description
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINTFRPickLineQuantity)	         ;5
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINTFRPickLineUnit)                 ;6
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINTFRPickLineUsebyDate)            ;7
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINTFRPickLineLotNumber)            ;8
	
	set ANZM = 0
	do Header(pidTRN)		
	set YDDSATZ = 0
	
	set intLn = 0
	for {
		set intLn = $order(^INTFRPickLine(0,pidTRN,intLn))
		quit:intLn=""
		
		set LFN   = ""
		set YFELD = $get(^INTFRPickLine(0,pidTRN,intLn,1))
		
		if ANZM'=0 {
			if ANZM#ANZSEIT=0 {
				write "</TD></TR>"
				write "</TABLE>"
			;	do ZUSATZTEXT
				do FF^WWWW()
				do Header(pidTRN)    ; FIXME : Should pass in UES
			}
		}
		do ANZ                       ; FIXME : Should pass in intLn
	}
	do LEER^WWWTAB(15,8) ; add columns from 6 to 8
	do STOP^WWWTAB
	do ZUSATZTEXT(TEXT)
	quit
	
	
ANZ
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the Transfer
    ;
    ; Params : None
    ; 
    ; By Ref : YFELD		^INTFRPickLine record
    ;
    ; Returns: None
    ;
    ;
    ; History:
	; 04-Apr-2011	shobby	SR17714.4: Redirected previous change to INLP.
    ; 02-May-2011	shobby	SR17714.2: Corrected Macro.
	; 28-Apr-2011	shobby	SR17714.2: Use CodeWithTSU formatting.
    ; 15-Sep-2010	GRF		SR17534: Use Picked Quantity D11 rather than D3 so split
    ;							line qty shown correctly (was repeating original)
 	; 24-Jun-2009	PPP		SR16682: Get the Bundle details directly from the Bundle
 	; 							Properties	
    ; 15-Dec-2007   DavidR  SR16302: Modified to populate extra fields added (7 and 8)
    ;                       	also modified call to use macro to make it readable.
    ; 12-Dec-2007	PPP		SR15597: Created
    ;-------------------------------------------------------------------------------
	new dteUBD,idUnit,objItem,strItemDescription
	 
	set ANZM = 1+ANZM
	write YCR
	do NL^WWWTAB
	
	; 1. Line Number
	;---------------------------------------
	do NF^WWWTAB
	write YCR,intLn,"&nbsp;"
	
	; 2. Storage
	;---------------------------------------
	do ZWL^WWWTAB
	;SR17714.2 write $$$INTFRPickLineSendingStockLocation(YFELD),"&nbsp;"
	write $$CodeWithTSU2^INLP($$$INTFRSendingLocation($get(^INTFR(0,pidTRN,1))),$$$INTFRPickLineSendingStockLocation(YFELD))	;SR17714.4

	; 3. Item
	;---------------------------------------
	do ZWL^WWWTAB
	write $$$INTFRPickLineItem(YFELD),"&nbsp;"
	
	; 4. Item Description
	;---------------------------------------
	do ZWL^WWWTAB
	set objItem = $get(^INART(0,$piece(YFELD,Y,1),1))
	set strItemDescription = $$$INARTSearchName(objItem)
	if $length(strItemDescription) > 15 {
		set strItemDescription = $extract(strItemDescription,1,15)_"..."
	}
	write strItemDescription,"&nbsp;"
	
	; 5. Quantity
	;---------------------------------------
	do ZWR^WWWTAB
	;write $$$INTFRPickLineQuantity(YFELD),"&nbsp;"  ; SR17534
	write $$$INTFRPickLinePickedQuantity(YFELD),"&nbsp;"             ; [IU]
	
	; 6. Unit
	;---------------------------------------
	do ZWL^WWWTAB
	set idUnit = $$$INTFRPickLineUnit(YFELD)                         ; [IU]
	write idUnit_" ("_$$$AppEnum("EINHEIT",idUnit)_")","&nbsp;"
	
	; 7. Use By Date
	;---------------------------------------
	do ZWL^WWWTAB
	set dteUBD = $$$INTFRPickLineUsebyDate(YFELD)
	if dteUBD'=""  set dteUBD = $$^WWWDATE(dteUBD)
	write dteUBD,"&nbsp;"
	
	; 8. Lot No
	;--------------------------------------- ;  Added
	do ZWL^WWWTAB
 	set idLot = $$$INTFRPickLineBatch(YFELD)
	write idLot,"&nbsp;"	
	write YCR
	do EL^WWWTAB
	
	quit
	
	
ZUSATZTEXT(TEXT)   ;ZUSATZTEXT
 	new YI
 	
	if ANZM'=0 if TEXT'="" do
	. write "<BR>"
	. if '$find(TEXT,"<") do  quit
	. . for YI=1:1  quit:$piece(TEXT,"|",YI,9999)=""  write $piece(TEXT,"|",YI),"<BR>",YCR  ;NUR TEXT ;only Text 
	. ;
	. for YI=1:1  quit:$piece(TEXT,"|",YI,9999)=""  write $piece(TEXT,"|",YI),YCR  ;HTML
	
	write YCR
	do FF^WWWW()
	write YCR
	write YCR
	quit
	
	
Header(pidTRN)
	;-------------------------------------------------------------------------------
	; History:
	; 15-Sep-2008	GRF		replace "I" with "loop"
	;-------------------------------------------------------------------------------
	new loop
	
	do Title(pidTRN)
	
	do START100^WWWTAB  ;STARTEN DER ÃœBERSCHRIFT ;launching the superscription 
	write "<THEAD>" 
	do NL^WWWTAB
	for loop=1:1 quit:$piece(UES,Y,loop,99)=""  do NHWZ^WWWTAB write $piece(UES,Y,loop),"&nbsp;" do EH^WWWTAB
	do EL^WWWTAB
	write "</THEAD>"
	quit
	
	
Title(pidTFR)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 02-Jun02911	GRF		SR17723: close Font tags
    ; 12-Dec-2007	PPP		SR15597: Created
	;-------------------------------------------------------------------------------
	new dteTransfer,idFromLoc,idRef,idToLoc,intStatus,objRecord,objTFR,strDescription
	
	set objTFR    = $get(^INTFR(0,pidTFR,1))
	set idFromLoc = $$$INTFRSendingLocation(objTFR)
	set idToLoc   = $$$INTFRReceivingLocation(objTFR)
	set idRef     = $$$INTFRDispatch(objTFR)
	set intStatus = $$$INTFRStatus(objTFR)
	
	do START1000^WWWTAB
	write YCR,"<TR>"
	write YCR,"<TD>"
	write "<NOBR>"
	
	;---------------------------------------
	
	write YCR,YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	write YCR,"<TR><TD ALIGN=CENTER COLSPAN=2>"
	write "<FONT SIZE=3> <B>"
	write $$^WWWFORMNAME("INTFR")_" "_$$^WWWTEXT(32503)    ; "Work Orders"
	write "</B> </FONT>"
	write "<BR>"
	write YCR,"<FONT SIZE=2>"
	write $$^WWWTEXT(33404)_" "_$$^WWWTEXT(388)_": "_idFromLoc_" ("_$piece($get(^WWW0121(0,0,idFromLoc,1)),Y,1)_")"  ; "Issued By Location"
	write "</FONT>"
	write YCR,"</TD></TR>"
	write YCR,"</TABLE>"
	
	;---------------------------------------
	
	write YCR,YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	write YCR,"<TR><TD WIDTH=33%>"
	write "<FONT SIZE=2><B>"
	write $$^WWWTEXT(32807)_" "_$$^WWWTEXT(388)_": "  ; "For Location"
	write idToLoc										; To location
	if idToLoc'="" write " ("_$piece($get(^WWW0121(0,0,idToLoc,1)),Y,1)_")"  ; Location Name
	write "<BR>"
	write $$^WWWTEXT(32247)_": "                      ; "Reference" (route?)
	write idRef
	write "</B></FONT>"
	write YCR,"</TD>"
	
	write YCR,"<TD ALIGN=CENTER VALIGN=BOTTOM WIDTH=33%>",YCR
	write "<FONT SIZE=2><B>"
	write " "
	write "<BR>"
	write $$^WWWTEXT(32781)_": "_intStatus            ; "Status"
	write "</B></FONT>"
	write YCR,"</TD>"
	
	write YCR,"<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>",YCR
	write "<FONT SIZE=2><B>"
	if pidTFR '= "" {
		write $$^WWWFELDNAME("INTFR","P",1)_": "_pidTFR
		write "<br />"_$$$Text(32168)_": "_$$^WWWDATE($$$INTFRDueDate(objTFR))  ; "Delivery Date"
	}
	write "</B></FONT>"
	write YCR
	write "</TD></TR>",YCR
	
	write "</TABLE>",YCR
	
	;---------------------------------------
	
	do EH^WWWTAB
	do EL^WWWTAB
	do STOP^WWWTAB
	
	quit
	
]]></Routine>
</Export>