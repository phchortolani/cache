<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSORTFAST" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSORTFAST(FILE,BIT)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FASTSORT ON FILES WITH 1/2 KEYS
	;	JOB ^WWWSORT("FILE",1)::1
	;
	; Inputs : 
	;	FILE		FILENAME
	;	BIT			=1 ;ALSO GENERATE BITMAP ;thus bit map 
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 02-Jun-2006	GRF		Dot Level
	; 17,09,2004	TYBD	Created
	;-------------------------------------------------------------------------------
	DO ^WWWVAR
	
	SET LC="ÜÄÖüäöß][\}{|abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$CHAR(128)_""""_" "
	SET UC="UAOUAOSUAOUAOABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	SET EXEC="^"_FILE_"s(YM)"
	
	KILL @EXEC
	IF $GET(BIT)=1 DO
	. SET EXEC="^"_FILE_"b(YM)"
	. KILL @EXEC
	. SET EXEC="^"_FILE_"bi(YM)"
	. KILL @EXEC
	
	SET MAXKEY=+$ORDER(^WWW002(0,FILE,""),-1)
	IF MAXKEY>2 QUIT
	
	DO ^WWWSORTKEY(FILE)
	SET FILE1  = "^"_FILE_"("_YM_",D)"
	SET FILE11 = "^"_FILE_"("_YM_",D,1)"
	SET FILE2  = "^"_FILE_"("_YM_",D,L)"
	SET FILE21 = "^"_FILE_"("_YM_",D,L,1)"
	SET D=""
	SET L="" 
	
	IF MAXKEY=1 SET D="" FOR  SET D=$ORDER(@FILE1) QUIT:D=""  DO
	. SET D1=$GET(@FILE11)
	. DO SET
	
	IF MAXKEY=2 SET D="" FOR  SET D=$ORDER(@FILE1) QUIT:D=""  DO
	. SET L=""
	. FOR  SET L=$ORDER(@FILE2) QUIT:L=""  DO
	. . SET D1=$GET(@FILE21)
	. . DO SET
	
	QUIT
	
SET ;SET SORT  ;table-mat 
	SET SKEY=""
	FOR  SET SKEY=$ORDER(YSKEY(SKEY)) QUIT:SKEY=""  DO
	. KILL YSFELD
	. SET YSDATEI="^"_FILE_"s("_YM_","_SKEY
	. FOR YI=1:1 SET YLFN=$PIECE(YSKEY(SKEY),",",YI) QUIT:YLFN=""  DO
	. . IF $EXTRACT(YLFN)="F" SET YSFELD(YI)=$PIECE(D1,Y,+$EXTRACT(YLFN,2,9))
	. . IF $EXTRACT(YLFN)="K" SET YSFELD(YI)=$PIECE(D_","_L,",",+$EXTRACT(YLFN,2,9))
	. . SET YSFELD(YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)
	. . IF YSFELD(YI)="" SET YSFELD(YI)=" "
	. . SET YSDATEI=YSDATEI_","_""""_YSFELD(YI)_""""
	. ;
	. IF MAXKEY=1 SET YSDATEI=YSDATEI_","_""""_D_""""_")"
	. IF MAXKEY=2 SET YSDATEI=YSDATEI_","_""""_D_""""_","_""""_L_""""_")"
	. SET @YSDATEI=""
	
	IF $GET(BIT)=1 IF $DATA(^WWW001B(0,FILE)) DO
	. IF MAXKEY=1 SET OK=$$^WWWBITSET(FILE,D,1,D1)       QUIT
	. IF MAXKEY=2 SET OK=$$^WWWBITSET(FILE,D_","_L,1,D1) QUIT
	
	QUIT
]]></Routine>
</Export>