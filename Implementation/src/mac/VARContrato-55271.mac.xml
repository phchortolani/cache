<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContrato" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContrato
#include COMSYS
#include COMConst
#include VARConst
 
OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Salva o grid com os executores, grava o término da vigência, dentre outros cálculos.
    ;
    ; History:
    ; 13-Oct-2009   Gustavo Fiuza   Changed  Proteção contra o preenchimento da data de publicação
    ; 16-Sep-2009	Gustavo Fiuza	Changed
    ; 25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new strStatus, criterioVigencia, dteAssinatura, dtePublicacao, intDuracao, dteInicio, 
		dteTermino, tipoPeriodo
		
	set strStatus = $$$OK
	set strStatus = $$$GRIDSave(pYKEY)
		
	//Cálculos da Vigência
	set criterioVigencia = $piece(pYFELD,Y,6)
	set intDuracao  = $piece(pYFELD,Y,16)
	set tipoPeriodo = $piece(pYFELD,Y,37)
	
	if (intDuracao < 0) set strStatus = "A duração não pode receber um valor negativo."
	if (intDuracao = 0) set strStatus = "A duração deve ser maior que 0."
		
	if ((tipoPeriodo = 1) && (intDuracao > 2190)) set strStatus = "A duração não pode ser maior do que 72 meses."
	if ((tipoPeriodo = 2) && (intDuracao > 72))   set strStatus = "A duração não pode ser maior do que 72 meses."
	if ((tipoPeriodo = 3) && (intDuracao > 6))    set strStatus = "A duração não pode ser maior do que 72 meses."
	
	;Protegendo contra o não preenchimento da Data de Publicação
	set dtePublicacao = $piece(pYFELD,Y,10)
	if (criterioVigencia = 2) && (dtePublicacao = "") set strStatus = "Quando o critério de vigência é igual à Data de Publicação, é necessário preencher o campo Data de Publicação." 
	
	if $$$ISOK(strStatus) {
	
		//Tipo 1 - Data de Assinatura		
		if (criterioVigencia = 1) {
			set dteAssinatura = $piece(pYFELD,Y,9)			
			
			// Periodo 1 - Dia(s)
			if tipoPeriodo = 1 { 
				set dteTermino = (dteAssinatura + intDuracao) - 1
			}
			// Periodo 2 - Mes(es)
			elseif (tipoPeriodo = 2) {
				set dteTermino = ($$AddMonth^COMUtilDate(dteAssinatura,intDuracao) - 1)
			}
			// Periodo 3 - Ano(s)
			elseif (tipoPeriodo = 3) {
				set dteTermino = ($$AddMonth^COMUtilDate(dteAssinatura,(intDuracao * 12)) - 1)
			}
	
			set $piece(pYFELD,Y,15) = dteAssinatura
			set $piece(pYFELD,Y,17) = dteTermino		
		}
		
		//Tipo 2 - Data de Publicação	
		elseif (criterioVigencia = 2) { 
			set dtePublicacao = $piece(pYFELD,Y,10)
	
			// Periodo 1 - Dia(s)
			if tipoPeriodo = 1 { 
				set dteTermino = (dtePublicacao + intDuracao) - 1
			}
			// Periodo 2 - Mes(es)
			elseif (tipoPeriodo = 2) {
				set dteTermino = ($$AddMonth^COMUtilDate(dtePublicacao,intDuracao) - 1)
			}
			// Periodo 3 - Ano(s)
			elseif (tipoPeriodo = 3) {
				set dteTermino = ($$AddMonth^COMUtilDate(dtePublicacao,(intDuracao * 12)) - 1)
			}
	
			set $piece(pYFELD,Y,15) = dtePublicacao
			set $piece(pYFELD,Y,17) = dteTermino
		}
		
		//Tipo 3 - Informado pelo Usuário
		elseif (criterioVigencia = 3) {
			set dteInicio  = $piece(pYFELD,Y,15)
			set dteTermino = $piece(pYFELD,Y,17)
				
			if (dteInicio = "") || (dteTermino = "") {
				set $piece(pYFELD,Y,16) = ""
			}
			elseif (dteTermino < dteInicio) {
				set strStatus = "A data informada de término de vigência não pode ser menor do que a data de início da vigência. Favor verificar."
			} 
			else {			
				set duracao = (dteTermino - dteInicio) + 1
				set $piece(pYFELD,Y,16) = duracao
			}
		}
	}
	
	if $$$ISOK(strStatus) {
		//Verifica se o contrato já foi cadastrado
		new idContrato, noContrato, noContratoAtual		
		
		set noContratoAtual = $piece(pYFELD,Y,38)		
		$$$Order2(^VARContrato,YM,idContrato)
			
			continue:(idContrato = pYKEY)
			
			set noContrato = $$GetNumeroContrato(idContrato)			
			quit:(noContrato = "")
			
			if (noContrato = noContratoAtual) {
				set strStatus = "Já existe um contrato cadastrado com o número "_noContratoAtual_". O código do contrato é o "_idContrato_"."
				quit
			}
					
		$$$End
	}
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}	
	
	quit
 
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Abre os grids dependendo da aba e tabelas auxiliares.
    ;
    ; History:
    ;	08-Mai-2012	Gustavo Fiuza	Inserida aba Histórico
    ;	28-Sep-2010 Gustavo Fiuza	Inserida aba Itens
    ;	25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:((pYKEY = "") || (pYKEY = "+"))
	
	if (YSEITE = 3) {  // 3-Aba Executores
		w "<br /><strong>&nbsp;Executores associados a este contrato</strong>"
		do LoadGridExecutores(pYKEY)
	}
	
	elseif (YSEITE = 5) {  // 5-Aba Manutenção
		w "<br /><strong>&nbsp;Equipamentos de manutenção associados a este contrato</strong>"
		do LoadGridManutencao(pYKEY,"VARContrato")
	}
	
	elseif (YSEITE = 6) {  // 6-Aba Notificações
		do OnAfterDataFields^VARContratoNotificacao(pYKEY)
	}
	
	elseif (YSEITE = 7) {  // 7-Aba Veículo
		w "<br /><strong>&nbsp;Veículos associados a este contrato</strong>"
		do LoadGridVeiculo(pYKEY,"VARContrato")
	}
	
	elseif (YSEITE = 8) {  // 8-Aba Itens
		w "<br /><font size=2><strong>&nbsp;Itens associados a este contrato</strong></font>"
		do LoadGridItens(pYKEY,"VARContrato")
		quit
	}
	
	elseif (YSEITE = 9) {  // 9-Aba Histórico
		//w "<br /><font size=2><strong>&nbsp;Histórico de alterações do contrato</strong></font>"
		do LoadTableHistorico(pYKEY)
		quit
	}
	
	do OnAfterDataFields^VARContratoProcesso(pYKEY)
	do OnAfterDataFields^VARContratoAditivo(pYKEY)
 
	quit
 
LoadGridExecutores(pYKEY) 
 	;-------------------------------------------------------------------------------
    ; Carrega o grid de executores do contrato.
    ; 
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
	new idForm,YAUSWAHL
	set idForm="VARContratoExecutor"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 350
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($piece(YVOR,Y,23)'=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "VARContrato"
		
	$$$GRIDStart(idForm,pYKEY)
	
	quit
 
 
LoadGridManutencao(pYKEY,pYFORM) 
 	;-------------------------------------------------------------------------------
    ; Carrega o grid de cadastro de equipamentos em contratos de manutenção.
    ; 
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
	new idForm,YAUSWAHL
	set idForm="VARContratoManutencao"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 350
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($piece(YVOR,Y,23)'=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = pYFORM
		
	$$$GRIDStart(idForm,pYKEY)
	
	quit
	
LoadGridVeiculo(pYKEY,pYFORM) 
 	;-------------------------------------------------------------------------------
    ; Carrega o grid de cadastro de veículos associados ao contrato.
    ; 
    ; History:
    ; 22-Sep-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
	new idForm,YAUSWAHL
	set idForm="VARContratoVeiculo"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 350
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($piece(YVOR,Y,23)'=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = pYFORM
		
	$$$GRIDStart(idForm,pYKEY)
	
	quit
	
LoadGridItens(pYKEY,pYFORM) 
 	;-------------------------------------------------------------------------------
    ; Carrega o grid de cadastro de itens do contrato
    ; 
    ; History:
    ; 28-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
	new idForm,YAUSWAHL
	set idForm="VARContratoItem"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 350
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($piece(YVOR,Y,23)'=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = pYFORM
		
	$$$GRIDStart(idForm,pYKEY)
	
	quit
 
OnBeforeDelete(pidContrato)
 	;-------------------------------------------------------------------------------
    ; Antes de deletar o registro pai, deleta as linhas das tabelas filhas para que
    ; não hajam registros órfãos nestas.
    ; 
    ; History:
    ; 28-Sep-2010	Gustavo Fiuza		Updated		Incluída tabela de itens
    ; 24-Nov-2009   Gustavo Fiuza		Updated		Incluída tabela de veículos
    ; 16-Sep-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
	new strStatus		
	set strStatus = $$$OK
	set strStatus = $$Transaction^COMTransaction("OnBeforeDeleteTxn^VARContrato("""_pidContrato_""")",$$$YES)
 
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	}
 
	quit strStatus
	
OnBeforeDeleteTxn(pidContrato)	
	new idExecutor, keyExecutor, idNotificacao, keyNotificacao, idAditivo, keyAditivo, idEquipamento,
		keyManutencao, idProcesso, keyProcesso, idVeiculo, keyVeiculo, idItem, keyItem
 
	//Apagando os executores do contrato
	$$$Order3(^VARContratoExecutor,YM,pidContrato,idExecutor)
		set keyExecutor = pidContrato_$$$COMMA_idExecutor
		set strStatus=$$KILL^COMUtils("VARContratoExecutor",keyExecutor)
		quit:$$$ISERR(strStatus)
	$$$End
	
	if $$$ISOK(strStatus) {	
		//Apagando as notificações do contrato
		$$$Order3(^VARContratoNotificacao,YM,pidContrato,idNotificacao)
			set keyNotificacao = pidContrato_$$$COMMA_idNotificacao
			set strStatus=$$KILL^COMUtils("VARContratoNotificacao",keyNotificacao)
			quit:$$$ISERR(strStatus)
		$$$End
	}
	
	if $$$ISOK(strStatus) {	
		//Apagando as alterações/aditivos do contrato
		$$$Order3(^VARContratoAditivo,YM,pidContrato,idAditivo)
			set keyAditivo = pidContrato_$$$COMMA_idAditivo
			set strStatus=$$KILL^COMUtils("VARContratoAditivo",keyAditivo)
			quit:$$$ISERR(strStatus)
		$$$End
	}
	
	if $$$ISOK(strStatus) {	
		//Apagando os equipamentos associados ao contrato
		$$$Order3(^VARContratoManutencao,YM,pidContrato,idEquipamento)
			set keyManutencao = pidContrato_$$$COMMA_idEquipamento
			set strStatus=$$KILL^COMUtils("VARContratoManutencao",keyManutencao)
			quit:$$$ISERR(strStatus)
		$$$End
	}
	
	if $$$ISOK(strStatus) {	
		//Apagando os processos associados ao contrato
		$$$Order3(^VARContratoProcesso,YM,pidContrato,idProcesso)
			set keyProcesso = pidContrato_$$$COMMA_idProcesso
			set strStatus=$$KILL^COMUtils("VARContratoProcesso",keyProcesso)
			quit:$$$ISERR(strStatus)
		$$$End
	}
	
	if $$$ISOK(strStatus) {	
		//Apagando os veículos associados ao contrato
		$$$Order3(^VARContratoVeiculo,YM,pidContrato,idVeiculo)
			set keyVeiculo = pidContrato_$$$COMMA_idVeiculo
			set strStatus=$$KILL^COMUtils("VARContratoVeiculo",keyVeiculo)
			quit:$$$ISERR(strStatus)
		$$$End
	}
	
	if $$$ISOK(strStatus) {
		//Apagando os itens associados ao contrato
		$$$Order3(^VARContratoItem,YM,pidContrato,idItem)
			set keyItem = pidContrato_$$$COMMA_idItem
			set strStatus=$$KILL^COMUtils("VARContratoItem",keyItem)
			quit:$$$ISERR(strStatus)
		$$$End
	}
	
	quit strStatus
 
OnBlurTipo(pYFELD="")
	;-------------------------------------------------------------------------------
    ; Carrega as categorias de contrato no OnBlur conforme o tipo de contrato
    ; selecionado.
    ;
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYFELD = "")
	
	new Tipo, idCategoria, descCategoria, tipoContrato
		
	kill ^COMTempList(YM,YBED,"VARContratoCategoria")
		
	set Tipo = $piece(pYFELD,Y,2)
	quit:(Tipo = "")
		
	set tipoContrato = $piece($get(^VARContrato(YM,YKEY,1)),Y,2)
	if (tipoContrato = Tipo) && (tipoContrato '= "") && ($piece(pYFELD,Y,3) '= "")  quit
 
	set %TXT(1) = ""	
	set %TXT(1)=%TXT(1)_"#YVARContratoD3~REMOVE"	
	
	$$$Order3(^VARContratoTipoCateg,YM,Tipo,idCategoria)
		set descCategoria = $$GetDescricaoCategoria^VARContratoTipoCateg(Tipo,idCategoria)
		set %TXT(1) = %TXT(1)_"#YVARContratoD3"_"~"_idCategoria_"~"_descCategoria ;_"~"_pos
		set ^COMTempList(YM,YBED,"VARContratoCategoria",idCategoria,1) = descCategoria				
	$$$End
 
	quit
	
OnBlurTipoManual
	;-------------------------------------------------------------------------------
    ; Carrega as categorias de contrato no OnBlur conforme o tipo de contrato
    ; selecionado.
    ;
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new dteHoje, objDATEN, Tipo, idCategoria, descCategoria, tipoContrato
 
	kill ^COMTempList(YM,YBED,"VARContratoCategoria")
 
	set dteHoje = $piece($h,",",1)
	set objDATEN = $get(^WWWDATEN(YM,dteHoje,YUSER,YFORM,"M",1))
 
	set Tipo = $piece(objDATEN,Y,1)	
	quit:(Tipo = "")
	
	set %TXT(1) = ""	
	set %TXT(1) = %TXT(1)_"#YVARContratoAlertaM2~REMOVE"
	
	$$$Order3(^VARContratoTipoCateg,YM,Tipo,idCategoria)
		set descCategoria = $$GetDescricaoCategoria^VARContratoTipoCateg(Tipo,idCategoria)
		set %TXT(1) = %TXT(1)_"#YVARContratoAlertaM2"_"~"_idCategoria_"~"_descCategoria ;_"~"_pos
		set ^COMTempList(YM,YBED,"VARContratoCategoria",idCategoria,1) = descCategoria				
	$$$End
 
	quit	
 
 
OnAfterPrimaryKey(pYKEY,pYFELD="")
	;-------------------------------------------------------------------------------
    ; 1. Insere mensagem em vermelho na tela caso exista um aditivo de prorrogação
    ; 2. Carrega a lista de categorias do contrato
    ;
    ; History:
    ; 13-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
			
	do LoadListaCategoria(pYFELD)
	
	if $$GetStatusContrato(pYKEY) = 1 {
		do ShowMensagemContrato(pYKEY)
	}
 
	quit
 
ShowMensagemContrato(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o contrato possui algum aditivo ou se está vencido, e caso afirmativo,
    ; mostra uma mensagem na tela para o usuário.
    ;
    ; History:
    ; 13-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new dteToday, dteVigencia, NoAditivo, objAditivo, tipoAditivo, blnAlerta, isProcessado
	
	set dteToday = $piece($horolog,",",1)
	set dteVigencia = $$GetVigenciaContrato(pYKEY)
 
	if ((dteVigencia < dteToday) && (dteVigencia '= "")){
		w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:5px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
		w "	<font color=red size=2>"
		w "		Atenção: o prazo de vigência deste contrato venceu em <strong>"_$zdate(dteVigencia,4)_"</strong>."
		w "	</font>"
		w "</div>"
	
	} elseif (dteVigencia >= dteToday) {	
		set blnAlerta = $$$NO
		set blnAlerta = $$blnExisteAditivoProrrog(pYKEY)
	
		if blnAlerta = $$$YES {
			w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:5px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
			w "	<font color=blue size=2>"
			w "		Atenção: este contrato possui aditivo(s) de prorrogação que extende(m) a sua vigência até <strong>"_$zdate(dteVigencia,4)_"</strong>."
			w "	</font>"
			w "</div>"
		}
	}
	
	quit
 
GetVigenciaContrato(pYKEY)
	;-------------------------------------------------------------------------------
    ; Calcula a data de vigência de um contrato, inclusive considerando a existência
    ; de aditivos do tipo 2-Prorrogação ou 4-Outras Alterações.
    ;
    ; History:
    ; 27-Nov-2009	Gustavo Fiuza	Updated
    ; 13-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if pYKEY = "" quit ""
	
	$$$VAR
	
	new objContrato, dteVigencia, NoAditivo, objAditivo, tipoAditivo, intDuracao, tipoPeriodo,
		isProcessado
	
	set objContrato = $get(^VARContrato(YM,pYKEY,1))
	set dteVigencia = $piece(objContrato,Y,17)
	
	if dteVigencia = "" quit ""
	
	if $$blnExisteAditivoProrrog(pYKEY) = $$$YES {
		
		$$$Order3(^VARContratoAditivo,YM,pYKEY,NoAditivo)
		
			set objAditivo  = $get(^VARContratoAditivo(YM,pYKEY,NoAditivo,1))
			
			set isProcessado = $piece(objAditivo,Y,13)
			continue:(isProcessado '= 1)
			
			;Tipo 2 = Aditivo de Prorrogação e 4 = Outras Alterações (inclui prorrogação, mas é *opcional*)
			set tipoAditivo = $piece(objAditivo,Y,2)
			continue:((tipoAditivo '= 2) && (tipoAditivo '= 4))
			
			set intDuracao  = ""
			set intDuracao  = $piece(objAditivo,Y,3)
			set tipoPeriodo = $piece(objAditivo,Y,12)
			
			;Como para o tipo 4 a prorrogação é opcional, se estiver vazio não entra no cálculo
			continue:((intDuracao = "") || (tipoPeriodo = ""))
			
			if tipoPeriodo = 1 {
				set dteVigencia = dteVigencia + intDuracao
			}
			elseif tipoPeriodo = 2 {
				set dteVigencia = $$AddMonth^COMUtilDate(dteVigencia,intDuracao)				
			}
			elseif tipoPeriodo = 3 {
				set dteVigencia = $$AddMonth^COMUtilDate(dteVigencia,(intDuracao * 12))
			}
				
		$$$End
	}
	
	quit dteVigencia
 
 
GetVigenciaContratoAditivo(pidContrato)
	;-------------------------------------------------------------------------------
    ; Retorna vazio caso não existam aditivos. Na função GetVigenciaContrato, caso
    ; não existam aditivos ela retorna a vigência normal do contrato.
    ;
    ; History:
    ; 17-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new dteVigenciaAditivos
	
	if $$blnExisteAditivoProrrog(pidContrato) = $$$NO {
		set dteVigenciaAditivos = ""  ;Não existem aditivos
	
	} else {
		set dteVigenciaAditivos = $$GetVigenciaContrato(pidContrato)
	}
	
	quit dteVigenciaAditivos
 
	
blnExisteAditivoProrrog(pidContrato)
	;-------------------------------------------------------------------------------
    ; Retorna se o contrato possui ou não um aditivo do tipo "Prorrogação" ou "Outras
    ; Alterações"
    ;
    ; History:
    ; 17-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if pidContrato = "" quit $$$NO
	
	new blnExisteAditivoProrrogacao, tipoAditivo, NoAditivo, isProcessado, intDuracao, tipoPeriodo
	
	set blnExisteAditivoProrrogacao = $$$NO
 
	$$$Order3(^VARContratoAditivo,YM,pidContrato,NoAditivo)
	
		set objAditivo  = $get(^VARContratoAditivo(YM,pidContrato,NoAditivo,1))		
		
		set isProcessado = $piece(objAditivo,Y,13)
		continue:(isProcessado '= 1)
				
		set tipoAditivo = $piece(objAditivo,Y,2)
		
		if ((tipoAditivo = 2) || (tipoAditivo = 4)) { ; 2-Prorrogação e 4-Outras Alterações
 
			set intDuracao  = $piece(objAditivo,Y,3)
			set tipoPeriodo = $piece(objAditivo,Y,12)		
			
			if ((intDuracao '= "") && (tipoPeriodo '= "")) { ;Tipo 4 a prorrogação é opcional
				set blnExisteAditivoProrrogacao = $$$YES
				quit
			}
		}
		
	$$$End
 
	quit blnExisteAditivoProrrogacao
 
GetDuracaoContrato(pidContrato)
	;-------------------------------------------------------------------------------
    ; Calcula a duração total do contrato, somando o contrato base e seus respectivos
    ; aditivos de prorrogação.
    ;
    ; History:
    ; 16-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if pidContrato = "" quit 0
	
	new dteInicioVigencia, dteTerminoVigencia, intDuracaoContrato
	
	set dteInicioVigencia  = $piece($get(^VARContrato(YM,pidContrato,1)),Y,15)
	set dteTerminoVigencia = $$GetVigenciaContrato(pidContrato)
	
	set intDuracaoContrato = dteTerminoVigencia - dteInicioVigencia
 
	quit intDuracaoContrato
 
 
LoadListaCategoria(pYFELD="")
	;-------------------------------------------------------------------------------
    ; Monta ao carregar o form a lista temporária com as possíveis categorias
    ;
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYFELD = "")
	
	new Tipo, idCategoria, descCategoria
		
	kill ^COMTempList(YM,YBED,"VARContratoCategoria")
		
	set Tipo = $piece(pYFELD,Y,2)
	quit:(Tipo = "")
 
	set %TXT(1) = ""	
	set %TXT(1)=%TXT(1)_"#YVARContratoD3~REMOVE"	
	
	$$$Order3(^VARContratoTipoCateg,YM,Tipo,idCategoria)
		set descCategoria = $$GetDescricaoCategoria^VARContratoTipoCateg(Tipo,idCategoria)
		set %TXT(1) = %TXT(1)_"#YVARContratoD3"_"~"_idCategoria_"~"_descCategoria ;_"~"_pos
		set ^COMTempList(YM,YBED,"VARContratoCategoria",idCategoria,1) = descCategoria				
	$$$End
	
 
	quit
 
OnBlurCalculaVigencia(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Calcula a data de término da vigência do contrato.
    ;
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	
	/*
	Rotina desatualizada - agora o cálculo é feito na OnBeforeSave^VARContrato.
	
		
	new criterioVigencia, dteAssinatura, qtyDuracao, dteTermino, dtePublicacao
	
	set criterioVigencia = $piece(pYFELD,Y,6)
	if (criterioVigencia = "") {
		set %TXT(1) = "#YVARContratoD15~"
		set %TXT(1) =  %TXT(1)_"#YVARContratoD17~"
		quit
	}
	 
	if (($piece(pYFELD,Y,16) <= 0) && ($piece(pYFELD,Y,16) '= "")) {
		set %TXT(1) = "#YVARContratoD16~"
		w "alert('Atenção: A duração não pode ser menor ou igual a 0.');"
		quit
	}
	
	
	;1-Data de Assinatura	
	if (criterioVigencia = 1) {
		set dteAssinatura = $piece(pYFELD,Y,9) 
		set qtyDuracao    = $piece(pYFELD,Y,16)
		
		if (dteAssinatura = "") || (qtyDuracao = "") {
			set %TXT(1) = "#YVARContratoD17~"
		}
		else {
			set dteTermino = (dteAssinatura + qtyDuracao) - 1
			set %TXT(1) = "#YVARContratoD15~"_$zdate(dteAssinatura,4)
			set %TXT(1) = %TXT(1)_ "#YVARContratoD17~"_$zdate(dteTermino,4)
		}
	}
	
	;2-Data de Publicação
	elseif (criterioVigencia = 2) {
		set dtePublicacao = $piece(pYFELD,Y,10)
		set qtyDuracao    = $piece(pYFELD,Y,16)
		
		if (dtePublicacao = "") || (qtyDuracao = "") {
			set %TXT(1) = "#YVARContratoD17~"
		}
		else {
			set dteTermino = (dtePublicacao + qtyDuracao) - 1
			set %TXT(1) = "#YVARContratoD15~"_$zdate(dtePublicacao,4)
			set %TXT(1) = %TXT(1)_"#YVARContratoD17~"_$zdate(dteTermino,4)
		}		
	}
	
	;3-Informado pelo Usuário
	elseif (criterioVigencia = 3) { 
 
		set dteInicioVigencia  = $piece(pYFELD,Y,15)
		set dteTerminoVigencia = $piece(pYFELD,Y,17)
		
		set duracao = ""
		
		if ((dteInicioVigencia '= "") && (dteTerminoVigencia '= "")) {
			set duracao = (dteTerminoVigencia - dteInicioVigencia) + 1		
		
		} else {			
			set %TXT(1) = "#YVARContratoD15~"
			set %TXT(1) = %TXT(1)_"#YVARContratoD16~"
			set %TXT(1) = %TXT(1)_"#YVARContratoD17~"
		}
		
		if ((duracao <= 0) && (dteInicioVigencia '= "") && (dteTerminoVigencia '= "")) {
			set %TXT(1) = "#YVARContratoD16~"
			set %TXT(1) = %TXT(1)_"#YVARContratoD17~"
			w "alert('Atenção: a data de início da vigência tem que ser uma data anterior à data de término da vigência.');"
		
		} else {		
			set %TXT(1) = "#YVARContratoD16~"_duracao
		}
	}
	*/
	
	quit
	
OnBlurContratada(pYFELD)
	;-------------------------------------------------------------------------------
    ; Carrega o nome do fornecedor e o CNPJ no OnBlur.
    ;
    ; History:
    ; 25-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYFELD = "")
	
	new idFornecedor, CNPJ
	set idFornecedor = $piece(pYFELD,Y,7)
	
	set CNPJ 	 = $$getCNPJ^VARINLIEF(idFornecedor)
	set nome 	 = $$SQLGetDescFornecedor^VARSQL(idFornecedor)
	set tel		 = $$SQLGetTelFornecedor^VARSQL(idFornecedor)
	set contato  = $$SQLGetContatoFornecedor^VARSQL(idFornecedor)
	set fax  	 = $$SQLGetFaxFornecedor^VARSQL(idFornecedor)
	set email 	 = $$SQLGetEmailFornecedor^VARSQL(idFornecedor)
	set cel 	 = $$SQLGetCelFornecedor^VARSQL(idFornecedor)
	
	set %TXT(1) = "#YVARContratoD8~"_CNPJ
	set %TXT(1) = %TXT(1)_"#YVARContratoD29~"_nome
	set %TXT(1) = %TXT(1)_"#YVARContratoD34~"_tel
	set %TXT(1) = %TXT(1)_"#YVARContratoD33~"_contato
	set %TXT(1) = %TXT(1)_"#YVARContratoD39~"_fax
	set %TXT(1) = %TXT(1)_"#YVARContratoD36~"_email
	set %TXT(1) = %TXT(1)_"#YVARContratoD35~"_cel
 
	quit
	
	
VerificaExistenciaCategorias(pYFELD)
	;-------------------------------------------------------------------------------
    ; Verifica se um determinado tipo de contrato possui ou não categorias dentro
    ; dele. Se possuir, o campo é obrigatório, caso contrátio, somente leitura (regra
    ; de customização do campo Categoria).
    ;
    ; History:
    ; 01-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new tipo	
	set tipo = $piece(pYFELD,Y,2)
	if tipo = "" quit $$$YES
	
	if ($data(^VARContratoTipoCateg(YM,tipo)) > 0){
		quit $$$NO
	
	} else {	
		quit $$$YES
	}
 
 
HideAbaManutencao(pYKEY)
	;-------------------------------------------------------------------------------
    ; Esconde a aba "Manutenção" caso o tipo de contrato não seja 1 e a categoria
    ; 6 - Obras e Serviços de Engenharia
    ;
    ; History:
    ; 31-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if (pYKEY = "") quit $$$YES
	
	new TipoContrato, Categoria
	
	set TipoContrato = $$GetTipoContrato^VARContrato(pYKEY)
	set Categoria    = $$GetCategoria^VARContrato(pYKEY)
	
	if (TipoContrato = 1) && (Categoria = 6) { ;1=Contrato 11=Obras e Serviços de Engenharia
		quit $$$NO
	}
 
	quit $$$YES
 
HideAbaEngenharia(pYKEY)
	;-------------------------------------------------------------------------------
    ; Esconde a aba "Engenharia" caso o tipo de contrato não seja 1 e a categoria
    ; 11 - Obras e Serviços de Engenharia
    ;
    ; History:
    ; 31-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if (pYKEY = "") quit $$$YES
	
	new TipoContrato, Categoria
	
	set TipoContrato = $$GetTipoContrato^VARContrato(pYKEY)
	set Categoria    = $$GetCategoria^VARContrato(pYKEY)
	
	if (TipoContrato = 1) && (Categoria = 11) { ;1=Contrato 11=Obras e Serviços de Engenharia
		quit $$$NO
	}
 
	quit $$$YES
	
HideAbaVeiculo(pYKEY)
	;-------------------------------------------------------------------------------
    ; Esconde a aba "Veículo" caso o tipo de contrato não seja 1 e a categoria
    ; 8 - Serviços de Manutenção de Veículos
    ;
    ; History:
    ; 22-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if (pYKEY = "") quit $$$YES
	
	new TipoContrato, Categoria
	
	set TipoContrato = $$GetTipoContrato^VARContrato(pYKEY)
	set Categoria    = $$GetCategoria^VARContrato(pYKEY)
	
	if (TipoContrato = 1) && (Categoria = 8) { ;1=Contrato 8=Serviços de Manutenção de Veículos
		quit $$$NO
	}
 
	quit $$$YES	
	
GetNumeroContrato(pYKEY)
	if (pYKEY = "") quit ""
	new CodContrato	
	set CodContrato = $$$KEY1(pYKEY)
	
	quit $piece($get(^VARContrato(YM,CodContrato,1)),Y,38)
 
 
GetObjetoContrato(pYKEY)
	if (pYKEY = "") quit ""
	new CodContrato	
	set CodContrato = $$$KEY1(pYKEY)
	
	quit $piece($get(^VARContrato(YM,CodContrato,1)),Y,4)
	
GetDescTipoContrato(pYKEY)	
	if (pYKEY = "") quit ""
	
	new CodContrato, Tipo, descTipo
	set CodContrato = $$$KEY1(pYKEY)
	
	set Tipo = $piece($get(^VARContrato(YM,CodContrato,1)),Y,2)
	set descTipo = $piece($get(^VARContratoTipo(YM,Tipo,1)),Y,1)
	
	quit descTipo
	
GetDescCategoriaContrato(pYKEY)	
	if (pYKEY = "") quit ""
	
	new CodContrato, Tipo, Categoria, descCategoria
	set CodContrato = $$$KEY1(pYKEY)
 
	set Tipo = $piece($get(^VARContrato(YM,CodContrato,1)),Y,2)
	set Categoria = $piece($get(^VARContrato(YM,CodContrato,1)),Y,3)
	
	if Categoria '= "" {
		set descCategoria = $piece($get(^VARContratoTipoCateg(YM,Tipo,Categoria,1)),Y,1)
	} else {
		set descCategoria = ""
	}
	
	quit descCategoria
 
GetTipoContrato(pYKEY)
	if (pYKEY = "") quit ""
	quit $piece($get(^VARContrato(YM,pYKEY,1)),Y,2)
	
GetCategoria(pYKEY)
	if (pYKEY = "") quit ""
	quit $piece($get(^VARContrato(YM,pYKEY,1)),Y,3)
	
GetStatusContrato(pYKEY)
	if (pYKEY = "") quit ""
	quit $piece($get(^VARContrato(YM,pYKEY,1)),Y,1)
 
 
CanFirmar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o status do contrato admite ser firmado.
    ;
    ; History:
    ; 28-Sep-2009	Gustavo Fiuza	Compara com o total dos itens, se tiverem itens no contrato.
    ; 13-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
 	new statusContrato, objContrato, criterioVigencia, inicioVigencia, terminoVigencia, 
 		vlueContratoManual, vlueContratoAjust, vlueContratoItens, vlueContrato
 	
 	set objContrato = $get(^VARContrato(YM,pYKEY,1))
 	
	set statusContrato 	 = $$$VARContratoCelebrado(objContrato)
	set criterioVigencia = $$$VARContratoCriteriodeVigencia(objContrato)	
	set inicioVigencia   = $$$VARContratoDatadeIniciodaVigencia(objContrato)
	set terminoVigencia  = $$$VARContratoDatadeTerminodaVigencia(objContrato)
	
	set vlueContratoItens  = $$GetValorTotalItens(pYKEY)
	
	set YQ = $$$YQEnable
	
	if (statusContrato = $$$YES) {
		set YQ = $$$YQDisable("Este contrato já está celebrado.")
		quit
	
	} 
	
	if (criterioVigencia = 3) {
		if ((inicioVigencia = "") || (terminoVigencia = "")) {
			set YQ = $$$YQDisable("Quando o critério de vigência for do tipo '3 - Informado pelo Usuário', é necessário informar os campos Data de Início de Vigência e Data de Término da Vigência antes de marcar o contrato como celebrado.")
			quit
		}	
	} 
	
	if (vlueContratoItens > 0) {
		
		set vlueContratoManual = $$$VARContratoValorTotal(objContrato)	
		set vlueContratoAjust  = $$GetValorTotalAjustado(pYKEY)
	
		if (vlueContratoAjust '= "") {
			set vlueContrato = vlueContratoAjust		
		} else {
			set vlueContrato = vlueContratoManual
		}
		
		if (vlueContrato '= vlueContratoItens) {
			set YQ = $$$YQDisable("O valor total do contrato não confere com o valor total dos itens.")
			quit
		}
	}
	
	quit
	
FirmarContrato(pYKEY)
	;-------------------------------------------------------------------------------
    ; Seta o status do contrato para Firmado
    ;
    ; History:
    ; 13-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new strStatus, objContrato
	set strStatus = $$$OK
	
	set objContrato = $get(^VARContrato(YM,pYKEY,1))			
	
	if $piece(objContrato,Y,1) '= 1 {
	
		set $piece(objContrato,Y,1) = 1
		
		set strStatus = $$$Save("VARContrato",pYKEY,objContrato,1)
	
		if $$$ISERR(strStatus) { 
			$$$StartScript()
			w "alert('Não foi possível firmar o contrato. Favor verificar.');"
			$$$EndScript()
		}	
		
		do GoToForm^COMUtilForm("VARContrato",pYKEY)	
	}
		
	quit
	
	
CanRevisar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o contrato admite ser revisado
    ;
    ; History:
    ; 13-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
 	new statusContrato	
	set statusContrato = $piece($get(^VARContrato(YM,pYKEY,1)),Y,1)
	
	if (statusContrato = "") {
		set YQ = $$$YQDisable("Este contrato já está em modo de edição.")
	} else { 
		set YQ = $$$YQEnable
	}
	
	quit
	
RevisarContrato(pYKEY)
	;-------------------------------------------------------------------------------
    ; Seta o status do contrato vazio (não firmado)
    ;
    ; History:
    ; 13-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new strStatus, objContrato
	set strStatus = $$$OK
	
	set objContrato = $get(^VARContrato(YM,pYKEY,1))			
	
	if $piece(objContrato,Y,1) = 1 {
	
		set $piece(objContrato,Y,1) = ""
		
		set strStatus = $$$Save("VARContrato",pYKEY,objContrato,1)
	
		if $$$ISERR(strStatus) { 
			$$$StartScript()
			w "alert('Não é possível revisar o contrato. Favor verificar.');"
			$$$EndScript()
		}	
		
		do GoToForm^COMU]]><![CDATA[tilForm("VARContrato",pYKEY)	
	}
		
	quit
	
	
CanInsertAditivo(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o aditivo pode ser firmado
    ;
    ; History:
    ; 16-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
 	new statusContrato	
	set statusContrato = $piece($get(^VARContrato(YM,pYKEY,1)),Y,1)
	
	if (statusContrato '= 1) {
		set YQ = $$$YQDisable("Só é possível fazer alterações em contratos que estejam celebrados.")
	} else { 
		set YQ = $$$YQEnable
	}
	
	quit
	
OnAfterSaveSelecao
	new codContrato
	set codContrato = $get(VORG(1))
	do GoToForm^COMUtilForm("VARContratoAditivo",codContrato)
 
	quit
	
OnAfterSaveSelecao2
	new codContrato
	set codContrato = $get(VORG(1))
	do GoToForm^COMUtilForm("VARContratoNotificacao",codContrato)
 
	quit
	
OnBeforeButtonLine(pYKEY)
	set ^CacheTempEvent(YUCI,"VARContratoManutencao","Format") = 1
	set ^CacheTempEvent(YUCI,"VARContratoVeiculo","Format") = 1
 
	if ($get(^COMTempList(YM,YUSER,"VARJustVisitedExecutores",1,1)) = 1) {
		if ((pYKEY '= "") && (pYKEY '= "+")) {
			set YSEITE = 3 //Aba de Executores
		}
		kill ^COMTempList(YM,YUSER,"VARJustVisitedExecutores",1,1)
	}
	
	quit
	
GetValorMensalAjustado(pYKEY)	
	quit:(pYKEY = "") ""
	
	new idAlteracao, objAlteracao, isProcessed, fltValorMensalAjustado
	
	set fltValorMensalAjustado = ""
	
	$$$Order3(^VARContratoAditivo,YM,pYKEY,idAlteracao)
		
		set objAlteracao = $get(^VARContratoAditivo(YM,pYKEY,idAlteracao,1))
		set isProcessed = $piece(objAlteracao,Y,13)
		
		continue:(isProcessed '= 1)
		
		if $piece(objAlteracao,Y,16) '= "" {
			set fltValorMensalAjustado = $piece(objAlteracao,Y,16)
		}
	
	$$$End
	
	quit fltValorMensalAjustado
	
GetValorTotalAjustado(pYKEY)	
	quit:(pYKEY = "") ""
	
	new idAlteracao, objAlteracao, isProcessed, fltValorTotalAjustado
	
	set fltValorTotalAjustado = ""
	
	$$$Order3(^VARContratoAditivo,YM,pYKEY,idAlteracao)
		
		set objAlteracao = $get(^VARContratoAditivo(YM,pYKEY,idAlteracao,1))
		set isProcessed = $piece(objAlteracao,Y,13)
		
		continue:(isProcessed '= 1)
		
		if $piece(objAlteracao,Y,16) '= "" {
			set fltValorTotalAjustado = $piece(objAlteracao,Y,17)
		}
	
	$$$End
	
	quit fltValorTotalAjustado
	
GetValorTotalItens(pYKEY)	
	;-------------------------------------------------------------------------------
    ; Calcula a somatória de cada valor total de itens no contrato.
    ;
    ; History:
    ; 28-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "") ""
	
	new idItem, objItem, vlueTotalItem, vlueTotalContrato	
	set vlueTotalContrato = 0

	$$$Order3(^VARContratoItem,YM,pYKEY,idItem)
		
		set objItem = $get(^VARContratoItem(YM,pYKEY,idItem,1))
		quit:(objItem = "")
		
		set vlueTotalItem	  = $piece(objItem,Y,6)
		set vlueTotalContrato = vlueTotalContrato + vlueTotalItem
	
	$$$End
	
	quit $normalize(vlueTotalContrato,4)
	
	
LoadTableHistorico(pYKEY)
	;-------------------------------------------------------------------------------
    ; Exibe o histórico de alterações do contrato.
    ;
    ; History:
    ; 18-Mai-2012	Gustavo Fiuza	Direção setada para descendente
    ; 08-Mai-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
    quit:(pYKEY = "")
    
    set VORG(1) = "VARContrato"							;Classe
    set VORG(5) = $$Replace^COMUtilStr(pYKEY,"/",",")	;Chave
    set VORG(6) = $$$YES								;HideFieldID
    set VORG(7) = -1									;Direction (-1 = descendente)
    
    do ShowResults^VARWWWPRO()
    
    quit
]]></Routine>
</Export>