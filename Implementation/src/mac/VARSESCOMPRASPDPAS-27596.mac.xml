<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASPDPAS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASPDPAS
#include COMSYS
#include COMConst

OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Salva o grid de itens.
    ;
    ; History:
    ; 	29-Apr-2011 Gustavo Fiuza	Updated
    ;	29-Mar-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new strStatus, statusProcesso
	
	set strStatus = $$$OK
	
	do OnBeforeSave^VARSESCOMPRAS(pYKEY)
	
	set strStatus = $$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	
	set statusProcesso = $$GetPosicao^VARSESCOMPRAS(pYKEY)
	
	;PositionHere -> 1 - Aguardando Inserir Itens
	if (statusProcesso = 1) {
		if $data(^VARSESCOMPRASLinha(YM,pYKEY)) {
			;PositionHere -> 4 - Aguardando Registrar Publicação
			set $piece(YFELD,Y,7) = 4
		}
	}
	
	if $$$ISOK(strStatus) {
		if ( (pYKEY = 0) || (pYKEY = "") || (pYKEY = "+") ) {
			do GerarChavePDPAS(pYKEY,YFELD)
		}
	}

	quit

OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Abre o grid para inserção de itens.
    ;
    ; History:
    ; 29-Mar-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:((pYKEY = "") || (pYKEY = "+"))
	
	do LoadGridItens(pYKEY)
	
	quit

OnBeforeButtonLine(pYKEY)
	;-------------------------------------------------------------------------------
    ; Abre o grid para inserção de itens.
    ;
    ; History:
    ; 27-May-2011	Gustavo Fiuza	Updated: não incluir locais interditados na lista
    ; 29-Mar-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new codUnidadeSaude, idLocal, descLocal
	
	set codUnidadeSaude = $piece($get(^WWW0121(YM,YM,YLOCATION,1)),Y,64)
	
	kill ^COMTempList(YM,YBED,"VARSESLOCAISPDPAS")
	
 		&sql(DECLARE LocaisUnidade CURSOR FOR

			 SELECT Location, LocationName
			   INTO :idLocal, :descLocal					  
			   FROM WWW0121
			  WHERE FREE5 = :codUnidadeSaude
			    AND $UPPER(StorageLocn) = 1
			    AND $PIECE(Location,".",2) != 99 --Não incluir locais interditados
		)

		&sql(OPEN LocaisUnidade )
		&sql(FETCH LocaisUnidade )
		
		while ('SQLCODE) {
			
			set ^COMTempList(YM,YBED,"VARSESLOCAISPDPAS",idLocal,1) = descLocal
				
	 		&sql(FETCH LocaisUnidade )
 		}

 		&sql(CLOSE LocaisUnidade )
    
    quit
	
	
LoadGridItens(pYKEY) 
 	;-------------------------------------------------------------------------------
    ; Carrega o grid.
    ; 
    ; History:
    ; 29-Mar-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
	new idForm,YAUSWAHL
	set idForm="VARSESCOMPRASPDPASLinha"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 275
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($piece(YVOR,Y,23)'=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "VARSESCOMPRASPDPAS"
		
	$$$GRIDStart(idForm,pYKEY)
	
	quit
	
GerarChavePDPAS(pYKEY, pYFELD)	
 	;-------------------------------------------------------------------------------
    ; Gera o código PDPAS  de acordo com contador definido.
    ; 
    ; History:
    ; 29-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------			
	new strStatus, proxCodigo, proxCodigoStr, novoCodigo, codUnidadeSaude
	set strStatus = $$$OK
	
	;Resgata o código da unidade de saúde para definir no contador
	set codUnidadeSaude = $piece($get(^WWW0121(YM,YM,YLOCATION,1)),Y,64)
	
	if (codUnidadeSaude = "") {
		set strStatus = "O processo de compra não pôde ser cadastrado pois falta o código da unidade de saúde."
	
	} else {
		set proxCodigo = $increment(^VARSESCOMPRASPDPASContador(codUnidadeSaude), 1)
	
		;Preenche com zeros a esquerda
		set proxCodigoStr = $$FillChar(proxCodigo,6,"0")
		
		set novoCodigo = codUnidadeSaude_"-"_proxCodigoStr
	}
	
	if $$$ISOK(strStatus) {
		if $data(^VARSESCOMPRAS(YM,novoCodigo)){
			set strStatus = "O processo de compra "_novoCodigo_" não pôde ser cadastrado."
	
		} else {
			;Insere os dados na Classe(Classe,Chave Primária,Valores,Always Save)
			set strStatus = $$$Save("VARSESCOMPRAS",novoCodigo,pYFELD,1)
		}
	}	
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ^WWWINFO(strStatus)		
	
	} else {
		$$$StartScript()
		write "BEARB('Salvo',1); "
		$$$EndScript()
		
		do RedirectForm^COMUtilForm("VARSESCOMPRASPDPAS",novoCodigo,YBACK,YPARA,YSEITE)
			
		set Q = $$$QDontSave
	}

	quit
	

FillChar(pstrString,pintLength,pstrFillChar)
	;-------------------------------------------------------------------------------
	; Fills a string with a certain amount of a specific character.
	;
	; Parameters:
	;		pstrString		:	The string to fill
	;		pintLength		:	The number of occurrences to create
	;		pstrFillChar	:	The char(s) to fill with
	;
	; Returns:
	;		The filled string
	;		
	; History:
	; 02-Feb-2004		shobby		Creation (Adapted from WRS LedOnlineBanking)
	;-------------------------------------------------------------------------------
 	;
	quit $translate($$RightPad(pstrString,pintLength)," ",pstrFillChar)
	

RightPad(pstrString,pintLength)
	;-------------------------------------------------------------------------------
	; Right pad the string with spaces
	;
	; Parameters:
	;		pstrString	:	The string to pad
	;		pintLength	:	The amount of characters to pad out to
	;
	; Returns:
	; 		The padded string
	;
	; History:
	; 02-Feb-2004		shobby		Creation (Adapted from WRS LedOnlineBanking)
	;-------------------------------------------------------------------------------
 	;
	quit $justify($extract(pstrString,1,pintLength),pintLength)
	
	
isGestorPDPAS(pstrPerfilUsuario)
 	;-------------------------------------------------------------------------------
    ; Verifica se o perfil do usuário contém o perfil de gestor PDPAS 
    ; 
    ; History:
    ; 04-May-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------			
	quit:(pstrPerfilUsuario = "") ""

	new blnGestorPDPAS, perfilGestorPDPAS, cnt, perfil
	
	set blnGestorPDPAS    = $$$NO	
	set perfilGestorPDPAS = 89 ;89-Gestor PDPAS

	for cnt = 1:1:$length(pstrPerfilUsuario,";") {
			set perfil = $piece(pstrPerfilUsuario,";",cnt)
			continue:(perfil = "")
			
			if (perfil = perfilGestorPDPAS) {
				set blnGestorPDPAS = $$$YES
				quit
			}			
	}
			
	quit blnGestorPDPAS

GetLinhaCompraPDPASFromProduto(pidCompra,pidProduto)	
	new idCompraLinha
	set value = ""
	$$$Order5(^VARSESCOMPRASLinhas,0,1,pidProduto,pidCompra,idCompraLinha)
		
		set value = idCompraLinha
			
	$$$End
 
	quit value
]]></Routine>
</Export>