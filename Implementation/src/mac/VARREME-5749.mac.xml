<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARREME" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[VARREME
#include COMSYS
 
 
OnBeforeFormConstruction
	if $piece($get(^VARTempTabelaREME(YUSER)),Y,5) = 0 {
		set $piece(^VARTempTabelaREME(YUSER),Y,5) = 1
	}
 
	quit
 
OnAfterSave()
	new optionAB, optionCE, optionMC, optionHO, optionME, ordenacao, idGrupo, categoria
		
	set optionAB	= +$get(VORG(1))
	set optionCE	= +$get(VORG(2))
	set optionMC	= +$get(VORG(3))
	set optionHO	= +$get(VORG(4))
	set ordenacao	= +$get(VORG(5))
	set idGrupo		= $get(VORG(6))
	set optionME	= +$get(VORG(7))
	set categoria	= +$get(VORG(8))
	
	set ^VARTempTabelaREME(YUSER) = optionAB_Y_optionCE_Y_optionMC_Y_optionHO_Y_ordenacao_Y_idGrupo_Y_optionME_Y_categoria

	do VARLogAfterSave^VARUtil(YBED,YFORM)	
	do GoToForm^COMUtilForm("VARREME",YUSER)
 	
 	quit 	
 	
OnAfterDataFields()

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
 
 	new optionAB, optionCE, optionMC, optionHO, ordenacao, idGrupo, categoria
 	
 	set optionAB = $piece($get(^VARTempTabelaREME(YUSER)),Y,1)	
	if +optionAB = 0 {
		set optionAB = +$get(VORG(1))
	}	
	
	set optionCE = $piece($get(^VARTempTabelaREME(YUSER)),Y,2)	
	if +optionCE = 0 {
		set optionCE = +$get(VORG(2))
	}
	
	set optionMC = $piece($get(^VARTempTabelaREME(YUSER)),Y,3)	
	if +optionMC = 0 {
		set optionMC = +$get(VORG(3))
	}
	
	set optionHO = $piece($get(^VARTempTabelaREME(YUSER)),Y,4)	
	if +optionHO = 0 {
		set optionHO = +$get(VORG(4))
	}
 
	set ordenacao = $piece($get(^VARTempTabelaREME(YUSER)),Y,5)	
	if +ordenacao = 0 {
		set ordenacao = +$get(VORG(5))
	}
	
	set idGrupo = $piece($get(^VARTempTabelaREME(YUSER)),Y,6)	
	if idGrupo = "" {
		set idGrupo = $get(VORG(6))
	}
 	
	set optionME = $piece($get(^VARTempTabelaREME(YUSER)),Y,7)
	if +optionME = 0 {
		set optionME = +$get(VORG(7))
	}
	
	set categoria = $piece($get(^VARTempTabelaREME(YUSER)),Y,8)
	if +categoria = 0 {
		set categoria = +$get(VORG(8))
	}
 
	set ^VARTempTabelaREME(YUSER) = optionAB_Y_optionCE_Y_optionMC_Y_optionHO_Y_ordenacao_Y_idGrupo_Y_optionME_Y_categoria
 	
 	do MontaTabelaREME(optionAB,optionCE,optionMC,optionHO,ordenacao,idGrupo,optionME,categoria)
	
	quit
	
MontaWhere(poptionAB="0",poptionCE="0",poptionMC="0",poptionHO="0",pidGrupo,poptionME="0",pcategoria)
	set strWhere = ""
	if ((+poptionAB) || (+poptionCE) || (+poptionMC) || (+poptionHO) || (+poptionME) || (pidGrupo'="") || (pcategoria'="")) {
		if ((+poptionAB) || (+poptionCE) || (+poptionMC) || (+poptionHO) || (+poptionME)){
			set strWhere = strWhere_ " AND( "
			if +poptionAB {
				if (strWhere'=" AND( ") set strWhere = strWhere_"OR "
				set strWhere = strWhere_"NivelDeAtencao like '%1%' "
			}
			if +poptionCE {
				if (strWhere'=" AND( ") set strWhere = strWhere_"OR "
				set strWhere = strWhere_"NivelDeAtencao like '%2%' "
			}
			if +poptionMC {
				if (strWhere'=" AND( ") set strWhere = strWhere_"OR "
				set strWhere = strWhere_"NivelDeAtencao like '%3%' "
			}
			if +poptionHO {
				if (strWhere'=" AND( ") set strWhere = strWhere_"OR "
				set strWhere = strWhere_"NivelDeAtencao like '%4%' "
			}
			if +poptionME {
				if (strWhere'=" AND( ") set strWhere = strWhere_"OR "
				set strWhere = strWhere_"NivelDeAtencao like '%5%' "
			}
			set strWhere = strWhere_") "
		}
		if (pidGrupo'="") {
			set strWhere = strWhere_" AND Grupo LIKE '"_pidGrupo_"%' "
		}
		if (pcategoria'="") {
			set strWhere = strWhere_" AND Categoria = '"_pcategoria_"' "
		}
	}
	
	quit strWhere
	
	
MontaTabelaREME(poptionAB="0",poptionCE="0",poptionMC="0",poptionHO="0",pordenacao,pidGrupo,poptionME="0",categoria)
	new queryData, lstHeader, strSQL, idItem, descricao, unidade, grupo, count, optionME,
		optionAB, optionCE, optionMC, optionHO, strWhere
	
	set queryData = ##class(%ResultSet).%New()  ;Create Result Set Object
	set strSQL = "SELECT * FROM Report.VARREME WHERE company = 0 "
	set strSQL = strSQL_$$MontaWhere(poptionAB,poptionCE,poptionMC,poptionHO,pidGrupo,poptionME,categoria)

	if pordenacao = 2 {
		set strSQL = strSQL_" ORDER BY Descricao"
	
	} else {
		set strSQL = strSQL_" ORDER BY Codigo"
	}
	
	do queryData.Prepare(strSQL) ;Prepare Query
	do queryData.Execute()  	;Execute Query
 
	set count = 0
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Seq.","Código","Descrição","UN","Grupo","AB","MC","CE","HO","ME")
 
	// Inicia Tabela
	if $$Start^COMTable(lstHeader) {
		
		While (queryData.Next()) {
			
			set idItem    = queryData.GetData(1)
			set descricao = queryData.GetData(2)
			set unidade   = queryData.GetData(3)
			set grupo     = queryData.GetData(4)
			set optionAB  = queryData.GetData(5)
			set optionCE = queryData.GetData(6)
			set optionMC  = queryData.GetData(7)
			set optionHO  = queryData.GetData(8)
			set optionME  = queryData.GetData(9)
 			set DescGrupo = ""
 			
 			if (grupo '= ""){
 				set DescGrupo = $$SQLGetDescGrupoItem^VARSQL(grupo)
 			}
 			
			set count = $i(count)
      
  			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idItem)
			do InsertCell^COMTable(descricao,,,,,,,,,1)			
			do InsertCell^COMTable(unidade,,,,"CENTER")
			do InsertCell^COMTable(DescGrupo,,,,"CENTER",,,,,1)
			do InsertCell^COMTable(optionAB,,,,"CENTER")
			do InsertCell^COMTable(optionMC,,,,"CENTER")
			do InsertCell^COMTable(optionCE,,,,"CENTER")
			do InsertCell^COMTable(optionHO,,,,"CENTER")			
			do InsertCell^COMTable(optionME,,,,"CENTER")			
			do EndLine^COMTable()						
		}					
	}
	
	if count = 0 {
		do NewLine^COMTable()
		do InsertCell^COMTable("Nenhum produto encontrado.<br>&nbsp;Obs: para compor a lista, os produtos devem estar marcados no cadastro de produtos como pertencentes ao elenco.",,,,,,10)
		do EndLine^COMTable()		
	}
		
	do Stop^COMTable()		
	do queryData.Close()
	
	quit
	
SQLMostrarValor(pNivelDeAtencao,pTipo)
 
	if ($find(pNivelDeAtencao,pTipo) && (pTipo = 1)) {
		quit "AB"
	}
	if ($find(pNivelDeAtencao,pTipo) && (pTipo = 3)) {
		quit "MC"
	}
	if ($find(pNivelDeAtencao,pTipo) && (pTipo = 2)) {
		quit "CE"
	}		
	if ($find(pNivelDeAtencao,pTipo) && (pTipo = 4)) {
		quit "HO"
	}	
	if ($find(pNivelDeAtencao,pTipo) && (pTipo = 5)) {
		quit "ME"
	}	
	quit ""]]></Routine>
</Export>