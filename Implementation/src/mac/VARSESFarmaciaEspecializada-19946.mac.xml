<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESFarmaciaEspecializada" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include VARConst
#include COMSYS
 
VARSESFarmaciaEspecializada
	quit
	
CadastraFarmaciaEspecializada(pLocation)
	new strStatus, objParams, strLocations
	set strStatus = 1
	set objParams = $get(^VARSESParams(YM,0,1))
	set strLocations = $$$VARSESParamsFarmaciaEspecializada(objParams)
	if ('$$IsFarmaciaEspecializada(pLocation)) {
		set $$$VARSESParamsFarmaciaEspecializada(objParams) =
		 $$$VARSESParamsFarmaciaEspecializada(objParams) _";"_pLocation
		set strStatus = $$$Save("VARSESParams",0,objParams,$$$YES)
	}
	quit strStatus
	
IsFarmaciaEspecializada(pLocation)
	new blnEspecializada, strLocations, objParams, listEspecializadas
	
	set blnEspecializada = 0
	
	set objParams = $get(^VARSESParams(YM,0,1))
	set strLocations = $$$VARSESParamsFarmaciaEspecializada(objParams)
	set listEspecializadas = $LISTFROMSTRING(strLocations,";")
	set blnEspecializada = $LISTFIND(listEspecializadas,pLocation)
	
	if (blnEspecializada > 0) {
		quit 1
	}
	else {
		quit 0
	}
	
IsFormReadOnly()
	;-------------------------------------------------------------------------------
	; Disable edit access for locations different from the ones set at VARSESParams 
	;
	;-------------------------------------------------------------------------------
 
	new objParams, strLocations, strLocation, bolEnable, loop
	
	set bolEnable = 0
	
	set objParams = $get(^VARSESParams(YM,0,1))
	set strLocations = $$$VARSESParamsFarmaciaEspecializada(objParams)
	
	for loop=1:1 {
		set strLocation = $piece(strLocations,";",loop)
		quit:(strLocation = "")
		if (strLocation = YLOCATION) {
			set bolEnable = 1
		}
	}
	
	quit bolEnable
		
importCID(fileName="c:\tabcid.txt")
 
 	new fileStream, line, idCID, CIDDescription, strStatus, idINPARA
 
	$$$VAR
	
	do limparCIDs()
	
	set fileStream = ##class(%FileCharacterStream).%New()
	set fileStream.Filename = fileName
 
	while 'fileStream.AtEnd {
		
		set line = fileStream.ReadLine()
		
		set idCID 		   = $piece(line, "^", 1)
		set CIDDescription = $piece(line, "^", 2)
 
		set idINPARA = "VARSESCID"_$$$COMMA_"PT"_$$$COMMA_idCID
 
		set strStatus = $$$Save("INPARA",idINPARA,CIDDescription)
	}	
	
	quit
	
limparCIDs()
 
 	new idioma, id, key, strStatus
 
	$$$VAR
 
	$$$Order3(^INPARA,YM,"VARSESCID",idioma)
		$$$Order4(^INPARA,YM,"VARSESCID",idioma,id)
		
			set key = "VARSESCID"_$$$COMMA_idioma_$$$COMMA_id
			set strStatus = $$$Kill("INPARA",key)
			;write strStatus,!
		
		$$$End
	$$$End
 
	quit
	
	// Tirar os . das associações para aproveitar o trabalho feito pelo Paulo.
atualizarCIDMedicamento()
 
	new idItem, counter, objCIDMedicamento, strStatus, key
	
	$$$VAR
 
	$$$Order2(^VARSESCIDMedicamento,YM,idItem)
	
		$$$Order3(^VARSESCIDMedicamento,YM,idItem,counter)
		
			set objCIDMedicamento = $get(^VARSESCIDMedicamento(YM, idItem, counter, 1))
			set objCIDMedicamento = $translate(objCIDMedicamento, ".", "")
			set key = idItem_$$$COMMA_counter
			
			set strStatus = $$$Save("VARSESCIDMedicamento",key,objCIDMedicamento,$$$YES)
			
			if $$$ISERR(strStatus) {
				write "Erro atualizando o cadastro de "_key_" -> "_objCIDMedicamento,!
			}
		
		$$$End
	
	$$$End
 
	quit
]]></Routine>
</Export>