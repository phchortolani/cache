<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARMEDDispenseMan" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARMEDDispenseMan

#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst
#define YCR	"|"
OnBlurPatient(pidPat,&pYFELD,&pYMFELD) 
	;--------------------------------------------------------------------------------
	; On Patient Blur
	;
	;
	; History:
	; 29-May-2014	SCR		HEVA-1505: Created	
	;-------------------------------------------------------------------------------
	new idAdm,objAdm


	if $get(pidPat) = "" quit
	
	set idAdm	=	$$GetAdmission^MEDAdmission(pidPat)
	if idAdm="" {
		set %TXT(1) = "#FUNCTION~alert('"_$$$Text("MED00412")_"'); window.setTimeout('document."_YHTMFORM_".Y"_YFORM_"D1.focus()',1);"
		quit
	}
	set objAdm	= $get(^MEDAdmission(YM,idAdm,1))
	
	set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"M"_(2)_"~"_idAdm
	set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"D"_(6)_"~"_$$$MEDAdmissionLocation(objAdm)
	if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
		set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"M"_(7)_"~"_$$$MEDAdmissionFREE4(objAdm)
		set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"M"_(8)_"~"_$$$MEDAdmissionFREE5(objAdm)
	}
	quit

OnBlurAdmin(pidAdm,&pYFELD,&pYMFELD) 
	;-------------------------------------------------------------------------------
	; On Admin Blur
	;
	;
	; History:
	; 16-Jun-2014	SCR		HEVA-1505: Created	
	;-------------------------------------------------------------------------------
	new objAdm,idPat
	quit:pidAdm=""
	
	set objAdm	= $get(^MEDAdmission(YM,pidAdm,1))
	
	set idPat	= $$$MEDAdmissionPatient(objAdm)

	if $get(idPat) = "" quit
	
	
	set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"D"_(15)_"~"_idPat
	set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"D"_(6)_"~"_$$$MEDAdmissionLocation(objAdm)
	if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
		set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"M"_(7)_"~"_$$$MEDAdmissionFREE4(objAdm)
		set %TXT(1)	= %TXT(1)_"#"_"Y"_YFORM_"M"_(8)_"~"_$$$MEDAdmissionFREE5(objAdm)
	}
	quit
	
OnBeforeButtonLine(pYKEY,&pYFELD,&pYMFELD,pYPARA, &pYVOR)
	;-------------------------------------------------------------------------------
	; Update Manual fields
	;
	; History:
	; 23-Jun-2014	SCR		HEVA-1505: Redirect if not a Manual Dispense 
	; 29-May-2014	SCR		HEVA-1505: Created	
	;-------------------------------------------------------------------------------
	new idAdm,idPat,objAdm,objDisp
	
	quit:$$$NoKey(pYKEY)    ; early exit

	set objDisp	= $get(^MEDDispense(YM,pYKEY,1))
	if $$$MEDDispenseStatusManual(objDisp) = "" {
		Do RedirectForm^COMUtilForm("MEDDispense",pYKEY,$get(YBACK),$get(YPARA),$get(YSEITE))
		quit
	}
	set idPat	= $$$MEDDispensePatient(pYFELD)
	quit:idPat=""  ; early exit
	set idAdm	= $$GetAdmission^MEDAdmission(idPat)
	if idAdm'="" {
		set $piece(pYMFELD,Y,2)=idAdm
		if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
			set objAdm				= $get(^MEDAdmission(YM,idAdm,1))
			set $piece(pYMFELD,Y,7)	= $$$MEDAdmissionFREE4(objAdm)
			set $piece(pYMFELD,Y,8)	= $$$MEDAdmissionFREE5(objAdm)
		}
	}
	if '$$Editable(pYKEY) {
		set $$$WWW120AuthorizationToModifyData(pYVOR) = $$$EnumReadOnly 
	}
	quit
	

OnAfterSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By :  VARMEDDispenseMan form
	;
	;
	; History:
	; 02-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------

	do LoadLines^VARMEDDispenseManPickLine(pYKEY,pYFELD)
	quit
	
OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
	; Called By :  VARMEDDispenseMan form
	;
	;
	; History:
	; 12-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new idStatusDes,idStatusMan,blnCanProc
	
	set idStatusMan	= $$$MEDDispenseStatusManual(pYFELD)
	quit:'idStatusMan  ; Early exit if no Status Manual
	set idStatusDes	= 2
	if idStatusMan=3 {
		set idStatusDes	= 3
	}
	set blnCanProc	= $$CanProcess(pYKEY,pYFELD)
	if (idStatusMan=1) && (blnCanProc) {
		set $$$MEDDispenseStatusManual(pYFELD) = 2
	} elseif (idStatusMan=2) && ('blnCanProc) {
		set $$$MEDDispenseStatusManual(pYFELD) = 1
	}
	set $$$MEDDispenseStatus(pYFELD)	= idStatusDes
	quit

CanProcess(pidDispense,pYFELD)
	;-------------------------------------------------------------------------------
	; Test whether this record is postable
	;
	;
	; ByRefs:
	;
	; Returns:	postable status
	;
	; History:
	; 12-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	quit $$CanProcess^MEDDispenseProcess(pidDispense,pYFELD)

Process(pidDispense)
	;-------------------------------------------------------------------------------	
	; Process the Dispense
	;
	; Params:	pidDispense	- Dispense
	;
	; Returns:	
	 ;
	; History:
	; 12-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------	
	new strStatus
	set strStatus	= $$Process^MEDDispenseProcess(pidDispense)
	quit strStatus

Editable(pidDispense) 
	;-------------------------------------------------------------------------------
	; Is form editable (unprocessed) ?
	;
	; Params:	pidPres - Prescription id
	;
	; Returns:	status
	;
	; History:
	; 12-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new strStatus,objDispense
	
	set strStatus = $$$OK
	if $data(^MEDDispense(0,pidDispense,1)) {
		set objDispense = $get(^MEDDispense(0,pidDispense,1))
		
		if $$$MEDDispenseStatusManual(objDispense) > 2  {   ;"Processed"
			set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"
		}
	}
	quit strStatus
	
Load(pidDispense)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 26-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	do GoToForm^COMUtilForm("VARMEDDispenseMan",pidDispense,"")
	quit
	
OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Called By :  WWW001VARHooks form VARMEDDispenseMan on OnBeforeDelete
	;
	;
	; History:
	; 26-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new idDisp,idLine
	set idDisp	= $$$KEY1(pYKEY)
	set idLine	= ""
	for {
		set idLine	= $order(^MEDDispensePickLine(YM,idDisp,idLine))
		quit:idLine=""
		do OnBeforeDelete^VARMEDDispenseManPickLine(idDisp_","_idLine,$$$YES)
	}
	quit $$$OK
]]></Routine>
</Export>