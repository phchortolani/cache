<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FININTGLPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FININTGLPost  ; Interface Post to GL
	
#include COMSYS
#include FINConst
	
AfterDataFieldsPost(Type)
    ;-------------------------------------------------------------------------------
    ; Show Data for Interface Post
    ;
    ; History:
    ; 24-Sep-2004	PaulK	Modifed to show the schedule. (SR10332)
    ;-------------------------------------------------------------------------------
	if Type="" do ShowTypes
	if Type'="" {
		do ShowUnPostedBatches(Type)
		do AfterDataField^COMSchedule(Type,"FININTBATCH","PostBatches^FININTBatch(´"_Type_"´)")
	}
	do Locations(Type)
	quit
	
	
Locations(Type)
	kill ^FINSYSTempList(YM,YUSER,"GLPostLocation")
	set ^FINSYSTempList(YM,YUSER,"GLPostLocation",1,1)   = "Unspecified"
	set ^FINSYSTempList(YM,YUSER,"GLPostLocation"," ",1) = "All Locations"
	quit
	
	
AfterDataFields(Type)
    ;-------------------------------------------------------------------------------
    ;  Show Data for Interface
    ;
    ; Returns:
    ;
    ; History:
    ; 20-Sep-2004	Shobby	SR10332: Don't do the AfterDataField^COMSchedule if Type
    ; 							is empty. Error caused if trying to create a schedule.
    ;-------------------------------------------------------------------------------
	if Type="" {
		do ShowTypes
	} else {
		do ShowUnPostedBatches(Type)
		do AfterDataField^COMSchedule(Type,"FININTBATCH","PostBatches^FININTBatch(´"_Type_"´)")
	}
	quit
	
	
ShowTypes
    ;-------------------------------------------------------------------------------
    ; Shows Batch Types
    ;
    ; History:
    ; 10-Dec-2004	PaulK	Fixed language texts
    ;-------------------------------------------------------------------------------
 	new Type,TypeRec,lstHeader,Desc

	set lstHeader=""
	set lstHeader=lstHeader_$listbuild($$$Text("Fin00522",0))  ;Source)
	set lstHeader=lstHeader_$listbuild($$$Text("Fin00053",0))  ;Description
	set lstHeader=lstHeader_$listbuild($$$Text("Fin00523",0))  ;Unposted Count
	
	do Start^COMTable(lstHeader,$$$Text($listbuild("Fin00041"),0))  ; Lines
 
	set Type=""
	for {
		set Type=$order(^FININTBatchType(YM,Type))
		quit:Type=""
		
		do NewLine^COMTable("")
		set TypeRec=$Get(^FININTBatchType(YM,Type,1))
		do InsertCell^COMTable(Type,"FININTGLPost",Type)
		do InsertCell^COMTable($$$FININTBatchTypeDescription(TypeRec),"FININTGLPost",Type)
		do InsertCell^COMTable($$UnpostedCount(Type),"FININTGLPost",Type,,"Right")
		do EndLine^COMTable()
	}
 
	do Stop^COMTable()
	quit
	
	
UnpostedCount(Type)
	; Count Batches for a Type
	new Posted,Batch,Count
	
	set Count=0
	set Posted=" "
	set Batch=""
	for {
		set Batch=$order(^FININTBatchs(YM,2,Type,Posted,Batch))
		quit:Batch=""
		
		set Count=Count+1
	}
	quit Count
	
	
ShowUnPostedBatches(Type)
    ;-------------------------------------------------------------------------------
	; Show unposted Batches
    ;
    ; History:
    ; 24-Sep-2004	Paul K	Changed Header to say "Batches" instead of "Lines"
	; 12-Jun-2003	SCR		Added Location Col
    ;-------------------------------------------------------------------------------
 	new Batch,Posted,BatchRec,lstHeader
 	
	set lstHeader=""
	set lstHeader=lstHeader_$listbuild($$$StrFININTBatchNumber)
	set lstHeader=lstHeader_$listbuild($$$StrFININTBatchBatchDate)
	set lstHeader=lstHeader_$listbuild($$$StrFININTBatchLastBlockNumber)
	set lstHeader=lstHeader_$listbuild($$$StrFININTBatchLocation)
	
	do Start^COMTable(lstHeader,$$$Text("Fin00361"))  ;Batches
 
	set Posted=" "
	set Batch=""
	for {
		set Batch=$order(^FININTBatchs(YM,2,Type,Posted,Batch))
		quit:Batch=""
		
		do NewLine^COMTable("")
		set BatchRec=$Get(^FININTBatch(YM,Batch,1))
		do InsertCell^COMTable(Batch,"FININTBatch",Batch)
		do InsertCellDate^COMTable($$$FININTBatchTypePosttoDate(BatchRec),"FININTBatch",Batch)
		do InsertCell^COMTable($$$FININTBatchLastBlockNumber(BatchRec),"FININTBatch",Batch,,"Right")
		do InsertCell^COMTable($$$FININTBatchLocation(BatchRec),"FININTBatch",Batch)
		do EndLine^COMTable()
	}
	
	do Stop^COMTable()
	quit
	
FileLoad
	;---------------------------------------------------------------------------
	; Load ^FININTBatchType from the Application Parameter "FININTBATCHTYPE"
	; Call from form FININTBatchType
	; 
	; History
	; 03-Nov-2004	shobby	Only save if record has changed 
	;---------------------------------------------------------------------------
	new Code,Rec,Desc
	
	set Code=""
	for {
		set Code=$order(^WWW101(0,"FININTBATCHTYPE",SPRACHE,Code))
		quit:Code=""
		
		set Rec=$get(^FININTBatchType(YM,Code,1))
		if $piece(Rec,Y,3,4)="" {
			set $piece(Rec,Y,3,4)="1"_Y_"1" ; Preset Location & Date options
		}
		if $piece(Rec,Y,6)="" {
			set $piece(Rec,Y,6)=" " ; Default Location to All
		}
		set Desc=$piece($get(^WWW101(0,"FININTBATCHTYPE",SPRACHE,Code,1)),Y,1)
		set $$$FININTBatchTypeDescription(Rec)=Desc
		if Rec'=$get(^FININTBatchType(YM,Code,1)) {
			do Save^FINSYS("FININTBatchType",Code,Rec,1)
		}
	}
 	quit
 	
]]></Routine>
</Export>