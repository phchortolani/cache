<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPatientConsumptionUI" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESPatientConsumptionUI
	#include COMConst
	#include INConst
	#include WWWConst
	#include FIN
 	#include COMSYS
 	#include MEDConst
	#include COMGridEdit31
	#include VARConst
	#include VARSESTreatmentMC 
	#include VARSESConst
	
	#define ConsumptionCounter 5
	
	quit
	
OnAfterDataFields()
 
 	set patientId = $piece($get(^VARTempPacienteMC(YBED)),Y,1)
 	
 	if (patientId = "") {
		set patientId = $piece(YPARA,$$$COMMA,1)
 	}
 	
 	if (patientId '= $piece(YPARA,$$$COMMA,1)){ 
 		set patientId = $piece(YPARA,$$$COMMA,1)
 	}
 	
 	set pYFORM 	  = $piece(YPARA,$$$COMMA,2)
 	
 	if (patientId = "") {
	 	&js<<b><font size=5>Por favor, selecione um paciente antes realizar a consulta>
 	} else {
		set objPatient = $get(^MEDPatient(0, patientId, 1))
		set patientName = $$$MEDPatientName(objPatient)
		
		if (pYFORM = "INDispenseToPatientLine"){
			do printPatientsAllSixMonths(patientId,$$$NO,$$$NO,$$$NO,patientName)
		} 
		else {
			write "<br><b><font size=3>Últimos consumos do paciente "_patientName_"</font>"
			do printPatients(patientId)
		}
 	}
 	do PrintCloseJs()
	quit	
 
PrintCloseJs()
	write !,"<INPUT TYPE='button' id='closeme1' onclick='closeWin()'>"
	write !,"<script language=""javascript"">"
	&js<
 
	function closeWin() {
  		window.close();
	}
 
	if (window.Event) document.captureEvents(Event.ONKEYDOWN);
	document.onkeydown = closeWin;
	
	if (window.Event) document.captureEvents(Event.ONCLICK);
	document.onclick = closeWin;
	
	>
	write !,"</script>"
	
	quit	
 
Start()
 	
	//write "Teste"
	do GoToForm^COMUtilForm("VARSESPatientConsumption","")
 
	quit
	
searchPatient(pCNS)
 	quit:(pCNS = "")
 	
 	new idPacient
	set idPacient  = $order(^MEDPatients(YM, 2, pCNS, ""))
	if idPacient '= "" {
		&js<
			alertconsumptions('#(idPacient)#');
		>
	}
 
	quit 
 
printPatients(pidPatient)
	do printPatientsAll(pidPatient)
	quit
 
printPatientsAll(pidPatient,pblnPrescription="",pblnThisMonth="",pblnCounter="",pstrName="")
	new idINIssue,idxIdPatient, objINIssue, idPrescricao, dteConsumo, idLine, objINIssueLine,
		fltQtdConsumida, idItem, objItem, itemName, strItem, intCounter, arrCons,
		intConsumptionCounter
	
	new strCurrentDate, strHeader, descMesPT
	
	set descMesPT = ""	
	set idINIssue = ""
 	if (pblnPrescription="") set pblnPrescription = $$$OK
 	if (pblnThisMonth="") set pblnThisMonth = $$$NO
 	if (pblnCounter="") set pblnCounter = $$$YES
 	
 	if (pblnThisMonth) {
		set strThisDate = $$GetMonthYear^VARSESPatientConsumptionMCLog($horolog)
		set strThisMonth = $piece(strThisDate,Y,1)
		set strThisYear = $piece(strThisDate,Y,2)	 	
		set descMesPT = $case(strThisMonth,"01":"Janeiro", "02":"Fevereiro", "03":"Março",
								"04":"Abril",   "05":"Maio",      "06":"Junho",
								"07":"Julho",   "08":"Agosto",    "09":"Setembro",
								"10":"Outubro",  "11":"Novembro", "12":"Dezembro")
 	}
 
	set intConsumptionCounter = 0
	set idxIdPatient = $$$Index(pidPatient)
	set idLocation = ""
	set idINIssue = ""
	for { 
		set idLocation = $order(^INIssues(YM,8,idxIdPatient,idLocation),-1)
		quit:(idLocation = "")
		for {
			set idINIssue = $order(^INIssues(YM,8,idxIdPatient,idLocation,idINIssue),-1)
			quit:(idINIssue = "")
			quit:((pblnCounter)&&(intConsumptionCounter = $$$ConsumptionCounter))
		
			set objINIssue = $get(^INIssue(YM,idINIssue,1)) 
			; not processed yet
			continue:($$$INIssueStatus(objINIssue) = 0)
		
			; not Medium Complexity
			if (pblnPrescription = $$$OK ) continue:($$$INIssueFREE16(objINIssue) '= 4)
 
			if (pblnPrescription = $$$OK ) {
				set idPrescricao = $$$INIssueReference(objINIssue)
			}
			else {
				set idPrescricao = idINIssue
			}				 
			set dteConsumo = $$$INIssueFREE14(objINIssue)
			continue:(dteConsumo="")
			
			set idLine = ""
			set cont = 0
		
			for {
				set cont = cont + 1
				set idLine = $order(^INIssueLine(YM,idINIssue,idLine))
				quit:(idLine = "")
			
				set objINIssueLine = $get(^INIssueLine(YM,idINIssue,idLine,1))
				set fltQtdConsumida = $$$INIssueLineQtyIssued(objINIssueLine)
				if (fltQtdConsumida < 0) set fltQtdConsumida = fltQtdConsumida * (-1)
				set idItem = $$$INIssueLineItem(objINIssueLine)
				set objItem  = $get(^INART(YM, idItem, 1)) 
				set itemName = $piece(objItem, Y, 1)
				set strItem		 	= idItem_" - "_itemName			

				set arrCons(dteConsumo,idPrescricao,idLine) = strItem_Y_fltQtdConsumida
			
			}
			set intConsumptionCounter = intConsumptionCounter + 1	
		}
	}
	if (pblnPrescription = $$$OK ) {
		set strHeader = "Prescrição"
	}
	else {
		
		//write "<P><B>Retiradas do mês "_ descMesPT _ " para o paciente " _ pstrName _""
		write "<P><B>Últimas retiradas (30 dias) para o(a) paciente " _ pstrName _""
		write "&nbsp;<button type=button onclick=alertconsumptions();> Ver últimos 6 meses</button><BR/></P>"
		set strHeader = "Consumo"
	}
	set dteConsumo = ""
	for {
		set dteConsumo = $order(arrCons(dteConsumo),-1)
		quit:(dteConsumo = "")
		
		do Start^COMTable($listbuild("Data",strHeader,"Local Abastecedor","Item","Qtd. Cons."))
		set idPrescricao = ""
		for {
			set idPrescricao = $order(arrCons(dteConsumo, idPrescricao))
			quit:(idPrescricao = "")
			set idLocalAbs = $piece(idPrescricao,"-",1)
			set intCounter = 0
			set idLine = ""
			for {
				set idLine = $order(arrCons(dteConsumo, idPrescricao, idLine))
				quit:(idLine = "")
				
				set strItem = $piece(arrCons(dteConsumo, idPrescricao, idLine),Y,1)
				set fltQtdConsumida = $piece(arrCons(dteConsumo, idPrescricao, idLine),Y,2)
				
				
				;w dteConsumo _ " x " _ idPrescricao _ "." _ idLine  _ " x " _  strItem _ " x " _  fltQtdConsumida , !
				
				if (intCounter # 2) = 0 {
					do NewLine^COMTable(YWHITE)
		 		} else {
					do NewLine^COMTable(YGRAY)
		 		} 
		 		// strThisMonth
			 		do InsertCellDate^COMTable(dteConsumo)
					do InsertCell^COMTable(idPrescricao)
					do InsertCell^COMTable($$GetLocalAbastecedor(idPrescricao),,,,,,,,,$$$YES)
					do InsertCell^COMTable(strItem)
					do InsertCell^COMTable(fltQtdConsumida)
					do EndLine^COMTable()
 
 
				set intCounter = intCounter + 1
			
			}
		}
		do Stop^COMTable()
	}
 	quit 
 	
PrintJSAlertConsumptions(idPatient)
	;AEPOfferLineItem.MAC(911): retval = EventValue('#(YUCI)#','#(YUSER)#','#(YFORM)#','FIX','itemNumberUpdateValues^AEPOfferLineItem',itemNumber,6,'');
	
	set formURL = "www.cls?EP=WWWFORM&YUCI="_YUCI_"&YFORM=VARSESPatientConsumption&YUSER="_YUSER_"&YBED="_YBED_"&YM="_YM_"&YTRAKT="_YTRAKT
 
	write !,"<script language=""javascript"">"
	&js<
		function alertconsumptions(idPatient) {
 			
 			//debugger;
 			if (idPatient == null) {
	 			idPatient = document.WWW2.YVARINDispenseToPatientD23.value
 			}
 			
			window.open("#(formURL)#"+"&YPARA="+idPatient+","+YFORM,"","resizable=1,width=640, height=480");
			//window.open("", "","resizable=1,width=200, height=100");
			
		}
		
		function searchPatientFromCNS() {
			CallBack('searchPatient^VARSESPatientConsumptionUI',document.WWW2.YVARINDispenseToPatientD23.value);
		}
	>
	write !,"</script>"
	quit
	
printPatientsAllSixMonths(pidPatient,pblnPrescription="",pblnThisMonth="",pblnCounter="",pstrName="")
	new idINIssue,idxIdPatient, objINIssue, idPrescricao, dteConsumo, idLine, objINIssueLine,
		fltQtdConsumida, idItem, objItem, itemName, strItem, intCounter, arrCons,
		intConsumptionCounter
	
	new strCurrentDate, strHeader, descMesPT
	
	kill ^VARSESTempUltimosConsumosPac(pidPatient)
	
	set descMesPT = ""
	
	set idINIssue = ""
 	if (pblnPrescription="") set pblnPrescription = $$$OK
 	if (pblnThisMonth="") set pblnThisMonth = $$$NO
 	if (pblnCounter="") set pblnCounter = $$$YES
 	
 	if (pblnThisMonth) {
		set strThisDate = $$GetMonthYear^VARSESPatientConsumptionMCLog($horolog)
		set strThisMonth = $piece(strThisDate,Y,1)
		set strThisYear = $piece(strThisDate,Y,2)	 	
		set descMesPT = $case(strThisMonth,"01":"Janeiro", "02":"Fevereiro", "03":"Março",
								"04":"Abril",   "05":"Maio",      "06":"Junho",
								"07":"Julho",   "08":"Agosto",    "09":"Setembro",
								"10":"Outubro",  "11":"Novembro", "12":"Dezembro")
 
 	}
 
	set intConsumptionCounter = 0
	set idxIdPatient = $$$Index(pidPatient)
	set idLocation = ""
	set idINIssue = ""
	for { 
		set idLocation = $order(^INIssues(YM,8,idxIdPatient,idLocation),-1)
		quit:(idLocation = "")
		for {
			set idINIssue = $order(^INIssues(YM,8,idxIdPatient,idLocation,idINIssue),-1)
			quit:(idINIssue = "")
			
			quit:((pblnCounter)&&(intConsumptionCounter = $$$ConsumptionCounter))
		
			set objINIssue = $get(^INIssue(YM,idINIssue,1))
			; not processed yet
			continue:($$$INIssueStatus(objINIssue) = 0)
		
			; not Medium Complexity
			if (pblnPrescription = $$$OK ) continue:($$$INIssueFREE16(objINIssue) '= 4)
 
			if (pblnPrescription = $$$OK ) {
				set idPrescricao = $$$INIssueReference(objINIssue)
			}
			else {
				set idPrescricao = idINIssue
			}	
			set dteConsumo = $$$INIssueFREE14(objINIssue)
			continue:(dteConsumo="")
		
			if (pblnThisMonth) {
				set strTheDate = $$GetMonthYear^VARSESPatientConsumptionMCLog(dteConsumo)
				set strTheMonth = $piece(strTheDate,Y,1)
				set strTheYear = $piece(strTheDate,Y,2)		
			
				continue:((strThisMonth'=strTheMonth)||(strThisYear'=strTheYear))	
			}
		
			set idLine = ""
		
			for {
				set idLine = $order(^INIssueLine(YM,idINIssue,idLine))
				quit:(idLine = "")
			
				set objINIssueLine = $get(^INIssueLine(YM,idINIssue,idLine,1))
				set fltQtdConsumida = $$$INIssueLineQtyIssued(objINIssueLine)
				if (fltQtdConsumida < 0) set fltQtdConsumida = fltQtdConsumida * (-1)
				set idItem = $$$INIssueLineItem(objINIssueLine)
				set objItem  = $get(^INART(YM, idItem, 1)) 
				set itemName = $piece(objItem, Y, 1)
				set strItem		 	= idItem_" - "_itemName			
			
				set arrCons(dteConsumo,idPrescricao,idLine) = strItem_Y_fltQtdConsumida
			}
			set intConsumptionCounter = intConsumptionCounter + 1	
		}
	}
	if (pblnPrescription = $$$OK ) {
		set strHeader = "Prescrição"
	}
	else {
		set strHeader = "Consumo"
	}
	set dteConsumo = ""
	write "<P><B>Retiradas dos últimos 6 mês para o paciente " _ pstrName _""
 
	set strTheDate = $order(arrCons(dteConsumo),-1)
	set strTheMonth = $piece($zdate($horolog),"/",1)
	set strTheYear = $piece($zdate($horolog),"/",3)	
	
	for x=1:1:6 {
 
		for {
			set dteConsumo = $order(arrCons(dteConsumo),-1)
			quit:(dteConsumo = "")
					
			continue:(+$zdate(dteConsumo) '= +strTheMonth)
			continue:(+$zdate(dteConsumo,3) '= +strTheYear)
					
			do Start^COMTable($listbuild("Data","Consumo","Local Abastecedor","Item","Qtd. Cons."))
			set idPrescricao = ""
			
			for {
				set idPrescricao = $order(arrCons(dteConsumo, idPrescricao))
				quit:(idPrescricao = "")
				
				set intCounter = 0
				
				set idLine = ""
				for {
					set idLine = $order(arrCons(dteConsumo, idPrescricao, idLine))
					quit:(idLine = "")
	
								
					set strItem = $piece(arrCons(dteConsumo, idPrescricao, idLine),Y,1)
					set fltQtdConsumida = $piece(arrCons(dteConsumo, idPrescricao, idLine),Y,2)
					;w dteConsumo _ " x " _ idPrescricao _ "." _ idLine  _ " x " _  strItem _ " x " _  fltQtdConsumida , !
					
					// Dados do relatório
					set objReport = $$GetLocalAbastecedor(idPrescricao)_Y_strItem_Y_fltQtdConsumida_Y_dteConsumo
				    set stKey = idxIdPatient_$$$COMMA_idPrescricao_$$$COMMA_$piece(strItem," ",1)
			    	set status = $$$Save("VARSESTempUltimosConsumosPac",stKey,objReport,1)
			    	
					if (intCounter # 2) = 0 {
						do NewLine^COMTable(YWHITE)
			 		} else {
						do NewLine^COMTable(YGRAY)
			 		}
			
					do InsertCellDate^COMTable(dteConsumo)
					do InsertCell^COMTable(idPrescricao,,,,,"CENTER")
					do InsertCell^COMTable($$GetLocalAbastecedor(idPrescricao),,,,,,,,,$$$YES)
					do InsertCell^COMTable(strItem,,,,,,,,,$$$YES)
					do InsertCell^COMTable(fltQtdConsumida,,,,"RIGHT",,,,,$$$YES)
					do EndLine^COMTable()
					set intCounter = intCounter + 1
				}
			}
 
			do Stop^COMTable()
		}
	
		set strTheMonth=strTheMonth-1
		if strTheMonth<1 {
			set strTheYear = strTheYear - 1
			set strTheMonth=12
		}
	
	}
		
 	quit
 	
CanImprimir(pYPARA)
	set YQ = $$$YQEnable
 
	set patientId = $piece($get(^VARTempPacienteMC(YBED)),Y,1)
	
	if (patientId = ""){
		set YQ = $$$YQDisable("Primeiro selecione um paciente.")
	}
	
	quit
	
Imprimir(pYPARA)
	new patientId
	
	set patientId = $piece($get(^VARTempPacienteMC(YBED)),Y,1)
	set paramReport = $piece(pYPARA,$$$COMMA,2)
	
	do RunReportUltimosConsumosPaciente^VARSESJasperRunReportContinuacao(patientId,paramReport,$$$ConsumptionCounter)
	
	quit
	
GetLocalAbastecedor(idPrescricao)
	set objINIssue = $get(^INIssue(0,idPrescricao,1))
	set idLocal = $$$INIssueFromLocn(objINIssue)
	set localAbastecedor = $$SQLGetLocationName^VARSQL(idLocal)
	quit localAbastecedor
 
 ; w $$AlteraDataAltoCusto^VARSESPatientConsumptionUI("EX14/000055","14/01/2014 12:34:14")
AlteraDataAltoCusto(IdIssue,dataCriacao)
	if (($length(dataCriacao) > 7) || ($length(IdIssue)>1)){
		try {
			set data = $ZDATETIMEH(dataCriacao,4)
			if ($length($get(^INIssue(0,IdIssue,1))) > 10){
				if ($$$INIssueStatus(^INIssue(0,IdIssue,1)) = 9) {
					set $$$INIssueCreatedOn(^INIssue(0,IdIssue,1)) = data
					set $$$INIssueChangedOn(^INIssue(0,IdIssue,1)) = data
					set status = "alterado com sucesso"
				}
				else {
					set status = "a Dispensação deve estar processada para alterar a data"
				}
			}
		}
		catch errojb {
			set status = "data invalida o formato deve ser DD/MM/AA se existir hora DD/MM/AA hh:mm:ss"
		}
	}
	else {
			set status = "data ou chave da Dispensaçao inválida"
	}
	quit status

]]></Routine>
</Export>