<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescriptionHospLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescriptionHospLine  ; Single page Precription form

#include COMSYS 
#include MEDConst
#include INConst
#include WWWConst

EditOld()
	;-------------------------------------------------------------------------------
	; Edit Line
	;		
	; Params:	(YKEY)
	;
	;
	; Returns:
	;
	; History :
	; 16-Apr-2012	SCR		SR17993:Created
	;-------------------------------------------------------------------------------
#dim YKEY,objLine,strForm As %String
#dim intSol As %Integer
	new YKEY,objLine,YPARA,intSol,strForm
	set YKEY  = $$$GRIDGetCurrentYKEY
	set YPARA = ""
	set strForm="MEDPrescriptionHosp"
	if $piece(YKEY,",",2)'="" {
		set objLine=$get(^MEDPrescriptionLine(YM,$piece(YKEY,",",1),$piece(YKEY,",",2),1))
		set intSol= $$$MEDPrescriptionLineSolution(objLine)
		if intSol'="" {
			do Solution^MEDPrescriptionSolLine(,intSol)
			set YFORM="MEDPrescriptionHosp"
			set YPARA=$piece(YKEY,",",2)
			set strForm="MEDPrescriptionSol"
			set ^CacheTemp("S",$i(^CacheTemp("S")))=$get(YBACK)

		}
		
	}
	Do RedirectForm^COMUtilForm(strForm,YKEY,"MEDPrescriptionHosp,",YPARA,"")

	quit
	
Delete(pa,pb)
	;-------------------------------------------------------------------------------
	; Ask for Delete Line
	;		
	; Params:	(YKEY)
	;
	;
	; Returns:
	;
	; History :
	; 18-May-2012	SCR		SR17993:Created
	;-------------------------------------------------------------------------------
#dim YKEY,objLine,strForm,strStatus As %String
#dim intSol As %Integer
	new YKEY,objLine,YPARA,intSol,strForm,strStatus
	set YKEY  = $$$GRIDGetCurrentYKEY
	set YPARA = ""
	if $piece(YKEY,",",2)'="" {
		write "if (confirm('"_$$$Text("MED01286")_"')) { CallBackNow('DeleteLine^MEDPrescriptionHospLine'); }"
		  ;Delete Data Record   ?
		;set strStatus = $$$Kill("MEDPrescriptionLine",YKEY)
	}

	quit ""
DeleteLine(pa,pb)
	;-------------------------------------------------------------------------------
	; Ask for Delete Line
	;		
	; Params:	(YKEY)
	;
	;
	; Returns:
	;
	; History :
	; 02-Jul-2012	SCR		SR17993:If a solution is deleted remove all lines for the solution
	; 18-May-2012	SCR		SR17993:Created
	;-------------------------------------------------------------------------------
#dim YKEY,objLine,strForm,strStatus As %String
#dim intSol As %Integer
	new YKEY,objLine,YPARA,intSol,strForm,strStatus,idRx,idLine
	set YKEY  = $$$GRIDGetCurrentYKEY
	set YPARA = ""
	set strForm="MEDPrescriptionHosp"
	set idRx=$piece(YKEY,",",1)
	set idLine=$piece(YKEY,",",2)
	set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
	set intSol= $$$MEDPrescriptionLineSolution(objLine)
	if $piece(YKEY,",",2)'="" {
		set strStatus = $$$Kill("MEDPrescriptionLine",YKEY)
		if intSol {
			set idLine=""
			for {
				set idLine=$order(^MEDPrescriptionLine(YM,idRx,idLine))
				quit:idLine=""
				set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
				if intSol = $$$MEDPrescriptionLineSolution(objLine) {
					set strStatus = $$$Kill("MEDPrescriptionLine",idRx_","_idLine)
				}
			}
		}
	}
	do ReNumber^MEDPrescriptionRenum(idRx)

	Do RedirectForm^COMUtilForm(strForm,YKEY,"MEDPrescriptionHosp,",YPARA,"")

	quit ""

	
Suspend(pstrKey)
	;-------------------------------------------------------------------------------
	; Suspend Rx or Rx Line 
	; 
	; Param YKEY = Rx or Rx,Line  
	;
	; History:
	; 12-Jul-2012	SCR		SR17979: Created
	;-------------------------------------------------------------------------------
	;new strStatus,YKEY
	;set YKEY  = $$$GRIDGetCurrentYKEY
	
	;set strStatus = $$Transaction^COMTransaction("SuspendTxn^MEDPrescription("""_YKEY_""")",$$$YES)
	
	;Do RedirectForm^COMUtilForm("MEDPrescriptionHosp",YKEY,"MEDPrescriptionHosp,",YPARA,"")

	;quit strStatus
	

	new strPrompt
	new strCallback
	new idRx
	new strYKEY
	new strAlert

	if ($get(pstrKey) '= "") {
		set strYKEY = pstrKey
	} else {
		set strYKEY = $$GetKey^COMGridEdit31G()
	}
	
	set strPrompt   = $$$JSText($$$Text( "MED01289"))  ;Please specify the reason for Suspension
	set strCallback = "SuspenseSet^MEDPrescriptionHospLine"
	set idRx	 	= $$$KEY1(strYKEY)
	set strAlert 	= $$$JSText($$$Text("MED00415")) //Please enter a Reason

	$$$StartScript()
	&js<
		//var strResult=prompt('#(strPrompt)#','','');
		
		var strResult = iePrompt('#(strPrompt)#');
				
		if (strResult!=false) {
			CallBackNow('#(strCallback)#','#(strYKEY)#',strResult);
		} else {
			CallBackNow('GoToForm^COMUtilForm','MEDPrescriptionHosp','#(idRx)#');		
		}
		
	>
	$$$EndScript()
	
	quit $$$OK

SuspenseSet(pidRx, pstrReason)
	;-------------------------------------------------------------------------------	
	; Set the status and add Reason to Remarks
	;
	; Params:	pidRx	- Prescription, Line Key
	;			Reason
	;
	; Returns:	
	;
	; History:
	; 26-Jul-2012	SCR		SR17994 : Created
	;-------------------------------------------------------------------------------	
	new strStatus,YKEY
	set YKEY  = $$$GRIDGetCurrentYKEY
	
	set strStatus = $$Transaction^COMTransaction("SuspendTxn^MEDPrescription("""_YKEY_""","""_pstrReason_""")",$$$YES)
	
	
	Do RedirectForm^COMUtilForm("MEDPrescriptionHosp",YKEY,"MEDPrescriptionHosp,",YPARA,"")

	;quit strStatus
	quit $$$OK


	
StartSol(YKEY)
	;-------------------------------------------------------------------------------
	; Edit Line
	;		
	; Params:	(YKEY)
	;
	;
	; Returns:
	;
	; History :
	; 06-May-2012	SCR		SR17993:Created
	;-------------------------------------------------------------------------------
	set YFORM="MEDPrescriptionHosp"
	do StartForm^MEDPrescriptionSol(YKEY)	
	quit

GetDoseText(YFELD)
	; SR18026
	
	new strText
	
	set strText= $$$MEDPrescriptionLineDoseQty(YFELD)
	if $$$MEDPrescriptionLineDoseUOM(YFELD) '="" {
		set strText=strText_" "_$$$AppEnum("EINHEIT", $$$MEDPrescriptionLineDoseUOM(YFELD))
	}
	quit strText

GetFromDescription(YFELD)
	;-------------------------------------------------------------------------------
	; Display date and time on grig
	;		
	; Params:	(YFELD)
	;
	;
	; Returns:
	;
	; History :
	; 27-Jun-2012	SCR			SR17993:Only when Item is added
	; 06-Jun-2012	Shobby		SR18026:Created
	;-------------------------------------------------------------------------------
	
	new strText,dtDate,tmTime
	
	set strText=""
	if $$$MEDPrescriptionLineItem(YFELD)'="" {
		set dtDate = $$$MEDPrescriptionLineFromDate(YFELD)
		set tmTime = $$$MEDPrescriptionLineFromTime(YFELD)
		set:dtDate strText=$$^WWWTR(0,1,dtDate)
		set:tmTime strText=strText_" "_$piece($$^WWWTR(0,7,tmTime),":",1,2)
	}
	quit strText
	
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Set Dose & From Date Time on each line
	;
	; Returns:
	;
	; History:
	; 17-May-2012	SCR		SR17993: Added From Dat/Time
	; 16-May-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	quit ;SR18026 
OnAfterRowChange(pstrYFORM,pstrParams)
	;-------------------------------------------------------------------------------
	; Set 'read only' field on row change to 'x'. Edit button clears it. 
	;
	; Returns:
	;
	; History:
	; 09-Apr-2013	SCR		HEVA-875: Set Infusion UOM Description for 1st time load
	; 24-Oct-2012	SCR		SR18156: Calculate Volume
	; 22-Oct-2012 	SCR		SR18132: Keep Unit
	; 18-Jul-2012	SCR		SR17993: Update Diag Desc & Diluent UOM
	; 04-Jul-2012	SCR		SR17993: Update Line Status
	; 26-Jun-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new YFELD,YMFELD,strReadOnlyFlag,idKey,idLineStatus,strLineStatus,idDiag,strDiagDesc
	new idUnit,idDil,strDilUM,intVol,idItem,intDose,idInfuseUOM

	set YFELD = $$GetYFELD^COMGridEdit31G(pstrYFORM,pstrParams)         
	set YMFELD = $$GetYMFELDContainer^COMGridEdit31G()
	set strReadOnlyFlag="x" ; Read Only
	set:$$$MEDPrescriptionLineItem(YFELD)="" strReadOnlyFlag="" ; New Line = Editable
	set idLineStatus =  +$$$MEDPrescriptionLineStatus(YFELD)
	set strLineStatus = $$GetDescription^WWWStatus("MEDPrescription",+idLineStatus,$get(SPRACHE))
	do PopulateDataField^COMUtils("MEDPrescriptionHosp",37,strReadOnlyFlag,"M",YFELD) 
	do PopulateDataField^COMUtils("MEDPrescriptionHosp",13,strLineStatus,"M",YFELD)
	set idUnit = $$$MEDPrescriptionLineDoseUOM(YFELD) ; SR18132
	do UpdateUnits^INARTUNIT($$$MEDPrescriptionLineItem(YFELD),"YMEDPrescriptionHospM16",idUnit) ; SR18132
	;do UpdateUnits^INARTUNIT($$$MEDPrescriptionLineItem(YFELD),"YMEDPrescriptionHospM16")
	set YRETVAL=YRETVAL_%TXT(1) ; populated from UpdateUnits
	set YRETVAL=YRETVAL_$$Lists("YMEDPrescriptionHospM17","YMEDPrescriptionHospM18",$$$MEDPrescriptionLineFrequency(YFELD), $$$MEDPrescriptionLineRouteOfAdministration(YFELD))
	set idDiag = $$$MEDPrescriptionLineDiagnosis(YFELD)
	set strDiagDesc=""
	if idDiag'="" {
		set strDiagDesc= $$$MEDICDDescription($get(^MEDICD(YM,idDiag,1)))
	}
	do PopulateDataField^COMUtils("MEDPrescriptionHosp",36,strDiagDesc,"M",YFELD) 
	set idDil 	= $$$MEDPrescriptionLineDiluent(YFELD)
	set idItem 	= $$$MEDPrescriptionLineItem(YFELD) 	; SR18156
	set intDose = $$$MEDPrescriptionLineDoseQty(YFELD)  ; SR18156
	set intVol = $$VolumeItem^MEDPrescriptionSolLine(idItem,idUnit) * intDose ; SR18156
	if idDil'="" {
		set strDilUM = $$$AppEnum("EINHEIT",$piece($get(^INART(YM,idDil,1)),Y,40))
		do PopulateDataField^COMUtils("MEDPrescriptionHosp",35,strDilUM,"M",YFELD)
		set intDose = $$$MEDPrescriptionLineDiluentQty(YFELD)
		set intVol = intVol + ($$VolumeItem^MEDPrescriptionSolLine(idDil) * intDose) ; SR18156
	}
	do PopulateDataField^COMUtils("MEDPrescriptionHosp",43,intVol,"M",YFELD) ; SR18156 
	; HEVA-875 vvvv
	;set idInfuseUOM	= $$$MEDPrescriptionLineInfusionUOM(YFELD)
	;if idInfuseUOM'="" {
	;	set YRETVAL = YRETVAL_"#YMEDPrescriptionHospM33~"_idInfuseUOM_"~"_$$$INPARAText($get(^INPARA(0,"MEDINFUSEUNIT",LANGUAGE,idInfuseUOM,1)))
	;}
	; HEVA-875 ^^^^


	quit

Lists(pidFreq,pidRoute,pstrFreq,pstrRoute)
	;-------------------------------------------------------------------------------
	; Get List for Frequency and Route
	;
	; Returns: Freq & Route Fields
	;
	; History:
	; 25-Jul-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new strTxt,idCode,strData,intLine
	set strTxt="" 
	set strTxt = strTxt_"#"_pidFreq_"~REMOVE"
	set strTxt = strTxt_"#"_pidFreq_"~"_$$$NULLID_"~"_$$$SPACE_"~"_0

	
	set idCode = ""
	for intLine=1:1 {
		set idCode = $order(^MEDFrequency(YM,idCode))
		quit:idCode=""
		
		set strTxt = strTxt_"#"_pidFreq_"~"_idCode_"~"_idCode_" - "_$piece($get(^MEDFrequency(YM,idCode,1)),Y,1)_"~"_intLine
	}
	set strTxt = strTxt_"#"_pidFreq_"~"_pstrFreq

	set strTxt = strTxt_"#"_pidRoute_"~REMOVE"
	set strTxt = strTxt_"#"_pidRoute_"~"_$$$NULLID_"~"_$$$SPACE_"~"_0
	set idCode = ""
	for intLine=1:1 {
		set idCode = $order(^INPARA(0,"MEDROUTEADMIN",LANGUAGE,idCode))
		quit:idCode=""
		
		set strTxt = strTxt_"#"_pidRoute_"~"_idCode_"~"_idCode_" - "_$piece($get(^INPARA(0,"MEDROUTEADMIN",LANGUAGE,idCode,1)),Y,1)_"~"_intLine
	}
	set strTxt = strTxt_"#"_pidRoute_"~"_pstrRoute

	quit strTxt

ButtonCheck(pidRx)
	;-------------------------------------------------------------------------------
	; Check if Grid buttons are ok
	;
	; Returns:
	;
	; History:
	; 18-May-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new blnCanEdit
	set blnCanEdit=$$Editable^MEDPrescription(pidRx,1)
	if $$$ISOK(blnCanEdit) {
		set YQ = $$$QSave
	} else {
		set YQ = $$$QDontSave_" "_$$$Text("MED01263")  ;Prescription processed)
	}
	quit

	
	
MakeEditable(pstrYFORM,pstrParams) ;SR18016
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 28-Aug-2012	shobby		SR18016.1: Renamed from ChangeRow2
	;-------------------------------------------------------------------------------
	new YFELD,YMFELD,idKey
	set YFELD = $$GetYFELD^COMGridEdit31G(pstrYFORM,$piece(pstrParams,Y,1))         
	set YMFELD = $$GetYMFELDContainer^COMGridEdit31G()
	set idKey = $$GetYKEY^COMGridEdit31G($piece(pstrParams,Y,1))
	if $$$MEDPrescriptionLineSolution(YFELD) {
		;write "alert(window.location);" 
		set ^CacheTemp(YUSER,"GridLine") = $piece(idKey,",",2)
		do RedirectForm^COMUtilForm("MEDPrescriptionSol",idKey,"MEDPrescriptionHosp,","","")
	} else {
		do PopulateDataField^COMUtils("MEDPrescriptionHosp",37,"","M",YFELD) 
		set ^CacheTempMedRx(YUSER,"Line") = pstrParams ; Current line	, used by onBeforeSave^MEDPrescriptionHosp

	}
	quit $$$OK
	
	
FormatItem(YFELD,&pstrFontColor)

	;-------------------------------------------------------------------------------
	; Set the font color based on Required/if needed
	;
	; Params: Current Line
	;
	; Returns: By ref font color
	;
	; History:
	; 29-Jul-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	if $$$MEDPrescriptionLineIfNeeded(YFELD) {
		set pstrFontColor = $$$SysEnum("FARBE",$$$MEDPrescriptionSetupIfRequiredColour($get(^MEDPrescriptionSetup(0,0,1))))
	}
	if $$$MEDPrescriptionLineUrgent(YFELD) {
		set pstrFontColor = $$$SysEnum("FARBE",$$$MEDPrescriptionSetupUrgentColour($get(^MEDPrescriptionSetup(0,0,1))))
	}

	quit $$$YES

DefaultTime()
	quit ($piece($h,",",2)\60*60) ;HEVA-713.4
]]></Routine>
</Export>