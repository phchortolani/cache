<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilCookie" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilCookie
	
#include COMSYS
	
AddCookie(pidSession="")
	;-------------------------------------------------------------------------------
	; Add a cookie common across all namespaces.  Basically this keeps a count on the 
	; number of successful logins to Alphalinc across all namespaces as the %session.SessionId
	; will be common across multiply browser sessions running on the same PC and the
	; same instance of Cache.
	;
	; Params:
	; 			pidSession:The value of the %session.SessionId
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Oct-2009	shobby	SR16948: Leave a cookie here for the original URL
	; 20-Mar-2007	RPW		SRBR014275: + on $h is faster here.
	; 19-Mar-2007	shobby	SRBR014275:	Renamed global from ^["%SYS"]AlphalincCookie
	; 19-Mar-2007	shobby	SRBR014275: Created
	;-------------------------------------------------------------------------------
	do CleanUp()
	set ^CacheTempCookie(pidSession,$order(^CacheTempCookie(pidSession,""),-1)+1) = +$horolog
	if ($get(YUCI)'="") && ($get(YUSER)'="") && ($get(YXURL)'="") set ^CacheTempURL(YUCI,YUSER) = YXURL  ;16948
	quit
	
	
RemoveCookie(pidSession="")
	;-------------------------------------------------------------------------------
	; Removes the cookies created in AddCookie.  Removed in reverse order by date/time.
	; It is not really important in which order they are removed only to know when the
	; last one is about to be removed so that the %session.SessionId can be ended freeing
	; up a license on cache.
	;
	; Params:
	; 			pidSession:The value of the %session.SessionId
	;
	; ByRefs:
	;
	; Returns:
	;			A flag indicating whether the last cookie has been removed and the
	;			session can be ended.
	;			
	; History:
	; 19-Mar-2007	shobby	SRBR014275:	Renamed global from ^["%SYS"]AlphalincCookie
	; 19-Mar-2007	shobby	SRBR014275: Created
	;-------------------------------------------------------------------------------
	new idCookie
	
	do CleanUp()
	set idCookie = $order(^CacheTempCookie(pidSession,""),-1)
	if idCookie'="" {
		kill ^CacheTempCookie(pidSession,idCookie)
	}
	quit '$data(^CacheTempCookie(pidSession))
	
	
CleanUp()
	;-------------------------------------------------------------------------------
	; Remove orphaned cookies that are still here because of system crashes.  Actually
	; the session id will have already been cleaned up by Cache (after about 20 minutes)
	; but if we ever get to reuse the same sessionid we don't want interference from
	; crashed sessions.  Actually the chance of reuse of a sessionid outside of the 
	; reuse expected from the same user on the same pc running multiple versions of 
	; Alphalinc is probably 1 in 839,299,365,868,340,224
	; but might as well do the cleanup anyway.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-May-2010	GRF		-: replace order macro
	; 20-Mar-2007	RPW		SRBR014275: + is not needed on $h here.
	; 19-Mar-2007	shobby	SRBR014275:	Renamed global from ^["%SYS"]AlphalincCookie
	; 19-Mar-2007	shobby	SRBR014275: Created
	;-------------------------------------------------------------------------------
	new intCookie,dteDate,idSession
	
	set idSession = ""
	for {
		set idSession = $order(^CacheTempCookie(idSession))
		quit:idSession=""
		
		set intCookie = ""
		for {
			set intCookie = $order(^CacheTempCookie(idSession,intCookie))
			quit:intCookie=""
			
			set dteDate = $get(^CacheTempCookie(idSession,intCookie))
			if ($horolog-dteDate)>2 {
				kill ^CacheTempCookie(idSession,intCookie)
			}
		}
	}
	quit
	
]]></Routine>
</Export>