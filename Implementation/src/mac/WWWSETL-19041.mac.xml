<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSETL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSETL(pstrGlobal,YTEST)
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Read Values
	;		WERTE LESEN
	; 
	; Usage :
	;	set YFELD=$$^WWWSETL("^DATEI(YM,KEY,KEYn,1)")
	; 
	; Inputs : 
	;	pstrGlobal
	;	YTEST
	;
	; ByRef :
	;
	;
	; Returns :
	;	pobjRecord   = GESAMTER DATENSATZ ;data record
	;
	; History :
	; 12-Jul-2007	RPW		SR15571: Rewrote in Brace Syntax
	; 05-Feb-2007	RPW		SR14900: Remove unused code.
	; 12-Jul-2005	GRF		Doco
	; 31.08.1997	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	new pobjRecord,idClass,objWWW001,enumAltSaveProc,strAltDelim
	
	set pobjRecord=""
	quit:'$data(pstrGlobal) pobjRecord               ;KEIN SCHLÜSSEL ;no keys 
	quit:$find(pstrGlobal,"""""") pobjRecord         ;KEIN SUBSCRIPT ;null subscript 
	quit:$extract(pstrGlobal)'="^" pobjRecord        ;FALSCHER PARAMETER
	quit:$length(pstrGlobal)>255 pobjRecord          ;FALSCHE LÄNGE  ;too long 
	
	;TYBD;IN UND WWW IMMER STANDARD @NET ZUGRIFF;30,9,2004
	do SetGlobal^WWWSETObject(.pstrGlobal,.objWWW001,.idClass)
	
	set enumAltSaveProc=$$$WWW001AltSaveProcedure(objWWW001)
	
	if enumAltSaveProc>0 {         ;ALTERNATIVE SPEICHERUNG ;option 
		if enumAltSaveProc=4 {
			set pobjRecord=$$OBJECT(idClass,pstrGlobal)   ;CACHE
		
		} elseif enumAltSaveProc=5 {
			do PROG(pstrGlobal,.pobjRecord)
		}
	
	} else {
		if $length(pstrGlobal,"""")#2=0 {
			set pobjRecord= ""                 ; odd number of double quotes is invalid   ; UNGLEICHE ""
		} else {
			set pobjRecord=$get(@pstrGlobal)
			if (pobjRecord="") && ($get(YTEST)=1) && $data(@pstrGlobal) {
				set pobjRecord=Y               ;DATENSATZ VORHANDEN;TYBD;30,04,2003
			}
			
			set strAltDelim = +$$$WWW001AltDelimiterASCII(objWWW001)
			
	;		126 "~" 223 "ß"
			if (pobjRecord'="") && (strAltDelim'=0) && (strAltDelim'=126) {
				set pobjRecord=$translate(pobjRecord,$char(strAltDelim)_Y,Y_$char(223))
			}
		}
	}
	quit pobjRecord
	 
	
OBJECT(pidClass,pstrGlobal) ;READ AS OBJECT
	;-------------------------------------------------------------------------------
	;	SAVE AS OBJECT 
	;	SET YA2      = ##class(class).%New()   ;OPEN ;table-mat 
	;	SET YA2.NAME = VALUE                   ;table-mat 
	;	SET YA5      = YA2.%Save()             ;speichern ;table-mat memorize 
	;	SET YA5      = YA2.%close()            ;close ;table-mat
	;
	; History:
	; 10-Jul-2007	RPW		SR15571: Complete rewrite to brace syntax and nice variables.
	;-------------------------------------------------------------------------------
	new idObject,objRecord,objForm,objClassField,strPropertyName,idList,idField,lstProperty
	new loop,enumInputType,strValue
	
	set idObject=$$GetObjectId^WWWSETObject(pstrGlobal)
	
	set objRecord=""  ;DATENSATZ ;data record 
	if $$Exists^WWWSETObject(pidClass,idObject) {
		set objForm = $SYSTEM.OBJ.OpenId(pidClass,idObject)
		set idField = ""
		for {
			set idField = $order(^WWW003(0,pidClass,idField))
			quit:idField=""
		
			set objClassField = $translate($get(^WWW003(0,pidClass,idField,1)),";-:_/*#",",,,,,,")  ;DEFINITON
			quit:objClassField="" 
			
			set strPropertyName = $$$WWW003PropertyName(objClassField)
			if strPropertyName="" set strPropertyName = $$$WWW003PropertyDescription(objClassField)  //$PIECE(objClassField,Y,1)  ;PROPERTYNAME
			continue:strPropertyName=""
			                        ;KEINER ;nobody 
			if $$$WWW003AlternateInputType(objClassField)'="" {
				do DEEPLOAD($$$WWW003AlternateInputType(objClassField),strPropertyName,objForm)
			} else {
				
				set enumInputType=$$$WWW003InputType(objClassField)
			
				if enumInputType'=15 {
					set strValue=$zobjproperty(objForm,strPropertyName)
					/*
					if enumInputType=14 {
						if strValue'="" {
							set strValue=$zdatetimeh(strValue,3) // Convert back from ODBC format for timestamps
						}
					}
					*/
					set $piece(objRecord,Y,idField)=strValue
					
				} else {
					set lstProperty=$zobjproperty(objForm,strPropertyName)
					set idList=""
					for loop=1:1:$listlength(lstProperty) {
						set $piece(idList,";",loop)=$listget(lstProperty,loop)
					}
					set $piece(objRecord,Y,idField)=idList
				}
			}
		}
	}
 
	quit objRecord
	
DEEPLOAD(pidClass,pstrPropertyName,pobjForm) 
	;-------------------------------------------------------------------------------
	; Load existing objects 
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 10-Jul-2007	RPW		SR15571: Rewrote in brace syntax (renamed from DEEPSAVE)
	;-------------------------------------------------------------------------------
	quit:($get(YFORM)="")||($get(YUSER)="")
	new idField,objClassField,strSubPropertyName
	
	set idField=""
	for {
		set idField=$order(^WWW003(0,pidClass,idField))
		quit:idField=""
		
		set objClassField=$translate($get(^WWW003(0,pidClass,idField,1)),";-:_/*#",",,,,,,,")
		quit:objClassField=""
		
		set strSubPropertyName=$$$WWW003PropertyName(objClassField)
		if strSubPropertyName="" set strSubPropertyName=$$$WWW003PropertyDescription(objClassField)
		continue:strSubPropertyName=""
		
		if $$$WWW003AlternateInputType(objClassField)'="" {
			do DEEPLOAD($$$WWW003AlternateInputType(objClassField),pstrPropertyName_"."_strSubPropertyName)
		} else {
			if $ZOBJPROPERTY(pobjForm,pstrPropertyName)'="" {
				set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"DEEP",pidClass,pstrPropertyName,strSubPropertyName)=$ZOBJPROPERTY(pobjForm,pstrPropertyName_"."_strSubPropertyName)
			} else {
				set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"DEEP",pidClass,pstrPropertyName,strSubPropertyName)=""
			}
		}
	}
		
	quit
	
PROG(pstrGlobal,&pobjRecord)
	;------------------------------------------------------------------------------- 
	;	read program		;LESEN PROGRAMM
	;	Loads a program and passes through each line
	;	
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 10-Jul-2007	RPW		SR15571: Rewrote in brace syntax
	;-------------------------------------------------------------------------------
	QUIT:$FIND($GET(YAKTION),"/csp/") ;; Normally Disclinc would always have this
	QUIT
	
	/* vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	NEW YPRO,YEXE,I
	SET YPRO=$TRANSLATE($PIECE($PIECE(pstrGlobal,"(",2),",",2),"""")
	if ((YPRO'="") && (YPRO'="WWWSEAR3") && ($data(^$ROUTINE(YPRO)))) {
		SET YEXE="ZL @YPRO F I=1:1 Q:$T(+I)=""""  S pobjRecord=$E(pobjRecord_$T(+I),1,14000)_""|"""
		XECUTE YEXE
	}
	QUIT
	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END */
 
]]></Routine>
</Export>