<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFILESEARCH" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFILESEARCH(FILE)
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		BUTTON FÜR DIE AUSWAHL VON DATEIEN
	;		BUTTON FOR THE SELECTION OF FILES
	;	
	;	SUCH NACH DATEIEN IN EINEM VERZEICHNIS AUF DEM SERVER ;within within tabulation upon 
	;	FUNKTION KANN HINTER EINEM DATENFELD IM fORMULAR AUFGERUFEN WERDEN: ;posterior data item 
	;	Z.B. DO ^WWWFILESEARCH() ODER ^WWWFILESEARCH("C:\")
	;	WENN KEIN PFAD ANGEGEBEN WIRD, DANN WIRD DER PFAD AUS DEN MANDANTENPARAMETERN GENOMMEN.
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 23-Nov-2005	GRF		SR13171 : Doco; FIXME
	; 30-May-2005	RobertW	SR12056: Attempt at Performance Increase
	; 28.07.03		BEC		Created
	;-----------------------------------------------------------------------------*/
	SET FILE=$GET(FILE)    ;VERZEICHNIS ;tabulation 
	
	/*++++++++++++++++++++++++++++++++++++++
	;	VORGM		objWWW012		General Company Parameter
	;+++++++++++++++++++++++++++++++++++++*/
 
	IF FILE="" DO   ;SETZTEN PFADVORGABE AUS MANDANTENPARAMETERN ;out of 
	. NEW VORGM
	. SET VORGM=$GET(^WWW012(0,YM,1))
	. SET FILE=$PIECE($GET(VORGM),Y,49)									; Physical WWW Directory
	. IF FILE'="" SET FILE=$PIECE(FILE,$PIECE($GET(VORGM),Y,47),1)		; Picture Directory System
	. SET FILE=FILE_$PIECE($GET(VORGM),Y,48)							; Append Picture Directory User ?????
	;                                      FIXME : Is this right??? Not used this way in WWWAMVAR or WWWVORG <GRF>
	
	IF $FIND(FILE,"\") DO  ;MASKIEREN VON "\" DA ES SONST IM JAVASCRIPTAUFRUF PROBLEME GIBT
	. NEW YI,FILE1
	. SET FILE1=""
	. FOR YI=1:1:10 DO
	. . IF '$FIND($PIECE(FILE,"\",YI,999),"\") QUIT
	. . SET FILE1=$GET(FILE1)_$PIECE(FILE,"\",YI)_"\\"
	. SET FILE=FILE1
	
	DO  ;ERSTELLEN DES BUTTONS IM FORMULAR ;form 
	. QUIT:$GET(YHID)=1
	. QUIT:$GET(YHID)=2
	. ;
	. WRITE YCR,"<A HREF="_""""
	. SET URL=YAKTION_"EP=WWWMANU1&amp;YEXEC=D|START^WWWFILESEARCH&amp;YUSER="_YUSER_"&amp;YBED="_YBED_"&amp;YTRAKT="_YTRAKT_"&amp;YUCI="_$GET(YUCI)_"&amp;YM="_YM_"&amp;YKEY="_YKEY_"&amp;YBACK="_YBACK_"&amp;YLFDAT="_"Y"_YFORM_YART_YLFN_"&amp;YLFFORM="_$GET(YBBN)_"&amp;YFORM="_YFORM_"&amp;YAUSWAHL="_FILE_"!"_"'+document."_YHTMFORM_".Y"_YFORM_YART_YLFN_".value+'"
	. SET OPT="HEIGHT=500,WIDTH=400,SCROLLBARS=YES,RESIZEABLE=YES"
	. WRITE "javascript:var parameter=window.open('"_URL_"','Stucture','"_OPT_"');"  
	. WRITE """"
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"search1.gif"_""""_" ALIGN=ABSBOTTOM TITLE="_""""_$$^WWWTEXT(148)_""""_" border=0>"
	. WRITE "</A>"
	
	QUIT
	
	
START ;AUSWERTEN VARIABELEN UND STARTEN WWWPARAM (ANZEIGE DATEN IN MENUFORM) ;And launching within 
	;AUSFÜHREN DES SUCHFENSTER NACH EINER SUCHABFRAGE ;implement within unit 
	NEW (%request,%session,%KEY,%,%ZCS,%CGIEVAR)
	
	DO ^WWWVAR  ;VORGABEN SETZEN ;typeset 
	NEW BETRIEB,DIR,SUCH
	
	SET FILE=$PIECE($GET(YAUSWAHL),"!",1)
	SET SUCH=$PIECE($GET(YAUSWAHL),"!",2)
	KILL ^WWWSOR(YUSER)
	NEW YPARA,YSORT,FILENAME
	
	SET YPARA(1)=FILE
	IF SUCH="" SET YPARA(1)=YPARA(1)_"\*"         ;SELECT ALL
	;IF SUCH'="" SET YPARA(1)=YPARA(1)_"\"_SUCH   ;GEZIELT NACH FELDINHALT  ;tybd;23,09,2003;24302
	IF SUCH'="" SET YPARA(1)=YPARA(1)_"\*"        ;GEZIELT NACH FELDINHALT  ;;tybd;23,09,2003;24302
	SET YSORT(1)=$ZSEARCH(YPARA(1))
	IF YSORT(1)'=""  FOR  SET YSORT(1)=$ZSEARCH("") QUIT:YSORT(1)=""  DO
	. QUIT:$FIND(YSORT(1),"..")   ;DIRECTORY
	. QUIT:'$FIND(YSORT(1),".")   ;NO EXPANSION OR DIR ;expansion yourself 
	. ;
	.;IF SUCH'="" IF '$FIND($$^WWWUPER(YSORT(1)),$TRANSLATE($$^WWWUPER(SUCH),"*"))       QUIT  ;;tybd;23,09,2003;24302
	. IF SUCH'="" IF '$FIND($zconvert(YSORT(1),"U"),$TRANSLATE($zconvert(SUCH,"U"),"*")) QUIT  ;;tybd;23,09,2003;24302
	. SET FILENAME=$PIECE(YSORT(1),"\",$LENGTH(YSORT(1),"\"))  ;
	. QUIT:FILENAME=""
	. SET ^WWWSOR(YUSER,FILENAME)=" "_Y_FILENAME
	
	
	/*++++++++++++++++++++++++++++++++++++++
	;	&FILE   = DATEI DIE ANGEZEIGT WIRD (STANDARD=WWWEXPLORE)  ;who 
	;	&FIX    = FESTER ERSTER KEY (NUR WENN &FILE)              ;premier KEY when 
	;	&RECORD = FELDER DIE ANGEZEIGT WERDEN SOLLEN (STANDARD=1) ;who will should 
	;	&OFFSET = STRUKTUR (WIRD GEBILDET)
	;	&HEAD   = TEXT FÜR HEADER
	;+++++++++++++++++++++++++++++++++++++*/
	
	SET %(YQUERY,"TARGET") = $GET(YTARGET)     ;TARGET DER AUSGABE ;the expenses 
	SET %(YQUERY,"FILE")   = "WWWSOR"
	SET %(YQUERY,"FIX")	   = $GET(YUSER)
	SET %(YQUERY,"RECORD") = "1"               ;FELDER, DIE ANGEZEIGT WERDEN (1,2,3,4)
	SET %(YQUERY,"YUCI")   = $GET(YUCI)        ;NAMESPACE
	SET %(YQUERY,"HEAD")   = $$^WWWTEXT(200)_" "_SUCH_" -> "_FILE         ;BETRIEB
	SET %(YQUERY,"YKEY")   = YKEY
	SET %(YQUERY,"YTRAKT") = YTRAKT
	SET %(YQUERY,"YBACK")  = YBACK
	SET %(YQUERY,"FORM")   = YFORM             ;ERFASSEN  ;Edit 
	SET %(YQUERY,"YFORM")  = YFORM             ;ERFASSEN BETRIEB ;Edit location 
	SET %(YQUERY,"OFFSET") = ""                ;ANZEIGE AB SUBSCRIPT ;Show Confirm. 
	
	MERGE %KEY=%(YQUERY)
	DO ^WWWPARAM
	QUIT
	
]]></Routine>
</Export>