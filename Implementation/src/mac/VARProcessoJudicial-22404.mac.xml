<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARProcessoJudicial" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARProcessoJudicial
#include COMSYS
#include COMSYS
#include COMConst
#include VARConst

OnBeforeButtonLine(pYKEY)
	;-------------------------------------------------------------------------------
    ; Bloqueia para somente leitura ou não.
    ;
    ; History:
    ; 20-Out-2011	Karine	Created
    ;-------------------------------------------------------------------------------	
 	new idProcesso, objProcesso, temPedido
 	
 	if pYKEY '= "" {
 	
		set temPedido = ""
		
		set idProcesso = $piece(pYKEY,",",1)
		set idPaciente = $piece(pYKEY,",",2)
		
		set objProcesso = $get(^VARProcessoJudicial(YM,pYKEY,1))
		set temPedido = $$$VARProcessoJudicialStatus(objProcesso)
		
 		if temPedido > 0  {
			set $piece(YVOR,Y,23) = $$$EnumReadOnly
		}
	}
	quit

OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnBeforeSave^VARProcessoJudicialLinha.mac  Aba Controle do Form
    ; 
    ; Histórico:
    ; 05-Out-2011		Karine:	Alterado
    ;-------------------------------------------------------------------------------	
	
	new idPaciente, noProcessoJudicial, noProcessoCompra, key, noOutrosProcs
	
	set noProcessoJudicial = $$$KEY1(pYKEY)
	set idPaciente         = $$$KEY2(pYKEY)
	set noOutrosProcs      = ""

	quit:(pYKEY = "")

	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("No. Processo Judicial","No. Processo Compra")
	
 	if $$Start^COMTable(lstHeader,"<div style=padding:4px;>Processos judiciais para o paciente "_idPaciente_" - "_$$SQLGetPatientName^VARSQL(idPaciente)_":</div>",,,$$$NO) {
	
		$$$Order4(^VARProcessoJudicials,YM,1,idPaciente,noOutrosProcs)
			
			set noProcessoCompra = $piece($get(^VARProcessoJudicial(YM,noOutrosProcs,idPaciente,1)),Y,1)
			continue:(noProcessoJudicial=noOutrosProcs)
			
			set key = noProcessoJudicial_$$$COMMA_idPaciente
			
			do NewLine^COMTable()
	    	do InsertCell^COMTable(noOutrosProcs,"VARProcessoJudicial",key)
	    	do InsertCell^COMTable(noProcessoCompra)
	    	do EndLine^COMTable()
	
		$$$End
		
		do Stop^COMTable()
	
 	}

	quit
	
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnBeforeSave^VARProcessoJudicial.mac : Aba Controle do Form
    ; 
    ; Histórico:
    ; 05-Out-2011		Karine:	Criado
    ;-------------------------------------------------------------------------------	
	
	new strStatus, idPedidoCompra, noProcesso, noProcessoAtual
	
	
	set strStatus = $$$OK
	
	
	quit strStatus
	
CanGerarPedidoCompra(pKeyProcesso)
	;-------------------------------------------------------------------------------
    ; Verifica se existe linha e dados mínimos para a geração do Pedido
    ;
    ; History:
    ; 20-Out-2011	Karine	Created
    ;-------------------------------------------------------------------------------	
	new strStatus, idProcesso, idPaciente, idProcessoLinha, objProcesso, objProcesso, temPedido
	
	set strStatus = $$$OK
	set temPedido = ""
		
	if pKeyProcesso '= "" {
		set idProcesso = $$$KEY1(pKeyProcesso)
		set idPaciente = $$$KEY2(pKeyProcesso)
		
		set objProcesso = $get(^VARProcessoJudicial(YM,idProcesso,idPaciente,1))
		set temPedido = $$$VARProcessoJudicialNoProcessodeCompra(objProcesso)
		
		if temPedido '= "" {  ;Já foi gerado pedido para essa Ação Judicial
			set YQ = $$$YQDisable("Já existe um pedido para esse Processo."_$c(10)_"Pedido No. = "_temPedido)
		}	
		elseif $data(^VARProcessoJudicialLinha(YM,idProcesso,idPaciente)) = 0 {
			set YQ = $$$YQDisable("Nenhum produto foi inserido.")
		}	
	}	
	
	quit strStatus

OnBeforeEditAcess(pKeyProcesso)
	;-------------------------------------------------------------------------------
    ; Verifica se já existe pedido de compra para esse processo Judicial
    ;
    ; History:
    ; 20-Out-2011	RGB	Created
    ;-------------------------------------------------------------------------------
    new strStatus, idProcesso, idPaciente, idProcessoLinha, objProcesso, objProcesso, temPedido
	
	set strStatus = $$$OK
	set temPedido = ""
		
	if pKeyProcesso '= "" {
		set idProcesso = $$$KEY1(pKeyProcesso)
		set idPaciente = $$$KEY2(pKeyProcesso)
		
		set objProcesso = $get(^VARProcessoJudicial(YM,idProcesso,idPaciente,1))
		set temPedido = $$$VARProcessoJudicialNoProcessodeCompra(objProcesso)
		
		if temPedido '= "" {  ;Já foi gerado pedido para essa Ação Judicial
			set strStatus = $$$NO
		}	
		else {
			set strStatus = $$$YES
		}	
	}	
	
	quit strStatus
	 
	
GerarPedidoCompra(pKeyProcesso)
	;-------------------------------------------------------------------------------
    ; Gera a Pedido de Compra (VARPedidoCompra) através do registro da Ação Judicial
    ;
    ; History:
    ; 20-Out-2011	Karine	Created
    ;-------------------------------------------------------------------------------	
	new strStatus, idProcesso, idPaciente, idProcessoLinha, objProcessoLinha, objProcesso, 
	    idPedidoCompra, objPedidoCompra, idCompraLinha, objProduto, Grupo,
		objCompraLinha, idKeyLinha, idProduto, idUnidade, precoUnitario,
		idPedidoPaciente, objPedidoPaciente
		
	set strStatus = $$$OK
		
		;IF YBED = "KARINE2" {FOR k=1:1:100 hang 1}
	if pKeyProcesso '= "" {
		;$$$Alert("Nenhum item foi selecionado.")
		
		set idProcesso = $$$KEY1(pKeyProcesso)
		set idPaciente = $$$KEY2(pKeyProcesso)
		//Cabeçalho
		set objProcesso = $get(^VARProcessoJudicial(YM,idProcesso,idPaciente,1))
		
		set idPedidoCompra = $$^WWWNEXT("VARPedidoCompra")		  ;noPedido
		
		set objPedidoCompra = ""
		set $$$VARPedidoCompraTipo(objPedidoCompra) 			  = 3    ; Ação Judicial
		set $$$VARPedidoCompraFontedeNatureza(objPedidoCompra)    = 5    ; Componente Judicial
		set $$$VARPedidoCompraLocaldeEntrega(objPedidoCompra)     = 1    ; CD Central YLOCATION?
		set $$$VARPedidoCompraTermodeReferencia(objPedidoCompra)  = $$$VARProcessoJudicialNoAcaoJudicial(objProcesso)
		set $$$VARPedidoCompraNoProcesso(objPedidoCompra)         = idProcesso
		set $$$VARPedidoCompraStatus(objPedidoCompra)             = 0 		;Status
		set $$$VARPedidoCompraDatadeEmissao(objPedidoCompra)      = $h 		;Status
		set $$$VARPedidoCompraCriadoEm(objPedidoCompra)           = $h
		set $$$VARPedidoCompraCriadoPor(objPedidoCompra)          = YBED
		set $$$VARPedidoCompraDestino(objPedidoCompra)            = $$$VARProcessoJudicialLocaldeEntrega(objProcesso)
		
		set strStatus = $$$Save("VARPedidoCompra",idPedidoCompra,objPedidoCompra,1)
		
		if $$$ISOK(strStatus) {
			
			set idProcessoLinha = ""
			
			//Linhas
			$$$Order4(^VARProcessoJudicialLinha,YM,idProcesso,idPaciente,idProcessoLinha)
			 
			;for { 
			; set idProcessoLinha = $order(^VARProcessoJudicialLinha(YM,idProcesso,idPaciente,idProcessoLinha)) 
			 ;quit:idProcessoLinha=""
				
				set objProcessoLinha  = $get(^VARProcessoJudicialLinha(YM,idProcesso,idPaciente,idProcessoLinha,1))
				
				set idProduto   = $$$VARProcessoJudicialLinhaProduto(objProcessoLinha)
				set idUnidade   = $$$VARProcessoJudicialLinhaDoseUOM(objProcessoLinha)
				set getQtdTotal = $$$VARProcessoJudicialLinhaQuantidade(objProcessoLinha)
				
				set objProduto = $get(^INART(0,idProduto,1))	
				set Grupo	   = $piece(objProduto,Y,30)
				
				set idPedidoCompraLinha = idProcessoLinha

				set objCompraLinha = ""
				set $$$VARPedidoCompraLinhaProduto(objCompraLinha)     = idProduto
				set $$$VARPedidoCompraLinhaUnidade(objCompraLinha)     = idUnidade
				set $$$VARPedidoCompraLinhaGrupo(objCompraLinha)       = Grupo 
				set $$$VARPedidoCompraLinhaQuantidade(objCompraLinha)  = getQtdTotal
			   	
		   		set idKeyLinha = idPedidoCompra_$$$COMMA_idPedidoCompraLinha
				set strStatus = $$$Save("VARPedidoCompraLinha",idKeyLinha,objCompraLinha,1)
					
			$$$End
		}
		
		//Cria o Paciente na VARPedidoCompra
		if $$$ISOK(strStatus) {
			set objPedidoPaciente = ""
			
			set idPedidoPaciente = idPedidoCompra
			set idPedidoPacienteSeq = idPedidoCompraLinha
			set $$$VARPedidoCompraPacientePaciente(objPedidoPaciente) = idPaciente
			set $$$VARPedidoCompraPacienteCriadoEm(objPedidoPaciente) = $h
			set $$$VARPedidoCompraPacienteCriadoPor(objPedidoPaciente) = YBED
			
			set idKeyLinhaPaciente = idPedidoPaciente_$$$COMMA_idPedidoPacienteSeq
			
			set strStatus = $$$Save("VARPedidoCompraPaciente",idKeyLinhaPaciente,objPedidoPaciente,1)
				
		}	
	
		if $$$ISERR(strStatus) {
			do ReturnError^COMUtilError(strStatus)
	
		} else {
			set $$$VARProcessoJudicialStatus(objProcesso) = 1
			set $$$VARProcessoJudicialNoProcessodeCompra(objProcesso) = idPedidoCompra
			
			set strStatus = $$$Save("VARProcessoJudicial",pKeyProcesso,objProcesso,1)
			
			$$$Alert("Pedido de Compra "_idPedidoCompra_" gerada com sucesso !"_$c(10)_"Favor finalizar o cadastro do Pedido de Compra.")
			do GoToForm^COMUtilForm("VARPedidoCompra",idPedidoCompra)
		}	
	}
	
	quit

CanViewReport(pYKEY)

	new objProcesso,idProcesso,idPaciente
	
	if $$$NoKey(pYKEY) {

		set YQ = $$$YQDisable("Selecione um registro.")
	
	}
	/* Processo não bem definido - pode imprimir relatório antes de solicitar a compra por enquanto
	else{
		set idProcesso = $piece(pYKEY,",",1)
		set idPaciente = $piece(pYKEY,",",2)
		set objProcesso = $get(^VARProcessoJudicial(YM,idProcesso,idPaciente,1))

     	if ($piece(objProcesso,Y,22) > 0)  {
			set YQ = $$$YQEnable
		}else{
			set YQ = $$$YQDisable("É necessário solicitar a compra para poder gerar o relatório.")
		}
	}
	*/
	 elseif '$data(^VARProcessoJudicialLinha(0,$piece(pYKEY,",",1),$piece(pYKEY,",",2))) { 
		set YQ = $$$YQDisable("Não existe linhas para o registro.")
	} else {
		set YQ = $$$YQEnable
	}	
	
	quit


ImprimeRelatorio(pYKEY)
	
	    new idProcesso,idPaciente
		
		set idProcesso = $piece(pYKEY,",",1)
		set idPaciente = $piece(pYKEY,",",2)
		
		do RunReportProcessoJudicial^VARJasperRunReport(idProcesso,idPaciente)
	
	
	quit
]]></Routine>
</Export>