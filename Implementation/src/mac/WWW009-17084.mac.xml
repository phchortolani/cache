<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW009" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW009
#include COMSYS
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		GRUPPENTEXTE MEHRSPRACHIG EINFACHER
	;	
	;	PROGRAMM WIRD AUS ARARBEITEN MONATSDATEI AUFGERUFEN ;programme out of 
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 15-Mar-2007	GM		SRBR014336: Included a call to DeleteCache^WWWDCM  
	; 23-Mar-2006	GRF		SR14203 : AfterData - show other language settings
	; 03.Jan.2003	BEC		Created
	;-----------------------------------------------------------------------------*/
	NEW
	DO ^WWWVAR
	NEW YFORM,YFELD,YINHALT,YOK
	SET YOK=0
	DO FORMULAR
	QUIT
	
OnBeforeDelete(YKEY)
	;-------------------------------------------------------------------------------
	; Delete texts in Edit Language Text form
	;
	; Params: YKEY
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Mar-2007	GM		SRBR014336: Included a call to DeleteCache^WWWDCM  
	;-------------------------------------------------------------------------------	
	
	quit $$DeleteCache^WWWDCM("CacheTempWWWTEXT",YKEY) // BR014336  
	
OnAfterSave(YKEY)
	;-------------------------------------------------------------------------------
	; Save changes in Edit Language Text form
	;
	; Params: YKEY
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Mar-2007	GM		SRBR014336: Included a call to DeleteCache^WWWDCM  
	;-------------------------------------------------------------------------------	
	
	quit $$DeleteCache^WWWDCM("CacheTempWWWTEXT",YKEY) // BR014336
	
FORMULAR  ;SUCHEN NACH GRUPPENFELDERN ;seek within 
	SET YFORM=""
	FOR  SET YFORM=$ORDER(^WWW122(0,YFORM)) QUIT:YFORM=""  DO
	. SET YFELD=""
	. FOR  SET YFELD=$ORDER(^WWW122(0,YFORM,YFELD)) QUIT:YFELD=""  DO 
	. . SET YOK=0
	. . SET YINHALT=$GET(^WWW122(0,YFORM,YFELD,1))
	. . IF $EXTRACT($PIECE(YINHALT,Y,39),1,2)="::" DO GRUPPE 
	
	QUIT
	
	;+++++++++++++++++++++++++++++++++++++++
	;  Forms can be broken up into groups of data fields which can optionally
	;  have a heading.     [D39		$$$WWW122GroupName()]
	;
	;  If that heading starts with "::"
	;  - if it is followed by some text that matches a German text entry in ^WWW009,
	;  
	;  - if not,
	;      create a new WWW009 entry
	;  
	;  - and then
	;      replace   ::strHeading
	;      with      idWWW009;strHeading   in WWW122
	;+++++++++++++++++++++++++++++++++++++++
	
GRUPPE  ;GRUPPENSPRACHE SUCHEN ;seek 
	NEW TEXTALT
	SET TEXTALT=$EXTRACT(($PIECE(YINHALT,Y,39)),3,999)
	IF $DATA(^WWW009s(0,1,$$^WWWUMLAU(TEXTALT,1),"DE")) DO SPRACHE
	IF '$DATA(^WWW009s(0,1,$$^WWWUMLAU(TEXTALT,1),"DE")) DO SPRACHENEU
	QUIT
	
SPRACHE  ;SETZTEN DER GRUPPENBEZEICHNUNG MIT SPRACHENTEXTE        ;the by means of 
	NEW TEXTNUM
	SET TEXTNUM=$ORDER(^WWW009s(0,1,$$^WWWUMLAU(TEXTALT,1),"DE",""))
	IF TEXTNUM'="" IF $DATA(^WWW009(0,"DE",TEXTNUM)) DO 
	. IF $GET(^WWW009(0,"DE",TEXTNUM,1))'="" DO
	. . SET $PIECE(YINHALT,Y,39)=TEXTNUM_";"_$GET(^WWW009(0,"DE",TEXTNUM,1))
	. . DO
	. . . NEW YKEY
	. . . set YKEY=YFORM_","_YFELD
	. . . NEW YVOR,YFORM
	. . . SET YOK=$$^WWWSPEI("WWW122",YKEY,YINHALT,1) 
	
	QUIT
	
SPRACHENEU  ;SETZTEN DER GRUPPE MIT NEUEM SPRACHENTEXTE   ;the group by means of 
	NEW TEXTNUM
	SET TEXTNUM=$$^WWWNEXT("WWW009")   ;NEUE NUMMER SPRACHENTEXT ;numeral 
	DO 
	. NEW YVOR,YKEY,YFORM
	. SET YKEY="DE"_","_TEXTNUM
	. SET YOK=$$^WWWSPEI("WWW009",YKEY,TEXTALT,1) 
	
	SET $PIECE(YINHALT,Y,39)=TEXTNUM_";"_TEXTALT
	DO 
	. NEW YKEY
	. set YKEY=YFORM_","_YFELD
	. NEW YVOR,YFORM
	. SET YOK=$$^WWWSPEI("WWW122",YKEY,YINHALT,1) 
	
	QUIT 
	
AfterData(pKey="")
	;-------------------------------------------------------------------------------
	; Show table of equivalent texts in other languages
	; 
	; History
	; 23-Mar-2006	GRF		SR14203 : Created from CSTMatrixStart
	;-------------------------------------------------------------------------------
	new lstHeader,MyLanguage,objText,TextNo,LangCode
	
	quit:$get(YUSER)=""
	
	set MyLanguage = $piece(pKey,$$$COMMA,1)
	set TextNo     = $piece(pKey,$$$COMMA,2)
	quit:TextNo=""
	
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("WWW009",,1))     ; P1
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("WWW009",1))      ; D1
	
	do Start^COMTable(lstHeader,"")
	
	set LangCode = ""
	for {
		set LangCode = $order(^WWW009(0,LangCode))
		quit:LangCode=""
		continue:LangCode=MyLanguage
		
		set objText = $get(^WWW009(0,LangCode,TextNo,1))
		if objText'="" {
			do NewLine^COMTable("")
			
			do InsertCell^COMTable(LangCode,YFORM,LangCode_","_TextNo)
			do InsertCell^COMTable(objText,YFORM,LangCode_","_TextNo)
			
			do EndLine^COMTable()
		}
	}
	
	do Stop^COMTable()
	
	quit
	
]]></Routine>
</Export>