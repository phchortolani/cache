<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESRelPagamentoEfetuado" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[VARSESRelPagamentoEfetuado
#include COMSYS

OnAfterSave
	new unidade
	set unidade = $get(VORG(1))
	set $piece(^VARSESTempRelPagamentoEfetuado(YM,YBED,1),Y,1) = unidade
	set $piece(^VARSESTempRelPagamentoEfetuado(YM,YBED,1),Y,2) = $get(VORG(2))
	set $piece(^VARSESTempRelPagamentoEfetuado(YM,YBED,1),Y,3) = $get(VORG(3))
 	do RefreshForm^COMUtilForm()
	
	quit


OnAfterDataFields

	do MontaGrid
	
	quit
	
MontaGrid
	quit:($get(VORG(1)) = "")
	
	new unidade,bimestre,ano,qtd,dataInicial,dataFinal,pLocal,descUnidade,idUnidade,ValorTotal
	
	set qtd			= 0
	set ValorTotal  = 0
	set unidade		= $get(VORG(1))
	set bimestre    = $piece($get(^VARSESTempRelPagamentoEfetuado(YM,YBED,1)),Y,2)
	set ano			= $piece($get(^VARSESTempRelPagamentoEfetuado(YM,YBED,1)),Y,3)
	
	if (bimestre=1){
		set dataInicial = ano_"-01-01"
		if (ano#4 = 0){
			set dataFinal = ano_"-02-29"
		}
		else{
			set dataFinal = ano_"-02-28"
		}
	}
	elseif (bimestre=2){
		set dataInicial = ano_"-03-01"
		set dataFinal 	= ano_"-04-30"
	}
	elseif (bimestre=3){
		set dataInicial = ano_"-05-01"
		set dataFinal 	= ano_"-06-30"
	}
	elseif (bimestre=4){
		set dataInicial = ano_"-07-01"
		set dataFinal 	= ano_"-08-31"
	}
	elseif (bimestre=5){
		set dataInicial = ano_"-09-01"
		set dataFinal 	= ano_"-10-31"
	} else{
		set dataInicial = ano_"-11-01"
		set dataFinal 	= ano_"-12-31"
	}
	
	set $piece(^VARSESTempRelPagamentoEfetuado(YM,YBED,1),Y,4) = dataInicial
	set $piece(^VARSESTempRelPagamentoEfetuado(YM,YBED,1),Y,5) = dataFinal
	
	Set RelPgto = ##class(%ResultSet).%New()
	
	set meuSQL = ""
	set meuSQL = $$MontaSQL(unidade,dataInicial,dataFinal)
	
	Do RelPgto.Prepare(meuSQL)
	Do RelPgto.Execute()
	
	do PrintHeader(unidade,bimestre,ano)
	
	//Tabela
   	new lstHeader
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Item","Processo","Fornecedor","CNPJ/CPF","Tipo","Número Doc.","Data Doc.","Cheque","Data Pgto.","Nat.","Valor")
	
	if $$Start^COMTable(lstHeader) {	
	
		While (RelPgto.Next()) {
			
			set Fornecedor   	= RelPgto.GetData(1)
			set Processo  	 	= RelPgto.GetData(2)
			set NoDocEntrada	= RelPgto.GetData(3)
			set DataNotaFiscal  = RelPgto.GetData(4)
			set NoCheque  		= RelPgto.GetData(5)
			set DatadePagamento = RelPgto.GetData(6)
			set Valor			= RelPgto.GetData(7)
			set NumerodaEntrada = RelPgto.GetData(8)
			set CNPJUnidade		= RelPgto.GetData(9)
			set Descricao       = RelPgto.GetData(10)
			set CodigodaUnidade = RelPgto.GetData(11)
			set NoProcesso		= RelPgto.GetData(12)
			
			set qtd = $increment(qtd)
			
			set descFornecedor = $$SQLGetDescFornecedor^VARSQL(Fornecedor)
			set CNPJFornecedor = $$SQLGetCNPJFornecedor^VARSQL(Fornecedor)
			set docData 	   = $$SQLGetDataFormatada^VARSQL(DataNotaFiscal)
			set PgtoData 	   = $$SQLGetDataFormatada^VARSQL(DatadePagamento)
			
			
			do NewLine^COMTable()

	    	do InsertCell^COMTable(qtd)
	    	do InsertCell^COMTable(NoProcesso)
	    	do InsertCell^COMTable(descFornecedor)
	    	do InsertCell^COMTable(CNPJFornecedor)
	    	do InsertCell^COMTable("NF")
	    	do InsertCell^COMTable(NoDocEntrada)
	    	do InsertCell^COMTable(docData)
	    	do InsertCell^COMTable(NoCheque)
	    	do InsertCell^COMTable(PgtoData)
	    	do InsertCell^COMTable("")
	    	do InsertCurrencyCell^COMTable(Valor)

			do EndLine^COMTable()
			
			set ValorTotal = ValorTotal + Valor
 	
	  	}
	  	if 'qtd {
			do NewLine^COMTable()
	    	do InsertCell^COMTable("Não existem pagamentos cadastrados para essa unidade nesse bimestre.",,,,,,11)
			do EndLine^COMTable()
	  	} else {
			do NewLine^COMTable("lightsteelblue")
			do InsertCell^COMTable("<strong>Total do Bimestre</strong>",,,,"RIGHT",,10)
			do InsertCell^COMTable("<strong>"_$$FormatCurrency^COMTable(ValorTotal)_"</strong>",,,,"RIGHT")
			
			set ValorTotaldoAno = $$GetValorTotalAno(ano,unidade)
			
			do NewLine^COMTable("lightsteelblue")
			do InsertCell^COMTable("<strong>Total do Ano</strong>",,,,"RIGHT",,10)
			do InsertCell^COMTable("<strong>"_$$FormatCurrency^COMTable(ValorTotaldoAno)_"</strong>",,,,"RIGHT")
			do EndLine^COMTable()			
		}
	
		do Stop^COMTable()	
		
	}
	
	do RelPgto.Close()
	
	set $piece(^VARSESTempRelPagamentoEfetuado(YM,YBED,1),Y,6) = qtd
		
	quit
	
PrintHeader(pidUnidade,pidBimestre,pidAno)
	new descUnidade, CNPJ
	set descUnidade = $piece($get(^VARSESUNI(0,pidUnidade,1)),"~",1)
	set CNPJ = $piece($get(^VARSESUNI(0,pidUnidade,1)),"~",17)
 	
	write "<hr>"
	write "&nbsp;<b>Unidade: "_pidUnidade_" - "_descUnidade_"</b></br>"
	write "&nbsp;<b>CNPJ: "_CNPJ_"</b></br>"
	write "&nbsp;<b>"_pidBimestre_"º Bimestre/"_pidAno_"</b>"
	write "<hr>"
	write "<table style=font-size:12px>"
	write "</table>"
	
	quit	

CanPrint()
	set YQ = $$$YQDisable("É necessário preencher a unidade, bem como todos os campos obrigatórios.")
	
	if (YKEY'=""){
		set VORG(1) = YKEY
	}elseif (YPARA'=""){
		set VORG(1) = YPARA
	}
	
	if ($get(VORG(1)) '= ""){
		set YQ = $$$YQEnable
		quit
	}
	
	quit	

Imprimir()
	set unidade	 	= $get(VORG(1))
	set bimestre 	= $get(VORG(2))
	set ano		 	= $get(VORG(3))
	set dtInicial 	= $piece($get(^VARSESTempRelPagamentoEfetuado(YM,YBED,1)),Y,4)
	set dtFinal 	= $piece($get(^VARSESTempRelPagamentoEfetuado(YM,YBED,1)),Y,5)
	set qtd			= $piece($get(^VARSESTempRelPagamentoEfetuado(YM,YBED,1)),Y,6)
	
	if (unidade = "")||(qtd = 0) {
		$$$Alert("Não existem pagamentos cadastrados para essa unidade nesse bimestre.")
		do RefreshForm^COMUtilForm()
	}else{
		do RunReportRelPagamentoEfetuado^VARJasperRunReportContinuacao(unidade,bimestre,ano,dtInicial,dtFinal)
	}
	
	quit
	
MontaSQL(unidade,dataInicial,dataFinal)
	new meuSQL
	set meuSQL = ""
		
	set meuSQL = "SELECT "
	set meuSQL = meuSQL_" INREC.Supplier as Fornecedor, "
	set meuSQL = meuSQL_" INREC.FREE10 as NoProcesso, "
	set meuSQL = meuSQL_" INREC.InvoiceNumber as DocNumero, "
	set meuSQL = meuSQL_" INREC.InvoiceDate as DocData, "
	set meuSQL = meuSQL_" NULL as Cheque, "
	set meuSQL = meuSQL_" INREC.FREE13 as PgtoData, "
	set meuSQL = meuSQL_" INREC.SummaryTotal as Valor, "
	set meuSQL = meuSQL_" INREC.ReceiptNumber as NumerodaEntrada, "
	set meuSQL = meuSQL_" Uni.CNPJ, "
	set meuSQL = meuSQL_" Uni.Descricao, "
	set meuSQL = meuSQL_" Uni.CodigodaUnidade, "
	set meuSQL = meuSQL_" Compra.NoProcesso as NumerodoProcesso "
	
	set meuSQL = meuSQL_" FROM INREC INREC "
	
	set meuSQL = meuSQL_" INNER JOIN VARSESUNI Uni "
	set meuSQL = meuSQL_" ON $$SQLGetIDUnidadeSES^VARSQL(INREC.Location) = Uni.CodigodaUnidade "
	set meuSQL = meuSQL_" AND INREC.Company = Uni.Company "
	
	set meuSQL = meuSQL_" LEFT JOIN VARSESCOMPRAS Compra "
	set meuSQL = meuSQL_" ON INREC.FREE10 = Compra.NoProcessoCompra "
	set meuSQL = meuSQL_" AND INREC.Company = Compra.Company "
	
	set meuSQL = meuSQL_" WHERE INREC.Company = 0 "
	set meuSQL = meuSQL_" AND INREC.InvoiceDate IS NOT NULL "
	set meuSQL = meuSQL_" AND INREC.Status <> 0 "
	set meuSQL = meuSQL_" AND Uni.CodigodaUnidade = '"_unidade_"' "
	set meuSQL = meuSQL_" AND DATEDIFF('dd',INREC.InvoiceDate,'"_dataInicial_"') <= 0 "
	set meuSQL = meuSQL_" AND DATEDIFF('dd',INREC.InvoiceDate,'"_dataFinal_"') >= 0 "
	
	set meuSQL = meuSQL_" GROUP BY INREC.ReceiptNumber "
	set meuSQL = meuSQL_" ORDER BY INREC.InvoiceDate,INREC.FREE10 "
	
	quit meuSQL
	
GetValorTotalAno(ano,unidade)
	new dtInicial,dtFinal,TotaldoAno
	set TotaldoAno = 0
	set dtInicial = ano_"-01-01"
	set dtFinal   = ano_"-12-31"
	
	Set valorTotalAno = ##class(%ResultSet).%New()
	
	set totalSQL = ""
	set totalSQL = $$MontaSQL(unidade,dtInicial,dtFinal)
	
	Do valorTotalAno.Prepare(totalSQL)
	Do valorTotalAno.Execute()
	
	While (valorTotalAno.Next()) {
		set Valor = valorTotalAno.GetData(7)
		set TotaldoAno = TotaldoAno + Valor
	}
	
	quit TotaldoAno]]></Routine>
</Export>