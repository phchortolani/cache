<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWVORG" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWVORG
#include COMSYS
#include WWWConst
	;------------------------------------------------------------------------------
	; Description of Function :
	;		VORGABEN WWW ; WWW Defaults.
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 14-Jan-2008	FIS		SR15619: replaced it again -> moved to WWW012
	; 14-Jan-2008	GRF		SR15619: base on WWW012 not WWW120; boolean macros
	; 10-Jan-2008	FIS		SR15619: set "framedform" in WWW012 always to 1
	; 11-Sep-2007	shobby	SRBR014713: YTARGET and YTARGET2 should be preserved.
	; 23-May-2007	GRF		SR15525: Call common code and default UNKNOWN should be
	; 						"EN" and dot, not "EN" and comma;  move YBED up and use.
	; 13-Mar-2007	RPW		SR15408: How many times do we need to get YM
	; 20-Feb-2007	GRF		SR15448: YAKTION to use www.cls only
	; 23-Nov-2005	GRF		SR13171: Doco; Macro
	; 27-Jul-2005	FIS		SR12390: Get old session values
	; 30-May-2005	RobertW	SR12056: Attempt at Performance Increase
	; 29.07.1997	DT
	;-------------------------------------------------------------------------------
	
	;INTERSYSTEMS CACHE WEBLINK / CSP
	SET Y       = "~"
	SET %KEY    = ""
	SET YAKTION = ""
	SET YQUERY  = "%KEY"
	IF $GET(YHYPER)=""          SET YHYPER=0          ;KEIN HYPEREVENT SONDERN EVENTBROKER WENN NICHT GESETZT ;no when Not staid 
	IF $GET(^WWW012(0,0,1))=""  SET ^WWW012(0,0,1)="MANDANT~~~~123~8~2~~2~~Inhaltsverzeichnis,Table of Contents,Table des matières~8~1~51~138~79~3~1~FRAME22~~~1~1~1~KEY~A~VV~MK1~~~3~~~~~~~~127.0.0.1~127.0.0.1~110~25~ihreDomain.com~http://127.0.0.1~C:/inetpub/ftproot/~127.0.0.1/~/bin/~/bin/~C:/inetpub/wwwroot/bin/~~~~~100~100~~~137~~~~~2~67~1~0~1~c:/inetpub/ftproot/Austausch/~~0~1~~,~info@intraprend.de~EUR~0~69~~~~~1~~Postmaster~admin~~~~~~2~~~~~~http://translator.abacho.de?ab_cmd=translate&lp=de_en&urltext=~C:/inetpub/wwwroot/dtd/~~~96~html.gif~~~~~~~~25~137~69"
	IF $GET(YM)=""              SET YM=$ORDER(^WWW012(0,""))  ;MANDANT
	IF (YM="")||($ascii(YM)=0)  SET YM=0
	
	IF +YM'=YM IF +YM=0         SET @YM=YM  ;TYBD;17,12,2003;TYBD;22,07,2003;WENN MANDANT ALPHANUM, DANN VARIABLE BILDEN. ZB. YM=TEST DANN TEST="TEST"
	IF $GET(%(YQUERY,"YM"))'="" SET YM=%(YQUERY,"YM")  ;VORGABE MANDANT AUS VORGABE ;default Company out of default 
	
	;USER VORGABE ZURÜCKHOLEN;FIS;06.05.04;25460
	;WIRD EINE ROUTINE MIT JOB AUFGERUFEN, SIND DIE CSP-OBJEKTE NICHT MEHR VORHANDEN. IN DIESEM FALL
	;KANN ES WÜNSCHENSWERT SEIN, DIE CGI-VARIABLEN ZURPÜCKZUHOLEN (SPEICHERUNG IN WWWVAR)
	IF YHYPER'=0 IF $GET(%request)="" IF ($GET(YUSER)'="")||($GET(%(YQUERY,"YUSER"))'="") DO
	. NEW YUSER,YVAR
	. IF $GET(YUSER)="" SET YUSER=$GET(%(YQUERY,"YUSER"))
	. IF YUSER'="" IF $DATA(^WWWUSER1(0,YUSER)) DO
	. . FOR YVAR="%KEY","%CGIEVAR" DO
	. . . QUIT:$DATA(@YVAR)  ;VARIABLEN BEREITS VORHANDEN ;yet on hand 
	. . . SET YVAR(1)=""
	. . . FOR  SET YVAR(1)=$ORDER(^WWWUSER1(0,YUSER,YVAR,YVAR(1))) QUIT:YVAR(1)=""  DO
	. . . . IF $ORDER(^WWWUSER1(0,YUSER,YVAR,YVAR(1),""))="" SET YEXEC="SET "_YVAR_"("_""""_YVAR(1)_""""_")="_""""_$GET(^WWWUSER1(0,YUSER,YVAR,YVAR(1)))_"""" XECUTE YEXEC QUIT
	. . . . SET YVAR(2)=""
	. . . . FOR  SET YVAR(2)=$ORDER(^WWWUSER1(0,YUSER,YVAR,YVAR(1),YVAR(2))) QUIT:YVAR(2)=""  DO
	. . . . . SET YEXEC="SET "_YVAR_"("_""""_YVAR(1)_""""_","_""""_YVAR(2)_""""_")="_""""_$GET(^WWWUSER1(0,YUSER,YVAR,YVAR(1),YVAR(2)))_""""
	. . . . . XECUTE YEXEC
	
	;-------------------------------------------------------------------------------
	; A link such as
	;	http://server/csp/dev/www.cls?EP=WWWHELP&YFORM=INExample&YX=&YUCI=TEST&YBED=AAA&YUSER=1355x75&YUCI=QA
	; 
	; will return the following
	;	%request.Data("EP",1)     = "WWWHELP"
	;	%request.Data("YBED",1)   = "AAA"
	;	%request.Data("YFORM",1)  = "INExample"
	;	%request.Data("YUCI",1)   = "TEST"
	;	%request.Data("YUCI",2)   = "QA"         ; multiple instances not normally found
	;	%request.Data("YUSER",1)  = "1355x75"
	;	%request.Data("YX",1)     = ""           ; parameters are not normally generated by WWWCGI when value is null
	;	
	;	NOTE : If a null entry does exist, it will not clear any prior array value.
	;	       If a request with multiple instances is received, subsequent single
	;	       instance requests will not clear the second level array values.
	;-------------------------------------------------------------------------------
	if $get(%request)'="",$get(%request.CgiEnvs("HTTP_ID_KEY"))'=""{
		try{
			set prm=##class(%CSP.Page).Decrypt(%request.CgiEnvs("HTTP_ID_KEY"))
		}
		catch(e){
			set prm = ""
		}
		for piece=1:1:$length(prm,"&"){
			set campo=$piece(prm,"&",piece)
			set param=$piece(campo,"=",1)
			set valor=$piece(campo,"=",2)
			if param="" continue
			set %request.Data(param,1)=valor
		}
	}
	IF '$DATA(%KEY("HYPEREVENT")) IF $GET(%request)'="" IF $GET(%request.Data("EP",1))'="" DO
	. SET YHYPER=1
	. NEW YVAR
	. SET YVAR=""
	. FOR  SET YVAR=$ORDER(%request.Data(YVAR)) QUIT:YVAR=""  DO
	. . ; single subscript
	. . IF $GET(%request.Data(YVAR))'=""    SET %KEY(YVAR)=$GET(%request.Data(YVAR))   QUIT
	. . IF $ORDER(%request.Data(YVAR,1))="" SET %KEY(YVAR)=$GET(%request.Data(YVAR,1)) QUIT
	. . ; multiple instances
	. . SET YVAR(1)=""
	. . FOR  SET YVAR(1)=$ORDER(%request.Data(YVAR,YVAR(1))) QUIT:YVAR(1)=""  DO
	. . . SET %KEY(YVAR,YVAR(1))=%request.Data(YVAR,YVAR(1))
	. ;
	. SET YVAR=""
	. FOR  SET YVAR=$ORDER(%request.CgiEnvs(YVAR)) QUIT:YVAR=""  DO
	. . ; single subscript
	. . IF $GET(%request.CgiEnvs(YVAR))'=""    SET %CGIEVAR(YVAR)=$GET(%request.CgiEnvs(YVAR))   QUIT
	. . IF $ORDER(%request.CgiEnvs(YVAR,1))="" SET %CGIEVAR(YVAR)=$GET(%request.CgiEnvs(YVAR,1)) QUIT
	. . ; multiple instances
	. . SET YVAR(1)=""
	. . FOR  SET YVAR(1)=$ORDER(%request.CgiEnvs(YVAR,YVAR(1))) QUIT:YVAR(1)=""  DO
	. . . SET %CGIEVAR(YVAR,YVAR(1))=%request.CgiEnvs(YVAR,YVAR(1))
	. ;
	. NEW MIME,MIMES,len,YIII
	. IF $GET(%request.Data("EP",1))="WWWXMLSERVER" DO READXML QUIT  ;XML
	
	SET YIPADDR=$$^WWWIP1($GET(%CGIEVAR("REMOTE_ADDR")))
	
	
	;TEST!!
	;S ^TEST($H)=%session.CSPSessionCookie_"  "_##class(%CSP.Page).EscapeURL(##class(%CSP.Page).Encrypt("User.www.HyperEvent"))
	
	
	DO
	. ;NEW MGWLPN,MGWLIB
	. MERGE %KEY=%("VAR")
	. MERGE %(YQUERY)=%KEY
	. SET MGWLPN=$GET(%KEY("MGWLPN"))            ; INTERSYSTEMS CACHE WEBLINK (obsolete?)
	. SET MGWLIB=$GET(%KEY("MGWLIB"))
	. ;IF YHYPER'=1 IF MGWLPN="" SET MGWLPN="LOCAL"
	. ;IF YHYPER'=1 IF MGWLIB="" SET MGWLIB="/scripts/mgwms32.dll"
	. IF MGWLIB'="" IF MGWLPN'="" SET YAKTION=MGWLIB_"?MGWLPN="_MGWLPN_"&" QUIT
	. ;
	. ;-------------------------------------
	. ; FIXME : <GRF> What if suffix is ".html" - can end up with ".clsl"
	. ;-------------------------------------
	. ;
	. IF $FIND($GET(%CGIEVAR("HTTP_REFERER")),".htm") SET %CGIEVAR("HTTP_REFERER")=$PIECE($PIECE($GET(%CGIEVAR("HTTP_REFERER")),"?",1),".htm",1)_".cls"
	. ;
	. ;-------------------------------------
	. ; SR15448  start
	. ;SET YAKTION="/csp/"_$PIECE($PIECE($GET(%CGIEVAR("HTTP_REFERER")),"?",1),"/csp/",2)_"?"  ;csp
	. ;IF $GET(%KEY("YUCI"))="" SET %KEY("YUCI")=$ZUTIL(5)                  ;TYBD;23.11.2003
	. ;IF $GET(%KEY("YUCI"))="" SET %KEY("YUCI")="USER"                     ;TYBD;23.11.2003
	. ;;SET YAKTION="/csp/"_$$^WWWLOW($GET(%KEY("YUCI")))_"/www.cls?"
	. ;SET YAKTION="/csp/"_$zconvert($GET(%KEY("YUCI")),"L")_"/www.cls?"
	. ;-------------------------------------
	. set YAKTION="www.cls?"
	. ; SR15448  end
	. ;-------------------------------------
	. SET YHYPER=1  ;HYPEREVENT
	. IF $GET(%ZCS("USE_PORT"))="" SET %ZCS("USE_PORT")=""
	. ;IF '$FIND($$^WWWUPER($IO),":\")   SET %ZCS("USE_PORT")="U "_""""_$IO_""""
	. IF '$FIND($zconvert($IO,"U"),":\") SET %ZCS("USE_PORT")="U "_""""_$IO_""""
	. SET %ZCS("FLUSH")=""     ;"W *-3"
	
	MERGE %(YQUERY)=%("VAR")             ; unnecessary duplication? <GRF>
	KILL %("VAR")
	SET YKOMMA = ","                     ; use $$$COMMA instead?
	SET YNULL  = ""                      ; not used?
	SET YPAGE  = 70
	SET YCR	   = $CHAR(13,10)
	;SET YAM     = "&"             ;FUER '_Button' WENN AKTIV
	SET YAM    = ""
	
	//IF $GET(%(YQUERY,"YM"))'="" SET YM=%(YQUERY,"YM")  ;VORGABE MANDANT AUS VORGABE ;default Company out of default // SR15408
	
	/*++++++++++++++++++++++++++++++++++++++
	;	YVOR1		objWWW012		General Company Parameter
	;+++++++++++++++++++++++++++++++++++++*/
	
	SET YVOR1=$GET(^WWW012(0,YM,1))
	
	;IF +$$$WWW120DisplayFrames(YVOR1)'=1 DO  ;SR15619; set "framed forms" always to 1  ; revised 14-Jan-2008 vvv
	;. set $piece(^WWW012(0,YM,1),Y,13)=1
	;. set $piece(YVOR1,Y,13)=1
	
	/*if $$$WWW012FormsFramed(YVOR1)'=$$$YES {  ;SR15619; set "framed forms" always to 1
		set $$$WWW012FormsFramed(^WWW012(0,YM,1)) = $$$YES
		set $$$WWW012FormsFramed(YVOR1)           = $$$YES
	}*/
	
	DO  ;ZUSAMMENBAU VARIABLE MIT SONDERZEICHEN FÜR VERGLEICH IN WWWUMLAU;FIS;24771;08.12.03
	. SET YUMLAU=""
	. IF $$$WWW012FastConversionOfSortIndex(YVOR1)=$$$YES QUIT  ; D165  KEINE UMLAUTE;TYBD;6,10,2004;26526
	. NEW CHAR
	. SET CHAR=""
	. FOR  SET CHAR=$ORDER(^WWWUMLAU(0,CHAR)) QUIT:CHAR=""  SET YUMLAU=YUMLAU_CHAR_","
	. IF YUMLAU'="" SET YUMLAU=$EXTRACT(YUMLAU,1,$LENGTH(YUMLAU)-1)  ;OHNE, AM ENDE
	
	;SET YTARGET  = $PIECE($PIECE(YVOR1,Y,19),"/",1)_YUSER
	;SET YTARGET2 = $PIECE($PIECE(YVOR1,Y,19),"/",2)
	
	; Do Not Display the Header (D65)
	; This is always being reset to NO
	IF $$$WWW012DoNotDisplayHeader(YVOR1)=$$$YES SET $$$WWW012DoNotDisplayHeader(^WWW012(0,YM,1))=$$$NO
	
	SET YFIXHEADER = +$$$WWW012FixedHeader(YVOR1)  ; D115  FIXED HEADER
	
	SET YWHR  = $$$WWW012StandardCurrency(YVOR1)   ; D75  STANDARD WÄHRUNGSKZ   ; Standard Currency
	
	/*--------------------------------------
	;  Colours   -   Farbe
	;                   D64     Font Color Mandatory Input      ; $$$WWW012FontColorMandatoryInput
	;	YSILVER			D77		Frame Body Colour
	;	YBLUE			D101	Colour Code For Header Left		; STANDARD GRAU IN FORMS 
	;	YDARKGRAY		D110	Colour Table Heading			; STANDARD LISTENKOPF
	;	YWHITE			D111	Column Colour Bright			; STANDARD WEISS IN TABELLEN
	;	YGRAY (Silver)	D112	Column Colour Dark				; STANDARD GRAU IN TABELLEN
	;	YRED			D116	Warning Colour					; STANDARD ROT FÜR WARNHINWEISE
	;	YLIGHTGREY		D119	Fieldset Frame Colour			; STANDARD GRAU FÜR RAHMENFARBE
	;-------------------------------------*/
	SET YBLUE = $PIECE(YVOR1,Y,101)
	IF YBLUE'="" SET YBLUE=$PIECE($GET(^WWW100(0,"FARBE","EN",YBLUE,1)),Y,1)
	IF YBLUE=""  SET YBLUE="BLUE"
	
	SET YSILVER=$PIECE(YVOR1,Y,77)
	IF YSILVER'="" SET YSILVER=$PIECE($GET(^WWW100(0,"FARBE","EN",YSILVER,1)),Y,1) 
	IF YSILVER=""  SET YSILVER="SILVER"
	
	SET YDARKGRAY=$PIECE(YVOR1,Y,110)
	IF YDARKGRAY'="" SET YDARKGRAY=$PIECE($GET(^WWW100(0,"FARBE","EN",YDARKGRAY,1)),Y,1) 
	IF YDARKGRAY=""  SET YDARKGRAY="DARKGRAY"
	
	SET YWHITE=$PIECE(YVOR1,Y,111)
	IF YWHITE'="" SET YWHITE=$PIECE($GET(^WWW100(0,"FARBE","EN",YWHITE,1)),Y,1) 
	IF YWHITE=""  SET YWHITE="WHITE"
	
	SET YGRAY=$PIECE(YVOR1,Y,112) 
	IF YGRAY'="" SET YGRAY=$PIECE($GET(^WWW100(0,"FARBE","EN",YGRAY,1)),Y,1) 
	IF YGRAY=""  SET YGRAY=YSILVER
	
	SET YRED=$PIECE(YVOR1,Y,116) 
	IF YRED'="" SET YRED=$PIECE($GET(^WWW100(0,"FARBE","EN",YRED,1)),Y,1) 
	IF YRED=""  SET YRED="RED"
	
	SET YLIGHTGREY=$PIECE(YVOR1,Y,119) 
	IF YLIGHTGREY'="" SET YLIGHTGREY=$PIECE($GET(^WWW100(0,"FARBE","EN",YLIGHTGREY,1)),Y,1) 
	IF YLIGHTGREY=""  SET YLIGHTGREY="LIGHTGREY"
	
	/*----------------------------------------------------------------------------*/
	
	SET YLINKCOL = ""                     ;FARBE FÃR LINK (DFLT. = BLAU)
	SET YDREID   = +$$$WWW012GroupingFrameIn2Colors(YVOR1)   ; D157  TWO-TONE FRAME  RAHMEN ZWEIFARBIG (3D-EFFEKT)
	SET YMANDANT =  $$$WWW012UniqueCompanyIdentifier(YVOR1)  ; D94
	SET YPAGE	 =  $$$WWW012PrinterPageLength(YVOR1)        ; D16  FORMULARLNG  SEITENLÄNGE
	IF YPAGE="" SET YPAGE = 70
	
	; SSL Activation - use HTTPS: rather than HTTP:
	;---------------------------------------
	SET YSSL=""
	IF $$$WWW012SSLActivation(YVOR1)=$$$YES SET YSSL="s"    ; D33
	SET YURL     =  $$$WWW012VirtualWWWDirectory(YVOR1)     ; D44  VORGABE FÜR URL ;default to URL 
	IF (YSSL="s") && '$FIND($zconvert(YURL,"U"),"HTTPS") {
		SET:$FIND(YURL,"HTTP") YURL=$PIECE(YURL,"HTTP",1)_"HTTPS"_$PIECE(YURL,"HTTP",2)
		SET:$FIND(YURL,"http") YURL=$PIECE(YURL,"http",1)_"https"_$PIECE(YURL,"http",2)
	}
	
	/* Picture Directories
	;-------------------------------------*/
	SET YGIF  = $$$WWW012PictureDirectorySystem(YVOR1)     ; D47  WWW GIF SYSTEM
	SET YGIF1 = $$$WWW012PictureDirectoryUser(YVOR1)       ; D48  WWW GIF ANWENDERN
	IF YGIF1="" SET YGIF1 = YGIF
	
	/* Default Language/Decimals        ;SPRACHVORGABE
	;-------------------------------------*/
	if $get(SPRACHE)="" set SPRACHE="EN"
	set LANGUAGE    = SPRACHE
	set YDECIMALLEN = 2
	set YDECIMAL    = "."
	if $$$WWW012DecimalSigns(YVOR1)'="" set YDECIMAL = $$$WWW012DecimalSigns(YVOR1)    ; D73
	
	/* D79/80  Button Width and Height  Normal 24/22
	;-------------------------------------*/
	SET YWIDTH=""
	IF $$$WWW012ButtonWidth(YVOR1)>4  SET YWIDTH ="WIDTH="_+$$$WWW012ButtonWidth(YVOR1)
	SET YHEIGHT=""
	IF $$$WWW012ButtonHeight(YVOR1)>4 SET YHEIGHT="HEIGHT="_+$$$WWW012ButtonHeight(YVOR1)
	
	/* Authorisations - Don't proceed if no user
	;-------------------------------------*/
	SET YBEDBER=""
	SET YBEDMOD=""
	
	QUIT:$GET(%(YQUERY,"YBED"))=""                    ; User Code (BEDIENER)
	
	/******************************************************************************/
	
	set YBED      = $get(%(YQUERY,"YBED"))
	SET YLOCATION = $PIECE($GET(^WWW013(0,YBED,1)),Y,44)  ;BETRIEB      ; User's Home Location
	SET YUSER     = $GET(%(YQUERY,"YUSER"))               ;BEDIENER     ; User Session Number
	
	IF YUSER'="" DO   ;GET OLD SESSION VALUES;FIS;27-Jul-2005;SR12390
	. NEW YUSER1
	. SET YUSER1=$GET(^WWWUSER(0,YUSER,1))
	. IF $PIECE(YUSER1,Y,20)'="" SET YM        = $PIECE(YUSER1,Y,20)   ;GET CURRENT COMPANY // Can this ever be different from above?
	. IF $PIECE(YUSER1,Y,21)'="" SET YLOCATION = $PIECE(YUSER1,Y,21)   ;GET CURRENT LOCATION
	. SET YTARGET  = $PIECE($PIECE(YVOR1,Y,19),"/",1)_YUSER  ;BR014713
	. SET YTARGET2 = $PIECE($PIECE(YVOR1,Y,19),"/",2)		 ;BR014713
	
	SET YBEDBER = $$^WWWBEDBER(YBED)             ; BERECHTIGUNG       ; user authorisation
	SET YBEDMOD = $$^WWWBEDMOD(YBED)             ; BERECHTIGUNG MODUL ; user module authorisation
	IF +YBEDBER'=0 SET $ZERROR="^WWWERROR"       ; ERRORTRAPING
	
	; Revise Language/Decimals
	if YBED'="" set SPRACHE = $$^WWWLANGU(YBED)
	set LANGUAGE = SPRACHE
	set YDECIMAL = $extract($$GetNumberDelimiters^COMUtilLocale(SPRACHE),2)
	
	quit
	
	
READXML ;lesen content, SAVE AS XML
	NEW YIII,FILENAME,%DEV
	
	; D98	Physical DTD Table
	SET YIII=0
	SET YFILENAME=$PIECE($GET(^WWW012(0,YM,1)),Y,98)_"XMLIMPORT"_$JOB_".XML"  ;ZWISCHENSPEICHERN
	SET %DEV=$$^WWWDEV(YFILENAME)
	IF %DEV'="" USE %DEV         ;USE DER FILE ;the 
	WHILE (%request.Content.AtEnd = 0) {
		set len = 30000
		SET YIII=YIII+1
		SET %KEY("CONTENT",YIII)=%request.Content.Read(.len)
		WRITE %KEY("CONTENT",YIII)
	}
	IF %DEV'="" CLOSE %DEV         ;CLOSE DER FILE ;the 
	DO OPEN^WWWSTART
	QUIT
	
]]></Routine>
</Export>