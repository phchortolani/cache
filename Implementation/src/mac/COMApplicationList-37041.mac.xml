<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMApplicationList" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMApplicationList
	
#include COMConst
	
OnAfterButtonLine()
    ;-------------------------------------------------------------------------------
    ; Just produce a COMTempList with a list of number equaling the count of applications
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Jun-2005	RPW		Created SR12280
    ;-------------------------------------------------------------------------------
	new strApplication,arrApplication,strData,intCount
 
	kill ^COMTempList(YM,YUSER,"COMAPPLICATIONLISTORDER")
	
	set strApplication = ""
	for {
		set strApplication = $order(^WWW004(0,strApplication))
		quit:strApplication=""
		set arrApplication(strApplication) = ""
	}
	
	set intCount = 0
	set strApplication = ""
	for {
		set strApplication = $order(arrApplication(strApplication))
		quit:strApplication=""
		
		set intCount = $increment(intCount)
		
		set strData = ""
		set $$$COMTempListField1(strData) = intCount
		set ^COMTempList(YM,YUSER,"COMAPPLICATIONLISTORDER",intCount,1) = strData
	}
	
	quit
	
CheckAll()
    ;-------------------------------------------------------------------------------
    ; Check all for 'include in build'
    ;
    ; Returns:
    ;
    ; History:
    ; 05-Jul-2005	SteveS	Created
    ;-------------------------------------------------------------------------------
	new idApplication,objApplication,strStatus
	
	set strStatus = $$$OK
	
	set idApplication = ""
	for {
		set idApplication = $order(^COMApplicationList(YM,idApplication))
		quit:(idApplication="")||($$$ISERR(strStatus))
		
		set objApplication = $get(^COMApplicationList(YM,idApplication,1))
		set $$$COMApplicationListIncludeInBuild(objApplication) = $$$YES
		set strStatus = $$$Save("COMApplicationList",idApplication,objApplication,$$$YES)
	}
 
	if $$$ISOK(strStatus) {
		do ^WWWFORM
	} else {
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
	
	
ClearAll()
    ;-------------------------------------------------------------------------------
    ; Clear all for 'include in build'
    ;
    ; Returns:
    ;
    ; History:
    ; 05-Jul-2005	SteveS	Created
    ;-------------------------------------------------------------------------------
	new idApplication,objApplication,strStatus
	
	set strStatus = $$$OK
	
	set idApplication = ""
	for {
		set idApplication = $order(^COMApplicationList(YM,idApplication))
		quit:(idApplication="")||($$$ISERR(strStatus))
		
		set objApplication = $get(^COMApplicationList(YM,idApplication,1))
		set $$$COMApplicationListIncludeInBuild(objApplication) = $$$NO
		set strStatus = $$$Save("COMApplicationList",idApplication,objApplication,$$$YES)
	}
 
	if $$$ISOK(strStatus) {
		do ^WWWFORM
	} else {
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
	
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Show the Schedule Setup
	;
	; Called By: Form COMApplicationList : Execute after Data Fields
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Sep-2006	RPW		Created
	;-------------------------------------------------------------------------------
	do AfterDataField^COMSchedule(1,"COMNightlyBuild","NightlyBuild^COMBuild()")
	quit
]]></Routine>
</Export>