<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM6NEW" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[WWWFORM6NEW

	;	YINPAGE	:	Currently selected tab?
	;	YXS		:	Picture tab
	
#include COMSYS
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SEITENPARAMETER
	;
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 28-Jun-2012:shobby: Rewrite of WWWFORM6
	;-------------------------------------------------------------------------------
	new YLFN,YRLANG,YREITER
	
	if $get(YINPAGE)'=1 do Style() do MAIN
	if $get(YINPAGE)=1  do INPAGE^WWWFORM6
	quit
	
MAIN
	;-------------------------------------------------------------------------------
	; Tabs          ; HAUPT SEITE
	; 
	; History:
	; 23-Aug-2013	shobby	CORE-239:	Rewrite to improve display with more than one line of tabs
	; 03-Sep-2012	shobby	SR18046.1:  Correction if no tabs are defined for the current language.
	; 17-Aug-2012	shobby	SR18046:	Corrected previous correction.
	; 16-Aug-2012	shobby	SR18046:	Corrected a problem if the first tab is hidden.
	; 28-Jun-2012	shobby  SR18046: 	Rewrite of WWWFORM6
	; 25-Oct-2006	RPW		SRBR014072: removed some YCR's, also find the first tab
	; 							if needed.
	; 25-Jun-2006	shobby	SR014072: Customisation of tab properties WWW1203D
	; 11-May-2006	FrankF	SR14556: Customize Tabs
	;-------------------------------------------------------------------------------
	new strDrawOrder,intLength,YPAGEPOSITION,idSite,intActivePagePosition,i,strDrawOrder1,i2,YNEWROW
	
	set strDrawOrder=""
	set YNEWROW=0
	
	set strDrawOrder1 = $$GetDisplayOrder^WWWFORM6(YFORM,SPRACHE)
	;for i=1:1:10 set strDrawOrder=$replace(strDrawOrder,Y_Y,Y) ; strip out non-displayed tabs 
	for i=1:1:$length(strDrawOrder1,Y) {
		if $piece(strDrawOrder1,Y,i)'="" {
			set $piece(strDrawOrder,Y,$zincr(i2))=$piece(strDrawOrder1,Y,i)
		}
	}
	set intLength    = $length(strDrawOrder,Y)
		
	set idSite = $$FindFirstTab^WWW1203(YFORM,SPRACHE,YSEITE)
	if (YSEITE=1) && (idSite'=1) {
		set YSEITE = idSite
	}
	
	write YCR,"<DIV class='tabDIV'> "
		
	set YLFN=""
	
	set YRLANG=0

	set intActivePagePosition=0		;SR18046.1
	for YPAGEPOSITION=1:1:intLength { ;CORE-239
		set YLFN=$piece(strDrawOrder,Y,YPAGEPOSITION)
		do DRAWSEITE
	}
	
	write YCR,"</DIV>" ;SR18046.1
	quit

DRAWSEITE
	; 03-Sep-2012	shobby	SR18046.1:  Correction if no tabs are defined for the current language.
	if YLFN'="" {  ;SR18046.1
		if $data(^WWW122s(0,1,YLFN,YFORM)) || $data(^WWW1203(0,YFORM,SPRACHE,YLFN)) {
			set YREITER=YLFN 
			do SEITE
		}
	}
	quit
	
		
SEITE 
	;-------------------------------------------------------------------------------
	; PAGE HAUPTSEITE MIT BILDERN ;page by means of
	; 
	; History:
	; 23-Aug-2013	shobby	CORE-239:	Rewrite to improve display with more than one line of tabs
	; 20-Aug-2013	shobby	CORE-223.1: Improved appearance if Tab text is broken on two lines.
	; 25-Oct-2006	RPW		SRBR014072: Removed unused code
	; 25-Jun-2006	shobby	SRBR014072: Customisation of tabs.
	; 11-May-2006	FrankF	SR14556: Customize Tabs
	;-------------------------------------------------------------------------------
	new TOOLTIP,PAGE,strImage,FONTCOLOR,FONTWEIGHT,TABWIDTH,BACKGROUNDCOLOR,FONTSIZE,OVERLAP,TABSTYLE
	
	set PAGE = $$GET^WWW1203(YFORM,SPRACHE,YREITER)
	
	set TOOLTIP=$piece(PAGE,Y,3)


	set YYSEIT=$piece(PAGE,Y,1)
	
	set strImage=$$GetTabImage(YLFN=YSEITE,.FONTCOLOR,.FONTWEIGHT,.TABWIDTH,.BACKGROUNDCOLOR,.FONTSIZE,.OVERLAP,.TABSTYLE)

	if YYSEIT="" {
		if SPRACHE="DE"  set YYSEIT="seite"_$piece(YREITER,".",1)_".gif"   ; FIXME : Internationalise or standardise?
		if SPRACHE'="DE" set YYSEIT="page"_$piece(YREITER,".",1)_".gif"
	}

	if $piece(PAGE,Y,2)=1 {
		write YCR,"</DIV><DIV class='tabDIV' style='position:relative; top:0px;'>"
		set YNEWROW=YPAGEPOSITION
	}
	if $$MenuType^WWW013()=14{
		write YCR,"<DIV class='tabDIVInner "_$select(YLFN=YSEITE:"selected",1:"")_"' style='left:"_((-OVERLAP)*(YPAGEPOSITION-YNEWROW-1))_"px; position:relative; width:"_TABWIDTH_";z-index:"_$select(YLFN=YSEITE:100,1:(100-YPAGEPOSITION))_";' >" ;CORE-233
	}else{
		write YCR,"<DIV class='tabDIVInner' style='left:"_((-OVERLAP)*(YPAGEPOSITION-YNEWROW-1))_"px; position:relative; width:"_TABWIDTH_";z-index:"_$select(YLFN=YSEITE:100,1:(100-YPAGEPOSITION))_";' >" ;CORE-233
	}
	if $$$DEVMODE set TOOLTIP=TOOLTIP_" YSEITE="_YREITER
	if YLFN'=YSEITE do ChangeTabs^WWWFORM6("",+YLFN)

	if strImage'="" {
		write YCR,"<IMG SRC='"_YGIF_strImage_"' class='tabIMG' TITLE='"_TOOLTIP_"' "
		if BACKGROUNDCOLOR'="" write " style='background-color:"_BACKGROUNDCOLOR_";'"
		write ">"
	} else {
		;write YCR,"<DIV style='border-radius: 8px 8px 0px 0px;border:1px solid darkgray;width:100%; height:30px; background-color:"_BACKGROUNDCOLOR_";'>"
		write YCR,"<DIV style='height:30px;border:0px;text-decoration:none;background-color:"_BACKGROUNDCOLOR_";' width='100%'>"
	}
	; CORE-223.1 vvvv
	write YCR,"<DIV class='tabTXT' style='"
	if YUSERAGENT="MSIE" {
		write "top:50%;"
	} else {
		write "top:0px; display:table-cell; vertical-align:middle; height:30px; "	
	}	
	write " font-size:"_FONTSIZE_"; width:"_TABWIDTH_"px; color:"_FONTCOLOR_"; font-weight:"_FONTWEIGHT_";' >"
	write "<DIV class='tabTXTInner"_TABSTYLE_"' "
	if (YUSERAGENT'="MSIE") write "style='vertical-align:middle; width:"_TABWIDTH_"px; height:30px; display:table-cell;'"
	write ">"
	write YYSEIT
	write "</DIV>"
	; CORE-223.1 ^^^^
	write "</DIV>"
	if strImage="" write "</DIV>"
	if YLFN'=YSEITE write YCR,"</A>"

	write "</DIV>" ;SR18046.1 
	
	quit

GetTabImage(pblnSelected,&FONTCOLOR="white",&FONTWEIGHT="normal",&TABWIDTH,&BACKGROUNDCOLOR="",&FONTSIZE="12px",&OVERLAP=0,&intTabStyle="")
	;-------------------------------------------------------------------------------
	; History:
	; 12-Jul-2012	SR18046.2: New, wider buttons.
	; 04-Jul-2012	SR18046.1: Default values to reduce standards warnings.
	;-------------------------------------------------------------------------------
	new strImage
	
	set intTabStyle=+$$$WWW013TabStyle($get(^WWW013(0,YBED,1)))
	
	if pblnSelected set FONTCOLOR="black"
	set objCompany = $get(^WWW012(0,YM,1))
	if intTabStyle=1 {
		if pblnSelected {
			set strImage="alpha_tab_01_wider_lighter.gif"
			set FONTWEIGHT="bold"
		} else {
			set strImage="alpha_tab_01_off_wider_lighter.gif"
		}
		set TABWIDTH=107
	} elseif intTabStyle=2 {
		if pblnSelected {
			set strImage="alpha_tab_02_wider_lighter.png"
			set FONTWEIGHT="bold"
		} else {
			set strImage="alpha_tab_02_Off_wider_lighter.png"
		}
		set TABWIDTH=102
	} elseif intTabStyle=4 {
		if pblnSelected {
			set strImage="alpha_tab_03_wider_lighter.png"
		} else {
			set strImage="alpha_tab_03_off_wider_lighter.png"
		}
		set TABWIDTH=107
		set FONTSIZE="12px"
		set OVERLAP=7
	} elseif intTabStyle=3 {
		if pblnSelected {
			set strImage="alpha_tab_03_wider_lighter.png"
		} else {
			set strImage=""
		}
		set BACKGROUNDCOLOR="#848484"
		set TABWIDTH=107
		set FONTSIZE="12px"
	}
	if $$MenuType^WWW013()=14{
		set BACKGROUNDCOLOR = ""
		set FONTWEIGHT="bold"
		if pblnSelected {
			set strImage=""
			set idColor = $$$WWW012ActiveTabColor(objCompany)
			if idColor'="" set BACKGROUNDCOLOR = $$$SysEnum("FARBE",idColor)
			if BACKGROUNDCOLOR="" set BACKGROUNDCOLOR = YSILVER
			;
			set FONTCOLOR = ""
			set idColor = $$$WWW012ActiveTabTextColor(objCompany)
			if idColor'="" set FONTCOLOR = $$$SysEnum("FARBE",idColor)
			if FONTCOLOR="" set FONTCOLOR = "black"
		} else {
			set strImage=""
			set idColor = $$$WWW012InactiveTabColor(objCompany)
			if idColor'="" set BACKGROUNDCOLOR = $$$SysEnum("FARBE",idColor)
			if BACKGROUNDCOLOR="" set BACKGROUNDCOLOR = "darkgray"
			;
			set FONTCOLOR = ""
			set idColor = $$$WWW012InactiveTabTextColor(objCompany)
			if idColor'="" set FONTCOLOR = $$$SysEnum("FARBE",idColor)
			if FONTCOLOR="" set FONTCOLOR = "black"
		}
		;
		;set BACKGROUNDCOLOR="#848484"
		set TABWIDTH=107
		set FONTSIZE="12px"
	}
	if strImage'="" set strImage="Tabs/"_strImage
	quit strImage
	

Style()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 20-Nov-2013	shobby	CORE-262: Correct problem with layout when using Firefox. (display:table in tabDIV)
	; 23-Aug-2013	shobby	CORE-239:	Rewrite to improve display with more than one line of tabs
	; 20-Aug-2013	shobby		CORE-223.1: New tabTXTInner, other style changes to position button in middle
	;								Need some special tricks here as Alphalinc is running in IE5 quirks mode.
	; 29-Jun-2012	shobby		SR18046: Created
	;-------------------------------------------------------------------------------
	;	<!DOCTYPE html PUBLIC ""-//W3C//DTD XHTML 1.0 Transitional//EN"" ""http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"">
	&html<
		<style>
			.tabDIV {
				width  :100%;
				display:table;
			} 
			.tabDIVInner {
				height:30px;
				float:left;
				overflow:hidden;
			}
			.tabDIV IMG {
				border	:none;
			}

			.tabTXT {
				cursor	:pointer;
				position:absolute;
				text-align:center;
				padding-top:0px;
				font-size:13px;
				color:gainsboro;
				text-decoration:none;
				margin-left:auto;
				margin-right:auto;
				left:0px;
			}
			.tabTXT:hover {
				color:white;
				text-decoration:none;
			}
			.tabTXTInner3,.tabTXTInner4 {padding-left:7px; padding-right:7px;}
		</style>
	>
	quit]]></Routine>
</Export>