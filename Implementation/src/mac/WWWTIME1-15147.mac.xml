<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWTIME1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWTIME1(ptmeValue)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Convert Time to seconds
	;		UMWANDELN ZEIT IN $H
	;
	; Inputs : 
	;	ptmeValue		Time in HH:MM[:SS] format (allowing . , - in place of : as delimiters)
	;					Can also have HH:MM[:SS] a[m]   or  HH:MM[:SS] p[m]
	;					  with AM or PM in place of am or pm
	;					Also 00min => sec as special format  e.g. 005 => 300; 0015 => 900
	;					
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 15-Jun-2007	GRF		SR15525: don't want exclusive new; doco; variable names
	; 11.08.1997	DT
	;-------------------------------------------------------------------------------
	new intAddMorning,intHours
	
	if ptmeValue="" quit ptmeValue
	
	set ptmeValue=$translate(ptmeValue,"ap.,-","AP:::")
	
	; Convert minutes to seconds only if the number of minutes is preceded by exactly 2 zeros.
	if ($extract(ptmeValue,1,2)="00") && ($extract(ptmeValue,3)>0) {
		set ptmeValue = ptmeValue*60
		quit ptmeValue
	}
	
	; If we don't have any delimiters and there are no AM/PM markers - treat as $h provided tength is greater than 2
	; Otherwise assume it is just hours (e.g. 6 or 12)
	if ($length(ptmeValue)>2) && '$find(ptmeValue,":") && '$find(ptmeValue,"P") && '$find(ptmeValue,"A") quit ptmeValue
	
	set intAddMorning=0
	if ($find(ptmeValue,"P")) set intAddMorning=43200
	
	set intHours = $piece(ptmeValue,":",1)
	if intHours>12 set intAddMorning=0          ; e.g. ignore pm in 15:45 pm
	
	quit ($piece(ptmeValue,":",1)*3600)+($piece(ptmeValue,":",2)*60)+$piece(ptmeValue,":",3)+intAddMorning
	
	/*  SR15525 - replaces
WWWTIME1(YA)
	NEW (YA)
	
	IF YA="" QUIT YA
	SET YA=$TRANSLATE(YA,".,-",":::")
	IF $EXTRACT(YA,1,2)="00" IF $EXTRACT(YA,3)>0 SET YA=YA*60 QUIT YA
	IF $LENGTH(YA)>2 IF '$FIND(YA,":") IF '$FIND(YA,"P") IF '$FIND(YA,"p") IF '$FIND(YA,"A") IF '$FIND(YA,"a") QUIT YA  ;SCHON $H FORMAT ;yet format 
	SET YPM=0
	IF $FIND(YA,"p")!($FIND(YA,"P")) SET YPM=12*60*60
	QUIT ($PIECE(YA,":",1)*60*60)+($PIECE(YA,":",2)*60)+$PIECE(YA,":",3)+YPM
	*/
	
]]></Routine>
</Export>