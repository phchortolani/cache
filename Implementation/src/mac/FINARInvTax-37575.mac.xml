<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvTax" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvTax
	
#include COMSYS
#include FINConst
	
RecalculateTaxes(pobjInvoice="")
    ;-------------------------------------------------------------------------------
    ; 
    ; Returns:
    ; 
    ; History:
	; 10-Apr-2010	GRF		SR17177: arrGrid rather than objGrid; GRID Macros
	; 02-Jun-2005	shobby	Moved from FINARInv (CodeCheck)
    ; 04-Mar-2005	JW		FC
    ; 18-Feb-2005	RobertW	Newed some variables
    ; 17-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new arrGrid,idInvoice,idLine,lineAmount,objInvLine,totalTax,subTotal
	new defaultTaxCode,itemTaxCode
	
	$$$GRIDGetContents(.arrGrid)
	set totalTax = 0
	set subTotal = 0
	
	set idInvoice = $order(arrGrid(""))
	if idInvoice'="" {
		set defaultTaxCode = $$$FINARInvDefaultTaxCode(pobjInvoice)
		
		set idLine = ""
		for {
			set idLine = $order(arrGrid(idInvoice,idLine))
			quit:idLine=""
			
			set objInvLine = $get(arrGrid(idInvoice,idLine))
			
			set itemTaxCode = $$$FINARInvLineTaxCode(objInvLine)
			if (itemTaxCode'="") && ($data(^COMTempList(YM,YUSER,"TaxCodes",itemTaxCode,1))#2=0) {
				set $$$FINARInvLineTaxCode(objInvLine) = defaultTaxCode
			}
			
			do CalculateTax^FINARInvLine(.objInvLine,pobjInvoice)
			set arrGrid(idInvoice,idLine) = objInvLine
			
			set totalTax = $$$FCAdd(totalTax,$$$FINARInvLineTax(objInvLine))
			set subTotal = $$$FCAdd(subTotal,$$$FINARInvLineAmountTaxEx(objInvLine))
		}
	}
	;set total = $$$FCAdd($piece($$GetYMFELDContainer^COMGridEdit31Interface(),Y,22),totalTax)
	
	$$$GRIDSetContents(.arrGrid)
	
	do PopulateDataField^COMUtils(YFORM,22,subTotal,"M")
	do PopulateDataField^COMUtils(YFORM,$$$FldFINARInvTax,totalTax)
	do PopulateDataField^COMUtils(YFORM,23,$$$FCAdd(totalTax,subTotal),"M")
	
	quit
 
 
UpdateTaxFields(pYFELD,blnTaxChanged=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Updates tax information.
    ; Note: Also called by OnClick of tax checkboxes
    ; 
    ; Returns:
    ; 
    ; History:
    ; 31-Mar-2005	JW		SR11937: Created (copied from FINAPVoucher)
    ;-------------------------------------------------------------------------------
	new linesIncludeTax,taxInvoice,blnTaxInc,strShow
	
	set linesIncludeTax = $$$FINARInvLinesIncludeTax(pYFELD)
	set taxInvoice = $$$FINARInvTaxInvoice(pYFELD)
	
	set strShow = 'taxInvoice_";"_+linesIncludeTax_";"_'linesIncludeTax
	
	// Hide tax columns depending on checkboxes.
	$$$GRIDHideShowColumns("4;6;2;5",strShow)
	
	// Make tax field read only if not tax invoice or tax on lines 
	if $$Editable^FINSYSReview("FINAPInv",$$$FINARInvStatus(pYFELD)) {
		;do ToggleReadOnly^COMUtilForm($$$FldFINARInvTax,linesIncludeTax||'taxInvoice,"FINARInv")
		;do ToggleReadOnly^COMUtilForm($$$FldFINARInvDefaultTaxCode,'taxInvoice,"FINARInv")
	}
	
	if blnTaxChanged {
		do RecalculateTaxes(pYFELD)
	}
	
	quit
	
 
ConsistentTax(pidInvoice)
	;-------------------------------------------------------------------------------
	; If the tax total isn't zero, then one of the lines must be taxable.
	; 
	; Returns: bln whether above holds true.
	;
	; History:
	; 02-Jun-2005	shobby	Moved from FINARInv (CodeCheck)
	;  4-Apr-2005	JW		SR11937: Created (copied from FINAPVoucher)
	;-------------------------------------------------------------------------------
	new blnConsistent,objInvoice,objLine,idLine
	
	set blnConsistent = $$$NO
	
	if $get(pidInvoice)'="" {
		set objInvoice=$get(^FINARInv(YM,pidInvoice,1))
		
		if $$$FCZero($$$FINARInvTax(objInvoice)) || $$$FINARInvLinesIncludeTax(objInvoice) {
			set blnConsistent = $$$YES
		} else {
			set idLine = ""
			for {
				set idLine=$order(^FINARInvLine(YM,pidInvoice,idLine))
				quit:idLine=""
				
				set objLine=$get(^FINARInvLine(YM,pidInvoice,idLine,1))
				if $$$FINARInvLineTaxCode(objLine) '= 0 {
					set blnConsistent = $$$YES
				}
				quit:blnConsistent
			}
		}
	}
	quit blnConsistent
	
]]></Routine>
</Export>