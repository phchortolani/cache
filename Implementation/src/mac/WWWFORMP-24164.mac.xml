<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMP
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Display Primary Keys
	;
	; Called By : RUECK^WWWFORM  [if (YFOART=1) || (YFOART=3)]
	;             COM.fwk.www.ui.FormDataFieldsUI :: show
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 09-Nov-2010	GRF		SR17243: Doco; macros; remove duplicated test
	; 20-Oct-2008	GRF		SR12505: "Don't Allow Characters" over-ride
	; 08.09.2004	FIS		Customise Executable Calls for Keys
	; 28,11,2003	TYBD
	; 05.09.2003	TYBD
	; 05.08.1997	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	new YI,YPRIM,YA,YLFN
	
	quit:YFORM=""
	
	if $$$WWW120CreateFormAutomatically($get(^WWW120(0,YFORM,1)))'=$$$YES if '$data(^WWW121(0,YFORM)) do  quit
	. if YKEY'="" set YFKEY=""                ;NEUFESTLEGEN FIXKEY
	. if YKEY="" if YFKEY'="" set YKEY=YFKEY  ;FÜR NEU BEI UNTERFORMULAREN ;to recent next to 
	.;if YFOART=3 if '$data(^WWW121(0,YFORM)) do ^WWWFORM3     ; SR17243
	. if YFOART=3 do ^WWWFORM3                ; Form Type 3 : GRID - no keys
	. write YCR,"<INPUT TYPE=HIDDEN NAME=""Y"_YFORM_"P1"" VALUE="""_YKEY_""">"
	
	;---------------------------------------
	
	if $piece($get(^WWW121(0,YFORM,1,1)),Y,16)="" set YFKEY = ""       ; $$$WWW121FixedInputForHiddenField()
	if YKEY'="" set YFKEY = ""                ;NEUFESTLEGEN FIXKEY
	if YKEY="" if YFKEY'="" set YKEY = YFKEY  ;FÜR NEU BEI UNTERFORMULAREN ;to recent next to
	
	if '$data(YA) set YA = ""
	set YART = "P"  ;P=PRIMÄR,D=DATENFELD,M=MANUELLE,L=LISTGENERATOR
	
	set YLFN = ""
	for  set YLFN = $order(^WWW121(0,YFORM,YLFN)) quit:YLFN=""  do
	. set YSATZ = $get(^WWW121(0,YFORM,YLFN,1))
	. if $data(^WWW121D(0,YFORM,YLFN,YM,1)) do             ; Customise Executable Calls for Keys 
	. . new YSATZ1
	. . set YSATZ1 = $get(^WWW121D(0,YFORM,YLFN,YM,1))
	. . if $$$WWW121DDontAllowCharacters(YSATZ1) '="" set $$$WWW121DontAllowChars(YSATZ) = $$$WWW121DDontAllowCharacters(YSATZ1)
	. . if $piece(YSATZ1,Y,92)'="" set $piece(YSATZ,Y,92) = $piece(YSATZ1,Y,92)   ; $$$WWW121ExecuteOnFormStartBeforeD()
	. . if $piece(YSATZ1,Y,99)'="" set $piece(YSATZ,Y,99) = $piece(YSATZ1,Y,99)   ; $$$WWW121DExecuteInDataField() [No WWW121 definition]
	. . if $piece(YSATZ1,Y,97)'="" set $piece(YSATZ,Y,97) = $piece(YSATZ1,Y,97)   ; $$$WWW121ExecuteOnFormConstruct()
	. ;
	. set YBBN=YLFN
	. do ^WWWFORM9
	. if $piece(YVOR,Y,2)'=3 do      ; (YFOART)  ; Display keys if not a Grid
	. . new LASTHIDD,LFN2
	. . set LASTHIDD = 0
	. . ;
	. . set LFN2 = $order(^WWW121(0,YFORM,YLFN))
	. . if LFN2'="" if $piece($get(^WWW121(0,YFORM,LFN2,1)),Y,4)=2 quit  ;KEIN ENDE ;no termination 
	. . ;
	. . if YLFN=$order(^WWW121(0,YFORM,""),-1) if $piece($get(^WWW121(0,YFORM,YLFN,1)),Y,16)'="" set LASTHIDD=1 if YLFN'=$order(^WWW121(0,YFORM,"")) if $order(^WWW121(0,YFORM,YLFN))'="" quit  ; WENNN MEHRERE UNSICHTBARE FELDER VON WAC
	. . if $piece(YVOR,Y,66)=2 if ($get(YTYPE)="HIDDEN") || ($get(YHID)=1) || ($get(YTYP)=0) quit    ; WACH, BEI FORMS MIT MANUELLEN UND HIDDEN FELDERN
	. . if LASTHIDD'=1         if ($get(YTYPE)="HIDDEN") || ($get(YHID)=1) || ($get(YTYP)=0) quit
	. . write YCR,"</TR>",YCR               ; ABSCHLUSS FELD ;field 
	. . if $piece(YVOR,Y,44)'=$$$YES do 
	. . . write YCR,"</TABLE>",YCR          ; ABSCHLUSS FORMAT
	. . . set YTABLEANZ = $get(YTABLEANZ)-1
	
	;ZEITABHÄNIGE VORABERFASSUNG VON DATEN = GÜLTIG AB DATUM ;valuable Confirm. Date 
	if $get(YTIMEFORM)=1 do
	. set YSATZ = ""
	. set YBBN  = $order(^WWW121(0,YFORM,""),-1)+1
	. set YLFN  = YBBN
	. do ^WWWFORM9
	. write YCR,"</TR>",YCR   ;ABSCHLUSS FELD ;field 
	. if $piece(YVOR,Y,44)'=$$$YES do
	. . write YCR,"</TABLE>",YCR   ;ABSCHLUSS FORMAT
	. . set YTABLEANZ = $get(YTABLEANZ)-1
	
	quit
	
]]></Routine>
</Export>