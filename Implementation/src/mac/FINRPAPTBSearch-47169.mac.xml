<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPAPTBSearch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPAPTBSearch
	#include COMSYS
	#include FINConst
 	
ShowSelected()
	/*------------------------------------------------------------------------------
	; Show the selected suppliers
	;
	; Params: 
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	new arrData
 
	if '$data(YMFELD) kill ^CacheTempSuppliers(YUSER)
 	
 	if YSEITE=1 {
	 	do UpdateSelected(.arrData)
	 	write "<B>"_$$$Text("Fin00965")_"</B>",! ;Suppliers Selected
		write "",!
		write "<table style='font-size:14px'>",!
		write "<tr>",!
		write "<td><input readonly style='background-color:lightgrey' size=10 name='SuppliersSelectedCounter' value='"_arrData("SuppliersSelectedCounter")_"'></td>",!
		write "</tr>",!
		write "<tr>",!
		write "<td><textarea readonly style='background-color:lightgrey' cols=60 rows=30 name='SuppliersSelected'></textarea></td>",!
		write "</tr>",!
		write "</table>",!
		
		$$$StartScript()
		;write "<script type='text/javascript'>"
		write "document.WWW2.SuppliersSelected.value="""_arrData("SuppliersSelected")_""";",!
		;write "</script>"
		$$$EndScript()
	}
	 
	quit
	
Search()
	/*------------------------------------------------------------------------------
	; Start the COMView search for suppliers
	;
	; Params: 
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	do Initialise^COMViewCustom("FINAPSupplier","SuppliersSelected^FINRPAPTBSearch")
	do Start^COMViewCustom()
	
	quit $$$OK
	
	
SuppliersSelected(pobjResultSet)
	/*------------------------------------------------------------------------------
	; Routine to run when a supplier is selected
	;
	; Params: pobjResultSet		: The COMView result set
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	new idSupplier,arrData
	
	while pobjResultSet.Next() {
		set idSupplier=$$GetID^COMViewCustom(pobjResultSet)
		if idSupplier'="" set ^CacheTempSuppliers(YUSER,idSupplier)=""
	}
	
	do UpdateSelected(.arrData)
	
	write "document.WWW2.SuppliersSelected.value="""_arrData("SuppliersSelected")_""";",!
	write "document.WWW2.SuppliersSelectedCounter.value="_arrData("SuppliersSelectedCounter")_";",!
	
	quit	
	
UpdateSelected(parrData)
	/*------------------------------------------------------------------------------
	; Update current data selected suppliers
	;
	; Params:
	;
	; ByRefs: parrData		: The array with the supplier data
	;
	; Returns:
	;
	; History:
	; 03-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	new idSupplier,intCount,strSuppliers,strName,blnLimit,strText
	
	set intCount=0
	set idSupplier=""
	
	set strSuppliers=""
	set blnLimit=$$$NO
	
	for {
		set idSupplier=$order(^CacheTempSuppliers(YUSER,idSupplier))
		quit:(idSupplier="")
		
		set intCount=intCount+1
		
		if (intCount<=500) {
			set strSuppliers=strSuppliers_$justify(idSupplier,10)
			set strName=$$$FINAPSupplierSupplierName($get(^FINAPSupplier(YM,idSupplier,1)))
			if strName="" set strName="---"
			set strSuppliers=strSuppliers_": "_strName_"\n"
		} else {
			if 'blnLimit {
				set blnLimit=$$$YES
				set strText=$zconvert($$$Text("Fin00967"),"U")  ;Only displaying first 500 entries
				set strSuppliers=$justify(strText,5)_"\n"_strSuppliers
			}	
		}
	}
	
	set parrData("SuppliersSelected")=strSuppliers
	set parrData("SuppliersSelectedCounter")=intCount
	
	quit
]]></Routine>
</Export>