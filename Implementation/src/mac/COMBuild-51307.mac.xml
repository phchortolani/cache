<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMBuild" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMBuild
	
#include COMConst
	
OnAfterButtonLine()
    ;-------------------------------------------------------------------------------
    ; Show the list of Application selected for this build in build order
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Jun-2005	RPW		Created SRSR12022,12280: Created..
    ;-------------------------------------------------------------------------------
	new lstHeadings,strOrder,strApplication,objList,strModule
	
	set lstHeadings=$listbuild($$^WWWFELDNAME("COMApplicationList","P",1),
	$$^WWWFELDNAME("COMApplicationList","D",1),
	$$^WWWFELDNAME("COMApplicationList","D",2))
	
	do Start^COMTable(lstHeadings)
	
	set strOrder=""
	for {
		set strOrder=$order(^COMApplicationLists(YM,1,$$$YES,strOrder))
		quit:strOrder=""
		
		set strApplication=""
		
		for {
			set strApplication=$order(^COMApplicationLists(YM,1,$$$YES,strOrder,strApplication))
			quit:strApplication=""
			
			set objList=$get(^COMApplicationList(YM,strApplication,1))
			if objList'="" {
				set strModule=$$$COMApplicationListModule1(objList)
			}
			
			do NewLine^COMTable()
			do InsertCell^COMTable(strApplication)
			do InsertCell^COMTable($get(strModule))
			do InsertCell^COMTable(strOrder)
		}
	}
	do Stop^COMTable()
	quit
	
	
OnAfterSave(pblnNightly=$$$NO,&pidLog="")
    ;-------------------------------------------------------------------------------
    ; Validate that the build number is valid and if so, perform the build.
    ; 
    ; Parameters:
    ; pblnNightly: Is this happening as a nightly job?
    ; pidLog     : The id of the message log
    ;
    ; Returns:
    ;
    ; History:
    ; 04-May-2006	RPW		SR14354: Use COMDistOut6
    ; 29-Mar-2006	SC		SR12942: Use JobWrapper^COMUtilJob
    ; 23-Mar-2006	RPW		SR14354: Require the cache version to be passed through.
    ; 08-Jan-2005	PO		SR14015: Use new build routine
    ; 19-Dec-2005	RPW		SR13942: Pass the current environment through to the job.
    ; 23-Jun-2005	RPW		Created SRSR12022,12280: Created
    ;-------------------------------------------------------------------------------
    new strVersion,strStatus,Log,strCacheVersion,intLength
	
	set strVersion      = $get(DBUILD(1))
	set strCacheVersion = $get(DBUILD(2),$SYSTEM.Version.GetNumber())
		
	set strStatus = $$$OK
	set intLength = $length(strVersion,".")
	
	if (intLength>4) || (intLength<3) {
		set strStatus = $listbuild("COM05",strVersion)
	}
	
	if $$$ISERR(strStatus) {
		do:'pblnNightly ReturnError^COMUtilError(strStatus)
		
	} else {
		do StartLog^COMLog("AlphaLinc "_strVersion_" Build",.Log) ; FIXME Make into a Language Text
		set:pblnNightly pidLog=Log
		
		//TODO should this be COMDistOut6
		// SR14354: Add the cache version number
		job AutoBuild^COMDistOut6(strVersion,strCacheVersion,Log):(:1::)     ; Environment created for new job NFA SR13942 // SR14354
		;do JobWrapper^COMUtilJob("AutoBuild^COMDistOut6("""_strVersion_""","""_strCacheVersion_""","_Log_")",YUSER) ;SR13942
		
		if 'pblnNightly {
			hang 2 ;wait for the process to start up
			
			; Ready to goto the Detail form
			do RedirectForm^COMUtilForm("COMMsg",Log,YFORM,"","")
		}
	}
	quit
	
	
NightlyBuild()
	;-------------------------------------------------------------------------------
	; Prepare a nightly build
	;
	; Called By: OnAfterDataFields^COMApplicationList
	;            => do AfterDataField^COMSchedule(1,"COMNightlyBuild","NightlyBuild^COMBuild()")
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-Jan-2008	GRF		SR15623: Add text to ensure all quits return values
	; 19-Sep-2006	RPW		Created
	;-------------------------------------------------------------------------------
	new DBUILD,idLog
	
	set $ztrap="NBError"
	
	quit:($zutil(110)'="ORION")&&($zutil(5)'="DEV") "Attempt to run on "_$zutil(110)_" : "_$zutil(5)_" did not proceed"
 
	set DBUILD(1) = $piece($get(^WWWVERSION(0,"AlphaLinc",1)),Y,1)
	set DBUILD(2) = $SYSTEM.Version.GetNumber()
	
	quit:$get(^SysSetup("DoingBuild"))=1 "Build already in progress, unable to progress further"
	
	set ^SysSetup("DoingBuild")=1
	do OnAfterSave($$$YES,.idLog)
	kill ^SysSetup("DoingBuild")
	
	quit "Build "_DBUILD(1)_" complete, please check Log Number "_idLog
	
NBError
	set $ztrap=""
	kill ^SysSetup("DoingBuild")
	
	quit "Build "_$get(DBUILD(1))_" failed "_$zconvert($zerror,"HTML","OUT")
	
	
GetVersion()
	quit $$$WWWVERSIONActualVersionNo($get(^WWWVERSION(0,"AlphaLinc",1)))
	
	
]]></Routine>
</Export>