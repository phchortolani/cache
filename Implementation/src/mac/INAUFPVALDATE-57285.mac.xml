<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPVALDATE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPVALDATE
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SET VALUATION DATE OF LINE ITEM
	; 
	; Called by :
	;   INAUF			INAUFANLAGE			INAUFP
	;   INDRUCK6		INDRUCKEDI6			INDRUCKEDIP6
	;   INDRUCKXML6		INDRUCKXML6PRINT	INTAGES1
	;   
	; 
	; Inputs : 
	;    YKEY   idOrder,idOrderLine  [from INAUF]
	;    YFELD  (objINAUFP [from INAUF])
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 31-Jul-2006	GRF		Dot Level; doco; !=>||; remove unnecessary "+" from <,> tests
	; 03.05.2006	FIS		29358; WERTE NEU SETZEN
	; 17.01.2005			Created
	;----------------------------------------------------------------------------------
	NEW AUFTRAG,POSITION,AUFDAT,RECHDAT
	
	SET AUFTRAG  = $PIECE($GET(YKEY),",",1)
	SET POSITION = $PIECE($GET(YKEY),",",2)
	QUIT:$GET(YFELD)=""
	
	;SET VALUATION PERIOD
	SET AUFDAT=$PIECE(YFELD,Y,193)  ;ORDER DATE
	IF AUFDAT="" SET AUFDAT=$PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,4),$PIECE(YFELD,Y,193)=AUFDAT
	IF AUFDAT="" SET AUFDAT=+$HOROLOG
	
	SET RECHDAT=$PIECE(YFELD,Y,97)  ;INVOICE DATE
	IF RECHDAT="" IF $PIECE(YFELD,Y,7)=1 DO  ;SOURCE = ORDER
	. IF $PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,2)'=2 QUIT
	. IF $DATA(^INERECH1(YM,AUFTRAG,POSITION)) DO
	. . NEW LIEF,RENUM,REDAT
	. . SET LIEF=$ORDER(^INERECH1(YM,AUFTRAG,POSITION,""))
	. . QUIT:LIEF=""
	. . SET RENUM=$ORDER(^INERECH1(YM,AUFTRAG,POSITION,LIEF,""))
	. . QUIT:RENUM=""
	. . SET REDAT=$PIECE($GET(^INERECH1(YM,AUFTRAG,POSITION,LIEF,RENUM,1)),Y,1)
	. . IF REDAT="" SET REDAT=$PIECE($GET(^INERECH1(YM,AUFTRAG,POSITION,LIEF,RENUM,1)),Y,37)
	. . QUIT:REDAT=""
	. . SET RECHDAT=REDAT
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;IF RECHDAT="" IF $DATA(^INWEAUF(YM,AUFTRAG,POSITION)) DO
	. . NEW BET,LAP,WED
	. . SET BET=$ORDER(^INWEAUF(YM,AUFTRAG,POSITION,""))
	. . QUIT:BET=""
	. . SET LAP=$ORDER(^INWEAUF(YM,AUFTRAG,POSITION,BET,""))
	. . QUIT:LAP=""
	. . SET WED=$ORDER(^INWEAUF(YM,AUFTRAG,POSITION,BET,LAP,""),-1)
	. . QUIT:WED=""
	. . SET RECHDAT=WED
	. .;SET RECHDAT=$P($G(^INWEAUF(YM,AUFTRAG,POSITION,BET,LAP,WED,1)),Y,1)
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	 
	;IF RECHDAT="" IF $PIECE(YFELD,Y,60)=1 SET RECHDAT=+$HOROLOG  ;ORDER IS CLOSED
	
	;CHECK OLD DATA
	IF $LENGTH(+$PIECE(YFELD,Y,386))'=4 SET $PIECE(YFELD,Y,386)=""   ;WRONG WEEK DATA
	IF ($PIECE($PIECE(YFELD,Y,386),"-",2)<1) || ($PIECE($PIECE(YFELD,Y,386),"-",2)>53) SET $PIECE(YFELD,Y,386)=""   ;WRONG WEEK DATA
	IF $LENGTH(+$PIECE(YFELD,Y,398))'=4 SET $PIECE(YFELD,Y,398)=""   ;WRONG WEEK DATA
	IF ($PIECE($PIECE(YFELD,Y,398),"-",2)<1) || ($PIECE($PIECE(YFELD,Y,398),"-",2)>53) SET $PIECE(YFELD,Y,398)=""   ;WRONG WEEK DATA
	
	;SET NEW DATA
	
	;WEEK
	IF $PIECE(YFELD,Y,386)="" DO
	. SET $PIECE(YFELD,Y,386)=$$^WWWWEEK(AUFDAT,3)  ;WEEK OUT OF ORDER DATE
	
	IF $PIECE(YFELD,Y,398)="" IF RECHDAT'="" DO
	. SET $PIECE(YFELD,Y,398)=$$^WWWWEEK(RECHDAT,3)  ;WEEK OUT OF INVOICE DATE
	
	;MONTH
	IF $PIECE(YFELD,Y,387)="" DO
	. SET $PIECE(YFELD,Y,387)=$$^WWWYEAR(AUFDAT)_"-"_$EXTRACT(100+$$^WWWMONTH(AUFDAT),2,3)    ;MONTH YYYY-MM
	
	IF $PIECE(YFELD,Y,399)="" IF RECHDAT'="" DO
	. SET $PIECE(YFELD,Y,399)=$$^WWWYEAR(RECHDAT)_"-"_$EXTRACT(100+$$^WWWMONTH(RECHDAT),2,3)  ;MONTH YYYY-MM
	
	;QUARTER
	IF $PIECE(YFELD,Y,388)="" DO
	. SET $PIECE(YFELD,Y,388)=$$^WWWYEAR(AUFDAT)_"-"_$$^WWWQUARTER(AUFDAT)    ;QUARTER YYYY-Q
	
	IF $PIECE(YFELD,Y,400)="" IF RECHDAT'="" DO
	. SET $PIECE(YFELD,Y,400)=$$^WWWYEAR(RECHDAT)_"-"_$$^WWWQUARTER(RECHDAT)  ;QUARTER YYYY-Q
	
	;YEAR
	IF $PIECE(YFELD,Y,389)="" DO
	. SET $PIECE(YFELD,Y,389)=$$^WWWYEAR(AUFDAT)   ;YEAR YYYY
	
	IF $PIECE(YFELD,Y,401)="" IF RECHDAT'="" DO
	. SET $PIECE(YFELD,Y,401)=$$^WWWYEAR(RECHDAT)  ;YEAR YYYY
	
	IF $PIECE(YFELD,Y,9)=1 DO  ;SET NEW VALUES for Cancellation   ;WERTE NEU SETZEN;FIS;03.05.06;29358
	. NEW YI
	. FOR YI=386,387,388,389,398,399,400,401 SET $PIECE(YFELD,Y,YI)=""
	
	QUIT
	
SET(AUF,POS,NEU)
	; called by INSERV334 (with NEU=1)
	NEW YKEY,YFELD,YFELDX
	
	SET AUF=$GET(AUF)     QUIT:AUF=""
	SET POS=$GET(POS)     QUIT:POS=""
	
	SET YKEY=AUF_","_POS
	SET YFELD=$GET(^INAUFP(YM,AUF,POS,1))
	IF $GET(NEU)=1 DO  ;WERTE NEU SETZEN;FIS;03.05.06;29358
	. NEW YI
	. FOR YI=386,387,388,389,398,399,400,401 SET $PIECE(YFELD,Y,YI)=""
	
	SET YFELDX=YFELD
	DO ^INAUFPVALDATE
	IF YFELD=YFELDX QUIT  ;NO CHANGE
	
	NEW YFORM,YVOR,YOK
	SET YOK=$$^WWWSPEI("INAUFP",YKEY,YFELD,1,,1)
	
	QUIT
]]></Routine>
</Export>