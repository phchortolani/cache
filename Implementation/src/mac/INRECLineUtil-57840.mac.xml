<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRECLineUtil" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRECLineUtil
	
#include COMSYS
#include INConst
	
 ;++++++++++++++++++++++++++++++++++++++++
 ; Interface for Unit conversions in INRECLine
 ; as used in Supplier Receipts (old)
 ; +++++++++++++++++++++++++++++++++++++++
	
ConvInvQtyToOtherQty(pidItem,pidOtherUnit,pfltInventQty)
	;-------------------------------------------------------------------------------
	; Convert qty from Inventory Units to Other (Order or Received) Units
	;
	; Inputs:
	;
	; Returns: Qty in Order/Received Units
	;
	; History:
	; 27-Aug-2009	GRF		SR16857: Created. 
	;-------------------------------------------------------------------------------
	new fltConvFactor,fltOtherQty
	
	set fltConvFactor = $$GetUOMConversion^INUOMConversion(pidItem,pidOtherUnit)
	set fltOtherQty   = pfltInventQty / fltConvFactor
	
	quit fltOtherQty	
	
	
ConvOtherQtyToInvQty(pidItem,pidOtherUnit,pfltOtherQty)
	;-------------------------------------------------------------------------------
	; Convert qty to Inventory Units from Other (Order or Received) Units
	;
	; Inputs:
	;
	; Returns: Qty in Inventory Units
	;
	; History:
	; 27-Aug-2009	GRF		SR16857: Modified for more general use. 
	; 25-Aug-2009	DWR		SR16857: Created. 
	;-------------------------------------------------------------------------------
	new fltConvFactor,fltInventQty
	
	set fltConvFactor = $$GetUOMConversion^INUOMConversion(pidItem,pidOtherUnit)
	set fltInventQty  = pfltOtherQty * fltConvFactor
	
	quit fltInventQty	
	
	
UpdateRecQty(&pobjLine)
	;-------------------------------------------------------------------------------
	; Convert the quantity received as received units (e.g. CTN of 20) into the
	; quantity as inventory units for the item (e.g. EA - Conversion factor of 20)
	; 
	; ByRef :
	;	pobjSummary	- INRECLine record - updated
	; 
	; History:
	; 25-Aug-2009	DWR		SR16857: Modified from INRECSummaryUtil
	;-------------------------------------------------------------------------------
	new fltConv
	
	set fltConv = $$GetUOMConversion^INUOMConversion($$$INRECLineItem(pobjLine),$$$INRECLineReceivedUnit(pobjLine)) 
	set $$$INRECLineQuantity(pobjLine) = $$$INRECLineUnitQuantity(pobjLine) * fltConv	
	quit
	
	
GetInvCost(pidItem,pidOrdUnit,pfltOrdEach)
	;-------------------------------------------------------------------------------
	; convert Order Unit Cost to Inventory Unit Cost where it was ordered in a unit
	; different to the item's inventory unit.
	; 
	; Returns : Inventory Unit Cost
	; 
	; History:
	; 25-Aug-2009	DWR		SR16857: Modified from INRECSummary
	;-------------------------------------------------------------------------------
	new fltInvConv,fltInvCost
	
	set fltInvConv = $$GetUOMConversion^INUOMConversion(pidItem,pidOrdUnit)
	set fltInvCost = pfltOrdEach / fltInvConv
	
	quit fltInvCost
	
]]></Routine>
</Export>