<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMTempList" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMTempList
#include WWWConst
#include COMSYS
	
PurgeTempList()
	;-------------------------------------------------------------------------------
	; Purge all items that are not currently in use
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 10-Sep-2008	shobby	BR014980: COMHelp* is now temporary data and can be removed.
	; 20-Dec-2007	shobby	SRBR014856:   Fixed issue where COMTempList was being
	; 							skipped (going straight to COMMTempList1 for example)
	; 01-Nov-2006	shobby	SRBR014547: Loops through and removes all orphaned data in
	; 							classes whose names begin with COMTempList
	; 							Rewrote routine to bring closer to standards
	;-------------------------------------------------------------------------------
	new intCount,idUser,idWWW001
	
	set intCount=0
	
	set idWWW001 = "COMTempList"
	set idWWW001 = $order(^WWW001(0,idWWW001),-1)
	for {
		set idWWW001=$order(^WWW001(0,idWWW001))
		quit:(idWWW001="")
		quit:($extract(idWWW001,1,$length("COMTempList"))'="COMTempList")   ;*** Very important that this is right or we can end up blowing away all sorts of data
		quit:(idWWW001="COMView")			;Let's put another safety net just in case someone messes up the previous line.
		
		set idUser=""
		for {
			xecute "set idUser=$order(^"_idWWW001_"(YM,idUser))"
			quit:idUser=""
			
			if '$data(^WWWUSER(0,idUser,1)) {            ; ripe to purge
				xecute "kill ^"_idWWW001_"(YM,idUser)"
				set intCount=intCount+1
			}
		}
	}
 	do Delete^COMHelp()		;BR014980
	quit intCount_" items purged"
 
 
LookForRelations()
	;-------------------------------------------------------------------------------
	; Look for relationships in properties that have dropdown boxes populated with a
	; COMTempList to see if any of these point to valid classes that might have a
	; Hyperlink created on the corresponding Caption.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 01-Nov-2006	shobby	SRBR014296: Created
	;-------------------------------------------------------------------------------
	new idClass,idForm,idField,objWWW003,strRelationalPrimaryKeys,strClass,objWWW122
	
	if $get(Y)="" do ^WWWVAR
	
	; Check in the class definitions
	set idClass=""
	for {
		set idClass=$order(^WWW003(0,idClass))
		quit:idClass=""
		
		set idField=""
		for {
			set idField=$order(^WWW003(0,idClass,idField))
			quit:idField=""
			
			set objWWW003=$get(^WWW003(0,idClass,idField,1))
			if $$$WWW003RelationDatabase(objWWW003)="COMTempList" {
				set strRelationalPrimaryKeys=$$$WWW003RelationalPrimaryKeys(objWWW003)
				if $piece(strRelationalPrimaryKeys,$$$COMMA,1)="YUSER" {
					set strClass=$translate($piece(strRelationalPrimaryKeys,$$$COMMA,2),"""")
					if strClass'="" {
						if $data(^WWW001(0,strClass)) {
							; If the class used in the relationship of the 2nd part exists
							write !,"Class "_idClass_","_idField_","_strRelationalPrimaryKeys
						}
					}
				}
			}
		}
	}
	
	
	; Check in the form definitions
	set idForm=""
	for {
		set idForm=$order(^WWW122(0,idForm))
		quit:idForm=""
		
		set idField=""
		for {
			set idField=$order(^WWW122(0,idForm,idField))
			quit:idField=""
			
			set objWWW122=$get(^WWW122(0,idForm,idField,1))
			if $$$WWW122RelationClass(objWWW122)="COMTempList" {
				set strRelationalPrimaryKeys=$$$WWW122RelationalPrimaryKey(objWWW122)
				if $piece(strRelationalPrimaryKeys,$$$COMMA,1)="YUSER" {
					set strClass=$translate($piece(strRelationalPrimaryKeys,$$$COMMA,2),"""")
					if strClass'="" {
						if $data(^WWW001(0,strClass)) {
							; If the class used in the relationship of the 2nd part exists
							write !,"Form  "_idForm_","_idField_","_strRelationalPrimaryKeys
						}
					}
				}
			}
		}
	}
	
	quit
	
]]></Routine>
</Export>