<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDDrugInteraction" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDDrugInteraction
	#include COMSYS
	#include INConst
	#include VARConst
	#include MEDConst
	quit

	
OnAfterSave(pYKEY)
 	quit	

OnAfterDelete(pYKEY)
 	quit	
 	
ShowInteractionPrescriptionMessage(pYKEY,window=1)
	;-------------------------------------------------------------------------------
	; Show Interaction Message for Drugs from the Prescription
	; 
	; Params:
	; pYKEY : KEY ID
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 20-Jan-2014	shobby		SMSPE-15: Created
	;-------------------------------------------------------------------------------
	
	new idLine1, idLine2, objItem1, objItem2, objLine2,msg,item, item1,idItem1, idItem2, intCont
	new idLine, idPrescription, qtdTotal, objLine1, arrGrid, strStatus, idMainLine,numeroLinha, tamanho
	set msg = ""
	set intCont = 0

	if '$$$NoKey(pYKEY) {
		set idPrescription = $piece(pYKEY,",",1)		

		$$$GRIDGetContents(.arrGrid,"MEDPrescriptionLine")
		set idLine1 = ""
		SET idLine2 = ""
		for {
			set idLine1 = $order(arrGrid(idPrescription,idLine1))
			quit:idLine1=""
 			set objLine1 = $get(arrGrid(idPrescription,idLine1))
			set idItem1 = $$$MEDPrescriptionLineItem(objLine1)
			for {
				set idLine2 = $order(arrGrid(idPrescription,idLine2))
				quit:idLine2=""
	 			set objLine2 = $get(arrGrid(idPrescription,idLine2))
				set idItem2 = $$$MEDPrescriptionLineItem(objLine2)
				set objItem1 = $get(^MEDDrugInteraction(0,idItem1,idItem2,1))
				if (objItem1 '="") set msg=msg_$$GetMessage(idItem1,idItem2,objItem1, , .intCont)
			}
		}
	}
	
				
	if (msg'="") do PrepareMessage(msg,window)	
	quit	

ShowInteractionMessage(pYKEY="", pblnValidOnly=$$$NO, window=1,pidPrescription="") ;SMSPE-15
	;-------------------------------------------------------------------------------
	; Show Interaction Message for Drugs
	; called by Drug Interaction Button on MEDDispenseLine
	; 
	; Params:
	; pYKEY : KEY ID
	;
	; ByRefs:
	;
	; Returns:
	; Boolean
	;
	; History:
	; 31-Jan-2013	SCR		HEVA-792: Use common code
	; 07-Aug-2012	PPP		SR18067: Updated to check diluents
	; 16-Jul-2012	SCR		SR18002: Changed to look at all active 
	; 13-Jun-2012	PPP		SR18002.1: Added Diluent Drug Interaction
	; 04-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------	
	new strMsg,intCont
	
	set intCont=0
	
	do CheckDrugInteraction(pYKEY, pblnValidOnly,,.strMsg,.intCont)

	if strMsg'="" do PrepareMessage(strMsg,window,.intCont)	
	
	quit

PrepareMessage(pstrMsg="",pblnWindow=1,pintCont)
	;-------------------------------------------------------------------------------
	; Show Interaction Message for Drugs from the Prescription
	; 
	; Params:
	; pYKEY : KEY ID
	;
	; ByRefs:
	;
	; Returns:
	; Boolean
	;
	; History:
	; 20-Jan-2014	shobby		SMSPE-15: Created
	;-------------------------------------------------------------------------------
	new intLines,intSize
	
	if (pblnWindow){				
		set intLines = $l(pstrMsg,"</br>")+2
		set intSize = intLines * 27
		
		if (intSize > 600){
			set intSize = 600
		}
		set pstrMsg = $replace(pstrMsg,"'","`")
		set pstrMsg = $$$Text($lb("MED00416",pintCont))_pstrMsg		//Dispense contains <b>%1</b> Drug Interaction(s)</br>
		
		do Show^WWWPopupMessage(pstrMsg,,,,"dscPopupDrugInteraction",,,,$$$YES,,600) ;SMSPE-15
		
	} else {				

	    write "<IMG SRC='"_YGIF_"delimiter.gif' border=0>"      
   		write "<a onclick='doLink(this);' href='javascript:history.go(-1);'><IMG SRC="_YGIF_"hback.gif id='BtnIni' TITLE='Voltar' border=0 align='top'></a>"     
   		write "<IMG SRC='"_YGIF_"delimiter.gif' border=0>"      
		write pstrMsg
	    write "</BR><IMG SRC='"_YGIF_"delimiter.gif' border=0>"      
   		write "<a onclick='doLink(this);' href='javascript:history.go(-1);'><IMG SRC="_YGIF_"hback.gif id='BtnIni' TITLE='Voltar' border=0 align='top'></a>"     
   		write "<IMG SRC='"_YGIF_"delimiter.gif' border=0></BR>"      
	}
	quit
	
	
GetInteractionMessage(&parrDrugs,&pintCont,pidRx,pidCurDisp="")
	;-------------------------------------------------------------------------------
	; Get Interaction Message for Drugs
	; 
	; Params:
	; parrDrugs List of drugs , Count , Rx
	;
	;
	; Returns:
	; Message
	;
	; History:
	; 19-Feb-2013	SCR		HEVA-792: Dont Check Current Dispense (If loaded from edit grid)
	; 04-Jul-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------	
	new strMsg,idItem1,idItem2,objItem,idDiluent,objRx,idPatient
	set strMsg=""
		
	set objRx		= $get(^MEDPrescription(YM,pidRx,1))
	set idPatient	= $$$MEDPrescriptionPatientID(objRx)
	do DrugLoad(idPatient,.parrDrugs,pidCurDisp)		

	set idItem1 = ""
	for {
		set idItem1 = $order(parrDrugs("h",idItem1))
		quit:idItem1=""

			
		set idItem2 = ""
		for {
			set idItem2 = $order(parrDrugs("h",idItem2))
			quit:idItem2=""
			continue:idItem1=idItem2

			continue:('$data(parrDrugs("l",idItem1))&&'$data(parrDrugs("l",idItem2)))
					
			set objItem 	= $get(^MEDDrugInteraction(0,idItem1,idItem2,1))
				 
			if (objItem '="") && $$$MEDDrugInteractionActive1(objItem) {
				if (+$get(parrDrugs("h",idItem1))<$h) || (+$get(parrDrugs("h",idItem2))<$h) {
					continue
				}
				set strMsg = strMsg_$$GetMessage(idItem1,idItem2,objItem, , .pintCont)
				set parrDrugs("x",idItem1,idItem2)=""
				set parrDrugs("x",idItem2,idItem1)=""
			}
				
		}  //End of Multi Line Interaction
			
	}

	quit strMsg
	

GetMessage(pidItem1,pidItem2,pobjItem1, &parrItems, &pintCont)
	;-------------------------------------------------------------------------------
	; Display Interaction Message for Drugs
	; 
	; Params:
	; pidItem1,pidItem2,pobjItem1, &parrItems, &pintCont
	;
	; ByRefs:
	;
	; Returns:
	; None
	;
	; History:
	; 25-Jul-2012	PPP		SR18067: Initialize variable strMsg
	; 13-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------	
	new idClinic,idInteraction,idProbablity,strMsg
					
	set idClinic = ""
	set idInteraction = ""
	set idProbablity = ""
	set parrItems(pidItem1,pidItem2) = $increment(parrItems(pidItem1,pidItem2))
	set strMsg = ""		//SR18067
	
	if ($$$MEDDrugInteractionClinicalImportance(pobjItem1)'= ""){ 
    	set idClinic = $$GetParamDesc^INPARA("MEDCLIIMP",$$$MEDDrugInteractionClinicalImportance(pobjItem1))
	}
	if ($$$MEDDrugInteractionInteractionImportance(pobjItem1)'= ""){ 
    	set idInteraction = $$GetParamDesc^INPARA("MEDCLIIMP",$$$MEDDrugInteractionInteractionImportance(pobjItem1))
	}
	if ($$$MEDDrugInteractionOccuranceProbability(pobjItem1)'= ""){ 
    	set idProbablity = $$GetParamDesc^INPARA("MEDPROBABILITY",$$$MEDDrugInteractionOccuranceProbability(pobjItem1))
	}

	if (parrItems(pidItem1,pidItem2) < 2){
		set pintCont = $Increment(pintCont) 
		
		set strMsg = "<h4>"_$$^WWWFORMNAME("MEDDrugInteraction")_" #"_pintCont_"</h4>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","P",1)_": </b> "_"("_pidItem1_") "_$$^INARTNAME(pidItem1)_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","P",2)_": </b> "_"("_pidItem2_") "_$$^INARTNAME(pidItem2)_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",1)_": </b> "_idClinic_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",2)_": </b> "_idInteraction_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",3)_": </b> "_$$$MEDDrugInteractionEarlyInteraction(pobjItem1)_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",4)_": </b> "_idProbablity_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",5)_": </b> "_$$$MEDDrugInteractionEffect(pobjItem1)_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",6)_": </b> "_$$$MEDDrugInteractionMechanism(pobjItem1)_"</br>"_
		"<b>"_$$^WWWFELDNAME("MEDDrugInteraction","D",7)_": </b> "_$$$MEDDrugInteractionSuggestion(pobjItem1)_"</br></br>"
		
	}

	quit strMsg

	
CheckDrugInteraction(pYKEY,pblnValidOnly=$$$NO,pblnProcess=$$$NO,&pstrMessage,&pintCont)
	;-------------------------------------------------------------------------------
	; Check if Interaction Message exist for Drugs
	; called by Drug Interaction Button on MEDDispenseLine
	;
	; Params:
	; pYKEY : KEY ID
	;
	; ByRefs:
	;
	; Returns:
	; Boolean
	;
	; History:
	; 19-Feb-2013	SCR		HEVA-792: Dont check unvalidated lines
	; 01-Feb-2013	SCR		HEVA-792: Check Rejected
	; 31-Jan-2013	SCR		HEVA-792: Return Message
	; 30-Jan-2013	SCR		HEVA-792: Include Recjected when not suspended
	; 07-Aug-2012	PPP		SR18067: Updated to check diluents
	; 24-Jul-2012	PPP		SR18067: Added $$$YQHandler
	; 16-Jul-2012	SCR		SR18002 Acced all Active Drug checking
	; 13-Jun-2012	PPP		SR18002.1: Added Diluent Drug Interaction
	; 04-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------		
	new blnExist,idDispense,idLine1,idLine2,objLine1,objLine2,arrGrid,idItem1
	new objItem1,idItem2,idDiluent,objItem2,objDispense,idRx,arrDrugs,dtTo

	
	set YQ = $$$YQDisable("MED00417")	//Dispense does not contain any Drug Interactions.

	set blnExist = $$$NO
	set idDispense 	= $piece(pYKEY,",",1)
	set objDispense	= $get(^MEDDispense(YM,idDispense,1))
	set idRx		= $$$MEDDispensePrescription(objDispense)		

	if '$$$NoKey(pYKEY) {
		set idDispense = $piece(pYKEY,",",1)	
		$$$GRIDGetContents(.arrGrid,"MEDDispenseLine")

		set idLine1 = ""			
		for {
			set idLine1 = $order(arrGrid(idDispense,idLine1))
			quit:idLine1=""
			
			set objLine1 	= $get(arrGrid(idDispense,idLine1))

			;continue:$$$MEDDispenseLineValidatedStatus(objLine1)="refuse.gif"
			set objRxLine	= $get(^MEDPrescriptionLine(YM,idRx,idLine1,1))

			if ($$$MEDDispenseLineValidatedStatus(objLine1)="refuse.gif") {
			;if ($$$MEDDispenseLineValidatedStatus(objLine1)="refuse.gif") &&
			;	($$$MEDPrescriptionLineStatus(objRxLine) = 6) {
					continue
			}

			if $$$MEDDispenseLineValidation(objLine1)'=1 continue
			; HEVA-792 vvvv
			;if pblnValidOnly {
			;	continue:'$$$MEDDispenseLineValidation(objLine1)
			;} 
			; HEVA-792 ^^^^
			if pblnProcess {
				continue:$$$MEDDispenseLineValidatedStatus(objLine1)="blank_flat.gif"			
			}				

			set idItem1 = $$$MEDDispenseLineItemName(objLine1)

			set dtTo= $$$MEDDispenseLineToDate(objLine1)
			set:dtTo>+$get(arrDrugs("h",idItem1)) arrDrugs("h",idItem1)=dtTo_Y_idRx_Y_idLine1
			set arrDrugs("l",idItem1)=""

			set idDiluent = $$$MEDDispenseLineDiluent(objLine1)
			if idDiluent'="" {
				;set arrDrugs("d",idItem1,idDiluent)=""
				set arrDrugs("l",idDiluent)=""
				set:dtTo>+$get(arrDrugs("h",idDiluent)) arrDrugs("h",idDiluent)=dtTo_Y_idRx_Y_idLine1
				;set arrDrugs("h",idDiluent)=""

			}
			
			
		}

	}
	set pstrMessage = $$GetInteractionMessage(.arrDrugs,.pintCont,idRx,$get(idDispense))

	if pstrMessage'="" {
		set blnExist=$$$YES		
	 	$$$YQHandler(blnExist)

	}
	
	quit blnExist

DrugLoad(pidPatient,&parrDrugs,pidCurDisp="")		
	;-------------------------------------------------------------------------------
	; Get Drugs for patient
	; 
	; Params:
	; pid, parrDrugs List of drugs
	;
	;
	; Returns:
	; Message
	;
	; History:
	; 19-Feb-2013	SCR		HEVA-792: Dont Check Current Dispense (If loaded from edit grid)
	; 30-Jan-2013	SCR		HEVA-792: Include closed drugs, Rejected & (Suspended & dispensed)
	; 06-Jul-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------	
	new idRx,objRx,idLine,objLine,idxPat,idStatus,idItem,dtTo
	if ($get(pidPatient)'="") {
		set idxPat=$$$Index(pidPatient)
		set idRx=""
		for {
			set idRx=$order(^MEDPrescriptions(YM,1,idxPat,idRx),-1)
			quit:idRx=""
			set objRx=$get(^MEDPrescription(YM,idRx,1))
			do LoadDispenses(idRx,.parrDrugs,pidCurDisp) ; HEVA-792
			set idLine=""
			/*
			for {
				set idLine=$order(^MEDPrescriptionLine(YM,idRx,idLine))
				quit:idLine=""
				set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
				set idStatus = $$$MEDPrescriptionLineStatus(objLine)
				; HEVA-792 vvvv
				;continue:idStatus>4 ; HEVA-792
				if (idStatus=8) || (idStatus=9) {
					continue
				}
				if idStatus=6 {
					if '$$$MEDPrescriptionLineDispensedQty(objLine) {
						continue
					}
				}
				; HEVA-792 ^^^^
				set dtTo= $$$MEDPrescriptionLineToDate(objLine)
				set idItem = $$$MEDPrescriptionLineItem(objLine) 
				set:dtTo>+$get(parrDrugs("h",idItem)) parrDrugs("h",idItem)=dtTo_Y_idRx_Y_idLine
				set idItem = $$$MEDPrescriptionLineDiluent(objLine)
				if idItem'="" {
					set:dtTo>+$get(parrDrugs("h",idItem)) parrDrugs("h",idItem)=dtTo_Y_idRx_Y_idLine
				}
			}
			*/
		}
	}
	quit
	
LoadDispenses(pidRx,&parrDrugs,pidCurDisp="")
	;-------------------------------------------------------------------------------
	; Load Dispensed Drugs including Alternate and Modified Diluents
	; 
	; Params: Rx & Array
	;
	; Returns: Updated array
	;
	; History:
	; 26-Feb-2013	SCR		HEVA-792: Check Rejected Lines
	; 20-Feb-2013	SCR		HEVA-792: Correction
	; 19-Feb-2013	SCR		HEVA-792: Dont Check Current Dispense (If loaded from edit grid)
	; 19-Feb-2013	SCR		HEVA-792: Dont check unvalidated dispenses & lines
	; 30-Jan-2013	SCR		HEVA-792: Created
	;-------------------------------------------------------------------------------
	new idxRx,idDisp,idLine,objLine,idItem,dtTo,objRxLine,objDisp,blnDispensed
	
	set idxRx 	= $$$Index(pidRx)
	set idDisp	= ""
	for {
		set idDisp 	= $order(^MEDDispenses(YM,1,idxRx,idDisp))
		quit:idDisp=""
		;if idDisp=pidCurDisp continue
		set objDisp = $get(^MEDDispense(YM,idDisp,1))
		set idLine 	= ""
		if $$$MEDDispenseStatus(objDisp)<2 Continue
		for {
			set idLine		= $order(^MEDDispenseLine(YM,idDisp,idLine))
			quit:idLine=""
			set objLine		= $get(^MEDDispenseLine(YM,idDisp,idLine,1))
			set dtTo		= $$$MEDDispenseLineToDate(objLine)
			set idItem		= $$$MEDDispenseLineItemName(objLine)
			set objRxLine	= $get(^MEDPrescriptionLine(YM,pidRx,idLine,1))
			if ($$$MEDDispenseStatus(objDisp) = 3) && ($data(^MEDDispensePickLine(YM,idDisp,idLine,1))) {
				set blnDispensed = $$$YES
			} else {
				set blnDispensed = $$$NO
			}
			if ($$$MEDDispenseLineValidatedStatus(objLine)="refuse.gif") &&
				($$$MEDPrescriptionLineStatus(objRxLine) = 6)  && (blnDispensed = $$$NO) {
				;($$$MEDPrescriptionLineStatus(objRxLine) = 6) {
					continue
			}
			if $$$MEDDispenseLineValidation(objLine)'=1 continue

			if idItem'="" {
				set:dtTo>+$get(parrDrugs("h",idItem)) parrDrugs("h",idItem)=dtTo_Y_pidRx_Y_idLine
			}

			set parrDrugs("disp",pidRx,idLine)=""
			set idItem	= $$$MEDDispenseLineDiluent(objLine)
			if idItem'="" {
				set:dtTo>+$get(parrDrugs("h",idItem)) parrDrugs("h",idItem)=dtTo_Y_pidRx_Y_idLine
			}
			
		}
	}
	quit	

OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
	; Show Drugs Interactions
	; 
	; Params:
	; pid
	;
	;
	; Returns:
	; Message
	;
	; History:
	; 08-Jan-2013	SCR		HEVA-748: Created
	;-------------------------------------------------------------------------------	
	new lstHeader,rs,intCount,idInteractiveDrug,idClinicalImportance,idInteractionImportance,fltEarlyInteraction,idOccuranceProbability
	new strKey,strInteractiveDrug,strClinicalImportance,strInteractionImportance,strOccuranceProbability
	
	if ($$$NoKey(pYKEY)) quit

	// Monta cabe√ßalho da tabela
  	set lstHeader = $listbuild("Seq.",$$$Text("MED01413"),$$$Text("MED01414"),$$$Text("MED01415"),$$$Text("MED01416"),$$$Text( "MED01417" ))
	;Interactive Drug ;Clinical<br/>Importance ;Interaction<br/>Importance ;Interaction Time ;Interaction Probability
  	if ($$Start^COMTable(lstHeader, "<div style=margin:3px;>" _ $$$Text("MED01412") _"</div>", , , $$$NO)) {  ;Registered Drug Interactions
	 	set rs = ##class(%ResultSet).%New()
	    do rs.Prepare("SELECT   InteractiveDrug, ClinicalImportance, "_
	    	          "         InteractionImportance,EarlyInteraction, "_
	    	          "         OccuranceProbability "_
	    	          "FROM     MEDDrugInteraction "_
	    	          "WHERE    Drug = ? "_
	    	          "ORDER BY InteractiveDrug")
	    do rs.Execute($$$KEY1(pYKEY))

		set intCount = 0

	    while (rs.Next()) {
			if ($System.Status.IsOK(rs)) { // check if this succeeded
										   // without errors		
	        	// Pegando valores da query
	        	set idInteractiveDrug		= rs.GetData(1)
	        	set idClinicalImportance	= rs.GetData(2)
	        	set idInteractionImportance	= rs.GetData(3)
	        	set fltEarlyInteraction		= rs.GetData(4)
	        	set idOccuranceProbability	= rs.GetData(5)

				set strKey = $$$KEY1(pYKEY)_$$$COMMA_idInteractiveDrug

				set strInteractiveDrug = idInteractiveDrug_" - "_
					$$$INARTSearchName($get(^INART(YM, idInteractiveDrug, 1)))
				set strClinicalImportance = idClinicalImportance
				if ($length(idClinicalImportance) > 0) {
					set strClinicalImportance =
						$$$VARPARAText($get(^INPARA(YM, "MEDCLIIMP", SPRACHE, idClinicalImportance, 1)))
				}
				set strInteractionImportance = idInteractionImportance
				if ($length(strInteractionImportance) > 0) {
					set strInteractionImportance =
						$$$VARPARAText($get(^INPARA(YM, "MEDCLIIMP", SPRACHE, idInteractionImportance, 1)))
				}
				set strOccuranceProbability =
					idOccuranceProbability
				if ($length(idOccuranceProbability) > 0) {
					set strOccuranceProbability =
						$$$VARPARAText($get(^INPARA(YM, "MEDPROBABILITY", SPRACHE, idOccuranceProbability, 1)))
				}

				set intCount = $increment(intCount)

    			do NewLine^COMTable()
    			do InsertCell^COMTable(intCount, 				YFORM, strKey)
			    do InsertCell^COMTable(strInteractiveDrug,    	YFORM, strKey)
			    do InsertCell^COMTable(strClinicalImportance,  	YFORM, strKey, , "CENTER")
			    do InsertCell^COMTable(strInteractionImportance,YFORM, strKey, , "CENTER")
			    do InsertCell^COMTable(fltEarlyInteraction,    	YFORM, strKey, , "CENTER")
			    do InsertCell^COMTable(strOccuranceProbability,	YFORM, strKey)
		        do EndLine^COMTable()
			}
	    }

	    if (intCount = 0) {
			do NewLine^COMTable() 
			do InsertCell^COMTable( $$$Text("MED01418") , , , , , , 6)  ;No interaction recorded for this medicine
			do EndLine^COMTable()
	    }

		do NewLine^COMTable("lightsteelblue")
		do InsertCell^COMTable("<strong>"_$$$Text("MED01419")_"</strong>",YFORM, $$$KEY1(pYKEY), , "RIGHT", , 6) ;Register new interaction
		do EndLine^COMTable()

	 	do Stop^COMTable()
  	}
	quit


]]></Routine>
</Export>