<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESTreatmentHCLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMGridEdit31
#include INConst
#include VARConst
#include VARSESFarmaciaAltoCusto 
 
VARSESTreatmentHCLine
	quit
 
OnLineAdded(pintRowCount="", pstrKey="")
	do AttachAdditional(pintRowCount,pstrKey)
	quit $$$OK
 
AttachAdditional(pintRow, pstrKey)
	new objTHC, idTreatmentHC, idTreatmentHCLine, blnHide,
		idTreatmentHCLine1, objTHCLine1
 
	set objTHC = $$GetYFELDContainer^COMGridEdit31Interface()
 
	if ($$$GRIDAllowLinkedLines) {
		set idTreatmentHC = $piece(pstrKey, $$$COMMA, 1)
		set idTreatmentHCLine = $piece(pstrKey, $$$COMMA, 2)
 
		if ($length(idTreatmentHCLine) > 0) {
			set blnHide = ($length(objTHC) > 0)
 
			set idTreatmentHCLine1 = $order(^VARSESTreatmentHCLine1(YM,
				idTreatmentHC, idTreatmentHCLine, ""))
			while ($length(idTreatmentHCLine1) > 0) {
				set objTHCLine1 = $get(^VARSESTreatmentHCLine1(YM,
					idTreatmentHC, idTreatmentHCLine, idTreatmentHCLine1, 1))
				$$$GRIDAddLine(objTHCLine1,idTreatmentHC_$$$COMMA_idTreatmentHCLine_$$$COMMA_idTreatmentHCLine1,"VARSESTreatmentHCLine1",$$$NO,idTreatmentHCLine)
 
				set idTreatmentHCLine1 = $order(^VARSESTreatmentHCLine1(YM,
					idTreatmentHC, idTreatmentHCLine, idTreatmentHCLine1))
			}
		}
	}
	quit
 
GetQuantidadeMensalLinhaPrescrita(pidTreatment, pidLine, pdatReferencia,
	pstrDescricaoPorMes)
	new fltQuantidade, idSeq, objTreatmentLine1
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0)) quit -1
 
	set pdatReferencia = +pdatReferencia
	set fltQuantidade = 0
	set pstrDescricaoPorMes = ""
 
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 
		if ((pdatReferencia = 0) ||
			($$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia) =
				$$GetMes^VARSESFarmaciaAltoCustoUtil(
					$$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1)))) {
			set fltQuantidade = fltQuantidade +
				$$$VARSESTreatmentHCLine1Quantidade(objTreatmentLine1)
 
			if ($length(pstrDescricaoPorMes) > 0) {
				set pstrDescricaoPorMes = pstrDescricaoPorMes_" "
			}
			set pstrDescricaoPorMes = pstrDescricaoPorMes_"("_
				$$GetDescricaoMesAbreviado^VARSESFarmaciaAltoCustoUtil(
					$$GetMes^VARSESFarmaciaAltoCustoUtil(
						$$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1)))_")"_
					$$$VARSESTreatmentHCLine1Quantidade(objTreatmentLine1)
 
			if (pdatReferencia '= 0) quit
		}
 
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
 
	quit fltQuantidade
 
GetQuantidadeTotalLinhaPrescrita(pidTreatment, pidLine, pstrDescricaoPorMes)
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0)) quit -1
 
	quit $$GetQuantidadeMensalLinhaPrescrita(pidTreatment, pidLine, 0,
		.pstrDescricaoPorMes)
 
GetQuantidadeMensalLinhaConsumida(pidTreatment, pidLine, pdatReferencia)
	new fltQuantidadeConsumida, idINIssue, objINIssue, objINIssueLine
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0) ||
		($length(pdatReferencia) = 0)) {
		quit -1
	}
	set pdatReferencia = +pdatReferencia
	set fltQuantidadeConsumida = 0
 	set idINIssue = ""
	set idINIssue = $order(^INIssues(YM,5,3,pidTreatment,idINIssue))
	// inserido o tratamento para tentar recupara com ou sem o Index formatado pelo $$$SAVE
	if (idINIssue = "") {
		set pidTreatment = $$$Index(pidTreatment)
		set idINIssue = $order(^INIssues(YM,5,3,pidTreatment,idINIssue))
	}
	while ($length(idINIssue) > 0) {
		if ($data(^INIssue(YM, idINIssue, 1))'=1) {
			set idINIssue = $order(^INIssues(YM,5,3,pidTreatment,idINIssue))
		}
		else {
			set objINIssue = ^INIssue(YM, idINIssue, 1)
 
			if ((($$$INIssueFREE16(objINIssue) = $$$VARSESTIPOPACIENTEUSERAltaComplexidade) ||
				 ($$$INIssueFREE16(objINIssue) = $$$VARSESTIPOEstornoAltaComplexidade))&&
				($$$INIssueStatus(objINIssue) = 9) &&
			     $data(^INIssueLine(YM, idINIssue, pidLine, 1)) &&
			     ((pdatReferencia = 0) ||
				  ($$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia) =
				  	$$GetMesCompetencia^VARSESPatientConsumptionHC(objINIssue)))) {
				set objINIssueLine = ^INIssueLine(YM, idINIssue, pidLine, 1)
 
	 			if ($$$INIssueFREE16(objINIssue) = $$$VARSESTIPOPACIENTEUSERAltaComplexidade) {
					if ($$$INIssueLineQtyPicked(objINIssueLine) > 0) {
						set fltQuantidadeConsumida = fltQuantidadeConsumida +
							$$$INIssueLineQtyPicked(objINIssueLine)
					}
	 			} 
	 			if ($$$INIssueFREE16(objINIssue) = $$$VARSESTIPOEstornoAltaComplexidade) {
					if ($$$INIssueLineQtyPicked(objINIssueLine) < 0) {
						set fltQuantidadeConsumida = fltQuantidadeConsumida +
							$$$INIssueLineQtyPicked(objINIssueLine)
					}
	 			} 
 			
			}
			set idINIssue = $order(^INIssues(YM,5,3,pidTreatment,idINIssue))
		}
	}
 
	quit fltQuantidadeConsumida
 
GetQuantidadeMensalLinhaResquicio(pidTreatment, pidLine, pdatReferencia)
	new fltQuantidadeResquicio, idSeq, datRef, arrDatRef,
		fltQuantidadePrescrita, fltQuantidadeConsumida
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0) ||
		($length(pdatReferencia) = 0)) {
		quit -1
	}
 
	set pdatReferencia = +pdatReferencia
	set fltQuantidadeResquicio = 0
 
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 
		set datRef = $$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1)
		set arrDatRef(datRef) = ""
 
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
 
	set datRef = $order(arrDatRef(""))
	while ($length(datRef) > 0) {
		if ($$DataPrimeiroMaiorQueSegundoHC^VARSESFarmaciaAltoCustoUtil(pdatReferencia,datRef)) {
			set fltQuantidadePrescrita = $$GetQuantidadeMensalLinhaPrescrita(
				pidTreatment, pidLine, datRef)
			set fltQuantidadeConsumida = $$GetQuantidadeMensalLinhaConsumida(
				pidTreatment, pidLine, datRef) + fltQuantidadeResquicio
 
			if (fltQuantidadeConsumida <= fltQuantidadePrescrita) {
				set fltQuantidadeResquicio = 0
			}
			else {
				set fltQuantidadeResquicio = fltQuantidadeConsumida -
					fltQuantidadePrescrita
			}
		}
 
		set datRef = $order(arrDatRef(datRef))
	}
 
	quit fltQuantidadeResquicio
 
GetQuantidadeMensalLinhaEmUso(pidTreatment, pidLine, pdatReferencia,
	pbolUltimoCompleto, pstrDescricaoPorMes)
	new fltQuantidadeEmUso, idSeq, datRef, arrDatRef, fltQuantidadeConsumida,
		fltQuantidadeResquicio, fltQuantidadePrescrita, fltQuantidade
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0)) quit -1
 
	set pdatReferencia = +pdatReferencia
	set fltQuantidadeEmUso = 0
	set pstrDescricaoPorMes = ""
 
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 
		set datRef = $$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1)
		set arrDatRef(datRef) = ""
 
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
 
	set datRef = $order(arrDatRef(""))
	while ($length(datRef) > 0) {
		if ((pdatReferencia = 0) ||
			($$DataPrimeiroIgualAoSegundoHC^VARSESFarmaciaAltoCustoUtil(pdatReferencia,datRef))) {
			set fltQuantidadeConsumida = $$GetQuantidadeMensalLinhaConsumida(pidTreatment,
				pidLine, datRef)
			set fltQuantidadeResquicio = $$GetQuantidadeMensalLinhaResquicio(pidTreatment,
				pidLine, datRef)
			set fltQuantidade = fltQuantidadeConsumida + fltQuantidadeResquicio
 
			set fltQuantidadePrescrita = $$GetQuantidadeMensalLinhaPrescrita(
				pidTreatment, pidLine, datRef)
			if (fltQuantidade < 0) {
				set fltQuantidade = 0
			}
			if ((fltQuantidade > fltQuantidadePrescrita) &&
				(('pbolUltimoCompleto) ||
					($length($order(arrDatRef(datRef))) > 0))) {
				set fltQuantidade = fltQuantidadePrescrita
			}
			set fltQuantidadeEmUso = fltQuantidadeEmUso + fltQuantidade
 
			if ($length(pstrDescricaoPorMes) > 0) {
				set pstrDescricaoPorMes = pstrDescricaoPorMes_" "
			}
			set pstrDescricaoPorMes = pstrDescricaoPorMes_
				"("_$$GetDescricaoMesAbreviado^VARSESFarmaciaAltoCustoUtil(
						$$GetMes^VARSESFarmaciaAltoCustoUtil(datRef))_")"_
				fltQuantidade
		}
 
		set datRef = $order(arrDatRef(datRef))
	}
 
	quit fltQuantidadeEmUso
 
GetQuantidadeTotalLinhaEmUso(pidTreatment, pidLine, pbolUltimoCompleto,
	pstrDescricaoPorMes)
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0)) quit -1
 	
	quit $$GetQuantidadeMensalLinhaEmUso(pidTreatment, pidLine, 0,
		pbolUltimoCompleto, .pstrDescricaoPorMes)
 
GetQuantidadeTotalLinhaEmUsoSQL(pidTreatment, pidLine, pdatMes)
	do ^WWWVAR
	
	quit $$GetQuantidadeMensalLinhaEmUso(pidTreatment, pidLine, pdatMes, 1)
 
GetQuantidadeLiberada(pidTreatment, pidLine, pdatMes)
	quit $$GetQuantidadeMensalLinhaEmUso(pidTreatment, pidLine, pdatMes, 1)
 
GetQuantidadeMensalLinhaDisponivel(pidTreatment, pidLine, pdatReferencia)
	new fltQuantidadePrescrita, fltQuantidadeEmUso, fltQuantidadeDisponivel
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0) ||
		($length(pdatReferencia) = 0)) {
		quit -1
	}
 
	set pdatReferencia = +pdatReferencia
	set fltQuantidadePrescrita = $$GetQuantidadeMensalLinhaPrescrita(pidTreatment,
		pidLine, pdatReferencia)
	set fltQuantidadeEmUso = $$GetQuantidadeMensalLinhaEmUso(pidTreatment,
		pidLine, pdatReferencia, 0)
	set fltQuantidadeDisponivel = fltQuantidadePrescrita - fltQuantidadeEmUso
 
	quit $select(fltQuantidadeDisponivel<0: 0, 1:fltQuantidadeDisponivel)
 
GetQuantidadeTotalLinhaDisponivel(pidTreatment, pidLine, pdatReferencia)
	new fltQuantidadeDisponivel, idSeq, datRef, arrDatRef,
		fltQuantidadePrescrita
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0) ||
		($length(pdatReferencia) = 0)) {
		quit -1
	}
 
	set pdatReferencia = +pdatReferencia
	set fltQuantidadeDisponivel = $$GetQuantidadeMensalLinhaDisponivel(
		pidTreatment, pidLine, pdatReferencia)
 
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 
		set datRef = $$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1)
		set arrDatRef(datRef) = ""
 
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
 
	set datRef = $order(arrDatRef(pdatReferencia))
	while ($length(datRef) > 0) {
		set fltQuantidadePrescrita = $$GetQuantidadeMensalLinhaDisponivel(
			pidTreatment, pidLine, datRef)
		set fltQuantidadeDisponivel = fltQuantidadeDisponivel +
			fltQuantidadePrescrita
 
		set datRef = $order(arrDatRef(datRef))
	}
 
	quit fltQuantidadeDisponivel
 
 /// Jorge - 06-2010 
 /// Retorna verdadeiro se o autorizado de todos os meses for atendido
IsCompleto(pidTreatment, pidLine)
	new bolCompleto, idSeq
 
	if (($length(pidTreatment) = 0) || ($length(pidLine) = 0)) {
		quit -1
	}
 
	set bolCompleto = 1
 
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 
		//if ($$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia) =
		//		$$GetMes^VARSESFarmaciaAltoCustoUtil(
		//			$$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1))) {
			set bolCompleto = +$$$VARSESTreatmentHCLine1Atendido(objTreatmentLine1) && bolCompleto
			quit:'bolCompleto
		//}
 
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
 
	quit bolCompleto
	
IsLinhaDisponivel(pidTreatment, pidLine, pdatReferencia)
	set bolDisponivel = 0
	set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, ""))
	set objTreatmentHCLine = ^VARSESTreatmentHCLine(YM, pidTreatment, pidLine, 1)
	if ($length($$$VARSESTreatmentHCLineStatusTRAK(objTreatmentHCLine))>0) {
		if (($$$VARSESTreatmentHCLineStatusTRAK(objTreatmentHCLine) '=
				$$$TreatmentStatusTRAKAvaliado) &&
			($$$VARSESTreatmentHCLineStatusTRAK(objTreatmentHCLine) '=
				$$$TreatmentStatusTRAKAprovado)) {
			quit 0	
		}
	}

	while ($length(idSeq) > 0) {
		set objTreatmentLine1 = ^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine,
			idSeq, 1)
 		set strReferencia = $$GetMes^VARSESFarmaciaAltoCustoUtil(pdatReferencia)
 		set strDatTreat = $$GetMes^VARSESFarmaciaAltoCustoUtil(
					$$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1))
		set strMesRef = +$piece(strReferencia,"/",1)
		set strMesTreat = +$piece(strDatTreat,"/",1)
		set strAnoRef = +$piece(strReferencia,"/",2)
		set strAnoTreat = +$piece(strDatTreat,"/",2)
		
		if ((strAnoRef < strAnoTreat) ||
			((strAnoRef = strAnoTreat) && (strMesRef '> strMesTreat))) {
			set fltQuantDisp = 
				$$GetQuantidadeMensalLinhaDisponivel^VARSESTreatmentHCLine(
					pidTreatment, pidLine, $$$VARSESTreatmentHCLine1DataReferencia(objTreatmentLine1))
			set bolDisponivel = (fltQuantDisp > 0) 	
			quit:bolDisponivel
		}
 
		set idSeq = $order(^VARSESTreatmentHCLine1(YM, pidTreatment, pidLine, idSeq))
	}
	quit bolDisponivel
]]></Routine>
</Export>