<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPatientConsumptionHCLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESPatientConsumptionHCLine
	#include COMSYS
	#include INConst
	#include VARConst
	#include VARSESConst
	#include VARSESFarmaciaAltoCusto
	quit
 	//okv3
OnAfterPrimaryKey()
	new idINIssue, idINIssueLine, objINIssue, objTreatmentHCLine, idINART, objINART, 
		idINARTAlt
 
	set idINIssue = $piece(YKEY, $$$COMMA, 1)
	set idINIssueLine = $piece(YKEY, $$$COMMA, 2)
	set objINIssue = ^INIssue(YM, idINIssue, 1)
 
	set objTreatmentHCLine = $get(^VARSESTreatmentHCLine(YM, $$$INIssueReference(objINIssue),
		idINIssueLine, 1))
		
	if (objTreatmentHCLine = "") {
		$$$Alert("Esta não é uma dispensação de alta complexidade!")
		do GoToForm^COMUtilForm("VARSESPatientConsumptionHCPesquisa","")
		quit	
	}
	
	set idINART = $$$VARSESTreatmentHCLineCodigoMedicamento(objTreatmentHCLine)
	set objINART = $get(^INART(YM, idINART, 1))
	kill ^COMTempList(YM, YUSER, "VARSESTreatmentHCLineItem", idINIssueLine)
	set ^COMTempList(YM, YUSER, "VARSESTreatmentHCLineItem", idINIssueLine, idINART,
		1) = $$$INARTSearchName(objINART)
	set idINARTAlt = $order(^INARTA(YM, idINART, ""))
	while ($length(idINARTAlt) > 0) {
		set objINART = $get(^INART(YM, idINARTAlt, 1))
		set ^COMTempList(YM, YUSER, "VARSESTreatmentHCLineItem", idINIssueLine,
			idINARTAlt, 1) = $$$INARTSearchName(objINART)
 
		set idINARTAlt = $order(^INARTA(YM, idINART, idINARTAlt))
	}
 
	set GblQuantidadeAutorizada =
		$$GetQuantidadeMensalLinhaPrescrita^VARSESTreatmentHCLine(
			$$$INIssueReference(objINIssue), idINIssueLine, $$$INIssueCreatedOn(objINIssue))
	set GblEstoqueCentral = $$GetSOHLocal($$$INIssueLineItem(YFELD), 5)
	do GetQuantidadeTotalLinhaPrescrita^VARSESTreatmentHCLine(
		$$$INIssueReference(objINIssue), idINIssueLine, .GblTotalAutorizado)
	do GetQuantidadeTotalLinhaEmUso^VARSESTreatmentHCLine(
		$$$INIssueReference(objINIssue), idINIssueLine, 1, .GblTotalDispensado)
	quit
 	//okv3
OnBeforeSave()
	new idINIssue, idINARTNovo, idINIssueLine, objINIssueLine, idINARTAtual
 
	set idINIssue = $piece(YKEY, $$$COMMA, 1)
	set idINIssueLine = $piece(YKEY, $$$COMMA, 2)
	if ($$IsEditavel^VARSESPatientConsumptionHC(idINIssue)) {
		do SetValoresRelacionadosProduto(.YFELD)
	}
	if ($length($$$INIssueLineQtyPicked(YFELD)) = 0) {
		set $$$INIssueLineQtyPicked(YFELD) = 0
	}
	quit
 	//okv3
CreateLinesFromTreatment(pidTreatment, pidINIssue)
 	new idINIssueLine, idTreatmentLine, objTreatmentLine, fltQuantity,
 		objINIssueLine
 
 	set idINIssueLine = $order(^INIssueLine(YM, pidINIssue, ""))
 	while ($length(idINIssueLine) > 0) {
 		set strStatus = $$$Kill("INIssueLine",pidINIssue_","_idINIssueLine)
 		if ('$$$ISOK(strStatus)) quit
 
		set idINIssueLine = $order(^INIssueLine(YM, pidINIssue, idINIssueLine))
 	}
 
 	set idTreatmentLine = $order(^VARSESTreatmentHCLine(YM, pidTreatment, ""))
 	while ($length(idTreatmentLine) > 0) {
		set objTreatmentLine = ^VARSESTreatmentHCLine(YM, pidTreatment,
			idTreatmentLine, 1)
 
	 	set idINIssueLine = pidINIssue_","_idTreatmentLine
		set objINIssueLine = ""
		set $$$INIssueLineStatus(objINIssueLine) = 1
 		set $$$INIssueLineItem(objINIssueLine) =
 			$$$VARSESTreatmentHCLineCodigoMedicamento(objTreatmentLine)
 		//set $$$INIssueLineQtyPicked(objINIssueLine) = ""
 		do SetValoresRelacionadosProduto(.objINIssueLine)
 		set $$$INIssueLineStatus(objINIssueLine) = 1
 		set fltQtdePrescrica = $$GetQuantidadeMensalLinhaPrescrita^VARSESTreatmentHCLine(
		pidTreatment, idTreatmentLine, $horolog)
		if (fltQtdePrescrica) {
	 		set strStatus = $$$Save("INIssueLine",idINIssueLine,objINIssueLine,1)
 			if ('$$$ISOK(strStatus)) quit
		}
 
		set idTreatmentLine = $order(^VARSESTreatmentHCLine(YM, pidTreatment,
			idTreatmentLine))
 	}
 	quit
 	//okv3
OnItemBlur(&pYFELD) 
	new idINART 
  
	set idINART = $$$INIssueLineItem(pYFELD) 
	if ($length(idINART) = 0) quit 
  
	set $$$INIssueLineQtyPicked(pYFELD) = "" 
	do SetValoresRelacionadosProduto(.pYFELD) 
	do OnItemBlurMessage()
	quit 
 	//okv3
ExecutaPosProcessamento(pidTreatment, pidINIssue, pdatReferencia)
 	new strStatus, idINIssueLine, objINIssueLine, objINIssue, strCBO
 
	set strStatus = $$$OK
 	set idINIssueLine = $order(^INIssueLine(YM, pidINIssue, ""))
 	while ($length(idINIssueLine) > 0) {
		set objINIssueLine = ^INIssueLine(YM, pidINIssue, idINIssueLine, 1)
 
 		do SetValoresRelacionadosProduto(.objINIssueLine)
 		set strStatus = $$$Save("INIssueLine",pidINIssue_","_idINIssueLine,objINIssueLine,1)
 		if ('$$$ISOK(strStatus)){ quit 
 		} else {
			set idTreatmentHCLine = idINIssueLine
			set datMesReferencia = $$GetMes^VARSESFarmaciaAltoCustoUtil(
					$$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil())
			set idTreatmentHCLine1 = $$GetSeq^VARSESTreatmentHCLine1(pidTreatment,
					idTreatmentHCLine, datMesReferencia)
			
			set fltQuantidade = $$$INIssueLineQtyPicked(objINIssueLine)
			//conferir $$$INIssueLineFREE3
			set bolEstorno = ($$$INIssueLineFREE3(objINIssueLine)=$$$VARSESTIPOEstornoAltaComplexidade)
			if (fltQuantidade '= 0) {
				do RegistrarAlteracao^VARSESTreatmentHCHist(pidTreatment, 5, 
						idTreatmentHCLine, idTreatmentHCLine1, 
 						fltQuantidade, datMesReferencia)
			}
		}
 
		set idINIssueLine = $order(^INIssueLine(YM, pidINIssue, idINIssueLine))
 	}
 
	set objINIssue = $Get(^INIssue(YM, pidINIssue, 1))
	set strCBO = $$$WWW013FREE4($get(^WWW013(0,YBED,1)))
	
	set strStatus = $$SetMotivoSaida^VARSESTreatmentHCLine1(pidTreatment,
		$$$INIssueCreatedOn(objINIssue), $$$MotivoSaidaConsumoNormal, strCBO)
 	quit strStatus
  	//okv3
SetValoresRelacionadosProduto(pobjINIssueLine)
	set $$$INIssueLineUnit(pobjINIssueLine) =
		$$LoadUnitOfMeasureList^INARTUtilities($$$INIssueLineItem(pobjINIssueLine))
	set $$$INIssueLineLocationInventory(pobjINIssueLine) =
		$$GetSOHLocal($$$INIssueLineItem(pobjINIssueLine), YLOCATION)
	set $$$INIssueLineFromStorage(pobjINIssueLine) = YLOCATION
	quit
  	//okv3
ValidarQuantidade()
	new fltQtdeAtender
 
	set $$$INIssueLineLocationInventory(YFELD) =
		$$GetSOHLocal^VARSESPatientConsumptionHCLine(
			$piece(YFELD, Y, 1), YLOCATION)
 
	set fltQtdeAtender = $$$INIssueLineQtyPicked(YFELD)
	if ($length(fltQtdeAtender) = 0) {
		quit 1
	}
	elseif (+fltQtdeAtender < 0) {
		$$$Alert($$$Text(334021))
		quit 0
	}
	elseif (+fltQtdeAtender < +$$$INIssueLineQtyPicked(YFELD)) {
		$$$Alert($$$Text(334022))
		quit 0
	}
	quit 1
  	//okv3
  	
updateLines(idINIssue)
	new objLine, objINIssue
	set objLine = ""
	set objINIssue = $get(^INIssue(YM,idINIssue,1))
	set idIssueLine = ""
	set status = 1
	#Dim objIssueLineCreate as alDRP.dStockIssue
	set idIssueLine = $order(^INIssueLine(YM,idINIssue,idIssueLine))
 	for {
		quit:(idIssueLine = "")
		if ($length(idIssueLine,".") > 1) quit
		set objLine = $get(^INIssueLine(YM,idINIssue,idIssueLine,1))
		//todo inserir outra condição
		if ($$$INIssueLineQtyPicked(objLine) > 0 ) {
				if ((+$$$INIssueStatus(objINIssue) '= 0) &&  ($$$INIssueStatus(objINIssue) > 1)) {
					set $$$INIssueLineStatus(objLine)   = $$$INIssueStatus(objINIssue)
				}
				else {
					set $$$INIssueLineStatus(objLine) = 1
				}
				set strStatus = $$$Save("INIssueLine",idINIssue_$$$COMMA_idIssueLine,objLine,$$$YES)
				set $$$INIssueLineQtyRequired(objLine) = $$$INIssueLineQtyPicked(objLine)				
		 		set objIssueLineCreates = ##class(%Library.ListOfObjects).%New()
		 		if ($$$INIssueLineStatus(objLine) = 1){
		 			set objIssueLineCreates = ##class(alDRP.iDRP).SourceIssue($$$INIssueLineItem(objLine),$$$INIssueFromLocn(objINIssue),$$$INIssueLineQtyRequired(objLine))
					for iCount=1:1:objIssueLineCreates.Count() {
						set objIssueLineCreate = objIssueLineCreates.GetAt(iCount)
						set $$$INIssueLineBundle(objLine) 	    = objIssueLineCreate.Bundle.GetBundleIDforHashKey(objIssueLineCreate.Bundle.GetBundleHash())
						set $$$INIssueLineFromStorage(objLine)  = objIssueLineCreate.Storage.Code
						set $$$INIssueLineQtyIssued(objLine)	= objIssueLineCreate.QtyToIssue
						set $$$INIssueLineReference(objLine) 	= $$$INIssueReference(objINIssue)
						if (($$$INIssueStatus(objINIssue) '= 0) &&  ($$$INIssueStatus(objINIssue) > 1)) {
							set $$$INIssueLineStatus(objLine)   = $$$INIssueStatus(objINIssue)
						}
						else {
							set $$$INIssueLineStatus(objLine) = 1
						}
						if (objIssueLineCreates.Count() > 1 ){
							set idLines = idIssueLine_"."_iCount
						}
						else {
							set idLines = idIssueLine
						}
						set strStatus = $$$Save("INIssueLine",idINIssue_$$$COMMA_idLines,objLine,$$$YES)
					}
					set idIssueLine = $order(^INIssueLine(YM,idINIssue,idIssueLine))
				}
			}
			else {
				set status = "Não deve existir linhas com quantidade = 0 (zero). Favor exluir as linhas que não serão atendidas"
				set $$$INIssueLineBundle(objLine) 	    = 1
				set $$$INIssueLineQtyRequired(objLine)  = $$$INIssueLineQtyPicked(objLine)				
				set $$$INIssueLineQtyIssued(objLine)	= 0
				set strStatus = $$$Save("INIssueLine",idINIssue_$$$COMMA_idIssueLine,objLine,$$$YES)
				set idIssueLine = $order(^INIssueLine(YM,idINIssue,idIssueLine))
				//w strStatus
			}
 	}
	quit status
	  	
OnQuantidadeBlur()
	new fltQtdeAtender, idINIssue, objINIssue, idINIssueLine, fltQtdeRestante,
		strMessage
 
	set fltQtdeAtender = $$$INIssueLineQtyPicked(YFELD)
	set idINIssue = $piece(YKEY, $$$COMMA, 1)
	set objINIssue = ^INIssue(YM, idINIssue, 1)
	set idINIssueLine = $piece(YKEY, $$$COMMA, 2)
	set fltQtdeRestante = $$GetQuantidadeMensalLinhaDisponivel^VARSESTreatmentHCLine(
		$$$INIssueReference(objINIssue), idINIssueLine,
		$$GetDataHoraAtual^VARSESFarmaciaAltoCustoUtil)
	if (fltQtdeRestante < 0) set fltQtdeRestante = 0
	if (fltQtdeAtender > fltQtdeRestante) {
		set strMessage = "Quantidade informada está superior à quantidade "_
			"autorizada para este mês."_$char(10)_$char(10)_
			"        Quantidade informada: "_fltQtdeAtender_$char(10)_
			"        Quantidade autorizada: "_fltQtdeRestante
		$$$Alert($$$Text(strMessage))
	}
	if (fltQtdeAtender= 0){
		$$$Alert($$$Text("Caso não for atender essa linha favor excluí-la antes de processar o documento."))
	}
	do OnItemBlurMessage()
	quit
  	//okv3
OnItemBlurMessage()
	new fltQtdeAtender, idINIssue, objINIssue, idINIssueLine, fltQtdeRestante,
		strMessage
 
	set idINART = $$$INIssueLineItem(YFELD)
	set objINART = ^INART(YM, idINART, 1)
	set strFREE51 = $$$INARTFREE51(objINART)
	if ($length(strFREE51)>0) {
		set strMessage = "Atenção! "_$char(10)_"Reter receita!"_
					$char(10)_"Medicamento sob controle especial."
		$$$Alert($$$Text(strMessage))
	}
	quit
  	//okv3
IsEditavel()
	new idINIssue, idINIssueLine, objINIssue, objINIssueLine, fltQtde, blnCompleto, intMotivo,
		bolDisponivel
 
	set idINIssue = $piece(YKEY, $$$COMMA, 1)
	set idINIssueLine = $piece(YKEY, $$$COMMA, 2)
	if (idINIssue = "") || (idINIssueLine = "") {
		quit 0
	}
	set objINIssue = ^INIssue(YM, idINIssue, 1)
 	set objINIssueLine = ^INIssueLine(YM, idINIssue, idINIssueLine, 1)
		
	set fltQtdePrescrica = $$GetQuantidadeMensalLinhaPrescrita^VARSESTreatmentHCLine(
		$$$INIssueReference(objINIssue), idINIssueLine, $$$INIssueCreatedOn(objINIssue))
	if (fltQtdePrescrica=0) quit 0
	set bolDisponivel = $$IsLinhaDisponivel^VARSESTreatmentHCLine(
		$$$INIssueReference(objINIssue), idINIssueLine, $$$INIssueCreatedOn(objINIssue))
	if ('bolDisponivel) quit 0
 
	set blnCompleto = $$IsCompleto^VARSESTreatmentHCLine($$$INIssueReference(objINIssue),
		idINIssueLine)
	if (blnCompleto) quit 0
 	set intMotivo = $$GetMotivoSaida^VARSESTreatmentHCLine1($$$INIssueReference(objINIssue),
 		idINIssueLine, $$$INIssueCreatedOn(objINIssue))
 	if (($length(intMotivo) '= 0) && (intMotivo '= $$$MotivoSaidaConsumoNormal)) quit 0
 
	quit 1
  	
  	//okv3
GetSOHLocal(pItem, pLocation)
	if ($get(pItem) = "") quit $$$RecordNotSelected
 	quit ##class(alSOH.iSOH).GetQtyAvailable(pItem,pLocation)
 
  	//okv3
CanUserEditItem(pYBED)
	set strPerfil = $$$WWW013UserAccess(^WWW013(0,pYBED,1))
	set bolSaida = (strPerfil [ "84" )
	quit bolSaida
]]></Routine>
</Export>