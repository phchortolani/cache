<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMEditor" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMeditor
 
#include COMSYS
#include WWWConst
#include COMConst
 
#define  NAME(%1)		"COM"_$$$UPPER(%1)
#define  SKIN(%1)		$$$NAME(%1)_"SKIN"
#define  TOOLBAR(%1)	$$$NAME(%1)_"TOOLBAR"
 
OnAfterButtonLine()
	;-------------------------------------------------------------------------------
	; Prepare to show the current Editor - form COMEditor
	; This allows a basic pluggable editor. 
	; Later this could be modified to use anything else we wish.
	;
	; NOTE: This is called only when testing the form COMEditor.
	; 		This form is for testing the editor only.
	;
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 01-Nov-2006	JW		SR14914: Peer - don't need get, removed superfluous code.
	; 13-Oct-2006	RPW		SR14914: Created
	;-------------------------------------------------------------------------------
	new idLang,strHTML,idEditor,idSkin,idToolbar,intMaxCharacter,blnUseStandard
	
	set YNOFOOT=1
	
	set idLang=$$$LOWER(SPRACHE)
	
	// The following lines are temporary. They support EN <==> en-au and PT <==> pt-br
	
	if idLang="en" set idLang="en-au"
	if idLang="pt" set idLang="pt-br"
	
	set strHTML=""
	
	do GetEditor(.idEditor,.idSkin,.idToolbar,.intMaxCharacter,.blnUseStandard)
	
	xecute "do ShowEditor^COM"_idEditor_"(YGIF,idLang,strHTML,idSkin,idToolbar,intMaxCharacter,blnUseStandard)"
 
	quit
 
OnBeforeButtonLine(pstrKey)
	;-------------------------------------------------------------------------------
	; Once saved store the current skin and toolbar names for the relationship.
	; Form - COMEditorConfiguration
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Oct-2006	JW		SR14914: Peer - changed from OnAfterSave. Added parameter
	; 19-Oct-2006	RPW		SR14914: Created
	;-------------------------------------------------------------------------------
 	new idEditor
	set ^CacheTempEditorSkin(YUSER)=""
	set ^CacheTempEditorToolbar(YUSER)=""
	
	quit:$$$NoKey(pstrKey)
	
	set idEditor=$$$KEY2(pstrKey)
	
	set ^CacheTempEditorSkin(YUSER)=$$$SKIN(idEditor)
	set ^CacheTempEditorToolbar(YUSER)=$$$TOOLBAR(idEditor)
	
	quit
	
OnChange()
	;-------------------------------------------------------------------------------
	; When changing the Editor Name, save it automatically.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Oct-2006	RPW		SR14914: Created
	;-------------------------------------------------------------------------------
 
	write "SAVENOW(0);"
	
	quit	
	
GetEditor(&pidEditor,&pidSkin,&pidToolbar,&pintMaxCharacter,&pblnUseStandard)
	;-------------------------------------------------------------------------------
	; Get the editor configuration or the default.
	;
	; Params:
	; pidEditor       : The id of the editor
	; pidSkin         : The id of the skin to use
	; pidToolbar      : The id of the toolbar to use
	; pintMaxCharacter: The maximum number of characters to allow (including tags)
	; pblnUseStandard : Whether to show the Disclinc templates or the standard templates.
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 01-Nov-2006	JW		SR14914: Peer - made ^COMEditorConfiguration shared
	; 24-Oct-2006	RPW		SR14914: Created
	;-------------------------------------------------------------------------------
 	new objEditor
 	
	set pidEditor=$$$WWW012HTMLEditor($get(^WWW012(0,YM,1)))
	
	set objEditor=$get(^COMEditorConfiguration(0,YM,pidEditor,1))		// SR14914 - JW
	
	if objEditor'="" {
		set pidSkin=$$$LOWER($$$COMEditorConfigurationSkin(objEditor))
		if pidSkin="" set pidSkin="default"
		set pidToolbar=$$$COMEditorConfigurationToolbarSet(objEditor)
		if pidToolbar="" set pidToolbar="Default"
		set pintMaxCharacter=$$$COMEditorConfigurationMaximumCharacterCount(objEditor)
		if pintMaxCharacter="" set pintMaxCharacter=32000
		set pblnUseStandard=+$$$COMEditorConfigurationUseStandardTemplates(objEditor)
	} else {
		set pidSkin="default"
		set pidToolbar="Default"
		set pintMaxCharacter=32000
		set pblnUseStandard=$$$NO
	}
 
	quit
	
	
	
ConvertDir(pstrHTML,pNull)
	;-------------------------------------------------------------------------------
	; Translate the picture directory to YGIF - called from HyperEvent
	;
	; Params:	pstrHTML - string to translate
	; 			pNull	 - Not used (2nd parameter from EventValue)
	;
	; ByRefs:
	;
	; Returns:	string
	;
	; History:
	; 08-Nov-2006	JW		SR14914: Created
	;-------------------------------------------------------------------------------
	set pstrHTML = $$Replace^COMUtilStr(pstrHTML,"src="""_YGIF,"src=""{YGIF}")
	quit pstrHTML
	
]]></Routine>
</Export>