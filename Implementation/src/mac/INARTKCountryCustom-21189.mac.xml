<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTKCountryCustom" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTKCountryCustom
	
#include INConst
#include COMSYS
	
DisplayRate(YKEY="")
    ;-------------------------------------------------------------------------------
    ;
    ; Returns:
    ;
    ; History:
	; 20-May-2010 	PPP		SR17314: Only display message when defined on INVORG
    ; 11-Jan-2007	GRF		Expand all commands
    ; 15-Jul-2005	shobby	SR12984:Unable to change page correctly (newed YSEITE)
    ; 16-Jun-2005	RobertW	SR12056: Do not $$$Text things twice.
    ; 10-Jun-2005	shobby	If no rate found display a 'no rate' type message. SR12408
    ; ??-Jun-2005	shobby	Created SR12408
    ;-------------------------------------------------------------------------------
	new strForm,strMessage,YSEITE
	
	if '($$$INVORGHideCustomsInformation($get(^INVORG(YM,YM,1)))) {	   //SR17314
		do GetRate(.YKEY,$horolog,.strForm,.strMessage)
		if strMessage="" {
			set strMessage="IN00336" ;$$$Text("IN00336")  ;No Customs Duty Rate has been set for this Item
		}
		do DisplayMessage^COMUtils(strForm,YKEY,strMessage,,0)
	}
	quit
	
	
GetRate(YKEY="",pdteDate="",pstrForm="",pstrMessage="")
    ;-------------------------------------------------------------------------------
    ; Display a hyperlinked message to the current Customs Rate.  Looks at Country then
    ; Supplier then item level
    ;
    ; Returns:
    ;
    ; History:
    ; 11-Jan-2007	GRF		SR15370: Reset YKEY when formname is adjusted
    ; 12-Jul-2005	shobby	Code Check
    ; 14-Jun-2005	shobby	Check the ExcludedFromDuty flag.  SR12408
    ; 07-Jun-2005	shobby	Created SR12408
    ;-------------------------------------------------------------------------------
	new strMessage,idItem,idSupplier,idCountry,idDate,objINARTKCountryCustom,fltRate
	new objINARTKCustom,objINARTCustom,objINARTKCountry,objINART
	
	set fltRate=""
	if YKEY'="" {
		if pdteDate="" set pdteDate = +$horolog
		set idItem     = $piece(YKEY,",",1)
		set idSupplier = $piece(YKEY,",",2)
		set idCountry  = $piece(YKEY,",",3)
		
		if $$IsExempt(idItem,idSupplier,idCountry) {
			set fltRate=0
			set pstrMessage=$listbuild("IN00334",fltRate,$$$Text("IN00338"))
		} else {
			if (idCountry'="") {
				set pstrForm="INARTKCountryCustom"
				set objINARTKCountry=$get(^INARTKCountry(YM,idItem,idSupplier,idCountry,1))
				set idDate=$order(^INARTKCountryCustom(YM,idItem,idSupplier,idCountry,pdteDate+1),-1)
				if idDate'="" {
					set objINARTKCountryCustom=$get(^INARTKCountryCustom(YM,idItem,idSupplier,idCountry,idDate,1))
					set fltRate=$$$INARTKCountryCustomRate(objINARTKCountryCustom)
				}
				if fltRate'="" {
					set YKEY=YKEY_","_idDate
					set pstrMessage=$listbuild("IN00334",fltRate,idItem_","_idSupplier_","_$$$SysEnum("LAND",idCountry)_","_$$^WWWDATE(idDate))  ;Customs Rate=%1% (%2)
				}
			}
			if (fltRate="")&&(idSupplier'="") {
				set pstrForm="INARTKCustom"
				set YKEY = idItem_","_idSupplier                                ; SR15370
				set idDate=$order(^INARTKCustom(YM,idItem,idSupplier,pdteDate+1),-1)
				if idDate'="" {
					set objINARTKCustom=$get(^INARTKCustom(YM,idItem,idSupplier,idDate,1))
					set fltRate=$$$INARTKCustomRate(objINARTKCustom)
				}
				if fltRate'="" {
					set YKEY=YKEY_","_idDate
					set pstrMessage=$listbuild("IN00334",fltRate,idItem_","_idSupplier_","_$$^WWWDATE(idDate))  ;Customs Rate=%1% (%2)
				}
			}
			if (fltRate="")&&(idItem'="") {
				set objINART=$get(^INART(YM,idItem,1))
				set pstrForm="INARTCustom"
				set YKEY = idItem                                             ; SR15370
				set idDate=$order(^INARTCustom(YM,idItem,pdteDate+1),-1)
				if idDate'="" {
					set objINARTCustom=$get(^INARTCustom(YM,idItem,idDate,1))
					set fltRate=$$$INARTCustomRate(objINARTCustom)
				}
				if fltRate'="" {
					set YKEY=YKEY_","_idDate
					set pstrMessage=$listbuild("IN00334",fltRate,idItem_","_$$^WWWDATE(idDate))  ;Customs Rate=%1% (%2)
					set YSEITE=1
				}
			}
		}
	}
	quit fltRate
	
	
IsExempt(pidItem="",pidSupplier="",pidCountry="")
    ;-------------------------------------------------------------------------------
    ; Determines in there this an 'ExemptFromDuty' flag set.
    ;
    ; Returns:
    ;
    ; History:
    ; 14-Jun-2005	shobby		Created SR12408
    ;-------------------------------------------------------------------------------
	new blnFlag,objINART,objINARTK,objINARTKCountry
	
	set blnFlag=$$$NO
	if pidItem'="" {
		set objINART=$get(^INART(YM,pidItem,1))
		set blnFlag=+$$$INARTExemptFromDuty(objINART)
		if ('blnFlag)&&(pidSupplier'="") {
			set objINARTK=$get(^INARTK(YM,pidItem,pidSupplier,1))
			set blnFlag=+$$$INARTKExemptFromDuty(objINARTK)
			if ('blnFlag)&&(pidCountry'="") {
				set objINARTKCountry=$get(^INARTKCountry(YM,pidItem,pidSupplier,pidCountry,1))
				set blnFlag=+$$$INARTKCountryExemptFromDuty(objINARTKCountry)
			}
		}
	}
	quit blnFlag
	
]]></Routine>
</Export>