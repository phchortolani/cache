<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFM104" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFM104
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LAGERUMBUCHUNGSHINWEIS
	;		
	; Determine Standard Pack Qty from either INDRPITEM (for Locn) or INARTK (for Supplier)
	; Manual Field on form INAUF can contain;
	;    idItem_"+"_fltQty		e.g. ABC0123+12     12 of base unit of item ABC0123
	;    
	;    Validate that the quantity specified is a multiple of the Standard Pack Qty if set 
	;
	;    idOrder_"-"_idOrdLine_"-S"   e.g. SO01234-2-s   or SO01234-0-s
	;    ("S" is not case-sensitive)				Return order line or whole order ?
	;
	; Called By: Form INAUF F104 - On Blur
	; 
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 24-Aug-2004	shobby	Macros
	; 24-Jul-2009	GRF		Doco; macro
	; 27-Jul-2006	GRF		Doco; !=>||
	; 30-Mar-2005	Paul K	Added confirm box if qty not in standard box size. And
	; 						get pack qty from INARTK if not in DRP (SR#10981)
	; 10.02.2003	ULM
	;-------------------------------------------------------------------------------
	NEW YKEY,AUF,POS,VGL
	
	QUIT:$GET(YINHALT)=""
	
	; FIXME : There has been a change to Order Units in INAUFP under SR17138.
	;         Previously the Order Unit defaulted to the Inventory Unit and now it
	;         defaults to the Delivery Unit specified in INARTK.
	;         
	;         The Standard Pack Quantity (D77/F79) in INARTK should be relative to
	;         the Delivery Unit.
	;         
	;         e.g. 1 Pack = 6 Boxes = 72 Each
	;              Delivery Unit = Pack   Std Pack Quantity = 4
	;         
	;         When the order has a value specified in field 104 (e.g. ABC+10) it will
	;         add 10 of the delivery unit to the order line
	;         
	;              Order 10 packs - not a multiple of 4
	;         This will then warn of an exception case.
	;         
	;         This may be satisfactory - need to check and check whether there can
	;         be an inconsistency with the unit applicable to $$$INDRPITEMStdPackQuantity.
	;---         
 	;         There should be similar code in INAUFP to detect when the quantity or
 	;         the order unit is being changed using that form
 	;         
	;         In that case the new order unit needs to be compared with the 
	;              Order 12 boxes - equivalent to 2 packs - not a multiple of 4
	;               
	;              Should probably report required quantity in units specified
	;              i.e. need multiples of 24 boxes in that case
	
	
	;---------------------------------------
	;	FELD		objOrder
	;	D2		$$$INAUFOrderType()
	;	D6		$$$INAUFLocation()
	;	D12		$$$INAUFSupplierNumber()
	;---------------------------------------
	
	IF $FIND(YINHALT,"+") DO  QUIT:$EXTRACT(%TXT(1))="ยง"
	. NEW YFELD,YKEY,FELD,YLOCATION,QTY
	. QUIT:$PIECE(YINHALT,"+",1)=""
	. SET YFELD = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1))
	. SET YKEY  = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1))
	. QUIT:$$$KEY1(YKEY)=""
	. ;
	. SET FELD = $GET(^INAUF(YM,$$$KEY1(YKEY),1))
	. IF $$$INAUFOrderType(FELD)=2 DO                                  ; Purchase Order
	. . SET YLOCATION = $$$INAUFLocation(FELD)
	. . QUIT:YLOCATION=""
	. . SET QTY = $$$INDRPITEMStdPackQuantity($GET(^INDRPITEM(YM,YLOCATION,$PIECE(YINHALT,"+",1),1)))
	. . if QTY="" if $$$INAUFSupplierNumber(FELD)'="" set QTY = $PIECE($GET(^INARTK(YM,$PIECE(YINHALT,"+",1),$$$INAUFSupplierNumber(FELD),1)),Y,77) ;Standard Pack Quantity
	. . IF QTY>1 DO   ;DRPITEM STANDARD QTY
	. . . SET QTY(1) = $PIECE(YINHALT,"+",2)
	. . . IF QTY(1)#QTY'=0 DO
	. . . . write "if (VBConfirm('"
	. . . . write $zcvt($$DecodeError^COMUtils($listbuild("IN00309",QTY)),"o","JS") ; "Warning: Pack size should be in multiples of %1. Proceed?"
	. . . . write "',292,'"
	. . . . write $zcvt($$DecodeError^COMUtils("IN00310"),"o","JS")                 ; "Pack size error."
	. . . . write "')==7) {WWW2.YINAUFM104.value='';WWW2.YINAUFM104.focus();}"  ;      
	. . . . ;SET %TXT(1)="ยง"_$$^WWWTEXT(32776,,1)_": "_$PIECE($GET(^INDRPITEM(YM,YLOCATION,$PIECE(YINHALT,"+",1),1)),Y,24)
	
	
	; 33303 : "Please determine whether Returned Items should go back to stock!"
	
	IF $EXTRACT(YINHALT)="-" SET %TXT(1)="!"_$$^WWWUMLAU($$^WWWTEXT(33303),"") QUIT
	
	IF $EXTRACT(YINHALT)'="-" IF ($PIECE(YINHALT,"-",3)="S") || ($PIECE(YINHALT,"-",3)="s") DO  ;STORNO
	. SET AUF = $PIECE(YINHALT,"-",1)
	. SET POS = $PIECE(YINHALT,"-",2)
	. QUIT:AUF=""
	. ;WENN GESAMTER AUFTRAG ;when order 
	. IF POS=0 IF $DATA(^INAUF(YM,AUF))        SET %TXT(1)="!"_$$^WWWUMLAU($$^WWWTEXT(33303),"") QUIT
	. ;WENN AUFTRAGSPOSITION ;when 
	. IF POS'="" IF $DATA(^INAUFP(YM,AUF,POS)) SET %TXT(1)="!"_$$^WWWUMLAU($$^WWWTEXT(33303),"") QUIT
	
	QUIT
	
]]></Routine>
</Export>