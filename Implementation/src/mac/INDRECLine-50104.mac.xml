<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRECLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRECLine
 
#include COMSYS
#include INConst
 
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^INDRECLine("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
CanSave(pstrKeys,pobjLine)
	;-------------------------------------------------------------------------------
	; Can we save the receipt line ? Called from Save and Post validation.
	;
	; Called By: OnBeforeSave^INRECLine
	;            PostableLine^INRECPost (from Postable^INRECPost)
	;            OnBeforeSave^INReceiptLineForm
	;            OnBeforeSave^INRECBLine
	; Params:
	;
	; ByRefs:
	;
	; Returns:	status
	;
	; History:
	; 02-Apr-2013	SCR		HEVA-811: Convert INBRAND to INBrand
	; 29-Feb-2012	GRF		SR17910.1: Replace early quits with elseif
	; 13-Nov-2011	shobby	SR17910: Test if valid INBRAND
	; 14-Jul-2009	PPP		SR16721: Optional policy definitions (can be 1-Yes, 2-No, 3-Optional)
	; 22-Jun-2009	PPP		SR16510: Updated the extra Field checks based on Item Policy
	; 27-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new blnLotNo,blnUseByDate,blnLotUnique,idItem,idSupplier
	new intBrand,intOwner,intProgram,strStatus
	new objHeader,idLoc
	

	set objHeader	= ^INREC(YM,$$$KEY1(pstrKeys),1)
	set idLoc		= $$$INRECLocation(objHeader)
	
	$$$LogR("CanSave",pstrKeys_"<"_pobjLine)
	
	set idSupplier = $$$INRECLineSupplier(pobjLine)
	set strStatus  = $$CanReceive^INLIEF(idSupplier)

	if $$$ISOK(strStatus) {
		set idItem = $$$INRECLineItem(pobjLine)
		do GetAttributes^INItemPolicyUtils(idItem,.blnLotNo,.blnUseByDate,.blnLotUnique,.intBrand,.intOwner,.intProgram,idLoc)

		if blnLotNo && ($$$INRECLineLotNumber(pobjLine) = "") {
			set strStatus = $$$MakeStatus("IN01061",$$$KEY2(pstrKeys)) 	  ; "Line (%1) Lot Number not entered"
			
		} elseif blnUseByDate && ($$$INRECLineExpirationDate(pobjLine) = "") {
			set strStatus = $$$MakeStatus("IN01062",$$$KEY2(pstrKeys)) 	  ; "Line (%1) Expiry Date not entered"
			
		} elseif (intBrand = 1) && ($$$INRECLineBrand(pobjLine) = "") {
			set strStatus = $$$MakeStatus("IN01063",$$$KEY2(pstrKeys)) 	  ; "Line (%1) Brand not entered"
			
		;} elseif ($$$INRECLineBrand(pobjLine)'="") && '$data(^INBRAND(0,$$$INRECLineBrand(pobjLine))) { ;SR17910
		} elseif ($$$INRECLineBrand(pobjLine)'="") && '$data(^INBrand(0,$$$INRECLineBrand(pobjLine))) { ;HEVA-811
			set strStatus = $$$MakeStatus("IN01330",$$$INRECLineBrand(pobjLine),$$$KEY2(pstrKeys))
								; "Entry needs to be created in the Master File for Brand '%1' on Line '%2'."
		
		} elseif (intOwner=1) && ($$$INRECLineOwner(pobjLine) = "") {
			set strStatus = $$$MakeStatus("IN01064",$$$KEY2(pstrKeys)) 	  ; "Line (%1) Owner not entered"
			
		} elseif (intProgram=1) && ($$$INRECLineProgram1(pobjLine) = "") {
			set strStatus = $$$MakeStatus("IN01065",$$$KEY2(pstrKeys)) 	  ; "Line (%1) Program not entered"
		}
	}	
	
	if $$$ISOK(strStatus) {
		set strStatus = $$CheckQty(pobjLine)
	}
	quit strStatus
	
	
CheckQty(pobjLine)
	;-------------------------------------------------------------------------------
	; Is the qty ok for the receipt line ?
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 26-Feb-2007	JW		SR15453: Created (encapsulated from OnBeforeSave, removed +)
	;-------------------------------------------------------------------------------
	new strStatus
	
	; FIXME : <GRF> what is situation when = 0?
	;               Error message says is a problem but test allows.
	
	set strStatus = $$$OK
	if $$$INRECLineQuantity(pobjLine)<0 {
		set strStatus = $listbuild("IN00442") ; "Receipt quantity must be greater than zero"
	}
	
	quit strStatus
	
]]></Routine>
</Export>