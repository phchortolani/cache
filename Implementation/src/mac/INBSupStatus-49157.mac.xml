<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INBSupStatus" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INBSupStatus
	
#include COMSYS
#include INSupStatus
	
OnBeforeSave(pidStatus,pobjStatus)
	;-------------------------------------------------------------------------------
	; If changing deafult, update current default
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 22-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new idDefault,idGroup,idParent,objDefault,objGroup,strStatus
	
	set strStatus = $$$OK
	
	if $$$INSupStatusDefault1(pobjStatus) {
		// Can only have one default, so if we have just made this one default,
		// remove check from old default
		set idDefault = $$$DefaultStatus
		if (idDefault'="") && (idDefault'=pidStatus) {
			set objDefault = $get(^INSupStatus(0,idDefault,1))
			set $$$INSupStatusDefault1(objDefault) = $$$NO
			set strStatus = $$$Save("INSupStatus",idDefault,objDefault,$$$YES)
		}
	}
	
	if $$$ISERR(strStatus) {
		$$$Error(strStatus)
		set Q = $$$QDontSave
	}
	quit strStatus
	
	
OnBeforeDelete(pidStatus,pobjStatus)
	;-------------------------------------------------------------------------------
	; Can we delete?  ie is it the default, or on a supplier?
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 22-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idxStatus
	
	set strStatus = $$$OK
	
	if $$$INSupStatusDefault1(pobjStatus) {
		set strStatus = $listbuild("IN00663")  	                ; "The default status cannot be deleted."
		
	} else {
		set idxStatus = $$$Index(pidStatus)
		if $data(^INLIEFs(0,2,idxStatus)) || $data(^FINAPSuppliers(0,3,idxStatus)) {
			set strStatus = $listbuild("IN00587",pidStatus)
		} ; "This record (%1) is in use. It cannot be deleted."
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontDelete_strStatus
	}
	quit strStatus
	
]]></Routine>
</Export>