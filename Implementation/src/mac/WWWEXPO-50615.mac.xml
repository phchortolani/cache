<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWEXPO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWEXPO
	;-------------------------------------------------------------------------------
	; 		Export Build Data
	;		EXPORT VON DATEN
	;	DO ^WWWEXPO
	;
	; Called By:
	; 	WWWOUT with VAR(1-6) = WWWOUT,"",FORMDAT.TXT,4,"",1   and   VAR(999) = 1
	; 
	; ***************************************************
	; *                                                 *
	; *                      ALERT                      *
	; *                                                 *
	; *  Note different functionality for YCR variable  *
	; *                                                 *
	; ***************************************************
 	;
 	; Inputs : 
	;	None
	;
	; ByRef :
	;	VORG(1)		YDATEI	Class
	;	VORG(2)		YFKEY	Key
	;	VORG(3)		LW		Filename
	;	VORG(4)				FORMAT 		0=Separator, 1=AS IS, 2=HTML, 3=XML, 4=Caché
	;	VORG(5)		TRENN	Separator						TRENNZEICHEN
	;	VORG(6)				1 = Delete data file			LÖSCHEN DATEI 
	;	VORG(999)			1 = Screen device not open		NICHT BILDSCHIRM DEV ÖFFNEN
	;
	;
	; Returns :
	;	Nothing
	;
	; History :
	; 26-Mar-2008	GRF		Doco; quits
	; 21.08.1997	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	NEW YDATA,YDEV,TRENN,ENCODE
	
	SET YDEV   = $IO
	SET YDATEI = $GET(VORG(1))
	QUIT:$EXTRACT(VORG(1),$LENGTH(VORG(1)))="."
	
	SET YDATA=""
	;IF YDATEI'="" SET YDATA=$GET(^WWW100(0,YDATEI,1))  ;FEHLER
	IF YDATEI'="" SET YDATA=$GET(^WWW001(0,YDATEI,1))  ;FIS;28.10.04;26652
	SET YFKEY = $GET(VORG(2))
	SET LW    = $GET(VORG(3))
	SET TRENN = $GET(VORG(5)) 
	IF +TRENN=0 SET TRENN=59         ; semicolon
	SET ENCODE=$PIECE($GET(^WWWENCODE(0,TRENN,1)),Y,1)  ;26971;TYBD;EXPORT MIT TRENNERUMWANDLUNG IN ENCODE;10,12,2004
	SET TRENN=$CHAR(TRENN)
	SET VERZ=""
	SET VERZ1=""
	
	;	D45		$$$WWW012PhysicalFTPDirectory()
	;	D46		$$$WWW012VirtualFTPDirectory()
	
	IF VORG(3)'="" IF '$FIND(VORG(3),":") DO
	. SET VERZ=$TRANSLATE($PIECE($GET(^WWW012(0,YM,1)),Y,45),"\Ö/",$CHAR(92)_$CHAR(92)_$CHAR(92))
	. IF $EXTRACT(VERZ,$LENGTH(VERZ))'=$CHAR(92) SET VERZ=VERZ_$CHAR(92)
	. SET VERZ1=$PIECE($GET(^WWW012(0,YM,1)),Y,46)
	. IF VERZ1'="" IF $EXTRACT(VERZ1,$LENGTH(VERZ1))'="/" SET VERZ1=VERZ1_"/"
	. IF VERZ1'="" SET VERZ1=VERZ1_VORG(3)
	
	SET LW=VERZ_VORG(3)
	SET LW=$TRANSLATE(LW,"\Ö/",$CHAR(92)_$CHAR(92)_$CHAR(92))
	
	IF LW="" DO  QUIT
	. SET %DEV="" 
	. IF $GET(VORG(999))'=1 DO OPEN^WWWSTART
	. DO DATEI 
	
	QUIT:YDATEI=""
	IF LW'="" DO
	. QUIT:$GET(VORG(999))=1   ;NICHT AUF DEVICE ;Not upon 
	. IF VERZ1'="" DO
	. . WRITE "<BR>"
	. . WRITE " <A HREF="_""""_"ftp://"_VERZ1_""""_">"
	. . WRITE YCR,"<IMG SRC="_""""_YGIF_"docu.gif"_""""_" TITLE="_""""_VERZ1_""""_" border=0>"
	. . WRITE VERZ1,YCR
	. . WRITE "</A>"
	
	SET %DEV=$$^WWWDEV(LW)
	
	IF %DEV="" DO FEHL QUIT
	
	; *** DROP THROUGH ***
	
DEV ;
	USE %DEV
	
	; *** DROP THROUGH ***
	
DATEI ;SELEKTIEREN DER DATEN ;the 
	
	;---------------------------------------
	; VORG(4) : 0=Separator, 1=AS IS, 2=HTML, 3=XML, 4=Caché
	;---------------------------------------
	 
	IF VORG(4)=3 DO  QUIT               ;XML 
	. DO ^WWWXML(YDATEI)
	
	// Could this be a bit smarter and not do ^WWWSOR?
	DO ^WWWSOR(YDATEI,YFKEY,"",0,0,0,1,"")
	DO KOPF
	SET YA(2)=""
	FOR  SET YA(2)=$ORDER(^WWWSOR(YUSER,"KEY",YA(2))) QUIT:YA(2)=""  DO
	. SET YKEY=""
	. FOR  SET YKEY=$ORDER(^WWWSOR(YUSER,"KEY",YA(2),YKEY)) QUIT:YKEY=""  DO
	. . SET YA=$GET(^WWWSOR(YUSER,"KEY",YA(2),YKEY))
	. . DO SATZ
	
	IF VORG(4)=4 WRITE "*",YCR,"*",YCR,"**",YCR,"**",YCR,YCR,"EOF",YCR   ;DATEIEXPORT
	
	IF VORG(4)=2 DO
	. WRITE YCR,"</TABLE>"
	. WRITE "</BODY></HTML>"
	. SET YCR=$CHAR(13,10)             ; *** ALERT *** Note functionality restored ********************
	. WRITE YCR
	
	KILL ^WWWSOR(YUSER)
	IF %DEV'="" DO
	. IF VORG(6)=1 DO
	. . NEW YFILE
	. . SET YFILE=YDATEI
	. . IF $PIECE(YDATA,Y,22)'="" SET YFILE=$PIECE(YDATA,Y,22)  ;ANDERE DATEI ;data file 
	. . SET YA="^"_YFILE_"("_$$^WWWYM(YDATEI)_")"
	. . SET YDATA=$GET(^WWW001(0,YDATEI,1))
	. . IF $PIECE(YDATA,Y,12)'="" IF $PIECE(YDATA,Y,13)'="" DO  ;UCI UND VOL ;UCI And 
	. . . QUIT:$FIND(YA,"^[")
	. . . SET YA="^["_""""_$PIECE(YDATA,Y,12)_""""_","_""""_$PIECE(YDATA,Y,13)_""""_"]"_$PIECE(YA,"^",2,999)
	. . ;
	. . KILL @YA
	. ;
	. CLOSE %DEV 
	. IF $GET(VORG(999))=1 QUIT
	. DO OPEN^WWWSTART
	
	IF $GET(VORG(999))=1 QUIT
	WRITE "<BR>"
	QUIT
	
SATZ ;EINZELSAETZE
	SET YFELD=$$^WWWSETL(YA)
	IF %DEV'="" USE %DEV
	IF VORG(4)=4 WRITE YA,YCR,YFELD,YCR QUIT  ;DATEIEXPORT
	IF VORG(4)'=1 DO ANZEIGE
	;I VORG(4)=1 W YA," = ",YFELD,YCR
	;I VORG(4)=1 W $TR($P(YA,",",2),"""")_";"_YFELD,YCR
	IF VORG(4)=1 WRITE YFELD,YCR  ;AS IS UEBERTRAGUNG DER DATENSÄTZE ;ace the 
	QUIT
	
FEHL ;FEHLER AUF DISK ;shortcoming upon 
	QUIT:$GET(VORG(999))=1  ;NICHT ÖFFNEN ;Not unclose 
	DO OPEN^WWWSTART
	WRITE $$^WWWTEXT(191)     ; "Wrong Class Default"
	QUIT
	
KOPF ;UEBERSCHRIFT
	IF VORG(4)=4 DO  QUIT
	. NEW SPRACHE
	. SET SPRACHE="EN" 
	. WRITE " "_$$^WWWTIME($HOROLOG)_"  "_$$^WWWDATE($HOROLOG)_"  (MSM format)"
	. WRITE YCR
	. WRITE YCR
	
	QUIT:VORG(4)=1
	
	IF VORG(4)=2 DO
	. WRITE "<HTML><BODY><TABLE CELLSPACING=0 BORDER=1><TR><TH>"
	. SET TRENN="</TH><TH>"
	. SET YCR="</TH></TR>"_$CHAR(13,10)
	
	IF VORG(4)'=3 SET YLFN="" FOR  SET YLFN=$ORDER(^WWW002(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. NEW YKO,YMAXKEY
	. SET YKO=$PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,1)
	. SET YMAXKEY=$ORDER(^WWW002(0,YDATEI,""),-1)
	. IF $DATA(^WWW0021(0,YDATEI,YLFN,SPRACHE,1)) SET YKO=$PIECE(^(1),Y,1)
	. IF YMAXKEY=1 IF $TRANSLATE(YKO,"lfdnr. -","LFDNR")="LFDNR" SET YNOHEAD=1 QUIT  ;NUR WENN EIN KEY ;only when uni- KEY 
	. IF VORG(4)=0 WRITE """"
	. IF ENCODE'="" IF $FIND(YKO,TRENN) SET YKO=$$^WWWTRANSLATE(YKO,TRENN,ENCODE)  ;26971;TYBD;EXPORT MIT TRENNERUMWANDLUNG IN ENCODE;10,12,2004
	. WRITE YKO
	. IF VORG(4)=0 WRITE """"
	. WRITE TRENN
	
	IF VORG(4)'=3 SET YLFN="" FOR  SET YLFN=$ORDER(^WWW003(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. NEW YKO
	. SET YKO=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,1)
	. IF $DATA(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)) SET YKO=$PIECE(^(1),Y,1)
	. IF $EXTRACT(YKO,1,5)="_FREE" SET YKO=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,25)  ;SQL NAME WENN FREE;TYBD;3,1,2004
	. IF VORG(4)=0 WRITE """"
	. IF ENCODE'="" IF $FIND(YKO,TRENN) SET YKO=$$^WWWTRANSLATE(YKO,TRENN,ENCODE)      ;26971;TYBD;EXPORT MIT TRENNERUMWANDLUNG IN ENCODE;10,12,2004
	. WRITE YKO
	. IF VORG(4)=0 WRITE """"
	. WRITE TRENN
	
	WRITE YCR
	QUIT
	
ANZEIGE ;
	IF VORG(4)=2 DO
	. WRITE "<TR><TD>"
	. SET TRENN="&nbsp;</TD><TD>"
	. SET YCR="&nbsp;</TD></TR>"_$CHAR(13,10)             ; *** ALERT *** Note different functionality ********************
	
	IF $GET(YNOHEAD)'=1 SET YLFN="" FOR  SET YLFN=$ORDER(^WWW002(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. SET YINHALT=$PIECE(YKEY,",",YLFN)
	. SET YINHALT=$$^WWWFORMAT(YDATEI,"P",YLFN,YINHALT,4)  ;FORMAT
	. ;W $$^WWWUML(YINHALT)
	. IF ENCODE'="" IF $FIND(YINHALT,TRENN) SET YINHALT=$$^WWWTRANSLATE(YINHALT,TRENN,ENCODE) ;26971;TYBD;EXPORT MIT TRENNERUMWANDLUNG IN ENCODE;10,12,2004
	. IF VORG(4)=0 DO
	. . IF $FIND(YINHALT,"E") WRITE """" QUIT
	. . IF $FIND(YINHALT,"e") WRITE """" QUIT
	. . IF +YINHALT=0 IF YINHALT'=0 WRITE """"
	. ;
	. WRITE YINHALT
	. DO
	. . IF $FIND(YINHALT,"E") WRITE """" QUIT
	. . IF $FIND(YINHALT,"e") WRITE """" QUIT
	. . IF +YINHALT=0 IF YINHALT'=0 WRITE """"
	. ;
	. WRITE TRENN
	
	SET YLFN=""
	FOR  SET YLFN=$ORDER(^WWW003(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. SET YINHALT=$PIECE(YFELD,Y,YLFN)
	. ;
	. IF '$FIND(YINHALT,"|") SET YINHALT=$$^WWWFORMAT(YDATEI,"D",YLFN,YINHALT,4)   ;PARAMETER 4 EINGETRAGEN= EXPORT;TYBD;2,2,2005
	. ;W $$^WWWUML(YINHALT)
	. IF ENCODE'="" IF $FIND(YINHALT,TRENN) SET YINHALT=$$^WWWTRANSLATE(YINHALT,TRENN,ENCODE) ;26971;TYBD;EXPORT MIT TRENNERUMWANDLUNG IN ENCODE;10,12,2004
	. IF VORG(4)=0 DO
	. . IF $FIND(YINHALT,"E") WRITE """" QUIT
	. . IF $FIND(YINHALT,"e") WRITE """" QUIT
	. . IF +YINHALT=0 IF YINHALT'=0 WRITE """"
	. ;
	. WRITE $TRANSLATE(YINHALT,"|"," ")
	. IF VORG(4)=0 DO
	. . IF $FIND(YINHALT,"E") WRITE """" QUIT
	. . IF $FIND(YINHALT,"e") WRITE """" QUIT
	. . IF +YINHALT=0 IF YINHALT'=0 WRITE """"
	. ;
	. WRITE TRENN
	
	WRITE YCR
	QUIT
]]></Routine>
</Export>