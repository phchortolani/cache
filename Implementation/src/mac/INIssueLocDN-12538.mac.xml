<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INIssueLocDN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INIssueLocDN  ; Despatch Notice

 ; Copied from INDispenseToPatientDN
	
#include INConst
#include COMSYS
#include %occInclude
	
#def1arg TempStore(%1) ^CacheTemp(YUCI,YUSER,%1)
	
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; Called By : [D] Button on Form INIssueLoc
	
	;---------------------------------------------------------------------------+ List
	;                             Despatch Advice Notice                        |   Title (2A) <  Top
	;                Issued By Location    LOCN_ID (LOCATION_NAME)              |              <   of
	;-----------------------+------------------------+--------------------------+              <  Page
	;   For: Customer       |                        |                          |   Title (2B) <
	;   Reference: CUST_ID  |   Status: ENUM_STATUS  | Issue Number: ISSUE_NO   |              <
	;--------+---------+----+-+-------------+--------+-+------+--------+--------+              <
	; Number | Item    | Item |  Quantity   | Unit     |Unit  | Extended Cost   |   Header     <
	;        |         | Desc |             |          |Cost  |                 |              <
	;--------+---------+------+-------------+----------+------+--------+--------+
	;        |         |      |             |          |      |                 |   ANZ
	;        |         |      |             |          |      |                 |
	;        |         |      |             |          |      |                 |
	;--------+---------+------+-------------+----------+------+-----------------+
	;                                                                           |
	;---------------------------------------------------------------------------+
	
	; May be multiple pages each with Title through to ANZ
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
CanPrintDN(pidIssue)
	;-------------------------------------------------------------------------------
	;
	; Called By:
	;	PrintDN^INIssueLocDN
	;	Form INIssueLoc : Button 6 - Execute To Check For Disabled
	;	
	; Params:
	;	pidIssue - Key of header record
	;
	; ByRefs: None
	;
	; Returns: strStatus, error message if unable to print
	;
	; History:
	; 22-Feb-2010	PPP		SR17185: Created
	;-------------------------------------------------------------------------------
	new objIssue,strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pidIssue) {
		if $$$ISOK(strStatus) {
			if '$data(^INIssueLine(YM,pidIssue)) {
				set strStatus = $$$MakeStatus("IN00427")
			} ; "No stock has been selected to be transferred."
			
			set objIssue = $get(^INIssue(YM,pidIssue,1))
			if $$$ISOK(strStatus) {
				if $$IsLockedSiteLocation^INLocking($$$INIssueFromLocn(objIssue)) {
					set strStatus = $$ReasonLocked^INLocking($$$INIssueFromLocn(objIssue))
				}
			}
		}
 
	} else {
		set strStatus = $$$MakeStatus(32072)  ; "Please select a data record first."
	}
		
	$$$YQHandler(strStatus)
	
	quit strStatus
	
	
PrintDN(pidIssue)
	;-------------------------------------------------------------------------------
	; Print the Delivery Note for the Issue
	;
	; Called By:
	;	PrintDN^INIssueLocPick
	; 
	; Params: pidIssue
	;
	; Returns: Status
	;
	;
	; History:
	; 22-Feb-2010	PPP		SR17185: Created
	;-------------------------------------------------------------------------------
	new ANZM,strStatus,YDDSATZ
	
	kill ^WWWSOR(YUSER)
	
	set strStatus = $$$OK
	set YBACK     = ""	
	set ANZM      = 0
	set YNOFOOT   = 1
	set YWHR      = $get(YWHR)
	
	do List(pidIssue)
		
	if ANZM=0 do ^WWWINFO($$^WWWTEXT(46),1,,1)    ; "No Data Available" 		
	kill ^WWWSOR(YUSER)
		
	quit strStatus
	
	
List(pidIssue)
	;-------------------------------------------------------------------------------
	; Print the Pick List for the Transfer
	;
	; Params: pidIssue
	;
	; Returns: None
	;
	;
	; History:
	; 28-Apr-2010	GRF		SR17285.1: Fix HTML tag balancing
	; 22-Feb-2010	PPP		SR17185: Created
	;-------------------------------------------------------------------------------
	new ANZSEIT,fltTot,idForm,intLn,LFN,objVORGLANG,TEXT,UES,objDefaults,YFELD
	
	set YNOFOOT = 1
	do ^WWWBACK
	
	set objDefaults = $get(^INVORG(YM,YM,1))
	set objVORGLANG = $get(^INVORGLANG(YM,YM,SPRACHE,1))
	set TEXT = $$$INVORGLANGTextAfterPickList(objVORGLANG)
	
	set ANZSEIT = $$$INVORGNumberOfLineItemsOnPickLi(objDefaults)              ; Number within side
	if ANZSEIT="" SET ANZSEIT = 30
	
	set idForm = "INIssueLine"
	set UES = $$^WWWTEXT(207)                                                  ; "Number"
	set UES = UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineItem)           ; 1     "Item"
	set UES = UES_Y_$$^WWWTEXT(32026)                                          ; "Item Description"
	set UES = UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineQtyIssued)      ; 3     "Quantity"
	set UES = UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineUnit)           ; 4     "Unit"
	set UES = UES_Y_$$^WWWTEXT("AL00276")                                      ;       "Unit Cost"
	set UES = UES_Y_$$^WWWTEXT("AL00236")                                      ;       "ExtendedCost"
	
	set ANZM = 0
	
	do Title(pidIssue) ; Table with 2 sub-tables
	write YCR,"<TABLE BORDER=1 WIDTH=100% CELLSPACING=0>"
	do Header(UES)		
	set YDDSATZ = 0
	set fltTot  = 0
	
	set intLn = 0
	for {
		set intLn = $order(^INIssueLine(YM,pidIssue,intLn))
		quit:intLn=""
		
		set LFN   = ""
		set YFELD = $get(^INIssueLine(YM,pidIssue,intLn,1))
		
		if (ANZM'=0) && (ANZM#ANZSEIT=0) {   ; Close page and open new one
	;		write "</TD></TR>"               ; SR17285.1
			write "</TABLE>",YCR
			
			do FF^WWWW()        	// FORMFEED
			
			do Title(pidIssue)
			write YCR,"<TABLE BORDER=0 WIDTH=100% CELLSPACING=0>"
			do Header(UES)
		}
		set fltTot = fltTot + $$ListLine(pidIssue,intLn)  ; one row of 7 columns
	}
	
	do LEER^WWWTAB(10,7)  ; blank row fill - to 10 rows of 7 columns
	
	do TotalLine(fltTot)  ; one row of 7 columns
	write "</TABLE>",YCR
	quit
	
	
ListLine(pidIssue,pintLn)
	;-------------------------------------------------------------------------------
	; Print the Pick List Line for the Issue
	;
	; Params: pintLn - line number
	;
	; Returns: None
	;
	;
	; History:
	; 16-Feb-2010	PPP		SR17662: Removed the 15 character limit for Desc
	; 28-Apr-2010	GRF		SR17185: Fix HTML tag balancing; use strBGColour
	; 25-Feb-2010	PPP		SR17185.2: Removed the Unit ID, Get the Stored Cost
	; 23-Feb-2010	PPP		SR17185.1: Get the Unit Cost for the Item
	; 22-Feb-2010	PPP		SR17185: Created
	;-------------------------------------------------------------------------------
	new fltCost,fltIss,fltTot,idFromLocn,idItem,idUnit,objIssue,objItem
	new strBGColour,strBundleValues,strItemDesc
	
	set objIssue   = $get(^INIssue(YM,pidIssue,1))
	set idFromLocn = $$$INIssueFromLocn(objIssue)

	set idItem = $$$INIssueLineItem(YFELD)
	
	set ANZM = ANZM + 1
	set YDDSATZ = $get(YDDSATZ) + 1
	set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)
	write YCR,"<TR>"
	
	; 1. Line Number
	;---------------------------------------
	write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>"
	write pintLn,"&nbsp;"
	write "</FONT></TD>"
	
	; 2. Item
	;---------------------------------------
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=LEFT"_strBGColour_"><FONT SIZE=1>"
	write idItem,"&nbsp;"
	write "</FONT></TD>"
	
	; 3. Item Description
	;---------------------------------------
	set objItem     = $get(^INART(YM,idItem,1))
	set strItemDesc = $$$INARTSearchName(objItem)
	//SR17662
	//if $length(strItemDesc) > 15 {
	//	set strItemDesc = $extract(strItemDesc,1,15)_"..."
	//}
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=LEFT"_strBGColour_"><FONT SIZE=1>"
	write strItemDesc,"&nbsp;"
	write "</FONT></TD>"
	
	; 4. Quantity
	;---------------------------------------
	;do ZWR^WWWTAB
	set fltIss = $$$INIssueLineQtyRequired(YFELD)		//Unit Quantity SR17185.1
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
	write fltIss,"&nbsp;"
	write "</FONT></TD>"
	
	; 5. Unit
	;---------------------------------------
	set idUnit = $$$INIssueLineUnit(YFELD)
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=LEFT"_strBGColour_"><FONT SIZE=1>"
	write $$$AppEnum("EINHEIT",idUnit),"&nbsp;"
	write "</FONT></TD>"
	
	; 6. Unit Cost
	;---------------------------------------
	if idFromLocn="" set idFromLocn = $get(YLOCATION)
	set fltCost = $$$INIssueLineUnitCost(YFELD)		//SR17185:2
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
	write $$^WWWZAHL(fltCost,0,$$DPforInventoryCost^INVORG(),YWHR),"&nbsp;"	
	write "</FONT></TD>"
	
	; 7. Extended Cost
	;---------------------------------------
	set fltTot = $justify(fltCost*fltIss,0,2)
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
	write $$^WWWZAHL(fltTot,0,2,YWHR),"&nbsp;"
	write "</FONT></TD>"
	
	write YCR,"</TR>",YCR
	
	quit fltTot
	
	/*
	new fltCost,fltIss,fltTot,idFromLocn,idItem,idUnit,objIssue,objItem
	new strBundleValues,strItemDesc
	
	set objIssue   = $get(^INIssue(YM,pidIssue,1))
	set idFromLocn = $$$INIssueFromLocn(objIssue)

	set idItem = $$$INIssueLineItem(YFELD)
	
	set ANZM = ANZM + 1
	write YCR
	do NL^WWWTAB
	
	; 1. Line Number
	;---------------------------------------
	do NF^WWWTAB
	write YCR,pintLn,"&nbsp;"
	
	; 2. Item
	;---------------------------------------
	do ZWL^WWWTAB
	write idItem,"&nbsp;"
	
	; 3. Item Description
	;---------------------------------------
	do ZWL^WWWTAB
	set objItem = $get(^INART(YM,idItem,1))
	set strItemDesc = $$$INARTSearchName(objItem)
	if $length(strItemDesc) > 15 {
		set strItemDesc = $extract(strItemDesc,1,15)_"..."
	}
	write strItemDesc,"&nbsp;"
	
	; 4. Quantity
	;---------------------------------------
	do ZWR^WWWTAB
	//set fltIss = $$$INIssueLineQtyIssued(YFELD)
	set fltIss = $$$INIssueLineQtyRequired(YFELD)		//Unit Quantity SR17185:1
	write fltIss,"&nbsp;"
	
	; 5. Unit
	;---------------------------------------
	do ZWL^WWWTAB
	set idUnit = $$$INIssueLineUnit(YFELD)
	//write idUnit_" ("_$$$AppEnum("EINHEIT",idUnit)_")","&nbsp;"	//SR17185:2
	write $$$AppEnum("EINHEIT",idUnit),"&nbsp;"
	
	; 6. Unit Cost
	;---------------------------------------
	if idFromLocn="" set idFromLocn = $get(YLOCATION)
	//set fltCost = $$GetCost^INItemCost(idItem,idFromLocn)*$$GetUOMConversion^INUOMConversion(idItem,idUnit)	//Unit Cost, SR17185/1
	//set fltCost = $$GetCost^INARTCOST(idItem)*$$GetUOMConversion^INUOMConversion(idItem,idUnit)	//Unit Cost, SR17185/1
	set fltCost = $$$INIssueLineUnitCost(YFELD)		//SR17185:2

	do ZWR^WWWTAB
	write $$^WWWZAHL(fltCost,0,$$DPforInventoryCost^INVORG(),YWHR),"&nbsp;"	
	write YCR
	write "</TR>",YCR
	
	; 7. Extended Cost
	;---------------------------------------
	//set fltTot = $justify(fltCost*fltIss*$$GetUOMConversion^INUOMConversion(idItem,idUnit),0,2)
	set fltTot = $justify(fltCost*fltIss,0,2) //Unit Cost, SR17185/1
	do ZWR^WWWTAB
	write $$^WWWZAHL(fltTot,0,2,YWHR),"&nbsp;"
	write YCR
	write "</TR>",YCR
	
	quit fltTot
	*/
	
	
TotalLine(pfltTot)
	;-------------------------------------------------------------------------------
	; Print the Pick List Total Line for the Issue
	;
	; Params: pintLn - line number
	;
	; Returns: None
	;
	;
	; History:
	; 28-Apr-2010	GRF		SR17185: Fix HTML tag balancing; remove unnecessary font
	; 							and bold settings outside TD; use strBGColour
	; 22-Feb-2010	PPP		SR17185: Created
	;-------------------------------------------------------------------------------
	new intCnt,strBGColour
	
	set ANZM = ANZM+1
	
	set YDDSATZ = $get(YDDSATZ) + 1
	set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)
	write YCR,"<TR>"
	
	for intCnt=1:1:7 {
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=2><B>"
			
		if intCnt=6 {
			write "TOTAL"_"&nbsp;"
		} elseif intCnt=7 {
			write $$^WWWZAHL(pfltTot,0,2,YWHR),"&nbsp;"
		}
		write "</B></FONT></TD>"
	}
	write YCR,"</TR>"
	quit
	
	/*
	new intCnt
	set ANZM = ANZM+1
	write YCR,"<FONT SIZE=3><B>"	
	do NL^WWWTAB
	for intCnt=1:1:7 {
		do ZWR^WWWTAB
		write "<FONT SIZE=2><B>"	
		if intCnt=6 {
			write "TOTAL"_"&nbsp;"
		}
		if intCnt=7 {
			write $$^WWWZAHL(pfltTot,0,2,YWHR),"&nbsp;"
		}
		write "</B>"
		write YCR
		do EL^WWWTAB
	}
	quit
	*/
	
	
Title(pidIssue)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Apr-2010	GRF		SR17185: Fix HTML tag balancing
	; 25-Feb-2010	PPP		SR17185:2 Added the Status Desc
	; 22-Feb-2010	PPP		SR17185: Created
	;-------------------------------------------------------------------------------
	new dtePosted,dteTransfer,idFromLocn,idRef,idToLocn,intStatus
	new objIssue,objRecord,strDescription
	
	set objIssue   = $get(^INIssue(YM,pidIssue,1))
	set idFromLocn = $$$INIssueFromLocn(objIssue)
	
	if $$$INIssueType(objIssue) = 3 { 
 		set idToLocn   = $$$INIssueCustomerID(objIssue)
		if idToLocn'="" {
			set idToLocn = idToLocn_" ("_$$$INKUNDEName1($get(^INKUNDE(YM,idToLocn,1)))_")"	
		}
 
	} else {
 		set idToLocn   = $$$INIssueToLocn(objIssue)
		if idToLocn'="" {
			set idToLocn = idToLocn_" ("_$$$WWW0121LocationName($get(^WWW0121(0,YM,idToLocn,1)))_")"	
		}
	}
 
	set idRef      	= $$$INIssueCustomerName(objIssue)
	set intStatus  	= $$$INIssueStatus(objIssue)
	set dtePosted 	= $$$INIssuePosted(objIssue)
	
	; TABLE 1
	;---------------------------------------
	WRITE YCR,"<TABLE BORDER=0 WIDTH=100% CELLSPACING=0>"

	
	; Table 1 Row 1
	;---------------------------------------
	write YCR,"<TR><TD><NOBR>"
	
	;              TABLE 2A - 1 row of 1 column
	;---------------------------------------
	write YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	write YCR,"<TR>"
	
	write YCR,"<TD ALIGN=CENTER COLSPAN=2>"  ; FIXME : Is COLSPAN necessary?
	write "<FONT SIZE=3><B>"
	write $$^WWWTEXT("IN01206")     ; "Despatch Advice Note"
	write "</B></FONT>"
	write "<BR>",YCR
	write "<FONT SIZE=2>"      ; "Issued By  "  "Location"
	write $$^WWWTEXT(33404)_" "_$$^WWWTEXT(388)_": "_idFromLocn_" ("_$$$WWW0121LocationName($get(^WWW0121(0,YM,idFromLocn,1)))_")"
	write "</FONT>"
	write "</TD>"
	
	write YCR,"</TR>"
	write YCR,"</TABLE>",YCR
	;              TABLE 2A END
	;---------------------------------------
	
	;              TABLE 2B - 2 rows of 3 columns
	;---------------------------------------
	write YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	write YCR,"<TR>"
	
	write YCR,"<TD WIDTH=33%>"
	write "<FONT SIZE=2><B>"
	write $$^WWWTEXT(32807)_": "  ; "For" : To location
	write idToLocn
	write "<BR>"
	write $$^WWWTEXT(32247)_": "  ; "Reference" 
	write idRef
	write "</B></FONT>"
	write "</TD>"
	
	write YCR,"<TD ALIGN=CENTER VALIGN=BOTTOM WIDTH=33%>"
	write "<FONT SIZE=2><B>"
	write " "
	write "<BR>"
	write $$^WWWTEXT(32781)_": "_$$GetDescription^WWWStatus("INIssue",intStatus)  ; "Status"  //SR17185:2 Status Desc
	write "</B></FONT>"
	write "</TD>",YCR
	
	write YCR,"<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>"
	write "<FONT SIZE=2><B>"
	if pidIssue '= "" write $$^WWWFELDNAME("INIssueLoc","P",1)_": "_pidIssue
	write "</B></FONT>"
	write "</TD>"
	
	write YCR,"</TR>"
	
	;-----
	
 	write YCR,"<TR>"
 	
	write YCR,"<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%></TD>"
	write "  <TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%></TD>"
	write "  <TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>"
	write "<FONT SIZE=2><B>"
	write $$^WWWFELDNAME("INIssueLoc","D",$$$FldINIssuePosted)_":"_$select(dtePosted'="":$zdatetime(dtePosted,3),1:"")
	write "</B></FONT></TD>"
	
 	write YCR,"</TR>"
	write YCR,"</TABLE>"
	;              TABLE 2B END
	;---------------------------------------
 
	WRITE YCR,"</TABLE>",YCR
	; TABLE 1 END
	
	quit
	
	
Header(UES)
	;-------------------------------------------------------------------------------
	; History:
	; 28-Apr-2010	GRF		SR17185: Fix HTML tag balancing
	; 20-Feb-2009	GRF		SR16303: Move Title & <TABLE> into List to match </TABLE>
	; 03-Feb-2009	DavidR  SR16303: copied and modified from INTRNPick
	; 15-Sep-2008	GRF		replace "I" with "loop"
	;-------------------------------------------------------------------------------
	
	new loop
	
	write YCR,"<THEAD><TR>"
	
	for loop=1:1 {
		quit:$piece(UES,Y,loop,99)=""
		
		write YCR,"<TH VALIGN=TOP ALIGN=CENTER BGCOLOR="_YDARKGRAY_">"
		write "<FONT SIZE=1>"
		write $piece(UES,Y,loop),"&nbsp;"
		write "</FONT></TH>"
	}
	write YCR,"</TR></THEAD>"
	quit
	
]]></Routine>
</Export>