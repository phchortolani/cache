<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMValidation" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMValidation
#include COMSYS
#include WWWConst
 
#define FormData(%form,%type)		^WWWDATEN(0,+$horolog,YUSER,%form,%type,1)
#define FormDataOld(%form,%type)	^WWWDATEN(0,+$horolog,YUSER,%form,%type,2)
#define FormDataOldGet(%form,%type)	$get(^WWWDATEN(0,+$horolog,YUSER,%form,%type,2))
 
BeforeSave(pintPage,pnull)
	;-------------------------------------------------------------------------------
	; Validate during hyper event
	;
	; Params:	pintPage - current page (YSEITE)
	; 			pnull - not used
	;
	; ByRefs:
	;
	; Returns:	for eventvalue - alert error or submit form
	;
	; History:
	; 30-May-2011	GRF		Replace $$^WWWYM with 0
	; 27-Jan-2011	GRF		SR17579: replace $$$Order macros; stsField => strStatus;
	;							use dteToday
    ; 22-Aug-2008	FIS		SR15828: Correction to compare with new WWWDATEN(4)
    ; 							record (orig. data after WWWFORM)
    ; 12-Aug-2008	FIS		SR15828: Compare with new WWWDATEN(3) record (orig. data before WWWFORM)
    ; 05-Aug-2008	FIS		SR15828: Compare saved data with loaded data before allow saving
    ; 27-Sep-2007	GRF		SR15603: Macro change
	; 23-May-2007	RGB		SRBR014456: VARHOOK onbeforsave call is running here now
	; 						for fixing a bug. Added FormDataOld definition.
	; 27-Mar-2007	PO		SR15442: If valid key, don't perform field check
	; 22-Mar-2007	JW		SR15453: Get YOPTION, YOPTION1 from cache
	; 24-Jan-2006	JW		Don't check primary keys for search engine type.
	; 08-Aug-2006	JW		SR13594: Created
	;-------------------------------------------------------------------------------
	new blnContinue,blnValidKey,dteToday,idClass,idField,intCount
	new objDispData,objForm,objLoadData,objOrigData,objSrcData,strData,strExec
	new strKey,strKeys,strManual,strPerform,strReturn,strSrcClass,strStatus,strValue
	
	set blnContinue = $$$YES
	set strReturn   = ""
	set strPerform  = ""
	set dteToday    = +$horolog

	set objForm = $get(^WWW120(0,YFORM,1))
	set idClass = $$$WWW120ClassUsedInForm(objForm)
	set strKeys	= $$$GetFormData(YFORM,"P")
 
	if $$IsNewRecord^WWWFORMStatus() {
		set blnValidKey = $$$YES
		for intCount = 1:1:$length(strKeys,$$$COMMA) {
			set strKey      = $piece(strKeys,$$$COMMA,intCount)
			set blnValidKey = (blnValidKey && '$$$NoKey(strKey))
			quit:'blnValidKey
		}
		if blnValidKey {
			if $$$ISOK($$GetRecord^COMUtilClass(idClass,strKeys)) {
				set blnContinue = $$$NO
			}
		}
	
	} elseif (idClass'="") && ($translate(strKeys,"+")'="") {
		set strSrcClass = "^"_idClass_"(0,"_$$^WWWKEYBUILD(strKeys)_",1)"   ; 20-May-2011
		set objSrcData  = $get(@strSrcClass)
	;	set objLoadData = $get(^WWWDATEN(0,dteToday,YUSER,YFORM,"D",2))  ;data snapshot before form load, modified by default data changes
	;	set objOrigData = $get(^WWWDATEN(0,dteToday,YUSER,YFORM,"D",3))  ;data snapshot before form load (-> inactivated in WWWFORM)
		set objDispData = $get(^WWWDATEN(0,dteToday,YUSER,YFORM,"D",4))  ;data snapshot after form load (and after all executes that may changed the data)
		
		if (objSrcData '= "") && (objDispData '= "") && ($translate(objSrcData,Y) '= $translate(objDispData,Y)) {  //saved data '= loaded data	
			set ^WWWDATEN(0,dteToday,YUSER,YFORM,"D",2) = ""
			set ^WWWDATEN(0,dteToday,YUSER,YFORM,"D",1) = objSrcData
			set strPerform = $$^WWWREFRESH(YUSER,YFORM)
			set ^WWWDATEN(0,dteToday,YUSER,YFORM,"D",2) = objSrcData
			set ^WWWDATEN(0,dteToday,YUSER,YFORM,"D",4) = objSrcData
			set strPerform = strPerform_"#"_$$$AlertFollowing_$$$Text("WWW00090")
			set blnContinue = $$$NO
		} ; "The data has been changed by a different User. The Form will get updated. Please re-enter your changes."
	} 
	
	if blnContinue && $$$HYPERValidation(YFORM) {
		set strManual = $$$GetFormData(YFORM,"M")                    ; Manual fields
		set idField   = ""
		for {
			set idField = $order(^WWW122s(0,4," ",YFORM,idField))
			quit:idField=""
			continue:'$data(^WWW122s(0,1,pintPage,YFORM,idField))	; Only this page for manual fields
			
			set strStatus = $$Validate^WWWSAVM(YFORM,idField,$piece(strManual,Y,idField))
			if $$$ISERR(strStatus) {
				set strReturn = strReturn_$$$FieldName(YFORM,"M",idField)_": "_$$$Text(strStatus)_"|"
			}
		}
		
		// Only keys, data, Before Save for standard forms (from WWWSAVE)
		set objForm = $get(^WWW120(0,YFORM,1))
		set idClass = $$$WWW120ClassUsedInForm(objForm)
		
		if (idClass'="") && '$$$Contains((2,4,7),$$$WWW120FormType(objForm)) {
			set strKeys	= $$$GetFormData(YFORM,"P")                  ; Primary Keys
			set idField = ""
			for {
				set idField = $order(^WWW002(0,idClass,idField))
				quit:idField=""
				
				set strStatus = $$Validate^WWWSAVP(idClass,YFORM,idField,$piece(strKeys,",",idField))
				if $$$ISERR(strStatus) {
					set strReturn = strReturn_$$$FieldName(YFORM,"P",idField)_": "_$$$Text(strStatus)_"|"
				}
			}
			// Data Fields
		 	set YOPTION  = $get(^CacheTempForm(YUCI,YUSER,YFORM,"YOPTION"))
		 	set YOPTION1 = $get(^CacheTempForm(YUCI,YUSER,YFORM,"YOPTION1"))
			
			set strData	= $$$GetFormData(YFORM,"D")
 
			set strReturn = strReturn_$$DataFields(idClass,YFORM,.strData)
			if strReturn'="" {
				set blnContinue = $$$NO
			} else {
	;			On Before Save                                  ; *** EXECUTE ? ***
				set strExec = $$$WWW120ExecuteOnBeforeSaveData($get(^WWW120(0,YFORM,1)))
				if strExec'="" {
					if $extract(strExec)="#" {
						set $extract(strExec) = ""
					}
					set blnContinue = $$Execute(strExec,strKeys,.strData,strManual)   ; FIXME : see next call to $$Execute
				}
				if blnContinue {										 ;SR17245
					set strStatus = $$OnBeforeSave^COMGridEdit31Display(strKeys,strData)  ;SR17245
					if $$$ISERR(strStatus) {							 ;SR17245
						set blnContinue = $$$NO							 ;SR17245
						set strReturn   = $$$Text(strStatus)			 ;SR17245
					}													 ;SR17245
				}														 ;SR17245
	;			                                                ; *** EXECUTE ? ***
				if blnContinue {
					if $$EXIST^%R("Y"_YFORM_"onBeforeSave.OBJ",$get(YUCI)) {	// CUSTOMIZING EXECUTE VOR SPEICHERN Q:FORMULAR FERTIG
						set strExec = "DO ^Y"_YFORM_"onBeforeSave"
						set blnContinue = $$Execute(strExec,strKeys,.strData,strManual)   ; FIXME : Will repeat the WWWFORM5 processing - variable setup?
					}
				}
	;			                                                ; *** EXECUTE ? ***
				if blnContinue { // RGB - Added the varhook on before call
				//	set strDataOld = $$$FormDataOld(YFORM,"D")  //undefined if not exists
					set strDataOld = $$$FormDataOldGet(YFORM,"D")
					set strStatus  = $$ExecuteHook^WWW001Hook(idClass,$$$EnumWWWEVENTTYPEOnBeforeSave,strKeys,.strData,YFORM,strDataOld)
					if $$$ISERR(strStatus) {
						set blnContinue = $$$NO
						set strReturn   = $$$Text(strStatus)	
					}
				}
				if blnContinue {
					set $$$FormData(YFORM,"D") = strData		// Reset WWWDATEN. Ok ? :)
				}
			}
		}
	}
	
	if strReturn '= "" {
		set strReturn = $$$AlertFollowing_strReturn
	
	} elseif strPerform '= "" {
		set strReturn = strPerform
	
	} elseif blnContinue {
		set strReturn = $$$Perform_"FUNCTION~DefaultSubmit(0)"
	}
	quit strReturn
	
	
DataFields(pidClass,pidForm,&pstrData)
	;-------------------------------------------------------------------------------
	; Validate data fields before save
	;
	; Params:	pidClass	- class id
	; 			pidForm		- current form (YFORM)
	;
	; ByRefs:	pstrData	- data record (YFELD)
	;
	; Returns:	error message or null if fine
	;
	; History:
	; 11-May-2009	GRF		stsField => strStatus
	; 29-Mar-2008   heber   BR014919: Pattern match on grid fields
	; 29-Sep-2006 	HeberB	BR014265: Add YFELD as param to Validate
	; 09-Aug-2006	JW		SR13594: Created
	;-------------------------------------------------------------------------------
	new idField,idFormField,idxFormField,objField	
	new strStatus,strPattern,strReturn,strValue
	
	set strReturn = ""
	set idField   = ""
	for {
		set idField = $order(^WWW003(0,pidClass,idField))
		quit:idField=""
		
		set strValue  = $piece(pstrData,Y,idField)
		set strStatus = $$Validate^WWWSAVD(pidClass,pidForm,idField,.strValue,,pstrData)

		// BR014919
		if $$$ISOK(strStatus) {
			set idxFormField = $$$Index(idField)
			set idFormField  = $order(^WWW122s(0,4,idxFormField,pidForm,""))
			if (idFormField '= "") {
				set objField = $$Get^WWW122(pidForm,idFormField)
				set strPattern = $$$WWW122PatternMatch(objField)
				if (strPattern '= "") && (strValue '= "") {
					if '(strValue?@strPattern) {
						set strStatus = $$$MakeStatus("WWW00089",strPattern)
					} ; "Wrong Input Format, correct pattern: %1"
				}
			}
		}
		//^^^^^^^^^^^^^^
		
		set $piece(pstrData,Y,idField) = strValue
		if $$$ISERR(strStatus) {
			set strReturn = strReturn_$$$FieldName(pidForm,"D",idField)_": "_$$$Text(strStatus)_"|"
		}
	}
	quit strReturn
	
	
Execute(pstrExec,YKEY,&YFELD,YMFELD)
	;-------------------------------------------------------------------------------
	; Wrapper for On Before Save execution
	;
	; Params:	pstrExec - string to execute
	;			YKEY - current key
	;			YMFELD - manual field record
	;
	; ByRefs:	YFELD - data field record
	;
	; Returns:	boolean - whether save is ok
	;
	; History:
	; 09-Aug-2006	JW		SR13594: Created
	;-------------------------------------------------------------------------------
	new blnOk
	
	do ^WWWFORM5
	xecute pstrExec
	set blnOk = ($get(Q)'=1)		;EXECUTE VOR SPEICHERN Q:FORMULAR FERTIG ;EXECUTE pre- Save ready-made 
	
	quit blnOk
	
	
]]></Routine>
</Export>