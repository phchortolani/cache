<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMH" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMH
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN DATENFELDER 
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 11-Dec-2008	GRF		Disabled block duplicated existing lines - removed
	; 10-Jul-2007	GRF		Doco; quits; disabled block
	; 05.08.1997	DT
	;-------------------------------------------------------------------------------
	NEW ERSTES,YFFORMAT,YSPALTE,YZEILE,YZEIPO
	
	;	D44		$$$WWW122FormFormatting		0=Single Field Formatting
	SET YFFORMAT = $PIECE(YVOR,Y,44)   ; Preserve while processing
	SET $PIECE(YVOR,Y,44) = 0
	SET YZEILE = 0,YSPALTE = 0,YZEIPO = 0
	SET YHIDDSE = 0 
	
	NEW YI,YPRIM,YA,YLFN,YLFDNR,YSEI,YZEILE,YSPL,YPOS    ; FIXME : re-newing YZEILE
	
	;+++++++++++++++++++++++++++++++++++++++
	;  YPOS		Header Position (WWW122 D80)
	;  YBBN		Form Field ID (F#)
	;  YSATZ	objWWW122		Form Data - Fields
	;  YVOR		objWWW120		Form Data
	;+++++++++++++++++++++++++++++++++++++++
	
	QUIT:YFORM=""
	SET YPOS=""
	FOR  SET YPOS=$ORDER(^WWW122s(0,6,$$^WWWUMLAU(YFORM,1),YPOS)) QUIT:YPOS=""  QUIT:YPOS=" "  DO
	. SET YBBN=""
	. FOR  SET YBBN=$ORDER(^WWW122s(0,6,$$^WWWUMLAU(YFORM,1),YPOS,YFORM,YBBN)) QUIT:YBBN=""  DO
	. . SET YSATZ=$GET(^WWW122(0,YFORM,YBBN,1))  ;FELDDEFINITION
	. . SET YLFN=$PIECE(YSATZ,Y,1)               ;DATENBANKFELD
	. . QUIT:YLFN=""                             ;KEIN DATENFELD (MANUELLES) ;no data item 
	. . SET YART="D"                             ;P=PRIMÄR,D=DATENFELD,M=MANUELLE,L=LISTGENERATOR
	. . SET YERSTES=2
	. . DO FORMVOR
	
	/*   Proposed change
	
	set idxForm=$$Index^COMUtils(YFORM)
	set YPOS=""
	for {
		set YPOS=$order(^WWW122s(0,6,idxForm,YPOS))
		quit:YPOS=""
		
		set YBBN=""
		for {
			set YBBN=$order(^WWW122s(0,6,idxForm,YPOS,YFORM,YBBN))
			quit:YBBN=""
			
			SET YSATZ=$GET(^WWW122(0,YFORM,YBBN,1))  ;FELDDEFINITION
			SET YLFN=$PIECE(YSATZ,Y,1)  ;DATENBANKFELD
			QUIT:YLFN=""                ;KEIN DATENFELD (MANUELLES) ;no data item 
			SET YART="D"                ;P=PRIMÄR,D=DATENFELD,M=MANUELLE,L=LISTGENERATOR
			SET YERSTES=2
			DO FORMVOR
		}
	}
	*/
	
	WRITE YCR,"</TR>",YCR      ;ABSCHLUSS FELD  ;TYBD;10.3.2005;27437
	WRITE YCR,"</TABLE>",YCR   ;ABSCHLUSS FORMAT;TYBD;10.3.2005;27437
	SET $PIECE(YVOR,Y,44) = YFFORMAT   ; restore
	QUIT
	
	
FORMVOR
	;-------------------------------------------------------------------------------
	;	VORGABEN FÜR FORMULAR ;to form 
	;
	;	YKEY		= PRIMÄRSCHLÜSSEL  XX,XX,XX  ;WENN DATEI
	;	YFORM		= FORMULAR
	;	YLFN		= LFD DATENBANKNUMMER
	;	YBBN		= LFN BEARBEITUNGSNUMMER
	;	YART		= Type : O  = OPTIONEN,            P  = PRIMÄR,
	;				         D  = DATENFELDER,         M  = MANUELLE,
	;				         LP = LISTGENERATORPRIMÄR, LD = LISTFGEN.DATENFELDER,
	;				         S  = SORTIERUNG
	;	YFOART		= Form Type : 1=NORMAL 2=LISTE 3=GRID 4=MANUELLE
	;	YSATZ		= objWWW122		DEFINITION VORGABE WWW121,WWW122,WWW132,WWW133
	;	YFELD		= DATENSATZ  ;WENN DATEI ;when data file 
	;	YINHALT		= FELDINHALT  ;WENN MANUELL ;when 
	;	YGRIKEY		= STAMMKEY FUER GRIDSTAMM
	;	YLISTLF		= LISTEN EINZELFELD
	;	YVOR		= objWWW120     VORGABEN DES FORMULARS
	;	YOLDVAL		= VORGABEWERT
	;-------------------------------------------------------------------------------
	SET YPARA1=YPARA
	
	NEW YO,YI,YNAME,YNAME0,YNAME1,YTYPE,YPARA,YTYP,Q,YSORT,YXTYP,YFOART1,YJAVA,YPFLICHT
	
	QUIT:YFORM=""
	QUIT:YART=""   ;P=PRIMÄR,D=DATENFELDER,M=MANUELLE,L=LISTGENERATOR
	;Q:YLFN=""     ;LFD NUMMER
	;Q:YSATZ=""    ;DEFINITION VORGABE WWW121,WWW122,WWW1210
	
	; D36	Horizontal Orientation Of Label
	SET YFAUS="LEFT"
	IF $PIECE(YVOR,Y,36)=3  SET YFAUS="RIGHT"
	IF $PIECE(YSATZ,Y,36)=3 SET YFAUS="RIGHT"
	IF $PIECE(YVOR,Y,36)=2  SET YFAUS="CENTER"
	IF $PIECE(YSATZ,Y,36)=2 SET YFAUS="CENTER"
	IF YFOART=3             SET YFAUS="LEFT"   ;WENN GRID, DANN LINKS
	
	; D37	Vertical Orientation Of Label
	SET YVAUS="TOP"
	IF $PIECE(YVOR,Y,37)=3  SET YVAUS="BOTTOM"
	IF $PIECE(YSATZ,Y,37)=3 SET YVAUS="BOTTOM"
	IF $PIECE(YVOR,Y,37)=2  SET YVAUS="MIDDLE"
	IF $PIECE(YSATZ,Y,37)=2 SET YVAUS="MIDDLE"
	IF $PIECE(YVOR,Y,37)=4  SET YVAUS="BASELINE"
	IF $PIECE(YSATZ,Y,37)=4 SET YVAUS="BASELINE"
	IF YFOART=3             SET YVAUS="BOTTOM"  ;GRID=UNTEN
	
	SET YXTYP = 0
	SET YHID  = 0
	IF $GET(YBBN)="" SET YBBN = YLFN
	SET YNUMMER   = YLFN                ; FIXME : immediately overwritten <GRF>
	SET YNUMMER   = YBBN
	SET YFOART1   = YFOART
	SET YTYP      = ""
	SET YINHALT   = 1
	SET YNAME0    = ""
	SET YNAME1    = ""
	SET YPARA(7)  = 0
	SET YPARA(20) = 0
	SET YJAVA     = 0
	SET YPFLICHT  = 0
	SET YSTATUS   = $PIECE(YSATZ,Y,65)         ; Text Displayed In Status Line
	IF (YFOART=1) || (YFOART=3) SET YJAVA = 1  ;BEARBEITUNGSSTATUS
	FOR YI=53:1:56,60 SET YO(YI) = $PIECE(YVOR,Y,YI)
	
	;DATENFELDER
	SET YART="H"
	DO
	. SET YDATEI = $PIECE($GET(^WWW120(0,YFORM,1)),Y,11)  QUIT:YDATEI=""
	. SET YDVOR  = $GET(^WWW003(0,YDATEI,YLFN,1))         QUIT:YDVOR=""    ;DATEN AUS FELDDEV ;out of 
	. ;
	. SET YTYP   = $PIECE(YDVOR,Y,3)          ;ERFASSUNGSART
	. IF $PIECE(YSATZ,Y,5)'="" SET YTYP = $PIECE(YSATZ,Y,5)
	. SET YXTYP  = +$PIECE(YSATZ,Y,2)         ;ERFASSUNGSART AUS FORMULARFELD ;out of 
	. SET YLANGE = $PIECE(YDVOR,Y,4)          ;FELDLENGE
	. IF +$PIECE(YSATZ,Y,6)'=0 SET YLANGE = $PIECE(YSATZ,Y,6)
	. SET YNAME0 = ""                         ;TEXT VOR ;Text pre- 
	. SET YNAME1 = ""                         ;TEXT NACH ;Text within 
	. SET YNAME  = $PIECE(YDVOR,Y,2)          ;NORMALTEXT
	. IF $DATA(^WWW0031(0,YDATEI,YLFN,$$^WWWLANGU(YBED),1)) SET YNAME=$PIECE(^(1),Y,1)
	. ;IF $PIECE(YSATZ,Y,12)="" IF $DATA(^WWW122(0,YFORM,YBBN,1)) SET $PIECE(^WWW122(0,YFORM,YBBN,1),Y,12)=YNAME  ;WENN LEER
	. SET YPARA(12) = $PIECE(YDVOR,Y,7)   ;RELATION SORTIEREN ;assortment 
	. SET YPARA(1)  = $PIECE(YDVOR,Y,8)   ;RELATIONSDATEI  AUS DATEIDEV ;out of 
	. SET YPARA(2)  = $PIECE(YDVOR,Y,9)   ;REL KEY
	. SET YPARA(3)  = $PIECE(YDVOR,Y,10)  ;REL FELD ;field 
	. IF $PIECE(YSATZ,Y,63)'="" SET YPARA(3) = $PIECE(YSATZ,Y,63)
	. SET YPARA(20) = 1                   ;ERST SPÄTER ANZEIGEN ;only subsequent display 
	. IF $PIECE(YSATZ,Y,32)'="" DO
	. . SET YPARA(12)= $PIECE(YSATZ,Y,62) ;REL SORTIEREN ;assortment 
	. . SET YPARA(1) = $PIECE(YSATZ,Y,32) ;RELDATEI AUS FORMULAR DEV ;out of form 
	. . SET YPARA(2) = $PIECE(YSATZ,Y,33) ;KEY F. RELATION
	. . SET YPARA(3) = $PIECE(YSATZ,Y,34) ;FELDER 
	. . IF $PIECE(YSATZ,Y,63)'="" SET YPARA(3)=$PIECE(YSATZ,Y,63)
	. . SET YPARA(20)=1                   ;ERST SPÄTER ANZEIGEN ;only subsequent display 
	. ;
	. SET YPARA(67)=""                    ;NEUES FELD AUS EVENT BROKER ;something new field out of 
	. SET YPARA(79)=""                    ;RELATIONSFELD FÜR MARKIERUNG ;to 
	. SET YPARA(80)=""                    ;WELCHE FELDER MARKIEREN ;who 
	. SET YSPAL  = $PIECE(YPOS,".",2)     ;SPALTEN VORGABE ;split default 
	. SET YZEILE = $PIECE(YPOS,".",1)
	. IF $GET(YLASTZEIL)'="" IF YLASTZEIL'=YZEILE WRITE "</TABLE>"
	. SET YLASTZEIL = YZEILE 
	. SET YINHALT   = $PIECE(YFELD,Y,YLFN)  ;FELDINHALT
	. SET YHID = 2                          ; Field Disabled      ;FELDBERECHITIGUNG 
	. FOR YI=53:1:56 IF +$PIECE(YSATZ,Y,YI)'=0 SET YO(YI)=$PIECE(YSATZ,Y,YI)  ;VORGABEN FÜR OBJEKTANZEIGE ;to 
	. ;
	. DO ^WWWFORM7                        ; *** EXECUTE 15-20 ***   ; FIXME : Start^WWWFORM7($$$NO)
	
	QUIT
	
]]></Routine>
</Export>