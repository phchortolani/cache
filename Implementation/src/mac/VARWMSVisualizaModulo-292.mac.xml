<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARWMSVisualizaModulo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARWMSVisualizaModulo
#include COMSYS

PrintVisualizacaoModulo(pidLocal,pidModulo)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe a visualização do módulo, mostrando as ruas contidas neste.
 	;   
    ; Chamado por:
    ;	Página VARWMSVisualizaModulo.csp
    ; 
    ; Histórico:
    ; 	07-Jul-2011		Criado
    ;-------------------------------------------------------------------------------
    quit:( (pidLocal = "") || (pidModulo = "") )
    
    new strListaRuas, idRua, cnt
    set strListaRuas = $$getListaRuas^VARWMSUtils(pidLocal,pidModulo)
    
    set idRua = ""
    
    for cnt = 1:1:$length(strListaRuas,";") {
        set idRua = $piece(strListaRuas,";",cnt)
        continue:(idRua = "")
        
        do BuildRua(pidLocal,pidModulo,idRua)
    }

    quit

BuildRua(pidLocal,pidModulo,pidRua)
 	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Constrói a rua em um formato onde os endereços com númeração ímpar são mostrados
    ;   do lado esquerdo, e os endereços com numeração par são mostrados do lado direito.
    ;
    ; 	Exemplo:
    ;		----------
    ;		| 1 || 2 |
    ;		----------
	;   	| 3 || 4 |
	;		----------
	;		| 5 || 6 |
	;		----------
    ;		| x || y |
    ;		----------
 	;   
    ; Chamado por:
    ;	Página PrintVisualizacaoModulo^VARWMSVisualizaModulo.mac
    ; 
    ; Histórico:
    ; 	07-Jul-2011		Criado
    ;-------------------------------------------------------------------------------
   
    do BuildLadoRua(pidLocal,pidModulo,pidRua,"IMPAR")
    do BuildMeioRua(pidLocal,pidModulo,pidRua)
    do BuildLadoRua(pidLocal,pidModulo,pidRua,"PAR")
    
    quit
    
BuildLadoRua(pidLocal,pidModulo,pidRua,pLado)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe os lados da rua, listando os prédios em ímpares (lado esquerdo) e pares
    ;	(lado direito).
 	;   
    ; Chamado por:
    ;	PrintVisualizacaoModulo^VARWMSVisualizaModulo
    ; 
    ; Histórico:
    ; 	07-Jul-2011		Criado
    ;-------------------------------------------------------------------------------
    quit:( (pidLocal = "") || (pidModulo = "") || (pidRua = "") )
    
    new strListaPredios, cnt, idPredio  
   
   	set strListaPredios = $$getListaPredios^VARWMSUtils(pidLocal,pidModulo,pidRua,pLado)
    quit:(strListaPredios = "")

    write "<div style='display:inline-table;'>"
    write " <table style='border-color:#8a91d3;border-left-width:2px;border-right-width:2px;border-top-width:4px;border-bottom-width:4px;border-style:solid;background-color:#E0E0E0;'>"
    
    for cnt = 1:1:$length(strListaPredios,";") {
        set idPredio = $piece(strListaPredios,";",cnt)
        continue:(idPredio = "")
        
        write " <tr>"       
        write "     <td width='20' height='25' align='center' title='Rua "_pidRua_" - Prédio "_idPredio_"' style='background-color:#FFFFFF;font-size:10px;border:1px solid #999999;'> "_idPredio_" </td>"
        write " </tr>"
    }
    
    write " </table>"
    write "</div>"
    
    quit
    
BuildMeioRua(pidLocal,pidModulo,pidRua)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe a parte do meio da rua, com o link para abrir a visão por rua.
 	;   
    ; Chamado por:
    ;	PrintVisualizacaoModulo^VARWMSVisualizaModulo
    ; 
    ; Histórico:
    ; 	07-Jul-2011		Criado
    ;-------------------------------------------------------------------------------
    quit:( (pidLocal = "") || (pidModulo = "") || (pidRua = "") )

    new keyNavegacao, linkNavegacao
    
    set keyNavegacao = pidLocal_","_pidModulo_","_pidRua    
	set linkNavegacao = $$BuildFormLink^VARWMSUtils("VARWMSVisualizaRua",keyNavegacao)
	
	&html<
    	<div class='meioRua'>
    		<table>
    			<tr><td align='center' width='55'><a href=#(linkNavegacao)#><strong>Rua #(pidRua)#</strong></a></td>
    			</tr>    		
    			<tr><td>&nbsp;</td>
    			</tr>
    			<tr><td align='center'><img src='#(YGIF)#rua_flecha2.png' width='28' height='22' alt=''></td>
    			</tr>
    		</table>
    	</div>
    >
    
    quit
]]></Routine>
</Export>