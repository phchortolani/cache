<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAtaCompra" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[VARAtaCompra
#include COMSYS
#include COMConst
#include INConst
#include VARConst

OnAfterSave
	;-------------------------------------------------------------------------------
    ; Prepara as variáveis para a consulta do usuário.
	;
    ; History:
    ; 16-May-2012	Gustavo Fiuza	Incluído filtro de local
    ; 21-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new idFornecedor, idProduto, idAta, idLocal
	
	//Filter
	set idFornecedor = $get(VORG(1))
	set idProduto    = $get(VORG(2))
	set idAta 		 = $get(VORG(3))
	set idLocal		 = $get(VORG(4))

	set $piece(^VARTempAtaCompra(YM,YBED,1),Y,1) = idFornecedor
	set $piece(^VARTempAtaCompra(YM,YBED,1),Y,2) = idProduto
	set $piece(^VARTempAtaCompra(YM,YBED,1),Y,3) = idAta
	set $piece(^VARTempAtaCompra(YM,YBED,1),Y,4) = idLocal
	
	if ( (idFornecedor = "") && (idProduto = "") && (idAta = "") ) {
		kill ^VARTempAtaCompra(YM,YBED)
	}
	
	kill ^VARTempSelAtaCompra(YM,YBED)
	
	do VARLogAfterSave^VARUtil(YBED,YFORM)	
	do ReloadForm^COMUtilForm("VARAtaCompra")
	
	quit
	
	
OnAfterDataFields
	;-------------------------------------------------------------------------------
    ; Monta a tabela de consulta conforme filtrado pelo usuários das atas em vigência
    ; e com saldo disponível.
	;
    ; History:	
	; 18-Jul-2012	Gustavo   Incluída validação da situação do item da ata
	; 10-Jul-2012	Gustavo	  Incluída coluna quantidade aditivada no resultado da consulta
	; 16-May-2012	Gustavo	  Incluído filtro de Local
	; 03-May-2012	Gustavo   Incluído VARLogAfterSave
    ; 27-Sep-2011	Karine    Alteração nome de campo de: NumedodaAta -> NumeroProcesso
    ; 21-Sep-2011	Gustavo   Created
    ;-------------------------------------------------------------------------------	
	new flterFornecedor, flterProduto, flterAta, object, mySQL, flterLocal, fltQuantAditivo,
		fltQuantTotal, hintQuant

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	set flterFornecedor = $piece($get(^VARTempAtaCompra(YM,YBED,1)),Y,1)
	set flterProduto	= $piece($get(^VARTempAtaCompra(YM,YBED,1)),Y,2)
	set flterAta	    = $piece($get(^VARTempAtaCompra(YM,YBED,1)),Y,3)
	set flterLocal		= $piece($get(^VARTempAtaCompra(YM,YBED,1)),Y,4)

	set object = ##class(%ResultSet).%New()  ;Create Result Set Object
	
	if '$data(^VARTempAtaCompra(YM,YBED,1)) {
			
		w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:5px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
		w "	<font color=blue size=2>"
		w "		Informe um fornecedor, produto ou ata acima."
		w "	</font>"
		w "</div>"
		
	} else {
	   
		set mySQL = "SELECT Ata.CodATA,"
		set mySQL = mySQL_" Ata.NumeroProcesso,"
		set mySQL = mySQL_" Ata.Status,"
		set mySQL = mySQL_" $$GetValidadeAta^VARAta(Ata.CodAta,Linha.LinhadaAta) as DataValidade,"
		set mySQL = mySQL_" Linha.LinhadaAta,"
		set mySQL = mySQL_" Linha.Produto,"
		set mySQL = mySQL_" Linha.Fornecedor,"
		set mySQL = mySQL_" Linha.Quantidade,"
		set mySQL = mySQL_" Linha.QuantidadeAditivada,"
		set mySQL = mySQL_" Linha.Unidade,"
		set mySQL = mySQL_" Linha.ValorUnitario,"
		set mySQL = mySQL_" $$SQLGetSaldoItemQuant^VARAtaLinha(Ata.CodAta,Linha.LinhadaAta) as Saldo"
		set mySQL = mySQL_" FROM VARAta Ata"
		set mySQL = mySQL_" LEFT JOIN VARAtaLinha Linha"		
		set mySQL = mySQL_" ON Linha.CodAta = Ata.CodAta"
		set mySQL = mySQL_" WHERE UPPER(Ata.Status) = 1"
		set mySQL = mySQL_"  AND $$GetSituacaoItem^VARAtaLinha(Ata.CodAta,Linha.LinhadaAta,2) >= 5"
		
		if flterFornecedor '= "" {
			set mySQL = mySQL_" AND UPPER(Linha.Fornecedor) = "_flterFornecedor
		}
		
		if flterProduto '= "" {
			set mySQL = mySQL_" AND UPPER(Linha.Produto) = "_flterProduto
		}
		
		if flterAta '= "" {
			set mySQL = mySQL_" AND $UPPER(Ata.CodAta) = '"_flterAta_"'"
		}
		
		set mySQL = mySQL_" ORDER BY $$GetValidadeAta^VARAta(Ata.CodAta,Linha.LinhadaAta)"

		do object.Prepare(mySQL) ;Prepare Query
		do object.Execute()  	 ;Execute Query 

		//Tabela
	   	new lstHeader, CodAta, NumeroAta, Status, dteValidade, CodAtaLinha, idProduto, idFornecedor, count,
	   		fltQuantidade, idUnidade, vlueUnitario, fltSaldoItem, chave
	 	
	 	set lstHeader = ""
		set lstHeader = lstHeader_$listbuild("","N°. Processo","Fornecedor","Produto","Quantidade","Saldo","UN","Preço","Validade")
		
	  	set count = 0
	  	
	  	if $$Start^COMTable(lstHeader,"Atas em vigência e com saldo disponível") {

			while (object.Next()) {

				set CodAta 	        = object.GetData(1)
				set NumeroAta       = object.GetData(2)
				set Status          = object.GetData(3)
				set dteValidade     = object.GetData(4)
				set CodAtaLinha	    = object.GetData(5)
				set idProduto	    = object.GetData(6)
				set idFornecedor    = object.GetData(7)
				set fltQuantidade   = object.GetData(8)
				set fltQuantAditivo = object.GetData(9)
				set idUnidade	    = object.GetData(10)
				set vlueUnitario    = object.GetData(11)
				set fltSaldoItem    = object.GetData(12)
				
				if (flterLocal '= "") {
					continue:( '$data(^VARAtaLocals(YM,1,flterLocal,CodAta)) )
				}

				set count = $i(count)
				
				set chave = CodAta_$$$COMMA_CodAtaLinha
				
				set fltQuantTotal = fltQuantidade + fltQuantAditivo
				
				if (fltQuantAditivo > 0) {				
					set hintQuant = "Quant.: "_$$^WWWTR(0,12,fltQuantidade)_" + Quant. Aditivo: "_$$^WWWTR(0,12,fltQuantAditivo)_""
				} else {
					set hintQuant = "Quant.: "_$$^WWWTR(0,12,fltQuantidade)
				}
				
				do NewLine^COMTable()
				do InsertCellCheckbox^COMTable(chave,"SelecionarItem^VARAtaCompra",$$$NO,$$$NO)
		    	do InsertCell^COMTable(NumeroAta,"VARAta",CodAta)
		    	do InsertCell^COMTable("("_idFornecedor_") "_$$SQLGetDescFornecedor^VARSQL(idFornecedor),,,,,,,,,$$$YES)	    	
		    	do InsertCell^COMTable("("_idProduto_") "_$$SQLGetDescricaoProduto^VARSQL(idProduto),,,,,,,,,$$$YES)
		    	do InsertCell^COMTable("<font title='"_hintQuant_"'>"_$$^WWWTR(0,12,fltQuantTotal)_"</font>",,,,"RIGHT")
		    	do InsertCell^COMTable("<font color='blue'>"_$$^WWWTR(0,12,fltSaldoItem)_"</font>",,,,"RIGHT")		    	
		    	do InsertCell^COMTable($$SQLGetSiglaUnit^VARSQL(idUnidade))
		    	do InsertCurrencyCell^COMTable(vlueUnitario)
		    	do InsertCell^COMTable($zdate(dteValidade,4))
				do EndLine^COMTable()			
			}
		
			if (count = 0) {
				do NewLine^COMTable()
			    do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,9)
		    	do EndLine^COMTable()		
			
			} else {
				
				set strBotaoConfirma = "<IMG SRC="_YGIF_"AEPicoCloseDealOn.gif TITLE='Clique para gerar a ordem de fornecimento para os itens selecionados.' border=0 align=""center"" >"
					
				do NewLine^COMTable()
				do InsertCellCallback^COMTable(strBotaoConfirma,"GerarCompra^VARAtaCompra")
				do InsertCell^COMTable("",,,,,,8)
				do EndLine^COMTable()				
			}
		  	
			do Stop^COMTable()
			do object.Close()		
	  	}
	}
  	
	quit
	
	
SelecionarItem(pYKEY="",pChecked="")
	;-------------------------------------------------------------------------------
    ; Salva a marcação ou desmarcação do usuário no checkbox do item da ata.
	;
    ; History:
    ; 21-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new idAta, idAtaLinha
	
	set idAta 	   = $$$KEY1(pYKEY)
	set idAtaLinha = $$$KEY2(pYKEY)	
	
	if ($piece(pChecked,",",1) = "true") {
		set ^VARTempSelAtaCompra(YM,YBED,idAta,idAtaLinha,1) = ""
	
	} elseif ($piece(pChecked,",",1) = "false") {
		kill ^VARTempSelAtaCompra(YM,YBED,idAta,idAtaLinha)
	}
	
	quit $$$YES
	
	
GerarCompra(param)
	;-------------------------------------------------------------------------------
    ; Gera a compra (INAUF e INAUFP) com base nos itens que o usuário selecionou.
	;
    ; History:
    ; 16-May-2012	Gustavo Fiuza	Inserida validação de local autorizado
    ; 27-Set-2011	Karine			Add: INAUFLocation    
    ; 21-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new strStatus, idAta, idAtaLinha, idCompra, objCompra, idFornecedor, idCompraLinha,
		objCompraLinha, idKeyLinha, idProduto, idUnidade, precoUnitario, statusLocal
		
	set strStatus = $$$OK
	
	if $data(^VARTempSelAtaCompra(YM,YBED)) = 0 {
		$$$Alert("Nenhum item foi selecionado.")
		quit
	}
	
	if $$blnMultiplosFornecedores(YM,YBED) = $$$YES {
		$$$Alert("Selecione somente um fornecedor para cada ordem de fornecimento a ser gerada.")
		quit		
	}
	
	if $$blnMultiplasAtas(YM,YBED) = $$$YES {
		$$$Alert("Selecione somente uma ata para cada ordem de fornecimento a ser gerada.")
		quit		
	}
	
	set statusLocal = $$validaLocalAutorizado(YLOCATION)	
	if (statusLocal '= $$$OK) {
		$$$Alert(statusLocal)
		quit
	}
	
	//Cabeçalho
	$$$Order3(^VARTempSelAtaCompra,YM,YBED,idAta)
		
		set idCompra = $$^WWWNEXT("INAUF")		
		set idFornecedor = $$GetFornecedorSelecionado(YM,YBED)
		
		set objCompra = ""
		set $$$INAUFOrderType(objCompra) 	  = 2
		set $$$INAUFSupplierNumber(objCompra) = idFornecedor
		set $$$INAUFDateCreated(objCompra)    = $horolog
		set $$$INAUFCreatedBy(objCompra)      = YBED
		set $$$INAUFFREE14(objCompra)         = 0 		;Status
		set $$$INAUFLocation(objCompra)       = YLOCATION
		set $$$INAUFFREE12(objCompra)         = 7 ;Adesão a Ata
		
		set strStatus = $$$Save("INAUF",idCompra,objCompra,1)
		
		if $$$ISOK(strStatus) {
			
			set idCompraLinha = 0
			
			//Linhas
			$$$Order4(^VARTempSelAtaCompra,YM,YBED,idAta,idAtaLinha)
				
				set idProduto = $$GetProduto^VARAtaLinha(idAta,idAtaLinha)
				set idUnidade = $$GetUOM^VARAtaLinha(idAta,idAtaLinha)
				set precoUnitario = $$GetPrecoUnitario^VARAtaLinha(idAta,idAtaLinha)
								
				if $$$ISOK(strStatus) {
		   			set idCompraLinha = $i(idCompraLinha)

					set objCompraLinha = ""
					set $$$INAUFPItemName(objCompraLinha)     = $$SQLGetDescricaoProduto^VARSQL(idProduto)
					set $$$INAUFPItemNumber(objCompraLinha)   = idProduto
					set $$$INAUFPQuantity(objCompraLinha)     = "" 				;Não preencher a quantidade para o usuário
					set $$$INAUFPSource(objCompraLinha)		  = 1 				;Ordem
					set $$$INAUFPSupplier(objCompraLinha)	  = idFornecedor	
					set $$$INAUFPQuantityUnit(objCompraLinha) = idUnidade
					set $$$INAUFPFREE7(objCompraLinha)        = precoUnitario
					set $$$INAUFPFREE20(objCompraLinha) 	  = idAta
					set $$$INAUFPFREE21(objCompraLinha) 	  = idAtaLinha
			   	
		   			set idKeyLinha = idCompra_$$$COMMA_idCompraLinha
					set strStatus = $$$Save("INAUFP",idKeyLinha,objCompraLinha,1)
				}	
			
			$$$End
		}
	
		kill ^VARTempSelAtaCompra(YM,YBED) ;Limpa a temp depois que salvou

	$$$End
	
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	
	} else {
		
		$$$Alert("Ordem de fornecimento "_idCompra_" gerada com sucesso. Favor finalizar o cadastro da OF.")
		do GoToForm^COMUtilForm("VARCompra",idCompra)
	}	
	
	quit
	
blnMultiplosFornecedores(YM,YBED)
	;-------------------------------------------------------------------------------
    ; Verifica se o usuário selecionou mais de um fornecedor (nos checkboxes).
	;
    ; History:
    ; 21-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new blnMultiplosFornecedores, idAta, idAtaLinha, idFornecedor
	
	set blnMultiplosFornecedores = $$$NO
	set lastFornecedor = ""
	
	$$$Order3(^VARTempSelAtaCompra,YM,YBED,idAta)	
		quit:(blnMultiplosFornecedores = $$$YES)
		
		$$$Order4(^VARTempSelAtaCompra,YM,YBED,idAta,idAtaLinha)
			
			set idFornecedor = $$GetFornecedor^VARAtaLinha(idAta,idAtaLinha)								
			if lastFornecedor = "" set lastFornecedor = idFornecedor
			
			if (idFornecedor '= lastFornecedor) {
				set blnMultiplosFornecedores = $$$YES
				quit
			}
			
			set lastFornecedor = idFornecedor
			
		$$$End
	$$$End
	
	quit blnMultiplosFornecedores
	
blnMultiplasAtas(YM,YBED)
	;-------------------------------------------------------------------------------
    ; Verifica se o usuário selecionou mais de uma ata (nos checkboxes).
	;
    ; History:
    ; 19-Nov-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new blnMultiplasAtas, idAta, idAtaLinha, lastAta
	
	set blnMultiplasAtas = $$$NO
	set lastAta = ""
	
	$$$Order3(^VARTempSelAtaCompra,YM,YBED,idAta)	
		quit:(blnMultiplasAtas = $$$YES)
		
		$$$Order4(^VARTempSelAtaCompra,YM,YBED,idAta,idAtaLinha)
											
			if lastAta = "" set lastAta = idAta
			
			if (idAta '= lastAta) {
				set blnMultiplasAtas = $$$YES
				quit
			}
			
			set lastAta = idAta
			
		$$$End
	$$$End
	
	quit blnMultiplasAtas
	
	
GetFornecedorSelecionado(YM,YBED)
	;-------------------------------------------------------------------------------
    ; Retorna o fornecedor dos itens selecionados pelo usuário.
	;
    ; History:
    ; 21-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new idAta, idAtaLinha, idFornecedor
	
	set idFornecedor = ""
	
	$$$Order3(^VARTempSelAtaCompra,YM,YBED,idAta)		
		quit:(idFornecedor '= "")
		
		$$$Order4(^VARTempSelAtaCompra,YM,YBED,idAta,idAtaLinha)			
			set idFornecedor = $$GetFornecedor^VARAtaLinha(idAta,idAtaLinha)
			quit
			
		$$$End
	$$$End
	
	quit idFornecedor
	

validaLocalAutorizado(pidLocation)
	;-------------------------------------------------------------------------------
    ; Verifica se existe restrição de locais autorizados a utilizar a ata, e caso 
    ; sim, se o local do usuário é um deles.
	;
    ; History:
    ; 16-May-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pidLocation = "") "Local não definido"

	new strStatus, idAta	
	set strStatus = $$$OK
		
	$$$Order3(^VARTempSelAtaCompra,YM,YBED,idAta)		
		
		quit:($$$ISERR(strStatus))
		
		;Verifica se a Ata possui autorizações especiais
		continue:( '$data(^VARAtaLocal(YM,idAta)) )
	
		;Caso tenha locais autorizados, verifica se o local do usuário é um deles
		if '$data(^VARAtaLocals(YM,1,pidLocation,idAta)) {
			set strStatus = "A Ata "_idAta_" não está autorizada a ser emitida para este local."
			quit
		}
	
	$$$End
	
	quit strStatus]]></Routine>
</Export>