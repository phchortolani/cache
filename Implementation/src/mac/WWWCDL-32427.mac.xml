<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWCDL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWCDL(CLASS,MSG,NEW)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		UMSETZEN CDL IN WWW...
	;
	; Inputs : 
	;	CLASS = KASSENNAME
	;	MSG=1 = MIT ANZEIGE ;by means of Show 
	;	NEW=1 = LÖSCHEN ALTE WERTE  ;Delete 
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 23-Jun-2008	GRF		Doco; quits; expand commans
	; 09.10.2001	DT		Created
	;-------------------------------------------------------------------------------
	new DATA,INDEX,ISINDEX,ISKEY,LASTINDEX,NAME,NOOFDATA,NOOFKEY,PROP,SAVE,STORAGE
	new YDATEI,YFELD,YKEY,YLANG,YLFN,YLINK,YNUM,YTYP
	
	SET MSG=+$GET(MSG)
	IF $GET(VORG(1))'="" SET CLASS=VORG(1)  ;WENN MANUELLES FORMULAR ;when form 
	SET CLASS=$GET(CLASS)
	IF CLASS="" QUIT:MSG'=1  DO ^WWWINFO("NO DATA") QUIT
	IF '$DATA(^oddDEF(CLASS)) SET CLASS="User."_CLASS
	IF '$DATA(^oddDEF(CLASS)) QUIT:MSG'=1  DO ^WWWINFO("NO DATA") QUIT
	DO START
	IF $GET(YDATEI)'="" IF $DATA(^WWW001(0,YDATEI,1)) IF '$DATA(^WWW002(0,YDATEI,1,1)) SET ^WWW002(0,YDATEI,1,1)="ID"_Y_"ID"_Y_9_Y_7  ;ohne key ;Without 
	IF MSG=1 DO
	. SET YLINK=""
	. DO
	. . NEW YKEY,YI
	. . QUIT:$GET(YDATEI)=""
	. . SET YI=YAKTION_"EP=WWWFORM&amp;YFORM=WWW001&amp;YKEY="_YDATEI
	. . DO VAR^WWWCGI
	. . SET YLINK=YI
	. ;
	. DO ^WWWINFO($$^WWWTEXT(30013)_" ("_$GET(YDATEI)_")",0,YLINK)
	
	QUIT
	
	
START
	SET YDATEI=CLASS
	IF $FIND(CLASS,".") SET YDATEI=$PIECE(CLASS,".",$LENGTH(CLASS,"."))
	
	;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	;IF $GET(NEW)=1 DO  ;NEU  /DELETE ;recent 
	. DO ^WWWSKILL("WWW001",YDATEI)
	. KILL ^WWW0011(0,YDATEI)
	. DO ^WWWSKILL("WWW002",YDATEI)
	. KILL ^WWW0021(0,YDATEI)
	. DO ^WWWSKILL("WWW003",YDATEI)
	. KILL ^WWW0031(0,YDATEI)
	. KILL ^WWW0013(0,YDATEI)
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	
	SET YFELD=""
	;SET HEADER ;table-mat 
	DO
	. SET YFELD=""
	. SET $PIECE(YFELD,Y,1)  = $EXTRACT($PIECE($GET(^oddDEF(CLASS,"spec","description")),$CHAR(13,10),1),1,40)  ;BESCHREIBUNG ;description 
	. SET $PIECE(YFELD,Y,19) = $TRANSLATE($GET(^oddDEF(CLASS,"spec","description")),$CHAR(13,10),"|")           ;BESCHREIBUNG ;description 
	. IF $GET(^oddDEF(CLASS,"spec","persistent"))=1 SET $PIECE(YFELD,Y,20)="Library.Persistent"
	. IF $GET(^oddDEF(CLASS,"spec","super"))'=""    SET $PIECE(YFELD,Y,20)=$PIECE($GET(^oddDEF(CLASS,"spec","super")),",",1)
	. SET $PIECE(YFELD,Y,15)=$PIECE($GET(^oddDEF(CLASS,"spec","timecreated")),",",1)
	. SET SAVE=$ORDER(^oddDEF(CLASS,"storage",""))
	
	IF $FIND($ZVERSION,"4.2") || $FIND($ZVERSION,"5.") DO
	. SET YFELD=""
	. SET $PIECE(YFELD,Y,1)  = $EXTRACT($PIECE($GET(^oddDEF(CLASS,4)),$CHAR(13,10),1),1,40)  ;BESCHREIBUNG ;description 
	. SET $PIECE(YFELD,Y,19) = $TRANSLATE($GET(^oddDEF(CLASS,4)),$CHAR(13,10),"|")           ;BESCHREIBUNG ;description 
	. IF $GET(^oddDEF(CLASS,23))="persistent" SET $PIECE(YFELD,Y,20)="Library.Persistent"
	. SET $PIECE(YFELD,Y,15)=$PIECE($GET(^oddDEF(CLASS,63)),",",1)
	. SET SAVE=$GET(^oddDEF(CLASS,59))
	
	IF SAVE="" SET SAVE="Default"
	;LOOK FOR SAVE
	DO
	. SET $PIECE(YFELD,Y,8)  = 4   ;CACHÉ
	. SET $PIECE(YFELD,Y,21) = 1   ;MAPPED
	. SET $PIECE(YFELD,Y,20) ="Library.Persistent"   ;CACHÉ
	. SET $PIECE(YFELD,Y,22) = $PIECE($GET(^oddDEF(CLASS,"storage",SAVE,"datalocation")),"^",2)
	. IF $PIECE(YFELD,Y,22)="" SET $PIECE(YFELD,Y,22)=$PIECE($GET(^oddDEF(CLASS,"s",SAVE,24)),"^",2)
	. SET $PIECE(YFELD,Y,23) = $PIECE($GET(^oddDEF(CLASS,"storage",SAVE,"indexlocation")),"^",2)
	. IF $PIECE(YFELD,Y,23)="" SET $PIECE(YFELD,Y,23)=$PIECE($GET(^oddDEF(CLASS,"s",SAVE,25)),"^",2)
	
	SET YOK=$$^WWWSPEI("WWW001",YDATEI,YFELD,1) 
	SET YFELD=""
	
	;CREATE SEQUENCE
	IF SAVE="" QUIT
	
	SET DATA=$GET(^oddDEF(CLASS,"storage",SAVE,"defaultdata"))  ;DefaultData
	IF DATA="" SET DATA=$GET(^oddDEF(CLASS,"s",SAVE,22))  ;DefaultData
	QUIT:DATA=""  ;FALSCHE SPEICHERUNGSART
	
	DO   ;4.0
	. NEW NAME
	. SET YNUM=""
	. FOR  SET YNUM=$ORDER(^oddDEF(CLASS,"storage",SAVE,"data",DATA,"value",YNUM)) QUIT:YNUM=""  DO
	. . SET NAME=$GET(^oddDEF(CLASS,"storage",SAVE,"data",DATA,"value",YNUM,"value"))
	. . IF NAME'="" SET YNUM(NAME)=YNUM
	
	DO  ;5.0
	. NEW NAME
	. SET YNUM=""
	. FOR  SET YNUM=$ORDER(^oddDEF(CLASS,"s",SAVE,"D",DATA,"V",YNUM)) QUIT:YNUM=""  DO
	. . SET NAME=$GET(^oddDEF(CLASS,"s",SAVE,"D",DATA,"V",YNUM,21))
	. . IF NAME'="" SET YNUM(NAME)=YNUM
	
	SET ISKEY=""
	SET LASTINDEX=0
	
	;Suchen Primärschluessel 4.0 ;Search 
	SET INDEX=""
	FOR  SET INDEX=$ORDER(^oddDEF(CLASS,"index",INDEX)) QUIT:INDEX=""  DO
	. IF $GET(^oddDEF(CLASS,"index",INDEX,"idkey"))=1 DO  QUIT                 ;ISKEY
	. . SET ISKEY=$GET(^oddDEF(CLASS,"index",INDEX,"attributes"))
	. ;
	. IF $GET(^oddDEF(CLASS,"index",INDEX,"idkey"))'=1 DO  QUIT                ;ISINDEX
	. . NEW INDEXN
	. . SET INDEXN=+$TRANSLATE($$^WWWUMLAU(INDEX,1),"INDEX")
	. . IF INDEXN=0 SET INDEXN=LASTINDEX+1
	. . SET LASTINDEX=INDEXN
	. . SET ISINDEX(INDEXN)=$GET(^oddDEF(CLASS,"index",INDEX,"attributes"))
	. . SET ^WWW0013(0,YDATEI,INDEXN,1)=INDEX   ;SORTIERSCHLÜSSELNAME
	
	;Suchen Primärschluessel 5.0 ;Search 
	SET INDEX=""
	FOR  SET INDEX=$ORDER(^oddDEF(CLASS,"i",INDEX)) QUIT:INDEX=""  DO
	. IF $GET(^oddDEF(CLASS,"i",INDEX,26))=1 DO  QUIT  ;ISKEY
	. . SET ISKEY=$GET(^oddDEF(CLASS,"i",INDEX,28))
	. ;
	. IF $GET(^oddDEF(CLASS,"i",INDEX,26))'=1 DO  QUIT  ;ISINDEX
	. . NEW INDEXN
	. . SET INDEXN=+$TRANSLATE($$^WWWUMLAU(INDEX,1),"INDEX")
	. . IF INDEXN=0 SET INDEXN=LASTINDEX+1
	. . SET LASTINDEX=INDEXN
	. . SET ISINDEX(INDEXN)=$GET(^oddDEF(CLASS,"i",INDEX,28))
	. . SET ^WWW0013(0,YDATEI,INDEXN,1)=INDEX   ;SORTIERSCHLÜSSELNAME
	
	;DATENFELDER / PRIMÄRSCHL.  4.0
	SET PROP=""
	FOR  SET PROP=$ORDER(^oddDEF(CLASS,"property",PROP)) QUIT:PROP=""  DO
	. NEW YI,YFELD,YFORM,YVOR
	. SET NAME=PROP
	. IF $GET(^oddDEF(CLASS,"property",PROP,"name"))'=""                  SET NAME=^("name")   ; FIXME Naked References
	. SET YFELD=NAME
	. SET $PIECE(YFELD,Y,25)=PROP
	. SET $PIECE(YFELD,Y,2)=NAME
	. IF $GET(^oddDEF(CLASS,"property",PROP,"required"))=1              SET $PIECE(YFELD,Y,18)=1   ;MUß FELD / REQUIRED ;field 
	. IF $GET(^oddDEF(CLASS,"property",PROP,"parameter","CAPTION"))'="" SET $PIECE(YFELD,Y,2)=^("CAPTION")
	. SET YVAL=""
	. IF $GET(^oddDEF(CLASS,"property",PROP,"parameter","VALUELIST"))'="" SET YVAL=^("VALUELIST")
	. SET YLANG=""
	. IF $GET(^oddDEF(CLASS,"property",PROP,"parameter","MAXLEN"))'=""    SET YLANG=^("MAXLEN")
	. SET YMAXVAL=""
	. IF $GET(^oddDEF(CLASS,"property",PROP,"parameter","MAXVAL"))'=""    SET YMAXVAL=^("MAXVAL")
	. IF +YMAXVAL'=0 SET YLANG=$LENGTH(YMAXVAL)
	. IF $GET(^oddDEF(CLASS,"property",PROP,"description"))'="" SET $PIECE(YFELD,Y,15)=$TRANSLATE($GET(^oddDEF(CLASS,"property",PROP,"description")),$CHAR(13,10),"|")
	. SET YTYPE=""
	. IF $GET(^oddDEF(CLASS,"property",PROP,"type"))'=""                  SET YTYPE=^("type")
	. SET $PIECE(YFELD,Y,4)=YLANG
	. DO TYPE
	. DO PRUEF
	. SET YLFN=1
	. IF $DATA(YNUM(PROP)) SET YLFN=$GET(YNUM(PROP))  ;SEQUENCE OF DATAFIELD
	. SET INDEXN="" 
	. SET INDEX=""
	. FOR  SET INDEX=$ORDER(ISINDEX(INDEX)) QUIT:INDEX=""  FOR YI=1:1 QUIT:$PIECE(ISINDEX(INDEX),",",YI)=""  IF $PIECE(ISINDEX(INDEX),",",YI)=PROP SET INDEXN=INDEXN_INDEX_"."_YI_"," QUIT
	. IF $EXTRACT($REVERSE(INDEXN))="," SET INDEXN=$EXTRACT(INDEXN,1,$LENGTH(INDEXN)-1)
	. SET $PIECE(YFELD,Y,6)=INDEXN
	. IF ISKEY'="" IF $FIND(ISKEY,PROP) DO  QUIT  ;IS=KEY
	. . NEW YI
	. . SET YLFN="" 
	. . FOR YI=1:1 QUIT:$PIECE(ISKEY,",",1)=""  IF $PIECE(ISKEY,",",YI)=PROP SET YLFN=YI QUIT
	. . IF YLFN="" SET YLFN=1
	. . SET YOK=$$^WWWSPEI("WWW002",YDATEI_","_YLFN,YFELD,1)   ;SAVE KEY
	. ;
	. ;NO KEY= DATAFIELD
	. IF YLFN="" SET YLFN=1
	. SET YOK=$$^WWWSPEI("WWW003",YDATEI_","_YLFN,YFELD,1)  ;SAVE FIELDS
	
	;DATENFELDER / PRIMÄRSCHL.  5.0
	SET PROP=""
	FOR  SET PROP=$ORDER(^oddDEF(CLASS,"a",PROP)) QUIT:PROP=""  DO
	. NEW YI,YFELD,YFORM,YVOR
	. SET NAME=PROP
	. SET YFELD=NAME
	. SET $PIECE(YFELD,Y,25)=PROP
	. SET $PIECE(YFELD,Y,2)=NAME
	. IF $GET(^oddDEF(CLASS,"a",PROP,37))=1 SET $PIECE(YFELD,Y,18)=1   ;MUß FELD / REQUIRED ;field 
	. IF $GET(^oddDEF(CLASS,"a",PROP,"P","CAPTION"))'=""   SET $PIECE(YFELD,Y,2)=^("CAPTION")   ; FIXME Naked References
	. SET YVAL=""
	. IF $GET(^oddDEF(CLASS,"a",PROP,"P","VALUELIST"))'="" SET YVAL=^("VALUELIST")
	. SET YLANG=""
	. IF $GET(^oddDEF(CLASS,"a",PROP,"P","MAXLEN"))'=""    SET YLANG=^("MAXLEN")
	. SET YMAXVAL=""
	. IF $GET(^oddDEF(CLASS,"a",PROP,"P","MAXVAL"))'=""    SET YMAXVAL=^("MAXVAL")
	. IF +YMAXVAL'=0 SET YLANG=$LENGTH(YMAXVAL)
	. IF $GET(^oddDEF(CLASS,"a",PROP,4))'="" SET $PIECE(YFELD,Y,15)=$TRANSLATE($GET(^oddDEF(CLASS,"a",PROP,4)),$CHAR(13,10),"|")
	. SET YTYPE=""
	. IF $GET(^oddDEF(CLASS,"a",PROP,"T"))'=""             SET YTYPE=^("T")
	. IF YTYPE="" SET YTYPE=$GET(^oddDEF(CLASS,"a",PROP,5))
	. SET $PIECE(YFELD,Y,4)=YLANG
	. DO TYPE
	. DO PRUEF
	. SET YLFN=1
	. IF $DATA(YNUM(PROP)) SET YLFN=$GET(YNUM(PROP))  ;SEQUENCE OF DATAFIELD
	. SET INDEXN="" 
	. SET INDEX="" FOR  SET INDEX=$ORDER(ISINDEX(INDEX)) QUIT:INDEX=""  FOR YI=1:1 QUIT:$PIECE(ISINDEX(INDEX),",",YI)=""  IF $PIECE(ISINDEX(INDEX),",",YI)=PROP SET INDEXN=INDEXN_INDEX_"."_YI_"," QUIT
	. IF $EXTRACT($REVERSE(INDEXN))="," SET INDEXN=$EXTRACT(INDEXN,1,$LENGTH(INDEXN)-1)
	. SET $PIECE(YFELD,Y,6)=INDEXN
	. IF ISKEY'="" IF $FIND(ISKEY,PROP) DO  QUIT  ;IS=KEY
	. . NEW YI
	. . SET YLFN="" 
	. . FOR YI=1:1 QUIT:$PIECE(ISKEY,",",1)=""  IF $PIECE(ISKEY,",",YI)=PROP SET YLFN=YI QUIT
	. . IF YLFN="" SET YLFN=1
	. . SET YOK=$$^WWWSPEI("WWW002",YDATEI_","_YLFN,YFELD,1)   ;SAVE KEY
	. ;
	. ;NO KEY= DATAFIELD
	. IF YLFN="" SET YLFN=1
	. SET YOK=$$^WWWSPEI("WWW003",YDATEI_","_YLFN,YFELD,1)  ;SAVE FIELDS
	
	QUIT
	
	
TYPE ;SETZEN TYPE ;typeset letter 
	IF $FIND(YTYPE,"%Library.")  SET YTYPE=$PIECE(YTYPE,"%Library.",2)
	IF $FIND(YVAL,",Y,N")        SET YTYPE="Boolean",YLANG=1   ;JA/NEIN
	IF $FIND(YVAL,",J,N")        SET YTYPE="Boolean",YLANG=1   ;JA/NEIN
	IF $FIND(YTYPE,"String")     SET YTYPE=6  ;TEXT
	IF $FIND(YTYPE,"Date")       SET YTYPE=1
	IF $FIND(YTYPE,"Boolean")    SET YTYPE=2
	IF $FIND(YTYPE,"Binary")     SET YTYPE=4
	IF $FIND(YTYPE,"LongString") SET YTYPE=3
	IF $FIND(YTYPE,"Query")      SET YTYPE=3
	IF $FIND(YTYPE,"Numeric")    SET YTYPE=4
	IF $FIND(YTYPE,"Status")     SET YTYPE=4 SET YLANG=4
	IF $FIND(YTYPE,"Time")       SET YTYPE=7
	IF $FIND(YTYPE,"List")       SET YTYPE=15
	IF $FIND(YTYPE,"Collection") SET YTYPE=15
	IF $FIND(YTYPE,"Currency")   SET YTYPE=8
	IF $FIND(YTYPE,"Float")      SET YTYPE=12 IF +YLANG=0 SET YLANG=12
	IF $FIND(YTYPE,"SmallInt")   SET YTYPE=4  IF +YLANG=0 SET YLANG=12
	IF $FIND(YTYPE,"TinyInt")    SET YTYPE=4  IF +YLANG=0 SET YLANG=4
	IF $FIND(YTYPE,"Integer")    SET YTYPE=4  IF +YLANG=0 SET YLANG=12
	IF $FIND(YTYPE,"TimeStamp")  SET YTYPE=14
	IF $FIND(YTYPE,"Name")       SET YTYPE=6
	IF $FIND(YTYPE,"Stream")     SET YTYPE=3
	IF YLANG>200                 SET YTYPE=3
	
	IF +YTYPE=0 IF YTYPE'="" DO
	. SET $PIECE(YFELD,Y,8)=YTYPE
	. IF $PIECE(YTYPE,".",2)'="" SET $PIECE(YFELD,Y,8)=$PIECE(YTYPE,".",2)
	. SET $PIECE(YFELD,Y,10)=1   ;RELATION
	
	IF +YTYPE=0  SET YTYPE=6
	IF +YTYPE'=0 SET $PIECE(YFELD,Y,3)=YTYPE  ;FELDART
	QUIT
	
PRUEF ;PRÜFEN FELDLÄNGEN ;sift 
	NEW YTYPE
	
	SET YTYPE=$PIECE(YFELD,Y,3)
	IF YTYPE=1 DO
	. SET $PIECE(YFELD,Y,4)=10
	
	IF YTYPE=2 DO
	. SET $PIECE(YFELD,Y,4)=1
	. SET $PIECE(YFELD,Y,8)="WWW100"
	. SET $PIECE(YFELD,Y,9)=""""_"JA/NEIN"_""""_",SPRACHE"
	. SET $PIECE(YFELD,Y,10)=1
	;. SET $PIECE(YFELD,Y,20)=1
	
	IF YTYPE=7 DO
	. SET $PIECE(YFELD,Y,4)=5
	
	IF YTYPE=13 DO
	. SET $PIECE(YFELD,Y,4)=15
	
	IF YTYPE=14 DO
	. SET $PIECE(YFELD,Y,4)=16
	
	IF YTYPE'=12 SET $PIECE(YFELD,Y,16)=""
	IF YTYPE=12 IF +$PIECE(YFELD,Y,16)=0 SET $PIECE(YFELD,Y,16)=""
	IF YTYPE=8 DO
	. SET $PIECE(YFELD,Y,16)=2
	
	IF $PIECE(YFELD,Y,4)="" SET $PIECE(YFELD,Y,4)=30   ;LÄNGE 30 ;length 
	IF $PIECE(YFELD,Y,3)="" SET $PIECE(YFELD,Y,3)=6    ;ERFASSUNGSART TEXT
	QUIT
	
SUCH ;Aufbauen Classen
	NEW CLASS
	
	KILL ^WWWCDL(0)     ; Classes from OddDEF
	SET CLASS="User."
	FOR  SET CLASS=$ORDER(^oddDEF(CLASS)) QUIT:CLASS=""  QUIT:'$FIND(CLASS,"User.")  DO
	. SET ^WWWCDL(0,CLASS,1)=CLASS
	
	QUIT
	
]]></Routine>
</Export>