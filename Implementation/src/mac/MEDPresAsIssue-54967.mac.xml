<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPresAsIssue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPresAsIssue
	
#include MEDConst
#include COMSYS
#include COMGridEdit31
	
OnBeforePrimaryKey()
	;-------------------------------------------------------------------------------
	; Check if the Issue Due Date is overdue
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 28-Oct-2010	GRF		SR16642.1: Data typing clarified.
	; 14-Sep-2010	PPP		SR16642: Created
	;-------------------------------------------------------------------------------
	new dteDue,fltDaysChck,fltDispensed,fltFrequency,intElapsed,tmsIssued
 	
	set dteDue       = $$$MEDPrescriptionDueDate(YFELD)
	set fltFrequency = $$$MEDPrescriptionFrequency(YFELD)
	set fltDispensed = $$$MEDPrescriptionRepeatsDispensed(YFELD)
	set tmsIssued    = $$$MEDPrescriptionLastIssued(YFELD)
	
	// check the frequency of a prescription repeat against the elapsed days since created to check if an early dispense
	set fltDaysChck = fltFrequency
	set intElapsed  = $horolog - tmsIssued
	
	if tmsIssued '= "" {
		if (intElapsed > fltDaysChck) {
			set YCOLOFF  = "red"
			set YTOOLTIP = $$$Text($listbuild("MED01247",$$^WWWDATE(tmsIssued+fltDaysChck)))
		  ; "Prescription Overdue %1."
			
		} elseif (intElapsed < fltDaysChck) {
			set YCOLOFF  = "yellow"
			set YTOOLTIP = $$$Text($listbuild("MED00369",$$^WWWDATE(tmsIssued+fltDaysChck)))
		} ; "WARNING: Prescription due only on %1"
		
	} else {
		if dteDue<$horolog {
			set YCOLOFF  = "red"
			set YTOOLTIP = $$$Text($listbuild("MED01247",$$^WWWDATE(dteDue)))
		} ; "Prescription Overdue %1."
	}
	quit
	
	
OnBeforeFormat(pYFIELDNAME="",pYTEXT="",pYFELD="")
	;-------------------------------------------------------------------------------
	; OnBeforeFormat for Issues (MEDPresAsIssue COMViewGrid)
	;
	; Params:pYFIELDNAME,pYTEXT,pYFELD
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 14-Sep-2010	PPP		SR16642: Created
	;-------------------------------------------------------------------------------
 	new idClassField,idCol,idKey,idRow,strStatus
 	
	quit:($get(YUSER)="")||($get(YKEY)="")
	
	if $data(^CacheTempLocked(YUSER,YKEY)) {
		set strStatus = $get(^CacheTempLocked(YUSER,YKEY))
		set YTOOLTIP  = $$$Text(strStatus)
		
	} else {
		$$$GRIDSplitKey(pYFIELDNAME,idRow,idCol)
		set idClassField = $$$GetClassField(YFORM,idCol)
		
		if (idClassField = $$$FldMEDPrescriptionDueDate) {
			if $$$MEDPrescriptionDueDate(YFELD)<$horolog {
				set YWARNING = $$$YES
				set YTOOLTIP = $$$Text($listbuild("MED01247",$$^WWWDATE($$$MEDPrescriptionDueDate(YFELD))))
			} ; "Prescription Overdue %1."
		}
	}
	quit
	
]]></Routine>
</Export>