<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INForeRest" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INForeRest
	#include INConst
	#include COMSYS
	
AfterDataFields(pstrKey="")
    ;-------------------------------------------------------------------------------
    ; After Date Fields
    ;
    ; Returns:
    ;
    ; History:
    ; 13-Sep-2006	JW		SRBR014083: Added Update Settings. Cleaned up.
    ; 21-Oct-2004	SCR		Created
    ;-------------------------------------------------------------------------------
    new idItem,idLoc,objRest,idRule
 
    set idLoc = $$$KEY1(pstrKey)
    set idItem = $$$KEY2(pstrKey)
 
    if (idItem '= "") && (idLoc '= "") {
	    set objRest=$get(^INForeRest(YM,idLoc,idItem,1))
	    set idRule=$$$INForeRestRule(objRest)
 
		if idRule'="" {
			do ShowCalc^INForeItem(idItem,idLoc,idRule,$$$INForeRestCreated(objRest),$$$NO,$$$INForeRestSupplier(objRest),$$$NO)	//BR014083
		}
    }
	quit
 
RemoveResultsForProcess(pidProcess="")
    ;-------------------------------------------------------------------------------
    ; Remove all results for a process
    ;
    ; History:
	; 20-Feb-2008	STEVER		SRBR014376 Remove Masterfile sets
    ; 08-Apr-2005	Paul K		Created SR:12075
    ;------------------------------------------------------------------------------
    new strProcess,idLocation,idProduct
    
	set strProcess=$$$Index(pidProcess)
	set idLocation=""
	for {
		set idLocation=$order(^INForeRests(YM,5,strProcess,idLocation))
		quit:idLocation=""
		set idProduct=""
		for {
			set idProduct=$order(^INForeRests(YM,5,strProcess,idLocation,idProduct))
			quit:idProduct=""
			do KILL^COMUtils("INForeRest",idLocation_","_idProduct)
			do RemoveResultSet(idLocation,idProduct)
		}
	}
	quit
 
RemoveResultSet(pidLocation,pidProduct)	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Remove Materfile sets for Location/Item.
	;
	; Inputs :
	;	pidLocation=Location
	;	pidProduct=Item
	;
	;
	; History :
	; 20-Feb-2008	STEVER		SRBR014376: Created
	;-------------------------------------------------------------------------------
	new intCount
	
	set intCount=""
	
	for {
		set intCount=$order(^INForeRestSet(YM,pidLocation,pidProduct,intCount))
		quit:intCount=""
		do KILL^COMUtils("INForeRestSet",pidLocation_","_pidProduct_","_intCount)
	} 
	
	quit
]]></Routine>
</Export>