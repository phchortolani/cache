<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="INIssueLocLineScan" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[INIssueLocLineScan
	
#include COMGridEdit31
#include INConst
#include %occInclude
#def1arg WWWDATEN(%args) ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",%args)
#include WWWConst
#include COMSYS
	
AddScanLine(pidScan)
    ;-------------------------------------------------------------------------------
    ; Routine to Add a new line to a Grid after a Scan is validated
    ;
    ; Assumptions :
    ;  YFORM	-	 Form Name
    ; 
    ; Returns:
    ; 	Status
    ;
    ; History:
    ; 23-Apr-2013	SCR		HEVA-811: Check for Item not in List and Return error
    ; 20-Jul-2009	PPP		SR16773: ScanDelimiter is a function now
    ; 23-Jun-2009	PPP		SR16510: Updated for User defined policies
    ; 25-May-2009	PPP		SR16565: Scanning Prompt for Grid
    ;-------------------------------------------------------------------------------
	new arrBundle,arrFields,blnItemExists,idBundle,idClass,idData,idFromLocn
 	new idIssue,idItem,idLine,idLot,idOriginalLine,idReq,idStorage,idToLocn,idType
 	new objData,objHeader,objLine,objOldData,sc,strDelim,strKey,strStatus
 	//i $g(YBED) = "FELIPEADM" h 10
 	set strDelim = $$$ScanDelim
 	
	set idItem = ""	
	if $get(YFORM)'="" {
		set idClass       = $$$GetClass(YFORM)
		set objLine       = ""
		set idItem        = $piece(pidScan,strDelim,1)               //SR16773
		set idLot         = $piece(pidScan,strDelim,2)               //SR16773
		set objHeader     = $$$GRIDYFELDContainer
	 	set idFromLocn    = $$$INIssueFromLocn(objHeader)
	 	set idToLocn      = $$$INIssueToLocn(objHeader)
	 	set idType        = $$$INIssueType(objHeader)
  		set blnItemExists = $$$NO
		
	 	set idReq         = $$$INIssueReference(objHeader)
		set idIssue       = $$$KEY1($$$GRIDYKEYContainer)
 
 		// For Picking Screens Only
 		; HEVA-811 vvvv
 		set idOriginalLine = $$GetIssueLine(idIssue,idItem)
 		/*if idOriginalLine="" {
	 		set strStatus = $$$MakeStatus("IN01420",idItem)  ;Item %1 was not requested/confirmed
	 		quit strStatus    //EARLY EXIT
 		}*/
 		; HEVA-811 ^^^^
		set sc = $$GetBundleForAttData^WWW120Scan(idItem,idFromLocn,pidScan,.arrBundle,.idBundle,.idStorage)
 		if $$$ISERR(sc) set strStatus = $$ISStatusToDLStatus^COMUtilError(sc) quit strStatus    //EARLY EXIT
		
		//Process only if Bundle Exist for the Item/Lot combination and has Qty
		if $get(idBundle)'="" {
			/// Recupera a unidade de medida padrão para pode fazer o cálculo do custo médio
			set codINARTUnit 		= $$$INARTUnitofMeasure($get(^INART(0,idItem,1)))
			
			//Get the data for the Item if it already exists
			//Need to update arrBundle if lines already exist
			//set idLine = $$GetExistingItemRowData(idItem,.idBundle,.idStorage,.arrBundle,.blnItemExists)
			set idLine = $$GetRowLineWithQtyChecks^COMGridEdit31Scan(idItem, 
			                                                         $$$FldINIssueLineItem, 
			                                                         $$$FldINIssueLineBundle, 
			                                                         $$$FldINIssueLineFromStorage, 
			                                                         $$$FldINIssueLineQtyIssued, 
			                                                         .idBundle, 
			                                                         .idStorage, 
			                                                         .arrBundle, 
			                                                         .blnItemExists)
			
			if idLine {
				set (objLine,objOldData) = $$$GRIDGetYFELD(idLine)
				
				set fltUOMConversion	= $$GetUOMConversion^INUOMConversion(idItem,$$$INIssueLineUnit(objLine)) 
				
				set $$$INIssueLineQtyRequired(objLine) = $$$INIssueLineQtyRequired(objLine) + 1
				set $$$INIssueLineQtyIssued(objLine)   = $$$INIssueLineQtyIssued(objLine) + (1 * fltUOMConversion)
				
				kill arrFields
				//set the array for the fields that are updated
				set arrFields($$$FldINIssueLineQtyRequired)	= $$$INIssueLineQtyRequired(objLine)	//UPDATE
				set arrFields($$$FldINIssueLineQtyIssued)	= $$$INIssueLineQtyIssued(objLine)		//UPDATE
				
				//Updates the Row and Displayes it (for existing lines)
				//If the row is on another page displays the lines at the top of the grid
				do UpdateRow^COMGridEdit31Scan(YFORM, idLine, .arrFields, objLine)
	 
			} else {
				if $get(idBundle)'="" {
					set codINARTUnit 		= $$$INARTUnitofMeasure($get(^INART(0,idItem,1)))
					set fltUOMConversion	= $$GetUOMConversion^INUOMConversion(idItem,codINARTUnit) 
					set $$$INIssueLineItem(objLine)					= idItem
					set $$$INIssueLineUnit(objLine)					= codINARTUnit
					set $$$INIssueLineQtyIssued(objLine)   			= 1
					set $$$INIssueLineQtyRequired(objLine) 			= 1
					set $$$INIssueLineFromStorage(objLine) 			= idStorage
					set $$$INIssueLineBundle(objLine)      			= idBundle
					set $$$INIssueLineLocationInventory(objLine)	= $$GetSOHLocal^INIssueLocLine(idItem,YLOCATION)
					set $$$INIssueLineNetworkInventory(objLine)		= $$GetSOHCompany^INIssueLocLine(idItem)
					set $$$INIssueLineToStorage(objLine) 			= $$GetStorage^INIssueAuto(idItem,idToLocn)
					set $$$INIssueLineReference(objLine)			= idIssue
					set $$$INIssueLineUnitCost(objLine) 			= $$GetCost^INItemCost(idItem,$$$INIssueFromLocn(objHeader))*fltUOMConversion
					
					$$$GRIDAddLine(objLine,,YFORM,,,,,,.idDataLine)
					do UpdateContainer^COMGridEdit31G()
					kill arrFields
					set arrFields($$$FldINIssueLineLocationInventory)	= $$$INIssueLineLocationInventory(objLine)	//UPDATE
					set arrFields($$$FldINIssueLineNetworkInventory)	= $$$INIssueLineNetworkInventory(objLine)		//UPDATE
					do UpdateRow^COMGridEdit31Scan(YFORM, idDataLine, .arrFields, objLine)
				}
			}		
			
		} else {
			set strStatus = $$$MakeStatus("IN01040",idItem,idFromLocn)
		}   ; "Stock Unavailable for Item %1 at Location %2"
 
	}
	
	quit $$$OK
	
	
GetIssueLine(pidIssue, pidItem)
    ;-------------------------------------------------------------------------------
    ; Get the IssueLine associated with the given Item
    ; Assumptions :
    ;  YFORM	-	 Form Name
    ; 
    ; Returns:
    ; 	idLine
    ;
    ; History:
    ; 23-Jun-2009	PPP		SR16510: Created
    ;-------------------------------------------------------------------------------
	new idIssueLine,objIssueLine

	set idIssueLine = ""
	for {
		set idIssueLine = $order(^INIssueLine(YM,pidIssue,idIssueLine))
		quit:idIssueLine=""

		set objIssueLine = $get(^INIssueLine(YM,pidIssue,idIssueLine,1))
		if $$$INIssueLineItem(objIssueLine) = pidItem quit
	}

	quit idIssueLine]]></Routine>
</Export>