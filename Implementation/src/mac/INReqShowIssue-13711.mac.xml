<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReqShowIssue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReqShowIssue
	
#include COMSYS
#include INConst
	
ShowIssuedTable(pidReq)
	;-------------------------------------------------------------------------------
	; Get the Issues for this Requistion show the table of them.
	;
	; Called By : OnAfterDataFields^INReq
	;
	; Params:
	; pidRec : the id of the Requsition
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-Dec-2009	GRF		SR17085: Show Reversals as well
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
    ; 						Created
	;-------------------------------------------------------------------------------
	new arrIssues,arrReverse,idIssue,idRevDoc,idxIssueNo,idxReq,idxStatus
	
	quit:$$$NoKey(pidReq)
	
	set idxReq = $$$Index(pidReq)
	set idIssue = ""
	for {
		set idIssue = $order(^INIssues(YM,5,1,idxReq,idIssue))
		quit:idIssue=""
		
		set arrIssues(idIssue) = ""
		
		set idxIssueNo = $$$Index(idIssue)       ; SR17085 vvv
		set idxStatus  = ""
		for {
			set idxStatus = $order(^INIssueRevs(0,2,idxStatus))
			quit:idxStatus=""
			
			set idRevDoc   = ""
			for {
				set idRevDoc = $order(^INIssueRevs(0,2,idxStatus,idxIssueNo,idRevDoc))
				quit:idRevDoc=""
				
				set arrReverse(idRevDoc) = ""
			}
		}                                        ; SR17085 ^^^
	}
	do ShowRequistions(.arrIssues)
	do ShowReversals(.arrReverse)                ; SR17085
	quit
	
	
ShowReversedTable(pidIssue)
	;-------------------------------------------------------------------------------
	; Get the Issue Reversals for this Issue show the table of them.
	;
	; Called By : OnAfterDataFields^INIssue
	;
	; Params:
	; pidIssue : the id of the Issue.
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-Dec-2009	GRF		SR17085: Created from ShowIssuedTable
	;-------------------------------------------------------------------------------
	new arrReverse,idRevDoc,idxIssueNo,idxStatus
	
	quit:$$$NoKey(pidIssue)
	
	set idxIssueNo = $$$Index(pidIssue)
	set idxStatus  = ""
	for {
		set idxStatus = $order(^INIssueRevs(0,2,idxStatus))
		quit:idxStatus=""
		
		set idRevDoc   = ""
		for {
			set idRevDoc = $order(^INIssueRevs(0,2,idxStatus,idxIssueNo,idRevDoc))
			quit:idRevDoc=""
			
			set arrReverse(idRevDoc) = ""
		}
	}
	do ShowReversals(.arrReverse)
	quit
	
	
ShowRequistions(&parrIssues)
	;-------------------------------------------------------------------------------
	; Display a list of Issues for a Requisition at various stages.
	;
	; Called By : ShowIssuedTable
	;
	; History:
	; 23-Dec-2009	GRF		SR17085: Improve security with user name rather than id
    ; 29-May-2009	PPP		SR16586: Simplify Process
	;-------------------------------------------------------------------------------
	new blnHeader,idIssue,idKey,idLink,idLocn,idStatus,idUser,objIssue,lstHeader
	new strPara,strUserName
	
	set blnHeader = $$$YES
	
	set idIssue = ""
	for {
		set idIssue = $order(parrIssues(idIssue))
		quit:idIssue=""
		
		set objIssue = $get(^INIssue(YM,idIssue,1))
		quit:objIssue=""
		
		if blnHeader {
			set lstHeader = ""
			set lstHeader = lstHeader_$listbuild($$$StrINIssueIssueNumber)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueFromLocn)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueStatus)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueCreatedOn)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueCreatedBy)
			set lstHeader = lstHeader_$listbuild($$$StrINIssuePosted)
			
			do Start^COMTable(lstHeader,$$^WWWTEXT("IN01050"),1)    ; All Issues
			set blnHeader = $$$NO
		}
			
		set idLink = "INIssueView"
		set (strPara,idKey) = idIssue
		set idLocn = $$$INIssueFromLocn(objIssue)
		if idLocn'="" {
			set idLocn = idLocn _" ("_$$$WWW0121LocationName($get(^WWW0121(0,YM,idLocn,1)))_")"
		}
		set idStatus = $$$INIssueStatus(objIssue)
		if idStatus'="" {
			set idStatus = idStatus _" ("_$$GetDescription^WWWStatus("INIssue",idStatus,SPRACHE)_")"
		}
		set idUser = $$$INIssueCreatedBy(objIssue)                 ; SR17085 vvv
		if idUser'=""           set strUserName = $$$WWW013Name($get(^WWW013(0,idUser,1)))
		if $get(strUserName)="" set strUserName = idUser
		
		do NewLine^COMTable()
		do InsertCell^COMTable(    idIssue,                      idLink,idKey,strPara)
		do InsertCell^COMTable(    idLocn,                       idLink,idKey,strPara)
		do InsertCell^COMTable(    idStatus,                     idLink,idKey,strPara)
		do InsertCellDate^COMTable($$$INIssueCreatedOn(objIssue),idLink,idKey,strPara)
		do InsertCell^COMTable(    strUserName,                  idLink,idKey,strPara)
		do InsertCellDate^COMTable($$$INIssuePosted(objIssue),   idLink,idKey,strPara)
	}
	
	if 'blnHeader do Stop^COMTable()
	
	quit
	
	
ShowReversals(&parrReverse)
	;-------------------------------------------------------------------------------
	; Display a list of Req's at various stages.
	;
	; Called By : ShowIssuedTable
	;
	; History:
	; 23-Dec-2009	GRF		SR17085: Improve security with user name rather than id
	; 22-Dec-2009	GRF		SR17085: Created from ShowRequistions
	;-------------------------------------------------------------------------------
	new blnHeader,idRevDoc,idKey,idLink,idLocn,idStatus,idUser,objReverse
	new lstHeader,strPara,strUserName
	
	set blnHeader = $$$YES
	
	set idRevDoc = ""
	for {
		set idRevDoc = $order(parrReverse(idRevDoc))
		quit:idRevDoc=""
		
		set objReverse = $get(^INIssueRev(0,idRevDoc,1))
		quit:objReverse=""
		
		if blnHeader {
			set lstHeader = ""
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevNoDocument)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevIssueNumber)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevFromLocn)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevStatus)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevCreatedOn)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevCreatedBy)
			set lstHeader = lstHeader_$listbuild($$$StrINIssueRevPosted)
			
			do Start^COMTable(lstHeader,$$^WWWTEXT("IN01179"),1)    ; "All Returns"
			set blnHeader = $$$NO
		}
		
		set idLink = "INIssueRev"
		set (strPara,idKey) = idRevDoc
		set idLocn = $$$INIssueRevFromLocn(objReverse)
		if idLocn'="" {
			set idLocn = idLocn _" ("_$$$WWW0121LocationName($get(^WWW0121(0,YM,idLocn,1)))_")"
		}
		set idStatus = $$$INIssueRevStatus(objReverse)
		if idStatus'="" {
			set idStatus = idStatus _" ("_$$GetDescription^WWWStatus("INIssue",idStatus,SPRACHE)_")"
		}
		set idUser = $$$INIssueRevCreatedBy(objReverse)                 ; SR17085 vvv
		if idUser'=""           set strUserName = $$$WWW013Name($get(^WWW013(0,idUser,1)))
		if $get(strUserName)="" set strUserName = idUser
		
		do NewLine^COMTable()
		do InsertCell^COMTable(    idRevDoc,                            idLink,idKey,strPara)
		do InsertCell^COMTable(    $$$INIssueRevIssueNumber(objReverse),idLink,idKey,strPara)
		do InsertCell^COMTable(    idLocn,                              idLink,idKey,strPara)
		do InsertCell^COMTable(    idStatus,                            idLink,idKey,strPara)
		do InsertCellDate^COMTable($$$INIssueRevCreatedOn(objReverse),  idLink,idKey,strPara)
		do InsertCell^COMTable(    strUserName,                         idLink,idKey,strPara)
		do InsertCellDate^COMTable($$$INIssueRevPosted(objReverse),     idLink,idKey,strPara)
	}
	
	if 'blnHeader do Stop^COMTable()
	
	quit
	
]]></Routine>
</Export>