<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPARStatementPayment" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPARStatementPayment
	#include COMSYS
	#include FINConst
 
DiscountAmount(pidCompany,pidBank,pidBankLine)
    /*-------------------------------------------------------------------------------
    ; Get the amount of discount on a customer banking line.
    ;
    ; Params: pidCompany		: Company Number
    ; 		  pidBank			: Banking Number
    ; 		  pidBankLine		: Banking Line Number
    ;
    ; ByRefs:
    ;
    ; Returns: float currency
    ;
    ; History:
    ; 06-Jan-2006	Steve S		SR14140: Created
    ;-------------------------------------------------------------------------------*/
	new curAmount,YM,idInvoice,objBankInvoice
 
	set curAmount=0
	
	if ($get(pidCompany)'="")&&($get(pidBank)'="")&&($get(pidBankLine)'="") {
		set YM=pidCompany
		set idInvoice=""
		for {
			set idInvoice=$order(^FINARBankInvoice(YM,pidBank,pidBankLine,idInvoice))
			quit:(idInvoice="")
			
			set objBankInvoice=$get(^FINARBankInvoice(YM,pidBank,pidBankLine,idInvoice,1))
			
			continue:('$$$FINARBankInvoicePay(objBankInvoice))
			continue:('$$$FINARBankInvoiceDiscountApplied(objBankInvoice))
			
			set curAmount=curAmount+$$$FINARBankInvoiceDiscount(objBankInvoice)
		}	
	}
 
	quit curAmount
	
	
TotalPayment(pidCompany,pidBank,pidBankLine)
    /*-------------------------------------------------------------------------------
    ; Get the total payment amount on a banking line.
    ;
    ; Params: pidCompany		: Company Number
    ; 		  pidBank			: Banking Number
    ; 		  pidBankLine		: Banking Line Number
    ;
    ; ByRefs:
    ;
    ; Returns: float currency
    ;
    ; History:
    ; 06-Jan-2006	Steve S		SR14140: Created
    ;-------------------------------------------------------------------------------*/
	new curAmount,YM,objBankLine,curPayment
 
	set curAmount=0
	
	if ($get(pidCompany)'="")&&($get(pidBank)'="")&&($get(pidBankLine)'="") {
		set YM=pidCompany
		set objBankLine=$get(^FINARBankLine(YM,pidBank,pidBankLine,1))
		set curPayment=$$$FINARBankLineTotal(objBankLine)
		set curAmount=curPayment+$$DiscountAmount(pidCompany,pidBank,pidBankLine)	
	}
 
	quit curAmount
	
	
IncludeBanking(pdteBank,pdteStatement)
    /*-------------------------------------------------------------------------------
    ; Determines whether or not a banking record should be included on a
    ; statement run, given the banking date and statement date.
    ;
    ; Params: pdteBank		: The Banking Date
    ; 		  pdteStatement	: The Statement Date
    ;
    ; ByRefs:
    ;
    ; Returns: boolean
    ;
    ; History:
    ; 18-Aug-2006	PO			SR14549: Included alternative using macros, but if just checking months are the same then do just that.
    ; 06-Jan-2006	Steve S		SR14140: Created
    ;-------------------------------------------------------------------------------*/
	//new dteMonthStart,dteMonthEnd
	new intABSBankMonth,intABSStatementMonth
 
	if $ascii(pdteBank)=0 set pdteBank=""
	
	set intABSBankMonth = ($$$DateYear(pdteBank)*12) + $$$DateMonth(pdteBank)
	set intABSStatementMonth = ($$$DateYear(pdteStatement)*12) + $$$DateMonth(pdteStatement)
	
	quit (intABSBankMonth = intABSStatementMonth)
	
	//set dteMonthStart=$$GetStartOfMonth^COMUtilDate(pdteStatement)
	//set dteMonthEnd=$$GetEndOfMonth^COMUtilDate(pdteStatement)
	//quit $$WithinDateRange^COMUtilDate(pdteBank,dteMonthStart,dteMonthEnd)
 
	//set dteMonthStart = $$$DateFirstDayOfMonth(pdteStatement)
	//set dteMonthEnd = $$$DateLastDayOfMonth(pdteStatement)
	//quit $$$DateWithinRange(pdteBank,dteMonthStart,dteMonthEnd)
	
StatementLineDesc(pidCompany,pidCust,pidInv,pdteDate,pidLang)
    /*-------------------------------------------------------------------------------
    ; Get a description for the statement line
    ;
    ; Params: pidCompany		: Company Number (YM)
    ; 		  pidCust			: Customer Number
    ; 		  pidInv			: Invoice Number
    ; 		  pdteDate			: Statement Date
    ; 		  pidLang			: Language to use
    ;
    ; ByRefs:
    ;
    ; Returns: String
    ;
    ; History:
    ; 06-Jan-2006	Steve S		SR14140: Created
    ;-------------------------------------------------------------------------------*/
	new strDesc,YM,objFINARInvoice,strDetail,curBalance
 
	set strDesc=""
	
	set YM=$get(pidCompany)
	
	if (YM'="")&&($get(pidCust)'="")&&($get(pidInv)'="")&&($get(pidLang)'="") {
		set objFINARInvoice=$get(^FINARInvoice(YM,pidCust,pidInv,1))
		set strDetail=$$$FINARInvoiceDetail(objFINARInvoice)
		set curBalance=$$GetBalanceAtDate^FINARInvoice(pidCust,pidInv,pdteDate,YM)
		
		if (+curBalance>=0) {
			set strDesc=$$^WWWTEXT("Fin00433",,,pidLang) ;Invoice
		} else {
			set strDesc=$$^WWWTEXT("Fin00935",,,pidLang) ;Credit Invoice
		}
		
		if strDetail'="" set strDesc=strDesc_" ("_strDetail_")"
	}
	
	quit strDesc
 
]]></Routine>
</Export>