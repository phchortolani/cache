<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEAUFVOR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEAUFVOR
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PRÜFEN UND SETZEN WERTE IN INWEAUFVOR
	;	DO ^INWEAUFVOR - EXECUTE NACH BUTTONLEISTE  -> EINSPRUNG AUS INWEAUFVORA
	;
	; Inputs : 
	;
	;
	; ByRef :		YKEY
	; 				YFELD
	;
	; Returns :
	;
	;
	; History :
	; 03-Aug-2009	GRF		SR16602: Macros
	; 26-Jun-2006	GRF		SR14742: Doco; !=>||
	; 24-Mar-2006	JW		SR14422: Encapsulated code
	; 22-Sep-2005	GRF		SR12897 : Doco
	; 08-Jun-2005	shobby	Set up a default country of origin from Service Costs.
	; 13.05.2004	WEM		Created  
	;-------------------------------------------------------------------------------
	QUIT:$GET(YFELD)=""
	
	SET YKEY  = $GET(YKEY)
	set YFELD = $$GetDefaults(.YKEY)
	
	quit
	
	
GetDefaults(&YKEY,BETRIEB="",LAGERPLATZ="") 
	;-------------------------------------------------------------------------------
	; Get defaults for a INWEAUFVOR record.
	;
	; Params:	BETRIEB 	- (optional) location
	; 			LAGERPLATZ 	- (optional) stock location
	;
	; ByRefs:	YKEY - pass in with first two keys set (order and line)
	;
	; Returns:
	;
	; History:
	; 22-Mar-2006	JW		SR14422: Created. Encapsulated code from ^INWEAUFVOR
	;-------------------------------------------------------------------------------
	new objDefaults,idOrder,idOrderLine,idItem,WEDAT
	
	set idOrder     = $$$KEY1(YKEY)
	set idOrderLine = $$$KEY2(YKEY)
	
	quit:(idOrder="")||(idOrderLine="") ""
	
	set $$$INWEAUFSupplier(YFELD) = $$$INAUFPSupplier($GET(^INAUFP(YM,idOrder,idOrderLine,1)))
	
	set objDefaults = $$GetDefaults^INWEAUF1(idOrder,idOrderLine,.BETRIEB,.LAGERPLATZ,.WEDAT,$$$YES)
	
	set idItem = $$$INAUFPItemNumber($get(^INAUFP(YM,idOrder,idOrderLine,1)))
	if idItem'="" {
		set WEDAT = $$Increment^INWECounter(idItem,BETRIEB,LAGERPLATZ,WEDAT)
	}
	
	set $$$KEY3(YKEY) = BETRIEB
	set $$$KEY4(YKEY) = LAGERPLATZ
	set $$$KEY5(YKEY) = WEDAT
	
	set $$$INWEAUFVersionRev(YFELD)           = $$$INWEAUFVersionRev(objDefaults)
	set $$$INWEAUFExpirationDate(YFELD)       = $$$INWEAUFExpirationDate(objDefaults)
	set $$$INWEAUFIncomingGoodsFlags(YFELD)   = $$$INWEAUFIncomingGoodsFlags(objDefaults)
	set $$$INWEAUFUnitPrice(YFELD)            = $$$INWEAUFUnitPrice(objDefaults)
	set $$$INWEAUFQuantityUnit(YFELD)         = $$$INWEAUFQuantityUnit(objDefaults)
	set $$$INWEAUFPriceForeignCurrency(YFELD) = $$$INWEAUFPriceForeignCurrency(objDefaults)
	set $$$INWEAUFExchangeRate(YFELD)         = $$$INWEAUFExchangeRate(objDefaults)
	set $$$INWEAUFFinanceTxRef(YFELD)         = $$$INWEAUFFinanceTxRef(objDefaults)
	set $$$INWEAUFCountryOfOrigin(YFELD)      = $$$INWEAUFCountryOfOrigin(objDefaults)    ;SR12408
	set $$$INWEAUFCDV(YFELD)                  = $$$INWEAUFCDV(objDefaults)				  ;SR12408
	
	if +$$$INWEAUFIncomingGoodsQuantity(objDefaults)'=0 {
		set $$$INWEAUFIncomingGoodsQuantity(YFELD) = $$$INWEAUFIncomingGoodsQuantity(objDefaults)
	}
	
	quit YFELD
	
	
SET   ; EXECUTE NACH ONBLUR BEI AUFTRAG UND POSITION ;EXECUTE within next to order And 
	;
	; FIXME : Find where called and change to a subroutine name that can be searched on! <GRF>
	;
	NEW YFELD,YKEY,EXIST,YKEYX,EVENT,NKOMMA
	
	SET YKEY     = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1))
	SET YKEYX    = YKEY
	SET BETRIEBX = $$$KEY3(YKEY)
	SET YFELD    = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1))
	IF ($$$KEY1(YKEY)="") || ($$$KEY2(YKEY)="") QUIT
	IF '$DATA(^INAUFP(YM,$$$KEY1(YKEY),$$$KEY2(YKEY)))                 SET %TXT(1)="#Y"_YFORM_"P1~"_"#Y"_YFORM_"P2~"_"#!"_$$^WWWTEXT(119,,1)   QUIT  ;DATENSATZ NICHT VORHANDEN ;data record Not on hand 
	IF $PIECE($GET(^INAUFP(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1)),Y,83)="" SET %TXT(1)="#Y"_YFORM_"P1~"_"#Y"_YFORM_"P2~"_"#!"_$$^WWWTEXT(33697,,1) QUIT  ;BESTELLUNG NOCH NICHT GEDRUCKT ;sales order yet Not 
	
	DO INWEAUFVOR
	IF $DATA(^INWEAUFVOR(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),$$$KEY3(YKEY),$$$KEY4(YKEY),$$$KEY5(YKEY))) DO
	. SET $$$KEY4(YKEY) = ""
	. SET $$$KEY5(YKEY) = ""
	
	IF $$$KEY3(YKEYX)'="" SET $$$KEY3(YKEY) = $$$KEY3(YKEYX)
	IF $$$KEY4(YKEYX)'="" SET $$$KEY4(YKEY) = $$$KEY4(YKEYX)
	IF $$$KEY5(YKEYX)'="" SET $$$KEY5(YKEY) = $$$KEY5(YKEYX)
	
	SET %TXT(1)=""
	IF $$$KEY3(YKEY)'="" SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"P3~"_$$$KEY3(YKEY)
	IF $$$KEY4(YKEY)'="" SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"P4~"_$$$KEY4(YKEY)
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"P5~"_$$^WWWDATE($$$KEY5(YKEY))
	IF +$PIECE(YFELD,Y,4)'=0  SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D4~"_$$^WWWZAHL($PIECE(YFELD,Y,4),0,$LENGTH($PIECE($PIECE(YFELD,Y,4),".",2)))
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D7~"_$PIECE(YFELD,Y,7)
	IF $PIECE(YFELD,Y,7)=""   SET %TXT(1) = %TXT(1)_"#DUMMYD7~"
	IF $PIECE(YFELD,Y,7)'=""  SET %TXT(1) = %TXT(1)_"#DUMMYD7~"_$PIECE($GET(^INLIEF(YM,$PIECE(YFELD,Y,7),1)),Y,8)
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D10~"_$PIECE(YFELD,Y,10)
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D18~"_$PIECE(YFELD,Y,18)               ;MENGENEINHEIT
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D25~"_$$^WWWDATE($PIECE(YFELD,Y,25))
	SET NKOMMA=2
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D110~"_$$^WWWZAHL($PIECE(YFELD,Y,110),0,NKOMMA)
	SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D111~"_$$^WWWTR(0,12,$PIECE(YFELD,Y,111))
	IF +$PIECE(YFELD,Y,8)'=0   SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D8~"_$$^WWWTR(0,8,$PIECE(YFELD,Y,8))
	IF $PIECE(YFELD,Y,115)'="" SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D115~"_$PIECE(YFELD,Y,115)
	DO
	. NEW INFO,MENGE,EINHEIT,ARTIKEL
	. SET MENGE=$$MENGE(YKEY)
	. IF MENGE'=""   SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"M106~"_MENGE    ;BESTELLTE MENGE ANZEIGEN ;quantity display 
	. SET INFO=$$INFO(YKEY)
	. IF $PIECE(YFELD,Y,115)'="" SET INFO=$$INFO1()_$CHAR(13)_INFO
	. IF INFO'=""    SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"M107~"_INFO     ;WEITERE INFOS ANZEIGEN ;display 
	. SET ARTIKEL=$$ARTIKEL(YKEY)
	. IF ARTIKEL'="" SET %TXT(1) = %TXT(1)_"#Y"_YFORM_"D108~"_ARTIKEL  ;ARTIKEL ANZEIGEN ;item display 
	
	IF $$$KEY1(YKEY)'="" IF $$$KEY2(YKEY)'="" IF $$$KEY3(YKEY)'="" IF $$$KEY4(YKEY)'="" IF $$$KEY5(YKEY)'="" DO
	. SET %TXT(1) = %TXT(1)_"#FUNCTION~"_"document."_YHTMFORM_".Y"_YFORM_"D4.focus();"  ;FOCUS AUF MENGENFELD
	
	QUIT
	
INFO(YKEY)	  ;SHOW ADDITIONAL RECEIVING INFORMATION
	;-------------------------------------------------------------------------------
	; History:
	; 10-Apr-2006	JW		SR14421: Removed check. Not necessary as date increments
	;-------------------------------------------------------------------------------
	NEW INFO,SATZ,YQ
	
	QUIT:$$$KEY1($get(YKEY))="" ""  ;AUFTRAG ;order 
	QUIT:$$$KEY2($get(YKEY))="" ""  ;POSITION
	
	SET AUF = $$$KEY1(YKEY)
	SET POS = $$$KEY2(YKEY)
	SET INFO=""
	SET SATZ=$GET(^INAUFP(YM,AUF,POS,1))
	
	;DO CHECK^INWEAUFVORS(YKEY)	//SR14421
	
	IF $GET(YQ)'="" SET INFO=$$^WWWTEXT(185)_" !"_$CHAR(13)_$EXTRACT(YQ,2,999) QUIT INFO  ;BEREITS ERFASST ;yet 
	IF $DATA(^INWEAUF(YM,AUF,POS)) DO         ;BEREITS EINGELAGERT ;yet 
	. IF INFO'="" SET INFO=INFO_$CHAR(13)
	. SET INFO=INFO_$$^WWWTEXT(33875,,1)_":"  ;bereits erfasste we's ;yet 
	. NEW BET,LAP,WED,WE1
	. SET BET=""
	. FOR  SET BET=$ORDER(^INWEAUF(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . SET LAP=""
	. . FOR  SET LAP=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . SET WED=""
	. . . FOR  SET WED=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . SET WE1=$GET(^INWEAUF(YM,AUF,POS,BET,LAP,WED,1))
	. . . . SET INFO=INFO_$CHAR(13)           ;bereits erfasste we's ;yet 
	. . . . SET INFO=INFO_$$^WWWTEXT(32278,,1)_": "_BET_" - "_LAP_" ("_$$^WWWDATE(WED)_") "_$$^WWWTEXT(31407,,1)_": "_$PIECE(WE1,Y,4)  ;WE: xxx MENGE: n ;substitute product 
	
	IF $PIECE(SATZ,Y,4)'="" IF $PIECE($GET(^INART(YM,$PIECE(SATZ,Y,4),1)),Y,56)=1 DO  ;ARTIKEL MIT SERIENNUMMER ;item by means of 
	. IF INFO'="" SET INFO=INFO_$CHAR(13)
	. SET INFO=INFO_$$^WWWTEXT(32209,,1)_": "  ;ACHTUNG SERIENNUMMER ERFASSEN ;estimation Edit 
	
	IF +$PIECE(SATZ,Y,262)'=0 DO               ;URSPRÜNGLICHE MENGE (VOR SPLITTING) ;quantity 
	. IF INFO'="" SET INFO=INFO_$CHAR(13)
	. SET INFO=INFO_$$^WWWTEXT(33023,,1)_": "  ;GESAMTANZAHL AUS SPLITTING ;out of 
	. IF '$FIND($PIECE(SATZ,Y,262),".") SET INFO=INFO_$PIECE(SATZ,Y,262) QUIT
	. SET INFO=INFO_$$^WWWZAHL($PIECE(SATZ,Y,262),0,$LENGTH($PIECE($PIECE(SATZ,Y,262),".",2)))
	
	QUIT INFO
	
INFO1() ;RETURN COST INFORMATIONS
	NEW RETVAL
	
	SET RETVAL=""
	IF $PIECE($get(YFELD),Y,115)'="" IF $FIND(YFELD,"%3B") DO
	. NEW YI
	. SET RETVAL=RETVAL_$$^WWWFELDNAME("INSHIPINFOCOSTS","P",2)_":"
	. FOR YI=1:1  QUIT:$PIECE($PIECE(YFELD,Y,115),"%3B",YI,YI+99)=""  DO
	. . SET YI(1) = $PIECE($PIECE($PIECE(YFELD,Y,115),"%3B",YI),"%7C",1)
	. . SET YI(2) = $PIECE($PIECE($PIECE(YFELD,Y,115),"%3B",YI),"%7C",2)
	. . IF (YI(1)="") || (+YI(2)=0) QUIT  ;NO SERVICE COST
	. . SET RETVAL=RETVAL_$CHAR(13)
	. . SET RETVAL=RETVAL_$PIECE($GET(^INPARA(YM,"KOSTENARTEN",SPRACHE,YI(1),1)),Y,1)
	. . SET RETVAL=RETVAL_": "
	. . SET RETVAL=RETVAL_$$^WWWZAHL(YI(2),0,2)  ;_" "_$PIECE($GET(^WWWWAE(0,YWHR,1)),Y,2)
	
	QUIT RETVAL
	
ARTIKEL(YKEY)
	NEW ARTIKEL,objLine
	
	QUIT:$$$KEY1($get(YKEY))="" ""  ;AUFTRAG ;order 
	QUIT:$$$KEY2($get(YKEY))="" ""  ;POSITION
	
	set objLine = $get(^INAUFP(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1))
	
	SET ARTIKEL=$PIECE(objLine,Y,4)  ;ARTIKEL ;item 
	IF ARTIKEL'="" DO
	. IF $PIECE(objLine,Y,1)'="" SET ARTIKEL=ARTIKEL_" "_$PIECE(objLine,Y,1) QUIT
	. SET ARTIKEL=ARTIKEL_" "_$$^INARTNAME(ARTIKEL)
	
	QUIT ARTIKEL
	
MENGE(YKEY)
	NEW MENGE,EINHEIT
	
	QUIT:$$$KEY1($get(YKEY))="" ""  ;AUFTRAG ;order 
	QUIT:$$$KEY2($get(YKEY))="" ""  ;POSITION
	
	SET MENGE=$PIECE($get(^INAUFP(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1)),Y,5)     ;BESTELLTE MENGE ;quantity 
	SET MENGE=$$^WWWZAHL(MENGE,0,$LENGTH($PIECE(MENGE,".",2)))
	SET EINHEIT=$PIECE($GET(^INAUFP(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1)),Y,40)  ; EINHEIT ;unit 
	IF EINHEIT'="" SET EINHEIT=$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,EINHEIT,1)),Y,1)
	IF +MENGE=0 SET MENGE=""
	IF MENGE'="" IF EINHEIT'="" SET MENGE=MENGE_" "_EINHEIT
	QUIT MENGE
	
]]></Routine>
</Export>