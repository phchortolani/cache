<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INERECH1SPLIT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INERECH1SPLIT ;INERECH1SPLIT;FIS;AUFTRAGSPOSITION SPLITTEN ;14.01.2005  ; Compiled January 31, 2005 10:56:10
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUFTRAGSPOSITION SPLITTEN 
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 10-Jan-2007	GRF		Doco; quits
	; 14.01.2005	FIS
	;-------------------------------------------------------------------------------
	;	ML		FESTGELEGTE MENGE ;quantity 
	;	MG		GESAMT MENGE      ;quantity 
	;	MR		REST MENGE        ;quantity 
	;	POS1	DATENSATZ VON AUFTRAGPOSITION VOR SPLITTEN
	;-------------------------------------------------------------------------------
	NEW AUF,BET,LAG,LIEF,MG,ML,MNGU,MR,NETTO,POS,POS1,POSNEU,RECH,WED,YI,YQ
	
	; 32927 "Please Add And Save Data First"    ;Bitte zuerst Daten erfassen und speichern
	SET AUF  = $PIECE($GET(YKEY),",",1)       IF AUF=""  DO ^WWWINFO($$^WWWTEXT(32927)) QUIT 
	SET POS  = $PIECE($GET(YKEY),",",2)       IF POS=""  DO ^WWWINFO($$^WWWTEXT(32927)) QUIT
	SET LIEF = $PIECE($GET(YKEY),",",3)       IF LIEF="" DO ^WWWINFO($$^WWWTEXT(32927)) QUIT
	SET RECH = $PIECE($GET(YKEY),",",4)       IF RECH="" DO ^WWWINFO($$^WWWTEXT(32927)) QUIT
	
	SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	IF YFORM="INERECH1" SET RECH1=$GET(^INERECH1(YM,AUF,POS,LIEF,RECH,1))
	IF YFORM="INAUFPA"  SET RECH1=$GET(^INAUFPA(YM,AUF,POS,LIEF,RECH,1))
	SET MG=$PIECE(POS1,Y,5)    ;MENGE GESAMT
	SET ML=$PIECE(RECH1,Y,16)  ;RECHNUNGSMENGE
	DO
	. NEW FACTOR
	. SET FACTOR=$PIECE($GET(^INAUFPK(YM,AUF,POS,1)),Y,41)  ;UMRECHNUNGSFAKTOR
	. IF +FACTOR'=0 SET ML=$JUSTIFY(ML/FACTOR,0,0)
	
	SET ML(1)=0
	IF $DATA(^INWEAUF(YM,AUF,POS)) DO
	. SET BET=""
	. FOR  SET BET=$ORDER(^INWEAUF(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . SET LAG=""
	. . FOR  SET LAG=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAG)) QUIT:LAG=""  DO
	. . . SET WED=""
	. . . FOR  SET WED=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAG,WED)) QUIT:WED=""  DO
	. . . . SET ML(1)=ML(1)+$PIECE($GET(^INWEAUF(YM,AUF,POS,BET,LAG,WED,1)),Y,4)
	
	IF ML(1)>ML SET ML=ML(1)  ;BEREITS MEHR EINGELAGERT ALS RECHNUNGEN ERFASST
	IF (+MG=0)!(+ML=0)!(MG'>ML) DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$$^WWWTEXT(31407)_")") QUIT  ;BITTE PRÜFEN (MENGE)
	SET MR=MG-ML  ;NEUE MENGE
	DO ^INSPLIT(AUF,POS,MG,ML,MR)  ;NEU TYBD 20.10.2002 ;recent 
	
	;RÜCKSPRUNG
	NEW YI
	SET YI=""
	DO
	. NEW YBACK,YKEY
	. SET YBACK=""
	. SET YI=YAKTION_"EP=WWWFORM&YFORM=INAUFP&YKEY="_AUF_","_$GET(POSNEU,POS)
	. DO VAR^WWWCGI
	
	; 32677		"Split Line Item"
	; 31407		"Quantity"
	DO ^WWWINFO($$^WWWTEXT(32677)_": "_$GET(POSNEU)_" ("_$$^WWWTEXT(31407)_": "_MR_")",0,YI)
	QUIT
]]></Routine>
</Export>