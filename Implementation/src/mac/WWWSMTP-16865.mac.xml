<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSMTP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSMTP  ; Cache WebLink Developer: SMTP Client functions ; 07 Jan 2000  4:50 PM
	; Version 4.3
	; +--------------------------------------------------------+
	; | Copyright 1986-2002 by InterSystems Corporation,       |
	; | Cambridge, Massachusetts, U.S.A.                       |
	; | All rights reserved.                                   |
	; |                                                        |
	; | Confidential, unpublished property of InterSystems.    |
	; |                                                        |
	; | This media contains an authorized copy or copies       | ;upon 
	; | of material copyrighted by InterSystems and is the     |
	; | confidential, unpublished property of InterSystems.    |
	; | This copyright notice and any other copyright notices  |
	; | included in machine readable copies must be reproduced | ;within 
	; | on all authorized copies.                              |
	; +--------------------------------------------------------+
	;----------------------------------------------------------------------------------
	; CHANGE MARKS:
	;   RT (3 MAY 2000)  -  Add HTML Mail capability to BIGSEND function ;reg.tn. HTML 
	;   RT (10 DEC 2001) -  Generate path correctly for Cache VMS ;reg.tn. 
	;----------------------------------------------------------------------------------
	Q
	
GETIP() ; Get own IP address
	N ipd,ip
	S ipd=$ZU(54,13,$ZU(54,0)) ; get ip address + domain name
	S ip=$P(ipd,",",1)
	Q ip
	
SEND(smtpdomain,from,to,subject,message,%timeout,%GMTOffset,owndomain,%smtpport,%displayfrom,%displayto) ; Send a message via SMTP ;Send via 
	N %h,%inetdate,%inettime,%date,%mess,%from,%to,%lineno,%line,%alineno,%boundary,%html,%mixed
	K %attach S %attach=0,%html=0,%mixed=0
	; Get attachment list
	S %lineno=""
	F  S %lineno=$O(message(%lineno)) Q:%lineno=""  D
	. S %line=message(%lineno)
	. I $E(%line,1,9)="##attach " S message(%lineno)=$E(%line,2,$L(%line)) Q  ; override
	. I $E(%line,1,8)="#attach " D
	. . S %alineno=$G(%alineno)+1
	. . S %attach(%alineno)=$E(%line,9,$L(%line))
	. . S %attach=1
	. . I %attach(%alineno)[".htm" S %html=1
	. . E  S %mixed=1
	. . K message(%lineno)
	S %timeout=$G(%timeout) I %timeout="" S %timeout=20
	S smtpdomain=$G(smtpdomain)
	S %smtpport=$G(%smtpport)
	S from=$G(from)
	S to=$G(to)
	S subject=$G(subject)
	S %GMTOffset=$G(%GMTOffset) I %GMTOffset="" S %GMTOffset="GMT"
	S owndomain=$G(owndomain)
	S FailureReason=""
	I smtpdomain="" S FailureReason="No SMTP Domain specified" Q 0
	I from="" S FailureReason="No sender's email address specified" Q 0
	I to="" S FailureReason="No recipient's email address specified" Q 0
	I '$D(message) S FailureReason="Message array not found" Q 0
	S %h=$H
	S %inetdate=$$DECDATE^%wlduta(%h)
	S %inettime=$$INETTIME^%wlduta(%h,%GMTOffset)
	S %date=%inetdate_" "_%inettime
	S %mess(1)="Date: "_%date
	I $G(%displayfrom)'="" D
	. S %mess(2)="From: """_%displayfrom_"""<"_from_">"
	E  D
	. S %mess(2)="From: "_from
	I $G(%displayto)'="" D
	. S %mess(3)="To: """_%displayto_"""<"_to_">"
	E  D
	. S %mess(3)="To: "_to
	S %mess(4)="Subject: "_subject
	S %lineno=4
	I %attach D
	. S %lineno=%lineno+1
	. S %mess(%lineno)="MIME-Version: 1.0"
	. S %lineno=%lineno+1
	. S %boundary="--=_38ad1cfe3313754770a039199.MFSBCHJLHS"
	. I %mixed S %mess(%lineno)="Content-Type: multipart/mixed; boundary="""_%boundary_""""
	. I %html D
	. . S %mess(%lineno)="Content-Type: multipart/alternative; boundary="""_%boundary_"""" S %lineno=%lineno+1
	. . S %mess(%lineno)="" S %lineno=%lineno+1
	. . S %mess(%lineno)="This is a multi-part message in MIME format." S %lineno=%lineno+1
	. S %lineno=%lineno+1
	. S %mess(%lineno)=""
	S %lineno=%lineno+1
	S %mess(%lineno)=""
	I %attach D
	. S %lineno=%lineno+1
	. S %mess(%lineno)="--"_%boundary
	. S %lineno=%lineno+1
	. S %mess(%lineno)="Content-Type: text/plain; charset=us-ascii"
	. S %lineno=%lineno+1
	. S %mess(%lineno)="Content-Transfer-Encoding: quoted-printable"
	. S %lineno=%lineno+1
	. S %mess(%lineno)=""
	S message=$G(message)
	I message'=""  D
	. S %ineno=%lineno+1
	. S %mess(%lineno)=message
	E  D
	. S %nlno=%lineno,%lineno=""
	. F  S %lineno=$O(message(%lineno)) Q:%lineno=""  D
	. . S %nlno=%nlno+1
	. . S %mess(%nlno)=message(%lineno)
	I %attach D
	. S %alineno="",%lineno=$ZP(%mess(""))
	. F  S %alineno=$O(%attach(%alineno)) Q:%alineno=""  D
	. . ;
	. . N %elineno
	. . ;
	. . S %afile=%attach(%alineno)
	. . S %lineno=%lineno+1
	. . S %mess(%lineno)=""
	. . S %lineno=%lineno+1
	. . S %mess(%lineno)="--"_%boundary
	. . S %lineno=%lineno+1
	. . S %mess(%lineno)="Content-Type: application/octet-stream; name="_$$GetFile(%afile)
	. . S %lineno=%lineno+1
	. . S %mess(%lineno)="Content-Transfer-Encoding: base64"
	. . S %lineno=%lineno+1
	. . S %mess(%lineno)="Content-Disposition: attachment; filename="_$$GetFile(%afile)
	. . S %lineno=%lineno+1
	. . S %mess(%lineno)=""
	. . S %sessid=+$G(%sessid)
	. . D encfil^%wldbas64(%afile,%sessid)
	. . S %elineno=""
	. . F  S %elineno=$O(^%wldwork2(%sessid,%elineno)) Q:%elineno=""  D
	. . . S %lineno=%lineno+1
	. . . S %mess(%lineno)=^%wldwork2(%sessid,%elineno)
	. ;
	. S %lineno=%lineno+1
	. S %mess(%lineno)=""
	. S %lineno=%lineno+1
	. S %mess(%lineno)="--"_%boundary_"--"
	S %from="<"_from_">"
	S %to="<"_to_">"
	S %sent=$$smtp(smtpdomain,%from,%to,.%mess,%timeout,owndomain,%smtpport,.%attach)
	I %attach K ^%wldwork(%sessid),^%wldwork2(%sessid)
	Q %sent
	
smtp(smtpdomain,from,to,message,%timeout,owndomain,smtpport,%attach) ; raw SMTP client
	N dev,X,rcpt,mess,line,resp,error,port
	S %zt=$ZT
	K %smtpdialog
	S owndomain=$G(owndomain)
	I owndomain="",$$OS^%wldhfs()["CACHE" D
	. S owndomain=$$GETIP()
	S $ZT="smtperr"
	S port=$G(smtpport) I port="" S port=25
	S dev="|TCP|"_port,CRLF=$C(13,10)
	C dev ; just in case ;just now within 
	O dev:smtpdomain:%timeout E  S FailureReason="Failed to connect to smtp server" G FAIL
	U dev R resp:%timeout E  S FailureReason="Failed to initiate connection with smtp server" G FAIL
	D ADD2DIALOG(resp)
	U dev W "HELO ",owndomain,CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp G FAIL
	U dev W "MAIL FROM: ",from,CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp G FAIL
	S error=0
	S to=$G(to) 
	I to'="" D
	. D TO(to)
	E  D
	. S rcpt=""
	. F  S rcpt=$O(to(rcpt)) Q:rcpt=""  D
	. . D TO(rcpt)
	I error G FAIL
	U dev W "DATA",CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250",resp'["354" S FailureReason=resp G FAIL
	S mess=$G(message)
	I mess'="" D MESS(mess)
	E  D
	. S line=""
	. F  S line=$O(message(line)) Q:line=""  D
	. . S mess=message(line)
	. . D MESS(mess)
	U dev W ".",CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp G FAIL
	U dev W "QUIT",CRLF,! R resp:0
	D ADD2DIALOG(resp)
	C dev
	Q 1
	
FAIL ;
	c dev
	Q 0
	
OK ;
	c dev
	Q 1
	
smtperr ; error has occurred
	D ^%wldet
	S FailureReason="An Application error occurred - "_$ZE
	S $ZT=%zt
	Q 0
	
TO(to) ;
	U dev W "RCPT TO: ",to,CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp,error=1 Q
	Q
	
MESS(line) ;
	N %buf,%p1
	S %buf=$G(line)
	I %buf="" D  Q
	. u dev W CRLF,!
	F  Q:%buf=""  D
	. S %p1=$E(%buf,1,254),%buf=$E(%buf,255,$L(%buf))
	. I $E(%p1)="." S %p1="."_%p1
	. I $L(%p1) U dev W %p1,!
	U dev W CRLF,!
	Q
	
ADD2DIALOG(line) ;
	N %lineno
	S %lineno=$ZP(%smtpdialog(""))+1
	S %smtpdialog(%lineno)=line
	Q
	
GetFile(path) ; extract filename from path
	N %path,%file
	I $$OS^%wldhfs()="CACHEVMS" D
	. S %path=$TR(path,"]","/")
	E  D
	. S %path=$TR(path,"\","/") ; normalise for Unix and Windows
	S %path=$RE(%path)
	S %file=$P(%path,"/",1)
	S %file=$RE(%file)
	Q %file
	
BIGSEND(smtpdomain,from,to,subject,%timeout,%GMTOffset,owndomain,%smtpport,%displayfrom,%displayto) ; Send a message via SMTP ;Send via 
	; Message must be put in the global ^wldSMTP(LineNo)=Line_Of_Text
	; Attachment list is in the global ^wldAttach(LineNo)=path/filename
	N %h,%inetdate,%inettime,%date,%mess,%from,%to,%alineno,%attach,%sent
	S %alineno=$ZP(^wldAttach(""))
	S %attach=+%alineno
	S %timeout=$G(%timeout) I %timeout="" S %timeout=20
	S smtpdomain=$G(smtpdomain)
	S %smtpport=$G(%smtpport)
	S from=$G(from)
	S to=$G(to)
	S subject=$G(subject)
	S %GMTOffset=$G(%GMTOffset) I %GMTOffset="" S %GMTOffset="GMT"
	S owndomain=$G(owndomain)
	; Get attachment list from ^wldSMTP if defined there, and transfer to ^wldAttach
	S %lineno=""
	F  S %lineno=$O(^wldSMTP(%lineno)) Q:%lineno=""  D
	. S %line=^wldSMTP(%lineno)
	. I $E(%line,1,9)="##attach " S ^wldSMTP(%lineno)=$E(%line,2,$L(%line)) Q  ; override
	. I $E(%line,1,8)="#attach " D
	. . S %alineno=$G(%alineno)+1
	. . S ^wldAttach(%alineno)=$E(%line,9,$L(%line))
	. . S %attach=1
	. . K ^wldSMTP(%lineno)
	S FailureReason=""
	I smtpdomain="" S FailureReason="No SMTP Domain specified" Q 0
	I from="" S FailureReason="No sender's email address specified" Q 0
	I to="" S FailureReason="No recipient's email address specified" Q 0
	I '$D(^wldSMTP) S FailureReason="Warning: no message has been defined in ^wldSMTP"
	S %h=$H
	S %inetdate=$$DECDATE^%wlduta(%h)
	S %inettime=$$INETTIME^%wlduta(%h,%GMTOffset)
	S %date=%inetdate_" "_%inettime
	S %mess(1)="Date: "_%date
	I $G(%displayfrom)'="" D
	. S %mess(2)="From: """_%displayfrom_"""<"_from_">"
	E  D
	. S %mess(2)="From: "_from
	I $G(%displayto)'="" D
	. S %mess(3)="To: """_%displayto_"""<"_to_">"
	E  D
	. S %mess(3)="To: "_to
	S %mess(4)="Subject: "_subject
	;S %mess(5)=""
	S %from="<"_from_">"
	S %to="<"_to_">"
	S %sent=$$bigsmtp(smtpdomain,%from,%to,.%mess,%timeout,owndomain,%smtpport,%attach)
	Q %sent
	
bigsmtp(smtpdomain,from,to,message,%timeout,owndomain,smtpport,%attach) ; raw SMTP client
	N dev,X,rcpt,mess,line,resp,error,port,%boundary,%afile,%alineno,CRLF
	N %zt,%sessid
	S %attach=+$G(%attach)
	S %zt=$ZT
	K %smtpdialog
	S owndomain=$G(owndomain)
	I owndomain="",$$OS^%wldhfs()["CACHE" D
	. S owndomain=$$GETIP()
	S $ZT="smtperr"
	S port=$G(smtpport) I port="" S port=25
	S dev="|TCP|"_port,CRLF=$C(13,10)
	C dev ; just in case ;just now within 
	O dev:smtpdomain:%timeout E  S FailureReason="Failed to connect to smtp server" G FAIL
	U dev R resp:%timeout E  S FailureReason="Failed to initiate connection with smtp server" G FAIL
	D ADD2DIALOG(resp)
	U dev W "HELO ",owndomain,CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp G FAIL
	U dev W "MAIL FROM: ",from,CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp G FAIL
	S error=0
	S to=$G(to) 
	I to'="" D
	. D TO(to)
	E  D
	. S rcpt=""
	. F  S rcpt=$O(to(rcpt)) Q:rcpt=""  D
	. . D TO(rcpt)
	I error G FAIL
	U dev W "DATA",CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250",resp'["354" S FailureReason=resp G FAIL
	S line=""
	F  S line=$O(message(line)) Q:line=""  D
	. S mess=message(line)
	. D MESS(mess)
	I %attach D
	. S mess="MIME-Version: 1.0" D MESS(mess)
	. S %boundary="--=_38ad1cfe3313754770a039199.MFSBCHJLHS"
	. S mess="Content-Type: multipart/mixed; boundary="""_%boundary_"""" D MESS(mess)
	. S mess="" D MESS(mess)
	S mess="" D MESS(mess)
	I %attach,$D(^wldSMTP) D
	. S mess="--"_%boundary D MESS(mess)
	. ;S mess="Content-Type: text/plain; charset="_charset D MESS(mess)
	. S mess="Content-Type: text/html; charset="_charset D MESS(mess)
	. S mess="Content-Transfer-Encoding: "_CTEncoding D MESS(mess)
	. S mess="" D MESS(mess)
	S line=""
	F  S line=$O(^wldSMTP(line)) Q:line=""  D
	. S mess=^wldSMTP(line)
	. D MESS(mess)
	I %attach D
	. S %alineno=""
	. F  S %alineno=$O(^wldAttach(%alineno)) Q:%alineno=""  D
	. . ;
	. . N %elineno
	. . ;
	. . S %afile=^wldAttach(%alineno)
	. . ;I %afile[".htm" D  Q
	. . . N suppress
	. . . S suppress=0
	. . . S %afile=$$UCSUB2^%wlduta(%afile,":notext")
	. . . I %afile[":NOTEXT" D
	. . . . S %afile=$P(%afile,":NOTEXT",1)
	. . . . S %afile=$$RTRIM^%wlduta(%afile)
	. . . . S suppress=1
	. . . . ;
	. . . S location=$$parse(.%afile,"location")
	. . . S CTEncoding=$$parse(.%afile,"CTEncoding")
	. . . S charset=$$parse(.%afile,"charset")
	. . . D Attach^%wldhtmail(%afile,dev,%boundary,suppress,location,charset,CTEncoding) ; HTML Mail attachment
	. . ;
	. . S mess="" D MESS(mess)
	. . S mess="--"_%boundary D MESS(mess)
	. . S mess="Content-Type: application/octet-stream; name="_$$GetFile(%afile) D MESS(mess)
	. . S mess="Content-Transfer-Encoding: base64" D MESS(mess)
	. . S mess="Content-Disposition: attachment; filename="_$$GetFile(%afile) D MESS(mess)
	. . S mess="" D MESS(mess)
	. . S %sessid=+$G(%sessid)
	. . D encfil^%wldbas64(%afile,%sessid)
	. . S %elineno=""
	. . F  S %elineno=$O(^%wldwork2(%sessid,%elineno)) Q:%elineno=""  D
	. . . S mess=^%wldwork2(%sessid,%elineno) D MESS(mess)
	. ;
	. S mess="" D MESS(mess)
	. S mess="--"_%boundary_"--" D MESS(mess)
	U dev W ".",CRLF,! R resp
	D ADD2DIALOG(resp)
	I resp'["250" S FailureReason=resp G FAIL
	U dev W "QUIT",CRLF,! R resp:0
	D ADD2DIALOG(resp)
	C dev
	I %attach,$G(%sessid)'="" K ^%wldwork(%sessid),^%wldwork2(%sessid)
	Q 1
	
parse(%afile,%param) 
	N %p1,%p2,%p3
	S %p2=""
	S %param=":"_$$UPCASE^%wlduta(%param)_"="
	S %afile=$$UCSUB2^%wlduta(%afile,%param)
	I %afile[%param D
	. S %p1=$P(%afile,%param,1)
	. S %p1=$$LTRIM^%wlduta(%p1)
	. S %p2=$P(%afile,%param,2)
	. S %p3=""
	. I %p2[" :" S %p3=" :"_$P(%p2," :",2,255)
	. S %p2=$P(%p2," :",1)
	. S %afile=%p1_%p3
	. S %afile=$$RTRIM^%wlduta(%afile)
	Q %p2
]]></Routine>
</Export>