<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM7FATSearch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[WWWFORM7FATSearch
	#include COMSYS

Javascript()
	;-------------------------------------------------------------------------------
	; Javascript routines to support the FATSearch control are here..aa
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 02-Jul-2013	shobby	CORE-140: Stop highlight disappearing when pressing down key at end of list.
	; 26-Jun-2013	shobby	CORE-75:    Fixed javascript error when FATSearch is not in a grid and no results are returned.
	; 13-Jun-2013	shobby	CORE-79.10: Improved positioning of FATLists.
	; 30-May-2013	shobby	CORE-102:  FATGetTop to position list above text field in not enough room below.
	; 15-May-2013	shobby	CORE-79.6: Couldn't click on dropdown when FixedHeader form was scrolled (eg INReq)
	; 15-May-2013	shobby	CORE-79.5: Positioning of dropdown was covering cell in Firefox.	
	; 15-May-2013	shobby	CORE-79.4: Width of highlight line extended to full width of dropdown. 
	; 14-May-2013	shobby	CORE-79.3: FATListCreate: Improved sizing of drop down list.
	; 09-Apr-2013	shobby	CORE-75.8: Positioning of dropdown list when form is of type 'fixed header'
	; 08-Apr-2013	shobby	CORE-75.6: FATListCreate: Make sure drop down is on top of other FATSearch
	; 08-Apr-2013	shobby	CORE-75.5: FATListSelectLine: Corrected Javascript error.
	; 08-Apr-2013	shobby	CORE-75.4: FATListCreate: Removed .... from 'more' text.
	; 05-Apr-2013	shobby	CORE-75.2: FATIsIn needs to take in to account scroll position
	;								   of document.
	; 28-Mar-2013	shobby	CORE-75: Changes to work with Firefox.
	; 07-Nov-2011	shobby	SR17725: Created.
	;-------------------------------------------------------------------------------
	&js<
		var YFATTIMEOUT;
		var YHIGHLIGHTLINE;
		var YVALUE;
		var YSTATE;
		var YMANDATORY;
		
		function FATIsIn(obj) {
			var result=false;
			if (obj!=null) {  //CORE-75
				var left  =FATGetX(obj)-document.body.scrollLeft; 	//CORE-75.2
				var top   =FATGetY(obj)-document.body.scrollTop;	//CORE-75.2
				if (document.getElementById('divFixedHeader')!=undefined) {				//CORE-79.6
					left=left-document.getElementById('divFixedHeader').scrollLeft;		//CORE-79.6
					top=top-document.getElementById('divFixedHeader').scrollTop;		//CORE-79.6
				}																		//CORE-79.6
				var right =left+obj.offsetWidth;
				var bottom=top+obj.offsetHeight;
				result= !((event.clientX<left)||(event.clientX>right)||(event.clientY<top)||(event.clientY>bottom));
				//document.getElementById('DEVInfo').value=document.getElementById('DEVInfo').value+'('+left+'-'+event.clientX+'-'+right+','+top+'-'+event.clientY+'-'+bottom+')='+result+':'+document.body.scrollLeft+':'+document.body.scrollTop+':'+document.getElementById('divFixedHeader').scrollTop;
			}
			return result;
		}
		function FATClick(pid,pYFORM,pYART,pYLFN,pYMANDATORY) {
			if (FATListHidden(pid)) {
				FATSearch1(pid,pYFORM,pYART,pYLFN,pYMANDATORY);
			} else {
				FATClose(pid);
			}
			document.getElementById('Y'+pid).focus();
		}
		function FATIsDisabled(pid) {
			return document.getElementById('FATButtonTD'+pid).disabled;
		}
		
		function FATKeyDown(pid) {
			if (event.keyCode==9) {
				FATClose(pid);
			} else if ((event.keyCode==13)&&(FATListHidden(pid))) {
			} else {
				if (!FATIsDisabled(pid)) {
					if (YHIGHLIGHTLINE==undefined) {
					} else if (event.keyCode==40) {
						if (YHIGHLIGHTLINE.nextSibling!=null) {
							FATListHighlightLine(YHIGHLIGHTLINE.nextSibling);
						} else {
						}
					} else if (event.keyCode==38) {
						if (YHIGHLIGHTLINE.previousSibling!=null) {
							FATListHighlightLine(YHIGHLIGHTLINE.previousSibling);
						}
					} else if (event.keyCode==13) {
						FATListSelectLine(YHIGHLIGHTLINE,pid);
					}
					window.event.cancelBubble=true;
				}
			}
		}
		function FATKeyUp(pid,pYFORM,pYART,pYLFN,pYMANDATORY) {
			if (!FATIsDisabled(pid)) {
				if (event.keyCode==40) {
					if (FATListHidden(pid)) {
						YVALUE=999;
						FATSearch(pid,pYFORM,pYART,pYLFN,pYMANDATORY);
					}
				} else if(event.keyCode==27) {
					FATClose(pid);
					document.getElementById('Y'+pid).value=YVALUE;
				} else if ((event.keyCode!=38)&&(event.keyCode!=13)) {
					FATSearch(pid,pYFORM,pYART,pYLFN,pYMANDATORY);
				}
				window.event.cancelBubble=true;
			}
		}
		//function Control() {
		//	var obj=new Object;
		//	var id;
			
		//	obj.ListHidden =ListHidden;
		//	obj.id=id;

		//	function ListHidden(pid) {
		//		return document.getElementById('FATList'+pid)==null;
		//	}
		//	return obj;
		//}
		function FATListHidden(pid) {
			return document.getElementById('FATList'+pid)==null;
		}
		function FATOnBlur(pid) {
			if (event.type=='keydown') {  //Firefox
				FATClose(pid);
				return false;
			//} else if (FATIsIn(document.getElementById('FATButtonTD'+pid))) {
			//	return true;
			} else if (FATListHidden(pid)) {
				return false;
			} else if (!FATIsIn(document.getElementById('FATList'+pid))) {
				FATClose(pid);
				return false;
			} else {
				return true; 
			}
		}
		
		function FATDisable(pid) {
			var obj=document.getElementById(pid);
			obj.disabled=true;
			window.setTimeout(function() {obj.disabled=false},1);
		}
		function FATSearch(pid,pYFORM,pYART,pYLFN,pYMANDATORY) {
			if (YVALUE!=document.getElementById('Y'+pid).value) {
				YVALUE=document.getElementById('Y'+pid).value;
				if (YFATTIMEOUT!=null) window.clearTimeout(YFATTIMEOUT);
				YFATTIMEOUT=window.setTimeout(function() {FATSearch1(pid,pYFORM,pYART,pYLFN,pYMANDATORY);},500);
				
			}
		}
		function FATSearch1(pid,pYFORM,pYART,pYLFN,pYMANDATORY) {
			if (document.getElementById('Y'+pid)!=null) { //CORE-75
				CallBackNow('Populate^WWWFORM7FATSearch',pid,document.getElementById('Y'+pid).value,pYFORM,pYART,pYLFN,pYMANDATORY);
			}
		}
		function FATParent(obj,type) {
 			if (obj.tagName!=type) { obj=FATParent(obj.parentNode,type);}
 			return obj;
		}
		
		// --------------------  FATList -------------------------------------
		function FATListOnMouseOver() {
			FATListHighlightLine(FATParent(window.event.srcElement,'TR'));
		}
		function FATListHighlightLine(pobj) {
			if (!pobj.children[0].getAttribute('_ignore')) {  //CORE-140
				if (YHIGHLIGHTLINE!=undefined) {
					YHIGHLIGHTLINE.children[0].style.color='black';
					YHIGHLIGHTLINE.style.backgroundColor='white';
					YHIGHLIGHTLINE=undefined;
				}	
				YHIGHLIGHTLINE=pobj;
				YHIGHLIGHTLINE.children[0].style.color='white';
				//YHIGHLIGHTLINE.style.backgroundColor='midnightblue';
				YHIGHLIGHTLINE.style.backgroundColor='#336292';
			}
		}

		function FATListSelectLine(pobj,pid) {
			if ((pobj!=undefined) && (pobj._key!=undefined)) { //CORE-75.5
				var YFORM = document.getElementById('YFORM');
				var YAR   = document.getElementById('YART');
				var YLFN  = document.getElementById('YLFN');
			
				var objText  = document.getElementById('Y'+pid);
				if (objText!=undefined) {
					objText.value=pobj._key;
				//CORE-75} else {
				//CORE-75	saveData(pid,pobj._key,1);
				}
				FATClose(pid);
				objText.focus();
				objText.select();
			}
		}
		function FATOnClick(pid) {
			if (FATIsIn(document.getElementById('FATList'+pid))) {
				FATListSelectLine(YHIGHLIGHTLINE,pid);
			} else {
				if ((event.srcElement.id!='FATButtonTD'+pid)&&(event.srcElement.id!=('FATSEARCH'+pid+'IMG'))) {
					if (isFF()) {
						if (document.getElementById('FATList'+pid)!=null) {
							if (!FATIsIn(document.getElementById('FATList'+pid).parentNode.parentNode)) {
								FATClose(pid);
							}
						}
					}
				}
			}
		}
		function FATGetX(obj) {
			var x=0;  	//CORE-75
			if (obj!=null) {
	 			x=obj.offsetLeft;
	 			if (obj.offsetParent != null) x += FATGetX(obj.offsetParent);
			}
 			return x;
		}
		function FATGetY(obj) {
			var y=0;	//CORE-75
			if (obj!=null) {
	 			y=obj.offsetTop;
 				if (obj.offsetParent != null) y += FATGetY(obj.offsetParent);
			}
 			return y;
		}
		function FATgetPageOffsetLeft(obj) { //CORE-75.8
			var x=getPageOffsetLeft(obj);
			if (#($get(YFIXHEADER)&&(YUSERAGENT="MSIE"))#) { //CORE-79.5
				x=x-getPageOffsetLeft(document.getElementById('divFixedHeader'));
			}
			return x;
		}
		function FATgetPageOffsetTop(obj) {  //CORE-75.8	 //CORE-79.5
			var y=getPageOffsetTop(obj);
			if (#($get(YFIXHEADER)&&(YUSERAGENT="MSIE"))#) {
				y=y-getPageOffsetTop(document.getElementById('divFixedHeader'));
			}
			return y;
		}
		function FATListCreate(pid,parrValues,parrKeys,pstrMore,pintWidth) {
			if (parrValues.length==1) {  //CORE-75.2
				//Close if list is empty.
				FATClose(pid);
				return false;
			} else {
				var  bodyClientHeight=document.body.clientHeight; //CORE-79.10
				var intFontSize=14;
				var obj=document.getElementById('FATList'+pid);
				if (obj!=undefined) {
					obj.parentNode.removeChild(obj);
				}
				obj=document.createElement('DIV');
				var objStyle=obj.style;
				objStyle.overflow='hidden';
				objStyle.cursor='default';
				objStyle.left='0px';
				objStyle.top='24px';
				objStyle.position='absolute';
				objStyle.backgroundColor='white';
				objStyle.border='1px solid black';
				objStyle.MozUserSelect='none';
				obj.id='FATList'+pid;
			
				objTable=document.createElement('TABLE');
				objTable.cellSpacing=0;
				//CORE-79.4 if (objStyle.width!='') objTable.style.width='100%';
				obj.appendChild(objTable);
			
				//if (objContainer._container) {
				//if (objContainer.getAttribute('_container')) {
				if (pid.indexOf('_')!=-1) {
					//grid
					var objContainer=document.getElementById('FATContainer'+pid); //CORE-75.6
					document.getElementById('WWW2').appendChild(obj);
					objStyle.position='absolute';
					objStyle.left=FATgetPageOffsetLeft(document.getElementById('FATContainer'+pid))+'px';  //CORE-75.8
					//CORE-75.8 objStyle.top=24+getPageOffsetTop(document.getElementById('FATContainer'+pid))+'px';
					//CORE-102 objStyle.top=24+FATgetPageOffsetTop(document.getElementById('FATContainer'+pid))+'px'; //CORE-75.8 
					objStyle.zIndex='10000';
				} else {
					//document.getElementById('FATDIV'+pid).appendChild(obj); //CORE-75.6
					var objContainer=document.getElementById('FATDIV'+pid); //CORE-75.6
					objContainer.appendChild(obj); //CORE-75.6
				}
			
				var objBody=document.createElement('TBODY'); objTable.appendChild(objBody);
				objContainer.style.zIndex='2000'; //CORE-75.6
			
				if (pstrMore!='') parrValues[parrValues.length]=pstrMore; //CORE-75.4
			
				for (var idx = 0; idx &lt; parrValues.length; idx++) {
					var objTR=document.createElement('TR');
					objTR.style.top='0px';
					objTR.style.left='0px';
					objTR.id='FATListTR'+idx;

					objTD=document.createElement('TD');
					objTD.id='FATListTD'+pid;
					objTD.style.fontSize='12px';
					objTD.style.paddingLeft='5px';
					objTD.style.paddingRight='10px';
					objTD.style.paddingTop='4px';
					objTD.style.paddingBottom='4px';
					objTD.unselectable=true;
					objTD.noWrap=true;
					objTD.innerHTML=parrValues[idx].replace(' ','&nbsp');
					objTD.attachEvent('onmouseover',function() {FATListOnMouseOver()});  //CORE-75.1
					//objTD.style.width='100%';
					objTD.style.width='650px';
					objTD._key=parrKeys[idx];
					objTR._key=parrKeys[idx];
					if (objTD._key==undefined) {
						objTD.style.fontSize='11px';
						objTD.style.borderTop='1px solid black';
						objTD.style.backgroundColor='#F0F0F0';
						//objTD.style.fontStyle='italic';
						objTD.setAttribute('_ignore',true); //CORE-75.1
					}
					//objTD.attachEvent('onclick',function() {FATListSelectLine(pid);});
				
					objTR.appendChild(objTD);
					objBody.appendChild(objTR);	
					if (idx==0) FATListHighlightLine(objTR);
				}
				objStyle.width=getDropdownWidth(pintWidth,objTR,document.getElementById('tdY'+pid))+'px'; //CORE-79.3
				objTable.style.width='100%'; //CORE-79.4
				objStyle.top=FATGetTop(pid,bodyClientHeight)+'px'; //CORE-102 //CORE-79.10
				document.attachEvent('onclick',function() {FATOnClick(pid);});
			}
		}
		function FATGetTop(pid,pbodyClientHeight) { //CORE-102 //CORE-79.10
			var result=0;
			var top     = FATgetPageOffsetTop(document.getElementById('FATContainer'+pid));
			//CORE-79.10 var height1 = document.body.clientHeight;
			if (document.getElementById('FATList'+pid)==null) {
				var height2=0;
			} else {
				var height2 = document.getElementById('FATList'+pid).clientHeight;
			}
			var heightFixedHeader=0;
			if (document.getElementById('divFixedHeader')) {
				heightFixedHeader=getPageOffsetTop(document.getElementById('divFixedHeader'))
			}
			if (pid.indexOf('_')==-1) { //In Body
				if ((top+height2+24+heightFixedHeader)&lt;pbodyClientHeight) {
					result=24;
				} else {
					result=-height2-3;
				}
			} else { //In Grid
				if ((top+height2+24+heightFixedHeader)&lt;pbodyClientHeight) {
					result=top+24;
				} else {
					result=top-height2-2;
				}				
			}
			return result;
		}
		// --------------------  FATList -------------------------------------
		function getDropdownWidth(pintWidth,pobjTR,pobjCell) { //CORE-79.3
			var intWidth;
			var intWidthCell=0;
			var intWidthRow=0;
			if (pobjCell!=null) intWidthCell=pobjCell.offsetWidth;
			if (pobjTR!=null) intWidthRow=pobjTR.offsetWidth;
			intWidth=Math.max(intWidthCell,intWidthRow);
			if (pintWidth!='') intWidth=Math.min(pintWidth,intWidth);
			return intWidth;
		}
		function FATHighlightLine(pid) {
			if (event.srcElement.id==('Y'+pid)) {
				var obj=document.getElementById('Y'+pid);
				if (obj.size&gt;1) {
					var intLineHeight=obj.offsetHeight/obj.size;
					obj.selectedIndex=(event.offsetY/intLineHeight);
				}
			}
		}
		function FATClose(pid) {
			if (document.activeElement.id!=('FATButton'+pid)) {
				var obj=document.getElementById('FATList'+pid);
				if (obj!=null) {
					obj.parentNode.style.zIndex='200'; //CORE-75.6
					obj.parentNode.removeChild(obj);
					document.detachEvent('onclick',function() {FATOnClick(pid);});
					document.getElementById('Y'+pid).focus();
				}
			}
		}
	>	
	quit

SetState(pYFORM,pYART,pYLFN,pblnReadOnly,pblnMandatory)
	;-------------------------------------------------------------------------------
	; Set the drop down button to readonly
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 07-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new objWWW122
	set objWWW122=$$Get^WWW122(pYFORM,pYLFN)
	if $$$WWW122DataInputType(objWWW122)=18 {
		write " var obj=document.getElementById('FATButtonTD"_pYFORM_pYART_pYLFN_"');"
		write " if (obj!=null) {"
		write "   var blnDisabled="_$select(pblnReadOnly:"true",1:"false")_";"
		write "   obj.disabled=blnDisabled;"

		; *****   the following three lines handle the condition where a 'rule' is set up
		;         to disable the FATSearch control based on the contents of another field
		;         and the user has clicked directly from that field to the dropdown button
		;         on the FATSearch.
		
		write "   obj.focus();"
		write "   if (blnDisabled) { FATClose('"_pYFORM_pYART_pYLFN_"'); }"		
		write "	  window.setTimeout(function() {obj.disabled=blnDisabled},5);"
		; *****
		
		write "}"
		
		write "  YMANDATORY="_$select(pblnMandatory:"true",1:"false")_";"
	}
	quit
	

Display(YFORM,YART,YLFN)
	;-------------------------------------------------------------------------------
	; Display the control on screen.
	; 
	; <DIV>
	;    <TABLE>
	;        <TR>
	;           <TD>
	;				<SELECT>
	;			</TD>
	; Params:
	;
	; Returns:
	;
	; History:
	; 20-May-2013	shobby		CORE-103: Improve alignment of Search buttons.
	; 15-May-2013	shobby		CORE-79.7: Change images again.
	; 14-May-2013	shobby		CORE-79.2: Change button image.
	; 07-May-2013	shobby		CORE-75.11: Include buttons and relation within the same DIV
	;										to prevent buttons displaying on 2nd line. 
	; 08-Apr-2013	shobby		CORE-75.6: Make sure drop down is on top of other FATSearch
	; 07-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new YMANDATORY,strLeft ;CORE-75.6
	
	set YMANDATORY=(YPFLICHT=1) || (YART="P")
	if $get(YWIDTH)="" set YWIDTH=200
	
	write !,"<DIV id='FATDIV"_YFORM_YART_YLFN_"' style='position:relative; z-Index:200; float:left; width:120%;'" ;CORE-75.6
	if (YSTATUS'="") write !,$$STATHTML^WWWFORM7()
	write !,">"
	
	write !,"<DIV id='FATContainer"_YFORM_YART_YLFN_"' style='position:relative; float:left; '>"
	do DisplayText()
	write !,"</DIV>"

	; Button for the drop down]]><![CDATA[ list vvvvvvvv ------------------------------------------------------
	if YUSERAGENT="MSIE" {
		set strLeft="-4px"
	} else {
		set strLeft="0px"
	}
	write !,"<DIV id='FATButtonTD"_YFORM_YART_YLFN_"' style='font-size:12px; cursor:pointer; margin-top:0px; width:22px; height:22px; position:relative; _fl_oat:left; display:"_$select(YUSERAGENT="MSIE":"inline",1:"inline-block")_"; left:"_strLeft_";'" ;CORE-75 ;CORE-75.11 ;CORE-79.2
	if YHID=2 write !," disabled=true "
	write !," onclick='FATClick("""_YFORM_YART_YLFN_""","""_YFORM_""","""_YART_""","""_YLFN_""","""_YMANDATORY_""");' "
	write !,">"
	write !,"&nbsp;<IMG id='FATSEARCH"_YFORM_YART_YLFN_"IMG' src="_YGIF_"form_fat_arrow.gif "_$select(YHID=2:"style='"_$$Gray^WWWFORMCrossBrowserSupport(50)_"'",1:"")_">"  ;CORE-79.2 ;CORE-79.7
	write !,"</DIV>"
	; Button for the drop down list ^^^^^^^^ ------------------------------------------------------

	write !,"<DIV style='height:1px; position:relative; display:"_$select(YUSERAGENT="MSIE":"inline",1:"inline-block")_"; vertical-align:top; left:-5px; top:1px;'>" ;CORE-103
	;do PARASUCH^WWWFORM75	;CORE-75.11
	write !,"</DIV>" ;CORE-103
	do RELATION^WWWFORM7	;CORE-75.11
	write !,"</DIV>"
	$$$StartScript()
	write !,"YVALUE='"_YINHALT_"';"
	$$$EndScript()
	if YXTYP=6 do ^WWWFORM73(YFORM,YART,YLFN)  ;SCRIPT SCHREIBEN ;write 

	quit
	
	
DisplayText()
	;-------------------------------------------------------------------------------
	; Based on code from WWWFORM7 to build the textbox part of the control
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 18-Oct-2012	shobby		SR18066: Call to doBlur
	; 22-Sep-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	set YXTYP=1
	if +YLANGE=0 set YLANGE=1
	set YLANGE1=YLANGE

	if (YTYPE'="float") || (YLANGE>12) do  ;FELD VERLÄNGERN ;field prolong 
	. if YLANGE>2 set YLANGE1=YLANGE1+1
	. if YLANGE>6 set YLANGE1=YLANGE1+1
	;
	if YLANGE1>YLAMX set YLANGE1=YLAMX  ;NICHT GRÖßER ALS ;Not when 
	if YTYPE="FILE" set YLANGE=200
	if YTYP=8 set YLANGE=30
	if YTYP=7 if SPRACHE'="DE" set YLANGE1=10  ;TYBD;UHRZEIT ZU LANG IN "EN";4,8,2004    ; FIXME : "DE"
	;
	; If we have a customized field size for a Data Field, ignore the 'Modified Field Length' value.
	if (YART = "D") && (+$$$WWW122DFieldLength($get(^WWW122D(0,YFORM,YLFN,YM,1))) = 0) && ($piece(YSATZ,Y,88) > 0) do ;SRBR014471
	. set YLANGE1=+$piece(YSATZ,Y,88)
	;
	; If we have a customized field size for a Primary Key, ignore the 'Modified Input Field Length' value.
	if (YART = "P") && (+$$$WWW121DFieldLength($get(^WWW121D(0,YFORM,YLFN,YM,1))) = 0) && ($piece(YSATZ,Y,88) > 0) do ;SRBR014628
	. set YLANGE1=+$piece(YSATZ,Y,88)
	;
	write YCR,"<INPUT"
	;IF YHID=2 WRITE " readOnly"
	write YCR," NAME=""Y"_YFORM_YART_YLFN_""""
	write YCR," ID=""Y"_YFORM_YART_YLFN_""""
	write YCR," SIZE="""_YLANGE1_""""
	write YCR," MAXLENGTH="""_YLANGE_""""
	;
	; *** EXECUTE ***
	if $piece(YSATZ,Y,99)'="" xecute $piece(YSATZ,Y,99)  ;EXECUTE INNERHALB EINES EINGABEFELDES GUE ;EXECUTE inside 
	if $$EXIST^%R("Y"_YFORM_YART_YLFN_"onField.OBJ",$get(YUCI)) write YCR xecute "DO ^Y"_YFORM_YART_YLFN_"onField"  ;CUSTOMIZED EXECUTE;FIS;24947;10.01.04
	;
	write YCR," TYPE="""_YTYPE_""""
	if YINHALT'="" write YCR," VALUE="""_YINHALT_""""
	set intTabIndex = +$$$WWW122TabIndex(YSATZ)   ; SR17867.1
	if intTabIndex=0 if $get(YHID)=2  write YCR," TABINDEX=-1"    ; TAB STOP IN READ ONLY FIELD.14.07.06;FAN
	if intTabIndex=0 if $get(YHID)'=2 write YCR," TABINDEX="""_YTABX_""""
	if intTabIndex'=0                 write YCR," TABINDEX="""_intTabIndex_""""
	;
	;SR17725.awrite YCR," style=""position:relative; left:-30px; width:"_(YWIDTH-4)_"px; "            ;NEUER STYLE;TYBD;26159;28,7,2004
	write YCR," style=""position:relative; left:0px; width:"_(YWIDTH-4)_"px; height:22px; "            ;NEUER STYLE;TYBD;26159;28,7,2004 ;SR17725.a
	if YUSERAGENT="MSIE" write "top:-1px; "  ;CORE-75
	if $piece(YSATZ,Y,91)'="" do     ;SCHRIFTART NEU ;recent 
	. write "font-family: '"_$piece($get(^WWW100(0,"SCHRIFTART",SPRACHE,$piece(YSATZ,Y,91),1)),Y,1)_"'"
	write "; padding-top:0 ;padding-bottom:0"   ;style
	if YTYPE="float" write "; text-align:right"
	if (YART="P") && (YHID=2) /*&& blnBoldKeys*/ write "; font-weight:bold"     ; $$$WWW012PrimaryKeyFatRepresent
	if $piece(YSATZ,Y,78)'="" write "; "_$piece(YSATZ,Y,78)   ;style
	if YINHALT'="" if YPARA(20)=1 if YPARA(2)'="" set YCOLOR1=YPARA(2) set YPARA(5)=YINHALT do FARBE2^WWWFORM7
	if YHID=2 do LESEN1^WWWFORM7                             ; Disabled field
	if YMANDATORY do PFLICHT^WWWFORM7        ; Mandatory Field
	IF (YHID'=2) && (YPFLICHT'=1) && (YART'="P") write "; background-color:white; " 	;SR17861
	write "; margin-bottom:1px; " 										;SR17861
	write """"  ;STYLE ENDE ;termination 
	;
	if YHID=2 do LESEN^WWWFORM7
	if $piece(YSATZ,Y,82)'="" do READONLY^WWWFORM7  ;ANDERE FELDER AUF READ ONLY WENN EINGABE ;upon READ when 
	if $piece(YSATZ,Y,83)'="" do WRITE^WWWFORM7     ;ANDERE FELDER AUF WRITE WENN EINGABE ;upon when 
	if YJAVA=1 do JAVA^WWWFORM8
	
	;do SAVE^WWWFORM7
	if YSTATUS'="" write $$STATHTML^WWWFORM7()
	if YHID'=2 {
		write YCR," onfocus='select();'"
		write YCR," onkeydown='FATKeyDown("""_YFORM_YART_YLFN_""");' "
		write YCR," onkeyup='FATKeyUp("""_YFORM_YART_YLFN_""","""_YFORM_""","""_YART_""","""_YLFN_""","""_YMANDATORY_""");' "
		; The following replace the call to SAVE^WWWFORM7
		write YCR," onblur='"
		write YCR," FATOnBlur("""_YFORM_YART_YLFN_"""); "
		;SR18066 write YCR," if (!this.readOnly) { retval = EventValue("""_YUCI_""","""_YUSER_""","""_YFORM_""",""FIX"_$translate(YKEY,".","~")_""",""Y"_YFORM_YART_YLFN_""",this.value,""2"","""_$$VariableForEventBroker^WWWFORM7()_""","""_YSEITE_"""); }"
		write YCR," if (doBlur(this)) { retval = EventValue("""_YUCI_""","""_YUSER_""","""_YFORM_""",""FIX"_$translate(YKEY,".","~")_""",""Y"_YFORM_YART_YLFN_""",this.value,""2"","""_$$VariableForEventBroker^WWWFORM7()_""","""_YSEITE_"""); }" ;SR18066 
		write "'"
	}
	write ">"
	if YPARA(20)=1 set YSHOWRELA = 1
	quit
	

Populate(YFIELDNAME,pstrText,YFORM,YART,YLFN,YMANDATORY)
	;-------------------------------------------------------------------------------
	; Obtains the data to be placed in the drop down list.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 26-Aug-2014	shobby		IPIRANGA-268: Corrected an error when the search is on a Manual Field
	; 20-Nov-2013	shobby		SESPE-601: Improve FATSearch when Form and class field are different.
	; 14-May-2013	shobby		CORE-79.3: ListWidth changed to MaxListWidth
	; 24-Apr-2013	shobby		CORE-75.10: Temporary data is subscripted by YUSER
	; 08-Apr-2013	shobby		CORE-75.7: Correction when relationship is not the first property
	; 03-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new idINART,intLines,objINART,strText,objWWW122,idClass,YBBN
	new idRec,objRec,intRelationalDisplayItem
	new strSoundEx,blnMore,intItem,intItemCount,strField

	new YOPTIONWIDTH
	
	kill ^CacheTempFATSearch(YM,YUSER) ;CORE-75.10
	
	set pstrText=$zcvt(pstrText,"U")

	set intLines=0
	if (YART = "P"){
		set objWWW121	  = $$Get^WWW121(YFORM,YLFN)
		set YOPTIONWIDTH  = "" ;Keys don't have this property
		set idClass		  = $$$WWW121RelationClass(objWWW121)
		set intRelationalDisplayItem = $$$WWW121RelationalDisplayItems(objWWW121)
		if intRelationalDisplayItem  = "" set intRelationalDisplayItem=1

		set blnMore  = $$$NO
		set strField = ""
		for intItem=1:1:$length(intRelationalDisplayItem,";") {
			if strField'="" set strField=strField_","
			set strField=strField_$$$WWW003PropertyName($get(^WWW003(0,$$$WWW121RelationClass(objWWW121),$piece(intRelationalDisplayItem,";",intItem),1)))
		}		
				
	} else {
		if YART="M" {
			set YBBN=YLFN ;IPIRANGA-268
		} else {
			set YBBN=$$$GetFormField(YFORM,YLFN)
		}
		set objWWW122=$$Get^WWW122(YFORM,YBBN)
		set YOPTIONWIDTH=$$$WWW122MaxListWidth(objWWW122) ;CORE-79.3

		set idClass=$$$WWW122RelationClass(objWWW122)
		;set idClass="^"_idClass_"("_YM_")"
		set intRelationalDisplayItem=$$$WWW122RelationalDataField(objWWW122)
		if intRelationalDisplayItem="" set intRelationalDisplayItem=1

		set blnMore=$$$NO
		set strField=""
		for intItem=1:1:$length(intRelationalDisplayItem,";") {
			if strField'="" set strField=strField_","
			;CORE-75.6 set strField=strField_$$$WWW003PropertyName($get(^WWW003(0,$$$WWW122RelationClass(objWWW122),intItem,1)))
			set strField=strField_$$$WWW003PropertyName($get(^WWW003(0,$$$WWW122RelationClass(objWWW122),$piece(intRelationalDisplayItem,";",intItem),1))) //CORE-75.7
		}
	}
	set intLines=$$SQL(idClass,strField,pstrText,.blnMore)
	do PopulateComboBox(YFIELDNAME,pstrText,"CacheTempFATSearch",YUSER,$$$YES,YOPTIONWIDTH,blnMore,YMANDATORY) ;CORE-75.10
	quit


SQLOrderBy(pidClass)
	;-------------------------------------------------------------------------------
	; Create the 'ORDER BY' clause for the SQL
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 10-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new strOrderBy,idField
	
	set strOrderBy=""
	
	set idField="" for { set idField=$order(^WWW002(0,pidClass,idField)) quit:idField=""
		if strOrderBy'="" set strOrderBy=strOrderBy_","
		set strOrderBy=strOrderBy_$$$WWW002PropertyName($get(^WWW002(0,pidClass,idField,1)))
	}	
	if strOrderBy'="" set strOrderBy=" ORDER BY "_strOrderBy
	quit strOrderBy


SQL(pstrClass,pstrField,pstrText,&pblnMore)
	;-------------------------------------------------------------------------------
	; Use SQL to select records.  This is slower than 
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 01-Jul-2013	shobby		CORE-141: Improved search to recognise spaces in search.
	; 07-May-2013	shobby		CORE-79.1: Improved search.  'CAP' should return 'capsula' but not 'cabeca plastico' 
	; 24-Apr-2013	shobby		CORE-75.10: Temporary data is subscripted by YUSER
	; 24-Apr-2013	shobby		CORE-75.9: Limit list to 10 items.
	; 10-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new objResultSet,intLines,strData,strSQL,intMaxLines ;CORE-75.9
	
	set intLines=0
	set intMaxLines=10 ;CORE-75.9
	
	;CORE-141 set pstrText=$$Index^COMUtils(pstrText)

	;CORE-79.1 set strSQL = "SELECT DISTINCT TOP("_(intMaxLines+1)_") "_pstrClass_".ID,"_pstrField_" FROM SQLUser."_pstrClass_" WHERE "_pstrClass_".Company="_YM_" AND (($find(%upper(SQLUser."_pstrClass_"."_pstrField_"),"""_pstrText_""")>0) OR ($find(%upper(SQLUser."_pstrClass_"."_$$$WWW002PropertyName($get(^WWW002(0,pstrClass,1,1)))_"),"""_pstrText_""")>0)) "_$$SQLOrderBy(pstrClass)
	;set strSQL = "SELECT DISTINCT TOP("_(intMaxLines+1)_") "_pstrClass_".ID,"_pstrField_" FROM SQLUser."_pstrClass_" WHERE "_pstrClass_".Company="_YM_" AND (SQLUser."_pstrClass_"."_pstrField_" LIKE ""%"_pstrText_"%"" OR SQLUser."_pstrClass_"."_$$$WWW002PropertyName($get(^WWW002(0,pstrClass,$order(^WWW002(0,pstrClass,""),-1),1)))_" LIKE ""%"_pstrText_"%"") "_$$SQLOrderBy(pstrClass) ;CORE-79.1
	set strSQL = "SELECT DISTINCT TOP("_(intMaxLines+1)_") "_pstrClass_".ID,"_pstrField_" FROM SQLUser."_pstrClass_" WHERE "_pstrClass_".Company="_YM
	set strSQL = strSQL _ " AND (SQLUser."_pstrClass_"."_pstrField_" LIKE '%"_pstrText_"%' OR SQLUser."_pstrClass_"."
	set strSQL = strSQL _$$$WWW002PropertyName($get(^WWW002(0,pstrClass,$order(^WWW002(0,pstrClass,""),-1),1)))_" LIKE '%"_pstrText_"%') "
	If (pstrClass = "INART") {
		set strSQL = strSQL _"And INART.HoldReason not in (2) "
	}
	set strSQL = strSQL _$$SQLOrderBy(pstrClass) ;CORE-79.1

	set objResultSet = ##class(%Library.ResultSet).%New()
	if objResultSet.Prepare(strSQL) {
		if objResultSet.Execute() {
			while objResultSet.Next() {
				set strData=objResultSet.Data(pstrField)
				set strData=$extract(strData,1,300)
				set ^CacheTempFATSearch(YM,YUSER,$piece(objResultSet.Data("ID"),"||",2),1)=strData ;CORE-75.9 ;CORE-75.10
				set intLines=intLines+1 			;CORE-75.9
				if intLines=intMaxLines { 			;CORE-75.9
					set intLines=intLines+1 		;CORE-75.9
					set pblnMore=$$$YES
					quit
				}
			}
		}
	}

	quit intLines


SoundEx(pstrText)
	;-------------------------------------------------------------------------------
	; Soundex search.  (Unfinished)
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 07-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new Ch, LastCh, i, sx
	
	set pstrText=$zcvt($zstrip(pstrText,"<>W"),"U")
	
	if $length(pstrText)<1 {
		set sx=""
	} else {
		set sx=$extract(pstrText)
		set $extract(pstrText,1)=""
		set sx=sx_$translate(pstrText,"BFPVCGJKQSXZDTLMNRAEHIOUWY1234567890 "  ,"111122222222334556")
	}
	for i=1:1:$length(sx) {
		if $extract(sx,i) = $extract(sx,i+1) {
			set $extract(sx,i,i+1)=$extract(sx,i)
		}
	}
	for i=1:1:4 {
		if $length(sx)<4 set sx=sx_"0"
	}
	set sx=$extract(sx,1,4)
	quit sx


PopulateComboBox(YFIELDNAME, pstrValue="", pstrRelationClass, pstrRelationKeys="",pblnForce=$$$NO,pintWidth="",pblnMore=$$$NO,YMANDATORY=$$$NO) ;SR17225
	;-------------------------------------------------------------------------------
	; This method is used to change a Datafield into a Combo Box. The options for
	; the combo box will be based on the relation class and on the relation primary
	; keys. This works exactly like on the relation of the data field definition.
	;
	; Params:	YFORM				- form name
	;			YLFN				- manual or data field number
	;			pstrValue			- value to be stored in field
	;			YART				- field type - "M" or "D"
	;			pstrRelationClass	- the relation class used to build the combo
	;			pstrRelationKeys	- the relation primary keys used together with the relation class
	;			
	; History:
	; 21-Feb-2013	shobby	SESDF-1097: Removed change from SESPE-601: Here the pstrRelationKeys will be YUSER which
	;							could be of the form 10165578768x101 and needs to be turned into a string.
	; 21-Nov-2013	shobby	SESPE-601: removed KEYBUILD. Would turn variables such as SPRACHE into strings
	; 03-Nov-2011	shobby	SR17725: Based on version in COMUtils
	;-------------------------------------------------------------------------------
	new idClass,idCompany,loop,objClass,strClassDef,strClassLoop,strCommand,strData
	new strText,strMore,intStart
	
	set objClass = $get(^WWW001(0,pstrRelationClass,1))
	if +$$$WWW001SharedFile(objClass) {
		set idCompany = 0
	} else {
		set idCompany = YM
	}

	// If the user set the primary keys for the relation database.
	if pstrRelationKeys '= "" {
		set pstrRelationKeys=$$^WWWKEYBUILD(pstrRelationKeys)
		set strClassDef = "^"_pstrRelationClass_"("_idCompany_","_pstrRelationKeys_",idClass"
	
	} else {
		set strClassDef = "^"_pstrRelationClass_"("_idCompany_",idClass"
	}
	set strClassLoop = "set idClass = $order("_strClassDef_"))"
 
	// Get the values to be populated.
	write "var arrValues = new Array();"
	write "var arrKeys = new Array();"
	
	if YMANDATORY {
		set intStart=1
		write "arrValues[0]=' ';"
		write "arrKeys[0]='';"
	} else {
		set intStart=1
		; not mandatory
		write "arrValues[0]=' ';"
		write "arrKeys[0]='';"
	}
	set idClass = ""
	for loop=intStart:1 {
		xecute strClassLoop
		quit:idClass=""
		
		set strData    = ""
		set strCommand = "set strData = $piece($get("_strClassDef_",1)),Y,1)"
		xecute strCommand
		
		set strData=$$Highlight(strData,pstrValue)
		
		; TODO Don't display primary key if checked off.
		set strText=$$Highlight(idClass,pstrValue)_" - "_strData
		set strText=$zconvert(" "_strText,"o","JS") ;Doesn't format in blue the first character.
		write "arrValues["_loop_"] = '"_strText_"';"
		write "arrKeys["_loop_"] = '"_idClass_"';"
	}
	if pblnMore {
		set strMore=$$$Text("WWW00153")  ;more 
	} else {
		set strMore=""
	}
 	write "FATListCreate('"_YFIELDNAME_"',arrValues,arrKeys,'"_strMore_"','"_pintWidth_"');"

	quit
	
	
Highlight(pstrData,pstrValue)
	;-------------------------------------------------------------------------------
	; Puts a blue highlight around the specified value in the longer text.  (Case insensitive)
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 10-Nov-2011	shobby		SR17725: Created
	;-------------------------------------------------------------------------------
	new strText,int,strData,intLast,len
	
	if pstrValue="" {
		set strText=pstrData
	} else {
		set strText=""
		set strData =$$$UPPER($$^WWWUMLAU(pstr]]><![CDATA[Data))
		set pstrValue=$$$UPPER($]]><![CDATA[$^WWWUMLAU(pstrValue))
		set len=$length(pstrValue)
		if $find(strData,pstrValue)>0 {
			set int=0
			for {
				set intLast=int
				set int=$find(strData,pstrValue,int)
				if int=0 {
					set strText=strText_$extract(pstrData,intLast,$length(pstrData))
				} else {
					set strText=strText_$extract(pstrData,intLast,int-len-1)_"<strong>"_$extract(pstrData,int-len,int-1)_"</strong>"
				}
				quit:int=0
			}
		} else {
			set strText=pstrData
		}	
	}
	quit strText]]></Routine>
</Export>