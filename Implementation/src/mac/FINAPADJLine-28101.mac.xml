<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPADJLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPADJLine
	;-------------------------------------------------------------------------------
	; History:
	; 22-Mar-2005	GRF		Use Boolean Macros/QSave Macros
	;-------------------------------------------------------------------------------
#include FINConst
#include COMSYS
	
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Code to run before saving. Mainly validation.
	;
	; Returns:
	;
	; History:
	; 27-Jun-2006	JW			SR13755: Removed $$$Text, added amount check
	; 23-Jun-2006	JW			SR12775: Redundant due to field validation
	; 14-Jun-20005	shobby		Pass in YKEY to use to identify the line that failed.  SR12633
	; 14-Apr-2005	Paul K		Added ValidateCurrency SR:12136
	; 30-Nov-2004	shobby/JW	Changed error output.
	; 01-Mar-2004	shobby	Created
	;-------------------------------------------------------------------------------
    new strStatus
 
	//set strStatus=$$ValidateSupplier(pYFELD)	SR12775
    
    if ($$$FINAPADJLineCharge(pYFELD)="") && ($$$FINAPADJLineCredit(pYFELD)="") {
		set strStatus = $listbuild("Fin01048")  ;Enter either a charge or a credit		SR13755
    } else {
    	set strStatus=$$ValidateCurrency(pYKEY,pYFELD)
	}
    
    if $$$ISERR(strStatus) {
		//set YRETVAL=$$$Text(strStatus)
		set YRETVAL = strStatus		//SR13755
	}
	
	
	quit strStatus
 
 
ValidateCurrency(pYKEY,pobjInvoiceLine)
	;-------------------------------------------------------------------------------
	; Make sure that the currency on the line is right.
	;
	; Returns:Status
	;
	; History:
	; 08-Nov-2005	PO			New design required as Charge & Credit are mutually exclusive but discount can exist for either.
	; 08-Nov-2005	MS			Add currency check to discount
	; 14-Jun-2005	shobby		Changed error message to include id of failed line. SR12633
	; 14-Apr-2005	Paul K		Created:12136
	;-------------------------------------------------------------------------------
	new strStatus,idTranCurrency,idSupplierCurrency,curAmount
	
	set strStatus=$$$OK
	set idTranCurrency=""
	set idSupplierCurrency=$$GetFCCode^FINAPSupplier($$$FINAPADJLineSupplier(pobjInvoiceLine))
	
	if idSupplierCurrency="" set idSupplierCurrency=$get(YWHR)
	
	for curAmount = $$$FINAPADJLineCharge(pobjInvoiceLine),$$$FINAPADJLineCredit(pobjInvoiceLine),$$$FINAPADJLineDiscount(pobjInvoiceLine) {
		if +curAmount {
			set idTranCurrency=$$$FCur(curAmount)
			if idTranCurrency="" set idTranCurrency = $get(YWHR)
		}
 
		if (idTranCurrency'=idSupplierCurrency) && (idTranCurrency'="") {
			set strStatus = $listbuild("Fin00549",$$$FINAPADJLineSupplier(pobjInvoiceLine),idSupplierCurrency,idTranCurrency,$piece($get(pYKEY),",",2))  ;Cannot save line '%4' since supplier '%1' works in currency '%2' and the currency of transaction is '%3' which do not match.
			quit
		}
	}
 
	quit strStatus	
	
CheckDuplicateSupplierInvoice(pYFELD,pYKEY) private
    ;-------------------------------------------------------------------------------
    ; Check that the Reference used on this line with this supplier has not been
    ; used on another Adjustment or Invoice Matching.
    ;
    ; Returns:
    ;
    ; History:
    ; 10-Feb-2005	RobertW	Use the $$$Alert macro.
    ; 						This routine is currently not used. If it needs to be
    ; 						brought back to life, uncomment the calls on the Execute
    ; 						OnBlur on the Supplier & Reference fields of FINAPADJLine.
    ; 03-Feb-2005	shobby	Code Check.
    ; 01-Feb-2005	RobertW	Modified to use the better index
    ; 14-Jan-2005	RobertW	Created (SR10061)
    ;-------------------------------------------------------------------------------
	new idADJ,idADJLine,idCheckADJ,idCheckADJLine,idCheckFINAPSupplier
	new idFINAPInv,idFINAPSupplier,objFINAPADJLine,objSetup
	new strCheckReference,strError,strReference
		
	quit:($get(pYFELD)="")||($get(pYKEY)="")
	
	set idADJ           = $translate($piece(pYKEY,YKOMMA,1),$char(34))
	set idADJLine       = $piece(pYKEY,YKOMMA,2)
	set objFINAPADJLine = $get(pYFELD)
	set idFINAPSupplier = $$$FINAPADJLineSupplier(objFINAPADJLine)
	set strReference    = $$$FINAPADJLineRef(objFINAPADJLine)
	set strError        = ""
	
	if (idFINAPSupplier'="") && (strReference'="") {
		set objSetup = $get(^FINGeneralSetup(YM,YM,1))
			
		if objSetup'="" {
			if $$$FINGeneralSetupCheckForDuplicate(objSetup)=1 {
				// Check FINAPInv
				set idFINAPInv=$order(^FINAPInvs(YM,1,idFINAPSupplier,strReference,""))
				
				if idFINAPInv'="" {
					set strError=$listbuild("Fin00569",idFINAPSupplier,strReference,idFINAPInv)
					;Invoice Match %3 for Supplier %1, has invoice number %2 matched. Please choose another Reference.
				}
				
				set strCheckReference=$$^WWWUMLAU(strReference,1)
				
				// Check the FINAPADJLine
				set idCheckFINAPSupplier=""
				set idCheckADJ=$order(^FINAPADJLines(YM,52,idFINAPSupplier,strCheckReference,""))
				quit:idCheckADJ=""
				set idCheckADJLine=$order(^FINAPADJLines(YM,52,idFINAPSupplier,strCheckReference,idCheckADJ,""))
				quit:idCheckADJLine=""					
				
				if (idCheckADJLine'=idADJLine) || (idCheckADJ'=idADJ) {
					set strError=$listbuild("Fin00570",idCheckADJ,idCheckADJLine,strReference,idFINAPSupplier)
					;Adjustment %1, Line %2, has reference %3 for Supplier %4. Please choose another Reference.
				}
			}
		}			
	}
	
	if strError'="" {
		$$$Alert(strError)
	}
	
	quit
	
 
]]></Routine>
</Export>