<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFCHECK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFCHECK ;INAUFCHECK;TYBD;ORDERCHECK; 19,2,2005  ; Compiled March 11, 2005 11:34:05
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		ORDERCHECK
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| TYBD	 19,2,2005  
	;|
	;\------------------------------------------------------------------/
	;
	NEW UES,YDDSATZ,POSANZ
	;
	;
	SET UES=$$^WWWFELDNAME("INAUF","P",1,"Auftrag")_Y_$$^WWWFELDNAME("INAUF","D",2,"Kunde")_Y_$$^WWWFELDNAME("INAUF","D",22,"Bearbeitung")_Y_$$^WWWFELDNAME("INAUFP","P",2,"Position")_Y_$$^WWWTEXT("32162;Eingangsrechnung")_Y_$$^WWWTEXT("32161;Ausgangsrechnung")_Y_$$^WWWTEXT("32259;Differenz")
	;
	IF $GET(VORG(1))=""!($GET(VORG(2))="") DO ^WWWINFO($$^WWWTEXT(32310)) QUIT ;KEINE AUSWAGL GETROFFEN
	;
	DO ^WWWBACK    //BACKBUTTON
	;
	DO ^WWWLISTKOPF   //HEADER = WHAT TO PRINT
	;
	DO FF^WWWW()  //FORMFEED
	;
	DO HEADER  //HEADER  TABELLE 
	SET YDDSATZ=0  //SET COLOR READ LINE
	;
	DO SORT  //SORT  AND PRINT
	DO GESAMT
	;
	DO LEER^WWWTAB(30,7)   //SPACE AT THE END
	DO STOP^WWWTAB   // END CLOSE TABLE
	;
	QUIT
	;
	
HEADER   //HEADER FÃœR TABELLE //to tabulation 
	WRITE "</CENTER>"
	;
	DO START100^WWWTAB  //launching the superscription 
	WRITE "<THEAD>"    // HEADER ON EVERY PAGE
	DO NL^WWWTAB
	FOR I=1:1 QUIT:$PIECE(UES,Y,I,99)=""  DO 
	. DO NHW^WWWTAB 
	. WRITE $PIECE(UES,Y,I),"&nbsp;"
	. DO EH^WWWTAB
	. QUIT
	DO EL^WWWTAB
	WRITE "</THEAD>"   //END HEADER
	QUIT
	;
	;
	
SORT  //SORTIERUNG UNTERTEILE //sorting 
	SET DATE=VORG(1)-1
	FOR  SET DATE=$ORDER(^INAUFs(YM,2,DATE)) QUIT:DATE=""  QUIT:DATE>VORG(2)  DO
	. QUIT:DATE<VORG(1)
	. SET AUF="" FOR  SET AUF=$ORDER(^INAUFs(YM,2,DATE,AUF)) QUIT:AUF=""  DO
	. . SET AUF1=$GET(^INAUF(YM,AUF,1))
	. . IF $GET(VORG(3))'="" QUIT:$PIECE(AUF1,Y,1)'=VORG(3)  ;FALSCHER KUNDE;FIS;28.02.05;27398
	. . KILL POSANZ
	. . SET POSANZ=0
	. . DO SORT1
	. . IF +$G(VORG(5))=1 IF POSANZ>0 DO
	. . . DO NL^WWWTAB
	. . . FOR YI=1:1:7 DO
	. . . . DO NFRCSS^WWWTAB(,,1,2)
	. . . . ;IF YI=4 WRITE $$^WWWTEXT(31406)_": "_POSANZ
	. . . . IF YI=5 WRITE $$^WWWZAHL($GET(POSANZ("EK")),0,2)
	. . . . IF YI=6 WRITE $$^WWWZAHL($GET(POSANZ("VK")),0,2)
	. . . . IF YI=7 WRITE $$^WWWZAHL($GET(POSANZ("VK"))-$GET(POSANZ("EK")),0,2)
	. . . . WRITE "&nbsp;",YCR
	. . . . DO EF^WWWTAB
	. . . . QUIT
	. . . DO EL^WWWTAB
	. . . QUIT
	. . QUIT
	. QUIT
	QUIT	
	
SORT1 ;
	DO
	. QUIT:$PIECE(AUF1,Y,2)'=0
	. SET KUNDE=$PIECE(AUF1,Y,1)
	. SET POS="" FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . IF $PIECE(POS1,Y,7)'=1 IF '$DATA(^INERECH1(YM,AUF,POS)) QUIT  ;KEINE Bestellware oder keine Rechnung
	. . SET RECH1=""
	. . IF $DATA(^INERECH1(YM,AUF,POS)) DO  QUIT
	. . . SET LIEF=$ORDER(^INERECH1(YM,AUF,POS,""))
	. . . QUIT:LIEF=""
	. . . ;SET RECH=$ORDER(^INERECH1(YM,AUF,POS,LIEF,""))
	. . . ;QUIT:RECH=""
	. . . SET RECH="" FOR  SET RECH=$ORDER(^INERECH1(YM,AUF,POS,LIEF,RECH))  QUIT:RECH=""  QUIT:$PIECE($GET(^INERECH1(YM,AUF,POS,LIEF,RECH,1)),Y,80)=""
	. . . IF RECH="" IF +$GET(VORG(6))=1 DO PRINT  QUIT
	. . . QUIT:RECH=""
	. . . SET RECH1=$GET(^INERECH1(YM,AUF,POS,LIEF,RECH,1))
	. . . DO PRINT
	. . . QUIT
	. . IF $PIECE(POS1,Y,97)'="" IF RECH1="" DO  QUIT  ;NOCH KEINE RECHNUNG
	. . . DO PRINT 
	. . . QUIT
	. . IF +$GET(VORG(6))=1 DO PRINT
	. . QUIT
	. QUIT
	QUIT
	;
	
PRINT  //
	;
	SET POSANZ=POSANZ+1
	NEW BERECH1,BERECH2
	WRITE YCR
	DO NL^WWWTAB
	DO
	. DO NFL^WWWTAB
	. WRITE "&nbsp;"
	. QUIT:$GET(LASTAUF)=AUF
	. NEW VORG,YFORM,YBACK
	. SET YBACK="INAUFCHECK,"
	. WRITE "<A TITLE="_""""
	. WRITE $$^WWWTEXT(374)   
	. WRITE """"
	. WRITE " HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INAUF&YKEY="_AUF  
	. DO ^WWWCGI
	. WRITE """"
	. WRITE ">"
	. WRITE AUF
	. WRITE "&nbsp;"
	. WRITE $$^WWWDATE(DATE)
	. WRITE "&nbsp;"
	. WRITE "</A>"
	. QUIT
	DO  ;KUNDE
	. DO ZW^WWWTAB
	. WRITE "&nbsp;"
	. QUIT:$GET(LASTAUF)=AUF
	. WRITE $PIECE(AUF1,Y,1)             //DATA
	. WRITE "&nbsp;"
	. WRITE $EXTRACT($PIECE(AUF1,Y,13),1,20) 
	. WRITE "&nbsp;"
	. QUIT
	;
	DO  ;BEARBEITUNG
	. DO ZW^WWWTAB
	. WRITE "&nbsp;"
	. QUIT:$GET(LASTAUF)=AUF
	. WRITE $TRANSLATE($EXTRACT($PIECE(AUF1,Y,22),1,30),"|"," ")             //DATA
	. WRITE "&nbsp;"
	. QUIT
	SET LASTAUF=AUF
	DO
	. DO ZW^WWWTAB
	. ;POSITION
	. WRITE "&nbsp;"
	. NEW VORG,YFORM,YBACK
	. SET YBACK="INAUFCHECK,INAUF,"
	. WRITE "<A TITLE="_""""
	. WRITE $$^WWWTEXT(374)   
	. WRITE """"
	. WRITE " HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INAUFP&YKEY="_AUF_","_POS_"&YFKEY="_AUF  
	. DO ^WWWCGI
	. WRITE """"
	. WRITE ">"
	. WRITE POS
	. ;
	. WRITE "&nbsp;"
	. WRITE $EXTRACT($PIECE(POS1,Y,1),1,20)  ;TEXT
	. WRITE "&nbsp;"
	. WRITE "</A>"
	. QUIT
	DO  ;RECHNUNG
	. DO ZWR^WWWTAB
	. SET BERECH1=0
	. IF $GET(RECH1)'="" SET BERECH1=+$PIECE(RECH1,Y,22)
	. IF +BERECH1'=0 WRITE $$^WWWZAHL(BERECH1)           //DATA
	. SET TOTAL1=$GET(TOTAL1)+BERECH1
	. SET POSANZ("EK")=$GET(POSANZ("EK"))+BERECH1
	. WRITE "&nbsp;"
	. QUIT
	DO  ;VK
	. DO ZWR^WWWTAB
	. SET BERECH2=0
	. IF $PIECE(POS1,Y,97)'="" SET BERECH2=$PIECE(POS1,Y,123)
	. IF BERECH2'=0 WRITE $$^WWWZAHL(BERECH2)          //DATA
	. SET TOTAL2=$GET(TOTAL2)+BERECH2  
	. SET POSANZ("VK")=$GET(POSANZ("VK"))+BERECH2
	. WRITE "&nbsp;"
	. QUIT
	DO  ;DIFF
	. DO ZWR^WWWTAB
	. WRITE $$^WWWZAHL(BERECH2-BERECH1)          //DATA
	. SET TOTAL3=$GET(TOTAL3)+BERECH2-BERECH1 
	. WRITE "&nbsp;"
	. QUIT
	DO EF^WWWTAB
	DO EL^WWWTAB
	QUIT
	
GESAMT
	WRITE YCR
	DO NL^WWWTAB
	DO NFL^WWWTAB
	DO
	. WRITE "&nbsp;"
	. QUIT
	DO  ;KUNDE
	. DO ZW^WWWTAB
	. WRITE "&nbsp;"
	. QUIT
	;
	DO  ;BEARBEITUNG
	. DO ZW^WWWTAB
	. WRITE "&nbsp;"
	. QUIT
	DO
	. DO ZW^WWWTAB
	. ;POSITION
	. WRITE "&nbsp;"
	. QUIT
	DO  ;RECHNUNG
	. DO ZWR^WWWTAB
	. WRITE $$^WWWZAHL($get(TOTAL1))           //DATA 
	. WRITE "&nbsp;"
	. QUIT
	DO  ;VK
	. DO ZWR^WWWTAB
	. WRITE $$^WWWZAHL($get(TOTAL2))         //DATA   
	. WRITE "&nbsp;"
	. QUIT
	DO  ;DIFF
	. DO ZWR^WWWTAB
	. WRITE $$^WWWZAHL($get(TOTAL3))          //DATA
	. WRITE "&nbsp;"
	. QUIT
	DO EF^WWWTAB
	DO EL^WWWTAB
	QUIT
]]></Routine>
</Export>