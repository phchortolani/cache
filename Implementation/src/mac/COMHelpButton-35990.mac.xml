<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHelpButton" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHelpButton
	
#include COMSYS
#include COMConst
#include WWWConst
	
LoadInButton(pidForm,pidLang,pidSeq)
	;-------------------------------------------------------------------------------
	; Load in a button record if it hasn't already been loaded.
	;
	; Params:	pidForm		: The form id
	; 			pidLang		: The language we're in
	; 			pidSeq		: Button sequence number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-Sep-2008	shobby	BR014980: Load in Created/Changed information
	; 24-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
 	new objWWW124,strKey,intRank,strHelp,objCOMHelpButton
 	new strStatus
 
	set strStatus=$$$OK
 
	if ('$data(^COMHelpButton(0,pidForm,pidLang,pidSeq))) { //load in
		set objWWW124 = $get(^WWW124(0,pidForm,pidLang,pidSeq,1))
		
		set strKey = pidForm_","_pidLang_","_pidSeq
		
		set intRank = $$$WWW124ColumnPosition(objWWW124)
		set strHelp = $$$WWW124Hilfe(objWWW124)
		
		set objCOMHelpButton = $get(^COMHelpButton(0,pidForm,pidLang,pidSeq,1))
		
		set $$$COMHelpButtonRank(objCOMHelpButton)        = intRank
		set $$$COMHelpButtonHelpText(objCOMHelpButton)    = $$$FormatParagraph(strHelp)
		set $$$COMHelpButtonDateCreated(objCOMHelpButton) = $$$WWW124DateCreated(objWWW124)
		set $$$COMHelpButtonCreatedBy(objCOMHelpButton)   = $$$WWW124CreatedBy(objWWW124)
		set $$$COMHelpButtonDateChanged(objCOMHelpButton) = $$$WWW124DateChanged(objWWW124)
		set $$$COMHelpButtonChangedBy(objCOMHelpButton)   = $$$WWW124ChangedBy(objWWW124)
		
		set strStatus = $$$Save("COMHelpButton",strKey,objCOMHelpButton,$$$YES)
	}
 
	quit strStatus
	
	
MergeButtons(pidForm,pidLang)
	;-------------------------------------------------------------------------------
	; Originally, was to break the position of buttons
	;
	; Params:	pidForm		: The form id
	; 			pidLang		: The language we're in
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 17-Apr-2007	RPW		SRBR014436: Do not update the rank, it doesn't reflect reality.
	; 24-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idSeq,objLine,strKey,intRank,strHelp,objWWW124,strStatus
 
	set idSeq=""
 
	for {
		set idSeq = $order(^COMHelpButton(0,pidForm,pidLang,idSeq))
		quit:(idSeq="")
		
		set strKey  = pidForm_","_pidLang_","_idSeq
		set objLine = $get(^COMHelpButton(0,pidForm,pidLang,idSeq,1))
		
	//	set intRank = $$$COMHelpButtonRank(objLine)
		set strHelp = $$$COMHelpButtonHelpText(objLine)
		
		set objWWW124 = $get(^WWW124(0,pidForm,pidLang,idSeq,1))
		
		if strHelp'="" set $$$WWW124Hilfe(objWWW124)=$$$FormatParagraph(strHelp)
	//	set $$$WWW124ColumnPosition(objWWW124) = intRank
		
		set strStatus = $$$Save("WWW124",strKey,objWWW124,$$$YES)
	}
 
	quit
	
	
PreviewHelp()
	;-------------------------------------------------------------------------------
	; Preview help text for this button
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 10-Sep-2008	shobby	BR014980: Reviewed.  This can stay as is.  COMHelp* will exist at this time.
	; 04-Jun-2007	RPW		SR15369: Use the new ReplaceYGIF macro to make images work
	; 24-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strHelp
 
	set strHelp = $$$ReplaceYGIF($$$COMHelpButtonHelpText($$$GRIDGetYFELD($$$GRIDGetCurrentYKEY))) // SR15369
 
	if (strHelp="") {
		$$$Alert("WWW00052")  ;No help text
	} else {
		write "with (document.getElementById('Preview')) {"
		write "	style.visibility = 'visible';"
		write " innerHTML = unescape("""_$$$Escape(strHelp)_""");"
		write "}"
	}
 
	quit
	
	
GetDescription(pidRow)
	;-------------------------------------------------------------------------------
	; Get a button description.
	;
	; Params: pidRow		: The row primary key
	;
	; ByRefs:
	;
	; Returns: strName	(The button name)
	;
	; History:
	; 13-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strName,idForm,idLang,idButton,objWWW124
 
	set strName = ""
 
	if $$GetKey^COMUtilClass(pidRow,3,3,.idForm,.idLang,.idButton) {
		set objWWW124 = $get(^WWW124(0,idForm,idLang,idButton,1))
		set strName   = $$$WWW124ButtonDescription(objWWW124)
	}
 
	quit strName
	
	
GetImage(pidRow)
	;-------------------------------------------------------------------------------
	; Return the image file for a button
	;
	; Params: pidRow		: The row primary key
	;
	; ByRefs:
	;
	; Returns: strImage
	;
	; History:
	; 14-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strImage,objWWW124,idForm,idLang,idButton
 
	set strImage = ""
 
	if $$GetKey^COMUtilClass(pidRow,3,3,.idForm,.idLang,.idButton) {
		set objWWW124 = $get(^WWW124(0,idForm,idLang,idButton,1))
		set strImage = $$$WWW124PictureFileOnButton(objWWW124)
		if strImage="" {
			set strImage = $$$LOWER($extract($$$WWW124ButtonDescription(objWWW124)))_".gif"
		}
	}
 
	quit strImage
]]></Routine>
</Export>