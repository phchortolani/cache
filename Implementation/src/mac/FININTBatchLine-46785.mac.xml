<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FININTBatchLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FININTBatchLine
	
#include COMSYS
#include FINConst
#include WWWConst
	
#define HEAD 0
#define LINE 1 
	
PackBlock(pintBlockMajor,pintBlockMinor)
	;-------------------------------------------------------------------------------
	; Turns the two part block number into a single number.  pintBlockMinor may or
	; may not hold a value.
	;
	; Returns:
	;
	; History:
	; 23-Feb-2004	shobby	Creation
	;-------------------------------------------------------------------------------
	new strBlock
	
	set strBlock = $get(pintBlockMajor)
	if $get(pintBlockMinor)'="" {
		set strBlock = strBlock_"."_pintBlockMajor
	}
	quit strBlock
 
 
UnPackBlock(pstrBlock,pintBlockMajor="",pintBlockMinor="")
	;-------------------------------------------------------------------------------
	; Separates the decimal separated block into Major and Minor parts.
	;
	; Returns:
	;
	; History:
	; 23-Feb-2004	shobby	Creation
	;-------------------------------------------------------------------------------
 	set pintBlockMajor = $piece(pstrBlock,".",1)
	set pintBlockMinor = $piece(pstrBlock,".",2)
	quit
 
 
OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Checks whether this item can be deleted.
	; 
	; Notes: (Do not remove the following notes, used by 'Structure')
	; 		**Delete Check** FINAPInvoiceTran - FININTBatchLine can not be deleted.
	; 		**Delete Check** FINARInvoiceTran - FININTBatchLine can not be deleted.
	; 
	; History
	; 10-Mar-2004	shobby	Creation
	;-------------------------------------------------------------------------------
	quit $$$OK
	
	
InterfaceType(pidCompany,pidBatch)
	;-------------------------------------------------------------------------------
	; Returns Interface Batch Type - Used by calculated field
	;
	; Params: idBatch
	;
	; ByRefs: None
	;
	; Returns: Interface Batch Type
	;
	; History:
	; 19-May-2006	PO		SR14569: Fixed for use from external SQL query
	; 27-Feb-2006	PO		SR14225: Created
	;-------------------------------------------------------------------------------
	new objFININTBatch,strResult
 
	set objFININTBatch = $get(^FININTBatch(pidCompany,pidBatch,1))
 
	if $get(YUSER) = "" {
		set strResult = $$$FININTBatchBatchType(objFININTBatch)
	} else {
		set strResult = $listget($$DisplayValue^COMViewFilter("FININTBatch","D"_$$$FldFININTBatchBatchType,$$$FININTBatchBatchType(objFININTBatch)),2)
	}
	quit strResult
	
	
InterfaceLocation(pidCompany,pidBatch)
	;-------------------------------------------------------------------------------
	; Returns Interface Batch Location - Used by calculated field
	;
	; Params: idBlock
	;
	; ByRefs: None
	;
	; Returns: Interface Batch Location
	;
	; History:
	; 19-May-2006	PO		SR14569: Fixed for use from external SQL query
	; 28-Feb-2006	PO		SR14225: Created
	;-------------------------------------------------------------------------------
	new objFININTBatch
 
	set objFININTBatch = $get(^FININTBatch(pidCompany,pidBatch,1))
	quit $$$FININTBatchLocation(objFININTBatch)
	
	
CustomerNumber(pidCompany,pidBatch,pidBlock,pidLine)
	;-------------------------------------------------------------------------------
	; Returns Customer Number - Used by calculated field
	;
	; Params: pidBatch,pidBlock,pidLine
	;
	; ByRefs: None
	;
	; Returns: idCustomerNumber
	;
	; History:
	; 30-Jun-2006	RPW		SR14559: Fixed Customer Number for contra arbank
	; 16-Jun-2006	RPW		SR14559: Fixed ARINV and added ARBANK and ARADJ
	; 19-May-2006	PO		SR14569: Fixed for use from external SQL query
	; 01-Mar-2006	PO		SR14225: Created
	;-------------------------------------------------------------------------------
	new objFININTBatch,objFININTBatchBlock,idSourceRecord,objRecord,idCustomerNumber
	new idBatchType,objFININTBatchLine
	new strSource,idBank,idLine
 
	set idCustomerNumber = ""
	set pidBlock = $piece(pidBlock,".",1)
 
	set objFININTBatch = $get(^FININTBatch(pidCompany,pidBatch,1))
	set idBatchType = $$$FININTBatchBatchType(objFININTBatch)
	if $extract(idBatchType,1,2) = "AR" {
		set objFININTBatchBlock = $get(^FININTBatchBlock(pidCompany,pidBatch,pidBlock,1))
		;$$$FININTBatchBlockSourceForm(objFININTBatchBlock)
		set idSourceRecord = $$$FININTBatchBlockSourceRec(objFININTBatchBlock)
 
		if idSourceRecord '= "" {
			if idBatchType = "ARINV" {
				set objRecord = $get(^FINARInv(pidCompany,idSourceRecord,1)) // SR14559
				set idCustomerNumber = $$$FINARInvCustomer(objRecord)
			} elseif idBatchType="ARBANK" {
				 // SR14559
				set objFININTBatchLine = $get(^FININTBatchLine(pidCompany,pidBatch,pidLine,1))
				set idCustomerNumber = $$$FININTBatchLineReference(objFININTBatchLine)
				set strSource=$$$FININTBatchLineSource(objFININTBatchLine)
				set idBank=$piece(strSource,$$$COMMA,1)
				if idBank=idCustomerNumber {
					set idLine=$piece(strSource,$$$COMMA,2)
					if idLine'="" {
						set idCustomerNumber=$$$FINARBankLineCustomerNumber($get(^FINARBankLine(YM,idBank,$piece(strSource,$$$COMMA,2),1)))
					} else {
						set idCustomerNumber=""
					}
				}
					
				if idCustomerNumber=$$$FININTBatchLineSource(objFININTBatchLine) set idCustomerNumber=""
			} elseif idBatchType="ARADJ" {
				 // SR14559
				set objFININTBatchLine = $get(^FININTBatchLine(pidCompany,pidBatch,pidLine,1))
				set idCustomerNumber = $piece($$$FININTBatchLineReference(objFININTBatchLine),$$$COMMA,1)
			}
		}
	}
	quit idCustomerNumber
	
	
CustomerName(pidCompany,pidBatch,pidBlock,pidLine)
	;-------------------------------------------------------------------------------
	; Returns Customer Name - Used by calculated field
	;
	; Params: pidBatch,pidBlock,pidLine
	;
	; ByRefs: None
	;
	; Returns: Customer Name
	;
	; History:
	; 16-Jun-2006	RPW		SR14559: Fixed call to Customer Number
	; 19-May-2006	PO		SR14569: Fixed for use from external SQL query
	; 01-Mar-2006	PO		SR14225: Created
	;-------------------------------------------------------------------------------
	new objRecord,idCustomerNumber
 
	set objRecord = ""
	set idCustomerNumber = $$CustomerNumber(pidCompany,pidBatch,pidBlock,pidLine) // SR14559
 
	if idCustomerNumber '= "" {
		set objRecord = $get(^FINARCustomer(pidCompany,idCustomerNumber,1))
	}
 
	quit $$$FINARCustomerCustomerName(objRecord) 
	
	
SupplierNumber(pidCompany,pidBatch,pidBlock,pidLine)
	;-------------------------------------------------------------------------------
	; Returns Supplier Name - Used by calculated field
	;
	; Params: pidBatch,pidBlock,pidLine
	;
	; ByRefs: None
	;
	; Returns: idSupplierNumber
	;
	; History:
	; 16-Jun-2006	RPW		SR14559: Readded APADJ and APPAY, fixed APINV
	; 19-May-2006	PO		SR14569: Fixed for use from external SQL query
	; 01-Mar-2006	PO		SR14225: Created
	;-------------------------------------------------------------------------------
	new objFININTBatch,objFININTBatchBlock,objFININTBatchLine,idRecord,objRecord,idSupplierNumber,idSourceRecord ;,strSourceForm
	new idBatchType
 
	set idSupplierNumber = ""
	set pidBlock = $piece(pidBlock,".",1)
 
	set objFININTBatch = $get(^FININTBatch(pidCompany,pidBatch,1))
	set idBatchType = $$$FININTBatchBatchType(objFININTBatch)
	if $extract(idBatchType,1,2) = "AP" {
		set objFININTBatchBlock = $get(^FININTBatchBlock(pidCompany,pidBatch,pidBlock,1))
		;set strSourceForm = $$$FININTBatchBlockSourceForm(objFININTBatchBlock)
		set idSourceRecord = $$$FININTBatchBlockSourceRec(objFININTBatchBlock)
 
		if idSourceRecord '= "" {
			if idBatchType = "APINV" {
				set objRecord = $get(^FINAPInv(pidCompany,idSourceRecord,1))
				set idSupplierNumber = $$$FINAPInvInvoiceSupplier(objRecord) // SR14559
			} elseif idBatchType = "APADJ" {
				set objFININTBatchLine = $get(^FININTBatchLine(pidCompany,pidBatch,pidLine,1)) // SR14559
				set idSupplierNumber = $piece($$$FININTBatchLineParameters(objFININTBatchLine),",",1) // SR14559
			} elseif idBatchType = "APPAY" {
				set objFININTBatchLine = $get(^FININTBatchLine(pidCompany,pidBatch,pidLine,1)) // SR14559
				set idRecord = $$$FININTBatchLineSource(objFININTBatchLine) // SR14559
				set objRecord = $get(^FINAPPaymentLine(pidCompany,$piece(idRecord,",",1),$piece(idRecord,",",2),1)) // SR14559
				set idSupplierNumber = $$$FINAPPaymentLineSupplierNumber(objRecord) // SR14559
			}
		}
	}
 
	quit idSupplierNumber
	
	
SupplierName(pidCompany,pidBatch,pidBlock,pidLine)
	;-------------------------------------------------------------------------------
	; Returns Supplier Name - Used by calculated field
	;
	; Params: pidBatch,pidBlock,pidLine
	;
	; ByRefs: None
	;
	; Returns: Supplier Name
	;
	; History:
	; 19-May-2006	PO		SR14569: Fixed for use from external SQL query
	; 01-Mar-2006	PO		SR14225: Created
	;-------------------------------------------------------------------------------
	new objRecord,idSupplierNumber
 
	set objRecord = ""
	set idSupplierNumber = $$SupplierNumber(pidCompany,pidBatch,pidBlock,pidLine)
	
	if idSupplierNumber '= "" {
		
		set objRecord = $get(^FINAPSupplier(pidCompany,idSupplierNumber,1))
	}
 
	quit $$$FINAPSupplierSupplierName(objRecord)
	
	
GetDescription(pidCompany,pidBatch,pidBlock,pidLine,penumHeaderOrLine)
	;-------------------------------------------------------------------------------
	; Get the description of the header or the line given the batch details
	;
	; Params:
	; pidCompany       : The company to look through
	; pidBatch         : The id of the FININTBatch
	; pidBlock         : The id of the FININTBatchBlock
	; pidLine          : The id of the FININTBatchLine
	; penumHeaderOrLine: Are we after the header or the line object.
	;
	; ByRefs:
	;
	; Returns:
	; The line or header description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new objBlock,idForm,idObject,objForm,idClass,strResult,strSource,strParameters,objLine
	new objBatch,idType,strReference
	
	set $ztrap="GDError"
	set strResult=""
	
	set idClass=""
		
	set objBlock=$get(^FININTBatchBlock(pidCompany,pidBatch,$piece(pidBlock,".",1),1))
	set idForm=$$$FININTBatchBlockSourceForm(objBlock)
	set idObject = $$$FININTBatchBlockSourceRec(objBlock)
	
	if idForm="" {
		set objBatch=$get(^FININTBatch(pidCompany,pidBatch,1))
		set idType=$$$FININTBatchBatchType(objBatch)
		set idForm=$case(idType,"APADJ":"FINAPADJ","APINV":"FINAPInv","ARINV":"FINARInv","ARADJ":"FINARADJ","ARBANK":"FINARBank","GLJNL":"FINGLJournal","GLBR":"FINGLBankRecon",:"")
		set idClass=idForm
	}
	
	quit:idForm="" strResult                     ; *** EARLY EXIT ***
	
	if idClass="" {
		set objForm=$get(^WWW120(0,idForm,1))
		set idClass=$$$WWW120ClassUsedInForm(objForm)
	}
	
	quit:idClass="" strResult                     ; *** EARLY EXIT ***
	
	set objLine=$get(^FININTBatchLine(pidCompany,pidBatch,pidLine,1))
	set strSource=$$$FININTBatchLineSource(objLine)
	set strParameters=$$$FININTBatchLineParameters(objLine)
	set strReference=$$$FININTBatchLineReference(objLine)
	
	xecute "set strResult=$$"_idClass_"(pidCompany,idObject,penumHeaderOrLine,strSource,strParameters,strReference)"
	
	quit strResult
	
GDError ; Internal Tag
	if $get(^Development)'="" quit $get(idClass)_":"_$get(idObject)_":"_$zcvt($zerror,"o","HTML")
	quit ""
	
	
GetMyDescription(pstrClass,pstrLine,pidCompany,pidObject,penumHeaderOrLine,pstrKey,pintKeyPos,pfldDescription)
	;-------------------------------------------------------------------------------
	; Get the description for either the header or the line. This loads the objects and
	; interrogates the data directly.
	;
	; Params:
	; pstrClass        : The class to interrogate
	; pstrLine         : The line class to interrogate
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrKey          : The key of the header or the line
	; pintKeyPos       : Which piece of the key is the line id
	; pfldDescription  : Which fields has the description
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new strDescription,objObject,idLine,objLine
	
	if pfldDescription=0 {
		set strDescription=""
	} else {
		if penumHeaderOrLine=$$$LINE {
			set idLine=$piece(pstrKey,$$$COMMA,pintKeyPos)
		
			if idLine'="" {
				set objLine=$get(@("^"_pstrLine_"("_pidCompany_$$$COMMA_$$^WWWKEYBUILD(pidObject_$$$COMMA_idLine)_$$$COMMA_"1)"))
			} else {
				set objLine=""
			}
			set strDescription=$piece(objLine,"~",pfldDescription) // Can not use Y here, it may not be defined, ie coming from JReport
		} else {
			set objObject=$get(@("^"_pstrClass_"("_pidCompany_$$$COMMA_$$^WWWKEYBUILD(pidObject)_$$$COMMA_"1)"))
			set strDescription=$piece(objObject,"~",pfldDescription) // Can not use Y here, it may not be defined, ie coming from JReport
		}
	}
	
	quit strDescription
	
	
FINAPInv(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a Invoice Match or Supplier Invoice record
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos,strKey
	
	set fldField=$select(penumHeaderOrLine=$$$LINE:$$$FldFINAPInvChargeReference,1:$$$FldFINAPInvNotes)
	set intKeyPos=$select(penumHeaderOrLine=$$$LINE:3,1:1)
	set strKey=$select(penumHeaderOrLine=$$$LINE:pstrParameters,1:pstrSource)
	if pidObject="" set pidObject=$piece(pstrSource,$$$COMMA,1)
	
	quit $$GetMyDescription("FINAPInv","FINAPInvCharge",pidCompany,pidObject,penumHeaderOrLine,strKey,intKeyPos,fldField)
 
	
FINAPADJ(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a Supplier Adjustment
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos
	
	set fldField=$select(penumHeaderOrLine=$$$LINE:$$$FldFINAPADJLineRef,1:$$$FldFINAPADJComment)
	set intKeyPos=2
	if pidObject="" {
		if $piece(pstrSource,$$$COMMA,1)=pstrParameters {
			set pidObject=$piece(pstrReference,$$$COMMA,1)
		} else {
			set pidObject=$piece(pstrSource,$$$COMMA,1)
		}
	}
	
	quit $$GetMyDescription("FINAPADJ","FINAPADJLine",pidCompany,pidObject,penumHeaderOrLine,pstrReference,intKeyPos,fldField)
	
	
FINARInv(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a Customer Invoice
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos
	
	set fldField  = $select(penumHeaderOrLine=$$$LINE:$$$FldFINARInvLineDescription,1:$$$FldFINARInvNotes)
	set intKeyPos = $select(penumHeaderOrLine=$$$LINE:2,1:0)
	if pidObject="" {
		if pstrSource=$piece(pstrParameters,$$$COMMA,2) {
			set pidObject = pstrSource
		} else {
			set pidObject = $piece(pstrReference,$$$COMMA,1)
		}
	}
	quit $$GetMyDescription("FINARInv","FINARInvLine",pidCompany,pidObject,penumHeaderOrLine,pstrReference,intKeyPos,fldField)
	
	
FINARADJ(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a Customer Adjustment
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos
	
	set fldField  = $select(penumHeaderOrLine=$$$LINE:0,1:$$$FldFINARADJComment)
	set intKeyPos = 0
	if pidObject="" set pidObject = $$$KEY1(pstrSource)
	
	quit $$GetMyDescription("FINARADJ","",pidCompany,pidObject,penumHeaderOrLine,"",intKeyPos,fldField)
	
	
FINARBank(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a Customer Banking
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos
	
	set fldField  = $select(penumHeaderOrLine=$$$LINE:$$$FldFINARBankLineChequeName,1:0)
	set intKeyPos = $select(penumHeaderOrLine=$$$LINE:2,1:0)
	if pidObject="" set pidObject = $$$KEY1(pstrSource)
	
	quit $$GetMyDescription("FINARBank","FINARBankLine",pidCompany,pidObject,penumHeaderOrLine,pstrSource,intKeyPos,fldField)
	
	
FINGLJournal(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a General Ledger Journal
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos
	
	set fldField  = $select(penumHeaderOrLine=$$$LINE:$$$FldFINGLJournalLineReference,1:$$$FldFINGLJournalDescription)
	set intKeyPos = $select(penumHeaderOrLine=$$$LINE:2,1:0)
	
	quit $$GetMyDescription("FINGLJournal","FINGLJournalLine",pidCompany,pidObject,penumHeaderOrLine,pstrSource,intKeyPos,fldField)
	
FINGLBankRecon(pidCompany,pidObject,penumHeaderOrLine,pstrSource,pstrParameters,pstrReference)
	;-------------------------------------------------------------------------------
	; Get the descriptions for a Bank Reconciliation
	;
	; Params:
	; pidCompany       : The company to look through
	; pidObject        : The id of the main object
	; penumHeaderOrLine: Are we after the header or the line object.
	; pstrSource       : The source info from the Batch Line
	; pstrParameters   : The parameters info from the Batch Line
	; pstrReference    : The reference info from the Batch Line
	;
	; ByRefs:
	;
	; Returns:
	; The description
	;
	; History:
	; 16-Jun-2006	RPW		SR14569: Created
	;-------------------------------------------------------------------------------
	new fldField,intKeyPos
	
	set fldField  = $select(penumHeaderOrLine=$$$LINE:$$$FldFINGLBankReconLineReference,1:$$$FldFINGLBankReconNotes)
	set intKeyPos = $select(penumHeaderOrLine=$$$LINE:2,1:0)
	
	quit $$GetMyDescription("FINGLBankRecon","FINGLBankReconLine",pidCompany,pidObject,penumHeaderOrLine,pstrParameters,intKeyPos,fldField)
	
]]></Routine>
</Export>