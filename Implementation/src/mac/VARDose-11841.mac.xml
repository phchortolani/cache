<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARDose" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include VARConst
#include MEDConst
#include VARDose

VARDose
	quit
	
OnBeforeButtonLine()
	set YOPTION = $get(YSEITE)
	
	if (YOPTION = $$$abaMedicamentosPrescritos){
		set VORG(2)		= $piece(YPARA,",",2)
		set VORG(3)		= $piece(YPARA,",",3)
		set VORG(4)		= $piece(YPARA,",",4)
		set VORG(5)		= $piece(YPARA,",",5)
		set VORG(6)		= $piece(YPARA,",",6)
	}
	elseif (YOPTION = $$$abaHistoricoAtendimento) {
		set VORG(7)		= $piece(YPARA,",",7)
		set VORG(8)		= $piece(YPARA,",",8)
		set VORG(9)		= $piece(YPARA,",",9)
		set VORG(10)	= $piece(YPARA,",",10)
		set VORG(11)	= $piece(YPARA,",",11)
	}
	quit

OnAfterButtonLine(pYPARA)
	new idMEDPatient, urlFile, objMEDPatient, strDOB, intAge, strNome,
		idMEDAdmission, objMEDAdmission, Location, Room, Bed, DateAdmitted,
		MedAtivo, DoseAtiva, DoseAtrasada, SeNecessario, DoseOmitida, 
		DoseSuspensa, TimeAdmitted
	
	if ($length(pYPARA) = 0) quit
	set idMEDPatient = $piece(pYPARA,",",1)
	
	set ^CacheTempDose(YBED,YUSER,"idMEDPatient") = idMEDPatient
	
	set urlFile = $$LoadPicture^VARMEDPatient(idMEDPatient)
	set objMEDPatient = ^MEDPatient(0,idMEDPatient,1)
	set strDOB 		= $$$MEDPatientDOB(objMEDPatient)
	set strNome 	= $$$MEDPatientName(objMEDPatient)
	set strAlergias = $$$MEDPatientAllergies(objMEDPatient)
	
	set intAge = ($horolog - strDOB)\365
	if (intAge = 1) {
		set intAge = intAge_" ano"
	} else {
		set intAge = intAge_" anos"
	}
	
	set idMEDAdmission = $$GetOpenAdmission^MEDAdmission(idMEDPatient)
	quit:(idMEDAdmission = "")
	
	set objMEDAdmission = $get(^MEDAdmission(YM,idMEDAdmission,1))
	
	set Location	 = $$$MEDAdmissionLocation(objMEDAdmission)
	set Room		 = $$$MEDAdmissionRoom(objMEDAdmission)
	set Bed			 = $$$MEDAdmissionBed(objMEDAdmission)
	set DateAdmitted = $$$MEDAdmissionDateAdmitted(objMEDAdmission)
	set TimeAdmitted = $$$MEDAdmissionTimeAdmitted(objMEDAdmission)
	
	&sql(
	SELECT
			  (SELECT COUNT(1) FROM Report.VARDose Dose1 WHERE Dose1.Paciente = :idMEDPatient AND Dose1.LinhaPrescricaoStatus <> $$$PrescricaoSuspensa) AS MedAtivo
			, (SELECT COUNT(1) FROM Report.VARDose Dose2 WHERE Dose2.Paciente = :idMEDPatient AND Dose2.Situacao = $$$SituacaoPronto AND Dose2.LinhaPrescricaoStatus <> $$$PrescricaoSuspensa) AS DoseAtiva
			, (SELECT COUNT(1) FROM Report.VARDose Dose3 WHERE Dose3.Paciente = :idMEDPatient AND Dose3.Situacao = $$$SituacaoAtrasado AND Dose3.LinhaPrescricaoStatus <> $$$PrescricaoSuspensa) AS DoseAtrasada
			, (SELECT COUNT(1) FROM Report.VARDose Dose4 WHERE Dose4.Paciente = :idMEDPatient AND Dose4.Situacao = $$$SituacaoSeNecessario AND Dose4.LinhaPrescricaoStatus <> $$$PrescricaoSuspensa) AS SeNecessario
			, (	SELECT COUNT(1) FROM SQLUser.VARDose
				INNER JOIN SQLUser.MEDprescription
					ON MEDprescription.PrescriptionNumber = VARDose.Prescricao
				INNER JOIN SQLUser.MEDPrescriptionLine
					ON MEDPrescriptionLine.PrescriptionNumber = MEDPrescription.PrescriptionNumber
					AND MEDPrescriptionLine.Line = VARDose.Linha
				WHERE VARDose.Status = $$$VARDoseStatusOmitida
				) AS DoseOmitida
			, (	SELECT COUNT(1) FROM SQLUser.MEDprescription
				INNER JOIN SQLUser.MEDPrescriptionLine
					ON MEDPrescriptionLine.PrescriptionNumber = MEDPrescription.PrescriptionNumber
				WHERE MEDPrescriptionLine.Status = $$$PrescricaoSuspensa
				) AS DoseSuspensa
	INTO
			  :MedAtivo
			, :DoseAtiva
			, :DoseAtrasada
			, :SeNecessario
			, :DoseOmitida
			, :DoseSuspensa
	FROM VARDual
	)

	&html<
		<script type='text/javascript'>
			function openModal(url){
				var janelaModalBody = document.createElement('div');
				janelaModalBody.setAttribute('id', 'janelaModalBody');
				janelaModalBody.style.width = '100%';
				janelaModalBody.style.height = '100%';
				janelaModalBody.style.zIndex = '10';
				janelaModalBody.style.position = 'absolute';
				janelaModalBody.style.top = '0';
				janelaModalBody.style.left = '0';
				janelaModalBody.style.margin = '0';

				var janelaModalShadow = document.createElement('div');
				janelaModalShadow.setAttribute('id', 'janelaModalShadow');
				janelaModalShadow.style.width = '100%';
				janelaModalShadow.style.height = '100%';
				janelaModalShadow.style.backgroundColor = '#000000';
				janelaModalShadow.style.zIndex = '20';
				janelaModalShadow.style.position = 'absolute';
				janelaModalShadow.style.top = '0';
				janelaModalShadow.style.left = '0';
				janelaModalShadow.style.margin = '0';
				janelaModalShadow.style.opacity = '0.3';
				janelaModalShadow.style.filter = 'alpha(opacity=30)';

				var janelaModal = document.createElement('div');
				janelaModal.setAttribute('id', 'janelaModal');
				janelaModal.style.border = '1px solid #000000';
				janelaModal.style.backgroundColor = 'gainsboro';
				janelaModal.style.width = '610px';
				janelaModal.style.height = '440px';
				janelaModal.style.zIndex = '30';
				janelaModal.style.position = 'absolute';
				janelaModal.style.top = '50%';
				janelaModal.style.left = '50%';
				janelaModal.style.margin = '-220px 0 0 -305px';


				var frameModal = document.createElement('iframe');
				frameModal.setAttribute('id', 'frameModal');
				frameModal.src = url;
				frameModal.style.width = '100%';
				frameModal.style.height = '100%';
				frameModal.marginwidth = '0';
				frameModal.marginheight = '0';
				frameModal.frameBorder = 0;

				janelaModal.appendChild(frameModal);
				janelaModalBody.appendChild(janelaModalShadow);
				janelaModalBody.appendChild(janelaModal);
				document.body.appendChild(janelaModalBody);

			}
	
			function closeModal() {
				document.body.removeChild(document.body.lastChild);
			}

			function dosePrompt(barCode, idDose, idVARDoseAdministracao) { 
				eval('var url ="#(YAKTION)#EP=WWWFORM&YFORM=VARDoseAdministracao&YPARA=#(idMEDPatient)#,'+barCode+','+idDose+','+idVARDoseAdministracao+'&YUSER=#(YUSER)#&YBED=#(YBED)#"'); 
				openModal(url);
			}
			
			function reloadPage(idPaciente) {
				CallBackNow("GoToForm^COMUtils", "VARDose","","",idPaciente);
			}
			
			function VisualizaOmitir(idAdministracao, idLinha, idMEDPatient) {
				eval('var url ="#(YAKTION)#EP=WWWFORM&YFORM=VARDoseAdministracao&YPARA='+idMEDPatient+','+idAdministracao+','+idLinha+'&YUSER=#(YUSER)#&YBED=#(YBED)#&YSEITE=2"'); 
				openModal(url);
			}
		</script>

		<br/>
		<div style='width:100%'>
			<div style='float:left'>
				<div style='float:left;margin-left:10px'>
					<img src='#(urlFile)#' width='80' height='100' >
				</div>
				<div style='float:left; width:690px'>
					
					<table width="100%" style="font-size:13px;margin-left:10px;margin-bottom:5px">
						<tr>
							<td colspan="4"><a onclick="subWindow('#(YAKTION)#EP=WWWFORM&YFORM=MEDPatient&YKEY=#(idMEDPatient)#&YUSER=#(YUSER)#&YBED=#(YBED)#','1'); return false;" href="#"><strong>#(strNome)#</strong></a></td>
						</tr>						
						<tr>
							<td width="75"><em>Nascimento:</em></td>
							<td>#($zdate(strDOB,4,,4))#</td>
							<td align="right"><em>Idade:</em></td>
							<td>#(intAge)#</td>
							<td align="right"><em>Cód. Paciente:</em></td>
							<td><a onclick="subWindow('#(YAKTION)#EP=WWWFORM&YFORM=MEDPatient&YKEY=#(idMEDPatient)#&YUSER=#(YUSER)#&YBED=#(YBED)#','1'); return false;" href="#">#(idMEDPatient)#</a></td>							
						</tr>
						<tr>
							<td><em>Internação:</em></td>
							<td><a onclick="subWindow('#(YAKTION)#EP=WWWFORM&YFORM=MEDAdmission&YKEY=#(idMEDAdmission)#&YUSER=#(YUSER)#&YBED=#(YBED)#','1'); return false;" href="#">#(idMEDAdmission)#</a></td>
							<td align="right"><em>Data de Admissão:</em></td>
							<td>#($zdate(DateAdmitted,4,,4))#</td>
							<td align="right"><em>Hora:</em></td>
							<td>#($ztime(TimeAdmitted,2))#</td>
						</tr>
						<tr>
							<td><em>Unidade:</em></td>
							<td>#($$SQLGetLocationName^VARSQL(Location))#</td>
							<td align="right"><em>Quarto:</em></td>
							<td>#(Room)#</td>
							<td align="right"><em>Leito:</em></td>
							<td>#(Bed)#</td>
						</tr>				
						<tr>
							<td><em><font color="red">Alergias:</font></em></td>
							<td colspan="3">#(strAlergias)#&nbsp;</td>
						</tr>
					</table>
				</div>
			</div>
		</div>
	>
	if (YSEITE = $$$abaMedicamentosAtivos) {
		&html<
			<div style='float:left; width:760px; margin:10px 10px 0px 10px;font-size:13px'>
				<fieldset style="border-color:black;border-width:1px;padding-bottom:10px">
					<legend style="padding-left:2px;padding-right:2px;font-size:13px;"><b>Status Atual - Administração Medicamentosa</b></legend>
						<div style='margin:10px'>
							<div style='float:left; margin:0px 0px 10px 60px; width:150px;'><strong>Med. Ativos:</strong> #(MedAtivo)#</div>
							<div style='float:left; margin:0px 0px 10px 60px; width:150px;'><strong>S/N:</strong> <label style='color:#FF9900; font-weight:bold'>#(SeNecessario)#</label></div>
							<div style='float:left; margin:0px 0px 10px 60px; width:150px;'><strong>Doses Omitidas:</strong> #(DoseOmitida)#</div>
						</div>
						<div style='margin:10px;'>
							<div style='float:left; margin:0px 0px 10px 60px; width:150px;'><strong>Doses Ativas:</strong> <label style='color:#006600; font-weight:bold'>#(DoseAtiva)#</label></div>
							<div style='float:left; margin:0px 0px 10px 60px; width:150px;'><strong>Em Atraso:</strong> <label style='color:#990000; font-weight:bold'>#(DoseAtrasada)#</label></div>
							<div style='float:left; margin:0px 0px 10px 60px; width:150px;'><strong>Suspensos:</strong> #(DoseSuspensa)#</div>
						</div>
				</fieldset>
			</div>
		>
	}
	&html<
		<div style='float:left; width:760px; margin:10px 0px 10px 10px'>
	>
	// essas DIV's são fechadas no fim do método OnAfterDataField
	
	quit

OnAfterDataField(pYPARA)
	new lstHeader, Situacao, Medicamento, Dose, Via, Frequencia, Inicio, 
		UltimaDose, ProximaDose, Prescricao, Linha, idMEDPatient, KeyDose,
		objUltimaDose, temp, strSQL, rs, AdmissaoAtual, Termino, Status,
		strSQL, strWhere, dtAdministracao, count
	
	if ($length(pYPARA) = 0) quit
	set idMEDPatient = $piece(pYPARA,",",1)
	
 	set lstHeader = ""
  	
  	if (YSEITE = $$$abaMedicamentosAtivos) {
	  	set lstHeader = lstHeader_$listbuild("Situação","Medicamento","Dose","Via","Frequência","Início","Última Dose","Próxima Dose","Info.","Prescrição")
		do Start^COMTable(lstHeader)
	
		&sql(
		DECLARE Dose CURSOR FOR
		SELECT
			  Dose.Situacao
			, "("||Dose.Item||") "||$$SQLGetDescricaoProduto^VARSQL(Dose.Item)
			, Dose.QtdDose||" "||$$SQLGetSiglaUnit^VARSQL(Dose.Unidade)
			, $$GetVia^VARSQL(Dose.Via)
			, $$GetFrequency^VARSQL(Dose.Frequencia)
			, $$GetInicio^VARDose(Dose.Prescricao, Dose.Linha)
			, Dose.UltimaDose
			, Dose.ProximaDose
			, Dose.Prescricao
			, Dose.Linha
			, Dose.Dose
		INTO
			  :Situacao
			, :Medicamento
			, :Dose
			, :Via
			, :Frequencia
			, :Inicio
			, :UltimaDose
			, :ProximaDose
			, :Prescricao
			, :Linha
			, :KeyDose
		FROM Report.VARDose Dose
		WHERE
			Dose.Paciente = :idMEDPatient
			AND Dose.LinhaPrescricaoStatus <> $$$PrescricaoSuspensa
		ORDER BY
			$piece(Dose.ProximaDose,",",1)
			,$piece(Dose.ProximaDose,",",2)
		)
	
		&sql(OPEN Dose)
		&sql(FETCH Dose)
		
		set count = 0
		
		while (SQLCODE = 0){
			if (UltimaDose '= "-"){
				set UltimaDose = $zdatetime(UltimaDose,4,2)
				set UltimaDose = $piece(UltimaDose," ",1)_"<br/>"_$piece(UltimaDose," ",2)
				set temp = $$GetUltimaDose(Prescricao,Linha,KeyDose,.objUltimaDose)
				if ($$$VARDoseStatus(objUltimaDose) = $$$VARDoseStatusOmitida) {
					set UltimaDose = $$linkVisualizaOmitir(UltimaDose,$$$VARDoseAdministracao(objUltimaDose),$$$VARDoseAdministracaoLinha(objUltimaDose),idMEDPatient)
				}
			}
			
			set count = $increment(count)
			
			do NewLine^COMTable()
			do InsertCell^COMTable($$GetIconSituacao(Situacao),,,,"center")
			do InsertCell^COMTable(Medicamento)
			do InsertCell^COMTable(Dose)
			do InsertCell^COMTable(Via,,,,"center")
			do InsertCell^COMTable(Frequencia,,,,"center")
			do InsertCell^COMTable($piece(Inicio," ",1)_"<br/>"_$piece(Inicio," ",2),,,,"center")
			do InsertCell^COMTable(UltimaDose,,,,"center")
			do InsertCell^COMTable($piece($zdatetime(ProximaDose,4,2)," ",1)_"<br/>"_$piece($zdatetime(ProximaDose,4,2)," ",2),,,,"center")
			do InsertCell^COMTable($$GetInfo(Prescricao,Linha),,,,"center")
			do InsertCell^COMTable("<a onclick=""subWindow('"_YAKTION_"EP=WWWFORM&YFORM=MEDPrescriptionHosp&YKEY="_Prescricao_"&YUSER="_YUSER_"&YBED="_YBED_"','1'); return false;"" href="""">"_Prescricao_"</a>",,,,"center") //link montado manualmente para o YBACK ficar em branco, e evitar que o botão 'voltar' fique abilitado
			do EndLine^COMTable()
			&sql(FETCH Dose)

		}
		
		if (count = 0) {
			do NewLine^COMTable("white")
			do InsertCell^COMTable("Nenhum medicamento ativo para este paciente",,,,,,10)
			do EndLine^COMTable()
		}
		
		&sql(CLOSE Dose)
		do Stop^COMTable()

  	}
  	elseif (YSEITE = $$$abaMedicamentosPrescritos) {
	  	set lstHeader = lstHeader_$listbuild("Medicamento","Dose","Via","Frequência","Início","Prescrição","Status","Info.")
		do Start^COMTable(lstHeader)
		
		set AdmissaoAtual	= $piece(pYPARA,",",2)
		set Inicio			= $piece(pYPARA,",",3)
		set Termino			= $piece(pYPARA,",",4)
		set Medicamento		= $piece(pYPARA,",",5)
		set Status			= $piece(pYPARA,",",6)
	  	
		set strSQL	= 	" SELECT "_
						"		'('||MEDPrescriptionLine.Item||') '||$$SQLGetDescricaoProduto^VARSQL(MEDPrescriptionLine.Item) AS Medicamento "_
						"		, MEDPrescriptionLine.DoseQty||' '||$$SQLGetSiglaUnit^VARSQL(MEDPrescriptionLine.DoseUOM) AS Dose "_
						"		, $$GetVia^VARSQL(MEDPrescriptionLine.RouteOfAdministration) AS Via "_
						"		, $$GetFrequency^VARSQL(MEDPrescriptionLine.Frequency) AS Frequencia "_
						"		, $$GetInicio^VARDose(MEDPrescriptionLine.PrescriptionNumber,MEDPrescriptionLine.Line) AS Inicio "_
						"		, MEDPrescriptionLine.PrescriptionNumber AS Prescricao "_
						"		, $$GetStatusPrescriptionLine^VARSQL(MEDPrescriptionLine.Status) AS Status "_
						"		, $$GetInfo^VARDose(MEDPrescriptionLine.PrescriptionNumber,MEDPrescriptionLine.Line) AS Info "_
						" FROM	SQLUser.MEDPrescriptionLine "_
						" INNER JOIN MEDPrescription "_
						"	ON MEDPrescriptionLine.PrescriptionNumber = MEDPrescription.PrescriptionNumber "_
						" WHERE "_
						"		MEDPrescription.PatientID = "_idMEDPatient
		set strWhere = ""
				
		if ($length(Inicio) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.FromDate >= "_Inicio_" "
		}
		
		if ($length(Termino) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.FromDate <= "_Termino_" "
		}
		
		if ($length(Medicamento) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.Item = "_Medicamento_" "
		}
		
		if ($length(Status) '= 0){
			set strWhere = strWhere_" AND NVL(MEDPrescriptionLine.Status,0) = "_Status_" "
		}
		
		set strSQL = strSQL_strWhere
		
		set rs = ##class(%ResultSet).%New()
		do rs.Prepare(strSQL)
		do rs.Execute()
		
		if (rs.Next() = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,8)
			do EndLine^COMTable()
		}
		else {
			for {
				do NewLine^COMTable()
				do InsertCell^COMTable(rs.Get("Medicamento"))
				do InsertCell^COMTable(rs.Get("Dose"),,,,"center")
				do InsertCell^COMTable(rs.Get("Via"),,,,"center")
				do InsertCell^COMTable(rs.Get("Frequencia"),,,,"center")
				do InsertCell^COMTable(rs.Get("Inicio"),,,,"center")
				do InsertCell^COMTable("<a onclick=""subWindow('"_YAKTION_"EP=WWWFORM&YFORM=MEDPrescriptionHosp&YKEY="_rs.Get("Prescricao")_"&YUSER="_YUSER_"&YBED="_YBED_"','1'); return false;"" href="""">"_rs.Get("Prescricao")_"</a>",,,,"center") //link montado manualmente para o YBACK ficar em branco, e evitar que o botão 'voltar' fique abilitado)
				do InsertCell^COMTable(rs.Get("Status"),,,,"center")
				do InsertCell^COMTable(rs.Get("Info"),,,,"center")
				do EndLine^COMTable()
				if (rs.Next() = 0) quit
		  	}
		}
		do Stop^COMTable()
		do rs.Close()

  	}
  	elseif (YSEITE = $$$abaHistoricoAtendimento) {
	  	set lstHeader = lstHeader_$listbuild("Status","&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Medicamento&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;","Dose","Via","Frequência","Data&nbsp;&nbsp;<BR/>&nbsp;Prescrita","Data&nbsp;&nbsp;<BR/>&nbsp;&nbsp;Administração","&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Atendente&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;","&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Obs.&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;","Info.","Prescrição")
		do Start^COMTable(lstHeader)
		
		set AdmissaoAtual	= $piece(pYPARA,",",7)
		set Inicio			= $piece(pYPARA,",",8)
		set Termino			= $piece(pYPARA,",",9)
		set Medicamento		= $piece(pYPARA,",",10)
		set Status			= $piece(pYPARA,",",11)
	  	
		set strSQL	= 	" SELECT "_
						"		$$GetIconStatusAdministracao^VARDose($$getStatusAdministracao^VARDose(VARDose.Prescricao,VARDose.Linha,VARDose.Dose)) AS Status "_
						"		, '('||MEDPrescriptionLine.Item||') '||$$SQLGetDescricaoProduto^VARSQL(MEDPrescriptionLine.Item) AS Medicamento "_
						"		, MEDPrescriptionLine.DoseQty||' '||$$SQLGetSiglaUnit^VARSQL(MEDPrescriptionLine.DoseUOM) AS Dose "_
						"		, $$GetVia^VARSQL(MEDPrescriptionLine.RouteOfAdministration) AS Via "_
						"		, $$GetFrequency^VARSQL(MEDPrescriptionLine.Frequency) AS Frequencia "_
						"		, VARDose.PrevisaodeAdministracao AS DtPrescrita "_
						"		, VARDose.Administradoem AS DtAdministracao "_
						"		, VARDoseAdministracao.Enfermeiro AS Atendente "_
						"		, VARDoseAdministracaoLinha.Observacao AS Observacao "_
						"		, $$GetInfo^VARDose(MEDPrescriptionLine.PrescriptionNumber,MEDPrescriptionLine.Line) AS Info "_
						"		, MEDPrescriptionLine.PrescriptionNumber AS Prescricao "_
						" FROM	VARDose "_
						" INNER JOIN MEDPrescriptionLine "_
						"	ON MEDPrescriptionLine.PrescriptionNumber = VARDose.Prescricao "_
						"	AND MEDPrescriptionLine.Line = VARDose.Linha "_
						" INNER JOIN MEDPrescription "_
						"	ON MEDPrescriptionLine.PrescriptionNumber = MEDPrescription.PrescriptionNumber "_						
						" LEFT JOIN VARDoseAdministracao "_
						"	ON VARDoseAdministracao.Administracao = VARDose.Administracao "_
						" LEFT JOIN VARDoseAdministracaoLinha "_
						"	ON VARDoseAdministracaoLinha.Administracao = VARDose.Administracao "_
						"	AND VARDoseAdministracaoLinha.Linha = VARDose.AdministracaoLinha "_
						" WHERE "_
						"		MEDPrescription.PatientID = "_idMEDPatient
		set strWhere = ""
		
		if ($length(Termino) '= 0){
			set strWhere = strWhere_" AND $piece(VARDose.PrevisaodeAdministracao,"","",1) <= "_Termino_" "
		}
		
		if ($length(Medicamento) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.Item = "_Medicamento_" "
		}
		
		if ($length(Status) '= 0){
			set strWhere = strWhere_" AND $$getStatusAdministracao^VARDose(VARDose.Prescricao,VARDose.Linha,VARDose.Dose) = "_Status_" "
		}
		
		set strSQL = strSQL_strWhere_
						" ORDER BY "_
						"		$piece(VARDose.Administradoem,"","",1) DESC"_
						"		, $piece(VARDose.Administradoem,"","",2) DESC"_
						"		, $piece(VARDose.PrevisaodeAdministracao,"","",1) ASC"_
						"		, $piece(VARDose.PrevisaodeAdministracao,"","",2) ASC"_
						"		, MEDPrescriptionLine.Item DESC"
		
		set rs = ##class(%ResultSet).%New()
		do rs.Prepare(strSQL)
		do rs.Execute()
		
		if (rs.Next() = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,11)
			do EndLine^COMTable()
		}
		else {
			for {
				set dtAdministracao = ""
				if (rs.Get("DtAdministracao") '= "") {
					set dtAdministracao = $zdatetime(rs.Get("DtAdministracao"),4,2)
				}
				do NewLine^COMTable()
				do InsertCell^COMTable(rs.Get("Status"),,,,"center")
				do InsertCell^COMTable(rs.Get("Medicamento"),,,,,,,,,$$$YES)
				do InsertCell^COMTable(rs.Get("Dose"),,,,"center")
				do InsertCell^COMTable(rs.Get("Via"),,,,"center")
				do InsertCell^COMTable(rs.Get("Frequencia"),,,,"center")
				do InsertCell^COMTable($zdatetime(rs.Get("DtPrescrita"),4,2),,,,"center")
				do InsertCell^COMTable(dtAdministracao,,,,"center")
				do InsertCell^COMTable($$SQLGetUserName^VARSQL(rs.Get("Atendente")),,,,"center",,,,,$$$YES)
				do InsertCell^COMTable(rs.Get("Observacao"),,,,,,,,,$$$YES)
				do InsertCell^COMTable(rs.Get("Info"),,,,"center")
				do InsertCell^COMTable("<a onclick=""subWindow('"_YAKTION_"EP=WWWFORM&YFORM=MEDPrescriptionHosp&YKEY="_rs.Get("Prescricao")_"&YUSER="_YUSER_"&YBED="_YBED_"','1'); return false;"" href="""">"_rs.Get("Prescricao")_"</a>",,,,"center") //link montado manualmente para o YBACK ficar em branco, e evitar que o botão 'voltar' fique abilitado)
				do EndLine^COMTable()
				if (rs.Next() = 0) quit
		  	}
		}
		do Stop^COMTable()
		do rs.Close()

	}
	
	// fechamento das DIV's abertas no método OnAfterButtonLine
	&html<
				<br/>
			</div>
		</div>
	>
	
	quit

CreateDose(pidPrescription)
	new objMEDPrescriptionLine, objMEDPrescription, idLine, idDose, keyDose, objDose, 
		intFrequency, count, frequencyHour, frequencyConversion, doseAtual, doseInicial, strStatus,
		duracao, InicioAdm, FimAdm, intFrequencyIntConvert, diffTermino
	
	set idLine = ""
	set strStatus = $$$OK
	set objMEDPrescription = ^MEDPrescription(YM,pidPrescription,1)
	
	//Dose só são administradas diretamente ao paciente para prescrições do tipo Emergencial (Emergency) ou Internado (Inpatient)
	;if ($$$MEDPrescriptionIssueType(objMEDPrescription) = "I") || ($$$MEDPrescriptionIssueType(objMEDPrescription) = "E") {
		for {
			set idLine = $order(^MEDPrescriptionLine(YM,pidPrescription,idLine))
			if ($$$NoKey(idLine)) quit

			set objMEDPrescriptionLine = ^MEDPrescriptionLine(YM,pidPrescription,idLine,1)
			set doseInicial = $$$MEDPrescriptionLineFromTime(objMEDPrescriptionLine)
			if ($length(doseInicial) = 0) continue //Verifica se existe hora inicial marcada para essa prescrição

			set doseAtual = $zdatetimeh($zdatetime($$$MEDPrescriptionLineFromDate(objMEDPrescriptionLine)_$$$COMMA_doseInicial))

			set frequencyConversion = $$$MEDFrequencyConversion(^MEDFrequency(YM,$$$MEDPrescriptionLineFrequency(objMEDPrescriptionLine),1)) //conversão para vezes ao dia

			set InicioAdm = $$$MEDPrescriptionLineFromDate(objMEDPrescriptionLine)_","_$$$MEDPrescriptionLineFromTime(objMEDPrescriptionLine)
			set FimAdm    = $$$MEDPrescriptionLineToDate(objMEDPrescriptionLine)  _","_$$$MEDPrescriptionLineToTime(objMEDPrescriptionLine)

			if ($$$MEDPrescriptionLineSolution(objMEDPrescriptionLine) && $$$MEDPrescriptionLineContinuous(objMEDPrescriptionLine)){
				do ##class(VAR.infra.util.DateTime).getHorologDiff("mi",InicioAdm,FimAdm,.duracao)
				set duracao = duracao/60 // duração da dose em horas
				set intFrequency = duracao  / $$$MEDPrescriptionLineInfusionTime(objMEDPrescriptionLine)
				set frequencyHour = $$$MEDPrescriptionLineInfusionTime(objMEDPrescriptionLine)
			} else {	
				do ##class(VAR.infra.util.DateTime).getHorologDiff("hh",InicioAdm,FimAdm,.duracao)
				set duracao = 24 // duração da dose em horas
				set intFrequency = frequencyConversion * $$$MEDPrescriptionLineDuration(objMEDPrescriptionLine) //multiplica pela quantidade de dias
				set frequencyHour = duracao / frequencyConversion //recupera qual a frequência em horas
			}
			
			set intFrequencyIntConvert = ##class(%SYSTEM.SQL).CONVERT((intFrequency),"SQL_INTEGER")
			if ((intFrequency > intFrequencyIntConvert) && (intFrequency < intFrequencyIntConvert+1)){
				set intFrequency = intFrequencyIntConvert+1 // arredonda total de doses para cima
			}
			
			for idDose=1:1:intFrequency {
				set keyDose = pidPrescription_$$$COMMA_idLine_$$$COMMA_idDose
				set $$$VARDoseStatus(objDose) = $$$VARDoseStatusAguardando
				set $$$VARDosePrevisaodeAdministracao(objDose) = doseAtual
				do ##class(VAR.infra.util.DateTime).getHorologDiff("mi",doseAtual,FimAdm,.diffTermino)
				quit:(diffTermino < 0) // Não cria  doses que passam do horário de término
				set doseAtual = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("mi",frequencyHour*60,doseAtual),3,1)
				set strStatus = $$$Save("VARDose",keyDose,objDose,1)
				if ($$$ISERR(strStatus)) quit
			}
			if ($$$ISERR(strStatus)) quit
		}
	;}
	
	quit strStatus

GetInicio(pidPrescription,pLine)
	new objMEDPrescriptionLine
	set objMEDPrescriptionLine = $get(^MEDPrescriptionLine(0,pidPrescription,pLine,1))
	quit $zdatetime($$$MEDPrescriptionLineFromDate(objMEDPrescriptionLine)_$$$COMMA_$$$MEDPrescriptionLineFromTime(objMEDPrescriptionLine),4,2)

GetUltimaDose(pidPrescription,pLine,pDose,&objDose = "")
	new idUltima
	if ((pidPrescription="")||(pLine="")||(pDose="")) quit ""
	
	set idUltima = $order(^VARDose(0,pidPrescription,pLine,pDose),-1)
	if (idUltima="") quit "-"
	set objDose = ^VARDose(0,pidPrescription,pLine,idUltima,1)
		
	quit $$$VARDoseAdministradoem(objDose)

GetSituacao(pidPrescription,pLine,pDose,dataHora = "")
	new objDose, timeTolerancia, minTolerancia, maxTolerancia, idSituacao
	if ((pidPrescription="")||(pLine="")||(pDose="")) quit ""
	set idSituacao = $$$SituacaoAtivo

	/* TO DO
	esse trecho deve ser descomentado quando for criada a flag "Apenas se necessário"
	
	set objMEDPrescriptionLine = ^MEDPrescriptionLine(0,pidPrescription,pLine,1)
	if ($$$MEDPrescriptionLineCAMPONOVO(objMEDPrescriptionLine)){
		set idSituacao = $$$SituacaoSeNecessario
	}
	else {
	*/
		set objDose = $get(^VARDose(0,pidPrescription,pLine,pDose,1))
		set timeTolerancia = $$$VARParametroClienteToleranciaDosagem(^VARParametroCliente(0,0,1))
		if (dataHora = "") {
			set dataHora = $horolog
		}
		set minTolerancia = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("minute",-timeTolerancia,$$$VARDosePrevisaodeAdministracao(objDose)),3,1)
		set maxTolerancia = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("minute",timeTolerancia,$$$VARDosePrevisaodeAdministracao(objDose)),3,1)
		if ((##class(VAR.infra.util.DateTime).DateTimeCmp(minTolerancia,dataHora)<0)
			&& (##class(VAR.infra.util.DateTime).DateTimeCmp(maxTolerancia,dataHora)>0)){
			set idSituacao = $$$SituacaoPronto
		}
		elseif (##class(VAR.infra.util.DateTime).DateTimeCmp(maxTolerancia,dataHora)<0) {
			set idSituacao = $$$SituacaoAtrasado
		}
	//}
	
	quit idSituacao

GetIconSituacao(pidSituacao)
	new strIconSituacao
	set strIconSituacao = ""
	if (pidSituacao = $$$SituacaoAtivo){
		set strIconSituacao = ""
	}
	elseif (pidSituacao = $$$SituacaoPronto) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_verde.gif' title='Medicamento pronto para administração'>"
	}
	elseif (pidSituacao = $$$SituacaoAtrasado) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_vermelho.gif' title='Medicamento em atraso'>"
	}
	elseif (pidSituacao = $$$SituacaoSeNecessario) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_amarelo.gif' title='Apenas se necessário'>"
	}
	quit strIconSituacao

ImprimeRelatorio(abaAtual,pYPARA)
	if ((abaAtual="")||(pYPARA="")) quit
	
	new idMEDPatient, AdmissaoAtual, Inicio, Termino, Medicamento, Status, strWhere
	
	set idMEDPatient = $piece(pYPARA,",",1)
	if ($length(idMEDPatient) = 0){
		$$$Alert("É necessário selecionar um paciente.")
		do GoToForm^COMUtilForm("VARDose","",abaAtual,pYPARA)
		quit
	}
	
	//Limpa a global de valores do botão voltar
	kill ^VARTempDose
		
	//Define os valores para o botão de voltar após gerar o relatório
	set ^VARTempDose(0,YBED,1) = pYPARA_Y_abaAtual
	
	if (abaAtual = $$$abaMedicamentosPrescritos){
		set AdmissaoAtual	= $piece(pYPARA,",",2)
		set Inicio			= $piece(pYPARA,",",3)
		set Termino			= $piece(pYPARA,",",4)
		set Medicamento		= $piece(pYPARA,",",5)
		set Status			= $piece(pYPARA,",",6)
	
		set strWhere = ""
		
		if ($length(Inicio) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.FromDate >= "_Inicio_" "
		}
		
		if ($length(Termino) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.FromDate <= "_Termino_" "
		}
		
		if ($length(Medicamento) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.Item = "_Medicamento_" "
		}
		
		if ($length(Status) '= 0){
			set strWhere = strWhere_" AND NVL(MEDPrescriptionLine.Status,0) = "_Status_" "
		}
	
		do RunReportMedicamentosPrescritos^VARJasperRunReport(idMEDPatient,strWhere,AdmissaoAtual,Inicio,Termino,Medicamento,Status)

	} elseif (abaAtual = $$$abaHistoricoAtendimento){
		set idMEDPatient  = $piece(pYPARA,",",1)
		set AdmissaoAtual = $piece(pYPARA,",",7)
		set Inicio		  = $piece(pYPARA,",",8)
		set Termino		  = $piece(pYPARA,",",9)
		set Medicamento	  = $piece(pYPARA,",",10)
		set Status		  = $piece(pYPARA,",",11)

		set strWhere = ""

		if ($length(Inicio) '= 0){
			set strWhere = strWhere_" AND $piece(VARDose.PrevisaodeAdministracao,"","",1) >= "_Inicio_" "
		}
		
		if ($length(Termino) '= 0){
			set strWhere = strWhere_" AND $piece(VARDose.PrevisaodeAdministracao,"","",1) <= "_Termino_" "
		}
		
		if ($length(Medicamento) '= 0){
			set strWhere = strWhere_" AND MEDPrescriptionLine.Item = "_Medicamento_" "
		}
		
		if ($length(Status) '= 0){
			set strWhere = strWhere_" AND $$getStatusAdministracao^VARDose(VARDose.Prescricao,VARDose.Linha,VARDose.Dose) = "_Status_" "
		}

		do RunReportHistoricoAtendimento^VARJasperRunReport(idMEDPatient,strWhere,AdmissaoAtual,Inicio,Termino,Medicamento,Status)
	}
	
	quit

GetInfo(pidPrescription,pLine)
	new objMEDPrescriptionLine
	if ((pidPrescription="")||(pLine="")) quit ""
	set objMEDPrescriptionLine = ^MEDPrescriptionLine(0,pidPrescription,pLine,1)
	if ($length($$$MEDPrescriptionLineRemarks(objMEDPrescriptionLine))=0) quit ""

	quit "<img width='20' height='20' src='"_YGIF_"botao_dose_info.gif' title='"_$$$MEDPrescriptionLineRemarks(objMEDPrescriptionLine)_"'>"
	
SuspenderDoses(pidPrescricao,pidPrescricaoLinha)
	if ((pidPrescricao="")||(pidPrescricaoLinha="")) quit $$$NO
	
	new objDose,idDose,strStatus
	set idDose = ""
	set strStatus = $$$OK
	
	for{
		set idDose = $order(^VARDose(0,pidPrescricao,pidPrescricaoLinha,idDose))
		quit:idDose=""
		
		set objDose = $get(^VARDose(0,pidPrescricao,pidPrescricaoLinha,idDose,1))
		set $$$VARDoseStatus(objDose) = $$$VARDoseStatusSuspensa
		set strStatus = $$$Save("VARDose",pidPrescricao_$$$COMMA_pidPrescricaoLinha_$$$COMMA_idDose,objDose,1)
	}
	
	quit strStatus
	
OnBlurMedicamento(idVARDoseAdministracao="")
	new barCode, Item, Lote, Situacao, Validade, ProximaDose, 
		QtdDose, Unidade, UltimaDose, ProximaDose, LinhaPrescricaoStatus, 
		LocalLote, Dose, Prescricao, Linha, QtdLote, idMEDPatient, strStatus
		
	set idMEDPatient = ^CacheTempDose(YBED,YUSER,"idMEDPatient")
	set barCode = $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",YLFN)
	set Item = $piece(barCode,"@",1)
	set Lote = $piece(barCode,"@",2)
	set strStatus = $$$OK
	
	if ($length(barCode)) {
		if ($data(^INART(0,Item,1))) {
			&sql(
			DECLARE Administracao CURSOR FOR
			SELECT
				  Dose.Dose
				, Dose.Prescricao
				, Dose.Linha
				, Dose.Situacao
				, Bundle->UseByDate
				, Dose.ProximaDose
				, Dose.QtdDose
				, Dose.Unidade
				, Dose.UltimaDose
				, Dose.ProximaDose
				, MEDPrescriptionLine.Status AS LinhaPrescricaoStatus
				, CASE WHEN ISNULL(Storage->Location,0) = 0 THEN :YLOCATION ELSE Storage->Location END AS LocalLote

			INTO
				  :Dose
				, :Prescricao
				, :Linha
				, :Situacao
				, :Validade
				, :ProximaDose
				, :QtdDose
				, :Unidade
				, :UltimaDose
				, :ProximaDose
				, :LinhaPrescricaoStatus
				, :LocalLote

			FROM MEDPrescriptionLine
			INNER JOIN alSOH.dBundleStock Bundle
				ON Bundle.Item = MEDPrescriptionLine.Item
			INNER JOIN MEDPrescription
				ON MEDPrescriptionLine.PrescriptionNumber = MEDPrescription.PrescriptionNumber
			RIGHT JOIN Report.VARDose Dose
				ON Dose.Item = MEDPrescriptionLine.Item
				AND Dose.Prescricao = MEDPrescription.PrescriptionNumber
				AND Dose.Linha = MEDPrescriptionLine.Line
			WHERE	MEDPrescriptionLine.Item = :Item
					AND Bundle->LotNumber = :Lote
					AND MEDPrescription.PatientID = :idMEDPatient
					AND $$GetOpenAdmission^MEDAdmission(MEDPrescription.PatientID) is not null
					AND Storage->Location = :YLOCATION
			ORDER BY
				Dose.ProximaDose
			)
			&sql(OPEN Administracao)
			&sql(FETCH Administracao)
			if (SQLCODE = 0) {
				while (SQLCODE = 0) {
					if ((LinhaPrescricaoStatus '= $$$PrescricaoSuspensa)&&(LinhaPrescricaoStatus '= $$$AdministracaoConcluida)) quit
					&sql(FETCH Administracao)
				}
				&sql(CLOSE Administracao)
				if (UltimaDose '= "-"){set UltimaDose = $zdatetime(UltimaDose,4,2)}
				if (LinhaPrescricaoStatus = $$$PrescricaoSuspensa) {
					set strStatus = $$$MakeStatus("%1", "Medicação suspensa / descontinuada.")
				}
				elseif (LinhaPrescricaoStatus = $$$AdministracaoConcluida) {
					set strStatus = $$$MakeStatus("%1", "Este medicamento já foi integralmente administrado.")
				}
				elseif (Validade < $$$KEY1($horolog)) {
					set strStatus = $$$MakeStatus("%1", "Med]]><![CDATA[icamento com prazo de validade vencido.")
				}
				elseif (Situacao = $$$SituacaoAtivo) {
					&js<
						var confirmaDose = confirm('HORÁRIO INVÁLIDO PARA ADMINISTRAÇÃO DA DOSE.\n\nMedicamento: #("("_Item_") "_$$SQLGetDescricaoProduto^VARSQL(Item))#\nDose: #(QtdDose_" "_$$SQLGetSiglaUnit^VARSQL(Unidade))#\nÚltima dose: #(UltimaDose)#\nPróxima Dose: #($zdatetime(ProximaDose,4,2))#\n\nDeseja ADIANTAR a administração do medicamento?');
						if (confirmaDose == 1) {
							dosePrompt('#(barCode)#','#(Prescricao_"@"_Linha_"@"_Dose)#','#(idVARDoseAdministracao)#');
						}
						else{
							setTimeout("document.getElementById('Y#(YFORM)#M#(YLFN)#').focus()",100);
						}
					>
				}
				elseif (Situacao = $$$SituacaoAtrasado) {
					&js<
						var confirmaDose = confirm('HORÁRIO ATRASADO PARA ADMINISTRAÇÃO DA DOSE.\n\nMedicamento: #("("_Item_") "_$$SQLGetDescricaoProduto^VARSQL(Item))#\nDose: #(QtdDose_" "_$$SQLGetSiglaUnit^VARSQL(Unidade))#\nÚltima dose: #(UltimaDose)#\nPróxima Dose: #($zdatetime(ProximaDose,4,2))#\n\nDeseja CONTINUAR a administração do medicamento?');
						if (confirmaDose == 1) {
							dosePrompt('#(barCode)#','#(Prescricao_"@"_Linha_"@"_Dose)#','#(idVARDoseAdministracao)#');
						}
						else{
							setTimeout("document.getElementById('Y#(YFORM)#M#(YLFN)#').focus()",100);
						}
					>
				}
				else {
					&js<
						dosePrompt('#(barCode)#','#(Prescricao_"@"_Linha_"@"_Dose)#','#(idVARDoseAdministracao)#');
					>
				}
			}
			else {
				&sql(
					DECLARE Lote CURSOR FOR
					
					SELECT	COUNT(1)
					INTO	:QtdLote
					FROM SQLUser.MEDprescription
					INNER JOIN SQLUser.MEDPrescriptionLine
						ON MEDPrescriptionLine.PrescriptionNumber = MEDPrescription.PrescriptionNumber
					WHERE $$GetOpenAdmission^MEDAdmission(MEDPrescription.PatientID) is not null						
						AND MEDPrescriptionLine.Status NOT IN ($$$AdministracaoConcluida, $$$PrescricaoSuspensa)
						AND MEDPrescriptionLine.Item = :Item
				)
				&sql(OPEN Lote)
				&sql(FETCH Lote)
				if (SQLCODE = 0) {
					if (QtdLote = 0) {
						set strStatus = $$$MakeStatus("%1", "Medicamento não prescrito para o paciente.")
					}
					else {
						set strStatus = $$$MakeStatus("%1", "Não há quantidade suficiente do lote deste medicamento no estoque local.")
					}
				}
				&sql(CLOSE Lote)
			}
		}
		else {
			set strStatus = $$$MakeStatus("%1", "Código de barras inválido.")
		}
		if ($$$ISERR(strStatus)) {
			$$$Alert(strStatus)
			&js< setTimeout("document.getElementById('Y#(YFORM)#M#(YLFN)#').focus()",100); >
		}
		set %TXT(1) = "#Y"_YFORM_"M"_YLFN_"~"
		if (YLFN '= 1){
			set %TXT(1) = %TXT(1)_"#FUNCTION~setTimeout(""document.getElementById('Y"_YFORM_"M"_YLFN_"').focus()"",100);~"
		}
	}
	quit

linkVisualizaOmitir(pUltimaDose, pAdministracao, pAdministracaoLinha,pidMEDPatient)
	new strLink
	set strLink =	"<a onclick=""VisualizaOmitir('"_pAdministracao_"','"_pAdministracaoLinha_"','"_pidMEDPatient_"');"" href=""#"">"_
					"<label style='color:#FF0000;'>[Omitida]</label><br/>"_UltimaDose_
					"</a>"
	quit strLink

ConsultarPrescritos()
	if (YOPTION = $$$abaMedicamentosPrescritos){
		set $piece(YPARA,",",2)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",2)
		set $piece(YPARA,",",3)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",3)
		set $piece(YPARA,",",4)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",4)
		set $piece(YPARA,",",5)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",5)
		set $piece(YPARA,",",6)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",6)
		
		set Inicio =  $piece($get(YPARA),",",3)
		set Termino =  $piece($get(YPARA),",",4)
		
		if ( (Inicio '= "")&&(Termino '= "")&&(Inicio > Termino) ){
			$$$Alert("Erro: a data de término não pode ser anterior à data de início.")
			set $piece(YPARA,",",3) = ""
			set $piece(YPARA,",",4) = ""
		}
		
		do RedirectForm^COMUtilForm(YFORM,"","",YPARA,$$$abaMedicamentosPrescritos)
	}
	elseif (YOPTION = $$$abaHistoricoAtendimento){
		set $piece(YPARA,",",7)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",7)
		set $piece(YPARA,",",8)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",8)
		set $piece(YPARA,",",9)		= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",9)
		set $piece(YPARA,",",10)	= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",10)
		set $piece(YPARA,",",11)	= $piece(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1),"~",11)
		
		set Inicio =  $piece($get(YPARA),",",8)
		set Termino =  $piece($get(YPARA),",",9)
		
		if ( (Inicio '= "")&&(Termino '= "")&&(Inicio > Termino) ){
			$$$Alert("Erro: a data de término não pode ser anterior à data de início.")
			set $piece(YPARA,",",8) = ""
			set $piece(YPARA,",",9) = ""
		}
		
		do RedirectForm^COMUtilForm(YFORM,"","",YPARA,$$$abaHistoricoAtendimento)
	}
	quit

getStatusAdministracao(pidPrescription,pLine,pDose)
	if (($length(pidPrescription) = 0)||($length(pLine) = 0)||($length(pDose) = 0)) quit ""
	new strStatus, objDose
	
	set objDose = ^VARDose(0,pidPrescription,pLine,pDose,1)
	
	if ($$$VARDoseStatus(objDose) = $$$VARDoseStatusOmitida){
		set strStatus = $$$VARDoseStatusOmitida
	}
	elseif ($$$VARDoseStatus(objDose) = $$$VARDoseStatusSuspensa){
		set strStatus = $$$VARDoseStatusSuspensa
	}
	else{
		if ($$$VARDoseAdministracao(objDose) = ""){
			set strStatus = $$GetSituacao(pidPrescription,pLine,pDose)
			if ((strStatus = $$$SituacaoAtivo)||(strStatus = $$$SituacaoPronto)){
				set strStatus = $$$VARDoseStatusAguardando
			}
			elseif (strStatus = $$$SituacaoAtrasado){
				set strStatus = $$$VARDoseStatusAtrasada
			}
		}
		else{
			set strStatus = $$GetSituacao(pidPrescription,pLine,pDose,$$$VARDoseAdministradoem(objDose))
			if (strStatus = $$$SituacaoAtivo) {
				set strStatus = $$$VARDoseStatusAdministracaoAdiantada
			}
			elseif(strStatus = $$$SituacaoPronto) {
				set strStatus = $$$VARDoseStatusAdministrada
			}
			elseif(strStatus = $$$SituacaoAtrasado) {
				set strStatus = $$$VARDoseStatusAdministracaoAtrasada
			}
		}
	}
		
	quit strStatus

GetIconStatusAdministracao(idStatus)
	new strIconSituacao
	set strIconSituacao = ""
	if (idStatus = $$$VARDoseStatusAguardando) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_verde.gif' title='Aguardando Administração'>"
	}
	if (idStatus = $$$VARDoseStatusAdministrada) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_horacerta.gif' title='Administrada'>"
	}
	elseif (idStatus = $$$VARDoseStatusOmitida) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_omitida.gif' title='Omitida'>"
	}
	elseif (idStatus = $$$VARDoseStatusSuspensa) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_omitida.gif' title='Suspensa'>"
	}
	elseif (idStatus = $$$VARDoseStatusAdministracaoAdiantada) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_foradehora.gif' title='Administração Adiantada'>"
	}
	elseif (idStatus = $$$VARDoseStatusAtrasada) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_vermelho.gif' title='Atrasada'>"
	}
	elseif (idStatus = $$$VARDoseStatusAdministracaoAtrasada) {
		set strIconSituacao = "<img width='20' height='20' src='"_YGIF_"botao_dose_vermelho.gif' title='Administrada em Atraso'>"
	}
	quit strIconSituacao
]]></Routine>
</Export>