<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINPARTN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINPARTN
#include COMSYS
#include INConst

OnAfterDataFields(pYKEY)
	quit:( (pYKEY = "") || (pYKEY = "+") ) 
	
	//Monta tabela contatos principais
	new lstHeader2, objFornecedor
 	set lstHeader2 = ""
  	set lstHeader2 = lstHeader2_$listbuild("Contato 1", "Telefone Contato 1",
  		"Email", "Fax")
				 	 		    			    							 		
  	if $$Start^COMTable(lstHeader2,"<div style=margin:3px;>Contatos principais</div>",,,$$$NO) {
			
		set objFornecedor = $get(^INLIEF(YM,pYKEY,1))
			
		do NewLine^COMTable("white")
		do InsertCell^COMTable($$$INLIEFContactPerson(objFornecedor))
		do InsertCell^COMTable($$$INLIEFFREE4(objFornecedor))
		do InsertCell^COMTable($$$INLIEFEmail(objFornecedor))
		do InsertCell^COMTable($$$INLIEFFax(objFornecedor))
		do EndLine^COMTable()
		
		do Stop^COMTable()
  	}
  		
  	//Monta tabela outros contatos		
	new objContato, nome, sobrenome, setor, celular, telefone, fax, email, count
		
	set count = 0
		
   	new lstHeader
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","Nome","Sobrenome","Setor/Cargo","Telefone","Celular","Fax","Email")
				 			    			    							 		
  	if $$Start^COMTable(lstHeader,"<div style=margin:3px;>Outros contatos cadastrados para o fornecedor</div>",,,$$$NO) {

		$$$Order3(^INPARTN,YM,pYKEY,idContato)
			
			set objContato = $get(^INPARTN(YM,pYKEY,idContato,1))
			set sobrenome  = $piece(objContato,Y,2)
			set nome	   = $piece(objContato,Y,3)
			set setor	   = $piece(objContato,Y,4)
			set celular	   = $piece(objContato,Y,5)
			set telefone   = $piece(objContato,Y,6)
			set fax		   = $piece(objContato,Y,7)
			set email	   = $piece(objContato,Y,8)
				
			set count = $i(count)
				
			set key = pYKEY_","_idContato
				
			do NewLine^COMTable()
			do InsertCell^COMTable(count,"INPARTN",key)
			do InsertCell^COMTable(nome,"INPARTN",key)
			do InsertCell^COMTable(sobrenome,"INPARTN",key)
			do InsertCell^COMTable(setor,"INPARTN",key)
			do InsertCell^COMTable(telefone,"INPARTN",key)
			do InsertCell^COMTable(celular,"INPARTN",key)
			do InsertCell^COMTable(fax,"INPARTN",key)
			do InsertCell^COMTable(email,"INPARTN",key)
			do EndLine^COMTable()
			
			
		$$$End
			
	 	if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum contato cadastrado para o fornecedor.",,,,,,8)
			do EndLine^COMTable()   
		 }			
			
		do NewLine^COMTable("lightsteelblue")
		do InsertCell^COMTable("<strong>Cadastrar novo contato para o fornecedor</strong>","INPARTN",$$$KEY1(pYKEY),,"RIGHT",,8)
		do EndLine^COMTable()			
		
		do Stop^COMTable()
	}

	quit
]]></Routine>
</Export>