<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEINVEHeader" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEINVEHeader
	
#include COMConst
#include COMSYS
#include INConst
	
IsManagersView()
	;-------------------------------------------------------------------------------
	; Determines whether this is the managers view of the form.
	; The 'M' in YPARA is passed in from the button on the 'Perform Stocktake' form (INWEINVD)
	;
	; Params:
	;
	; Returns: $$$YES if manager view
	;
	; History:
	; 23-Apr-2008 	PJones 	We assume this is the manager's view if
	; 							IsCountProcessClosed (because we can only get into 
	; 							manager's view in this status, and can't get into
	; 							non-manager's view - could be dangerous if these
	; 							assumptions change)  Maybe ideally Manager's and
	; 							non-manager's views should be different forms
	; 12-Nov-2007	shobby	SRBR014769: Created
	;-------------------------------------------------------------------------------
	new blnIsManager,idStocktake
	
	set blnIsManager = $$$NO
	set idStocktake  = $$$KEY1($get(YKEY))
	
	if $piece($get(YPARA),Y,1)="M" {
		set blnIsManager = $$$YES
	
	} elseif (idStocktake) '= "" {
		if $$IsCountProcessClosed^INWEINVPStatus(idStocktake) {
			set blnIsManager = $$$YES
		}
	}
	quit blnIsManager
	
	
OnBeforeButtonLine()
	;-------------------------------------------------------------------------------
	; Makes the form read only if the Stocktaking has been closed.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Nov-2007	shobby	SRBR014769: Use standard status check routine.
	; 08-Nov-2007	shobby	SRBR014769: Changed $$$EnumINSTOCKTAKESTATUSClosed to
	; 							$$$EnumINSTOCKTAKESTATUSStocktakeComplete
	; 26-Oct-2007	shobby	SRBR014557: Created
	;-------------------------------------------------------------------------------
	if $$IsStocktakeComplete^INWEINVPStatus(YKEY) {
		set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
	} 
	quit
	
	
OnAfterPrimaryKey(pYKEY="")
	;-------------------------------------------------------------------------------
	; Store some parameters in a temporary structure for use by the grid lines.
	; ie YPARA will contain information about whether this form is being shown
	; in the 'Edit Stocktake Count' mode or the 
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 12-Nov-2007	shobby	SRBR014769: Created
	;-------------------------------------------------------------------------------
	set ^CacheTempStocktakePara(YUSER,"YPARA")    = $get(YPARA)
	set ^CacheTempStocktakePara(YUSER,"YAUSWAHL") = $get(YAUSWAHL)
	quit 
	
	
OnAfterDataFields(pYKEY="")
	;-------------------------------------------------------------------------------
	; Load the edit grid
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Oct-2008	GRF		SR15810: Remove explicit call to VAR routine; retain
	; 							functionality during count without building list of
	; 							items.
	; 26-Oct-2007	shobby	SRBR014557: Don't allow editing if we are not on the
	; 							last count.
	;  9-Aug-2006	JW		SR14730: Don't Store all, macros.
	; 22-May-2006	JW		SR14659: Do not update container
	; 03-May-2006	PP&JW	SR14508: Created
	;-------------------------------------------------------------------------------
	new idForm,YAUSWAHL
	
	kill ^CacheTempLocked(YUSER)
	
	set idForm="INWEINVELINE"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)          = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)       = 600
	set $$$COMGridEditParameterGridName(YAUSWAHL)            = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)             = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) && $$IsEnabled()
	set $$$COMGridEditParameterContainer(YAUSWAHL)           = "INWEINVEHeader"
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$YES
	set $$$COMGridEditParameterDontStoreAll(YAUSWAHL)        = $$$YES
	
	$$$GRIDStart(idForm,pYKEY)
	quit
	
	
OnBeforeSave(pYKEY)
	;-------------------------------------------------------------------------------
	; Routine to do any checks before saving the record.
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 10-Oct-2008	GRF		SR15810: use GridSave Macro (only place still calling
	; 							via Interface routine)
	; 03-May-2006	PP&JW	SR14508: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtils(strStatus)
	}
	quit strStatus
	
	
GetCount()
	new intCount
	
	set intCount=""
	if (YUCI'="") && (YUSER'="") {
		if $data(^CacheTempStockTake(YUCI,YUSER,"M",15)) {
			set intCount = ^CacheTempStockTake(YUCI,YUSER,"M",15)
			set $piece(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"M",1),Y,15) = intCount
		}
	}
	quit intCount
	
	
OnBlur()
	;-------------------------------------------------------------------------------
	; Store the 'Count Number' field in a variable that can be used to keep the 'Perform Stocktake'
	; screen in sync.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 26-Oct-2007	shobby		SRBR014557: Created
	;-------------------------------------------------------------------------------
	if (YUCI'="") && (YUSER'="") {
		set ^CacheTempStockTake(YUCI,YUSER,"M",15) = $get(YINHALT)
	}
	quit
	
	
IsEnabled()
	;-------------------------------------------------------------------------------
	; Last count in the stocktake?
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 26-Oct-2007	shobby	SRBR014557: Created
	;-------------------------------------------------------------------------------
	new objData
	
	set objData = $$GetFormData^COMUtilForm(YFORM,"M")
	quit ($piece(objData,Y,6)=$$$INWEINVPNumberofCounts(YFELD))
	
	
OnBeforeFormConstruction(pYKEY,pYFELD)
	if $$$INWEINVPStatus(pYFELD)'<3 {
		set YHID = 2
	}
	quit
	
	
OnBeforeFormConstructionTitle()
	;-------------------------------------------------------------------------------
	; Can display different headings on the form when called from different buttons
	; in the 'Perform Stocktake' screen.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 12-Nov-2007	shobby	SRBR014769: Created
	;-------------------------------------------------------------------------------
	if $$IsManagersView() {
		if (+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(YM,YM,1)))) {
			set YKOPF = "Ajuste de Inventario (Visualização do Gerente)"
		}
		else {
			set YKOPF = YKOPF_" "_$$$Text("IN00792")  ; "(Manager's View)"
		}
	}
	quit
	
]]></Routine>
</Export>