<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW013MSG" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW013MSG
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERSTELLEN INHOUSE-MESSAGES AN MITARBEITER
	;	Edit Info Messages For Employees (Instant Messages)
	;	
	; Called from : Form WWW013MSG
	;
	; ByRef :
	;	VORG(1)    List of user IDs
	;	VORG(2)    Text message
	;
	; Returns :
	;	Success or failure message
	;
	; History :
	; 03-Jul-2007	RPW		SRBR014477: Remove extra if test and joined to primary
	; 20-Jun-2007	GRF		SRBR014477: Don't send to terminated employees; cope with
	; 						single quotes, etc. in text; use single message subroutine
	; 						rather than duplicating code.
	; 31.03.2003	FIS
	;-------------------------------------------------------------------------------
	new loop,idUser,strText,strUserList
	
	set strUserList = $translate(VORG(1),",",";")
	set strText     = $zcvt($get(VORG(2)),"o","JS") // SRBR014477
	
	if ($translate(strUserList,";")="") || ($translate(strText," ")="") {
		do ^WWWINFO($$^WWWTEXT(32310)) ; "No Selection Made"   KEINE AUSWAHL GETROFFEN
	} else {
		for loop=1:1 {
			quit:$piece(strUserList,";",loop,999)=""
			
			set idUser = $piece(strUserList,";",loop)
			do MSG(idUser,strText,YBED) // SRBR014477
		}
		
		do ^WWWINFO($$^WWWTEXT(32896))       ; "Submit"
	}
	
	quit
	
 
MSG(pidTo="",pstrText="",pidFrom)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;	SET MESSAGE FROM OUT OF FORM !
	;	
	; Inputs : 
	;	pidTo     = ADDRESSEE
	;	pstrText  = MESSAGE
	;	pidFrom   = ADDRESSOR
	; History:
	; 03-Jul-2007	RPW		SRBR014477: Macro Usage and variable naming
	; 20-Jun-2007	GRF		SRBR014477: Rewrote from . syntax
	;-------------------------------------------------------------------------------
	new idMsgSeq,objEmployee,objMessage,strStatus
	
	if (pidTo'="") && (pidTo'="UNKNOWN") {
		set objEmployee = $get(^WWW013(0,pidTo,1))
		if (objEmployee'="") && ($$$WWW013TerminationOn(objEmployee)="") {
			set idMsgSeq = $$^WWWNEXT("WWW013M")
			if idMsgSeq'="" {
				set objMessage = ""
				set $$$WWW013MInfoText(objMessage)            = $translate(pstrText,Y)
				set $$$WWW013MMessageFromEmployee(objMessage) = $get(pidFrom)
				set strStatus = $$$Save("WWW013M",pidTo_","_idMsgSeq,objMessage,1)
			}
		}
	}
	quit
	
	/*  replacing
 WWW013MSG
	NEW YI,YA,YMIT
 
	SET VORG(1)=$TRANSLATE(VORG(1),",",";")
	SET VORG(2)=$GET(VORG(2))
	IF $TRANSLATE(VORG(1),";")="" DO ^WWWINFO($$^WWWTEXT(32310)) QUIT  ; "No Selection Made"   KEINE AUSWAHL GETROFFEN
	IF $TRANSLATE(VORG(2)," ")="" DO ^WWWINFO($$^WWWTEXT(32310)) QUIT
	FOR YI=1:1 QUIT:$PIECE(VORG(1),";",YI,999)=""  DO
	. SET YMIT=$PIECE(VORG(1),";",YI)
	. QUIT:YMIT="UNKNOWN"
	. QUIT:'$DATA(^WWW013(0,YMIT))
	. NEW YFORM,YVOR,SATZ,YNR,YOK
	. SET YNR=$$^WWWNEXT("WWW013M")
	. QUIT:YNR=""
	. SET SATZ=""
	. SET $PIECE(SATZ,Y,1)=VORG(2)  ;TEXT
	. SET $PIECE(SATZ,Y,4)=YBED     ;ABSENDER
	. SET YOK=$$^WWWSPEI("WWW013M",YMIT_","_YNR,SATZ,1)  ;SPEICHERN INFO-NACHRICHT ;Save 
	
	DO ^WWWINFO($$^WWWTEXT(32896)) QUIT  ;OK
	QUIT
	
 MSG(YMIT,YTXT,YABS) ;SET MESSAGE FROM OUT OF FORM ! ;table-mat shape 
	;YMIT=ADDRESSEE
	;YTXT=MESSAGE
	;YABS=ADDRESSOR
	IF $GET(YMIT)'="" DO
	. QUIT:YMIT="UNKNOWN"
	. QUIT:'$DATA(^WWW013(0,YMIT))
	. NEW YFORM,YVOR,SATZ,YNR,YOK
	. SET YNR=$$^WWWNEXT("WWW013M")
	. QUIT:YNR=""
	. SET SATZ=""
	. SET $PIECE(SATZ,Y,1)=$TRANSLATE($GET(YTXT),Y)  ;TEXT
	. SET $PIECE(SATZ,Y,4)=$TRANSLATE($GET(YABS),Y)  ;ABSENDER
	. SET YOK=$$^WWWSPEI("WWW013M",YMIT_","_YNR,SATZ,1)  ;SPEICHERN INFO-NACHRICHT ;Save 
	
	QUIT
	*/	
]]></Routine>
</Export>