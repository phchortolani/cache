<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHelpField" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHelpField
	
#include COMSYS
#include COMConst
#include WWWConst
	
#define HelpText(%form,%type,%int,%lang)	$$$WWW127HelpText($get(^WWW127(0,%form,%type,%int,%lang,1)))
#define NextLine							($order(^COMHelpField(0,pidForm,pidLang,""),-1)+1)
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2)	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMHelpField("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	
GetGridLine(pidForm,pstrType,pintNumber)
	;-------------------------------------------------------------------------------
	; Determine the position in the Help text editing grid that the field will be in.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 10-Sep-2008	shobby	SRBR014980: Created
	;-------------------------------------------------------------------------------
	new intLine,idKey,objWWW122
	
	set intLine = ""
	if pintNumber'=0 {  ;Header is first manual field (why?)
		set idKey = ""
		for {
			set idKey = $order(^WWW121(0,pidForm,idKey))
			quit:idKey=""
			
			set intLine = intLine+1
			quit:(pstrType="P")&&(pintNumber=idKey)
		}
		if pstrType'="P" {
			set idKey = ""
			for {
				set idKey = $order(^WWW122(0,pidForm,idKey))
				quit:idKey=""
				
				set intLine   =intLine+1
				set objWWW122 = $get(^WWW122(0,pidForm,idKey,1))
				quit:(pintNumber=$$$WWW122SequenceNumber(objWWW122))
			}
		}
	}
	quit intLine
	
	
LoadFields(pidForm,pidLang)
	;-------------------------------------------------------------------------------
	; Load in the required form fields
	;
	; Params:	pidForm		: The form id
	; 			pidLang		: Language we're translating in
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 25-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new SPRACHE,idKey,strStatus,idField
 
	set SPRACHE = pidLang // for WWWFELDNAME
	set strStatus = $$$OK
	
	// Keys 
	set idKey = ""
	for {
		set idKey = $order(^WWW121(0,pidForm,idKey))
		quit:(idKey="")||($$$ISERR(strStatus))
		
		set strStatus = $$LoadKey(pidForm,pidLang,idKey)
	}
	
	//fields
	set idField = ""
	for {
		set idField = $order(^WWW122(0,pidForm,idField))
 
		quit:(idField="")||($$$ISERR(strStatus))
		
		set strStatus = $$LoadField(pidForm,pidLang,idField)
	}
 
	quit strStatus
	
	
LoadKey(pidForm,pidLang,pidKey)
	;-------------------------------------------------------------------------------
	; Load in key help text
	;
	; Params: pidForm		: The form id
	; 		  pidLang		: Language we're translating in
	; 		  pidKey		: The form key number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-Sep-2008	shobby	BR014980: Add created/changed information
	; 08-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strRef,strHelp,objCOMHelpField,objWWW127
 
	set strRef = "P"_pidKey
		
	set strHelp   = $$$HelpText(pidForm,"P",pidKey,pidLang)
	set objWWW127 = $get(^WWW127(0,pidForm,"P",pidKey,pidLang,1))
	
	set objCOMHelpField = ""
		
	set $$$COMHelpFieldRank(objCOMHelpField)        = ""
	set $$$COMHelpFieldHelpText(objCOMHelpField)    = $$$FormatParagraph(strHelp)
	set $$$COMHelpFieldFieldRef(objCOMHelpField)    = strRef
	set $$$COMHelpFieldDateCreated(objCOMHelpField) = $$$WWW127DateCreated(objWWW127)
	set $$$COMHelpFieldCreatedBy(objCOMHelpField)   = $$$WWW127CreatedBy(objWWW127)
	set $$$COMHelpFieldDateChanged(objCOMHelpField) = $$$WWW127DateChanged(objWWW127)
	set $$$COMHelpFieldChangedBy(objCOMHelpField)   = $$$WWW127ChangedBy(objWWW127)
		
	quit $$$Save("COMHelpField",pidForm_","_pidLang_","_$$$NextLine,objCOMHelpField,$$$YES)
	
	
LoadField(pidForm,pidLang,pidField)
	;-------------------------------------------------------------------------------
	; Load in field help text
	;
	; Params: pidForm		: The form id
	; 		  pidLang		: Language we're translating in
	; 		  pidField		: The form field number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-Sep-2008	shobby	BR014980: Include Created/Changed information
	; 08-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idSeq,strRef,strHelp,objCOMHelpField,objWWW127,idNumber
 
	set idSeq = $$$WWW122SequenceNumber($get(^WWW122(0,pidForm,pidField,1)))
		
	if idSeq="" {
		set strRef = "M"_pidField
	} else {
		set strRef = "D"_idSeq
	}
		
	set objWWW127 = $get(^WWW127(0,pidForm,$extract(strRef),$extract(strRef,2,$length(strRef)),pidLang,1)) ;BR014980
 
	set strHelp=$$$WWW127HelpText(objWWW127)
	set objCOMHelpField = ""
		
	set $$$COMHelpFieldRank(objCOMHelpField)        = ""
	set $$$COMHelpFieldHelpText(objCOMHelpField)    = $$$FormatParagraph(strHelp)
	set $$$COMHelpFieldFieldRef(objCOMHelpField)    = strRef
	set $$$COMHelpFieldCreatedBy(objCOMHelpField)   = $$$WWW127CreatedBy(objWWW127)   	;BR014980
	set $$$COMHelpFieldDateCreated(objCOMHelpField) = $$$WWW127DateCreated(objWWW127)	;BR014980
	set $$$COMHelpFieldChangedBy(objCOMHelpField)   = $$$WWW127ChangedBy(objWWW127)		;BR014980
	set $$$COMHelpFieldDateChanged(objCOMHelpField) = $$$WWW127DateChanged(objWWW127)	;BR014980
	
	set idNumber = $$$NextLine
	quit $$$Save("COMHelpField",pidForm_","_pidLang_","_idNumber,objCOMHelpField,$$$YES)
 
	
UpdateFieldHelp(pidForm,pidLang)
	;-------------------------------------------------------------------------------
	; Merge over field help text into the master global
	;
	; Params:	pidForm : The form id
	; 			pidLang : Language we're translating in
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 25-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new intField,objCOMHelpField,strHelp,strRef,idType,intType,objWWW127
	new strKey,strStatus
	
	$$$LogR("UpdateFieldHelp",pidForm_","_pidLang)
	
	set intField = ""
	for {
		set intField = $order(^COMHelpField(0,pidForm,pidLang,intField))
		quit:(intField="")
		
		set objCOMHelpField = $get(^COMHelpField(0,pidForm,pidLang,intField,1))
		
		set strHelp   = $$$COMHelpFieldHelpText(objCOMHelpField)
		set strRef    = $$$COMHelpFieldFieldRef(objCOMHelpField)     ; "P2" or "D8"
		set idType    = $extract(strRef)                             ; "P"  or "D"
		set intType   = $extract(strRef,2,$length(strRef))           ;  2   or  8
		
		set objWWW127 = $get(^WWW127(0,pidForm,idType,intType,pidLang,1))
		set strKey    = pidForm_","_idType_","_intType_","_pidLang
		
	$$$LogRx(strKey)
		
		set $$$WWW127HelpText(objWWW127) = $$$FormatParagraph(strHelp)
		set strStatus = $$$Save("WWW127",strKey,objWWW127,$$$YES)
	}
	quit
	
	
PreviewHelp()
	;-------------------------------------------------------------------------------
	; Preview help text for this field
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 04-Jun-2007	RPW		SR15369: Use the new ReplaceYGIF macro to make images work
	; 24-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strHelp
 
	set strHelp = $$$ReplaceYGIF($$$COMHelpFieldHelpText($$$GRIDGetYFELD($$$GRIDGetCurrentYKEY))) // SR15369
	
	if (strHelp="") {
		$$$Alert("WWW00052")  ;No help text
	} else {
		write "with (document.getElementById('Preview')) {"
		write "	style.visibility = 'visible';"
		write " innerHTML = unescape("""_$$$Escape(strHelp)_""");"
		write "}"
	}
 
	quit
	
	
GetDescription(pidRow)
	;-------------------------------------------------------------------------------
	; Get a field description.
	;
	; Params: pidRow		: The row primary key
	;
	; ByRefs:
	;
	; Returns: strName	(The field name)
	;
	; History:
	; 13-Nov-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strName,objField,strRef,idForm,idLang,idField
 
	set strName=""
 
	if $$GetKey^COMUtilClass(pidRow,3,3,.idForm,.idLang,.idField) {
		set objField = $get(^COMHelpField(0,idForm,idLang,idField,1))
		set strRef = $$$COMHelpFieldFieldRef(objField)
		
		new SPRACHE // call to WWWFELDNAME
		set SPRACHE = idLang
		set strName = $$^WWWFELDNAME(idForm,$extract(strRef),$extract(strRef,2,$length(strRef)))
	}
 
 
	quit strName
	
]]></Routine>
</Export>