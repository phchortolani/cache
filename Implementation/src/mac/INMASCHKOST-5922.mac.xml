<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INMASCHKOST" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INMASCHKOST(YTYP,ART) ;FIS;ERMITTELN MASCHINEN- UND BEDIENERKALKULATIONSSTUNDENSÄTZE;05.02.2002 ;find And 
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERMITTELN MASCHINEN- UND BEDIENERKALKULATIONSSTUNDENSÄTZE
	;
	; Inputs : 
	;	YTYP	1-ARTIKEL 2-AUFTRAGSPOSITION  3-STANDARDKALKULATIONSVORGABE
	;	ART		ARTIKEL ;Type item 
	;	
	;	YFELD	POSITIONSDATEN (FÜR TYP2)  ; objINAUFPXL (others?)
	;
	; ByRef :
	;
	; Returns :
	;	MachineRate~WorkerRate~ServiceOverhead
	;
	; History :
	; 16-Aug-2005	GRF		SR13142 / SR13215 : code revision - returns Service
	; 						overhead as third piece now
	; 08-Aug-2005	GRF		SR13142 : Include Burden from Labour?
	; 22-Jul-2005	GRF		SR12968 : Doco; !=>||, unnecessary + removed; FIXME
	; 	05.02.2002 
	; 	
	; 	TODO : Should explicitly pass YFELD in (used but not altered here) <GRF>
	;-------------------------------------------------------------------------------
	new MachineNo,MachineRate,ServiceOverhead,WG,WorkerRate
	
	set YTYP = $get(YTYP)
	if +YTYP=0 set YTYP = 2
	set ART = $GET(ART)
	if (YTYP=2) && (ART="") set ART=$PIECE(YFELD,Y,4)  ;ARTIKEL ;item 
	if YTYP=3 set ART=""
	
	set MachineRate     = ""  ;MASCHINENSTUNDENSATZ ;machine hour rate 
	set WorkerRate      = ""  ;BEDIENERSTUNDENSATZ
	set ServiceOverhead = ""
	
	if YTYP=1 {
	;	ERMITTELN KOSTEN FÜR ARTIKEL ;find taste to item
	;	AUS MASCHINENSTAMMDATEN HOLEN ;out of send for 
		
		if ART'="" {
		;			D57		$$$INAUFPMachine()
			set MachineNo=$PIECE($GET(^INART(YM,ART,1)),Y,57)
			if (MachineNo'="") && $DATA(^INMASCH(YM,MachineNo,1)) {
				set objINMASCH = $get(^INMASCH(YM,MachineNo,1))
				set MachineRate     = $$$INMASCHMachineCostPerHour(objINMASCH)    ;MASCHINENSTUNDENSATZ
				set WorkerRate      = $$$INMASCHWorkerCostPerHour(objINMASCH)     ;BEDIENERSTUNDENSATZ
				set ServiceOverhead = $$$INMASCHServiceOverhead(objINMASCH)
				if (MachineRate+WorkerRate)=0 {
					set MachineRate = $$$INMASCHProductionCostPerHour(objINMASCH)
					set WorkerRate  = ""                   ;HERSTELLSTUNDENSATZ (MASCHINE+BEDIENER)
				}
			}
		}
	
	} elseif YTYP=2 {
	;	ERMITTELN KOSTEN FÜR AUFTRAGSPOSITION ;get from order preliminary costs 
	;	AUS ARTIKEL HOLEN ;get from item (Only set for services)
		
		set MachineRate = $PIECE(YFELD,Y,251)  ;GESPEICHERTER MASCHINENSTUNDENSATZ ;machine hour rate 
		set WorkerRate  = $PIECE(YFELD,Y,252)  ;GESPEICHERTER BEDIENERSTUNDENSATZ
		
		if (MachineRate+WorkerRate)=0 {
			if ART="" {
				set objINART    = $get(^INART(YM,ART,1))
				set MachineRate = $$$INARTMachineCostPerHour(objINART)
				set WorkerRate  = $$$INARTWorkerCostPerHour(objINART)
			}
		}
		
		;AUS MASCHINENSTAMMDATEN HOLEN ;get from machine 
		if (MachineRate+WorkerRate)=0 {
		;			D57		$$$INAUFPMachine()
			set MachineNo=$PIECE(YFELD,Y,57)
			if (MachineNo="")  && (ART'="") set MachineNo = $$$INARTMachine($get(^INART(YM,ART,1)))
			if (MachineNo'="") && $DATA(^INMASCH(YM,MachineNo,1)) {
				set objINMASCH = $get(^INMASCH(YM,MachineNo,1))
				set MachineRate     = $$$INMASCHMachineCostPerHour(objINMASCH)    ;MASCHINENSTUNDENSATZ
				set WorkerRate      = $$$INMASCHWorkerCostPerHour(objINMASCH)     ;BEDIENERSTUNDENSATZ
				set ServiceOverhead = $$$INMASCHServiceOverhead(objINMASCH)
				
				if (MachineRate+WorkerRate)=0 {
					set MachineRate = $$$INMASCHProductionCostPerHour(objINMASCH)
					set WorkerRate  = ""  ;HERSTELLSTUNDENSATZ (MASCHINE+BEDIENER)
				}
			}
		}
	}
	
	
	;AUS KALKULATIONSVORGABE ;out of 
	; FIXME : ART Will always be "" from initial YTYP=3 test
	;         Thus end up basing on default Item group REGARDLESS of what defaults are specified in INKALK!
	;         If INKALK set up for valid groups EXCEPT default group then will always get null returned. <GRF>
	if (YTYP=3) || ((MachineRate+WorkerRate)=0) {
		set WG=""
		if ART'="" set WG=$PIECE($get(^INART(YM,ART,1)),Y,30)  ;AUS ARTIKEL WENN VORHANDEN ;out of item when on hand 
		if WG=""   set WG=$PIECE($get(^INVORG(YM,YM,1)),Y,13)  ;STANDARDKALKULATIONSVORGABE AUS STANDARD WG ;out of IG 
		if WG=""   set WG=$ORDER(^INKALK(YM,YLOCATION,""))
		if (WG'="")  && '$DATA(^INKALK(YM,YLOCATION,WG)) {
			       set WG=$PIECE($get(^INVORG(YM,YM,1)),Y,13)   ;DFLT WARENGRUPPE = 1
		}
		
		; FIXME : Is this the right way around considering YTYP=1 and YTYP=2 leave Worker Rate null? <GRF>
		;         Note : the code using this is taking #2 of the returned values
		
		if WG'=""  {
			set WorkerRate  = $PIECE($get(^INKALK(YM,YLOCATION,WG,1)),Y,1)
			set MachineRate = ""                  ;DFLT STUNDENSATZ
		}
		if (ART'="") && $DATA(^INKALKART(YM,YLOCATION,ART)) {
			set WorkerRate  = $PIECE($get(^INKALKART(YM,YLOCATION,ART,1)),Y,1)
			set MachineRate = ""
		}
	}
	
	quit MachineRate_Y_WorkerRate_Y_ServiceOverhead
	
	
OldVersion(YTYP,ART)
	NEW KALK1,KALK2,MASCH,WG
	
	SET YTYP=$GET(YTYP)
	IF +YTYP=0 SET YTYP=2
	SET ART=$GET(ART)
	IF YTYP=2 IF ART="" SET ART=$PIECE(YFELD,Y,4)  ;ARTIKEL ;item 
	IF YTYP=3 SET ART=""
	
	SET KALK1=""  ;MASCHINENSTUNDENSATZ ;machine hour rate 
	SET KALK2=""  ;BEDIENERSTUNDENSATZ
	set ServiceOverhead = ""
	
	;ERMITTELN KOSTEN FÜR AUFTRAGSPOSITION ;get from order preliminary costs 
	IF YTYP=2 DO
	. SET KALK1=$PIECE(YFELD,Y,251)  ;GESPEICHERTER MASCHINENSTUNDENSATZ ;machine hour rate 
	. SET KALK2=$PIECE(YFELD,Y,252)  ;GESPEICHERTER BEDIENERSTUNDENSATZ
	. ;AUS ARTIKEL HOLEN ;get from item (Only set for services)
	. IF +(KALK1+KALK2)=0 DO
	. . QUIT:ART=""
	. . ;		251		$$$INARTMachineCostPerHour()
	. . ;		252		$$$INARTWorkerCostPerHour()
	. . SET KALK1=$PIECE($GET(^INART(YM,ART,1)),Y,251)
	. . SET KALK2=$PIECE($GET(^INART(YM,ART,1)),Y,252)
	. . QUIT
	. ;AUS MASCHINENSTAMMDATEN HOLEN ;get from machine 
	. IF +(KALK1+KALK2)=0 DO
	. . ;		D57		$$$INARTMachine()
	. . SET MASCH=$PIECE(YFELD,Y,57)
	. . IF MASCH="" IF ART'="" SET MASCH=$PIECE($GET(^INART(YM,ART,1)),Y,57)
	. . IF MASCH'="" IF $DATA(^INMASCH(YM,MASCH,1)) DO
	. . . ;		D2		$$$INMASCHProductionCostPerHour()
	. . . ;		D31		$$$INMASCHMachineCostPerHour()
	. . . ;		D32		$$$INMASCHWorkerCostPerHour()
	. . . set objINMASCH = $get(^INMASCH(YM,MASCH,1))
	. . . set MachineRate     = $$$INMASCHMachineCostPerHour(objINMASCH)         ; SR13215
	. . . set WorkerRate      = $$$INMASCHWorkerCostPerHour(objINMASCH)
	. . . SET KALK1=$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,31)  ;MASCHINENSTUNDENSATZ ;machine hour rate 
	. . . SET KALK2=$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,32)  ;BEDIENERSTUNDENSATZ
	. . . set ServiceOverhead = $$$INMASCHServiceOverhead(objINMASCH)					;SR13142
	. . . IF +(KALK1+KALK2)=0 SET KALK1=$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,2),KALK2=""  ;HERSTELLSTUNDENSATZ (MASCHINE+BEDIENER)
	. . . QUIT
	. . QUIT
	. QUIT
	
	;ERMITTELN KOSTEN FÜR ARTIKEL ;find taste to item 
	IF YTYP=1 DO
	. ;AUS MASCHINENSTAMMDATEN HOLEN ;out of send for 
	. IF ART'="" DO
	. . SET MASCH=$PIECE($GET(^INART(YM,ART,1)),Y,57)
	. . IF MASCH'="" IF $DATA(^INMASCH(YM,MASCH,1)) DO
	. . . SET KALK1=$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,31)  ;MASCHINENSTUNDENSATZ ;machine hour rate 
	. . . SET KALK2=$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,32)  ;BEDIENERSTUNDENSATZ
	. . . IF +(KALK1+KALK2)=0 SET KALK1=$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,2),KALK2=""  ;HERSTELLSTUNDENSATZ (MASCHINE+BEDIENER)
	. . . QUIT
	. . QUIT
	. QUIT
	
	;AUS KALKULATIONSVORGABE ;out of 
	; FIXME : ART Will always be "" from initial YTYP=3 test
	;         Thus end up basing on default Item group REGARDLESS of what defaults are specified in INKALK!
	;         If INKALK set up for valid groups EXCEPT default group then will always get null returned. <GRF>
	IF (YTYP=3)||((KALK1+KALK2)=0) DO
	. SET WG=""
	. IF ART'="" SET WG=$PIECE($GET(^INART(YM,ART,1)),Y,30)  ;AUS ARTIKEL WENN VORHANDEN ;out of item when on hand 
	. IF WG=""   SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)  ;STANDARDKALKULATIONSVORGABE AUS STANDARD WG ;out of IG 
	. IF WG=""   SET WG=$ORDER(^INKALK(YM,YLOCATION,""))
	. IF WG'=""  IF '$DATA(^INKALK(YM,YLOCATION,WG)) SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)   ;DFLT WARENGRUPPE = 1
	. IF WG'=""  SET KALK2=$PIECE($GET(^INKALK(YM,YLOCATION,WG,1)),Y,1),KALK1=""                  ;DFLT STUNDENSATZ
	. IF ART'="" IF $DATA(^INKALKART(YM,YLOCATION,ART)) SET KALK2=$PIECE($GET(^INKALKART(YM,YLOCATION,ART,1)),Y,1),KALK1=""
	. QUIT
	
	QUIT KALK1_Y_KALK2
]]></Routine>
</Export>