<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWCLEAN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWCLEAN(YROUTINE,YVARIABLE)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SUCHEN NACH VARIABLEN DIE IN KEINER >NEW..< ZEILE STEHEN
	;		Find variables in the NEW lists in routines - list if used but not newed
	;		Also considers NEW (...) lists.
	;
	; Inputs : 
	;	YROUTINE		Routine name              (Null = all IN* routines?)
	;	YVARIABLE		Variable name to be found (Null = all variables in ^WWWZWS)
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 14-Nov-2007	GRF		Doco; quits; FIXME
	; 27.03.2001	FIS
	;-------------------------------------------------------------------------------
	DO ^WWWVAR
	
	NEW DAT,USER,TRAKT,VARIABLE,ROUTINE,NR,ZNR,ZEILE
	
	KILL ^WWWSOR(YUSER)
	
	SET DAT=""
	FOR  SET DAT=$ORDER(^WWWZWS(0,DAT)) QUIT:DAT=""  DO
	. SET USER=""
	. FOR  SET USER=$ORDER(^WWWZWS(0,DAT,USER)) QUIT:USER=""  DO
	. . SET TRAKT=""
	. . FOR  SET TRAKT=$ORDER(^WWWZWS(0,DAT,USER,TRAKT)) QUIT:TRAKT=""  DO
	. . . SET VARIABLE=$GET(YVARIABLE)
	. . . IF VARIABLE'="" IF '$DATA(^WWWZWS(0,DAT,USER,TRAKT,VARIABLE)) QUIT
	. . . ;
	. . . ;---------------------------------
	. . . ;
	. . . DO:VARIABLE'=""  FOR  SET VARIABLE=$ORDER(^WWWZWS(0,DAT,USER,TRAKT,VARIABLE)) QUIT:VARIABLE=""  DO
	. . . . ;
	. . . . QUIT:$DATA(^WWWSOR(YUSER,VARIABLE))
	. . . . SET ^WWWSOR(YUSER,VARIABLE)=""
	. . . . ;
	. . . . ;SET ROUTINE="" FOR  SET ROUTINE=$ORDER(^ROUTINE(ROUTINE)) QUIT:ROUTINE=""  DO
	. . . . SET ROUTINE="IN"
	. . . . IF $GET(YROUTINE)'="" SET ROUTINE=$ORDER(^ROUTINE(YROUTINE),-1)
	. . . . ;
	. . . . ;-------------------------------
	. . . . ;
	. . . . ;	FIXME : <GRF> if YROUTINE is null the only the first routine (if any)
	. . . . ;	will be tested as it is likely there will be routines that start with
	. . . . ;	something other than "IN" before we get to the "IN" ones.
	. . . . ;
	. . . . DO:$GET(YROUTINE)'=""  IF $GET(YROUTINE)="" FOR  SET ROUTINE=$ORDER(^ROUTINE(ROUTINE)) QUIT:ROUTINE=""  QUIT:$EXTRACT(ROUTINE,1,2)'="IN"  DO
	. . . . . QUIT:$EXTRACT(ROUTINE,1)="%"
	. . . . . ;
	. . . . . SET YQ(1)=0    ;  0=variable not newed   1=variable newed
	. . . . . SET YQ(2)=0    ;  0=variable not used    1=variable used
	. . . . . SET NR=""
	. . . . . FOR  QUIT:YQ(1)=1  SET NR=$ORDER(^ROUTINE(ROUTINE,NR)) QUIT:NR=""  DO
	. . . . . . SET ZNR=""
	. . . . . . FOR  QUIT:YQ(1)=1  SET ZNR=$ORDER(^ROUTINE(ROUTINE,NR,ZNR)) QUIT:ZNR=""  DO
	. . . . . . . ;
	. . . . . . . SET ZEILE(ZNR)=$GET(^ROUTINE(ROUTINE,NR,ZNR))
	. . . . . . . QUIT:ZEILE(ZNR)=""
	. . . . . . . SET ZEILE(ZNR)=$$^WWWUMLAU(ZEILE(ZNR),1)
	. . . . . . . ;
	. . . . . . . IF $FIND(ZEILE(ZNR),"NEW ") DO
	. . . . . . . . IF $FIND(ZEILE(ZNR),"(") IF $FIND(ZEILE(ZNR),")") SET:'$FIND(ZEILE(ZNR),VARIABLE) YQ(1)=1 QUIT
	. . . . . . . . IF $FIND(ZEILE(ZNR),VARIABLE) SET YQ(1)=1
	. . . . . . . ;
	. . . . . . . ; FIXME : <GRF> Doesn't consider white space after variable name.
	. . . . . . . ;
	. . . . . . . IF $FIND(ZEILE(ZNR)," "_VARIABLE_"=") SET YQ(2)=1
	. . . . . . ;
	. . . . . . QUIT:YQ(2)=0  ;VARIABLE WIRD NICHT IN ROUTINE BENUTZT ;Not within routine 
	. . . . . . QUIT:YQ(1)=1  ;VARIABLE IST IN NEW-ZEILE AUFGEFÜHRT ;within 
	. . . . . . ;
	. . . . . . WRITE YCR
	. . . . . . WRITE "VARIABLE: "_VARIABLE_" ROUTINE: "_ROUTINE
	
	WRITE YCR
	WRITE "Examination finished"     ;"Überprüfung beendet !"
	KILL ^WWWSOR(YUSER)
	
	QUIT
	
]]></Routine>
</Export>