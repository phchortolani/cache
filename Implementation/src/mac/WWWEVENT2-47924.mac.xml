<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWEVENT2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWEVENT2
	
#include COMSYS
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FAST SAVE
	;
	; Inputs : 
	;
	;
	; ByRef :
	;	%REQUEST="NAMESPACE.USER.FORM.FELDNUMMER.FUNKTION.FIXKEY.YVARIABLE"  ;SR18075
	;	%REQUEST="NAMESPACE.USER.FORM.FELDNUMMER.FUNKTION.FIXKEY.SPRACHE.YVARIABLE"  ;SR18075
	;	%REQUEST(1)="VALUE"
	;
	;
	; Returns :
	;
	;
	; History :
	; 02-Aug-2012	shobby	SR18075: piece 7 of %REQUEST is now SPRACHE
	; 17-Oct-2011	GRF		-: WWW120/WWW012 macros
	; 04-May-2010	GRF		SR15961: use dteToday for efficiency; dot to brace;
	; 							naked reference; language text comments
	; 09-Dec-2008	GRF		BR014638: doco; quits
	; 13-Jan-2006	JW		SR13195: After saving form, update save status.
	; 07-Sep-2005	RPW		SR13430: Defined YMFELD the same as YFELD, instead of D, use M.
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 24.08.2002	DT
	;-------------------------------------------------------------------------------
	new dteToday,YFKEY,YKEY,YUSER,YFORM,YDATEI,YFELD,YSATZ,YINHALT,YBED,YVAR,YART,YDATA,YPR,YPR1,YMFELD
	
	set %TXT(1) = " "
	set YKEY    = ""
	set YBED    = ""
	set YFKEY   = $extract($piece($get(%REQUEST),".",6),4,99)  ;FIX_VERBUNDEN MIT KEY ;by means of KEY 
	set YINHALT = $get(%REQUEST(1))                            ;EINGEGEBENER TEXT ZUR PRUEFUNG
	set YSATZ   = ""
	set YUSER   = $piece($get(%REQUEST),".",2)
	if YUSER'="" {
		set YBED = $piece($get(^WWWUSER(0,YUSER,1)),Y,2)
		set YM   = $piece($get(^WWWUSER(0,YUSER,1)),Y,20)
	}
	
	if $data(YBED) && (YBED'="") {    ;SPRACHE DES USERS ;Language 
		if YM="" set YM = $piece($get(^WWW013(0,YBED,1)),Y,5)
		if YM="" set YM = 0
		set YLOCATION = $piece($get(^WWW013(0,YBED,1)),Y,44)
		set SPRACHE   = $$^WWWLANGU(YBED)
		set YVOR1     = $get(^WWW012(0,0,1))
		set YWHR      = $$$WWW012StandardCurrency(YVOR1)         ; D75
		set YMANDANT  = $$$WWW012UniqueCompanyIdentifier(YVOR1)  ;VORGABE FÜR MANDANTENKENNUNG ; D94 
		set YDECIMAL  = ","
		if $$$WWW012DecimalSigns(YVOR1)'="" set YDECIMAL = $$$WWW012DecimalSigns(YVOR1)        ; D73
		if SPRACHE="EN" set YDECIMAL = "."
	}
	set YFORM = $piece($get(%REQUEST),".",3)
	quit:YFORM=""  ;FORMULAR NICHT VORHANDEN ;form Not on hand 
	
	set dteToday = +$horolog
	
	;SR18075 set YVAR = $piece($get(%REQUEST),".",7) 
	set YVAR = $piece($get(%REQUEST),".",8) ;SR18075
	set YART = $translate($piece($piece($get(%REQUEST),".",4),YFORM,2),"0123456789")
	if YART="" set YART = "D"
	
	set YLFN = $piece($piece($get(%REQUEST),".",4),YFORM_YART,2)
	set YVOR = $get(^WWW120(0,YFORM,1))
	if $$$WWW120SaveServerdata(YVOR)'=$$$YES  ;KEIN SAVE ;no      ; D123     ; FIXME : no action following test
	set YDATEI = $$$WWW120ClassUsedInForm(YVOR)   ; D11
	set YFELD  = $get(^WWWDATEN(0,dteToday,YUSER,YFORM,"D",1))
	set YMFELD = $get(^WWWDATEN(0,dteToday,YUSER,YFORM,"M",1))
	if $translate(YFELD,Y)="" if $$$WWW120SaveAlsoWithoutDataFields(YVOR)'=$$$YES quit  ; D80   ;NICHT SAVE WENN LEER;TYBD;14,9,2004;26381
	set YKEY   = $get(^WWWDATEN(0,dteToday,YUSER,YFORM,"P",1))
	quit:YDATEI=""  ; no class
	
	set YMAXKEY = +$order(^WWW002(0,YDATEI,""),-1)
	quit:YMAXKEY=0
	
	kill ^WWWSOR(YUSER)
	
	do
	. new YI,YTYP
	. for YI=1:1:YMAXKEY do 
	. . set YTYP=$piece($get(^WWW002(0,YDATEI,YI,1)),Y,3)
	. . set YKEY(YI)=$piece(YKEY,",",YI)
	. . do PRUEFP
	
	do
	. new YI,YTYP
	. set YI = ""
	. for  set YI = $order(^WWW003(0,YDATEI,YI)) quit:YI=""  do 
	. . set YTYP = $piece($get(^WWW003(0,YDATEI,YI,1)),Y,3)
	. . do PRUEFD
	
	set YI = ""
	for {
		set YI = $order(^WWWSOR(YUSER,YI))
		quit:YI=""
		
		if %TXT(1)=" " set %TXT(1) = "!"
		set %TXT(1) = %TXT(1)_$translate($get(^WWWSOR(YUSER,YI)),"|")_$char(13)
	}
	kill ^WWWSOR(YUSER)
	quit:%TXT(1)'=" "
	
	set strExecute = $$$WWW120ExecuteOnBeforeSaveData(YVOR)   ; D89
	if strExecute'="" {
		if $extract(strExecute)="#" set strExecute = $extract(strExecute,2,99)          ; *** EXECUTE ***
		xecute strExecute
		if $get(Q)=$$$QDontSave quit
	}
	if $$EXIST^%R("Y"_YFORM_"onBeforeSave.OBJ",$get(YUCI)) {
		xecute "DO ^Y"_YFORM_"onBeforeSave"
		if $get(Q)=$$$QDontSave quit
	}
	
	set YOK = $$^WWWSPEI(YDATEI,YKEY,YFELD,0)
	
	do UpdateStatus^WWWFORMStatus(YFORM,3)
	quit
	
	
PRUEFP
    ;-------------------------------------------------------------------------------
    ; Primary Keys
    ; 
    ; Params:
	;	YI = KEY
	;	YTYP=ERFASSUNGSTYP
	;	YKEY(YI)= PRRIMAERSCHLUESSEL
    ;
    ; Returns:
    ;
    ; History:
	; 07-Jul-2005	shobby	SR12892: WWW126 is no longer shared.
    ;-------------------------------------------------------------------------------
	if '$data(YLFZ) set YLFZ = 0
	new YPRUEF,A,Q,YPR,YPR1,YPR2,YTEXT
	
	set YTEXT = $piece($get(^WWW002(0,YDATEI,YI,1)),Y,2)
	if $data(^WWW0021(0,YDATEI,YI,SPRACHE,1)) set YTEXT=$piece(^WWW0021(0,YDATEI,YI,SPRACHE,1),Y,1)
	set A      = $get(YKEY(YI))
	set YPRUEF = $get(^WWW121(0,YFORM,YI,1))
	if ($extract($translate(A," "))="+") || (($piece(YPRUEF,Y,2)=1) && (A="")) do
	. if YI'=1 quit:$translate(YFKEY,",")=""  set A = $$^WWWNEXT1(YDATEI,YFKEY,YI) quit
	. set A = $$^WWWNEXT(YDATEI)
	
	;NÄCHSTER DATENSATZ ;data record 
	if YTYP=9 && (A="+") {
		set A = $$^WWWNEXT(YDATEI)
	}
	
	if A="+" set A = " "
	if A=""  set YLFZ = YLFZ+1,^WWWSOR(YUSER,YLFZ) = YTEXT_": "_$$^WWWTEXT(278,,1)_"|"  ; "No Primary Key"
	
	; Length wrong
	if (+$piece(YPRUEF,Y,14)'=0) && ($length(A)<$piece(YPRUEF,Y,14)) {
		set YLFZ=YLFZ+1
		set ^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(268,,1)_" "_+$piece(YPRUEF,Y,14)_"|"
	} ; "Min. Input Length"
	
	;ENTHALTENES ZEICHEN ;sign     ; "Only Following Characters Allowed"
	if $piece(YPRUEF,Y,17)'="" for YPR1=1:1 quit:$extract(A,YPR1)=""  if '$find($piece(YPRUEF,Y,17),$extract(A,YPR1)) set YLFZ=YLFZ+1,^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(269,,1)_" "_$piece(YPRUEF,Y,17)_"|" quit
	
	;NULLEN FÜLLEN ;stuff 
	if +$piece(YPRUEF,Y,18)'=0 if +A'=0 set A=$extract((1_$extract("000000000000000",1,+$piece(YPRUEF,Y,18))+A),2,99)
	
	;UMSETZEN FELDER ;transact money 
	if +$piece(YPRUEF,Y,19)'=0 do
	. if $piece(YPRUEF,Y,19)="1" set A = $zconvert(A,"U")
	. if $piece(YPRUEF,Y,19)="2" set A = $zconvert(A,"L")
	. if $piece(YPRUEF,Y,19)="3" do
	. . set YPR = A,A = ""
	. . for YPR1=1:1 set YPR2=$piece(YPR," ",YPR1) quit:$piece(YPR," ",YPR1,999)=""  set A=A_$zconvert($extract(YPR2),"U")_$zconvert($extract(YPR2,2,5000),"L")_" "
	. ;
	. if $piece(YPRUEF,Y,19)="4" do
	. . set YPR = A,A = ""
	. . for YPR1=1:2 set YPR2 = $extract(YPR,YPR1,YPR1+1) quit:YPR2=""  set:$extract(YPR2,2)'=" " YPR2=$extract(YPR2)_" "_$extract(YPR2,2) set A=A_YPR2
	. ;
	. if $piece(YPRUEF,Y,19)="5" set A = $translate(A," ")  ;LEER ;void 
	. if $piece(YPRUEF,Y,19)="6" set A = $translate($$^WWWUMLAU(A,3)," ")  ;GROß UND OHNE LEER ;macro- And without void 
	
	;MUSTERVERGLEICH   ; "Wrong Input Format"
	if $piece(YPRUEF,Y,20)'="" set YPR = $piece(YPRUEF,Y,20) if A?@YPR=0 set YLFZ = YLFZ+1 set ^WWWSOR(YUSER,YLFZ) = YTEXT_": "_$$^WWWTEXT(276,,1)_"|"
	set YKEY(YI)=$translate(A,"%","/")
	set $piece(YKEY,",",YI) = YKEY(YI)
	if $get(YBED)'="" set ^WWW126(0,YFORM,YBED,YI,1) = $get(YKEY(YI))
	quit
	
	
PRUEFD ;PRUEFEN DATENFELDER 
	new YPRUEF,A,Q,YPR,YTEXT
	
	if '$data(YLFZ) set YLFZ = 0
	set YTEXT = $piece($get(^WWW003(0,YDATEI,YI,1)),Y,2)
	if $data(^WWW0031(0,YDATEI,YI,SPRACHE,1)) set YTEXT = $piece(^WWW0031(0,YDATEI,YI,SPRACHE,1),Y,1)
	
	; vvv FIXME : <GRF> WWW003 [D22] (and thus this block) is deprecated
	set YTEXT(1) = $piece($get(^WWW003(0,YDATEI,YI,1)),Y,22)
	if YTEXT(1)=1 do   ;PRUEFEN DOPPELTER KEY BEI PRIMÄRSCHL IN DATENFELD ;KEY next to within data item 
	. quit:'$data(YKEY(1))
	. new DATA,YDATA
	. set YDATA = $get(^WWW001(0,YDATEI,1))
	. if $piece(YDATA,Y,8)'=4 set DATA = "^"_YDATEI_"("_$$^WWWYM(YDATEI,1)_""""_YKEY(1)_""""_",1)"
	. if $piece(YDATA,Y,8)=4  set DATA = "^"_YDATEI_"("_$$^WWWYM(YDATEI,1)_""""_YKEY(1)_""""_")"
	. set DATA(1) = $$^WWWSETL(DATA)
	. quit:DATA(1)=""  ;DATENSATZ NICHT VORHANDEN ;data record Not on hand 
	. ;S YLFZ=YLFZ+1 S ^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(286,,1)_"|"
	; ^^^ FIXME
	
	set A      = $piece(YFELD,Y,YI)
	set YBBN   = $order(^WWW122s(0,4,YI,YFORM,""))
	set YPRUEF = ""
	if YBBN'="" set YPRUEF = $get(^WWW122(0,YFORM,YBBN,1))
	if YBBN'="" if $get(YM)'="" if $data(^WWW122D(0,YFORM,YBBN,0,1)) do
	. if $piece(^WWW122D(0,YFORM,YBBN,0,1),Y,2)=1  set $piece(YPRUEF,Y,13) = 1  ;MANDANTENVORGABE
	. if +$piece(^WWW122D(0,YFORM,YBBN,0,1),Y,2)=0 set $piece(YPRUEF,Y,13) = 0  ;MANDANTENVORGABE
	
	if $piece(YPRUEF,Y,60)<1 set $piece(YPRUEF,Y,60) = 1
	
	;NEXT NUMMER ;numeral 
	if (YTYP=9) && (A="+") set A = $$^WWWNEXT(YDATEI)
	
	; FIXME : <GRF> WWW003 [D22] (and thus YTEXT(1)) is deprecated
	if (YTYP'=2) && ($piece(YPRUEF,Y,50)="") && ($piece(YPRUEF,Y,106)="") {
		 if ($piece(YPRUEF,Y,13)=1) || (YTEXT(1)=1) if A="" set YLFZ=YLFZ+1,^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(267,,1)_"|"
	} ; "Mandatory Field"
	if ($piece(YPRUEF,Y,50)'="") && $find(","_$piece(YPRUEF,Y,50)_",",","_$get(YOPTION)_",") {
		if ($piece(YPRUEF,Y,13)=1) || (YTEXT(1)=1) if A="" set YLFZ=YLFZ+1,^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(267,,1)_"|"
	}
	if ($piece(YPRUEF,Y,106)'="") && $find(","_$piece(YPRUEF,Y,106)_",",","_$get(YOPTION1)_",") {
		if ($piece(YPRUEF,Y,13)=1) || (YTEXT(1)=1) if A="" set YLFZ=YLFZ+1,^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(267,,1)_"|"
	}
	
	;LÄNGENPRUEFUNG
	if (+$piece(YPRUEF,Y,14)'=0) && ($length(A)<$piece(YPRUEF,Y,14)) {
		set YLFZ=YLFZ+1,^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(268,,1)_" "_+$piece(YPRUEF,Y,14)_"|"
	} ; "Min. Input Length"
	
	;ENTHALTEN ;include      ; "Only Following Characters Allowed"
	if $piece(YPRUEF,Y,17)'="" for YPR1=1:1 quit:$extract(A,YPR1)=""  if '$find($piece(YPRUEF,Y,17),$extract(A,YPR1)) set YLFZ=YLFZ+1,^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(269,,1)_" "_$piece(YPRUEF,Y,17)_"|" quit
	
	;FÜLLEN MIT NULLEN ;stuff by means of 
	if +$piece(YPRUEF,Y,18)'=0 if +A'=0 set A = $extract((1_$extract("000000000000000",1,+$piece(YPRUEF,Y,18))+A),2,99)
	
	;ZEICHENUMSETZUNG
	if +$piece(YPRUEF,Y,19)'=0 do
	. if $piece(YPRUEF,Y,19)="1" set A=$zconvert(A,"U")
	. if $piece(YPRUEF,Y,19)="2" set A=$zconvert(A,"L")
	. if $piece(YPRUEF,Y,19)="3" do
	. . set YPR=A,A=""
	. . for YPR1=1:1 set YPR2=$piece(YPR," ",YPR1) quit:$piece(YPR," ",YPR1,999)=""  set A = A_$zconvert($extract(YPR2),"U")_$zconvert($extract(YPR2,2,5000),"L")_" "
	. ;
	. if $piece(YPRUEF,Y,19)="4" do
	. . set YPR=A,A=""
	. . for YPR1=1:2 set YPR2=$extract(YPR,YPR1,YPR1+1) quit:YPR2=""  set:$extract(YPR2,2)'=" " YPR2=$extract(YPR2)_" "_$extract(YPR2,2)_" " set A=A_YPR2
	. ;
	. if $piece(YPRUEF,Y,19)="5" set A = $translate(A," ")
	. if $piece(YPRUEF,Y,19)="6" set A = $translate($zconvert(A,"U")," ")
	
	;MUSTERVERGLEICH    ; "Wrong Input Format"
	if $piece(YPRUEF,Y,20)'="" set YPR = $piece(YPRUEF,Y,20) if A?@YPR=0 set YLFZ=YLFZ+1 set ^WWWSOR(YUSER,YLFZ)=YTEXT_": "_$$^WWWTEXT(276,,1)_"|"
	quit
	
]]></Routine>
</Export>