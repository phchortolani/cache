<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewSetupVB" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
    ;-------------------------------------------------------------------------------
    ; COMViewSetupVB - VBScript for COMView
    ;-------------------------------------------------------------------------------
	#include COMConst
	#include COMSYS
	#define ButtonOK 1
	#define ButtonCancel 2
	#define ButtonAbort 3
	#define ButtonRetry 4
	#define ButtonIgnore 5
	#define ButtonYes 6
	#define ButtonNo 7
 
 
CallBack(pstrMessage,pintType=$$$EnumCOMMSGOKCancel,pstrTitle="",pstrButton1Code="",pstrButton2Code="",pstrButton3Code="")
    ;-------------------------------------------------------------------------------
    ; Prompts the user before actioning some javascript code.
    ; 
    ; pintype is enums from COMMSG summed together.
    ; 
    ; Example:
    ; $$$CallBack("Do you wish to proceed?",$$$EnumCOMMSGYesNo,"Confirm","CallBack('YesCode^YourRoutine','Some Param')","CallBack('NoCode^YourRoutine','Some Param')")
    ;
    ; Returns:String
    ;
    ; History:
    ; 08-Apr-2005	Paul K		Created
    ;-------------------------------------------------------------------------------
	new arrButton,strCode
	
	
	if pintType#16=$$$EnumCOMMSGOKOnly {
		set arrButton(1)=$$$ButtonOK
	} elseif pintType#16=$$$EnumCOMMSGOKCancel {
		set arrButton(1)=$$$ButtonOK
		set arrButton(2)=$$$ButtonCancel
	} elseif pintType#16=$$$EnumCOMMSGAbortRetryIgnore {
		set arrButton(1)=$$$ButtonAbort
		set arrButton(2)=$$$ButtonRetry
		set arrButton(3)=$$$ButtonIgnore
	} elseif pintType#16=$$$EnumCOMMSGYesNoCancel {
		set arrButton(1)=$$$ButtonYes
		set arrButton(2)=$$$ButtonNo
		set arrButton(3)=$$$ButtonCancel
	} elseif pintType#16=$$$EnumCOMMSGYesNo {
		set arrButton(1)=$$$ButtonYes
		set arrButton(2)=$$$ButtonNo
	} elseif pintType#16=$$$EnumCOMMSGRetryCancel {
		set arrButton(1)=$$$ButtonRetry
		set arrButton(2)=$$$ButtonCancel
	}
	set strCode="var result="_$$Alert(pstrMessage,pintType,pstrTitle)
	if $data(arrButton(1)) if pstrButton1Code'="" set strCode=strCode_"if (result=="_arrButton(1)_") {"_pstrButton1Code_"};"
	if $data(arrButton(2)) if pstrButton2Code'="" set strCode=strCode_"if (result=="_arrButton(2)_") {"_pstrButton2Code_"};"
	if $data(arrButton(3)) if pstrButton3Code'="" set strCode=strCode_"if (result=="_arrButton(3)_") {"_pstrButton3Code_"};"
	quit strCode
	
 
Setup()
    ;-------------------------------------------------------------------------------
    ; Pump VBScript to the screen
    ;
    ; History:
    ; 19-May-2010	FIS		SR17253: various fixes for firefox compatibilety
    ; 08-Apr-2005	Paul K		Commented
    ;-------------------------------------------------------------------------------
	&html<
	<script language=vbscript>
	Function VBConfirm_IE(Message,MsgType,Title)
	VBConfirm_IE = MsgBox (Message, MsgType,Title)
   	End Function
   	</script>
	<script language=javascript>  //SR17253 vvvvvvvvvvvvvvv
	function VBConfirm(Message,MsgType,Title) {
		if (isIE()) {  //SR17425
			return VBConfirm_IE(Message, MsgType,Title);
		}
		else if (MsgType == 0) {
			alert(Title + ': \n' + Message);
		}
		else {
			if (confirm(Title + ': \n' + Message)) result = true;
			else result = false;
			switch (MsgType) {
				case  1: return result == true ? 1 : 2;
				case  16: return result == true ? 1 : 2;
				case  35: return result == true ? 6 : 7;
				case  36: return result == true ? 6 : 7;
				default: return result;
			}
		}
	}
   	</script>
   	>
   	quit
   
Alert(pstrMessage,pintType=$$$EnumCOMMSGOKOnly,pstrTitle="")    
	;-------------------------------------------------------------------------------
    ; Show a message on the screen
    ;
    ; Returns:String
    ;
    ; History:
    ; 08-Apr-2005	Paul K		Created
    ;-------------------------------------------------------------------------------
 
	if pintType#256\16=0 {
		set pintType=pintType+64
	}
	if pstrTitle="" {
		if pintType#256\16=($$$EnumCOMMSGCritical/16) {
			set pstrTitle=$$$Text("Com00177")  ;System Error	
		} elseif pintType#256\16=($$$EnumCOMMSGQuestion/16) {
			set pstrTitle=$$$Text("Com00180")  ;Question
		} elseif pintType#256\16=($$$EnumCOMMSGExclamation/16) {
			set pstrTitle=$$$Text("Com00179")  ;Warning
		} elseif pintType#256\16=($$$EnumCOMMSGInformation/16) {
			set pstrTitle=$$$Text("Com00178")  ;Information
		}
	}
	quit "VBConfirm('"_$zcvt(pstrMessage,"o","JS")_"',"_pintType_",'"_$zcvt(pstrTitle,"o","JS")_"');"
	
 
 
	
	
	
	
	/*Constant Value Description 
	vbOKOnly    		0 Display OK button only. 
	vbOKCancel    		1 Display OK and Cancel buttons. 
	vbAbortRetryIgnore  2 Display Abort, Retry, and Ignore buttons. 
	vbYesNoCancel    	3 Display Yes, No, and Cancel buttons. 
	vbYesNo    			4 Display Yes and No buttons. 
	vbRetryCancel    	5 Display Retry and Cancel buttons. 
	vbCritical 			16 Display Critical Message icon.  
	vbQuestion 			32 Display Warning Query icon. 
	vbExclamation 		48 Display Warning Message icon. 
	vbInformation 		64 Display Information Message icon. 
	vbDefaultButton1    0 First button is default. 
	vbDefaultButton2  	256 Second button is default. 
	vbDefaultButton3  	512 Third button is default. 
	vbDefaultButton4  	768 Fourth button is default. 
	vbApplicationModal  0 Application modal; the user must respond to the message box before continuing work in the current application. 
	vbSystemModal 		4096 System modal; all applications are suspended until the user responds to the message box. 
	
 
	Return Values
	The MsgBox function has the following return values:
	
	Constant Value Button 
	vbOK 1 OK 
	vbCancel 2 Cancel 
	vbAbort 3 Abort 
	vbRetry 4 Retry 
	vbIgnore 5 Ignore 
	vbYes 6 Yes 
	vbNo 7 No */
 
]]></Routine>
</Export>