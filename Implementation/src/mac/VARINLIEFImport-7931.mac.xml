<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINLIEFImport" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINLIEFImport
	#include INConst
	quit
	
	;----------------------------------------------------------------
	; Descrição: Atualiza os registros que estão com o campo Supplier nulo para 1
	;
	; Chamdo por: VARImportarArquivo - Carga de fornecedores
	; Definido em: VARImportarConfig na configuração 001 - Carga de Fornecedores
	;
	; Histórico:
	; 14-Dez-2016	FC	LEAN-7	Criado
	;----------------------------------------------------------------
AtualizaFornecedorAposImportacao()
	
	&SQL(
		UPDATE INLIEF
		SET Supplier = 1
		WHERE Supplier IS NULL
	)
	
	quit $$$OK
	
	;----------------------------------------------------------------
	; Descrição: Valida a importação do número do CPF / CNPJ
	;
	; Chamado por: VARImportarArquivo - Carga de fornecedores
	; Definido em: VARImportarConfig na configuração 001 - Carga de Fornecedores
	;
	; Histórico:
	; 16-Dez-2016	FC	LEAN-7	Criado
	;----------------------------------------------------------------
ValidateCompanyNumber(pStrNumber)
	#define DadoNaturezaFiscal(%obj) $piece(%obj,"~",2)
	
	new strStatus
	
	set strStatus = $$$OK
	
	if ($get(DadosLinha) '= "") {
		set strNatureza = $$$DadoNaturezaFiscal(DadosLinha)
		
		set codNatureza = $$SQLGetFornNatureza^VARSQL(strNatureza)
		
		if (codNatureza = 2) { // Jurídica
			set strStatus = $$ValidateCNPJ^COMValidation(pStrNumber)
			if ('$$$ISOK(strStatus)) {
				set strStatus = "Erro;-CNPJ Inválido"
			}
		}
		elseif (codNatureza = 1) { // Física
			set strStatus = $$ValidateCPF^COMValidation(pStrNumber)
			if ('$$$ISOK(strStatus)) {
				set strStatus = "Erro;-CPF Inválido"
			}
		}
	}
	
	quit strStatus
	
	;----------------------------------------------------------------
	; Descrição: Formata o número do CPF / CNPJ
	;
	; Chamado por: VARImportarArquivo - Carga de fornecedores
	; Definido em: VARImportarConfig na configuração 001 - Carga de Fornecedores
	;
	; Histórico:
	; 16-Dez-2016	FC	LEAN-7	Criado
	;----------------------------------------------------------------
FormataCompanyNumber(pStrNumber)
	new strNumber
	
	set strNumber = $get(pStrNumber)
	
	if (strNumber '= "") {
		set strNumber = $replace($replace($replace(strNumber,".",""),"-",""),"/","")
	}
	
	quit strNumber
]]></Routine>
</Export>