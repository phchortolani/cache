<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEINVD" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEINVD
	
#include COMSYS
#include INConst
#include WWWConst
	;-------------------------------------------------------------------------------
	; Wrapper for older access through routine name
	; 
	;-------------------------------------------------------------------------------
	
	;###############################################################################
	; 
	;      THE REPORTING FUNCTION IN THIS ROUTINE HAS BEEN SUPERSEDED BY THE ROUTINE
	;       INStocktakeReport1       WHICH IS BASED ON STOCK BUNDLES.
	; 
	;###############################################################################
	; 11-May-2011	shobby	SR17559: Changes to form/class
	do Report()
	quit
	
GoTo()
	;-------------------------------------------------------------------------------
	; Go to the ...
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 04-Aug-2009	PPP		SR16816: Need to pass the Paramaters to the GoToFrom function
	; 03-Oct-2008	GRF		SR15810: Specific tag to meet coding standards.
	; 30-Sep-2008	GRF		SR15810: Call INStocktakePost rather than INWEINRUECK.
	; 03-Jan-2008	shobby	SRBR014827: Changing the 'Reason Code' field will
	; 						  refresh the form disabling/enabling the 'Post' button.
	; 08-Nov-2007	shobby	SRBR014769: New button 'M' that goes to a Managers View
	; 28-Oct-2007	shobby	SRBR014557: If VORG(15) has disappeared due to change in
	; 							stocktaking number then determine a valid value.
	; 19-Aug-2006	RPW		SR14857: Correct VORG from the header of the stocktake.
	; 29-May-2006	JW		SR14680: Added P-Post
	; 03-May-2006	PP&JW	SR14508: Created
	;-------------------------------------------------------------------------------
	new YEXITFORM,strPARA
	
	set VORG(1) = $$GetYKEY^WWWFORMD()  ;Not necessary????  Seems is necessary.
	do UpdateVORG(.VORG)
	
	merge ^CacheTempStocktakePara(YUSER) = VORG
	
	set VORG(15) = $$FindVORG15(VORG(1),$get(VORG(15))) ;SRBR014557
 	
	set strPARA = YPARA
	kill YPARA
	
	; vvvvv SR15810
	
	; FIXME : <GRF> see UpdateVORG - Values would appear to be in INWEINVP so
	;         shouldn't need to be passed to reports as can be extracted from
	;         the header record.
	 
	if strPARA="" {
		do GoToForm^COMUtilForm("INWEINVD",VORG(1),1,"",$$$NO,"","")
		
	} else {
		do $case(strPARA,
			"C":Report^INStocktakeReport1(),
			"E":GoToForm^COMUtilForm("INWEINVEHeader",VORG(1)),
			"M":GoToForm^COMUtilForm("INWEINVEHeader",VORG(1),,strPARA),		//SR16816
			"D":Report^INStocktakeReport2(),
			"T":Report^INStocktakeReport3(),
			"P":Post^INStocktakePost($get(VORG(1)),$get(VORG(2)),$get(VORG(3)),$get(VORG(7)),$get(VORG(8)),$get(VORG(13)))
			)
	}
	
	;-------------------------------------------------------------------------------
	; 			  INWEINVD
	; VORG(1)		P1		Stocktake No
	; VORG(2)		M2		Location
	; VORG(3)		M3		Item Group
	; VORG(7)		M7		From Storage
	; VORG(8)		M8		To Storage
	; VORG(13)		M13		Reason Code
	; VORG(15)		M15		Count
	;-------------------------------------------------------------------------------
	
	/*		
	if strPARA="" {
		do GoToForm^COMUtilForm("INWEINVD",VORG(1),1,"",$$$NO,"","")
	} else {
		do $case(strPARA,
			"C":^INWEINVD,
			"E":GoToForm^COMUtilForm("INWEINVEHeader",VORG(1)),
			"M":GoToForm^COMUtilForm("INWEINVEHeader",VORG(1)),
			"D":^INWEINVDIF,
			"T":^INWEINVDS,
			"P":^INWEINRUECK)
	}
	*/
	; ^^^^^ SR15810
	
	set YPARA=""
	quit
	
	
FindVORG15(pidStocktake,pVORG15="")
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 10-Oct-2008	GRF		SR15810: clarify variable
	; 27-Oct-2007	shobby	SRBR014730: If the value of VORG(15) has disappeared
	;							due to screen redraw.  Make a guess of a valid value.
	;-------------------------------------------------------------------------------
	new objINWEINVP,intGuess
	
	set objINWEINVP = $get(^INWEINVP(YM,pidStocktake,1))
	if (pVORG15>$$$INWEINVPNumberofCounts(objINWEINVP)) || (pVORG15="") {
		set intGuess = 1 ;$$$INWEINVPNumberofCounts(objINWEINVP)
	} else {
		set intGuess = pVORG15
	}
	quit intGuess
	
	
OnBeforeFormConstruction()
	;-------------------------------------------------------------------------------
	; Changes the view of a form to Standard or Manual form depending
	; on the context of the display
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 07-Jan-2008	shobby	SRBR014866: Clear YPARA so that refreshing the 'Reason
	; 							Code' field does not reuse the value previously set
	; 							when pressing the M or E buttons etc.
	; 13-Nov-2007	shobby	SRBR014769: Created
	;-------------------------------------------------------------------------------
	if YFORM="INWEINVD" {
		if YPARA="C" {
			set $piece(YVOR,Y,2) = 4	;Manual Form
		} else {
			set $piece(YVOR,Y,2) = 1	;Standard form
		}
	}
	set YPARA = ""
	quit 
	
	
UpdateVORG(&VORG)
	;-------------------------------------------------------------------------------
	; Setup VORG to use the header of the stocktake as a data source
	;
	; Params:
	; VORG: The entries of how the stocktake should be handled
	;
	; ByRefs:
	; VORG
	;
	; Returns:
	;
	; History:
	; 08-Nov-2007	shobby	SRBR014769: Added VORG(9)
	; 19-Aug-2006	RPW		SR14857: Created
	;-------------------------------------------------------------------------------
	new idStocktake,objStocktake,objINWEINVDManual
	
	set idStocktake  = VORG(1)
	set objStocktake = $get(^INWEINVP(YM,idStocktake,1))
	
	set VORG(2) = $$$INWEINVPSiteLocation(objStocktake)
	set VORG(3) = $$$INWEINVPItemGroup(objStocktake)
	set VORG(7) = $$$INWEINVPFromStockLocation(objStocktake)
	set VORG(8) = $$$INWEINVPToStockLocation(objStocktake)
	
	set objINWEINVDManual = $$GetFormData^COMUtilForm("INWEINVD","M")	
	set VORG(9) = $piece(objINWEINVDManual,Y,9)    ; bln - Print Stock On Hand Qty
	quit
	
	
OnBlur(pintField="", pYFELD="", pYKEY="")
	;-------------------------------------------------------------------------------
	; Called BY :
	; 	form INWEINVD : F15 (Count) - OnBlur
	; 
	; Params:
	;	pintField
	; 
	; Returns:
	;
	; History:
	; 10-Oct-2008	GRF		SR15810: Return value "1" not required - removed
	; 13-Nov-2007	shobby	SRBR014730: Created
	;-------------------------------------------------------------------------------
	quit:$$$NoKey(pYKEY)
	
	if (pintField'="") && (YUCI'="") && (YUSER'="") {
		set ^CacheTempStockTake(YUCI,YUSER,"M",pintField) = $get(YINHALT)
	}
	
	if ( (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1))))
	 || (+$$$WWWClientParamCoreChangesSESPE($get(^WWWClientParam(YM,YM,1))))
	 || (+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(YM,YM,1))))) {
	
		set fieldValue = $piece($$GetFormData^COMUtilForm("INWEINVD","M"),Y,15)
		set contagem = $piece($get(^INWEINVP(YM,pYKEY,1)),Y,26)	
		quit:(contagem="")
	
		if (fieldValue = "") {
			$$$Alert("Favor especificar a contagem que deseja visualizar.")
			set ^VARTempContagem(YM,pYKEY,$$$NO) = $$$OK
			do GoToForm^COMUtilForm("INWEINVD",pYKEY)	
		} elseif (+fieldValue '= 0){
			kill ^VARTempContagem(YM,pYKEY,$$$NO)
		}
	 }
	 
	quit
	
	
OnAfterPrimaryKey(YKEY)
	;-------------------------------------------------------------------------------
	; Code to run after the primary key
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Nov-2007	shobby	SRStocktake: Created
	;-------------------------------------------------------------------------------
	merge VORG = ^CacheTempStocktakePara(YUSER)
	do BuildCOMTempList(YKEY)
	quit
	
	
BuildCOMTempList(pidINWEINVP="")
	;-------------------------------------------------------------------------------
	; Builds the list of counts that can be selected.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 10-Oct-2008	GRF		SR15810: Return value "1" not required - removed
	; 26-Nov-2007	shobby	SR:Adhoc : Removed PopulateDataField (Can give a
	; 							javascript error) Anyway default value is set in the
	; 							'Default Value' event of the field.
	; 13-Nov-2007	shobby	SR:Stocktake Created
	;-------------------------------------------------------------------------------
	new objData,idINWEINVP,objINWEINVP,intLoop
	
	kill ^COMTempList(YM,YUSER,"StockTakeCount")
	
	if $get(pidINWEINVP)'="" {
		set objINWEINVP=$get(^INWEINVP(YM,pidINWEINVP,1))
		for intLoop=1:1:$$$INWEINVPNumberofCounts(objINWEINVP) {
			set ^COMTempList(YM,YUSER,"StockTakeCount",intLoop,1) = ""
		}
	}
	quit
	
	
OnAfterDataFields(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
	; Forces focus to the drop down 'Count' list.
	; There seems to be some curious logic in WWWBODY that is supposed to do something
	; like this, but only on certain conditions.  Rather than trying to debug that 5 minutes
	; before a build this routine was used to force the issue.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 10-Oct-2008	GRF		SR15810: Return value "1" not required - removed
	; 15-Nov-2007	shobby	SRBR014780: Created
	;-------------------------------------------------------------------------------
	$$$StartScript()
	write YCR," var objcontrol=document.getElementById('Y"_YFORM_"M15');"
	write " if(objcontrol.type!='hidden' && !objcontrol.disabled) {objcontrol.focus();}"
	$$$EndScript()
	quit
	
	
DefaultVariableInput(pYKEY="",pYFELD,pYLFN)
	new strValue
	
	set strValue = ""
	if pYLFN=15 {
		set strValue = $$$INWEINVPNumberofCounts(pYFELD)
	
	} elseif pYLFN=-1 {   ;Primary Key
		set strValue = YKEY
	}
	quit strValue
	
	
GetNumberofCounts()
	;-------------------------------------------------------------------------------
	; ***** TODO: Don't think this is used any more.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 15-Oct-2008	GRF		SR15810: variable prefix; use YM
	; 13-Nov-2007	shobby	SR<place SR Number here>: Created
	;-------------------------------------------------------------------------------
	new idStocktake,intCount,objData,objSTHeader
	
	set objData = $$GetFormData^COMUtilForm(YFORM,"M")
	
	set intCount    = ""
	set idStocktake = $piece(objData,Y,1)
	if idStocktake'="" {
		set objSTHeader = $get(^INWEINVP(YM,idStocktake,1))
		set intCount    = $$$INWEINVPNumberofCounts(objSTHeader)
	}
	quit intCount
	
	
IsButtonDisabled(pidStocktake,pintButton)
	;-------------------------------------------------------------------------------
	; Common method to determine for a number of buttons whether they are disabled.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 14-Oct-2008	GRF		SR15810: Move CanCloseCount test to INStocktakeCount;
	; 							get strStatus once.
	; 10-Jan-2008	pjones	BR014866 : pintButton=5 now calls CanPostHandler rather
	; 							than CanPost directly
	; 12-Nov-2007   pjones	Returning the strStatus 
	; 07-Nov-2007	shobby	SRBR014763: Created
	;-------------------------------------------------------------------------------
	new intMaxCounts,intMinCounts,objSetup,strStatus
	
	set strStatus = $$$OK
	set objSetup  = $get(^INVORG(YM,YM,1))
	
	if '$$$NoKey(pidStocktake) {   ;@netManager will handle if no key
		set intMaxCounts = $$$INVORGStocktakeMaxCount(objSetup)
		set intMinCounts = $$$INVORGStocktakeMinCount(objSetup)
		
		if pintButton      =1 { set strStatus = $$CanEditCount^INStocktaking(pidStocktake)
		} elseif pintButton=5 { set strStatus = $$CanPostHandler^INStocktaking(pidStocktake)
		} elseif pintButton=6 { set strStatus = $$CanCloseCounting^INStocktaking(pidStocktake,intMinCounts)
		} elseif pintButton=7 { set strStatus = $$CanCreateNewCount^INStocktaking(pidStocktake,intMaxCounts)
		} elseif pintButton=9 { set strStatus = $$CanCloseCount^INStocktakeCount(pidStocktake)
	;	} elseif pintButton=9 { set strStatus = $$CanCloseCount^INStocktaking(pidStocktake)  ; SR15810
		}
	}
	$$$YQHandler(strStatus)
	quit strStatus
	
	
	; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv  SUPERSEDED CODE
	
	;###############################################################################
	; 
	;      THE REPORTING FUNCTION IN THIS ROUTINE HAS BEEN SUPERSEDED BY THE ROUTINE
	;       INStocktakeReport1       WHICH IS BASED ON STOCK BUNDLES.
	; 
	;###############################################################################
Report()
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		DRUCKEN INVENTUR AUFNAHMELISTE
	;		(GENERATION OF STOCKTAKE COUNT SHEETS)
	;
	; Inputs : 
	;
	; ByRef :
	;	VORG(1) = INVENTUR
	;	VORG(2) = BETRIEB
	;	VORG(3) = WARENGRUPPE
	;	VORG(4) = RESERVEIERTE ODER ALLE ;Or 
	;	VORG(5) = Print Supplier
	;	VORG(6) = Print Serial Number
	;	VORG(7) = Bins From
	;	VORG(8) = Bin To
	;	VORG(9) = Print Snapped Quantity
	;	
	; Returns :
	;
	;
	; History :
	; 03-Oct-2008	GRF		SR15810: Specific tag to meet coding standards; separate Heading
	; 17-Aug-2006	GRF 	Doco; quits
	; 07-AUG-2006	FAN		SR14857: Stocktake Batch/Update changes 
	; 01-May-2006	JW&PP	SR14508: Added Bin and Snapped Qty parameters
	; 06.06.2000	DT		Created
	;-------------------------------------------------------------------------------
	new LFN,LFN1,BET,ART,ART1,WG,ME,LAP,AUF,POS,UES
 
	set YKOPF = $$$Text("IN00459") ;Stocktake Count Report		//SR14508
	
	merge VORG = ^CacheTempStocktakePara(YUSER)
 
	SET VORG(1)  =  $GET(VORG(1))  ;VORGABE INVENTUR ;default stocktaking 
	;IF +$GET(VORG(1))=0 DO ^WWWINFO($$^WWWTEXT(32292)) QUIT  ;KEIN Datum ;no Date ;BR014741 allow non numeric stocktakes.
	set VORG(2)  =  $TRANSLATE($get(VORG(2)),",",";")  ; Locn Filter List   ; FIXME : ";" *IS* the @netManager delimiter for
	set VORG(3)  =  $TRANSLATE($get(VORG(3)),",",";")  ; Group Filter List  ; multi-select so these lines should not be necessary
	SET VORG(4)  =  $GET(VORG(4))
	SET VORG(5)  =  $GET(VORG(5))   ;LIEFERRANT DRUCKEN ;print 
	SET VORG(6)  =  $GET(VORG(6))   ;chargen/SN-NUMMER DRUCKEN ;print 
	SET VORG(7)  =  $GET(VORG(7))	; From Storage
	SET VORG(8)  =  $GET(VORG(8))	; To Storage
	SET VORG(9)  = +$GET(VORG(9))	; Print Snapped Quantity
	SET VORG(15) =  $GET(VORG(15))	; Count
	SET VORG(18) =  $GET(VORG(18))	; Count
	
	KILL ^WWWSOR(YUSER)  ;LÖSCHEN ZWISCHENDATEI ;Delete 
	set UES(1) = $$SetHeader(VORG(1),VORG(9))              ; SR15810
	
	DO ^WWWBACK
	DO DRUCK             ;WERTE DRUCKEN ;print 
	DO STOP^WWWTAB       ;ENDE TABELLE ;termination tabulation 
	KILL ^WWWSOR(YUSER)  ;LÖSCHEN ZWISCHENDATEI ;Delete 
 
	QUIT
	
SetHeader(pidStocktake,pblnShowSOH)
	;-------------------------------------------------------------------------------
	;	BEARBEITEN ÜBERSCHRIFT ;Heading
	; 
	; History :
	; 03-Oct-2008	GRF		SR15810: Separate Heading
	;-------------------------------------------------------------------------------
	new strHeading
	
	; TODO : Need to add Lot No, Expiry Date etc. as required to match bin as well as storage.
	;        See INStocktakeReport1
	
	;   1            2            3               4                 5                6               7 (optional)
	;------------------------------------------------------------------------------------------------------------------+
	;         |              |           |                 |                 |                |                        |
	; ####### | Counted:____ | User:____ | Checked By:____ |                 |                |                        |
	;         |              |           |                 |                 |                |                        |
	;   Id #  |    Storage   |   Item    |   Description   | Unit of Measure | Count Quantity | Stock On Hand Quantity |
	;------------------------------------------------------------------------------------------------------------------+
	
	set $piece(strHeading,Y,1) = "<BR>"_VORG(1)_"<BR><BR>"_$$$StrINWEINVStocktakingCountingNumber
	set $piece(strHeading,Y,2) = "<BR>"_$$^WWWTEXT(32056)_":_____________<BR><BR>"_$$$StrINWEINVStockLocation
	set $piece(strHeading,Y,3) = "<BR>"_$$^WWWTEXT(32057)_":_____________<BR><BR>"_$$^WWWTEXT(32024)
	set $piece(strHeading,Y,4) = "<BR>"_$$^WWWTEXT(32058)_":_____________<BR><BR>"_$$^WWWTEXT(32026)
	set $piece(strHeading,Y,5) = "<BR><BR><BR>"_$$^WWWTEXT(32030)
	set $piece(strHeading,Y,6) = "<BR><BR><BR>"_$$$StrINWEINVActualQuantity
	
	if pblnShowSOH set $piece(strHeading,Y,7)="<BR><BR><BR>"_$$^WWWTEXT("IN00449")
	
	quit strHeading
	
SORT
	quit
	
 
DRUCK
	;-------------------------------------------------------------------------------
	; History:
	; 07-Oct-2008	GRF		SR15810: Snapped Qty being shown twice instead of Count
	; 							Qty and Snapped Qty
	; 28-Oct-2007 PJ/shobby	SRBR01557: Check whether the item is valid in this count
	; 27-Nov-2006	JW		BR014285: Item group change.  Lang text.
	; 30-May-2006	JW		SR14680: Show reserved/order nums
	; 19-May-2006	JW		SR14652: Mark as printed
	; 01-May-2006	JW&PP	SR14508: Added Bin and Snapped Qty parameters
	;-------------------------------------------------------------------------------
	new blnShow
	
	SET SUM=0  ;SUMME ;sum 
	//DO ^INLISTKOPF  ;DRUCKEN DECKBLATT ;print 
	DO Print^WWWLISTKOPF("11;12;13;14")	//SR14508
	
	DO UPUEB  ;DRUCKEN ÜBERSCHRIFT ;print superscription 
	SET YDDSATZ=0
	
	;AUSWERTUNG DRUCKEN ;print 
	SET LFN=""
	FOR  SET LFN=$ORDER(^INWEINV(YM,VORG(1),LFN)) QUIT:LFN=""  DO 
	. if '$$Exists^INWEINVCount(VORG(1),LFN,$get(VORG(15))) quit
	. SET LFN1=$GET(^INWEINV(YM,VORG(1),LFN,1))  ;BESTANDSDATEN
	. QUIT:LFN1=""
	. IF VORG(4)=1 IF $PIECE(LFN1,Y,32)'="" QUIT  ;NUR FREIE WARE ;only wares 
	. IF VORG(4)=2 IF $PIECE(LFN1,Y,32)=""  QUIT  ;NUR AUFTRÄGE ;only 
	. ;
	. SET BET=$PIECE(LFN1,Y,2)                    ;BETRIEB
	. IF VORG(2)'="" QUIT:'$FIND(";"_VORG(2)_";",";"_BET_";")  ;NICHT FÜR DIESEN BETRIEB
	. SET ART=$PIECE(LFN1,Y,1)                    ;ARTIKEL ;item 
	. QUIT:ART=""
	. SET ART1=$GET(^INART(YM,ART,1))
	. ;
	. SET WG=$PIECE(ART1,Y,30) ;WARENGRUPPE
	. IF $GET(VORG(3))'="" QUIT:WG=""||'$FIND(";"_VORG(3)_";",";"_WG_";")  ;FALSCHE WARENGRUPPE
	. ;
	. SET ME=""   ;MENGENEINHEIT
	. IF $PIECE(ART1,Y,40)'="" SET ME=$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(ART1,Y,40),1)),Y,1)
	. SET LAP=$PIECE(LFN1,Y,3) 
	. ;
	. quit:(VORG(7)'="")&&(VORG(7) ] LAP)		// SR14508: Stock location limiter
	. quit:(VORG(8)'="")&&(LAP ] VORG(8))
	. ;
	. if $$$INWEINVCountPrinted(LFN1)="" do  //SR14652 - Straight set as may save may interfere with editting...
	. . set $$$INWEINVCountPrinted(^INWEINV(YM,VORG(1),LFN,1))=$horolog
	. ;
	. SET WED=$PIECE(LFN1,Y,6) 
	. SET AUF=$PIECE(LFN1,Y,32) 
	. SET POS=$PIECE(LFN1,Y,33) 
	. ;
	. WRITE !                ; TODO : ! and YCR?
	. WRITE YCR
	. DO NL^WWWTAB
	. DO NFR^WWWTAB
	. WRITE LFN  ;ZÄHLNUMMER
	. WRITE "&nbsp;"
	. DO ZW^WWWTAB
	. WRITE BET," / ",LAP            ;BETRIEB UND LAGERPLATZ ;location And stock location 
	. IF $PIECE(LFN1,Y,9)="HISTORY" WRITE " "_$$^WWWTEXT(32859)	; History	//BR014285
	. IF $PIECE(LFN1,Y,6)'="" WRITE "<BR>("_$$^WWWDATE($PIECE(LFN1,Y,6))_")"
	. WRITE "&nbsp;"
	. DO ZW^WWWTAB
	. WRITE ART_"<BR>"
	. //IF WG'="" WRITE "("_WG_") "_$PIECE($GET(^INPARA(YM,"WARENGRUPPE",SPRACHE,WG,1)),Y,1)
	. IF WG'="" WRITE "("_WG_") "_$$GetDescription^INDItemGroup(WG)	//BR014285
	. WRITE "&nbsp;"
	. DO ZW^WWWTAB
	. ;
	. set blnShow = $$$NO
	. ;
	. if $$$INWEINVStockType(LFN1)="S" do 	//SR14680
	. . WRITE "<B>"
	. . WRITE $$^WWWTEXT(32047)_": "_AUF		; Order
	. . IF POS'="" WRITE "-"_POS
	. . WRITE "<BR></b>"
	. . set blnShow = $$$YES
	. ;
	. if $$$INWEINVStockType(LFN1)="R" do
	. . WRITE "<B>"
	. . WRITE $$^WWWTEXT(32480)_": "_AUF		; Reserved
	. . WRITE "<BR></b>"
	. . set blnShow = $$$YES
	. ;
	. SET YINHALT=$PIECE(ART1,Y,1)
	. WRITE $EXTRACT(YINHALT,1,40)
	. SET YINHALT=$PIECE(ART1,Y,14)
	. IF $TRANSLATE(YINHALT,"| ")=$TRANSLATE($PIECE(ART1,Y,1),"| ") SET YINHALT=""
	. IF YINHALT'="" WRITE "<BR>"
	. ;
	. ;LONGER TEXT 
	. FOR YIZ(1)=1:1 QUIT:$PIECE(YINHALT,"|",YIZ(1),999)=""  DO
	. . SET YIZ(2)=$TRANSLATE($$^WWWUML($PIECE(YINHALT,"|",YIZ(1)),1),"|"," ")
	. . QUIT:$TRANSLATE(YIZ(2)," ")=""
	. . WRITE $EXTRACT(YIZ(2),1,40)
	. . IF $EXTRACT(YIZ(2),41,80)'=""   WRITE "<BR>",$EXTRACT(YIZ(2),41,80)
	. . IF $EXTRACT(YIZ(2),81,120)'=""  WRITE "<BR>",$EXTRACT(YIZ(2),81,120)
	. . IF $EXTRACT(YIZ(2),121,160)'="" WRITE "<BR>",$EXTRACT(YIZ(2),121,160)
	. . IF $PIECE(YINHALT,"|",YIZ(1)+1,999)'="" WRITE "<BR>",YCR
	. ;
	. WRITE "&nbsp;"
	. IF VORG(5)=1 IF $PIECE(LFN1,Y,7)'="" DO        ;LIEFERANT INFO ;supplier 
	. . WRITE YCR,"<BR>"
	. . WRITE YCR,"("_$PIECE(LFN1,Y,7)_") "_$PIECE($GET(^INLIEF(YM,$PIECE(LFN1,Y,7),1)),Y,8)
	. ;
	. IF VORG(6)=1 IF ($PIECE(LFN1,Y,31)'="") || ($PIECE(LFN1,Y,5)'="") DO        ;MIT CHARGEN/SERIENNUMMERN DRUCKEN ;by means of print 
	. . WRITE YCR,"<BR>"
	. . WRITE YCR,$PIECE(LFN1,Y,31)  ;CHARGE
	. . IF $PIECE(LFN1,Y,31)'="" IF $PIECE(LFN1,Y,5)'="" WRITE " / "  ;CHARGE
	. . WRITE YCR,$PIECE(LFN1,Y,5)   ;SN
	. ;
	. DO ZWR^WWWTAB
	. WRITE ME                       ;MENGENEINHEIT
	. WRITE "&nbsp;"
	. DO ZWR^WWWTAB
	. ;IF blnShow WRITE +$PIECE(LFN1,Y,44)_"<BR>"            ; SR15810 : This is Snapped Qty (Target) not Count Qty
	. IF blnShow WRITE +$PIECE(LFN1,Y,4)_"<BR>"
	. ;
	. //IF $PIECE(LFN1,Y,32)'="" WRITE $$^WWWZAHL($PIECE(LFN1,Y,4),0,2)_"<BR>"
	. FOR YI=1:1:30 WRITE "&nbsp;"
	. ;
	. if VORG(9) do 	//SR14508
	. . DO ZWR^WWWTAB
	. . write +$piece(LFN1,Y,44)		; Snapped Quantity
	. . write "&nbsp;"
	. ;
	. ;ENDE ;termination 
	. DO EF^WWWTAB
	. DO EL^WWWTAB
	
	QUIT
	
GESAMT ;GESAMT ;total  ;total whole 
	QUIT
	
UPUEB ;ÜBERSCHRIFT ;superscription 
	WRITE "<CENTER>"
	DO START00^WWWTAB
	DO NL^WWWTAB
	DO NF^WWWTAB
	WRITE "<FONT SIZE=3>"
	WRITE "<B>"
	WRITE "<NOBR>"
	WRITE YKOPF
	do Title^INWEINVD(VORG(1))	//SR14508
	WRITE "</NOBR>"
	DO EF^WWWTAB
	DO EL^WWWTAB
	DO STOP^WWWTAB
	DO START100^WWWTAB
	WRITE "</CENTER>"
	
	WRITE "<THEAD>" 
	DO NL^WWWTAB
	FOR I=1:1 QUIT:$PIECE(UES(1),Y,I,99)=""  DO NHW^WWWTAB WRITE $PIECE(UES(1),Y,I),"&nbsp;" DO EH^WWWTAB
	DO EL^WWWTAB
	WRITE "</THEAD>"
	QUIT
	
Title(pidStocktake)
	new strName,dteDATE
	
	;WRITE "<BR>",$$^WWWDATE(pidStocktake)  ;INVENTURDATUM
	WRITE "<BR>",pidStocktake  ;INVENTUR; 07-AUG-2006  FAN     SR14857: Stocktake Batch/Update changes 
	set strName = $piece($get(^INWEINVP(YM,pidStocktake,1)),Y,4)
	IF strName'="" WRITE " - "_strName                            ;BEMERKUNG ;remark
	set dteDATE = $piece($get(^INWEINVP(YM,pidStocktake,1)),Y,2)  ;SR14857: Stocktake Batch/Update changes 
	IF dteDATE'="" WRITE " - "_$$^WWWDATE(dteDATE)                ;DATE   SR14857: Stocktake Batch/Update changes 
 
	quit
	
	; ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ SUPERSEDED CODE
	
]]></Routine>
</Export>