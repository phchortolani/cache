<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INUMSBIT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INUMSBIT(DATUM1,DATUM2,KATEGORIE,KEY,ART)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUFBEREITEN UMSATZ MIT BITSUCHE - PREPARE CONVERSION WITH BIT SEARCH
	;
	; Inputs : 
	;	DATUM1 = VON DATUM (FROM DATE)  IF="" : FROM THE BEGINNING OF THE JEAR  ($H FORMAT)
	;	DATUM2 = BIS DATUM  (UNTIL DATE) IF="" : UNTIL TODAY        (FORMAT: $H FORMAT)
	;
	;	KATEGORIE  ;category 
	;	  0 =GESAMT KUNDE                (SUMMERY CUSTOMER)
	;	  1 =GESAMT LIEFERANT            (SUMMERY SUPPLIER)
	;	  2 =GESAMT ARTIKEL              (SUMMERY ITEM)
	;	  3 =GESAMT WARENGRUPPEN         (SUMMERY ITEMGROUP)
	;
	;	KEY = KUNDE ODER LIEFERANT ODER ARTIKEL ODER WAREMGRUPPE;  WENN "", DANN =ALLES
	;	      (CUSTOMER OR SUPPLIER OR ITEM OR ITEM GROUP    IF =""  ALL)
	;
	;	ART = UMSATZART = UMSATZ 0=NACH AUFTRÄGEN 1=NACH RECHNUNGSDRUCK ;Type volume of trade 
	;	                  MENGE  2=NACH AUFTRÄGEN 3=NACH RECHNUNGSDRUCK ;quantity 
	;	      (KIND OF STATISTIC: SALES : 0= DEPENDING ON ORDERS 1= DEPENDING ON INVOICE PRINT   
	;	                       QUANTITY : 2= DEPENDING ON ORDERS 3= DEPENDING ON INVOICE PRINT)
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 07-Jun-2010	GRF		SR17146: call "DD.MM.YYYY" wrapper for WWWDATE1 (DMY) &
	;							for WWWDATE (IntToDMY)
	; 26-Sep-2006	GRF		Doco; !=>||; quits; remove unnecessary SUBSELECT'="" test
	; 26-Aug-2005	FIS		SR13264	filter cancelled items -> need field 9 in WWW001B !
	; 12.01.2005	FIS
	;-------------------------------------------------------------------------------
	NEW UMSATZ,BITSUM,MENGE
	
	IF $GET(YFORM)="" NEW YFORM SET YFORM="INAUFP"
	
	SET DATUM1=$GET(DATUM1)
	;F DATUM1="" SET DATUM1=$$^WWWDATE1("01.01."_$$^WWWYEAR($HOROLOG))  ; SR17146
	IF DATUM1="" SET DATUM1=$$DMY^WWWDATE1("01.01."_$$^WWWYEAR($HOROLOG))  ;VON DATUM ;From Date 
	
	SET DATUM2=$GET(DATUM2)
	IF DATUM2="" SET DATUM2=+$HOROLOG
	
	;I DATUM1'=$$^WWWDATE1("01.01.2005") Q ""  ;TEST
	SET KATEGORIE=+$GET(KATEGORIE)
	SET KEY=$GET(KEY)
	SET ART=+$GET(ART)
	
	SET BITSUM=1
	SET UMSATZ=""
	SET MENGE=""
	
	DO UMSATZ
	
	IF ART>1 QUIT MENGE  ;RÜCKGABE DER MENGE ;the quantity 
	QUIT UMSATZ
	
UMSATZ	;ERMITTELN UMSÄTZE; ACHTUNG !! EINSPRUNG AUS INUMS !!!
	;-------------------------------------------------------------------------------
	;-------------------------------------------------------------------------------
	
	;+++++++++++++++++++++++++++++++++++++++
	; AUF1			objINAUF
	; POS1			objINAUFP
	;+++++++++++++++++++++++++++++++++++++++
	
	NEW WOCHE1,WOCHE2,YDATEI,YEAR,WOCHE,YDATEI,YKEYX,JAHR,QUARTAL,MONATE,WOCHEN,TAGE,YFORM
	
	SET YFORM="INAUFP"
	SET YDATEI="INAUFP"
	KILL ^WWWSOR(YUSER,"UMSATZ")
	KILL ^WWWDATEN(YM,+$HOROLOG,YUSER_"TEMP",YFORM,"V")
	MERGE ^WWWDATEN(YM,+$HOROLOG,YUSER_"TEMP",YFORM,"V")=^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V")
	KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V")
	KILL ^WWWSOR(YUSER,"SEL")
	KILL ^WWWSOR(YUSER,"KEY")
	
	SET JAHR=""
	SET QUARTALE=""
	SET MONATE=""
	SET WOCHEN=""
	SET TAGE=""
	;W $$^WWWDATE(DATUM1)_"/"_$$^WWWDATE(DATUM2)
	
	;JAHRESUMSATZ (VON 01.01. BIS 31.12.)
	DO
	. NEW JAHR1,JAHR2,YEAR
	. SET JAHR1=$$^WWWYEAR(DATUM1)
	. SET JAHR2=$$^WWWYEAR(DATUM2)
	.;IF DATUM1=$$^WWWDATE1("01.01."_JAHR1) IF DATUM2=$$^WWWDATE1("31.12."_JAHR2) DO            ; SR17146
	. IF (DATUM1=$$DMY^WWWDATE1("01.01."_JAHR1)) && (DATUM2=$$DMY^WWWDATE1("31.12."_JAHR2)) DO
	. . FOR YEAR=JAHR1:1:JAHR2 SET JAHR=JAHR_YEAR_" "
	. . IF $EXTRACT($REVERSE(JAHR))=" " SET JAHR=$REVERSE($EXTRACT($REVERSE(JAHR),2,999))
	. . IF (ART=0) || (ART=2) DO  ;UMSATZ JAHR (AUFTRAGSDATUM)
	. . . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD389",1)=JAHR  ;VORGABE JAHR
	. . ;
	. . IF (ART=1) || (ART=3) DO  ;RECHNUNG JAHR (RECHNUNGSDATUM)
	. . . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD401",1)=JAHR  ;VORGABE JAHR
	
	;QUARTALSUMSATZ (Z.B. 2004-1 BIS 2004-2)
	IF JAHR="" DO
	. NEW JAHR1,JAHR2,YEAR,QUART
	. SET JAHR1=$$^WWWYEAR(DATUM1)
	. SET JAHR2=$$^WWWYEAR(DATUM2)
	.;IF '$FIND(";01.01."_JAHR1_";01.04."_JAHR1_";01.07."_JAHR1_";01.10."_JAHR1_";",";"_$$^WWWDATE(DATUM1,"DE")_";") QUIT
	.;IF '$FIND(";31.03."_JAHR2_";30.06."_JAHR2_";30.09."_JAHR2_";31.12."_JAHR2_";",";"_$$^WWWDATE(DATUM2,"DE")_";") QUIT
	. IF '$FIND(";01.01."_JAHR1_";01.04."_JAHR1_";01.07."_JAHR1_";01.10."_JAHR1_";",";"_$$IntToDMY^WWWDATE(DATUM1)_";") QUIT
	. IF '$FIND(";31.03."_JAHR2_";30.06."_JAHR2_";30.09."_JAHR2_";31.12."_JAHR2_";",";"_$$IntToDMY^WWWDATE(DATUM2)_";") QUIT
	. IF JAHR1'=JAHR2 DO  ;INNERHALB MEHRERER JAHRE
	. . FOR YEAR=JAHR1:1:JAHR2 DO
	. . . IF YEAR=JAHR1 FOR QUART=$$^WWWQUARTER(DATUM1):1:4 DO
	. . . . SET QUARTALE=QUARTALE_YEAR_"-"_QUART_" "
	. . . ;
	. . . IF YEAR=JAHR2 FOR QUART=1:1:$$^WWWQUARTER(DATUM2) DO
	. . . . SET QUARTALE=QUARTALE_YEAR_"-"_QUART_" "
	. . . ;
	. . . IF YEAR'=JAHR1 IF YEAR'=JAHR2 FOR QUART=1:1:4 DO
	. . . . SET QUARTALE=QUARTALE_YEAR_"-"_QUART_" "
	. ;
	. IF JAHR1=JAHR2 DO  ;INNERHALB EINES JAHRES
	. . FOR QUART=$$^WWWQUARTER(DATUM1):1:$$^WWWQUARTER(DATUM2) DO
	. . . SET QUARTALE=QUARTALE_JAHR1_"-"_QUART_" "
	. ;
	. IF $EXTRACT($REVERSE(QUARTALE))=" " SET QUARTALE=$REVERSE($EXTRACT($REVERSE(QUARTALE),2,999))
	. IF (ART=0) || (ART=2) DO  ;UMSATZ QUARTAL (AUFTRAGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD388",1)=QUARTALE  ;VORGABE QUARTALE
	. ;
	. IF (ART=1) || (ART=3) DO  ;RECHNUNG QUARTAL (RECHNUNGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD400",1)=QUARTALE  ;VORGABE QUARTALE
	
	;MONATSUMSATZ (Z.B. 2004-1 BIS 2004-2)
	IF JAHR="" IF QUARTALE="" DO
	. NEW YEAR,MONTH,JAHR1,JAHR2
	. SET JAHR1=$$^WWWYEAR(DATUM1)
	. SET JAHR2=$$^WWWYEAR(DATUM2)
	.;QUIT:DATUM1'=$$^WWWDATE1("01."_$$^WWWMONTH(DATUM1)_"."_JAHR1)  ; SR17146
	.;QUIT:DATUM2'=$$^WWWDATE1($$^WWWMONTHDAYS(DATUM2)_"."_$$^WWWMONTH(DATUM2)_"."_JAHR2)
	. QUIT:DATUM1'=$$DMY^WWWDATE1("01."_$$^WWWMONTH(DATUM1)_"."_JAHR1)                        ;ERSTER DES MONATS BIS
	. QUIT:DATUM2'=$$DMY^WWWDATE1($$^WWWMONTHDAYS(DATUM2)_"."_$$^WWWMONTH(DATUM2)_"."_JAHR2)  ;ZUM LETZTEN EINES MONATS
	. SET MONATE=""
	. IF JAHR1'=JAHR2 FOR YEAR=JAHR1:1:JAHR2 DO  ;ALLE JAHRE
	. . IF YEAR=$$^WWWYEAR(DATUM1) FOR MONTH=$$^WWWMONTH(DATUM1):1:12  DO  ;MONATE
	. . . SET MONATE=MONATE_YEAR_"-"_$EXTRACT(100+MONTH,2,3)_" "
	. . ;
	. . IF YEAR=$$^WWWYEAR(DATUM2) FOR MONTH=1:1:$$^WWWMONTH(DATUM2)  DO  ;MONATE
	. . . SET MONATE=MONATE_YEAR_"-"_$EXTRACT(100+MONTH,2,3)_" "
	. . ;
	. . IF YEAR'=JAHR1 IF YEAR'=JAHR2 FOR MONTH=1:1:12 DO  ;MONATE
	. . . SET MONATE=MONATE_YEAR_"-"_$EXTRACT(100+MONTH,2,3)_" "
	. ;
	. IF JAHR1=JAHR2 DO  ;INNERHALB EINES JAHRES
	. . FOR MONTH=$$^WWWMONTH(DATUM1):1:$$^WWWMONTH(DATUM2)  DO  ;MONATE
	. . . SET MONATE=MONATE_JAHR1_"-"_$EXTRACT(100+MONTH,2,3)_" "
	. ;
	. IF $EXTRACT($REVERSE(MONATE))=" " SET MONATE=$REVERSE($EXTRACT($REVERSE(MONATE),2,999))
	. IF (ART=0) || (ART=2) DO  ;UMSATZ MONAT (AUFTRAGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD387",1)=MONATE  ;VORGABE MONATE
	. ;
	. IF (ART=1) || (ART=3) DO  ;RECHNUNG MONAT (RECHNUNGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD399",1)=MONATE  ;VORGABE MONATE
	
	;WOCHENUMSATZ (Z.B. 2005-04 BIS 2006-03)
	IF JAHR="" IF QUARTALE="" IF MONATE="" DO
	. NEW YEAR
	. IF KATEGORIE'=1 QUIT:$$^WWWDAY(DATUM1)'=1  ;WOCHENANFANG (KEIN QUIT BEI LIEFERANTENUMSATZ)
	. IF KATEGORIE'=1 QUIT:$$^WWWDAY(DATUM2)'=7  ;WOCHENENDE
	. IF KATEGORIE=1 IF (ART=0) || (ART=2) QUIT:$$^WWWDAY(DATUM1)'=1  ;WOCHENANFANG (KEIN QUIT BEI LIEFERANTENUMSATZ NACH RECHNUNGSDATUM)
	. IF KATEGORIE=1 IF (ART=0) || (ART=2) QUIT:$$^WWWDAY(DATUM2)'=7  ;WOCHENENDE
	. SET MONATE=""
	. SET WOCHE1=$$^WWWWEEK(DATUM1,3)  ;VON ZEITRAUM WOCHE YYYY-WW
	. SET WOCHE2=$$^WWWWEEK(DATUM2,3)  ;VON ZEITRAUM WOCHE YYYY-WW
	. SET WOCHEN=""
	. IF $PIECE(WOCHE1,"-",1)'=$PIECE(WOCHE2,"-",1) FOR YEAR=$PIECE(WOCHE1,"-",1):1:$PIECE(WOCHE2,"-",1) DO  ;ALLE JAHRE
	. . IF YEAR=$PIECE(WOCHE1,"-",1) FOR WOCHE=+$PIECE(WOCHE1,"-",2):1:53  DO  ;WOCHEN
	. . . SET WOCHEN=WOCHEN_YEAR_"-"_$EXTRACT(100+WOCHE,2,3)_" "
	. . ;
	. . IF YEAR=$PIECE(WOCHE2,"-",1) FOR WOCHE=1:1:+$PIECE(WOCHE2,"-",2)  DO  ;WOCHEN
	. . . SET WOCHEN=WOCHEN_YEAR_"-"_$EXTRACT(100+WOCHE,2,3)_" "
	. . ;
	. . IF YEAR'=$PIECE(WOCHE1,"-",1) IF YEAR'=$PIECE(WOCHE2,"-",1) FOR WOCHE=1:1:53 DO  ;WOCHEN
	. . . SET WOCHEN=WOCHEN_YEAR_"-"_$EXTRACT(100+WOCHE,2,3)_" "  ;ALLE WOCHEN
	. ;
	. IF $PIECE(WOCHE1,"-",1)=$PIECE(WOCHE2,"-",1) DO  ;INNERHALB EINES JAHRES
	. . FOR WOCHE=+$PIECE(WOCHE1,"-",2):1:+$PIECE(WOCHE2,"-",2) DO  ;WOCHEN
	. . . SET WOCHEN=WOCHEN_$PIECE(WOCHE1,"-",1)_"-"_$EXTRACT(100+WOCHE,2,3)_" "
	. ;
	. IF $EXTRACT($REVERSE(WOCHEN))=" " SET WOCHEN=$REVERSE($EXTRACT($REVERSE(WOCHEN),2,999))
	. IF (ART=0) || (ART=2) DO  ;UMSATZ WOCHE (AUFTRAGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD386",1)=WOCHEN  ;VORGABE WOCHEN
	. ;
	. IF (ART=1) || (ART=3) DO  ;RECHNUNG WOCHE (RECHNUNGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD398",1)=WOCHEN  ;VORGABE WOCHEN
	
	;TAGESUMSATZ (Z.B. 16.01.2005 BIS 17.01.2005)
	IF JAHR="" IF QUARTALE="" IF MONATE="" IF WOCHEN="" DO
	. NEW TAG
	. SET TAGE=+$HOROLOG
	. FOR TAG=DATUM1:1:DATUM2 SET TAGE=TAGE_TAG_" "
	. ;
	. IF (ART=0) || (ART=2) DO  ;UMSATZ TAGE (AUFTRAGSDATUM)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD193",1)=TAGE  ;VORGABE TAGE
	. ;
	. IF (ART=1) || (ART=3) DO  ;RECHNUNG TAGE (RECHNUNGSDATUM) -> (RECHNUNG GEDRUCKT AM, NICHT LIEFERANTENUMSATZ !!)
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD97",1)=TAGE  ;VORGABE TAGE 
	
	;FILTER BY CATEGORY
	IF +KATEGORIE=0 IF KEY'="" DO  ;KUNDENUMSATZ
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD360",1)=KEY  ;SPECIFIC CUSTOMER
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","XINAUFPD360",1)=1    ;DEFAULT SEARCH LOGIC: AND
	
	IF KATEGORIE=1 IF KEY'="" DO  ;LIEFERANTENUMSATZ
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD12",1)=KEY  ;SPECIFIC SUPPLIER
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","XINAUFPD12",1)=1    ;DEFAULT SEARCH LOGIC: AND
	
	IF KATEGORIE=2 IF KEY'="" DO  ;ARTIKELUMSATZ
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD4",1)=KEY  ;SPECIFIC ITEM
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","XINAUFPD4",1)=1    ;DEFAULT SEARCH LOGIC: AND
	
	IF KATEGORIE=3 IF KEY'="" DO  ;WARENGRUPPENUMSATZ
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD30",1)=KEY  ;SPECIFIC ITEM GROUP
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","XINAUFPD30",1)=1    ;DEFAULT SEARCH LOGIC: AND
	
	;IF KATEGORIE=4 IF KEY'="" DO  ;VERKÄUFERUMSATZ (VERKÄUFER AUS AUFTRAG ODER POSITION) KEIN BIT: SIEHE UNTEN !
	;. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD"?,1)=KEY  ;SPECIFIC SALES PERSON
	;. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","XINAUFPD"?,1)=1    ;DEFAULT SEARCH LOGIC: AND
	
	IF $DATA(^WWW001B(0,"INAUFP",9)) DO  ;FILTER CANCELLED LINE ITEMS;FIS;SR13264;26-Aug-2005
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YINAUFPD9",1)=0  ;NOT CANCELLED !
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","XINAUFPD9",1)=1  ;DEFAULT SEARCH LOGIC: AND
	
	;STARTEN BITMAP SUCHE
	DO
	. NEW ANZAHL,ANZAB
	. SET ANZAHL=999999999
	. SET ANZAB=1
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAB",1)=ANZAB
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAHL",1)=ANZAHL
	. DO BITSEARCH^WWWSEARBIT2(YDATEI,$GET(BITSUM),,,$GET(BITSUM))
	;. NEW YRESULT
	;. SET YRESULT=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YRESULT",1)),Y,1)
	
	;SUMMENBILDUNG
	IF $GET(BITSUM)=1 DO
	. IF (ART=0) || (ART=1) SET UMSATZ = $PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YBITSUM",1)),Y,123)  ;NETTO-VERKAUFSPREIS
	. IF (ART=2) || (ART=3) SET MENGE  = $PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YBITSUM",1)),Y,5)    ;MENGE
	
	;VORSELEKTIERTE DATEN (AUFTRÄGE EINES KUNDEN/LIEFERATEN/ARTIKELS ODER WARENGRUPPE IM ZEITRAUM)
	IF $GET(BITSUM)'=1 DO
	. SET YKEYX="" FOR  SET YKEYX=$ORDER(^WWWSOR(YUSER,"SEL",YKEYX)) QUIT:YKEYX=""  DO
	. . SET AUF=$PIECE(YKEYX,",",1)                QUIT:AUF=""
	. . SET POS=$PIECE(YKEYX,",",2)                QUIT:POS=""
	. . SET AUF1=$GET(^INAUF(YM,AUF,1))            QUIT:AUF1=""
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))       QUIT:POS1=""
	. . QUIT:$PIECE(POS1,Y,9)=1  ;STORNIERT;TYBD;02,04,2003;
	. . ;
	. . IF +KATEGORIE=0 IF SUBSELECT=1 DO  QUIT:POS1=""  ;SUBSELECT (VON BIS ARTIKEL)
	. . . IF $GET(SUBFROM)'="" IF $GET(SUBFROM)]]$PIECE(POS1,Y,4)  SET POS1="" QUIT
	. . . IF $GET(SUBTO)'=""   IF $PIECE(POS1,Y,4)]]$GET(SUBTO)    SET POS1="" QUIT
	. . ;
	. . IF KATEGORIE=3  IF SUBSELECT=4 DO  QUIT:POS1=""  ;SUBSELECT (VON KUNDE BIS KUNDE)
	. . . IF $GET(SUBFROM)'="" IF $GET(SUBFROM)]]$PIECE(AUF1,Y,1) SET POS1="" QUIT
	. . . IF $GET(SUBTO)'=""   IF $PIECE(AUF1,Y,1)]]$GET(SUBTO)   SET POS1="" QUIT
	. . ;
	. . IF KATEGORIE=4 IF KEY'="" IF '$FIND(";"_$PIECE(POS1,Y,361)_";"_$PIECE(POS1,Y,362)_";"_$PIECE(AUF1,Y,30)_";"_$PIECE(AUF1,Y,31)_";",";"_KEY_";") QUIT  ;WRONG SALES PERSON
	. . IF BETRIEB'="" QUIT:'$FIND(","_BETRIEB_",",","_$PIECE(AUF1,Y,6)_",")   ;FALSCHER BETRIEB ;WRONG location
	. . IF WARENGRUPPE'="" QUIT:'$FIND(WARENGRUPPE,","_$PIECE(POS1,Y,30)_",")  ;NICHT AUSGEWÄHLTE WARENGRUPPE ;WRONG ITEM GROUP
	. . IF KATEGORIE'=1 IF +$PIECE(AUF1,Y,2)'=0  IF +$PIECE(AUF1,Y,2)'=3 QUIT   ;KEIN KUNDENAUFTRAG;TYBD;23812;24,06,2003 ;CUSTOMER ORDER ONLY
	. . IF KATEGORIE=1 QUIT:$PIECE(POS1,Y,7)'=1   ;KEINE BESTELLUNG ;no sales order
	. . IF KATEGORIE'=1 IF (ART=1) || (ART=3) QUIT:$PIECE(POS1,Y,97)=""  ;NOCH KEINE RECHNUNG ;NO INVOICE YET
	. . IF KATEGORIE=1  IF (ART=1) || (ART=3) QUIT:'$DATA(^INERECH1(YM,AUF,POS))  ;NOCH KEINE EINGANGSRECHNUNG ;NO INVOICE YET
	. . ;
	. . SET DATUM=$PIECE(POS1,Y,193)  ;AUFTRAGSDATUM
	. . IF (ART=1) || (ART=3) SET DATUM=$PIECE(POS1,Y,97)  ;RECHNUNG GEDRUCK AM
	. . IF KATEGORIE=1 IF DATUM="" SET DATUM=$$^WWWWEEK1($PIECE($PIECE(POS1,Y,386),"-",2)_"."_$PIECE($PIECE(POS1,Y,386),"-",1),1)  ;LF-RECHNUNG DATUM
	. . ;IF ART'=1 IF ART'=3 IF $PIECE(POS1,Y,386)'="" SET DATUM=$$^WWWWEEK1($PIECE($PIECE(POS1,Y,386),"-",2)_"."_$PIECE($PIECE(POS1,Y,386),"-",1),1)  ;DATUM AUS KW
	. . ;IF ART=1!(ART=3) IF $PIECE(POS1,Y,398)'="" SET DATUM=$$^WWWWEEK1($PIECE($PIECE(POS1,Y,398),"-",2)_"."_$PIECE($PIECE(POS1,Y,398),"-",1),1)  ;DATUM AUS KW
	. . ;
	. . IF SORT=0 SET SORTFOLGE=$$^WWWMONTH(DATUM)   ;NACH MONATEN ;within 
	. . IF SORT=1 SET SORTFOLGE=+$EXTRACT($PIECE(POS1,Y,30),1,2)   ;NACH WARENGRUPPEN ;within 
	. . ;
	. . SET MONUMS=$PIECE(UMSATZ,",",SORTFOLGE)
	. . SET MONMEN=$PIECE(MENGE,",",SORTFOLGE)
	. . IF VKEK=0 SET MONUMS=MONUMS+($PIECE(POS1,Y,123)/EINHEIT)  ;UMSATZ MINUS RABATT ;volume of trade minus deduction 
	. . IF VKEK=1 SET MONUMS=MONUMS+($PIECE(POS1,Y,47)/EINHEIT)  ;UMSATZ MINUS RABATT ;volume of trade minus deduction 
	. . SET $PIECE(UMSATZ,",",SORTFOLGE)=MONUMS
	. . SET MONMEN=MONMEN+($PIECE(POS1,Y,5)/EINHEIT)  ;MENGE ;quantum  ;quantity 
	. . SET $PIECE(MENGE,",",SORTFOLGE)=MONMEN
	. . ;
	. . ;SPEICHERN AUFBEREITUNG FÜR REPLENISHMENT ;Save to 
	. . IF SAVE=1 DO
	. . . NEW LOCAT,ITEM,YEAR,MONTH,ADJMEN,QTY
	. . . SET LOCAT=$PIECE($GET(^INAUF(YM,AUF,1)),Y,6)
	. . . IF LOCAT="" SET LOCAT=" "
	. . . SET ITEM=$PIECE(POS1,Y,4)
	. . . IF ITEM="" QUIT
	. . . SET YEAR=$$^WWWYEAR(DATUM)
	. . . SET MONTH=$$^WWWMONTH(DATUM)
	. . . SET QTY=$PIECE($GET(^INUMSA(YM,+$HOROLOG,YUSER,LOCAT,ITEM,YEAR,MONTH,1)),Y,1)
	. . . SET QTY=QTY+$PIECE(POS1,Y,5)
	. . . SET ^INUMSA(YM,+$HOROLOG,YUSER,LOCAT,ITEM,YEAR,MONTH,1)=QTY
	. . . SET ^INUMSA(YM,+$HOROLOG,YUSER," "," ",YEAR,MONTH,1)=1  ;SAVE AS CALCULATED ;ace 
	
	KILL ^WWWSOR(YUSER,"SEL")
	KILL ^WWWSOR(YUSER,"KEY")
	KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V")
	MERGE ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V")=^WWWDATEN(YM,+$HOROLOG,YUSER_"TEMP",YFORM,"V")
	KILL ^WWWDATEN(YM,+$HOROLOG,YUSER_"TEMP",YFORM,"V")
	QUIT
	
]]></Routine>
</Export>