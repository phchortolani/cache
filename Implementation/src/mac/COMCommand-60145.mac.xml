<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMCommand" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMCommand
	;-------------------------------------------------------------------------------
	; History:
	; 15-Mar-2005	GRF		Boolean Macros
	;-------------------------------------------------------------------------------
#include COMSYS
 
Command(pstrCommand)
	new strStatus
	
	set strStatus=$$$OK
	write !,"Command: "_pstrCommand
	if $zf(-1,pstrCommand)'=0 {
		set strStatus=$$$Text($listbuild("Com00172",pstrCommand))  ;Command Failed: %1
	}
	quit strStatus
	
MultiCommand(plstCommands)
	new strStatus,strCommand,CommandLoop,strTempDir
	
	set strStatus=$$$OK
	set strTempDir=##class(%File).ManagerDirectory()_"Commands\"_YUSER_"\"
	
	for CommandLoop=1:1:$listlength(plstCommands) {
		set strStatus=$$Command("echo "_$listget(plstCommands,CommandLoop)_" "_$select(CommandLoop=1:">",1:">>")_" "_strTempDir_"run.bat")
		quit:strStatus'=$$$OK
	}
	if strStatus=$$$OK set strStatus=$$Command(strTempDir_"run.bat")
	do $ZUTIL(140,5,strTempDir_"run.bat") 
	quit strStatus
	
Copy(pstrFromFile,pstrNewFile)
	;-------------------------------------------------------------------------------
    ; Copies a file (or directory) to another file (or directory)
    ;
    ; Returns:Status
    ;
    ; History:
    ; 15-Oct-2007	GRF		Standard Error Prefixes
    ; 28-Feb-2005	Paul K	Created
    ;-------------------------------------------------------------------------------
	new strStatus,strFileName
	
	set strStatus=$$$OK
	set strFileName=$piece(pstrFromFile,"\",$length(pstrFromFile,"\"))
	
	if strStatus=$$$OK if strFileName'="" if '##class(%File).Exists(pstrFromFile) set strStatus=$listbuild("Com00303",pstrFromFile)  ;File '%1' does not exist. Contact Support	
	if strStatus=$$$OK set strStatus=$$Command("copy "_pstrFromFile_" "_pstrNewFile)
	if $extract(pstrNewFile,$length(pstrNewFile))="\" {
		if strFileName'="" {
			set pstrNewFile=pstrNewFile_strFileName
		}
	}
	if strStatus=$$$OK if strFileName'="" if '##class(%File).Exists(pstrNewFile) set strStatus=$listbuild("Com00303",pstrNewFile)  ;File '%1' does not exist. Contact Support	
	quit strStatus
	
]]></Routine>
</Export>