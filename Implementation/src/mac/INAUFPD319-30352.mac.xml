<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPD319" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPD319 ;INAUFPD319;FIS;SUCHANZEIGE FÜR RMA;14.05.2003
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		SUCHANZEIGE FÜR RMA
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	14.05.2003
	;|
	;\------------------------------------------------------------------/
	;
	DO
	. WRITE YCR
	. WRITE "<A HREF="_""""
	. SET URL=YAKTION_"EP=WWWMANU1&YEXEC=D|SUCH1^INAUFPD319&YUSER="_YUSER_"&YBED="_YBED_"&YTRAKT="_YTRAKT_"&YUCI="_$GET(YUCI)_"&YM="_YM_"&YKEY="_$G(YKEY)
	. SET OPT="HEIGHT=350,WIDTH=350,SCROLLBARS=YES,RESIZEABLE=YES"
	. WRITE "javascript:var parameter=window.open('"_URL_"','Suche','"_OPT_"');"  
	. WRITE """"
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"search1.gif"_""""_" ALIGN=ABSBOTTOM BORDER=0 TITLE="_""""_$$^WWWTEXT(148)_""""_" border=0>"  ;suchen ;seek 
	. WRITE "</A>"
	. QUIT
	QUIT
	
SUCH1 ;ANZEIGEN DER FERTIGUNGSNUMMERN ;display the 
	NEW RMA,RMA1,ANZ
	DO ^WWWVAR
	IF $PIECE($GET(^WWW120(0,"INAUFP",1)),Y,123)=1 SET YHTMFORM="WWW2"
	DO ^WWWSTART()
	DO ^WWWBODY(2)
	SET ANZ=0
	WRITE "<TABLE BORDER=0 CELLSPACING=0 CELLPADDING=0>"
	FOR ERL=0," " DO
	. SET RMA="" FOR  SET RMA=$ORDER(^INRMAs(YM,1,ERL,RMA)) QUIT:RMA=""  DO
	. . SET RMA1=$GET(^INRMA(YM,RMA,1))
	. . QUIT:RMA1=""
	. . ;
	. . SET ANZ=ANZ+1
	. . WRITE "<TR><TD NOWRAP ALIGN=LEFT>"
	. . WRITE "<FONT FACE=ARIAL SIZE=2>"
	. . WRITE "<A onClick='return doLink(this)' HREF=""JavaScript::opener.document."_YHTMFORM_".YINAUFPD319.value='"_RMA_"';opener.document."_YHTMFORM_".YINAUFPD319.focus();document.close();close();"_""""
	. . WRITE ">"
	. . WRITE RMA
	. . WRITE " - "_$$^WWWDATE($PIECE(RMA1,Y,12))  ;DATUM ;Date  ;Date Date 
	. . IF $PIECE(RMA1,Y,1)'="" WRITE " ("_$P(RMA1,Y,1)_"-"_$P(RMA1,Y,2)_")"  ;AUFTRAG, POS
	. . IF $PIECE(RMA1,Y,5)'="" DO  ;KUNDE ;lore  ;customer 
	. . . WRITE " "_$PIECE(RMA1,Y,5)
	. . . WRITE " "_$EXTRACT($PIECE($GET(^INKUNDE(YM,$PIECE(RMA1,Y,5),1)),Y,8),1,15)
	. . . QUIT
	. . IF $PIECE(RMA1,Y,6)'="" WRITE ", "_$PIECE(RMA1,Y,6)  ;ANSPRECHPARTNER
	. . WRITE "</A>"
	. . WRITE "</FONT>"
	. . WRITE "</TD></TR>"
	. . WRITE YCR
	. . QUIT
	. QUIT
	IF ANZ=0 WRITE "<tr><td align=left><font size=2>"_$$^WWWTEXT(46)_"</font></td></tr>"  ;KEINE DATEN ;no 
	WRITE "</TABLE>"
	DO ^WWWSTOP
	QUIT
	
SET(YINHALT)   ;SETZEN FELDINHALTE ;typeset 
	NEW RMA1,YFELD
	IF $G(YINHALT)="" QUIT
	SET %TXT(1)=$G(%TXT(1))
	DO
	. SET RMA1=$G(^INRMA(YM,YINHALT,1))
	. QUIT:RMA1=""
	. SET YFELD=$G(^WWWDATEN(YM,+$H,YUSER,YFORM,"D",1))
	. SET %TXT(1)=%TXT(1)_"#Y"_YFORM_YART_"220~"_$P(RMA1,Y,3)  ;REKLAMATIONSGRUND
	. SET %TXT(1)=%TXT(1)_"#Y"_YFORM_YART_"222~"_$P(RMA1,Y,4)_"|"_$P(RMA1,Y,18)  ;REKLAMATIONSTEXT
	. SET %TXT(1)=%TXT(1)_"#Y"_YFORM_YART_"237~"_$P(RMA1,Y,1)  ;AUFTRAG ;mandate  ;order 
	. SET %TXT(1)=%TXT(1)_"#Y"_YFORM_YART_"238~"_$P(RMA1,Y,2)  ;POS
	. SET YCROK=0  ;AKTIVIEREN UMBRUCH IN MEMO-FELD ;make-up within 
	. QUIT
	QUIT
]]></Routine>
</Export>