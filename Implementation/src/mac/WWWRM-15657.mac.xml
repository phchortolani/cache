<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWRM" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWRM
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		RAUMÃœBERSICHT
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 04-Jun-2010	GRF		SR17146: call "DD.MM.YYYY" wrapper for WWWDATE1 (DMY) &
	;							for WWWDATE (IntToDMY); </FONT>
	; 02-Sep-2005	JW		SR12966: WWWRM is not shared
	; 05.07.1999	DT
	;-------------------------------------------------------------------------------
	SET MONAT  = $GET(%(YQUERY,"YFUNCT"))
	IF MONAT="" SET MONAT = +$HOROLOG
	SET MONATM = MONAT-30
	SET MONATP = MONAT+30
	SET MONAT  = $$IntToDMY^WWWDATE(MONAT)                      ; SR17146 vvv
	;IF $FIND(MONAT,".") SET VORG(1)=$EXTRACT(MONAT,7,10)_"-"_$EXTRACT(MONAT,4,5)
	;IF $FIND(MONAT,"/") SET VORG(1)=$EXTRACT(MONAT,7,10)_"-"_$EXTRACT(MONAT,1,2)
	SET VORG(1) = $EXTRACT(MONAT,7,10)_"-"_$EXTRACT(MONAT,1,2)  ; SR17146 ^^^
	WRITE "</PRE>"
	WRITE "<FONT FACE=ARIAL>"
	
	SET MON   = $PIECE(VORG(1),"-",2)
	SET JAH   = $PIECE(VORG(1),"-",1)
	SET A     =" 312831303130313130313031"
	SET MENDE = $EXTRACT(A,2*MON-1,2*MON)
	IF JAH#4=0&(+MON=2) SET MENDE = 29
	WRITE "<CENTER>"
	
	;---------------------------------------
	
	WRITE "<H3>"
	WRITE $$^WWWTEXT(370)_": "_$PIECE($GET(^WWW101(0,"MONAT",SPRACHE,+MON,1)),Y,1)_" - "_JAH  
	WRITE "&nbsp;"
	WRITE "&nbsp;"      
	
	IF SPRACHE="DE" DO
	. WRITE "<A HREF="
	. WRITE YAKTION_"EP=WWWFORM&amp;YFORM=WWWRM0&amp;YFUNCT="_MONATM
	. SET YKEY=""
	. DO ^WWWCGI
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"monatm.gif"_""""_" BORDER=0 TITLE='Monat zur&uuml;ck'></A>"
	. ;
	. WRITE "<A HREF="
	. WRITE YAKTION_"EP=WWWFORM&amp;YFORM=WWWRM0&amp;YFUNCT="_MONATP
	. SET YKEY=""
	. DO ^WWWCGI
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"monatp.gif"_""""_" BORDER=0 TITLE='Monat vor'></A>"
	
	IF SPRACHE'="DE" DO
	. WRITE "<A HREF="
	. WRITE YAKTION_"EP=WWWFORM&amp;YFORM=WWWRM0&amp;YFUNCT="_MONATM
	. SET YKEY=""
	. DO ^WWWCGI
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"mmonth.gif"_""""_" BORDER=0 TITLE='Month back'></A>"
	. ;
	. WRITE "<A HREF="
	. WRITE YAKTION_"EP=WWWFORM&amp;YFORM=WWWRM0&amp;YFUNCT="_MONATP
	. SET YKEY=""
	. DO ^WWWCGI
	. WRITE ">"
	. WRITE YCR,"<IMG SRC="_""""_YGIF_"pmonth.gif"_""""_" BORDER=0 TITLE='Month forward'></A>"
	
	WRITE "</H3>"
	
	;---------------------------------------
	
	SET GRUPPE=""
	FOR  SET GRUPPE=$ORDER(^WWWRMs(YM,1,GRUPPE)) QUIT:GRUPPE=""  DO
	. DO TABLE
	. WRITE YCR,"<BR>"
	
	WRITE "</CENTER>"
	QUIT
	
	
TABLE ;AUFLISTUNG NACH GRUPPE ;within group 
	SET GR=GRUPPE
	IF GRUPPE=" " SET GR=""
	DO START100^WWWTAB 
	WRITE "<TR>"
	WRITE "<TH BGCOLOR="_YDARKGRAY_" ALIGN=LEFT WIDTH=150>"
	WRITE "<FONT SIZE=1>"
	WRITE GRUPPE
	WRITE "<BR>"_$$^WWWTEXT(371)
	WRITE "</FONT></TH>"
	
	FOR I=1:1:MENDE DO
	. WRITE YCR
	. WRITE "<TH BGCOLOR="_YDARKGRAY_" ALIGN=LEFT>"
	. WRITE "<FONT SIZE=1>"
	. WRITE $EXTRACT(100+I,2,3)
	. WRITE "<BR>"
	. SET YDATE = $$DMY^WWWDATE1($EXTRACT(100+I,2,3)_"."_$EXTRACT(100+MON,2,3)_"."_JAH)   ; SR17146
	. SET A1    = $$^WWWTEXT(30022) WRITE $EXTRACT($PIECE(A1,",",$$^WWWDAY(YDATE)),1,2)
	. WRITE "</FONT></TH>"
	
	WRITE "</TR>"
	
	;---------------------------------------
	
	SET NAM=""
	FOR  SET NAM=$ORDER(^WWWRM(YM,NAM)) QUIT:NAM=""  DO
	. SET RAUM=$PIECE($GET(^WWWRM(YM,NAM,1)),Y,1)
	. QUIT:GR'=$$^WWWUMLAU($PIECE($GET(^WWWRM(YM,NAM,1)),Y,4),1)
	. WRITE YCR
	. WRITE "<TR>"
	. WRITE "<TD NOWRAP ALIGN=LEFT WIDTH=150>"
	. WRITE "<FONT SIZE=1>"
	. WRITE "<A HREF="
	. WRITE YAKTION_"EP=WWWFORM&amp;YFORM=WWWRM"
	. SET YKEY=NAM
	. SET YBACK="WWWRM0,"
	. DO ^WWWCGI
	. WRITE ">"
	. WRITE NAM_" "_RAUM
	. WRITE "&nbsp;"
	. WRITE "</A>"
	. WRITE "</TD>"
	. ;
	. FOR TAG=1:1:MENDE DO
	. . WRITE YCR
	. . WRITE "<TD ALIGN=LEFT WIDTH=26"
	. . SET SSN=0
	. . SET YDATE = $$DMY^WWWDATE1($EXTRACT(100+TAG,2,3)_"."_$EXTRACT(100+MON,2,3)_"."_JAH)   ; SR17146
	. . IF YDATE'="" IF $DATA(^TERMIN9(0,"DE",YDATE,1)) WRITE " BGCOLOR=MISTYROSE" SET SSN=1
	. . SET JN=$PIECE($GET(^WWWRM1(YM,NAM,YDATE,1)),Y,1,3)
	. . IF JN="" IF $$^WWWDAY(YDATE)>5                  WRITE " BGCOLOR=MISTYROSE" SET SSN=1
	. . IF JN="" IF SSN=0                               WRITE " BGCOLOR="_YWHITE
	. . IF JN'=""                                       WRITE " BGCOLOR=BLUE"
	. . WRITE ">"
	. . WRITE "<FONT SIZE=1>"
	. . WRITE "<A HREF="
	. . WRITE YAKTION_"EP=WWWFORM&amp;YFORM=WWWRM1"
	. . SET YFKEY=NAM
	. . SET YKEY=NAM_","_YDATE
	. . SET YBACK="WWWRM0,"
	. . DO ^WWWCGI
	. . WRITE ">"
	. . WRITE "&nbsp;"
	. . IF SSN'=1 IF JN'="" WRITE "X"
	. . IF SSN=1            WRITE "-&nbsp;"
	. . IF SSN=0  IF JN=""  WRITE "&nbsp;&nbsp;"
	. . WRITE "&nbsp;"
	. . WRITE "</A>"
	. . WRITE "</TD>"
	. ;
	. WRITE "</TR>"
	
	WRITE "</TABLE>"
	WRITE "</CENTER>"
	
	QUIT
	
]]></Routine>
</Export>