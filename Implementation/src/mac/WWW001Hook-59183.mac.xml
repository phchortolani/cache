<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW001Hook" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW001Hook
	
#include COMSYS
#include WWWConst
	
ExecuteHook(pidClass="",penumEvent,&YKEY="",&YFELD="",YFORM="",YFELDOLD="",YFIELDNAME="",&pblnCheck=$$$NO)  ;SR17847 ;CORE-286
	;-------------------------------------------------------------------------------
	; Checks if a Hook exists for a given form/event, and runs the Hook code.
	; If all is ok, goes on to try VAR Hooks.
	;
	; Params: 	Class Name, 
	; 			Event Type : WWW101("WWWEVENTTYPE") enumerator,
	; 			YKEY, YFELD, YFORM are all available as parameters to the function
	; 			YFELDOLD - Record state before event (kept in same format as the other params)
	;
	; ByRefs:
	;
	; Returns: none
	;
	; History:
	; 17-Oct-2013	shobby	CORE-286: Return a flag if a hook is found	
	; 20-Sep-2011	GRF		SR17847: penumEvent not pintEvent; Check if null
	; 29-Aug-2011	shobby	SR17847: Pass YFIELDNAME as a parameter.
	; 26-Aug-2011	shobby	SR17847: Allow non $$ routines.
	; 26-Oct-2010	GRF		SR17541: Extended byRef YKEY to VARHook processing
	; 30-Sep-2010	shobby	SR17541: YKEY may be modified here (add &), need to
	;							change SCHLUESSEL if it is.
	; 29-Jan-2007	JW		SR15304: Added YFELDOLD
	; 14-Dec-2006	SteveS	SR15316: Created
	;-------------------------------------------------------------------------------
	new objHook,strStatus
 
	set strStatus = $$$OK
	if (pidClass'="") && ($get(penumEvent)'="") {                                         ; *** EXECUTE Kill 2a ***
		set objHook = $get(^WWW001Hook(0,pidClass,penumEvent,1))
		if (objHook'="") {
			if $extract($$$WWW001HookCode(objHook),1,2)="$$" {
				xecute "set strStatus="_$$$WWW001HookCode(objHook)
			} else {
				xecute "do "_$$$WWW001HookCode(objHook)  ;SR17847
			}
			set pblnCheck=$$$YES		;CORE-286
		}
	
		if $$$ISOK(strStatus) {                                     ; *** EXECUTE Kill 2b ***
			set strStatus = $$ExecuteVARHook^WWW001VARHooks(pidClass,penumEvent,.YKEY,.YFELD,YFORM,YFELDOLD,YFIELDNAME,.pblnCheck) ;SR17847 ;CORE-286
		}
	}
	
	if $$$ISOK(strStatus) {                                     ; *** EXECUTE ???? 2c, 2d ***
		set strStatus = $$ExecuteHook^WWW120Hook(pidClass,penumEvent,.YKEY,.YFELD,YFORM,YFELDOLD,YFIELDNAME,.pblnCheck) ;SR17847 ;CORE-286
	}
	quit strStatus
	
]]></Routine>
</Export>