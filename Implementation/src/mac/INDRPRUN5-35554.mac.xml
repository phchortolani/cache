<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPRUN5" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPRUN5
#include COMSYS
#include INConst
#include WWWConst
	/*------------------------------------------------------------------------------
	;		ANZEIGEN ÜBERSICHT ;Display OVERVIEW
	;
	; Inputs : 
	;
	;
	; ByRef :
	;	ID			idOrder-idOrdLine
	;	TYPE		
	;	SATZ(1)		
	;	SATZ(2)		
	;	ACCESS
	;	BESTAND		(* A1 *)
	;	CBESTAND	(* A2 *)
	;	ATPMENGE	(* A3 *)
	;	ATP
	;	BETRIEB
	;	LEADTIME
	;	DAT
	;	COLOR()
	;	LEISTUNG
	;	ART2
	;
	; Returns :
	;
	;
	; History :
	; 25-Mar-2009	FIS		SR16450  Show reference no. of demands in msg column
	; 06-Dec-2007	PPP		SR15598  Added the Requisition Number (INReq) in the Display
	; 28-Nov-2006	GRF		SR15234: doco; use strBGCOLOR & strStyle; quits; !=>||;
	; 						</FONT> closure; naked reference; braces; fix else logic
	; 						for "Status" columns.
	; 28-Jun-2006	PO		SR14669: Only display link to Transfer Order if line
	; 						includes demand
	; 16-Dec-2005	GRF		SR13984 : Doco; Boolean Macros
	; 29.04.2005	FIS		SR12200: Max no of decimal places
	; 26.Sep.2001	FIS		Created
	;-----------------------------------------------------------------------------*/
	NEW ACC,AUF1,idTFR,NK,strBGCOLOR,strStyle,YII,idREQ
	
	/*     A#                     S#                                     D#
	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	;            |              Supply               |                 Demand                ; INDRPRUN1
	;----------------------------------------------------------------------------------------;
	;PAB|CPAB|ATP|Qty|DueDt|Type|IDNo|EntryDt|ELP|Sta|Qty|DueDt|Type|IDNo|EntryDt|ELP|Sta|Msg; INDRPRUN1
	;----------------------------------------------------------------------------------------;
	;   |    |   |   |     |    |    |       |   |   |   |     |    |    |       |   |   |   ; INDRPRUN4    Opening Balances
	;---+----+---|---+-----+----+----+-------+---+---|---+-----+----+----+-------+---+---+---;        (Blank INDRPRUN2)
	; 1 |  2 | 3 | 1 |  2  |  3 |  4 |   5   | 6 | 7 | 1 |  2  |  3 |  4 |   5   | 6 | 7 | 8 ; INDRPRUN5    (with A/S/D #)
	;---+----+---|---+-----+----+----+-------+---+---|---+-----+----+----+-------+---+---+---;        (Blank INDRPRUN2)
	;   |    |   |   |     |    |    |       |   |   |   |     |    |    |       |   |   |   ;  ...
	;---+----+---|---+-----+----+----+-------+---+---|---+-----+----+----+-------+---+---+---;
	;   |    |   |   |     |    |    |       |   |   |   |     |    |    |       |   |   |   ;  ...
	;========================================================================================;
	;   |    |   |xxx|xxxxx|    |    |       |   |   |   |     |    |    |       |   |   |   ; INDRPRUN9    Totals
	;========================================================================================;
	;   |    |   |   |xxxxx|    |    |       |   |   |   |     |    |    |       |   |   |   ; INDRPRUN9    Totals
	\+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
	
	;---------------------------------------
	;	AUF1		objOrder
	;	ACC			blnAccessAuthorised
	;---------------------------------------
	SET AUF1=""  ;DATENSATZ WENN AUFTRAG ;data record when order 
	IF $FIND(ID,"-") IF $PIECE(ID,"-",1)'="" SET AUF1=$GET(^INAUF(YM,$PIECE(ID,"-",1),1))  ;AUFTRAG   ;order 
	FOR YII=1,2 IF $FIND(TYPE,YII) IF $FIND($PIECE(SATZ(YII),Y,2),"-") DO                  ;ELP
	. IF $PIECE($PIECE(SATZ(YII),Y,2),"-",1)'="" SET AUF1=$GET(^INAUF(YM,$PIECE($PIECE(SATZ(YII),Y,2),"-",1),1))  ;AUFTRAG   ;order 
	
	;--------------------------------------------------------------------------
	;BERECHTIGUNGEN FÜR LINK FESTLEGEN
	if AUF1="" {
		set ACC = $piece(ACCESS,Y,7)             ;BERECHTIGUNG DEMANDBEARBEITUNG
	} else {
		set ACC = $$^INAUFBERECHT(AUF1)          ;AUFTRAGSBERECHTIGUNG;FAN;03.02.04
	;	D2  $$$INAUFOrderType(AUF1)
		if +$piece(AUF1,Y,2)=0 {
			if $piece(ACCESS,Y,12)'=1 set ACC=$$$NO  ;KEINE AUFTRAGSBEARBEITUNG KUNDEN, NUR TOOLTIP
		} else {
			if $piece(ACCESS,Y,11)'=1 set ACC=$$$NO  ;KEINE AUFTRAGSBEARBEITUNG LIEFERANT /EIGENAUFTRAG, NUR TOOLTIP
		}
	}
	;MAX. NUMBER OF DECIMAL PLACES TO SHOW            ;FIS;29.04.05;SR12200
	SET NK    = $LENGTH($PIECE(BESTAND,".",2))
	SET NK(1) = $PIECE($GET(^INDRPSYS(YM,BETRIEB,1)),Y,47)
	IF NK(1)'="" IF NK>NK(1) SET NK=NK(1)
	
	set strBGCOLOR = " BGCOLOR="_$select(YDDSATZ#2=1:YWHITE,1:YGRAY)           ; SR15234
	if (+LEADTIME'=0) && (DAT>LEADTIME) && (COLOR(5)'="") {
		set strStyle = " style="_$$$DBLQUOTE_"border-top:thin solid "_COLOR(5)_";"_$$$DBLQUOTE
	} else {
		set strStyle = ""
	}
 
	WRITE YCR,"<TR ID=""DRPTR"_YDDSATZ_""">"
	
	;================================  ON HAND =================================
	
	; (* A1 *)  PAB / VORAUSSICHTLICHER LAGERBESTAND INKL. UNBESTÄTIGTER VORGÄNGE
	;-------------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD1"" NOWRAP VALIGN=TOP ALIGN=RIGHT",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	;LAGERBESTAND
	IF LEISTUNG'=1 {  ;LAGERBESTAND ANZEIGEN (AUSSER BEI LEISTUNG) ;display next to 
		SET YA(2)=0
		IF BESTAND<0 {
			SET YA(2)=1   ;MINUSBESTAND
		} else {
		;	IF $PIECE(ART1,Y,134)'=0 IF BESTAND<$PIECE(ART1,Y,134) IF $PIECE(SATZ($EXTRACT(TYPE)),Y,98)'=1 SET YA(2)=1 QUIT  ;MINDESTBESTAND UNTERSCHRITTEN
			IF ($PIECE(ART2,Y,23)'=0) && (BESTAND<$PIECE(ART2,Y,23)) {
				IF $PIECE(SATZ($EXTRACT(TYPE)),Y,98)'=$$$YES SET YA(2)=1  ;MINDESTBESTAND UNTERSCHRITTEN ;safety level 
			}
		}
		IF YA(2)=1 WRITE "<FONT COLOR="_YRED_">"
		IF NK'=""  WRITE $$^WWWZAHL(BESTAND,0,NK)  ;FIS;29.04.05;SR12200
		IF NK=""   WRITE $$^WWWZAHL(BESTAND,0,$LENGTH($PIECE(BESTAND,".",2)))  ;FIS;29.04.05;SR12200
		;WRITE $$^WWWZAHL(BESTAND,0,$LENGTH($PIECE(BESTAND,".",2)))
		IF YA(2)=1 WRITE "</FONT>"
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* A2 *)  CPAB / LAGERBESTAND  NUR BESTÄTIGTE VORGÄNGE ! ;only 
	;-------------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD2"" NOWRAP VALIGN=TOP ALIGN=RIGHT",strBGCOLOR
	IF ATP'=$$$YES WRITE " style=""border-right:thin solid black;"""
	write strStyle,">"
	WRITE "<FONT SIZE=2>"
	;VERFÜGBARER LAGERBESTAND
	IF LEISTUNG'=1 {  ;LAGERBESTAND ANZEIGEN (AUSSER BEI LEISTUNG) ;display next to 
		SET YA(2)=0
		IF CBESTAND<0 {
			SET YA(2)=1  ;MINUSBESTAND
		} else {
		;	IF $PIECE(ART1,Y,134)'=0 IF CBESTAND<$PIECE(ART1,Y,134) IF $PIECE(SATZ($EXTRACT(TYPE)),Y,98)'=1 SET YA(2)=1 QUIT  ;MINDESTBESTAND UNTERSCHRITTEN
			IF ($PIECE(ART2,Y,23)'=0) && (CBESTAND<$PIECE(ART2,Y,23)) {
				IF $PIECE(SATZ($EXTRACT(TYPE)),Y,98)'=$$$YES SET YA(2)=1  ;MINDESTBESTAND UNTERSCHRITTEN ;safety level 
			}
		}
		
		IF YA(2)=1 WRITE "<FONT COLOR="_YRED_">"
		WRITE $$^WWWZAHL(CBESTAND,0,$LENGTH($PIECE(CBESTAND,".",2)))
		IF YA(2)=1 WRITE "</FONT>"
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* A3 *)  ATP / AVAILABLE TO PROMISE
	;--------------------------------------------------------------------------
	IF ATP=$$$YES {
		WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD3"" NOWRAP VALIGN=TOP ALIGN=RIGHT",strBGCOLOR
		WRITE " style=""border-right:thin solid black;"""
		WRITE strStyle,">"
		WRITE "<FONT SIZE=2>"
		
		;VERFÜGBARER LAGERBESTAND
		IF LEISTUNG'=1 {  ;LAGERBESTAND ANZEIGEN (AUSSER BEI LEISTUNG) ;display next to 
			SET YA(2)=0
			IF ATPMENGE<0 SET YA(2)=1  ;MINUSBESTAND
			
			IF YA(2)=1 WRITE "<FONT COLOR="_YRED_">"
			WRITE $$^WWWZAHL(ATPMENGE,0,$LENGTH($PIECE(ATPMENGE,".",2)))
			IF YA(2)=1 WRITE "</FONT>"
		}
		
		WRITE "&nbsp;</FONT></TD>",YCR
	}
	;================================  SUPPLY =================================
	
	; (* S1 *)  QTY
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD4"" NOWRAP VALIGN=TOP ALIGN=RIGHT",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	WRITE "&nbsp;"
	IF $FIND(TYPE,2) {
		DO LINK(2)
		IF LEISTUNG=1 WRITE $$^WWWTIME($PIECE(SATZ(2),Y,5))
		IF LEISTUNG=0 {
				WRITE $$^WWWZAHL($PIECE(SATZ(2),Y,5),0,$LENGTH($PIECE($PIECE(SATZ(2),Y,5),".",2)))
			//}
		}
		DO LINKEND(2)
	}
	
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* S2 *)  DUE DATE
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD5"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,2) DO
	. NEW DAT1,AUFX,POSX,LIEF,AB
	. SET DAT1=DAT
	. IF $PIECE(SATZ(2),Y,9)="05" DO  ;ABWEICHENDES DATUM BEI LAGERBESTELLUNG ;Date next to 
	. . SET AUFX=$PIECE($PIECE(SATZ(2),Y,2),"-",1)
	. . SET POSX=$PIECE($PIECE(SATZ(2),Y,2),"-",2)
	. . SET DAT1=$PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,19)
	. . IF DAT1="" IF AUFX'="" IF POSX'="" IF $DATA(^INAUFPK(YM,AUFX,POSX)) DO  ;LIEFERTERMIN LT. LIEFERANTENKONDITION ;time of delivery 
	. . . IF $PIECE($GET(^INAUFPK(YM,AUFX,POSX,1)),Y,23)'="" IF $PIECE(SATZ(2),Y,3)'="" DO
	. . . . SET DAT1=$PIECE(SATZ(2),Y,3)+$PIECE($GET(^INAUFPK(YM,AUFX,POSX,1)),Y,23)  ;AUFTRAGSDATUM + LIEFERZEIT IN TAGEN ;purchasing lead time within 
	. . . ;
	. . . IF DAT1'="" SET DAT1=DAT1+$$^INTAGEPLUS(+$HOROLOG,DAT1,BETRIEB,1,$PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,12))  ;PLUS FEIERTAGE UND WOCHENENDE;FIS;24406;04.11.03
	. . ;
	. . IF AUFX'="" IF POSX'="" IF $DATA(^INAUFPA(YM,AUFX,POSX)) DO  ;LIEFERTERMIN LT. AUFTRAGSBESTÄTIGUNG ;time of delivery order confirmation 
	. . . SET LIEF=$PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,12)  ;LIEFERANT   ;supplier 
	. . . IF LIEF="" SET LIEF=$ORDER(^INAUFPA(YM,AUFX,POSX,""))
	. . . SET AB=$ORDER(^INAUFPA(YM,AUFX,POSX,LIEF,""),-1)
	. . . IF AB'="" IF $PIECE($GET(^INAUFPA(YM,AUFX,POSX,LIEF,AB,1)),Y,23)'="" DO
	. . . . SET DAT1=$PIECE(^INAUFPA(YM,AUFX,POSX,LIEF,AB,1),Y,23)  ;VORAUSSICHTL. EINGANGSDATUM     ; 28-Nov-2006 Naked Reference
	. . ;
	. . IF DAT1="" SET DAT1=DAT
	. ;
	. DO LINK(2)
	. IF DAT1<$HOROLOG WRITE "<FONT COLOR="_YRED_">"
	. WRITE $$^WWWDATE(DAT1)
	. IF DAT1<$HOROLOG WRITE "</FONT>"
	. DO LINKEND(2)
	. ;DO MELDUNG1^INDRPINFO(2,YIZ)
	
	IF '$FIND(TYPE,2) && (+LEADTIME'=0) && (DAT>LEADTIME) && (COLOR(5)'="") {       ;MIT DATUM ANZEIGEN;FIS;07.02.05;26432
		WRITE "<FONT COLOR="_COLOR(5)_"><sup>"
		WRITE $$^WWWDATE(LEADTIME)
		WRITE "</sup></FONT>"
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* S3 *)  TYPE ;letter 
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD6"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,2) {
		DO LINK(2)
		WRITE $PIECE($GET(^INDRPPARA(YM,"SUPPLYTYPE",SPRACHE,+$PIECE(SATZ(2),Y,70),1)),Y,1)
		DO LINKEND(2)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* S4 *)  ID-No.
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD7"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
		
	IF $FIND(TYPE,2) {
		if $$$INDRPSUPPLYSupplyType(SATZ(2)) = 7 {
			DO LINK(2,2,BETRIEB_","_ID)   //SR15598
		} 	else {
			DO LINK(2)
		}
		WRITE ID
		DO LINKEND(2)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* S5 *)  Entry Date
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD8"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,2) {
		DO LINK(2)
		WRITE $$^WWWDATE($PIECE(SATZ(2),Y,3))
		DO LINKEND(2)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* S6 *)  # P/O NO. / AUFTRAGSNR. ELP
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD9"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	if TYPE [ 2 {
		set idTFR = $$$INDRPSUPPLYTransfer(SATZ(2)) // SR14669
		set idREQ = $$$INDRPSUPPLYRequisitionNumber(SATZ(2))	;SR15598
	}
	
	if ($get(idTFR) = "") && ($get(idREQ) = "") IF $FIND(TYPE,2) DO // SR14669
	. IF $PIECE(SATZ(2),Y,2)'="" DO
	. . IF $FIND($PIECE(SATZ(2),Y,2),"-") IF $PIECE(SATZ(2),Y,98)'=$$$YES IF $PIECE(ACCESS,Y,12)=1 DO  QUIT   ;GELÖSCHT/BERECHTIGUNG ?
	. . .;DO LINK(2,3,$PIECE($PIECE(SATZ(2),Y,2),"-",1)_","_$PIECE($PIECE(SATZ(2),Y,2),"-",2))  ;LINK NACH AUFTRAGSPOSITION ;within 
	. . . DO LINK(2,5,$PIECE($PIECE(SATZ(2),Y,2),"-",1))                ;LINK NACH AUFTRAG 
	. . ;
	. . DO LINK(2)
	. ;
	. DO  ;FIS;06.05.05;SHOW IMPORT NO. INSTEAD OF ORDER NUMBER (IF SO, TOOLTIP SHOULD SHOW ORDER NO., NOT MADE YET);SR12333
	. . NEW YQ,SUP,CON
	. . SET YQ=0
	. . IF $FIND(ID,"-") IF $PIECE(ID,"-",1)'="" IF $PIECE(ID,"-",2)'="" DO
	. . . SET SUP=$PIECE($GET(^INAUFP(YM,$PIECE(ID,"-",1),$PIECE(ID,"-",2),1)),Y,12)
	. . . IF SUP'="" IF $DATA(^INAUFPA(YM,$PIECE(ID,"-",1),$PIECE(ID,"-",2),SUP)) DO
	. . . . SET CON=""
	. . . . FOR  SET CON=$ORDER(^INAUFPA(YM,$PIECE(ID,"-",1),$PIECE(ID,"-",2),SUP,CON)) QUIT:CON=""  DO
	. . . . . IF $PIECE($GET(^INAUFPA(YM,$PIECE(ID,"-",1),$PIECE(ID,"-",2),SUP,CON,1)),Y,61)'="" DO
	. . . . . . WRITE $PIECE($GET(^INAUFPA(YM,$PIECE(ID,"-",1),$PIECE(ID,"-",2),SUP,CON,1)),Y,61)
	. . . . . . SET YQ=1
	. . ;
	. . QUIT:YQ=1
	. . WRITE $PIECE(SATZ(2),Y,2)
	. ;
	. ;WRITE $PIECE(SATZ(2),Y,2)
	. DO LINKEND(2)
	
	if (TYPE [ 2) && (idTFR '= "") { // SR14669
		do LINK(2,6,$piece(idTFR,$$$COMMA,1))
		write idTFR
		do LINKEND(2)
	}
 
	if (TYPE [ 2) && (idREQ '= "") { // SR15598
		do LINK(2)
		;do LINK(2,7,idREQ)
		write idREQ
		do LINKEND(2)
	}
	
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* S7 *)  STATUS
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD10"" NOWRAP VALIGN=TOP",strBGCOLOR
	WRITE " style=""border-right:thin solid black;"""
	WRITE strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,2) {
		DO LINK(2)
		IF STATUSTEXT(2)'="" {  ;STATUSANZEIGE
			IF $PIECE(STATUS(2),"#",2)'=1 IF $PIECE(STATUS(2),"#",1)'>90 WRITE "<FONT COLOR="_YRED_">"
			IF $FIND(STATUSTEXT(2),";") {
				WRITE $PIECE(STATUSTEXT(2),";",1)   ;STATUSKÜRZEL
			} else {
				WRITE STATUSTEXT(2)                 ;STATUSTEXT           ; SR15234
			}
			IF $PIECE(STATUS(2),"#",2)'=1 IF $PIECE(STATUS(2),"#",1)'>90 WRITE "</FONT>"
		}
		DO LINKEND(2)
	}
	
	WRITE "&nbsp;</FONT></TD>",YCR
	
	;==============================  DEMANDS ==================================
	
	; (* D1 *)  QTY / MENGE 
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD11"" NOWRAP VALIGN=TOP ALIGN=RIGHT",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	WRITE "&nbsp;"
	IF $FIND(TYPE,1) {
		DO LINK(1)
		IF LEISTUNG=1 WRITE $$^WWWTIME($PIECE(SATZ(1),Y,5))
		IF LEISTUNG=0 WRITE $$^WWWZAHL($PIECE(SATZ(1),Y,5),0,$LENGTH($PIECE($PIECE(SATZ(1),Y,5),".",2)))
		DO LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D2 *)  DATE / DATUM
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD12"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,1) {
		DO LINK(1)
		IF DAT<$HOROLOG WRITE "<FONT COLOR="_YRED_">"
		WRITE $$^WWWDATE(DAT)
		IF DAT<$HOROLOG WRITE "</FONT>"
		do LINKEND(1)
		;DO MELDUNG1^INDRPINFO(1,YIZ)
	}
	
	IF $FIND(TYPE,2) && (+LEADTIME'=0) && (DAT>LEADTIME) && (COLOR(5)'="") {     ;SHOW LEADTIME;FIS;05.05.05
		WRITE "<FONT COLOR="_COLOR(5)_"><sup>"
		WRITE $$^WWWDATE(LEADTIME)
		WRITE "</sup></FONT>"
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D3 *)  TYPE ;letter 
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD13"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,1) {
		DO LINK(1)
		;WRITE +$PIECE(SATZ(1),Y,70)
		WRITE $PIECE($GET(^INDRPPARA(YM,"DEMANDTYPE",SPRACHE,+$PIECE(SATZ(1),Y,70),1)),Y,1)
		DO LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D4 *)  ID-No.
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD14"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,1) {
		if $$$INDRPDEMANDDemandType(SATZ(1)) = 7 {
			DO LINK(1,1,BETRIEB_","_ID)   //SR15598
		} else {
			DO LINK(1)		
		}
		WRITE ID
		DO LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D5 *)  Entry Date
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD15"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,1) {
		DO LINK(1)
		WRITE $$^WWWDATE($PIECE(SATZ(1),Y,3))
		DO LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D6 *)  P/O NO. / AUFTRAGSNR. ELP
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD16"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	
	if $get(SATZ(1))'="" {
		set idTFR = $$$INDRPDEMANDTransfer(SATZ(1)) // SR14427 (Phase 2)
		set idREQ = $$$INDRPDEMANDRequisitionNumber(SATZ(1))	;SR15598
	}
	
	if ($get(idTFR) = "") & ($get(idREQ) = "") do
	. IF $FIND(TYPE,1) DO
	. . IF $PIECE(SATZ(1),Y,2)'="" DO
	. . . IF $FIND($PIECE(SATZ(1),Y,2),"-") IF $PIECE(SATZ(1),Y,98)'=$$$YES IF $PIECE(ACCESS,Y,12)=1 DO  QUIT  ;GELÖSCHT/BERECHTIGUNG ?
	. . . .;DO LINK(1,3,$PIECE($PIECE(SATZ(1),Y,2),"-",1)_","_$PIECE($PIECE(SATZ(1),Y,2),"-",2))  ;LINK NACH AUFTRAGSPOSITION ;within 
	. . . . DO LINK(1,5,$PIECE($PIECE(SATZ(1),Y,2),"-",1))            ;LINK NACH AUFTRAG
	. . . ;
	. . . DO LINK(1)
	. . ;
	. . WRITE $PIECE(SATZ(1),Y,2)
	. . DO LINKEND(1)
 
	if (TYPE [ 1) && ($get(idTFR) '= "") { // SR14427 (Phase 2), SR14669
		do LINK(1,6,$piece(idTFR,$$$COMMA,1))
		write idTFR
		do LINKEND(1)
	}
	if $FIND(TYPE,1)&&($get(idREQ) '= "") { // SR15598
		do LINK(1)	;,7,idREQ)
		write idREQ
		do LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D7 *)  STATUS
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD17"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	
	IF $FIND(TYPE,1) {
		DO LINK(1)
		IF STATUSTEXT(1)'="" {  ;STATUSANZEIGE
			IF $PIECE(STATUS(1),"#",2)'=1 IF $PIECE(STATUS(1),"#",1)'>90 WRITE "<FONT COLOR="_YRED_">"
			IF $FIND(STATUSTEXT(1),";") {
				WRITE $PIECE(STATUSTEXT(1),";",1)   ;STATUSKÜRZEL
			} else {
				WRITE STATUSTEXT(1)                 ;STATUSTEXT           ; SR15234
			}
			IF $PIECE(STATUS(1),"#",2)'=1 IF $PIECE(STATUS(1),"#",1)'>90 WRITE "</FONT>"
		}
		IF $PIECE(SATZ(1),Y,39)=1 WRITE "*"  ;FIS;29.04.05;SHOW IF READY TO DELIVER;SR12200
		DO LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	; (* D8 *)  MSG  ;MESSAGE
	;--------------------------------------------------------------------------
	WRITE "<TD ID=""DRPTR"_YDDSATZ_"TD18"" NOWRAP VALIGN=TOP",strBGCOLOR,strStyle,">"
	WRITE "<FONT SIZE=2>"
	IF $FIND(TYPE,1) {
		DO LINK(1)
		if '$find(ID,"-") {
			write $$$INDRPDEMANDReference(SATZ(1))  //show reference numbers of demands //SR16450
		}
		else {
			WRITE $$^WWWDATE($PIECE(SATZ(1),Y,45))  //service time of labor items
		}
		DO LINKEND(1)
	}
	WRITE "&nbsp;</FONT></TD>",YCR
	
	;--------------------------------------------------------------------------
	WRITE "</TR>"
	QUIT
	
	
LINK(YT,YL,YK) ;
	;-------------------------------------------------------------------------------
	; ANFANG LINK ;outset 
	;
	; Params:
	;	YT		TYPE  1, 2
	;	YL		LINK NACH:	1 = DEMAND			2 = SUPPLY
	;						3 = Order Line		4 = Bill of Materials (FERTIGUNGSTEIL)
	;						5 = Order Header	6 = transfer
	;						7 = Supply Record
	;						8 = Requisition
	;	YK		KEY (WENN "" DANN BETRIEB,ID)
	; 	
	; ByRef:
	; 	SATZ(YT)
	; 	ID
	; 	ACC		blnAccessAuthorised
	; 
	; Returns: nothing
	;
	; History:
	; 04-May-2011	PPP		SR17165: Changed Global ^INRequistion to "Supplies"
	; 29-Jun-2009	PPP		SR16663: Add the Calculated Relation Display Fields
	; 06-Dec-2007	PPP		SR15598: Added INReq in addition to INRequisition (Supplies)
	; 12-Nov-2007	HQN		SR15598: Refer to INReq rather than INRequisition
	; 09-Jan-2007	JW		SR15347: Added requisitions - rewrote
	;-------------------------------------------------------------------------------
	new YKEY,YFORM,YBACK,TITLE,strStruct,strState
	new strReqStatus
	
	set ACC = $get(ACC)
	if $get(YT)="" set YT = $extract(TYPE)
	
	if ($get(YL)="") || ($get(YK)="") {
		set strStruct = $$$INDRPDEMANDItemStructure(SATZ(YT))
		if $piece(strStruct,".",3,99)'="" && '$$$INDRPDEMANDDeleteOrder(SATZ(YT)) {  ;WENN FERTIGUNGSTEIL ;when , ;GELÖSCHT
			set YL = 4
			set YK = $piece(strStruct,".",1)_","_$piece(strStruct,".",2)_","_$piece(strStruct,".",3,99)  ;FERTIGUNGSTEIL
			
		} elseif $find(ID,"-") {
			set YL = 3
			set YK = $piece(ID,"-",1)_","_$piece(ID,"-",2)  ;AUFTRAGSPOSITION
			
		//} elseif $$$INDRPDEMANDDemandType(SATZ(YT)) = 5 {		; Supplies	^INRequisition SR15347
		} elseif $$$INDRPDEMANDDemandType(SATZ(YT)) = 6 {		; Supplies	^INRequisition SR15347	SR17165 ("Planned Supply")
			set YL = 7
			set YK = YM_","_ID   ; SR15598
			set strState = $$$AppEnum("INREQSTATUS",$$$INRequisitionStatus($get(^INRequisition(YM,YM,ID,1))))
 
		} elseif $$$INDRPDEMANDDemandType(SATZ(YT)) = 7 {		; Requisition	^INReq	SR15598
			set YL = 8
			set YK = $$$INDRPDEMANDRequisitionNumber(SATZ(YT)) 	; link now goes to INReq with Requisition as the key
			
			if YK'="" {
				//SR16663
				set strReqStatus = +$$$INReqStatus($get(^INReq(YM,YK,1)))
				set strState = $$GetDescription^WWWStatus("INReq",strReqStatus,$g(SPRACHE))
			}
			
		} else {
			if $get(YL)="" set YL = $extract(TYPE)
			if $get(YK)="" set YK = BETRIEB_","_ID
		}
	}
	
	/*	SR15347 - rewritten above
	IF ($GET(YL)="") || ($GET(YK)="") DO
	. IF $GET(YL)=""   SET YL=$EXTRACT(TYPE)
	. IF $GET(YK)=""   SET YK=BETRIEB_","_ID  ;BETRIEB,ID
	. IF $FIND(ID,"-") SET YL=3 SET YK=$PIECE(ID,"-",1)_","_$PIECE(ID,"-",2)  ;AUFTRAGSPOSITION
	. IF $PIECE($PIECE(SATZ(YT),Y,72),".",3,99)'="" DO  ;WENN FERTIGUNGSTEIL ;when 
	. . QUIT:$PIECE(SATZ(YT),Y,98)=1  ;GELÖSCHT
	. . SET YL=4
	. . SET YK=$PIECE($PIECE(SATZ(YT),Y,72),".",1)_","_$PIECE($PIECE(SATZ(YT),Y,72),".",2)_","_$PIECE($PIECE(SATZ(YT),Y,72),".",3,99)  ;FERTIGUNGSTEIL
	*/
	
	SET LINK=0
	DO
	.;IF (YL=3) || (YL=4) || (YL=5) IF $PIECE(SATZ(YT),Y,98)=1 QUIT  ;GELÖSCHT ; SR15598
	. IF (YL=3) || (YL=4) || (YL=5) IF $$$INDRPDEMANDDeleteOrder(SATZ(YT))=$$$YES QUIT  ;GELÖSCHT
	. ;
	. WRITE "<A HREF="""
	. IF ACC=$$$YES DO
	. . WRITE YAKTION_"EP=WWWFORM"
	. . 
	. . IF YL=1                 WRITE "&amp;YFORM=INDRPDEMAND&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","
	. . IF YL=2                 WRITE "&amp;YFORM=INDRPSUPPLY&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","
	. . IF YL=3                 WRITE "&amp;YFORM=INAUFP&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","
	. . IF YL=4 IF LEISTUNG=1   WRITE "&amp;YFORM=INAUFPT&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","
	. . IF YL=4 IF LEISTUNG'=1  WRITE "&amp;YFORM=INAUFPT0&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","
	. . IF YL=5                 WRITE "&amp;YFORM=INAUF&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","
	. . if YL=6                 write "&amp;YFORM=INTFR&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","             //SR14427
	. . if YL=7                 write "&amp;YFORM=INRequisition&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","		//SR15347
	. . if YL=8                 write "&amp;YFORM=INReq&amp;YKEY="_YK_"&amp;YBACK="_RUECKSPRUNG_","		        //SR15598
	. . DO ^WWWCGI
	. ;
	. IF ACC'=$$$YES WRITE "#"
	. WRITE """"
	. ;
	. SET TITLE=""
	. DO
	. . IF YL=1 SET TITLE = $$^WWWTEXT(32612)  ;DEMAND
	. . IF YL=2 SET TITLE = $$^WWWTEXT(32614)  ;SUPPLY
	. . IF YL=3 SET TITLE = $$^WWWTEXT(32558)  ;Processes Purchase Order Line
	. . IF YL=4 SET TITLE = $$^WWWTEXT(32151)  ;Production Line Item
	. . IF YL=5 SET TITLE = $$^WWWTEXT(32062)  ;Edit Order
	. . if YL=6 set TITLE = $$^WWWFORMNAME("INTFR") 			// SR14427
	. . ;
	. . if YL=7 do  quit		//SR15347
	. . . set TITLE = $$^WWWTEXT(32614)_YCR_"("_strState_")"	;Supplies
	. . ;
	. . if YL=8 do  quit		//SR15598
	. . . set TITLE = $$^WWWTEXT(33500)_YCR_"("_strState_")"	;Requisition
	. . ;
	. . IF (STATUSTEXT(YT)'="") && (YL'=6) DO  ;STATUSANZEIGE
	. . . NEW TEXT
	. . . SET TEXT=STATUSTEXT(YT)
	. . . IF $FIND(TEXT,";") SET TEXT=$PIECE(TEXT,";",2)          ;STATUSNAME
	. . . IF $LENGTH(TEXT)<4 SET TEXT=$$^WWWTEXT(32781)_" "_TEXT  ;STATUS
	. . . IF TEXT'="" SET TITLE=TITLE_YCR_"("_TEXT_")" // SR14669
	. . ;
	. . IF YT'=1 IF $PIECE(SATZ(YT),Y,13)="" IF $PIECE(SATZ(YT),Y,9)'="05" IF $PIECE(STATUS(YT),"#",1)<30 SET TITLE=TITLE_YCR_$$^WWWTEXT(32636)  ;not confirmed
	. . IF YT'=1 IF $PIECE(SATZ(YT),Y,9)="05"                         SET TITLE=TITLE_YCR_$$^WWWTEXT(32047)_" "_ID_" "_$$^WWWTEXT(33646)  ;AUFTRAG X ZUGEORDNETE LAGERBESTELLUNG ;order X 
	. . IF $PIECE(AUF1,Y,8)'=""                                       SET TITLE=TITLE_YCR_$$^WWWTEXT(32114)_": "_$EXTRACT($PIECE(AUF1,Y,8),0,100)       ;BEC;24.11.03;24679;PROJEKT HINZUGEFÜGT
	. . IF $PIECE(AUF1,Y,8)'="" IF $EXTRACT($PIECE(AUF1,Y,8),101)'="" SET TITLE=TITLE_".."                        ;BEC;24.11.03;24679;PROJEKT HINZUGEFÜGT
	. . IF YT=1 IF $PIECE(SATZ(YT),Y,120)'=""                         SET TITLE=TITLE_YCR_$$^WWWFELDNAME("INDRPDEMAND","D",120)_": "_$$^WWWDATE($PIECE(SATZ(YT),Y,120))_" "_$$^WWWTIME($PIECE(SATZ(YT),Y,120))
	. . IF YT=2 IF $PIECE(SATZ(YT),Y,120)'=""                         SET TITLE=TITLE_YCR_$$^WWWFELDNAME("INDRPSUPPLY","D",120)_": "_$$^WWWDATE($PIECE(SATZ(YT),Y,120))_" "_$$^WWWTIME($PIECE(SATZ(YT),Y,120))
	. . IF $FIND(ID,"-") IF $PIECE(ID,"-",1)'="" IF $PIECE($GET(^INAUF(YM,$PIECE(ID,"-",1),1)),Y,6)'=BETRIEB SET TITLE=TITLE_YCR_$$^WWWFELDNAME("INAUF","D",6)_": "_$PIECE($GET(^INAUF(YM,$PIECE(ID,"-",1),1)),Y,6)
	. . IF $FIND(ID,"-")                                              SET TITLE=TITLE_YCR_$$^WWWTEXT(32047)_": "_ID  ;FIS;SHOW ORDER IN LINK;SR12333;24.05.05
	. ;
	. IF TITLE'="" WRITE " TITLE="""_TITLE_""""  ;TOOLTIP
	. WRITE ">"
	. SET LINK=1
	
	IF $PIECE(STATUS(YT),"#",1)=90             WRITE "<STRIKE>"  ;GESPERRT ;DISABLED 
	IF $FIND(ID,"-") IF $PIECE(ID,"-",1)'="" IF $PIECE($GET(^INAUF(YM,$PIECE(ID,"-",1),1)),Y,6)'=BETRIEB WRITE "<I>"  ;OTHER LOCATION;04.05.05;FIS;SR12200
	IF $PIECE(SATZ(YT),Y,98)=$$$YES IF COLOR(2)'="" WRITE "<FONT COLOR="_COLOR(2)_">" QUIT  ;VORGANG GELÖSCHT
	IF $PIECE(SATZ(YT),Y,70)=9      IF COLOR(4)'="" WRITE "<FONT COLOR="_COLOR(4)_">" QUIT  ;PLANUNGSAUFTRÄGE
	IF $PIECE(SATZ(YT),Y,71)'="" IF $PIECE($GET(^WWW101(0,"PRIORITAET",SPRACHE,$PIECE(SATZ(YT),Y,71),1)),Y,2)'="" WRITE "<FONT COLOR="_$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,$PIECE($GET(^WWW101(0,"PRIORITAET",SPRACHE,$PIECE(SATZ(YT),Y,71),1)),Y,2),1)),Y,1)_">" QUIT  ;PRIORITY COLOR ;FIS;25.04.05;SR12200
	QUIT:YT=1
	
	IF $PIECE(SATZ(YT),Y,13)="" IF $PIECE(SATZ(YT),Y,9)'="05" IF STATUS(YT)<30 IF '$FIND(";"_$TRANSLATE($PIECE($GET(^INDRPSYS(YM,YLOCATION,1)),Y,43),",",";")_";",";3;") IF COLOR(3)'="" WRITE "<FONT COLOR="_COLOR(3)_">" QUIT  ;SUPPLY NOCH NICHT BESTÄTIGT ;yet Not 
	IF $PIECE(SATZ(YT),Y,9)="05" IF COLOR(6)'="" WRITE "<FONT COLOR="_COLOR(6)_">"  ;LAGERBESTELLUNG
	QUIT
	
	
LINKEND(YT) ;ENDE LINK ;termination 
	DO
	. IF $PIECE(STATUS(YT),"#",1)=90             WRITE "</STRIKE>"  ;GESPERRT ;DISABLED 
	. IF $FIND(ID,"-") IF $PIECE(ID,"-",1)'="" IF $PIECE($GET(^INAUF(YM,$PIECE(ID,"-",1),1)),Y,6)'=BETRIEB WRITE "</I>"  ;OTHER LOCATION;04.05.05;FIS;SR12200
	. IF $PIECE(SATZ(YT),Y,98)=$$$YES IF COLOR(2)'="" WRITE "</FONT>" QUIT
	. IF $PIECE(SATZ(YT),Y,70)=9      IF COLOR(4)'="" WRITE "</FONT>" QUIT  ;PLANUNGSAUFTRÄGE
	. IF $PIECE(SATZ(YT),Y,71)'="" IF $PIECE($GET(^WWW101(0,"PRIORITAET",SPRACHE,$PIECE(SATZ(YT),Y,71),1)),Y,2)'="" WRITE "</FONT>" QUIT  ;PRIORITY COLOR ;FIS;25.04.05;SR12200
	. QUIT:YT=1
	. IF $PIECE(SATZ(YT),Y,13)="" IF $PIECE(SATZ(YT),Y,9)'="05" IF $PIECE(STATUS(YT),"#",1)<30 IF '$FIND(";"_$TRANSLATE($PIECE($GET(^INDRPSYS(YM,YLOCATION,1)),Y,43),",",";")_";",";3;") IF COLOR(3)'="" WRITE "</FONT>" QUIT
	. IF $PIECE(SATZ(YT),Y,9)="05" IF COLOR(6)'="" WRITE "</FONT>"  ;LAGERBESTELLUNG
	
	IF LINK=1 WRITE "</A>"
	WRITE YCR
	SET LINK=0
	QUIT
	
]]></Routine>
</Export>