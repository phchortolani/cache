<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESTreatmentHC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESTreatmentHC
	#include COMConst
	#include COMGridEdit31
	#include MEDConst
	#include INConst
	#include VARConst
	#include VARSESFarmaciaAltoCusto
 	quit
 
OnBeforeEditAccess()
	quit $$IsAcessoPermitido^VARSESFarmaciaAltoCustoUtil() 
 
OnAfterKeyFields()
	new objPaciente, objProfSaude
 
	if ('$$$NoKey(YKEY)) {
		if ($$$VARSESTreatmentHCCodigoPaciente(YFELD) '= ""){
			set objPaciente = $get(^MEDPatient(0,
				$$$VARSESTreatmentHCCodigoPaciente(YFELD), 1))
			set GblPacienteCNS = $$$MEDPatientExternalCode(objPaciente)
			set GblPacienteNome = $$$MEDPatientName(objPaciente)
			if ($length($$$MEDPatientDOB(objPaciente)) > 0) {
				set GblPacienteDataNascimento =
					$zdate($$$MEDPatientDOB(objPaciente), 4)
			}
			else {
				set GblPacienteDataNascimento = ""
			}
		} else {
			set objPaciente = ""
			set GblPacienteCNS = ""
			set GblPacienteNome = ""
				set GblPacienteDataNascimento =	""
			
		}
 
		
	}
	else {
		set GblPacienteCNS = ""
		set GblPacienteNome = ""
		set GblPacienteDataNascimento = ""
		set GblProfissionalConselho = ""
		set GblProfissionalNumero = ""
		set GblProfissionalNome = ""
	}
	quit
 
OnAfterDataFields()
	if ($$$NoKey(YKEY)) quit
 	if ($get(YSEITE) = 2) {
		do LoadGridHistAlt(YKEY)
	} else {
		do LoadGrid(YKEY)	
	}
	
	quit
 
Install()
	set YFORM = "VARSESTreatmentHCLine"
	set $$$EventExists("LineAdded") = 1
 
	set YFORM = "VARSESTreatmentHCLine1"
	set $$$EventExists("LineAdded") = 1
	quit
 
LoadGrid(pidTHC)
 	new YFORM, YKEY, YAUSWAHL, YFELD
 
	set YFORM = "VARSESTreatmentHCLine"
	set YFELD = ""
	set YKEY = $get(pidTHC)
 
	//do Install()
 
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = 1
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 290
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)	   = $$$OK
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "VARSESTreatmentHC"
	set $$$COMGridEditParameterExpandLines(YAUSWAHL)   = 1
	set $$$COMGridEditParameterLinks(YAUSWAHL)         = "VARSESTreatmentHCLine1"
	$$$GRIDStart(YFORM,YKEY)
	quit
 
 // Chamado quando pelo menos uma dispensação foi processada
UpdateStatus(pidTreatment)
	new objTreatment, strStatus, bolTerminado, idLine, idLine1, objLine1
 
	if ($length(pidTreatment) = 0) quit
 
	set objTreatment = $get(^VARSESTreatmentHC(YM, pidTreatment, 1))
	if (($$$VARSESTreatmentHCStatus(objTreatment) '= $$$TreatmentStatusPronto) &&
		($$$VARSESTreatmentHCStatus(objTreatment) '= $$$TreatmentStatusEmAndamento) &&
		 ($$$VARSESTreatmentHCStatus(objTreatment) '= $$$TreatmentStatusTerminado)) {
		quit $$$OK
	}
 
	if ($$$VARSESTreatmentHCStatus(objTreatment) = $$$TreatmentStatusPronto) {
		set $$$VARSESTreatmentHCStatus(objTreatment) = $$$TreatmentStatusEmAndamento
	}
 
	if (($$$VARSESTreatmentHCStatus(objTreatment) = $$$TreatmentStatusEmAndamento)||
		(($$$VARSESTreatmentHCStatus(objTreatment) = $$$TreatmentStatusTerminado))) {
		set strStatus = $$UpdateStatus^VARSESTreatmentHCLine1(pidTreatment)
		if ('$$$ISOK(strStatus)) quit strStatus
 
		set bolTerminado = $$$YES
		set idLine = $order(^VARSESTreatmentHCLine(YM, pidTreatment, ""))
		while ($length(idLine) > 0) {
			set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
				idLine, ""))
 
			while ($length(idLine1) > 0) {
				set objLine1 = $get(^VARSESTreatmentHCLine1(YM, pidTreatment,
					idLine, idLine1, 1))
 
				if ('+$$$VARSESTreatmentHCLine1Atendido(objLine1)) {
					set bolTerminado = $$$NO
					quit
				}
 
				set idLine1 = $order(^VARSESTreatmentHCLine1(YM, pidTreatment,
					idLine, idLine1))
			}
			if ('bolTerminado) quit
 
			set idLine = $order(^VARSESTreatmentHCLine(YM, pidTreatment, idLine))
		}
 
		if (bolTerminado) {
			set $$$VARSESTreatmentHCStatus(objTreatment) = $$$TreatmentStatusTerminado
		}else{
			set $$$VARSESTreatmentHCStatus(objTreatment) = $$$TreatmentStatusEmAndamento
		}
	}
 
	quit $$$Save("VARSESTreatmentHC",pidTreatment,objTreatment,$$$YES)
 
BuscaTipoDocumentoPorString(pstrParametro)
	set intSaida = 0
	if (pstrParametro = "CRM") set intSaida = 1
	if (pstrParametro = "CRO") set intSaida = 2
	if (pstrParametro = "CRF") set intSaida = 3
	if (pstrParametro = "COREN") set intSaida = 4
	if (pstrParametro = "CRN") set intSaida = 5
	if (pstrParametro = "CPF") set intSaida = 6
	quit intSaida
	
LoadGridHistAlt(pid="")
	new YFORM,YKEY,YAUSWAHL,YFELD
	set YFORM="VARSESTreatmentHCHist"
	set YFELD=""
	set YKEY=$get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)=1
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)=290
	set $$$COMGridEditParameterGridName(YAUSWAHL)=YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)=0
	set $$$COMGridEditParameterContainer(YAUSWAHL)="VARSESTreatmentHC"
	$$$GRIDStart(YFORM,YKEY)
	quit
 
OnAfterSaveHook(pYKEY, pYFELD, pYFELDOLD) 
	if ($length($get(pYFELDOLD)) = 0) && ($length($get(pYFELD)) > 0) { 
		do RegistrarAlteracao^VARSESTreatmentHCHist( pYKEY, 1, 0, 0, 0, $h) 
	} 
	if (($length($get(pYFELDOLD)) > 0) && ($length($get(pYFELD)) > 0) && (pYFELDOLD'=pYFELD)) { 
		do RegistrarAlteracao^VARSESTreatmentHCHist( pYKEY, 7, 0, 0, 0, $h) 
	} 
	quit $$$OK 
	
 
]]></Routine>
</Export>