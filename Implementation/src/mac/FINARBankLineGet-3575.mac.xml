<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARBankLineGet" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARBankLineGet
	
#include COMSYS
#include FINConst
	
GetTotalPaying(pidBank,pidBankLine)
	;--------------------------------------------------------------------------------
	;
	; get the total of what we are paying off each invoice (except for the dummy invoice)
	;
	; History
	; 04-May-2006	Steve S		SR14597: Check ALL invoices
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 24-Jun-2004	shobby		Foreign Currency
	; 05-Aug-2003	shobby		Moved from FINARBank
	; 02-Jul-2003	shobby		Test whether an invoice is on hold before showing (TR005527)
	; 
	;--------------------------------------------------------------------------
	;
	new idBankInvoice,objBankInvoice,curAmount
 
	set idBankInvoice=""
	set curAmount=0
	if (pidBank'="")&(pidBankLine'="") {
		for {
			set idBankInvoice=$order(^FINARBankInvoice(YM,pidBank,pidBankLine,idBankInvoice))
			quit:idBankInvoice=""
			;if $$^WWWTEXT("Fin00057")'=idBankInvoice {  ; Unallocated //SR14597
			if '$$IsInvoiceOnHold(pidBank,pidBankLine,idBankInvoice) {
				set objBankInvoice=$get(^FINARBankInvoice(YM,pidBank,pidBankLine,idBankInvoice,1))
				set curAmount=$$$FCAdd(curAmount,$$$FINARBankInvoicePayAmount(objBankInvoice))
			}
		}
	}
	quit curAmount
 
 
GetAmountOwing(pYKEY="")
	;------------------------------------------------------------------------------
	;
	; History
	; 04-May-2006	Steve S		SR14597: Check ALL invoices
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 24-Jun-2004	shobby		Foreign Currency
	; 03-May-2004   Shobby   	Replaced YKOMMA with ","
	; 06-Aug-2003	shobby		Simplified parameter passing, field Default Variable Input on FINARBankLine didn't allow enough characters
	; 05-Aug-2003	shobby		Moved from FINARBank
	; 03-Jul-2003	shobby		Check whether an Invoice is on hold (TR005533)
	; 26-Jun-2003	shobby		'Expense Payment' case. (TR005463)
	;
	;------------------------------------------------------------------------------
	;
	new idBankInvoice,objBankInvoice,curAmount,objFINARBankLine,blnExpenseAccount
	new idBank,idBankLine
 
 	set idBank=$piece(pYKEY,",",1)
 	set idBankLine=$piece(pYKEY,",",2) 	
	set idBankInvoice=""
	;if $get(idBankLine)="" set idBankLine=1
	set curAmount=0
	set blnExpenseAccount=$$$NO
	if $get(idBankLine)'="" {
		set objFINARBankLine=$get(^FINARBankLine(YM,idBank,idBankLine,1))
		if objFINARBankLine'="" {
			if $$$FINARBankLineExpensePayment(objFINARBankLine) {
				set curAmount=$$GetBankAccountTotal(idBank,idBankLine)
				set blnExpenseAccount=$$$YES
			}
		}
	}
	if 'blnExpenseAccount {
		if (idBank'="")&(idBankLine'="") {
			for {
				set idBankInvoice=$order(^FINARBankInvoice(YM,idBank,idBankLine,idBankInvoice))
				quit:idBankInvoice=""
				;if $$^WWWTEXT("Fin00057")'=idBankInvoice {  ; Unallocated //SR14597
				if '$$IsInvoiceOnHold(idBank,idBankLine,idBankInvoice) {
					set objBankInvoice=$get(^FINARBankInvoice(YM,idBank,idBankLine,idBankInvoice,1))
					set curAmount=$$$FCAdd(curAmount,$$$FINARBankInvoiceAmountOwing(objBankInvoice))
				}
			}
		}
	}
	quit curAmount
 
 
GetBankAccountTotal(pidBank,pidBankLine)
	;------------------------------------------------------------------------------
	;
	; History
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 24-Jun-2004	shobby		Foreign Currency
	; 05-Aug-2003	shobby		Moved from FINARBank
	; 26-Jun-2003	shobby		Creation (TR005463)
	;
	;------------------------------------------------------------------------------
	;
	new curAmount,idx
	;
	set curAmount=0
	set idx=$query(^FINARBankAccount(YM,pidBank,pidBankLine))
	for {
		quit:idx=""
		quit:$piece(idx,",",2)'=pidBank
		if $piece(idx,",",3)=pidBankLine {
			set curAmount=$$$FCAdd(curAmount,@idx)
		}
		set idx=$query(@idx)
	}
	quit curAmount
 
 
GetDiscTotal(pidBank,pidBankLine)
	;-----------------------------------------------------------------------------
	;
	; History
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 24-Jun-2004	shobby		Foreign Currency
	; 06-Aug-2003	shobby		Renamed, field Default Variable Input on FINARBankLine didn't allow enough characters
	; 05-Aug-2003	shobby		Moved from FINARBank
	; 03-Jul-2003	shobby		Check whether an Invoice is on hold (TR005533)
	;
	;-----------------------------------------------------------------------------
	;
	new idBankInvoice,objBankInvoice,curAmount
	set idBankInvoice=""
	set curAmount=0
	if (pidBank'="")&(pidBankLine'="") {
		for {
			set idBankInvoice=$order(^FINARBankInvoice(YM,pidBank,pidBankLine,idBankInvoice))
			quit:idBankInvoice=""
			set objBankInvoice=$get(^FINARBankInvoice(YM,pidBank,pidBankLine,idBankInvoice,1))
			if $$$FINARBankInvoiceDiscountApplied(objBankInvoice) {
				if '$$IsInvoiceOnHold(pidBank,pidBankLine,idBankInvoice) {
					set curAmount=$$$FCAdd(curAmount,$$$FINARBankInvoiceDiscount(objBankInvoice))
				}
			}
		}
	}
	quit curAmount
 
 
GetBalance(pidBank,pidBankLine)
	;-------------------------------------------------------------------------------
	;
	; History
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 24-Jun-2004	shobby		Foreign Currency
	; 03-May-2004   Shobby   	Replaced YKOMMA with ","
	; 06-Aug-2003	shobby		Name of GetTotalPayments changed.
	; 06-Aug-2003	shobby		Definition of GetAmountOwing was changed.
	; 05-Aug-2003	shobby		Moved from FINARBank
	;-------------------------------------------------------------------------------
	;
	new objBankLine,curDiscounts,curPayment,curAmount
	
	set curAmount=0
 
	if (pidBank'="")&(pidBankLine'="") {
		set objBankLine=$get(^FINARBankLine(YM,pidBank,pidBankLine,1))
		set curAmount=$$$FCSub($$GetAmountOwing(pidBank_","_pidBankLine),$$GetTotalPay(pidBank,pidBankLine))
		}
	quit curAmount
 
 
GetTotalPay(pidBank,pidBankLine)
	;-------------------------------------------------------------------------------
	;
	; History
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 24-Jun-2004	shobby		Foreign Currency
	; 06-Aug-2003	shobby		Renamed, field Default Variable Input on FINARBankLine didn't allow enough characters
	; 05-Aug-2003	shobby		Moved from FINARBank
	;-------------------------------------------------------------------------------
	;
	new objBankLine,curAmountOwing,curDiscounts,curPayment,curAmount
	
	set curAmount=0
	if (pidBank'="")&(pidBankLine'="") {
		set objBankLine=$get(^FINARBankLine(YM,pidBank,pidBankLine,1))
		set curDiscounts=$$GetDiscTotal(pidBank,pidBankLine)
		set curPayment=$$$FINARBankLineTotal(objBankLine)
		set curAmount=$$$FCAdd(curPayment,curDiscounts)
		}
	quit curAmount
 
 
GetCustomerNumber(pidBank,pidBankLine)
	;---------------------------------------------------------------------------
	;
	; History
	; 05-Aug-2003	shobby		Moved from FINARBank
	; 02-Jul-2003	shobby		Modified to return the Customer Number (TR005527)
	; 
	;---------------------------------------------------------------------------
	;
	new idBankLine,objBankLine,strCustomerNumber
 
	if (pidBank'="")&(pidBankLine'="") {
		set objBankLine=$get(^FINARBankLine(YM,pidBank,pidBankLine,1))
		set strCustomerNumber=$$$FINARBankLineCustomerNumber(objBankLine)
		;w $$^WWWTEXT("Fin00056")_" - "_strCustomerNumber  ; Customer Number
		}
	quit strCustomerNumber
	
 
IsInvoiceOnHold(pidBank,pidBankLine,pidBankInvoice)
	;---------------------------------------------------------------------------
	; Find out whether an Invoice is on hold
	; 
	; History
	; 02-Jun-2005	shobby		Moved from FINARBankLine (CodeCheck)
	; 05-Aug-2003	shobby		Moved from FINARBank
	; 03-Jul-2003	shobby		Creation (TR005533)
	; 
	;---------------------------------------------------------------------------
	;
	new objFINARInvoice,idCustomerNumber
	;
	set idCustomerNumber=$$GetCustomerNumber(pidBank,pidBankLine)
	set objFINARInvoice=$get(^FINARInvoice(YM,idCustomerNumber,pidBankInvoice,1))
	;
	quit $$$FINARInvoiceOnHold(objFINARInvoice)
]]></Routine>
</Export>