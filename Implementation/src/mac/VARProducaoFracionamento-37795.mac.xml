<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARProducaoFracionamento" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARProducaoFracionamento
#include COMSYS

OnAfterSave
	new dteInicial, dteFinal, usuario, idProduto, formato
	
	set dteInicial   = $get(VORG(1))
	set dteFinal     = $get(VORG(2))
	set usuario  	 = $get(VORG(3))
	set idProduto    = $get(VORG(4))
	set formato		 = $get(VORG(5))
	
	kill ^VARTempProducaoFracionamento
	kill ^VARTempCamposProducaoFracionamento
	
	set ^VARTempCamposProducaoFracionamento(YBED) = dteInicial_Y_dteFinal_Y_usuario_Y_idProduto_Y_formato

	;Caso haja algum problema, não loga o after save e nenhum dado será mostrado até que o usuário conserte.
	if (dteInicial > dteFinal) {
		$$$Alert("A data inicial não pode ser maior que a data final.")
	} elseif (dteFinal > $piece($horolog,",",1)) {
		$$$Alert("A data final não pode ser maior que a data atual.")
	} else {
		do VARLogAfterSave^VARUtil(YBED,YFORM)
	}
	
	do GoToForm^COMUtilForm("VARProducaoFracionamento",1,,,,,)
	
	quit

OnAfterDataFields
	new dteInicial, dteFinal, usuario, idProduto, formato

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	set dteInicial   = $get(VORG(1))
	set dteFinal     = $get(VORG(2))
	set usuario  	 = $get(VORG(3))
	set idProduto    = $get(VORG(4))
	set formato		 = $get(VORG(5))
	
	if (formato = "") set formato = 1
	
	do ExibeResultados(dteInicial,dteFinal,usuario,idProduto,formato)
	
	quit
	
ExibeResultados(pdteInicial,pdteFinal,pUsuario,pidProduto,pFormato)
	new SQLProdFrac, meuSQL, lstHeader, count, countUsers, totalLinha, totalFrac, totalKits,
		totalGeral, totalUserFrac, totalUserKits, lastUsuario, usuario, nomeUsuario, produto, 
		descProduto, quantFrac, quantKits
	
	set SQLProdFrac = ##class(%ResultSet).%New()  ;Create Result Set Object

 	set meuSQL = "SELECT Etapa.Responsavel as Responsavel,"
 	set meuSQL = meuSQL_" $$SQLGetUserName^VARSQL(Etapa.Responsavel) as NomeResponsavel,"
 	set meuSQL = meuSQL_" Ordem.Produto as Produto,"
 	set meuSQL = meuSQL_" $$SQLGetDescricaoProduto^VARSQL(Ordem.Produto) as descProduto,"
 	set meuSQL = meuSQL_" SUM(Etapa.QuantSelEtiq) as QuantFrac,"
 	set meuSQL = meuSQL_" '0' as QuantKits"
 	set meuSQL = meuSQL_" FROM SQLUser.VAROrdemUnitarizacaoEtapa Etapa"
 	set meuSQL = meuSQL_" INNER JOIN SQLUser.VAROrdemUnitarizacao AS Ordem"
 	set meuSQL = meuSQL_" ON Ordem.CodOrdemUnitarizacao = Etapa.CodOrdemUnitarizacao"
 	set meuSQL = meuSQL_" WHERE Etapa.Situacao = 1"
 	set meuSQL = meuSQL_" AND $piece(Etapa.Data,',',1) BETWEEN "_pdteInicial_" AND "_pdteFinal
 	
 	if (pUsuario '= "") {
 		set meuSQL = meuSQL_" AND Etapa.Responsavel = '"_pUsuario_"'"
 	}
 	
	if (pidProduto  '= "") {
		set meuSQL = meuSQL_" AND Ordem.Produto = '"_pidProduto_"'"
	}
 	
 	set meuSQL = meuSQL_" AND Etapa.QuantSelEtiq > 0"
 	set meuSQL = meuSQL_" GROUP BY Etapa.Responsavel, Ordem.Produto" 	
 	
 	set meuSQL = meuSQL_" UNION ALL"
 	
 	set meuSQL = meuSQL_" SELECT Producao.ReceiptFirmedBy as Responsavel,"
 	set meuSQL = meuSQL_" $$SQLGetUserName^VARSQL(Producao.ReceiptFirmedBy) as NomeResponsavel," 	
 	set meuSQL = meuSQL_" Producao.ItemCode as Produto,"
 	set meuSQL = meuSQL_" $$SQLGetDescricaoProduto^VARSQL(Producao.ItemCode) as descProduto," 	
 	set meuSQL = meuSQL_" '0' as QuantFrac,"
 	set meuSQL = meuSQL_" SUM(Producao.Receipt) as QuantKits"
 	set meuSQL = meuSQL_" FROM INMO Producao"
 	set meuSQL = meuSQL_" WHERE Producao.Status = 5"
 	set meuSQL = meuSQL_" AND $piece(Producao.IssueFirmedDate,',',1) BETWEEN "_pdteInicial_" AND "_pdteFinal

 	if (pUsuario '= "") {
 		set meuSQL = meuSQL_" AND Producao.ReceiptFirmedBy = '"_pUsuario_"'"
 	}
 	
	if (pidProduto  '= "") {
		set meuSQL = meuSQL_" AND Producao.ItemCode = '"_pidProduto_"'"
	}
 	
 	set meuSQL = meuSQL_" AND Producao.Receipt > 0"
 	set meuSQL = meuSQL_" GROUP BY Producao.ReceiptFirmedBy, Producao.ItemCode"
 	
 	set meuSQL = meuSQL_" ORDER BY 2,4 ASC"

	do SQLProdFrac.Prepare(meuSQL)  ;Prepare Query
	do SQLProdFrac.Execute()  		;Execute Query

	//Tabela
 	set lstHeader = ""
 	
 	if (pFormato = 2) {
	 	set lstHeader = lstHeader_$listbuild("#","Código","Produto","Unid. Fracionadas","Kits Montados","Total")
 	} else {
	 	set lstHeader = lstHeader_$listbuild("#","Usuario","Colaborador","Unid. Fracionadas","Kits Montados","Total")
 	}
 	
  	if $$Start^COMTable(lstHeader) {
		
		set count = 0
		set countUsers = 0
		set totalLinha = 0
		set totalFrac 	 = 0
		set totalKits 	 = 0
		set totalGeral   = 0
		set totalUserFrac = 0
		set totalUserKits = 0
		
		set lastUsuario = ""
		
		while (SQLProdFrac.Next()) {
			
			set count = $increment(count)
			set usuario   	= SQLProdFrac.GetDataByName("Responsavel")
			set nomeUsuario = SQLProdFrac.GetDataByName("NomeResponsavel")
			set produto   	= SQLProdFrac.GetDataByName("Produto")
			set descProduto = SQLProdFrac.GetDataByName("descProduto")			
			set quantFrac 	= +SQLProdFrac.GetDataByName("QuantFrac")
			set quantKits 	= +SQLProdFrac.GetDataByName("QuantKits")		

			set totalLinha = quantFrac + quantKits
			set totalFrac    = totalFrac + quantFrac
			set totalKits	 = totalKits + quantKits
			
			if (lastUsuario '= usuario) && (lastUsuario '= "") {
				
				do NewLine^COMTable($$GetColorFormato(pFormato))
				
				if (pFormato = 2) {				
					do InsertCell^COMTable("<strong>Total</strong>",,,,"right",,3)
					do InsertCell^COMTable("<strong>"_totalUserFrac_"</strong>",,,,"right")
					do InsertCell^COMTable("<strong>"_totalUserKits_"</strong>",,,,"right")
					do InsertCell^COMTable("<strong>"_(totalUserFrac + totalUserKits)_"</strong>",,,,"right")					
				} else {
					set countUsers = $increment(countUsers)
					do InsertCell^COMTable(countUsers)
					do InsertCell^COMTable(lastUsuario)
					do InsertCell^COMTable($$SQLGetUserName^VARSQL(lastUsuario))
					do InsertCell^COMTable(totalUserFrac,,,,"right")
					do InsertCell^COMTable(totalUserKits,,,,"right")
					do InsertCell^COMTable((totalUserFrac + totalUserKits),,,,"right")
					
					// Insere os dados em uma classe temporaria
					set strKey     = YBED_$$$COMMA_countUsers
					set totalTotal = (totalUserFrac + totalUserKits)
					set objReport  = lastUsuario_Y_$$SQLGetUserName^VARSQL(lastUsuario)_Y_totalUserFrac_Y_totalUserKits_Y_totalTotal
					set status 	   = $$$Save("VARTempProducaoFracionamento",strKey,objReport,1)					
				}

				do EndLine^COMTable()

				set totalUserFrac = 0
				set totalUserKits = 0
				set count = 1
				
				if (pFormato = 2) {
					do NewLine^COMTable("#BABABA")
					do InsertCell^COMTable("",,,,,,6)
					do EndLine^COMTable()
				}
			}
		
			if (lastUsuario '= usuario) && (pFormato = 2) {				
				do NewLine^COMTable()
				do InsertCell^COMTable("<strong>("_usuario_") "_nomeUsuario_"</strong>",,,,,,6)
				do EndLine^COMTable()
			}
			
			if (pFormato = 2) {			
				do NewLine^COMTable()
				do InsertCell^COMTable(count)
				do InsertCell^COMTable(produto)
				do InsertCell^COMTable(descProduto)
				do InsertCell^COMTable($$^WWWTR(0,12,quantFrac),,,,"right")
				do InsertCell^COMTable($$^WWWTR(0,12,quantKits),,,,"right")
				do InsertCell^COMTable($$^WWWTR(0,12,totalLinha),,,,"right")			
				do EndLine^COMTable()
			}

			set totalUserFrac = totalUserFrac + quantFrac
			set totalUserKits = totalUserKits + quantKits			
			set lastUsuario = usuario		
		}
		
		if (count = 0) {
			do NewLine^COMTable("white")
			do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,6)
			do EndLine^COMTable()
			
		} else {
			
			do NewLine^COMTable($$GetColorFormato(pFormato))
		
			if (pFormato = 2) {				
				do InsertCell^COMTable("<strong>Total</strong>",,,,"right",,3)
				do InsertCell^COMTable("<strong>"_totalUserFrac_"</strong>",,,,"right")
				do InsertCell^COMTable("<strong>"_totalUserKits_"</strong>",,,,"right")
				do InsertCell^COMTable("<strong>"_(totalUserFrac + totalUserKits)_"</strong>",,,,"right")					
			} else {
				set countUsers = $increment(countUsers)
				do InsertCell^COMTable(countUsers)
				do InsertCell^COMTable(lastUsuario)
				do InsertCell^COMTable($$SQLGetUserName^VARSQL(lastUsuario))
				do InsertCell^COMTable(totalUserFrac,,,,"right")
				do InsertCell^COMTable(totalUserKits,,,,"right")
				do InsertCell^COMTable((totalUserFrac + totalUserKits),,,,"right")
				
				// Salva o último registro que falta na classe VARTemp
				set strKey = YBED_$$$COMMA_countUsers
				set totalTotal = (totalUserFrac + totalUserKits)
				set objReport  = lastUsuario_Y_$$SQLGetUserName^VARSQL(lastUsuario)_Y_totalUserFrac_Y_totalUserKits_Y_totalTotal
				set status = $$$Save("VARTempProducaoFracionamento",strKey,objReport,1)					
			}

			do EndLine^COMTable()	
		
			set totalGeral = totalFrac + totalKits
		
			do NewLine^COMTable("lightsteelblue")
			do InsertCell^COMTable("<strong>TOTAL GERAL</strong>",,,,"right",,3)
			do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,totalFrac)_"</strong>",,,,"right")
			do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,totalKits)_"</strong>",,,,"right")
			do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,totalGeral)_"</strong>",,,,"right")
			do EndLine^COMTable
		}
				
		do Stop^COMTable()
  	}
  	do SQLProdFrac.Close()
  	
	quit
	
GetColorFormato(pFormato)
	quit:(pFormato = 2) "lightsteelblue"
	quit ""
	
Imprimir
	
	new dtaInicial, dtaFinal, colaborador, produto, formato
	
	set dtaInicial 	= $get(VORG(1))
	set dtaFinal   	= $get(VORG(2))
	set colaborador = $get(VORG(3))
	set produto		= $get(VORG(4))
	set formato		= $get(VORG(5))
	
	do RunReportProducaoFracionamento^VARJasperRunReportContinuacao(dtaInicial,dtaFinal,colaborador,produto,formato)

]]></Routine>
</Export>