<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMSTARTZstop" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
%ZSTOP ; User shutdown routine. Must be installed in %SYS to operate
 
	;=========================================================================
	; WARNING - Any modifications to this routine when it is installed in %SYS
	; 			will be OVERRIDEN with the next upgrade of COM.
	; 			All required changes should be made in COM.
	;=========================================================================
 
#Define ME "ZSTOP"
#Define EndSet "End"
#Define Empty ""
#Define Empty ""
#Define OprLog 1
 
    ; cannot be invoked directly
    quit
 
IDENT ;;COMSTARTZstop
	; this identity ensures only COMSTARTZstop routines are replaced
	quit
 
SYSTEM ; Cache stopping
	; History:
	; 13-Mar-2007	RPW		SR10754: Do not use ^%COMSTARTUtils
    new EntryPoint
    
    set EntryPoint = "SYSTEM"
    ; record termination in the console log
    do WriteConsole(EntryPoint
                               _ "^%"
                               _ $$$ME
                               _ " called @ "
                               _ $ZDATETIME($HOROLOG, 3)) // SR10754
    ; write the standard log information
    ;do Logit(EntryPoint, $$$ME)
    quit
    
LOGIN ; a user logs out of Cache (user account or telnet)
    new EntryPoint
    
    set EntryPoint = "LOGIN"
    ;do Logit(EntryPoint, $$$ME)
    quit
    
JOB ; JOB'd process exits. 
    new EntryPoint
    
    set EntryPoint = "JOB"
    ;do Logit(EntryPoint, $$$ME)
    quit
     
CALLIN ; process exits via CALLIN interface. 
    new EntryPoint
    
    set EntryPoint = "CALLIN"
    ;do Logit(EntryPoint, $$$ME)
    quit
    
Logit(entrypoint, caller) PRIVATE ;
	; History:
	; 13-Mar-2007	RPW		SR10754: Do not use ^%COMSTARTUtils
    ; common logging for exits
    
    new items
    
     set items = $LISTBUILD($$$EndSet, $ZDATETIME($HOROLOG, 3))
    do WriteLog(caller, entrypoint, items) // SR10754
    quit
 
WriteConsole(LineText) PUBLIC ;
	; History:
	; 13-Mar-2007	RPW		SR10754: Copied from COMSTARTUtils
    ; write the line to the console log
    ; by default the file cconsole.log in the MGR directory
    new SaveIO
    
    ; save the current device and open the operator console
    ; set up error handling to cope with errors
    ; there is little to do if an error happens
    set SaveIO = $IO
    set $ZTRAP = "WriteConsoleExit"
    open $$$OprLog
    use $$$OprLog
    ; we do not need an "!" for line termination
    ; each WRITE becomes its own console record
    write LineText
    ; restore the previous io device
    close $$$OprLog
    ; pick up here in case of an error
WriteConsoleExit ;Internal Tag
	; History:
	; 13-Mar-2007	RPW		SR10754: Copied from COMSTARTUtils
    set $ZTRAP = ""
    use SaveIO
    quit
 
WriteLog(rtnname, entryname, items) PUBLIC ;
	; History:
	; 13-Mar-2007	RPW		SR10754: Copied from COMSTARTUtils
    ; write entries into the log file
    ; the log is presumed to be open as 
    ; the default output device
    ; 
    ; rtnname: distinguishes between ZSTART & ZSTOP
    ; entryname: the name of the entrypoint we came from
    ; items: a $LIST of name-value pairs
    new ThisIO, ThisLog
    new i, DataString
    
    ; preserve the existing $IO device reference
    ; set up error handling to cope with errors
    ; there is little to do if an error happens
    set ThisIO = $IO
    set $ZTRAP = "WriteLogExit"
 
    ; construct the name of the file
    ; use the month and day as part of the name so that 
    ; it will create a separate log file each day
    set ThisLog = "ZSS"
                _ "-"
                _ $EXTRACT($ZDATE($HOROLOG, 3), 6, 10)
                _".log"
    
    ; and change $IO to point to our file
    open ThisLog:"AWS":0
    use ThisLog
    
    ; now loop over the items writing one line per item pair
    for i = 1 : 2 : $LISTLENGTH(items)
    {
        set DataString = $LISTGET(items, i, "*MISSING*")
        if ($LISTGET(items, (i + 1), $$$Empty) '= $$$Empty)
        {
            set DataString = DataString
                           _ ": "
                           _ $LISTGET(items, (i + 1))
        }
        write $ZDATETIME($HOROLOG, 3, 1),
              ?21, rtnname,
              ?28, entryname,
              ?35, DataString, !
    }
    
    ; stop using the log file and switch $IO back
    ; to the value saved on entry
    close $IO
    ; pick up here in case of an error
WriteLogExit ;Internal Tag
	; History:
	; 13-Mar-2007	RPW		SR10754: Copied from COMSTARTUtils
    set $ZTRAP = ""
    use ThisIO
    quit
]]></Routine>
</Export>