<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMAILR1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMAILR1(SERVER,USER,PASSWORD,DELETE,GLO,ANZAHL) ;WWWMAILR1;SAN;holt eine e-Mail; ;one   ; Compiled February 9, 2005 13:10:40
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		holt eine e-Mail
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| SAN	holt eine e-Mail
	;|
	;\------------------------------------------------------------------/
	;
	;Kai Sann, SAN 15.3.2000
	;;SERVER:   IP-Adresse des e-Mail Servers, z.B: 172.16.0.2
	;;USER:     e-Mail User, z.B: Sann
	;;PASSWORD: Passwort des e-Mail Kontos
	;;DELETE:   1 = abgeholte e-Mail löschen, 0 = abgeholte e-Mail nicht löschen
	;;GLO:      Zugriff des Arrays in den die e-Mail geschrieben wird, z.B: "LOCAL" oder "^GLOBAL(""KNOTEN"")"
	;;ANZAHL:   Anzahl der e-Mails, die gelesen werden sollen (Default=1)
	NEW (SERVER,USER,PASSWORD,DELETE,GLO,ANZAHL,Y,YBED,YUSER)
	SET ERROR=0
	SET DEBUG=0
	SET ANZ=0        
	IF GLO="" SET GLO="^WWWMAI(0,"_""""_YBED_""""_")" ;Wenn kein Global mach Dir einen ;not 
	IF $ZVERSION["MSM" DO   ;Kompatibilität zu MSM, ACHTUNG funktioniert nur wenn der AUFRUF SOCKET funktioniert kein Standard MUMPS
	. SET DEV=56
	. OPEN 56
	. USE 56 WRITE /SOCKET(SERVER,110) ;110 Ist der Server Port des POP3 PROTOKOLLS ;who Server 
	. USE 56:(:1:3)
	. QUIT 
	IF $ZVERSION["Cache" DO
	. SET DEV="|TCP|110"
	. OPEN DEV:(SERVER:110:"C") 
	. USE DEV:(SERVER:110:"C")
	. QUIT 
	IF '$$OK GOTO END
	WRITE "USER "_USER_$CHAR(13,10) WRITE ! IF '$$OK GOTO END ;ACHTUNG: W !  des TCP/IP Protokolls gehe in Bereitschaft für nächste Befehl $C(13,10) funktioiert nicht!!!
	WRITE "PASS "_PASSWORD_$CHAR(13,10) WRITE ! IF '$$OK GOTO END  
	WRITE "STAT",! IF '$$OK GOTO END
	SET ANZ=+$PIECE(IN," ",2)
	SET $PIECE(ERGEBNIS,Y,3)=ANZ
	IF $GET(ANZAHL)="" SET ANZAHL=1
	IF ANZAHL<ANZ SET ANZ=ANZAHL
	FOR I=1:1:ANZ DO  QUIT:ERROR=1
	. LOCK +@GLO
	. SET MAILNR=$ORDER(@GLO@(""),-1)+1
	. SET @GLO@(MAILNR,1,1)=""
	. LOCK -@GLO
	. SET LFN=1
	. WRITE "RETR "_I,!
	. SET START=0
	. SET STRING2=""
	. IF '$$OK SET ERROR=1 QUIT
	. FOR  SET DUMMY=$$OK QUIT:IN="."  DO
	. . IF START=0 DO INTERP QUIT
	. . SET:$EXTRACT(IN)="." IN=$EXTRACT(IN,2,255)
	. . SET LFN=LFN+1 SET @GLO@(MAILNR,LFN,1)=IN
	. . QUIT
	. SET $PIECE(ERGEBNIS,Y,1)=I
	. IF DELETE WRITE "DELE "_I,! IF '$$OK SET ERROR=1 QUIT
	. SET $PIECE(ERGEBNIS,Y,2)=I
	. QUIT
	
END ; 
	USE DEV WRITE "QUIT",!
	CLOSE DEV 
	SET $PIECE(ERGEBNIS,Y,4)=ERROR
	QUIT ERGEBNIS
	
OK() ;
	NEW (IN,DEV,TCPIP,POP3)
	SET IN=""
	USE DEV
	FOR  READ DUMMY#1:1 QUIT:'$TEST  DO DEBUG(DUMMY) SET IN=IN_DUMMY QUIT:IN[$CHAR(13,10)
	SET IN=$PIECE(IN,$CHAR(13,10))
	QUIT $PIECE(IN," ")="+OK"
	
DEBUG(A) ;
	USE 0 WRITE A USE DEV
	QUIT
	
INTERP ;Hier werden Zeilen gelesen ;will 
	NEW I
	SET STRING="From~Subject~Date~MIME-Version~Content-Type~Content-Transfer-Encoding" 
	IF IN="" SET START=1 SET @GLO@(MAILNR,1,1)=STRING2 QUIT
	FOR I=1:1:$LENGTH(STRING,Y) DO 
	. SET TEST=$PIECE(STRING,Y,I)
	. IF $PIECE(IN,":")[TEST SET $PIECE(STRING2,Y,I)=$PIECE(IN,": ",2,9)
	. QUIT
	QUIT
]]></Routine>
</Export>