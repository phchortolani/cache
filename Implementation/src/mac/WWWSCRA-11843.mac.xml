<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSCRA" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSCRA
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SCREEN MOVE ANALYSIS
	;	DIESES PROGRAMM WERTET DIE EINGABEN AUS DEM SCREEN EDIT AUS
	;	This program evaluates the input from the screen edit
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 24-Aug-2007	GRF		Doco; quits; expand commands
	; 09.07.1999	DT
	;-------------------------------------------------------------------------------
	NEW YI,YQ,YDAT,YFELD
	
	SET YDAT  = $PIECE(YSCR,"-",1)
	SET YFELD = $PIECE(YSCR,"-",2)
	SET YQ    = $PIECE(YSCR,"-",3)
	
	QUIT:YDAT=""
	QUIT:YFELD=""
	QUIT:YFORM=""
	
	;---------------------------------------
	; FIXME : <GRF> There is no class WWW122DU
	;         either killed or set to 1 here and used in WWWFORMD to change hidden flags
	;---------------------------------------
	
	IF $GET(YUSERPROFILE)'="" IF YQ'="" DO  QUIT  ;USER ACCESS SPECIAL
	. IF YQ="K" SET ^WWW122DU(0,YFORM,YM,YUSERPROFILE,YFELD,1)=1  ;DO NOT SHOW
	. IF YQ="X" KILL ^WWW122DU(0,YFORM,YM,YUSERPROFILE)           ;SHOW
	
	IF YDAT="B" DO  QUIT  ;BUTTONAUSWERTUNG KOPF 
	. IF YFELD=1 DO  ;FORMULARART
	. . SET YI(1)=$PIECE($GET(^WWW120(0,YFORM,1)),Y,44)
	. . SET YI(1)=YI(1)+1
	. . IF YI(1)>2 SET YI(1)=0
	. . SET $PIECE(^WWW120(0,YFORM,1),Y,44)=YI(1)
	. ;
	. IF YFELD=2 DO  ;FORMULARART
	. . SET YI(1)=$PIECE($GET(^WWW120(0,YFORM,1)),Y,2)
	. . IF YI(1)=1 SET $PIECE(^WWW120(0,YFORM,1),Y,2)=3 SET $PIECE(^WWW120(0,YFORM,1),Y,44)=1 QUIT
	. . IF YI(1)=3 SET $PIECE(^WWW120(0,YFORM,1),Y,2)=1 QUIT
	. ;
	. IF YFELD=3 DO  ;FRAME
	. . SET YI(1)=$PIECE($GET(^WWW120(0,YFORM,1)),Y,13)
	. . IF +YI(1)=0 SET $PIECE(^WWW120(0,YFORM,1),Y,13)=1 QUIT
	. . IF YI(1)=1  SET $PIECE(^WWW120(0,YFORM,1),Y,13)=0 QUIT
	. ;
	. IF YFELD=4 DO  ;HORIZONTAL
	. . SET YI(1)=$PIECE($GET(^WWW120(0,YFORM,1)),Y,36)
	. . IF YI(1)=1  SET $PIECE(^WWW120(0,YFORM,1),Y,36)=3 QUIT
	. . IF YI(1)'=1 SET $PIECE(^WWW120(0,YFORM,1),Y,36)=1 QUIT
	
	QUIT:YQ=""
	
	SET YI="^WWW122(0,"_""""_YFORM_""""_","_YFELD_",1)"
	IF YDAT="P" SET YI="^WWW121(0,"_""""_YFORM_""""_","_YFELD_",1)"
	SET YI(1)=$GET(@YI)          ;SUCHEN FELD ;seek field 
	IF YDAT'="P" QUIT:YI(1)=""   ;WENN "PRIMÄRSCHLÜSSEL DANN KANN AUCH LEER
	IF YDAT'="P" DO
	. SET SK1=$$^WWWUMLAU(YFORM,1)
	. SET SK2=$PIECE(YI(1),Y,60)
	. IF SK2="" SET SK2=" "
	. SET SK3=$PIECE(YI(1),Y,3)
	. IF SK3="" SET SK3=" "
	. SET SK4=$PIECE(YI(1),Y,4)
	. IF SK4="" SET SK4=" "
	. KILL ^WWW122s(0,3,$$^WWWUMLAU(SK1,1),SK2,SK3,SK4,YFORM,YFELD)
	. KILL ^WWW122s(0,1,SK2,YFORM,YFELD)
	
	IF YQ'="K" DO   ; D @YQ  ;VERTEILER ;distributor 
	. IF YQ="A" DO A
	. IF YQ="B" DO B
	. IF YQ="C" DO C
	. IF YQ="D" DO D
	. IF YQ="E" DO E
	. IF YQ="G" DO G
	. IF YQ="H" DO H
	. IF YQ="L" DO L
	. IF YQ="M" DO M
	. IF YQ="O" DO O
	. IF YQ="P" DO P
	. IF YQ="R" DO R
	. IF YQ="S" DO S
	. IF YQ="T" DO T
	. IF YQ="U" DO U
	. IF YQ="V" DO V
	. IF YQ="W" DO W
	. IF YQ="X" DO X
	
	SET @YI=YI(1)   ;SUCHEN FELD ;seek field 
	
	IF YDAT'="P" DO
	. NEW SK1,SK2,SK3,SK4  ;TYBD;6,8,2004
	. SET SK1=$$^WWWUMLAU(YFORM,1)
	. SET SK2=$PIECE(YI(1),Y,60)
	. IF SK2="" SET SK2=" "
	. SET SK3=$PIECE(YI(1),Y,3)
	. IF SK3="" SET SK3=" "
	. SET SK4=$PIECE(YI(1),Y,4)
	. IF SK4="" SET SK4=" "
	. SET ^WWW122s(0,3,$$^WWWUMLAU(SK1,1),SK2,SK3,SK4,YFORM,YFELD)=""
	. SET ^WWW122s(0,1,SK2,YFORM,YFELD)=""
	
	IF YQ="K" DO   ;KILL
	. NEW YFORM1
	. SET YFORM1=YFORM
	. NEW YFORM
	. SET YFORM="WWW122"
	. DO ^WWWKILL("WWW122",YFORM1_","_YFELD)
	
	QUIT
	
	; YI(1)		objWWW122
	;	D3		$$$WWW122RowPosition()
	;	D4		$$$WWW122ColumnPosition()
	;	D7		$$$WWW122AddNoOfSpaces()
	;	D24		$$$WWW122Module1()
	;	D36		$$$WWW122HorizontalOrientation()
	;	D38		$$$WWW122NewGroup()
	;	D60		$$$WWW122DisplayOnPageNumber()
	;	D70		$$$WWW122PositionCaptionToAnswer()
	;	D88		$$$WWW122ModifiedFieldLength()
	
A ;UP AUF 1 ;upon 
	SET $PIECE(YI(1),Y,3)=1
	QUIT
	
X ;UP AUF 10 ;upon 
	SET $PIECE(YI(1),Y,3)=10
	QUIT
	
U ;UP
	SET YI(3)=$PIECE(YI(1),Y,3)
	SET YI(3)=YI(3)-1
	IF YI(3)<0 SET YI(3)=0
	SET $PIECE(YI(1),Y,3)=YI(3)
	QUIT
	
D ;DOWN
	SET YI(3)=$PIECE(YI(1),Y,3)
	SET YI(3)=YI(3)+1
	IF YI(3)<0 SET YI(3)=0
	SET $PIECE(YI(1),Y,3)=YI(3)
	QUIT
	
R ;RECHTS ;on the right 
	SET YI(4)=$PIECE(YI(1),Y,4)
	SET YI(4)=YI(4)+1
	IF YI(4)<1 SET YI(4)=1
	SET $PIECE(YI(1),Y,4)=YI(4)
	QUIT
	
L ;LINKS ;on the left 
	SET YI(4)=$PIECE(YI(1),Y,4)
	SET YI(4)=YI(4)-1
	IF YI(4)<1 SET YI(4)=1
	SET $PIECE(YI(1),Y,4)=YI(4)
	QUIT
	
P ;SEITE PLUS ;side plus 
	;Q:$P(YI(1),Y,1)=""   ;NUR FÜR DATENFELDER
	SET YI(60)=$PIECE(YI(1),Y,60)
	SET YI(60)=YI(60)+1
	IF YI(60)>8 SET YI(60)=8
	SET $PIECE(YI(1),Y,60)=YI(60)
	QUIT
	
M ;SEITE MINUS ;side minus 
	SET YI(60)=$PIECE(YI(1),Y,60)
	SET YI(60)=YI(60)-1
	IF YI(60)<1 SET YI(60)=1
	SET $PIECE(YI(1),Y,60)=YI(60)
	QUIT
	
S ;SPACE MEHR  ;more 
	SET YI(7)=$PIECE(YI(1),Y,7)
	SET YI(7)=YI(7)+1
	IF YI(7)>80 SET YI(7)=80
	SET $PIECE(YI(1),Y,7)=YI(7)
	QUIT
	
B ;BLANKS RAUS
	SET YI(7)=$PIECE(YI(1),Y,7)
	SET YI(7)=YI(7)-1
	;IF YI(7)<0 S YI(7)=0
	SET $PIECE(YI(1),Y,7)=YI(7)
	QUIT
	
W ;MEHR width  ;more 
	SET YI(7)=$PIECE(YI(1),Y,88)
	IF YI(7)="" SET YI(7)=25
	SET YI(7)=YI(7)+1
	SET $PIECE(YI(1),Y,88)=YI(7)
	QUIT
	
C ;weniger width ;minus 
	SET YI(7)=$PIECE(YI(1),Y,88)
	SET YI(7)=YI(7)-1
	IF YI(7)<1 SET YI(7)=""
	SET $PIECE(YI(1),Y,88)=YI(7)
	QUIT
	
H ;HORIZONTAL
	SET YI(36)=$PIECE(YI(1),Y,36)
	IF YI(36)=3  SET $PIECE(YI(1),Y,36)=1 QUIT
	IF YI(36)'=3 SET $PIECE(YI(1),Y,36)=3 QUIT
	QUIT
	
O ;ORIENTIERUNG
	SET YI(70)=$PIECE(YI(1),Y,70)
	SET YI(70)=YI(70)+1
	IF YI(70)>4 SET YI(70)=0
	SET $PIECE(YI(1),Y,70)=YI(70)
	QUIT
	
G ;GRUPPE ;group 
	SET YI(38)=$PIECE(YI(1),Y,38)
	IF YI(38)=1  SET $PIECE(YI(1),Y,38)=0 QUIT
	IF YI(38)'=1 SET $PIECE(YI(1),Y,38)=1 QUIT
	QUIT
	
E ;MODUL EK ;module Planned Cost 
	SET YI(24)=$PIECE(YI(1),Y,24)
	IF '$FIND(YI(24),"EK") DO
	. IF YI(24)'=""  SET YI(24)=YI(24)_";"
	. SET YI(24)=YI(24)_"EK"
	
	SET $PIECE(YI(1),Y,24)=YI(24)
	QUIT
	
V ;MODUL VK ;module Sale 
	SET YI(24)=$PIECE(YI(1),Y,24)
	IF '$FIND(YI(24),"VK") DO
	. IF YI(24)'=""  SET YI(24)=YI(24)_";"
	. SET YI(24)=YI(24)_"VK"
	
	SET $PIECE(YI(1),Y,24)=YI(24)
	QUIT
	
T ;TAB ORIENTIERUNG
	SET YI(70)=$PIECE(YI(1),Y,70)
	SET YI(70)=YI(70)+1
	IF YI(70)=1 SET YI(70)=2 SET $PIECE(YI(1),Y,7)=4
	IF YI(70)>4 SET YI(70)=0
	SET $PIECE(YI(1),Y,70)=YI(70)
	QUIT
	
]]></Routine>
</Export>