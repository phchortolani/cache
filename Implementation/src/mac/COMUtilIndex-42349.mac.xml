<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilIndex" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilIndex
#include COMSYS
 
Index(pstrVal="",pidClass="") 
	;-------------------------------------------------------------------------------
	; Convert Value to @NM Index
	;
	; Returns:
	;
	; History:
	; 03-Jan-2014	shobby	CORE-303: Modified Cache Index Option to obtain from class definition
	; 04-Dec-2013	SCR		SESDF-655: Use Cache Index option
	; 11-May-2011	shobby	SR17754: Reduced the number of global hits.
	; 08-May-2008	shobby	SRBR014941: Moved from COMUtils (Runs much faster in a small routine)
	; 06-Feb-2007	Steve S	SR15423: (Peer Review) Conform to coding standards
	; 02-Feb-2007	RPW		SR15423: Use 254 instead of 255 for subscript length.
	; 27-May-2005	RobertW	SR12056: Use $get instead of $data, we know the node we want.
	; 17-Feb-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new strIndex
	
	if pidClass'="" if $$$WWW001CacheIndex($get(^WWW001(0,pidClass,1))) quit $$CacheIndex(pstrVal)  ; Use cache index ;CORE-303
	
	quit:+pstrVal=pstrVal pstrVal
	
	if pstrVal="" set pstrVal = " "
	
	if $length(pstrVal)>254 set pstrVal = $extract(pstrVal,1,254)
	
	set strIndex = $get(^CacheTempIndex(pstrVal))
	if $get(strIndex)="" {
		set strIndex = $$^WWWUMLAU(pstrVal,1)
		set ^CacheTempIndex(pstrVal)=strIndex
	}
	quit strIndex

CacheIndex(pstrVal="") 
	;-------------------------------------------------------------------------------
	; Convert Value to Cache Index
	;
	; Returns: index
	;
	; History:
	; 03-Dec-2013	SCR		SESDF-655: Creates
	;-------------------------------------------------------------------------------
	new strIndex
	
	set strIndex	= $zu(28,pstrVal,5) 
	set:strIndex="" strIndex	= -1E14
	quit strIndex	
]]></Routine>
</Export>