<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDDispenseAlternate" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDDispenseAlternate
	quit

#include WWWConst
#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst

#define FORMFldMEDDispenseLineIssueUOM 	12
#define FORMFldMEDDispenseLineFrom		20
#define FORMFldMEDDispenseLineTo		21
#define FORMFldMEDDispenseLineDose		22
#define FORMFldMEDDispenseLineTotalRequested	26
#define FORMFldMEDDispenseLineSolution	29
#define YCR	"|"

CanAlternate(pYKEY)
	;-------------------------------------------------------------------------------
	; Check if the line can have alternate items
	;
	; Params:	pYKEY	- Dispense, Line Key
	;
	; Returns:	
	; 
	; History:
	; 11-Dec-2012	SCR		HEVA-704 Created
	;-------------------------------------------------------------------------------
	new strStatus,idDispense,idLine,objDisp,idStatus
	
	set idDispense=$$$KEY1(pYKEY)
	set idLine=$$$KEY2(pYKEY)
	set strStatus=$$$OK
	if '$$$NoKey(idDispense) {
		set objDisp = $get(^MEDDispense(YM,idDispense,1))
		set idStatus 	= +$$$MEDDispenseStatus(objDisp)
		if idStatus > 2 {
		 	set strStatus = $$$MakeStatus("MED01157")   //"Record already processed"
		}
		
	}


 	$$$YQHandler(strStatus)
	
	quit strStatus

Alternate(pidDispense)
	;-------------------------------------------------------------------------------
	; Alternate Button
	;
	; Params:	pidDispense	- Dispense
	;
	; Returns:	
	;
	; History:
	; 11-Dec-2012	SCR		HEVA-704 Created
    ;-------------------------------------------------------------------------------
	new blnInteraction,strStatus,idLine,objRxLine,objDisp,idRx,idItem,objItem,objLine
	set idLine 		= $$$KEY2($$$GRIDGetCurrentYKEY)					
	set strStatus	= $$$OK
	quit:pidDispense="" strStatus 	; Short cut exit
	set objDisp 	= $get(^MEDDispense(YM,pidDispense,1))
	set idRx 		= $$$MEDDispensePrescription(objDisp)
	quit:idRx="" strStatus 			; Short cut exit
	if idLine'="" {
		set objRxLine 	= $get(^MEDPrescriptionLine(YM,idRx,idLine,1))
		set idItem		= $$$MEDPrescriptionLineItem(objRxLine)
		set objLine		= $get(^MEDDispenseLine(YM,pidDispense,idLine,1))
		if $data(^INARTA(YM,idItem)) {
			if $$$MEDDispenseLineValidatedStatus(objLine) = "blank_flat.gif" {
				do Initialise^COMViewCustom("INARTA","ItemSelected^MEDDispenseAlternate",$$$YES)
				do AddFilter^COMViewCustom("P1",$$$EnumCOMVIEWCOMPARATOREquals,idItem)
				do Start^COMViewCustom()
			} else {
				$$$Alert("MED01408")  ;Can not change Item when it is Validated or Rejected
			}
		} else {
			set objItem = $get(^INART(YM,idItem,1))
			$$$Alert($listbuild("MED01405",$$$INARTSearchName(objItem)))  ;Item %1 does not have any alternitives
		}
	}
	
	quit

ItemSelected(pobjResultSet)
    ;-------------------------------------------------------------------------------
    ; Update Item to the grid.
    ;
    ; Returns:
    ;
    ; History:
	; 02-Jan-2013	SCR		HEVA-725 Update Unit & Qty
	; 19-Dec-2012	SCR		HEVA-725 Use Inpatient UOM and conversion
	; 11-Dec-2012	SCR		HEVA-704 Created
    ;-------------------------------------------------------------------------------
	new idKey,idLine,arrGrid,idItem,objLine,idDisp,idUnit,idIssueType,idRx, YKEY

 	$$$GRIDAddBatchStart
	while pobjResultSet.Next() {
		set idKey 	= $$GetID^COMViewCustom(pobjResultSet)
		set idItem	= $$$KEY2(idKey)
		set idDisp 	= $$$GRIDGetCurrentYKEY					
		set idLine 	= $$$KEY2(idDisp)					
		set idDisp	= $$$KEY1(idDisp)
	 	$$$GRIDGetContents(.arrGrid,"MEDDispenseLine")
	 	set objLine = $get(arrGrid(idDisp,idLine))
	 	set $$$MEDDispenseLineItemName(objLine)=idItem
		set objDisp 	= $get(^MEDDispense(0,idDisp,1))
	 	set idUnit		= $$$MEDDispenseLineIssueUOM(objLine)
		set idIssueType	= $$$MEDDispenseIssueType(objDisp)
		set idRx		= $$$MEDDispensePrescription(objDisp)
	 	if (idUnit'="") {
		 	if '$data(^INARTPACK(YM,idItem,idUnit)) {
			 	set idUnit	= ""
			 	set $$$MEDDispenseLineIssueUOM(objLine)=idUnit
		 	}
	 	}
	 	set objLine=$$UpdateDispenseLine^MEDDispenseCalc(idDisp,idLine,objLine) ; HEVA-725
	 	set arrGrid(idDisp,idLine) = objLine
		$$$GRIDSetContents(.arrGrid)
	 	
	}
	$$$GRIDAddBatchStop
	quit

AlternateUpdate(pidDispense)
	;-------------------------------------------------------------------------------
	; OnBeforeSave of the MEDDispenseLine
	; 
	; Params:	pidDispense	- Dispense
	;
	; Returns:	strStatus
	;
	; History :
	; 21-Dec-2012	SCR		HEVA-704: Chenged note prefix to - and remove @
	; 11-Dec-2012	SCR		HEVA-704: do not check when alternate product
	;-------------------------------------------------------------------------------
	new idLine,idRx,objRxLine,objLine,strStatus,strRemark,idItem,idRxItem,objDisp
	new strItem,strUser,strLnRemark
	set strStatus 	= $$$OK

	quit:pidDispense="" strStatus 	; Short cut exit

	set objDisp		= $get(^MEDDispense(YM,pidDispense,1))
	set idRx		= $$$MEDDispensePrescription(objDisp)

	quit:idRx="" strStatus 			; Short cut exit

	set idLine		= ""
	for {
		set idLine 		= $order(^MEDDispenseLine(YM,pidDispense,idLine))
		quit:idLine=""
		set objLine		= $get(^MEDDispenseLine(YM,pidDispense,idLine,1))
		set idItem		= $$$MEDDispenseLineItemName(objLine)
		set objRxLine	= $get(^MEDPrescriptionLine(YM,idRx,idLine,1))
		set idRxItem	= $$$MEDPrescriptionLineItem(objRxLine)
		continue:idItem=idRxItem
		set strLnRemark	= $$$MEDPrescriptionLineRemarks(objRxLine)
		continue:$piece(strLnRemark,("- ("_idRxItem_")"),$length(strLnRemark,("- ("_idRxItem_")")))[(" ("_idItem_")")
		set strUser 	= $$$WWW013Name($get(^WWW013(0,YBED,1)))
		set strRemark	= $$$MEDPrescriptionLineRemarks(objRxLine)
		set strItem 	= "("_idRxItem_") "_$$^INARTNAME(idRxItem)
		set strRemark 	= "- "_strItem_" "_$$$Text("MED01406")_$$$YCR  ;Changed to
		set strItem 	= "("_idItem_") "_$$^INARTNAME(idItem)
		set strRemark	= strRemark_" "_strItem
		set strRemark	= strRemark_" "_$get(strUser)_" "_$$$Text("293")_" "_$$^WWWDATE(+$h)_" "_$piece($$^WWWTIME($p($h,",",2)),":",1,2)_" "_$$$YCR
		//Update the Prescription Line				
		set $$$MEDPrescriptionLineRemarks(objRxLine) = $$$MEDPrescriptionLineRemarks(objRxLine)_$$$YCR _strRemark		
		set strStatus = $$$Save("MEDPrescriptionLine",idRx_$$$COMMA_(idLine),objRxLine,$$$YES)
	}
		
	quit strStatus	

]]></Routine>
</Export>