<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPHCAdjustmentsToBatch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPHCAdjustmentsToBatch

#include FINHC

Check(StartDate="",EndDate="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; All AP Posted Adjustments must update a Posted Interface Batch
    ; 
    ; Notes:
    ; 	Similar code in FINARHCAdjustmentsToBatch, check this routine when making changes.
    ;
    ; Returns:
    ;
    ; History:
    ; 28-May-2004	shobby		Handle keys in the index better.
    ; 26-May-2004	shobby		Changed to work off the third index of FININTBatchLines.
    ; 13-Apr-2004   Shobby   	Created (HCAP6)
    ;-------------------------------------------------------------------------------
	new strStatus,idFINAPADJ,idNumber,objFINAPADJ,idBatch,objFININTBatch,idxFININTBatchLineNumber
	new idLine,objFININTBatchLine,strBlock,strIndex
	
	kill Result
	if $get(Y)="" do ^WWWVAR
	set strStatus=$$$OK

	set idFINAPADJ=""
	for {
		set idFINAPADJ=$order(^FINAPADJ(YM,idFINAPADJ))
		quit:idFINAPADJ=""
		set objFINAPADJ=$get(^FINAPADJ(YM,idFINAPADJ,1))
		if $$$FINAPADJPostedDate(objFINAPADJ)'="" {
			set idNumber=""
			for {
				set idNumber=$order(^FINAPADJLine(YM,idFINAPADJ,idNumber))
				quit:idNumber=""
				set strIndex=$$Index^FINSYS(idFINAPADJ_YKOMMA_idNumber)
				if $data(^FININTBatchLines(YM,3,"APADJ",strIndex))=0 {
					set Result("Hash",$$$ErrorNo(Result("Errors")))=$$$Text($lb("Fin00337",idFINAPADJ,idNumber)) ;Adjustment ´%1,%2´ does not have an Interface Batch.
					set strStatus='$$$OK
				} else {
					set idBatch=$order(^FININTBatchLines(YM,3,"APADJ",strIndex,""))
					set objFININTBatch=$get(^FININTBatch(YM,idBatch,1))
					if $$$FININTBatchPostedDate(objFININTBatch)="" {
						set idLine=""
						for {
							set idLine=$order(^FININTBatchLines(YM,3,"APADJ",strIndex,idBatch,idLine))
							quit:idLine=""
							set objFININTBatchLine=$get(^FININTBatchLine(YM,idBatch,idLine,1))
							set strBlock=$$$FININTBatchLineBlockNumber(objFININTBatchLine)
							set Result("Hash",$$$ErrorNo(Result("Errors")))=$$$Text($lb("Fin00338",idFINAPADJ,idNumber,idBatch,strBlock))   ;Adjustment:%1,%2.  Interface Batch(%3,%4) has not been posted.
							set strStatus='$$$OK
						}
					}
				}
			}
		}
	}

	if strStatus=$$$OK {
		set strStatus=2  ; Because this is a Standalone Check return 2 if Ok
	}
	quit strStatus

]]></Routine>
</Export>