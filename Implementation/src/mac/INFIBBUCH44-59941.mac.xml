<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INFIBBUCH44" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INFIBBUCH44
	
#include COMSYS
#include INConst
	
	;-------------------------------------------------------------------------------
	; History:
	; 07-Apr-2005	GRF		SR10478 : Break routine into more manageable chunks
	;-------------------------------------------------------------------------------
	
	;------------------------------------------------------
	; DTD Element Structure : INProduction
	;
	; Company()
	; Date()
	; Lines()
	; 	CharVal(CST Characteristic Values)    
	;	LineAppliedAccount()
	;	LineAppliedAmount()
	;	LineRef(Line Reference (Stock Number))
	;	LineWIPAccount()
	; 	OrderLine(Order Line)    
	; Location(Location)    
	; Order(Customers Order number)    
	; OrderType(CST Order Type)
	; SourceGroup()
	; SourceLocn(CST Transaction Type)
	; TranType
	;------------------------------------------------------
		
	; from INPROBDEAUF
	; 
	; SET BUCHUNG=""
	; SET $PIECE(BUCHUNG,Y,2)=+$HOROLOG      ; DATE
	; SET $PIECE(BUCHUNG,Y,16)=BET           ; LOCATION
	; SET $PIECE(BUCHUNG,Y,27)=AUF           ; ORDER
	; SET $PIECE(BUCHUNG,Y,28)=POS           ; ORDER LINE
	; SET $PIECE(BUCHUNG,Y,29)=ART           ; ITEM
	; SET $PIECE(BUCHUNG,Y,32)=DAUER         ;(QTY) PERIOD
	
	;    TRANSACTION FOR 1: SERVICE AND 2: SERVICE OVERHEAD
	; FOR TYPE="LBR","BDN" DO
	; 	.SET $PIECE(BUCHUNG,Y,50)=TYPE        ;TRANSACTION TYPE
	; 	.IF TYPE="LBR" DO                     ;SERVICE COSTS
	; 		..IF +STDSATZ=0 SET $PIECE(BUCHUNG,Y,3)=KOST QUIT
	; 		..SET $PIECE(BUCHUNG,Y,3)=$JUSTIFY(KOST-((KOST/STDSATZ)*GEMEINK),0,2)
	; 	.IF TYPE="BDN" DO                     ;SERVICE OVERHEAD COSTS
	; 		..IF +STDSATZ=0 SET $PIECE(BUCHUNG,Y,3)=0 QUIT
	; 		..SET $PIECE(BUCHUNG,Y,3)=$JUSTIFY(((KOST/STDSATZ)*GEMEINK),0,2)
	; 	.SET OK=$$^INFIBBUCH(BUCHUNG,YLOCATION,"","",4)   ;SERVICE COSTS	
	
PRODUCTION(BUCHUNG,YFPARA)
	;-------------------------------------------------------------------------------
	; SERVICE COSTS OF PRODUCTION
	; 
	; History:
	; 11-Aug-2006	JW		SR14911 : Pass POS num to CopyCharVals
	; 24-Aug-2005	GRF		SR13191 : Transmit Parent Item
	; 17-Aug-2005	GRF		SR13215 : Include Quantity Reference for ease of checking
	; 30-Jun-2005	GRF		SR12843 : Use SOH account for OCR
	; 21-Jun-2005	GRF		SR10478 : Keep MAN for EXCHANGE^INFIBBUCH; pass YM
	; 02-Jun-2005	GRF		COM Routines will exist
	; 26-Apr-2005	GRF		SR10478 : FINBTR not defined; LBR not LAB
	; 11-Apr-2005	GRF		SR10478 : Validate WebService available BEFORE further
	; 							processing.
	; 16-Mar-2005	GRF		SR10478 : Preserve original location and item group for
	; 							post cost-matrix profile resolution
	; 03-Feb-2005	GRF		SR10478 : Added characteristic values; Common ITEM defn
	;-------------------------------------------------------------------------------
	new AcctNo,BTR,FINBTR,ITEM,MAN,OK,POS,TRANTYPE,WAGR
	
	set TRANTYPE = $piece(BUCHUNG,Y,50)    ; LBR, BDN, MBD?
	quit:TRANTYPE="COS"					; FIXME <GRF> 26-Apr-2005 INDRUCK71/INDRUCK72
	
	set MAN = YM
	set BTR = $piece(BUCHUNG,Y,16)
	if BTR="" set BTR = YLOCATION
	set FINBTR = BTR
	
	if $piece(YFPARA,Y,2)'="" set MAN    = $piece(YFPARA,Y,2)  ;Company in GL
	if $piece(YFPARA,Y,3)'="" set FINBTR = $piece(YFPARA,Y,3)  ;Location in GL 
	
	set ITEM=$piece(BUCHUNG,Y,29)
	set WAGR=""
	if ITEM'="" set WAGR = $piece($get(^INART(0,ITEM,1)),Y,30)
	if WAGR=""  set WAGR = $piece($get(^INVORG(0,0,1)),Y,13)  ;DFLT.
	
	set POS = $piece(BUCHUNG,Y,28)
	if +POS=0 set POS = 1
	
	;SERVICE COSTS
	set ^mtemp($job,"INProduction","Date")        = $piece(BUCHUNG,Y,2)    ; DATE
	set ^mtemp($job,"INProduction","Company")     = 0                      ; COMPANY
	set ^mtemp($job,"INProduction","Location")    = BTR                    ; Location IN GL
	set ^mtemp($job,"INProduction","TranType")    = TRANTYPE
	set ^mtemp($job,"INProduction","OrderType")   = 1                      ; Manufacturing Order
	set ^mtemp($job,"INProduction","Order")       = $piece(BUCHUNG,Y,27)   ; Order No
	
	set ^mtemp($job,"INProduction","Lines",POS,"Amount")      = $piece(BUCHUNG,Y,3)    ;total amount
	set ^mtemp($job,"INProduction","Lines",POS,"WIPAccount")  = $$EXCHANGE^INFIBBUCH($piece(YFPARA,Y,53),FINBTR,WAGR)
	set ^mtemp($job,"INProduction","Lines",POS,"Reference")   = ITEM                   ;item no
	set ^mtemp($job,"INProduction","Lines",POS,"OrderLine")   = POS                    ;order line
	set ^mtemp($job,"INProduction","Lines",POS,"SourceGroup") = WAGR                   ;item group
	set ^mtemp($job,"INProduction","Lines",POS,"QRef")        = $piece(BUCHUNG,Y,32)   ;quantity reference ;SR13215
	set ^mtemp($job,"INProduction","Lines",POS,"Parent")      = $piece(BUCHUNG,Y,42)   ;parent item reference ;SR13191
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ 
	;  The YFPARA accts were set up as what were believed to be the minumum
	;  necessary to provide default accounts if the Account ID Matrix wasn't
	;  used.  (D# refers to YFPARA record)
	;  
	;  The postings for ISS, LAB, BDN and MBD are;
	;  
	;  	 Dr WIP				(only one default defined - use matrix to
	;  						 set more - based on Finished Goods)  D53
	;  						
	;    Cr *** Recovered	(Inventory-RM, Labour, Service O/H, Mats O/H)
	;    					(based on Component - identify cost centre for
	;    					 expense items)           D31,D50,D51,D52
	;  	
	;  For OCR they are ;
	;  
	;  	 Dr Inventory (Finished Goods)	   D31
	;  	 
	;  	 Cr WIP				(as above)     D53
	;         
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ 
	
	if TRANTYPE="LBR" {			set AcctNo = $$EXCHANGE^INFIBBUCH($piece(YFPARA,Y,50),FINBTR,WAGR)  ;Labour Applied Acct
	} elseif TRANTYPE="BDN" {	set AcctNo = $$EXCHANGE^INFIBBUCH($piece(YFPARA,Y,51),FINBTR,WAGR)  ;Service Overhead Applied Acct
	} elseif TRANTYPE="MBD" {	set AcctNo = $$EXCHANGE^INFIBBUCH($piece(YFPARA,Y,52),FINBTR,WAGR)  ;Material Overhead Applied Acct
	} elseif TRANTYPE="OCR" {	set AcctNo = $$EXCHANGE^INFIBBUCH($piece(YFPARA,Y,31),FINBTR,WAGR)  ;SOH Acct - Temporary
		
		; FIXME : "OCR" could come from any of the WIP accts - could go to SOH/COGS/Variance - closer to "REC"
		;         This requires knowledge of stock levels - for now treat as all SOH still there with adjustment
		;         journal if required. <GRF>  NOTE : Review carefully before trusting this statement
		 
	} else {
		; TODO : handle unrecognised codes
		set AcctNo = ""
	}
	set ^mtemp($job,"INProduction","Lines",POS,"AppliedAccount") = AcctNo
	
	do CopyCharVals^INFIBBUCH4("INProduction",BUCHUNG,POS)
	
	set OK = $$setRelevantEvents^COMDCMControlModule("DiscLinc","INProduction","other","","")
	quit
	
]]></Routine>
</Export>