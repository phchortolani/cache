<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPregao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPregao
#include COMSYS
#include COMConst
#include VARConst
#include INConst

OnBeforeButtonLine(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o formulário está somente leitura ou não.
    ;
    ; History:
    ; 07-Mar-2013	Marcelo Ottoni	Created
    ;-------------------------------------------------------------------------------	
 	/*
 	quit:(pYKEY = "")
 	
 	new objPregao
 	set objPregao = $get(^VARAta(YM,pYKEY,1))
 	
 	set ^CacheTempEvent(YUCI,"VARPregaoLinha","Format") = 1
 	
	if ($$$VARAtaStatus(objPregao) > 0) {
		set $piece(YVOR,Y,23) = 5
	}
	*/
	quit

OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Realiza verificações antes de salvar a ata.
    ;
    ; History:
    ; 07-Mar-2013	Marcelo Ottoni	Created
    ;-------------------------------------------------------------------------------
	new strStatus, dteAbertura, dteEncerramento, strProcesso, found, idSRP

	set idSRP = $$$VARPregaoNumeroSRP(pYFELD)
	if (YLOCATION '= $$$VARSESSOLICPRECOSetorSolicitante($get(^VARSESSOLICPRECO(YM,idSRP,1)))){
		set strStatus = "O cadastro desta SRP não corresponde a este local"	
		$$$Alert(strStatus)
		set Q=$$$QDontSave
		quit
	}

	set strStatus = $$$OK
	set strStatus = $$$GRIDSave(pYKEY)		
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
	
OnBeforeCanDelete(pYKEY)	
	;Se este pregão foi associado a alguma ata, não deve ser possível excluí-lo.
	if $data(^VARAtas(YM,5,$$$Index(YKEY))) {	
		set Q = $$$QDontDelete 
	}

	quit	
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
	; TO DO: Carregar tabela com os dados das Atas relacionadas 
    ; 
    ; History:
    ; 07-Mar-2013	Marcelo Ottoni	Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	if (YSEITE = 1) {
		do LoadGrid(pYKEY)
		do LoadTableAtas(pYKEY)
	}
	
	quit	

LoadGrid(pYKEY="") 
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARPregao.mac.
    ; 
    ; Histórico:
    ; 	07-Mar-2013	Marcelo Ottoni	Created
    ;-------------------------------------------------------------------------------
	new idForm
	
	set idForm = "VARPregaoLinha"
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 300
	set $$$COMGridEditParameterGridName(YAUSWAHL)	   = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)	   = '($$$WWW120AuthorizationToModifyData(YVOR)=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)	   = "VARPregao"

	$$$GRIDStart(idForm,pYKEY)
	
	quit


OnAfterSave(pYKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Adiciona os produtos da SRP (quando o grid está vazio).
	;
   	; Chamado por:
   	;	Campo OnAfterSave do form VARPregao.
    ;
    ; History:
    ; 	19-Mar-2013		Criado
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new strStatus, idSRP	
	
	set strStatus = $$$OK	
	set idSRP 	  = $$$VARPregaoNumeroSRP(YFELD)

	//Adiciona os produtos da SRP (somente quando o grid está vazio e quando a SRP foi preenchida pelo usuário)
	if ( '$data(^VARPregaoLinha(YM,pYKEY)) && (idSRP '= "") ) {
			set strStatus = $$InserirItensSRP(pYKEY,idSRP)
	}
	
	if $$$ISERR(strStatus) { 
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
	
	
InserirItensSRP(pidPregao,pidSRP)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Insere os itens da SRP no pregão.
    ;
    ; Chamado por:
    ;	Função OnAfterSave^VARPregao.mac.
    ; 
    ; Histórico:
    ; 	19-Mar-2013		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidPregao = "") "Nenhum pregão informado."
	quit:(pidSRP = "") "Nenhuma SRP informada."
	
	new strStatus, objSRPLinha, idPregaoLinha, idProduto, unidade, quantidade,
		objPregaoLinha, keyPregaoLinha
	
	set strStatus 	   = $$$OK	
	set idPregaoLinha  = 0

	$$$Order3(^VARSESSOLICPRECOLinha,YM,pidSRP,idSRPLinha)
	
		set objSRPLinha = $get(^VARSESSOLICPRECOLinha(YM,pidSRP,idSRPLinha,1))		

		//Preparando a linha do Pregão
		set idPregaoLinha = $increment(idPregaoLinha)
		
		//Dados da SRP
		set idProduto  = $$$VARSESSOLICPRECOLinhaCdigoDescrio(objSRPLinha)
		set unidade    = $$$VARSESSOLICPRECOLinhaUnidadedeEstoque(objSRPLinha)
		set quantidade = $$$VARSESSOLICPRECOLinhaQuant(objSRPLinha)
		
		set objPregaoLinha = ""
		set $$$VARPregaoLinhaProduto(objPregaoLinha)     	  = idProduto
		set $$$VARPregaoLinhaQuantidade(objPregaoLinha)  	  = quantidade
		set $$$VARPregaoLinhaUnidade(objPregaoLinha)	 	  = unidade
		set $$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha) = 0			;0 - Aguardando Aceitação
		set $$$VARPregaoLinhaSRP(objPregaoLinha) 		 	  = pidSRP
		set $$$VARPregaoLinhaItemdaSRP(objPregaoLinha)	 	  = idSRPLinha
		
		set keyPregaoLinha = pidPregao_$$$COMMA_idPregaoLinha
		set strStatus = $$$Save("VARPregaoLinha",keyPregaoLinha,objPregaoLinha,$$$YES)
		
		quit:$$$ISERR(strStatus) strStatus
	
	$$$End
	
	quit strStatus


OnBeforeDataAccessCallback(YFORM,YKEY)
	;-------------------------------------------------------------------------------
    ; Filtra os dados da classe. *NÃO* foi implementado como um Hook, mas como um 
    ; evento da definição da classe na aba "Callback (Retorno)".
    ; 
    ; History:
    ; 22-Mar-2013	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	new blnDataAccess, objPregao
	set blnDataAccess = $$$YES
	
	if YBED '= "GUSTAVO" quit 1
	
	//Filtro é aplicado SOMENTE na consulta de Pregão da tela de Ata
	if (YFORM = "VARAta") {
		set objPregao = $get(^VARPregao(YM,YKEY,1))
		
		;Não mostra pregões que já foram inseridas em outros pregões
		if $data(^VARAtas(YM,5,$$$Index(YKEY))) {
			set blnDataAccess = $$$NO
		}
	}
    
    quit blnDataAccess


LoadTableAtas(pidPregao)
	quit:($$$NoKey(pidPregao))

	new lstHeader, objAta, status
	
	if $data(^VARAtas(YM,5,$$$Index(pidPregao))) {

	 	set lstHeader = ""
	  	set lstHeader = lstHeader_$listbuild("No. Ata","Status","Criado em")
	  	
	  	if $$Start^COMTable(lstHeader,"Ata associada a este Pregão",,,$$$NO) {
	
			$$$Order4(^VARAtas,YM,5,$$$Index(pidPregao),idAta)
				
				set objAta   = $get(^VARAta(YM,idAta,1))
				
				set status = $$$VARAtaStatus(objAta)
				set criadoEm = $$$VARAtaCriadoEm(objAta)
				
				do NewLine^COMTable()
				do InsertCell^COMTable(idAta,"VARAta",idAta)
				do InsertCell^COMTable($$SQLGetUserParamDescVAR^VARSQL("STATUSATA~"_status),"VARAta",idAta)
				do InsertCell^COMTable($zdate(criadoEm,4),"VARAta",idAta)
				do EndLine^COMTable()
		
			$$$End
			
			do Stop^COMTable()
	  	}
		
	}
	
	quit
	
	
OnBlurSRP(YKEY,&YFELD,YINHALT)
	new idSRP, objSRP, numProcesso
	
	set idSRP = YINHALT
	
	if ($$checkSRPVinculado^VARSESSOLICPRECO(idSRP) '= 0){
		$$$JS("alert('O documento "_idSRP_" já está vinculado à um pregão.');")
		set %TXT(1)=%TXT(1)_"#YVARPregaoD1~REMOVE"	
		quit	
	}
	
	if (idSRP '= "") {		
		set objSRP = $get(^VARSESSOLICPRECO(YM,idSRP,1))
		set numProcesso = $$$VARSESSOLICPRECONumerodoProcesso(objSRP)		
		set %TXT(1) = "#YVARPregaoD2~"_numProcesso		
	
	} else {
		set %TXT(1)=%TXT(1)_"#YVARPregaoD2~REMOVE"	
	}

	quit
	 
checkPregaoVinculado(pPregao)
	quit:(pPregao = "") 0
	new countPregaoVinculado
	
	&sql(SELECT COUNT(*)
	INTO :countPregaoVinculado
	FROM SQLUser.VARAta
	WHERE NumeroEdital = :pPregao)
	
	if (SQLCODE) quit 0
	
	quit countPregaoVinculado
	
getPregaoBySRP(pNumSRP)
	quit:(pNumSRP = "") ""
	quit $order(^VARPregaos(0, 1, $$$Index(pNumSRP), ""))
]]></Routine>
</Export>