<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMTab" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMTab
	;-------------------------------------------------------------------------------
	;  Tab headings on specially prepared tables in forms
	;-------------------------------------------------------------------------------
#include COMSYS
#include WWWConst
#define TabsPerRow 7
 
ShowTable(plstTabs,plngCurrentTab)
	;-------------------------------------------------------------------------------
	; Show Error for Matrix Default
	; Rules for Use:
	; 	plstTabs Is a list of lists. Each Tab Element Should be:
	; 		$lb(DesriptionShownOnTab,KeyToJumpTo,OptionalYPARA,ToolTip)
	; 		
	; 		. DesriptionShownOnTab: the text that will be shown as the tab title
	; 		. KeyToJumpTo: the YKEY value to be put in the link of the tab title
	; 		. OptionalYPARA: the YPARA value to be put in the link of the tab title
	; 		. ToolTip: the tool tip to appear when the user stops the mouse over the tab title
	; 		
	; 	plngCurrentTab should always be passed in with a value.
	; 	
	; 	the tab border colors are programmed to work with a gray background
	; 	
	; History
	; 29-Aug-2005	shobby	SR13313: Copied in changes from Brazil as per email.
	; 15-Dec-2004	GRF		SR10478 : Alignment of braces
	; 14-Aug-2003	Paul K	Added Multiple Rows if NumTabs Too Large to fit on one
	; 						row.
	; 30-May-2003	Paul K	Added &nbsp before startting tab to add a bit of space,
	; 						and allow different YPARA variable.
	;-------------------------------------------------------------------------------
	new HightLightRow,HightLightTab,ImageDirectory,lngCurrentTab,lngNumTabs,lngTabsForRow
	new loop,Row,RowData
	
	;find the row that contains the current Tab. It goes at the bottom
	set HightLightRow  = plngCurrentTab-1\$$$TabsPerRow+1
	set HightLightTab  = plngCurrentTab-1#$$$TabsPerRow+1
	set ImageDirectory = $translate($$$WWW012PictureDirectorySystem($get(^WWW012(0,YM,1))),"\","/")
	
	write YCR //,"&nbsp"
	
	; package the tabs into rows
	set lngNumTabs=$listlength(plstTabs)
	for loop=1:1:lngNumTabs {
		set Row=loop-1\$$$TabsPerRow+1
		if HightLightRow=Row set Row=0
		set RowData(Row)=$get(RowData(Row))_$lb($lg(plstTabs,loop))
	}
	
	;loop through the Rows going backwards (the 0 row is the highlighted one)
	set Row=""
	for {
		set Row=$order(RowData(Row),-1)
		quit:Row=""
		
		write YCR,"<TABLE vspace=0 border=0 frame=void cellpadding=0 cellspacing=0 BGCOLOR=lightgrey>"
		set lngTabsForRow=$listlength(RowData(Row))
 
		for lngCurrentTab=1:1:lngTabsForRow {
			write YCR,"<TD WIDTH=98 height=19 align=center NOWRAP background="""_ImageDirectory_"/reitertab.gif"">"
			write YCR,"<FONT SIZE=2 color=black>"
			if (0'=Row) || (lngCurrentTab'=HightLightTab) {
				new YPARA,YKEY
				write "<A "
				// prints the tooltip, if any
				if ($listget($listget(RowData(Row), lngCurrentTab), 4) '= "") {
					write " title=""" _ $listget($listget(RowData(Row), lngCurrentTab), 4) _ """"
				}
				write " HREF="_""""_YAKTION_"EP=WWWFORM"
				if $listget($listget(RowData(Row),lngCurrentTab),3)'="" {
					write "&amp;YPARA="_$listget($listget(RowData(Row),lngCurrentTab),3)
				} else {
					write "&amp;YPARA="_$listget($listget(RowData(Row),lngCurrentTab),2)
				}
				write "&amp;YKEY="_$listget($listget(RowData(Row),lngCurrentTab),2)
				write "&amp;YFORM="_YFORM
				do ^WWWCGI
				write """"_">"
				write $listget($listget(RowData(Row),lngCurrentTab),1)
				write "</A>"
			} else {
				write "<B "
				// prints the tooltip, if any
				if ($listget($listget(RowData(Row), lngCurrentTab), 4) '= "") {
					write " title=""" _ $listget($listget(RowData(Row), lngCurrentTab), 4) _ """"
				}
				write " >"
				write $listget($listget(RowData(Row),lngCurrentTab),1)
				write "</B>"
			}
			write YCR,"</TD>"
		}
		
		write YCR,"</TR>"
		; put a line underneath the tab to make it look like a box.
		; highlight the current selected tab
		write YCR,"<TR>"
		for lngCurrentTab=1:1:lngTabsForRow {
			write YCR,"<TD bgcolor="
			if (0=Row) && (lngCurrentTab=HightLightTab) {
				write "lightgrey>"
			} else {
				write "white>"
			}
			write YCR,"</TD>"
		}
		write YCR,"</TR>"
		write YCR,"</TABLE>"
	}
	quit
	
]]></Routine>
</Export>