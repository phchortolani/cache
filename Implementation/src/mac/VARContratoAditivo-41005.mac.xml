<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContratoAditivo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContratoAditivo
#include COMSYS
 
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Verifica se o contrato admite um aditivo de prorrogação.
    ;
    ; History:
    ; 13-Set-2009	Gustavo Fiuza	Changed
    ; 31-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new strStatus, CodContrato, objContrato, statusContrato, isProrrogavel, NoAditivo, tipoAditivo,
		intDuracaoContrato, intDuracaoAditivo, tipoDuracao, intDuracaoTotal, tipoContrato
		
	set strStatus = $$$OK
	set strStatus = $$$GRIDSave($$$KEY1(pYKEY))
 
	set CodContrato  = $$$KEY1(pYKEY)
	set NoAditivo   = $$$KEY2(pYKEY)
	set objContrato = $get(^VARContrato(YM,CodContrato,1))
	
	set statusContrato = $piece(objContrato,Y,1)
	set tipoContrato   = $piece(objContrato,Y,2)
 
	set intDuracaoAditivo = $piece(pYFELD,Y,3)
	set tipoDuracao		  = $piece(pYFELD,Y,12)
 
	if (statusContrato '= 1) {
		set strStatus = "Só é possível inserir alterações em contratos que estejam celebrados."
	}
	else {
		set isProrrogavel = $piece(objContrato,Y,5)
		set tipoAditivo   = $piece(pYFELD,Y,2)
		
		if (((tipoAditivo = 2) || (tipoAditivo = 4)) && (isProrrogavel '= 1)) {
			if ((intDuracaoAditivo '= "") || (tipoDuracao '= "")) {
				set strStatus = "Não é possível adicionar uma prorrogação no aditivo porque o contrato não é prorrogável. Favor verificar."
			}
		}
	}
		
	//Calcula a duração total do contrato (c/ aditivos) para saber se ainda pode ser prorrogado
	set intDuracaoContrato = $$GetDuracaoContrato^VARContrato(CodContrato)
		
	if tipoDuracao = 2 set intDuracaoAditivo = intDuracaoAditivo * 30
	if tipoDuracao = 3 set intDuracaoAditivo = intDuracaoAditivo * 365
	
	set intDuracaoTotal = intDuracaoContrato + intDuracaoAditivo
	
	if ((intDuracaoTotal > 2190) && (tipoContrato = 1)) set strStatus = "Não é possível prorrogar este contrato com a duração informada. O limite máximo é de 72 meses."
		
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Carrega a tabela de aditivos para o contrato
    ;
    ; History:
    ; 31-Aug-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:((pYKEY = "") || (pYKEY = "+"))	
	
	new CodContrato, isProcessoPrincipal, count, tipoAditivo, lstHeader, objAditivo, objeto, intDuracao,
		strTipoAditivo, isProcessado, strProcessado, tipoAlteracao, strTipoAlteracao, NoAlteracao,
		strNoAlteracao, tipoDuracao
		
	set CodContrato = $$$KEY1(pYKEY)
	quit:(CodContrato = "")
	
	set count 	  = 0
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("","No. Alteração","Tipo de Alteração","Tipo de Aditivo","Objeto Alteração","Data de Publicação")
 
  	if $$Start^COMTable(lstHeader,"&nbsp;Alterações do Contrato",,,$$$NO) {
	
		$$$Order3(^VARContratoAditivo,YM,CodContrato,Aditivo)
			
			set objAditivo  = $get(^VARContratoAditivo(YM,CodContrato,Aditivo,1))
			
			set isProcessado = $piece(objAditivo,Y,13)
			if isProcessado = 1 set strProcessado = "<font color=blue>Processado</font>"
			if isProcessado '= 1 set strProcessado = "<font color=red>Não Processado</font>"
			
			set objeto        = $piece(objAditivo,Y,1)
			set tipoAditivo   = $piece(objAditivo,Y,2)
			set tipoAlteracao = $piece(objAditivo,Y,14)
			set NoAlteracao   = $piece(objAditivo,Y,18)
			
			set strTipoAditivo	 = $$GetTipoAditivo(tipoAditivo)
			set strTipoAlteracao = $$GetTipoAlteracao(tipoAlteracao)
			
			if ((tipoAditivo = 2) || (tipoAditivo = 4)) {
				set intDuracao  = $piece(objAditivo,Y,3)
				set tipoDuracao = $piece(objAditivo,Y,12)
				
				if tipoDuracao = "" set tipoDuracao = 1
				if ((intDuracao '= "") && (tipoDuracao '= "")) {
					set strTipoAditivo = strTipoAditivo_" - Prorrogado por mais "_$piece(objAditivo,Y,3)_" "_$piece($get(^VARPARA(YM,"PERIODO","PT",tipoDuracao,1)),Y,1)
				}
			}
			
			set dataDOUDF = $piece(objAditivo,Y,6)
			if dataDOUDF '= "" set dataDOUDF = $zdate(dataDOUDF,4)
			
			set count = $i(count)
			set chave = CodContrato_","_Aditivo
			
			set strNoAlteracao = ""
			if NoAlteracao '= "" set strNoAlteracao = $piece($get(^VARPARA(YM,"CONTRATOALTNO",SPRACHE,NoAlteracao,1)),Y,1)
			
	 		do NewLine^COMTable()
	 		do InsertCell^COMTable(strProcessado,"VARContratoAditivo",chave)
	 		do InsertCell^COMTable(strNoAlteracao,"VARContratoAditivo",chave,,"CENTER")
	 		do InsertCell^COMTable(strTipoAlteracao,"VARContratoAditivo",chave)
	 		do InsertCell^COMTable(strTipoAditivo,"VARContratoAditivo",chave,,,,,,,1)
	 		do InsertCell^COMTable(objeto,"VARContratoAditivo",chave,,,,,,,1)
	 		do InsertCell^COMTable(dataDOUDF,"VARContratoAditivo",chave)
	 		do EndLine^COMTable()
 
		$$$End
		
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhuma alteração associada a este contrato.","VARContratoAditivo",CodContrato,,,,6)
			do EndLine^COMTable()
		}
		do Stop^COMTable()
  	}
	
	quit
	
	
OnAfterPrimaryKey(pYKEY)
	quit:($$$KEY2(pYKEY) = "")
	
	if (YSEITE = 3) { //Aba Manutenção
		
		set ^VARTempContratoAditivo(YBED) = pYKEY
		w "<font size=2>"
		w "		<br /><strong>Equipamentos de manutenção associados a este contrato</strong>"
		w "</font>"
		do LoadGridManutencao^VARContrato($$$KEY1(pYKEY),"VARContratoAditivo")
	}
	
	elseif (YSEITE = 4) { //Aba Veículos
		
		set ^VARTempContratoAditivo(YBED) = pYKEY
		w "<font size=2>"
		w "		<br /><strong>Veículos associados a este contrato</strong>"
		w "</font>"
		do LoadGridVeiculo^VARContrato($$$KEY1(pYKEY),"VARContratoAditivo")
	}	
	
	quit	
 
GetTipoAditivo(pidAditivo)
	if pidAditivo = "" quit ""
	quit $piece($get(^VARPARA(YM,"CONTRATOTIPOADITIVO","PT",pidAditivo,1)),Y,1)
 
GetTipoAlteracao(pidAlteracao)
	if pidAlteracao = "" quit ""
	quit $piece($get(^VARPARA(YM,"CONTRATOALTERACAO","PT",pidAlteracao,1)),Y,1)
	
GetStatusAditivo(pidContrato,pidAlteracao)
	if pidAlteracao = "" quit ""
	quit $piece($get(^VARContratoAditivo(YM,pidContrato,pidAlteracao,1)),Y,13)
 
	
CanProcessar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o status da alteração/aditivo admite ser processada
    ;
    ; History:
    ; 17-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:($$$KEY2(pYKEY) = "")
 	
 	new idContrato, idAditivo, status
 
	set idContrato = $$$KEY1(pYKEY)
	set idAditivo  = $$$KEY2(pYKEY)
	quit:(idAditivo = "")
	
	set status = $piece($get(^VARContratoAditivo(YM,idContrato,idAditivo,1)),Y,13)
	
	if (status = 1) {
		set YQ = $$$YQDisable("Essa alteração/aditivo já está processada.")
	
	} else {
		set YQ = $$$YQEnable
	}
	
	quit
	
Processar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Seta o status da alteração/aditivo para 1 - Processado.
    ;
    ; History:
    ; 23-Nov-2009	Gustavo Fiuza	Updated		Limpa a confirmação da notificação.
    ; 17-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:($$$KEY2(pYKEY) = "")
	
	new strStatus, idContrato, idAditivo, objAditivo, objContrato, intDuracaoAditivo, tipoDuracao
	set strStatus  = $$$OK
	
	set objAditivo = $get(^VARContratoAditivo(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))			
	
	if $piece(objAditivo,Y,13) '= 1 {
	
		//Atualiza a Alteração para o Status 1
		set $piece(objAditivo,Y,13) = 1
		set strStatus = $$$Save("VARContratoAditivo",pYKEY,objAditivo,1)
		
		//Limpa a confirmação de notificação, visto que foi extendida e terá nova data (caso seja prorrogação)
		if $$$ISOK(strStatus) {
			
			set intDuracaoAditivo = $piece(objAditivo,Y,3)
			set tipoDuracao		  = $piece(objAditivo,Y,12)
			 
			if ((intDuracaoAditivo '= "") && (tipoDuracao '= "")) {			 
				set objContrato = $get(^VARContrato(YM,$$$KEY1(pYKEY),1))		
				set $piece(objContrato,Y,30) = ""  ;Notificação Confirmada Em
				set $piece(objContrato,Y,31) = ""  ;Notificação Confirmada Por
 
				set strStatus = $$$Save("VARContrato",$$$KEY1(pYKEY),objContrato,1)
			}
		}
	
		//Verifica erros
		if $$$ISERR(strStatus) { 
			$$$StartScript()
			w "alert('Não foi possível processar a alteração/aditivo. Favor verificar.');"
			$$$EndScript()
		}
		
		do GoToForm^COMUtilForm("VARContratoAditivo",pYKEY)	
	}
		
	quit
	
	
CanRevisar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o aditivo pode ser revisado.
    ;
    ; History:
    ; 17-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	if ($$$KEY2(pYKEY) = "") {
		set YQ = $$$YQDisable("Nenhuma alteração selecionada/criada.")
		quit
	}
	
	quit:($$$KEY2(pYKEY) = "")
	 	
 	new status
	set status = $piece($get(^VARContratoAditivo(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1)),Y,13)
	
	if (status = "") {
		set YQ = $$$YQDisable("Essa alteração/aditivo já está em modo de edição.")	
	} else { 
		set YQ = $$$YQEnable
	}
	
	quit
	
RevisarAditivo(pYKEY)
	;-------------------------------------------------------------------------------
    ; Seta o status da alteração/aditivo para vazio (não firmado)
    ;
    ; History:
    ; 17-Sep-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:($$$KEY2(pYKEY) = "")
	
	new strStatus, objAditivo
	
	set strStatus = $$$OK	
	set objAditivo = $get(^VARContratoAditivo(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))			
	
	if $piece(objAditivo,Y,13) = 1 {
	
		set $piece(objAditivo,Y,13) = ""
		
		set strStatus = $$$Save("VARContratoAditivo",pYKEY,objAditivo,1)
	
		if $$$ISERR(strStatus) { 
			$$$StartScript()
			w "alert('Não é possível revisar a alteração/aditivo. Favor verificar.');"
			$$$EndScript()
		}	
		
		do GoToForm^COMUtilForm("VARContratoAditivo",pYKEY)	
	}
		
	quit
	
	
HideAbaManutencao(pYKEY)
	;-------------------------------------------------------------------------------
    ; Esconde a aba "Manutenção" caso:
    ; 	1. Categoria do contrato é diferente de 6 - Serviços de manutenção de equipamentos
    ; 	2. O tipo de alteração/aditivo não é 1 - Termo Aditivo
    ; 	3. O tipo de aditivo é 2 - Prorrogação
    ;
    ;
    ; History:
    ; 18-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if ($$$KEY2(pYKEY) = "") quit $$$YES
		
	new TipoContrato, Categoria, objAditivo, tipoAlteracao, tipoAditivo
 
	set TipoContrato = $$GetTipoContrato^VARContrato($$$KEY1(pYKEY))
	set Categoria    = $$GetCategoria^VARContrato($$$KEY1(pYKEY))
	
	if (TipoContrato '= 1) quit $$$YES 
	if (TipoContrato = 1) && (Categoria '= 6) { ;1=Contrato 6=Serviços de Manutenção de Equipamentos
		quit $$$YES
	}
	
	set objAditivo 	  = $get(^VARContratoAditivo(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))	
	set tipoAlteracao = $piece(objAditivo,Y,14)
	set tipoAditivo   = $piece(objAditivo,Y,2)
	
	if (tipoAlteracao '= 1) quit $$$YES
	if (tipoAditivo = 2) quit $$$YES
	
	quit $$$NO
	
HideAbaVeiculos(pYKEY)
	;-------------------------------------------------------------------------------
    ; Esconde a aba "Veículos" caso:
    ; 	1. Categoria do contrato é diferente de 8 - Serviços de manutenção de veículos
    ; 	2. O tipo de alteração/aditivo não é 1 - Termo Aditivo
    ; 	3. O tipo de aditivo é 2 - Prorrogação
    ;
    ;
    ; History:
    ; 18-Set-2009	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	if ($$$KEY2(pYKEY) = "") quit $$$YES
	
	new TipoContrato, Categoria, objAditivo, tipoAlteracao, tipoAditivo
 
	set TipoContrato = $$GetTipoContrato^VARContrato($$$KEY1(pYKEY))
	set Categoria    = $$GetCategoria^VARContrato($$$KEY1(pYKEY))
	
	if (TipoContrato '= 1) quit $$$YES 
	if (TipoContrato = 1) && (Categoria '= 8) { ;1=Contrato 8=Serviços de Manutenção de Veículos
		quit $$$YES
	}
	
	set objAditivo 	  = $get(^VARContratoAditivo(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))	
	set tipoAlteracao = $piece(objAditivo,Y,14)
	set tipoAditivo   = $piece(objAditivo,Y,2)
	
	if (tipoAlteracao '= 1) quit $$$YES
	if (tipoAditivo = 2) quit $$$YES
	
	quit $$$NO	
]]></Routine>
</Export>