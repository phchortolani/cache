<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INStocktakeReport3" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INStocktakeReport3
#include COMSYS
#include INConst
#include %occInclude
	
Report()
	;-------------------------------------------------------------------------------
	;  Stocktake Totals Report - derived from INWEINVDS
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 09-Oct-2008	GRF		SR15810: Copied
	;-------------------------------------------------------------------------------
	new idStocktake,strFromStore,strGroupList,strHeading,strLocnList,strToStore
	
	set idStocktake  =   $get(VORG(1))
	if idStocktake=""  do ^WWWINFO($$^WWWTEXT(32292)_" "_$$^WWWTEXT(47)) quit       ; "Check Please! Selection" 
	
	set strLocnList  =   $get(VORG(2))            ; semicolon delimited lists
	set strGroupList =   $get(VORG(3))            ; for filtering lines
	set strFromStore =   $get(VORG(7))
	set strToStore   =   $get(VORG(8))
	
	kill ^CacheTempStocktakeReport(YUCI,"Total",YUSER)
	
	set strHeading = $$SetHeader()
	
	do ^WWWBACK
	do Print^WWWLISTKOPF("9;11;12;13;14")                        ; Print Stocktake Criteria
	do PrintHeader(idStocktake,strHeading)
	do PrintLines(idStocktake,strLocnList,strGroupList,strFromStore,strToStore)
	do PrintTotals()
	write "</TABLE>",YCR                    ; close second table opened in PrintHeader
	
	kill ^CacheTempStocktakeReport(YUCI,"Total",YUSER)
	quit

	;-------------------------------------------------------------------------------
	;                                      Stocktake Totals Report                                       PrintHeader
	;                                      ########  -  DD/MM/YYYY                                       Title^INStocktakeReportCommon
	;-------------------------------------------------------------------------------------------------+
	;Id # | Storage | Item | Description | Count    | Unit of | Unit Price | Total Value | Difference |  SetHeader
	;     |         |      |             | Quantity | Measure |            |             |   Value    |  => strHeading
	;-------------------------------------------------------------------------------------------------+
    ;     |         |      | Total Value |          |         |            |    AU$ 0.00 |   AU$ 0.00 |
	;-------------------------------------------------------------------------------------------------+
    ;  1       2        3        4           5           6           7           8            9  10
	;-------------------------------------------------------------------------------

	
SetHeader()
	;-------------------------------------------------------------------------------
	;	BEARBEITEN ÃœBERSCHRIFT ;Heading
	; 
	; History :
	; 09-Oct-2008	GRF		SR15810: Separate Heading
	;-------------------------------------------------------------------------------
	new strHeading
	
	set strHeading = $$$StrINWEINVStocktakingCountingNumber_Y_$$$StrINWEINVStockLocation_Y_$$^WWWTEXT(32024)      ; 1...3
	set strHeading = strHeading_Y_$$^WWWTEXT(32026)_Y_$$$StrINWEINVActualQuantity_Y_$$^WWWTEXT(32030)             ; 4...6
	set strHeading = strHeading_Y_$$^WWWTEXT(31409)_Y_$$^WWWTEXT(32059)_Y_$$^WWWTEXT(32259)_" "_$$^WWWTEXT(32325) ; 7...10
	
	quit strHeading	
	
	
PrintHeader(pidStocktake,pstrHeading)
	new loop
	
	write "<CENTER>"
	do START00^WWWTAB
	write YCR,"<TR><TH><FONT SIZE=3>"
	write "<NOBR>"
	write "<B>"
	write $$$Text("IN00460")                ; "Stocktake Totals Report"
	do Title^INStocktakeReportCommon(pidStocktake)
	write "</B>"
	write "<BR>"
	write "</NOBR>"
	write "</FONT></TH></TR>",YCR
	write "</TABLE>",YCR
	write "<BR>"
	write "</CENTER>"
	
	do START100^WWWTAB 
	write "<THEAD>" 
	write YCR,"<TR>"
	for loop=1:1 {
		quit:$piece(pstrHeading,Y,loop,99)=""
		
		write YCR,"<TH BGCOLOR="_YDARKGRAY_"><FONT SIZE=1>"
		write $piece(pstrHeading,Y,loop),"&nbsp;"
		write "</FONT></TH>"
	}
	write "</TR>",YCR
	write "</THEAD>"
	quit
	
	
PrintLines(pidStocktake,pstrLocnList,pstrGroupList,pstrFromStore,pstrToStore)
	;-------------------------------------------------------------------------------
	; History:
	; 17-May-2011	shobby	SR17755: Formatting of Storage
	; 08-Oct-2008	GRF		SR15810: Copied and modified
	;-------------------------------------------------------------------------------
	new arrAttr,curCost,curCountValue,curDiffValue,curDiscount,curGroupDiff,curGroupTotal
	new fltDiscPercent,fltQtyCounted,fltSnapQty,idAttr,idDefGroup,idItem
	new idItemGroup,idLineNo,idLocn,idOrderNo,idOrderLine,idStore,idUnit
	new loop,objItem,objSetup,objSTLine
	new strBGColour,strEvalReason,strItemName,strRemark,strText,strUnit
	
	set pstrLocnList  = ";"_pstrLocnList_";"
	set pstrGroupList = ";"_pstrGroupList_";"
	
	set objSetup     = $get(^INVORG(YM,YM,1))
	set idDefGroup   = $$$INVORGDefaultItemGroup(objSetup)
	
	set YDDSATZ      = 0
	set idLineNo=""
	for {
		set idLineNo = $order(^INWEINV(YM,pidStocktake,idLineNo))
		quit:idLineNo=""
		
		set objSTLine=$get(^INWEINV(YM,pidStocktake,idLineNo,1))
		continue:+$$$INWEINVActualQuantity(objSTLine)=0            ; Ignore since looking for Count Totals
		
	
	;	set idOrderNo   = $$$INWEINVOrderNumber(objSTLine) 
	;	set idOrderLine = $$$INWEINVLineItemsNumber(objSTLine) 
		
		set idLocn = $$$INWEINVLocation(objSTLine)
		continue:((pstrLocnList'=";;") && '$find(pstrLocnList,";"_idLocn_";"))
		
		set idStore = $$$INWEINVStockLocation(objSTLine)
		continue:((pstrFromStore'="") && (pstrFromStore ]] idStore    ))
		continue:((pstrToStore  '="") && (idStore       ]] pstrToStore))
		
		set idItem = $$$INWEINVItemNumber(objSTLine)
		continue:idItem=""
		set objItem = $get(^INART(YM,idItem,1))
		
		set idItemGroup = $$$INARTItemGroup(objItem)
		if idItemGroup="" set idItemGroup = idDefGroup
		continue:((pstrGroupList'=";;") && '$find(pstrGroupList,";"_idItemGroup_";"))
		
		
		;-----------------------------------
		
		set strUnit = ""
		set idUnit  = $$$INARTUnitofMeasure(objItem)
		if idUnit'="" set strUnit=$piece($get(^WWW101(0,"EINHEIT",SPRACHE,idUnit,1)),Y,1)
		if strUnit="" set strUnit = "Unit:"_idUnit
	
		set fltQtyCounted  = $$$INWEINVActualQuantity(objSTLine)
		set fltSnapQty     = $$$INWEINVTargetQuantity(objSTLine)
		set fltDiscPercent = $$$INWEINVStocktakingDiscount(objSTLine)
 
		set curCost       = $$CalculatePrice^INStocktakeUtil(idItem,objItem,objSTLine,idLocn)
		set curDiscount   = $justify(fltDiscPercent/100*curCost,0,2)
		set curCost       = curCost - curDiscount
		set curDiffValue  = curCost * (fltQtyCounted - fltSnapQty)  ; FIXME : This doesn't consider the Stock History Delta Qty
	 	set curCountValue = curCost * fltQtyCounted / $$^INQTYUNIT(idItem)
		
		if idItemGroup'="" {
			set curGroupTotal = $get(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,1))
			set curGroupDiff  = $get(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,2))
			set ^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,1) = curGroupTotal + curCountValue
			set ^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,2) = curGroupDiff + curDiffValue
		}
		
		;---------------------------------------------------------------------------
		
		; New Line
		;+++++++++++++++++++++++++++++++++++++++
		set YDDSATZ = $get(YDDSATZ) + 1
		set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)
		write YCR,"<TR>"
		
		
		; Line No (ID#)
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
		write $justify(idLineNo,4,0)
		write "&nbsp;</FONT></TD>"
		
		
		; Location / Storage
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>"
		;SR17755 write idLocn," / ",idStore
		write idLocn," / "_$$CodeWithTSU2^INLP(idLocn,idStore) ;SR17755
		if $$$INWEINVStockLocationNote(objSTLine) = "HISTORY" write " "_$$^WWWTEXT(32859)_"&nbsp;"	; "History"
		
		do GetAttributes^INStocktakeUtil(idItem,objSTLine,SPRACHE,.arrAttr)
		set idAttr = ""
		for {
			set idAttr = $order(arrAttr(idAttr))
			quit:idAttr=""
			
			write "<BR>",$piece(arrAttr(idAttr),"~",1),": ",$piece(arrAttr(idAttr),"~",2),"&nbsp;"
		}
		write "</FONT></TD>"
		
		
		; Item ID, Group
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>"
		write idItem_"<BR>"
		if idItemGroup'="" write "("_idItemGroup_") "_$$GetDescription^INDItemGroup(idItemGroup)
		write "&nbsp;</FONT></TD>"
		
		
		; Description : Order|Reservation, ItemName, Remarks, Discount & Reason
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>"
		set idOrderNo   = $$$INWEINVOrderNumber(objSTLine) 
		set idOrderLine = $$$INWEINVLineItemsNumber(objSTLine) 
		
		if $$$INWEINVStockType(objSTLine)="S" {
			write "<b>"
			write $$^WWWTEXT(32047)_": "_idOrderNo_"-"_idOrderLine		; "Order"
			write "</b><BR>"
		
		} elseif $$$INWEINVStockType(objSTLine)="R" {
			write "<b>"
			write $$^WWWTEXT(32480)_": "_idOrderNo		                ; "Reserved"
			write "</b><BR>"
		}
		
		set strItemName = $$$INARTSearchName(objItem)
		write $extract(strItemName,1,40)
		
		set strRemark = $$$INARTRemarks(objItem)
		if $translate(strRemark,"| ")=$translate(strItemName,"| ") set strRemark=""
		if strRemark'="" WRITE "<BR>"
		
		; LONG TEXT - vertical separator indicates new line - changed to space - broken into 40 characters 
		for loop=1:1 {
			quit:$piece(strRemark,"|",loop,999)=""
			
			set strText = $translate($$^WWWUML($piece(strRemark,"|",loop),1),"|"," ")
			continue:$translate(strText," ")=""
			
			write $extract(strText,1,40)
			if $extract(strText,41,80)'=""   write "<BR>",$extract(strText,41,80)
			if $extract(strText,81,120)'=""  write "<BR>",$extract(strText,81,120)
			if $extract(strText,121,160)'="" write "<BR>",$extract(strText,121,160)
			if $piece(strRemark,"|",loop+1,999)'="" write "<BR>",YCR
		}
		write "&nbsp;</FONT></TD>"
		
		if fltDiscPercent '= "" write "<BR>-",fltDiscPercent,"% "    ; minus discount
		set strEvalReason  = $$$INWEINVEvaluationReason(objSTLine)
		if strEvalReason  '= "" write "(",strEvalReason,")"
		write "&nbsp;</FONT></TD>"
		
		
		; Count Quantity
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
		write fltQtyCounted
		write "&nbsp;</FONT></TD>"
		
		
		; Unit
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
		write strUnit
		write "&nbsp;</FONT></TD>"
		
		
		; Unit Cost
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
	 	write $$^WWWZAHL(curCost,0,2,YWHR)
		write "&nbsp;"
		if fltDiscPercent '= "" write "<BR>(-",fltDiscPercent,"%)"    ; minus discount
		write "&nbsp;</FONT></TD>"
		
		
		; Total Value
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
	 	write $$^WWWZAHL(curCountValue,0,2,YWHR)
		write "</FONT></TD>"
		
		
		; Difference Value
		;---------------------------------------
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"
	 	write $$^WWWZAHL(curDiffValue,0,2,YWHR)
		write "&nbsp;</FONT></TD>"
		
		write "</TR>"
	}	
	
	quit
	
	
PrintTotals()	
	;-------------------------------------------------------------------------------
	; Display totals
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-Oct-2008	GRF		SR15810: Copied and modified
	;-------------------------------------------------------------------------------
	new curAllDiff,curAllTotal,curGroupDiff,curGroupTotal,idItemGroup,loop,strBGColour
	
	set curAllTotal = 0      ;GESAMT=0
	set curAllDiff  = 0      ;GESAMTDIF=0
	set idItemGroup = ""
	for {
		set idItemGroup = $order(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup))
		quit:idItemGroup=""
		
		set curAllTotal   = curAllTotal + $get(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,1))
		set curAllDiff    = curAllDiff  + $get(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,2))
	}
	
	; New Line
	;+++++++++++++++++++++++++++++++++++++++
	set YDDSATZ = $get(YDDSATZ) + 1
	set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)
	write YCR,"<TR>"
	
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 1
	write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"               ; 2
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 3
	
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=MIDDLE"_strBGColour_"><FONT SIZE=1>"
	write "<B>",$$^WWWTEXT(32059),"</B>"                                        ; "Total Value"    ; 4
	write "&nbsp;</FONT></TD>"
 
	write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"               ; 5
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 6
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 7
	
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"                     ; 8
	write "<B>",$$^WWWZAHL(curAllTotal,0,2,YWHR),"</B>"
	write "&nbsp;</FONT></TD>"
	
	write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"                     ; 9
	write "<B>",$$^WWWZAHL(curAllDiff,0,2,YWHR),"</B>"
	write "&nbsp;</FONT></TD>"
	
	write "</TR>"
	
	
	; Blank Line
	;+++++++++++++++++++++++++++++++++++++++
	set YDDSATZ = $get(YDDSATZ) + 1
	set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)
	write YCR,"<TR>"
	
	for loop=1:1:9 {
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"
	}
	write "</TR>"
	
	; Analysis By Item Group
	;---------------------------------------
	set idItemGroup = ""
	for {
		set idItemGroup = $order(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup))
		quit:idItemGroup=""
		
		set curGroupTotal = $get(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,1))
		set curGroupDiff  = $get(^CacheTempStocktakeReport(YUCI,"Total",YUSER,idItemGroup,2))
	
		; New Line
		;+++++++++++++++++++++++++++++++++++++++
		set YDDSATZ = $get(YDDSATZ) + 1
		set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)
		write YCR,"<TR>"
		
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 1
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"               ; 2
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 3
		
		
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>"
		write idItemGroup," - ",$$GetDescription^INDItemGroup(idItemGroup)                             ; 4
		write "&nbsp;</FONT></TD>"
		
		write YCR,"<TD NOWRAP VALIGN=TOP"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"               ; 5
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 6
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>&nbsp;</FONT></TD>"   ; 7
 
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"                     ; 8
		write $$^WWWZAHL(curGroupTotal,0,2,YWHR)
		write "&nbsp;</FONT></TD>"
		
		write YCR,"<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"_strBGColour_"><FONT SIZE=1>"                     ; 9
		write $$^WWWZAHL(curGroupDiff,0,2,YWHR)
		write "&nbsp;</FONT></TD>"
		
		write "</TR>"
	}
	quit
	
	
 
]]></Routine>
</Export>