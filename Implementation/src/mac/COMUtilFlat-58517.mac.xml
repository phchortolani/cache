<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilFlat" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilFlat
	;-------------------------------------------------------------------------------
	; Covert Formdat.txt to a Flat File for Compare
	; Run from command line
	; 11-Jul-2003	SCR		Added Class Fields	
	; 03-May-2003	SCR		Block "INT" when there has is a "MAC" routine 
	;-------------------------------------------------------------------------------
	#include WWWConst
Convert(From,To)
	;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Returns:
	;
	; History:
	; 28-Jun-2004	Paul K	Newed variables
	;-------------------------------------------------------------------------------
	new Last,Count,Glo,Type,Header1,Header2,Ref,Glo2,Mode,Skip,Data
	
	kill ^mtemp($job) ; used for int file filter
	open From:"r"
	open To:"wn"
	set $ztrap="ERR"
	use From
	read Header1,Header2
	use To
	write Header1,!,Header2,!
	use 0
	write Header1,!,Header2,!
	set Last=$horolog
	for Count=1:1 {
		use From
		read Glo,Ref
		use 0
		quit:Glo="*"
		
		set Type=$qsubscript(Glo,3)
		if Type=0 { ; Command
			use To
			write "Cmd:",Ref,!
			use 0
		}
		if Type=1 {
			use From
			read Glo2,Data
			use 0
			use To
			do Norm(.Ref,.Mode,.Skip,Data)
			if '$get(Skip) {
				write Mode,":",Ref,"=",Data,!
			}
			use 0
 
		}
		if Last'=$horolog write Count,! set Last=$horolog
	}
	Close From
	Close To
	kill ^mtemp($job) ; used for int file filter
	quit
	
ERR  ;Internal Tag
	Close From
	Close To
	kill ^mtemp($job) ; used for int file filter
	Use 0
	write $zerror
	quit
 
Norm(Ref,Type,Skip,Data) ; Normalise the Global Name
	;-------------------------------------------------------------------------------
	; 11-Jul-2003	SCR		Added Class Field Names
	; 28-May-2003	SCR		Skip flag added to skip "int" routines if there has been a "mac" routine
	; 20-May-2003	SCR		'IF' Change
	;-------------------------------------------------------------------------------
	new OldRef,Glo,Class,Rtn
	
	set Type="Glo"
	set OldRef=Ref
	set Skip=0
	set Glo=$qsubscript(Ref,0)
	set Glo=$extract(Glo,2,99)
	if (Glo="rINC") || (Glo="rMAC") || (Glo="ROUTINE") {
		set Type="Rtn"
		set Rtn=$qsubscript(OldRef,1)
		set Ref=Rtn_"."_$select(Glo="rINC":"INC",Glo="rMAC":"MAC",1:"INT")
		if Rtn'="" {
			if Glo="rMAC" set ^mtemp($job,Rtn)=1                      
			if (Glo="ROUTINE")&&($get(^mtemp($job,Rtn))) set Skip=1
		}
	}
	if (Type="Glo") && (Glo'="") {
		set Class=$piece($get(^WWW001(0,Glo,1)),"~",1)
		if Class'="" {
			set Type=Class
			set Ref=$piece(Ref,Glo,2,999)
			do Class(Glo,Class,Ref,Data)
			set Skip=1
		}
	}
	quit
	
Class(Glo,Class,Ref,Data)
	new Fld,Y,Value,Name
	set Y="~"
	for Fld=1:1:$length(Data,Y) {
		set Value=$piece(Data,Y,Fld)
		if Value="" continue
		set Name=$$$WWW003PropertyDescription($get(^WWW003(0,Glo,Fld,1)))
		if Name="" set Name="Field "_Fld
		write Class,":",Ref,":",Name,"=",Value,!
	}
	quit
	
Test ; Test Code
	do Convert("C:\common\formdat.txt","C:\common\sr.txt")
	quit
	
]]></Routine>
</Export>