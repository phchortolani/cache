<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWTAG" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWTAG ;WWWTAG;DT;AUTOMATISCHES STARTEN VON PROGRAMMEN;26.01.2000
#include COMSYS
	;------------------------------------------------------------------\
	; Description of Function :
	;		AUTOMATISCHES STARTEN VON PROGRAMMEN
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 15-Jan-2009	GRF		SR15853: Pass audit string to NewUserSession
	; 19-Aug-2008	FIS		SR15853: create session in common routine
	; 29-Mar-2007	GRF		Quits; FIXME
	; 02-Sep-2005	JW		SR12966: WWWTAG is not shared
	;  5-Jul-2005	JW		SR12620: Removed timeout, commented out redundant code
	; DT	26.01.2000
	;------------------------------------------------------------------/
	;hintergrundprogramm auto email ;E-Mail 
	;NEW LFDPRG
	QUIT:'$DATA(^WWWTAG(YM))    ;KEINE VORGABEN ;no 
	LOCK +^WWWTAG(YM):0 QUIT:'$TEST   ;SCHON IN BEARBEITUNG ;yet within adaptation 
	
	; FIXME : May want to move unlock after JOB command so can't have second process start in interval <GRF>
	LOCK -^WWWTAG(YM)
	SET YQ=0
	;SET LFDPGM=$ORDER(^WWWTAG(YM,""))
	;DO:LFDPGM'=""  ;durchsuchen SCHEMA
	;. IF $PIECE($GET(^WWWTAG(YM,LFDPGM,1)),Y,2)=+$HOROLOG SET YQ=1  ;HEUTE SCHON GEPRÜFT (WENN 1. GEPRÜFT DANN ALLE GEPRÜFT) ;today yet 
	;QUIT:YQ=1
	;JOB START^WWWTAG(YM)::1    ;JOB STARTEN ;launching 
	
	JOB START^WWWTAG(YM)    ; Start job   SR12620
	
	QUIT
	
	
START(YMAN) ;STARTEN VERARBEITUNG ;launching processing 
	SET $ZTRAP="^WWWERROR"
	DO ^WWWVAR
	IF $GET(YMAN)'="" SET YM=YMAN
	LOCK +^WWWTAG(YM):4 QUIT:'$TEST   ;SCHON IN BEARBEITUNG ;yet within adaptation 
	set YUSER = $$NewUserSession^WWWUSER("WWWTAG")
	;FOR  SET YUSER=$RANDOM(10000000) QUIT:'$DATA(^WWWUSER(0,YUSER))     ;SR15853
	KILL ^WWWSOR(YUSER)
	DO
	. SET LFDPGM=""
	. FOR  SET LFDPGM=$ORDER(^WWWTAG(YM,LFDPGM)) QUIT:LFDPGM=""  DO           ;durchsuchen SCHEMA ;scour scheme 
	. . SET LFDPGM(1)=$GET(^WWWTAG(YM,LFDPGM,1))
	. . IF $PIECE(LFDPGM(1),Y,2)=+$HOROLOG                              QUIT  ;HEUTE SCHON GEPRÜFT ;today yet
	. . IF $PIECE(LFDPGM(1),Y,3)'="" IF $PIECE(LFDPGM(1),Y,3)>$HOROLOG  QUIT  ;AB DATUM ;Confirm. Date 
	. . IF $PIECE(LFDPGM(1),Y,4)'="" IF $PIECE(LFDPGM(1),Y,4)<$HOROLOG  QUIT  ;BIS DATUM ;until Date 
	. . SET YEXEC=$PIECE(LFDPGM(1),Y,1)                                       ;DATEN AUS SCHEMAVORGABE ;out of 
	. . IF $EXTRACT(YEXEC)=";"                                          QUIT  ;FIS;13.05.04;25667
	. . IF $FIND(YEXEC," ;") SET YEXEC=$PIECE(YEXEC," ;",1)  ;FIS;13.05.04;25667
	. . KILL ^WWWSOR(YUSER)
	. . IF YEXEC'="" XECUTE YEXEC  ;AUSFÜHREN PROGRAMM ;implement programme 
	. . SET $PIECE(^WWWTAG(YM,LFDPGM,1),Y,2)=+$HOROLOG  ;HEUTE BEARBEITET ;today 
	
	KILL ^WWWSOR(YUSER)
	LOCK -^WWWTAG(YM)  ;LOCK WIEDER FREIGEBEN;13.05.04;25667 ;re- 
	HALT
	
	
Schedule()
    ;-------------------------------------------------------------------------------
    ; Run the automatic executes via the scheduler
    ;
    ; Params: nil
    ;
    ; Returns: message
    ;
    ; History:
    ; 04-Jul-2005	JW		SR12807: Created
    ;-------------------------------------------------------------------------------
	do WWWTAG
	
	quit $$$Text("WWW00025")  ;Automatic Executes jobbed
	
]]></Routine>
</Export>