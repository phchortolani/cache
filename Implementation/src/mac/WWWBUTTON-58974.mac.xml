<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWBUTTON" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWBUTTON(YTEXT,YLINK,YTIP,YONCLICK)
#include WWWConst
#include COMConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		TEXT-BUTTON
	;
	;	BEISPIEL FÜR 1:  DO START^WWWBUTTON(Width,blnFixedPosn)
	;	                 DO BUTTON^WWWBUTTON(TEXT,LINK,TOOLTIP)
	;	          **     DO ZW^WWWBUTTON
	;	          **     DO BUTTON^WWWBUTTON(TEXT,LINK,TOOLTIP)
	;	                 DO END^WWWNBUTTON
	;	** Repeat pairs for more buttons
	;	
	; Inputs : 
	;	YOPT   ? :  0=STAND ALONE BUTTON
	;	            1=PART OF BUTTON LINE
	;	YTEXT    : BUTTON-TEXT (IMG-VALUE)
	;	YLINK    : LINK (EXECUTE ONCLICK)
	;	                                                           
	;	YTIP     : Tool tip
	;	YONCLICK : ZUSÄTZLICHER EXECUTE ONCLICK  
	;
	; ByRef :
	;	Y
	;
	; Returns :
	;
	;
	; History :
	; 20-Jun-2007	GRF		SR15549: Doco; braces; expand commands; macros
	; 10.04.2003	FIS		TEXT-BUTTON
	;-------------------------------------------------------------------------------
	;DO START(0)
	do START(100)   ; DWR test
	do BUTTON($get(YTEXT),$get(YLINK),$get(YTIP),$get(YONCLICK))
	do END
	quit
	
START(YWID,YFIX) ;STARTEN BUTTON-LEISTE / START BUTTON BAR
	new DESIGN
	
	set DESIGN="STANDARD"
	set YWID=+$get(YWID)
	write YCR,"<DIV STYLE="_""""
	write " height:22px;"
	write " padding:0px;"
	if +$get(YFIX)=1 write " position:absolute;"
	
	if DESIGN="STANDARD" {
		write " background-color:none;"
		write " border:none;"
	
	} elseif DESIGN="EFFECT" {                            ; Never executed
		write " background-color:"_YSILVER_";"
		write " border:1px outset;"
		write " border-bottom-color:ButtonShadow;"
		write " border-right-color:ButtonShadow;"
		write " border-top-color:ButtonHighlight;"
		write " border-left-color:ButtonHighlight;"
	}
	
	if YWID=100 {                       ;100% OF FRAME
		write " width:100%;"
	
	} elseif YWID=0 {                   ; DEPENDING ON TEXT LENGTH 
		set YWID = 50
		if $get(YTEXT)'="" {
			if $length(YTEXT)<2  set YWID = 24
			if $length(YTEXT)>10 set YWID = 100
		}
		write " width:"_YWID_"px;"
	} else {
		write " width:"_YWID_"px;"    	;individual
	}
	write """"_">"
	write YCR,"<table border=0 cellspacing=0 cellpadding=0><tr>"
	if DESIGN="EFFECT" write YCR,"<td><IMG SRC="_""""_YGIF_"delimiter1.gif"_""""_"></td>"
	write YCR
	quit
	
	
BUTTON(YTEXT,YLINK,YTIP,YONCLICK)
	;-------------------------------------------------------------------------------
	;   CREATE BUTTON
	;
	; History:
	; 05-Jul-2007	RPW		SR15549: Changed YCOLOR to strColour
	;-------------------------------------------------------------------------------
	new YI,YWID,DESIGN,strColour
	
	set DESIGN = "STANDARD"
	set YWID   = 50
	if $get(YTEXT)'="" {
		if $length(YTEXT)<2  set YWID = 24
		if $length(YTEXT)>10 set YWID = 100
	}
	write YCR,"<td nowrap>"
	if $get(YLINK)'="" {
		set strColour=$$$WWW012FontColor($get(^WWW012(0,YM,1)))     ; SR15549
		if strColour="" {
			set strColour="black"
		} else {
			set strColour=$piece($get(^WWW100(0,"FARBE",SPRACHE,strColour,1)),Y,1)
		}
		write "<a href="_""""_YLINK_""""
		write " style="_""""_"text-decoration:none;"
		write " color:"_strColour_";"_""""
		write ">"
	}
	
	write YCR,"<DIV STYLE="_""""
	write " background-color:buttonface;"
	write " height:22px;"
	write " width:"_YWID_"px;"
	write " cursor:pointer;"
	write " text-align:center;"
	write " text-decoration:none;"
	write " padding:2px;"
	
	if DESIGN="STANDARD" {
		write " border:2px outset;"
	;	write " border:2px solid;"
	;	write " border-bottom-color:ButtonShadow;"
	;	write " border-right-color:ButtonShadow;"
	;	write " border-top-color:ButtonHighlight;"
	;	write " border-left-color:ButtonHighlight;"
	
	} elseif DESIGN="EFFECT" {                            ; Never executed
		write " border:none;"
		write " padding:2px;"
	}
	write """"
	
	if DESIGN="EFFECT" {                                  ; Never executed
		write YCR," onMouseOver='"
		write " this.style.border="_""""_"2px outset"_""""_";"
		write " this.style.padding="_""""_"1px"_""""_";"
		write " '"
		write YCR," onMouseOut='"
		write " this.style.border="_""""_"none"_""""_";"
		write " this.style.padding="_""""_"3px"_""""_";"
		write " '"
	}
	write YCR," onClick='"
	
	write " this.style.border="_""""_"2px inset"_""""_";"
	write " this.style.paddingTop="_""""_"2px"_""""_";"
	write " this.style.paddingLeft="_""""_"2px"_""""_";"
	write " this.style.paddingRight="_""""_"0px"_""""_";"
	write " this.style.paddingBottom="_""""_"0px"_""""_";"
	
	;IF $G(YONCLICK)'="" WRITE " "_$TR(YONCLICK,"'","""")
	;IF $G(YLINK)'="" DO
	;. IF $F(YLINK,"'")  FOR YI=1:1  QUIT:$P(YLINK,"'",YI,999)=""   SET:$E($RE($P(YLINK,"'",YI)))'="\" $P(YLINK,"'",YI)=$P(YLINK,"'",YI)_"\"
	;. IF $F(YLINK,"""") FOR YI=1:1  QUIT:$P(YLINK,"""",YI,999)=""  SET:$E($RE($P(YLINK,"""",YI)))'="\" $P(YLINK,"""",YI)=$P(YLINK,"""",YI)_"\"
	;. WRITE " window.location="_""""_YLINK_""""_";"
	
	write " '"
	if $get(YTIP)'="" write YCR," title="_""""_YTIP_""""
	write YCR,">"
	write YCR
	if $get(YTEXT)'="" {
		if $length(YTEXT)<2 {
			write "<font size=2><b>"
		} else {
			write "<font style="_""""_"font-size:11px;"_""""_">"
		}
	}
	write $get(YTEXT)
	if $get(YTEXT)'="" write "</font>"              ; SR15549 only if open <font>   FIXME : <GRF> close <b>?
	write YCR,"</DIV>"
	if $get(YLINK)'="" write YCR,"</A>"
	write YCR,"</td>"
	quit
	
ZW ;ZWISCHEN 2 BUTTONS ;between t buttons 
	write YCR,"<td>&nbsp;</td>",YCR
	quit
	
END ;END OF BAR
	new DESIGN
	
	set DESIGN="STANDARD"
	if DESIGN="EFFECT" write YCR,"<td><IMG SRC="_""""_YGIF_"delimiter.gif"_""""_"></td>"    ; Never executed
	write YCR,"</tr></table></DIV>",YCR
	quit
 
CreateOnClick(pstrObject,pstrImage="",pstrTitle="",pstrId="",plstCode1="",plstCode2="",pstrLinkType="")
    ;-------------------------------------------------------------------------------
    ; The code that creates the button.
    ;
    ; Parameters:
    ; 	pstrObject: 	The field on the screen that the button is attached to.
    ; 	pstrImage:		The image on the button.  Note each image should have an associated disabled image.
    ; 	pstrTitle:  	The tooltip on the button.
    ; 	plstCode1:  	The code that runs when the button is clicked.
    ; 	plstCode2:  	The code that runs after the popup screen is closed.
    ; 	pstrLinkType:	Whether it is a 'class' or a 'HREF'.  It must be one of these.
    ; 
    ; Returns
    ; 	The javascript to be written to the screen
    ;
    ; History:
    ; 05-Jul-2007	shobby	SRBR014409: Rename pstrCode* to plstCode@
    ; 05-Jul-2007	shobby	SRBR014409: Don't return anything if pstrLinkType is not
    ;							one of the two allowed conditions.
    ; 04-Jul-2007	RPW		SRBR014409: Correct variable names, idx is a prefix not
    ;							a variable name result!=undefined can result in an
    ;							error, it should be result!==undefined
    ; 26-Jun-2007	shobby	SRBR014409: Use standard routine to find disabled
    ;							equivalent of known images
    ; 08-Jun-2007	shobby	SRBR014409: Created
    ;-------------------------------------------------------------------------------
	new strJS,loop
	
	set strJS = ""
	set strJS1 = ""
	if pstrLinkType="class" {
		set strJS = "<A class=link "
	} elseif pstrLinkType="HREF" {
		set strJS = "HREF=""javascript:"
	}
	if strJS'="" {
		set strJS = strJS_"onclick="""
		set strJS = strJS_"  var result='';"		
		;set strJS1 = strJS1_"  if ("_pstrObject_".readOnly != true) {"
		set strJS = strJS_"  if ($('#"_pstrObject_"').prop('readOnly') != true) {"
		for loop=1:1:$listlength(plstCode1) {
			set strJS = strJS_"    "_$listget(plstCode1,loop)
		}
		set strJS = strJS_"  }"
		set strJS = strJS_"  if ((result != '' )&&(result!==undefined)) {"
		set strJS = strJS_"    with (document.getElementById('"_pstrObject_"')) {"
		set strJS = strJS_"      value=result;"
		set strJS = strJS_"      focus();"
		set strJS = strJS_"      fireEvent('onBlur');"
		set strJS = strJS_"      fireEvent('onChange');"
		set strJS = strJS_"    }"
		for loop=1:1:$listlength(plstCode2) {
			set strJS = strJS_"    "_$listget(plstCode2,loop)
		}
		set strJS = strJS_"  }"
		set strJS = strJS_""""
		if pstrLinkType="HREF" set strJS = strJS_""
		set strJS = strJS_strJS_">"
		;
		
		if pstrImage'="" {
			set icone=""
			if $$MenuType^WWW013()=14{
				set icone = $$traducaoIcone^WWWFORMCOMMON(pstrImage)
			}
			if icone'=""{
				if $get(objCompany)="" set objCompany = $get(^WWW012(0,YM,1))
				set idColor = $$$WWW012FontColor(objCompany)
				set FONTCOLOR = ""
				if idColor'="" set FONTCOLOR = $$$SysEnum("FARBE",idColor)
				if FONTCOLOR="" set FONTCOLOR = "black"
				
				if disabledButton set corBotao="secondary"
				;set strJS=strJS_"   <div class='btn btn-outline-"_corBotao_" btn-sm' id="""_pstrId_"IMG"""
				;set strJS=strJS_" title="""_pstrTitle_""" border=0"
				;set strJS=strJS_" style='width:30px;background-color:#ffffff;'> "
				set strJS=strJS_"&nbsp;<i id="""_pstrId_"IMG"" class='"_icone_"' title="""_pstrTitle_""" style='color:"_FONTCOLOR_"'></i>"
				;set strJS=strJS_"</div>"
			}else{			
				set strJS = strJS_"<IMG SRC="""_YGIF_$$Name^WWWIMAGE(pstrImage,YHID=2)_""" ALIGN=ABSBOTTOM TITLE="""_pstrTitle_""" border=0"
				;SR17861 set strJS = strJS_"<IMG SRC="""_YGIF_$$Name^WWWIMAGE(pstrImage,YHID=2)_""" ALIGN=ABSBOTTOM TITLE="""_pstrTitle_""" border=0 style='vertical-align:center; margin:1px; offset:0px;'"
				if pstrId'="" set strJS = strJS_" id="""_pstrId_"IMG"""
				set strJS = strJS_">"
			}
		}
		set strJS = strJS_"</A>"
	}
	quit strJS
	
	
DoOnClick(pstrAction) ;16913
	quit " document.WWW.style.cursor = 'wait'; SaveAction(1); "_pstrAction_" "
	
	
CreateWindowLocation(pstrAction) ;16913
	quit " onClick=""document.WWW.style.cursor = 'wait'; SaveAction(1); window.location='"_pstrAction_"';"" "
	
]]></Routine>
</Export>