<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTKOSTT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTKOSTT(YART,YMENGE,YDAUER,YGEWICHT,YARTIKEL,YLFN,KOSTENART)
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ARTIKELPREIS NACH MENGE ODER DAUER
	;      NUR FUER TEILE RECHNUNG ;only tab
	;      
	; Inputs : 
	;	YART		Part Number			TEILEARTIKELNUMMER 
	;	YMENGE		Quantity
	;	YDAUER		Duration
	;	YGEWICHT	
	;	YARTIKEL	Main Item			HAUPTARTIKEL
	;	YLFN							TEILEZEILENNUMMER
	;	KOSTENART	Default Cost Type	VORGABE KOSTENART (TEUSERSTE, NIEDRIGSTE, ..)
	;	
	;	PREISE FUER GESAMMTEN ARTIKEL ERRECHNEN AUS INARTKOST (SELBSTKOSTEN) ;item out of 
	;
	; ByRef :
	;
	;
	; Returns :
	;
	; History :
	; 24-Jul-2007	GRF		SR15579: Doco; quits
	; 19-Aug-2005	GRF		SR13215 : block justification - call INTempARTKOST for
	; 						services (COPIED FROM INARTKOST)
	; 26-Apr-2005	GRF		Documentation
	; 30.06.2000	DT
	;-------------------------------------------------------------------------------
	;
	; ************************************************************
	;  ^WWW101(0,"EINHEIT")	Special processing
	;  
	;								1	2	3		see (*** # ***)
	;							-----------------
	;	  1	Each						2
	;	  2	Hr								3 (3600 sec)						
	;	  3	Litres/Gallon			1	
	;	  4	Kg						1	
	;	  5	Metre					1	
	;	  6	Sq Metre				1	
	;	  7	Set							2
	;	  9	Package						2
	;	 10	Minutes							3 (  60 sec)
	;	 11	Industry-Minutes				3 ( 100 sec)
	;	 12	m³						?	
	;	 13	Pair						2
	;	 14	Gram/Ounce				1	
	;	 15	Days						2
	;	 16	Weeks						2
	;	 17	Month						2
	;	 18	mm/Inches				?	
	;	 19	Km/Miles				?	2*
	;	 20	mm/Sq.Inches			1	
	;	 21	mg						?	
	;	 22 /100						
	;	 				(?) Why AREN'T these units recognised?
	;	 				(*) Why this value?
	; ************************************************************
	NEW YPREIS,YEINHEIT,VERSCHNITT
	
	SET YPREIS	   = "" 
	SET YARTIKEL   = $GET(YARTIKEL)
	SET YLFN	   = $GET(YLFN)
	SET YART	   = $GET(YART)
	SET YMENGE	   = $GET(YMENGE)      ;MENGE             ;quantity 
	SET YDAUER	   = $GET(YDAUER)      ;DAUER IN SEKUNDEN ;duration in seconds 
	SET YGEWICHT   =+$GET(YGEWICHT)    ;GEWICHT           ;weight 
	SET KOSTENART  = $GET(KOSTENART)
	;
	IF YART="" QUIT YPREIS   ;KEIN ARTIKEL ;no item 
	SET YEINHEIT=$PIECE($GET(^INART(YM,YART,1)),Y,40)
	;AUS TYBD;6,2,2005/ NEU INQTYUNIT
	;SET EINHEIT1=""
	;IF YEINHEIT'="" SET EINHEIT1=$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,YEINHEIT,1)),Y,1)
	;IF $FIND(EINHEIT1,"/") IF +$PIECE(EINHEIT1,"/",2)'=0 IF YMENGE>1 SET YMENGE=$JUSTIFY(YMENGE/$PIECE(EINHEIT1,"/",2),0,0)  ;WENN Z.B. MENGENEINHEIT= /100 ;when e.g. 
	
	; PREIS JE EINHEIT ;price of one unit   (all items)
	; ===================================
	;SET YPREIS=$$^INARTKOST(YART,,KOSTENART,1) 
	;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv Temp Code for 1.9.6 ;SR13215
	if $$$INARTItemType($get(^INART(YM,YART,1)))=2 {
		set YPREIS=$$^INTempARTKOST(YART,,KOSTENART,1)     ; Service - don't round too soon
	} else {
		set YPREIS=$$^INARTKOST(YART,,KOSTENART,1)
	}
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ 
	
	
	;+++++++++++++++++++++++++++++++++++++++
	;  (*** 1 ***)
	;+++++++++++++++++++++++++++++++++++++++
	
	; VERSCHNITT ; Blend in % addition - Increase for waste factor
	; ============================================================
	; 		D10		$$$INVORGBlendInAdd()
	SET VERSCHNITT=$PIECE($GET(^INVORG(YM,YM,1)),Y,10)  ;VERSCHNITT IN % ;within 
	IF $DATA(^INARTT(YM,YART)) SET VERSCHNITT=0  ;KEIN VERSCHNITT RECHNEN, WENN TEILEARTIKEL
	
	
	;IF +YGEWICHT'=0 IF YEINHEIT'=4 IF +YGEWICHT'=+$$^INGEWICHT(YART) DO   ;WENN WENIGER MASSE ALS ARTIKEL
	;.QUIT:+$$^INGEWICHT(YART)=0
	;.SET YPREIS=$JUSTIFY(YPREIS/$$^INGEWICHT(YART)*YGEWICHT,0,2)  ;PREIS AUS GEWICHT ERRECHNEN
	;.SET YPREIS=YPREIS+(YPREIS/100*VERSCHNITT)
	;.QUIT
	
	IF (YEINHEIT=3)!(YEINHEIT=4)!(YEINHEIT=5)!(YEINHEIT=6)!(YEINHEIT=14)!(YEINHEIT=20) DO  ;NUR BEI MENGEN ;only next to shuffle 
	. QUIT:+$GET(YMENGE)=0
	. SET YMENGE=YMENGE+(YMENGE/100*VERSCHNITT)
	
	IF +YGEWICHT'=0 IF YEINHEIT=4 DO  ;NUR BEI MENGEN KG ;only next to shuffle 
	. SET YMENGE=YGEWICHT+(YGEWICHT/100*VERSCHNITT)
	
	;+++++++++++++++++++++++++++++++++++++++
	;  (*** 2 ***)
	;+++++++++++++++++++++++++++++++++++++++
	
	; PREIS JE EINHEIT OHNE ZUSATZKOSTEN ;price for one unit without additional costs
	; ===============================================================================
	IF (YEINHEIT=1)!(YEINHEIT=7)!(YEINHEIT=9)!(YEINHEIT=13)!(YEINHEIT=15)!(YEINHEIT=16)!(YEINHEIT=17)!(YEINHEIT=19) DO
	. SET YPREIS=$$^INARTKOST(YART,,KOSTENART,1) 
	
	IF +YMENGE'=0 SET YPREIS=YPREIS*YMENGE/$$^INQTYUNIT(YART)   ;MENGE * PREIS ;where price is for xxx units
	
	;ZUSATZKOSTEN ;additional costs 
	IF +YMENGE'=0 DO
	. NEW YART1,YLOS,YEXTRA
	. SET YART1=$GET(^INART(YM,YART,1))
	. SET YEXTRA=+($PIECE(YART1,Y,74)+$PIECE(YART1,Y,75))  ;ZUSATZKOSTEN ; Tool & Extra costs 
	. QUIT:YEXTRA=0
	. SET YLOS=$PIECE(YART1,Y,72)                          ;LOSGRÖSSE    ; Lot Size
	. IF +YLOS=0 SET YLOS=999999999
	. SET YPREIS=YPREIS+(YEXTRA*((YMENGE\YLOS)+1))  ;PLUS ZUSATZKOSTEN JE LOSGRÖSSE ;plus additional costs once 
	
	;+++++++++++++++++++++++++++++++++++++++
	;  (*** 3 ***)
	;+++++++++++++++++++++++++++++++++++++++
	
	; DAUER RECHNEN ;duration based
	; =============================
	IF +YMENGE=0 IF +YDAUER'=0 DO 
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;DO  ;RUESTZEITEN
	. . IF YARTIKEL'="" IF YLFN'="" SET YDAUER=YDAUER+$$^INARTRUESTT(YARTIKEL,YLFN) QUIT
	. . SET YDAUER=YDAUER+$$^INARTRUEST(YART)
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	. ;
	. IF YEINHEIT=2  SET YPREIS=YPREIS*(YDAUER/3600)  QUIT   ;STUNDEN ;hours
	. IF YEINHEIT=10 SET YPREIS=YPREIS*(YDAUER/60)    QUIT   ;MINUTEN
	. IF YEINHEIT=11 SET YPREIS=YPREIS*(YDAUER/100)   QUIT   ;INDUSTIEMINUTEN
	
	; FIXME : What if we sell to INDONESIA?  One product worth 100,000,000
	;         rupiah (maybe $15,000) - reset price to zero <GRF>
	;         Maybe other countries with similar issues. (Vietnam is worse again - around $6000)
	IF YPREIS>100000000 SET YPREIS=0
	QUIT $JUSTIFY(YPREIS,0,2)
]]></Routine>
</Export>