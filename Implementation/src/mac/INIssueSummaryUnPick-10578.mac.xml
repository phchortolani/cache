<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INIssueSummaryUnPick" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INIssueSummaryUnPick
	quit

	
#include COMSYS
#include COMConst
#include INConst
#include COMGridEdit31
#include %occInclude

CanUnPick(pidIssue,pYFELD)
	;-------------------------------------------------------------------------------
	; Description : Determines if Button should be enabled
	;			
	; Called By : INIssue FORMSPEC - Button(2) UnPick
	;
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 25-Mar-2013	SCR		HEVA-811: Check for null pidIssue
	; 16-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new strStatus

	set strStatus = $$$OK
	
	; HEVA-811 vvvv
	if pidIssue="" {
			$$$YQHandler(strStatus)
			quit
	}
	; HEVA-811 ^^^^


	if '$data(^INIssueLine(0,pidIssue)) {
		set strStatus = $$$MakeStatus("IN01334")    ; "Pick Lines not created"
	} elseif '$data(^INIssueSummary(0,pidIssue)) {
		set strStatus = $$$MakeStatus("IN01336")    ; "Invalid Operation"	
	} else {
		if ($$$INIssueStatus(pYFELD) > 1) {
			set strStatus = $$$MakeStatus("IN01333")    ; "Invalid Operation for Current Status."			
		}
	}

	; MOBILE-52 vvvv
	if ($$$ISOK(strStatus)){
		set objIssue = $get(^INIssue(0,pidIssue,1))
		if $$$INIssueFREE11(objIssue) = 1 {
			set strStatus = $$$MakeStatus("INIssue001")
		}
	}
	; MOBILE-52 ^^^^
	
	$$$YQHandler(strStatus)
	
	quit

CreateUnPick(pidIssue)
	;-------------------------------------------------------------------------------	
	; Creation of the PickList Lines
	;
	; Params:	pidIssue	- ISsue
	;
	; Returns:	
	;
	; History:
	; 15-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------	
	new strStatus
	new objIssue
	
	set strStatus = $$$OK
	 			 
	set objIssue = $get(^INIssue(0,pidIssue,1))
	do CanUnPick(pidIssue, objIssue)

	//Operation available only if bothe INIssueSummary & INIssueLine are present
	if $data(^INIssueLine(0,pidIssue)) && ($data(^INIssueSummary(0,pidIssue))) {
		 
		if YQ = $$$YQEnable {
			set strStatus = $$Transaction^COMTransaction("CreateUnPickTxn^INIssueSummaryUnPick("""_pidIssue_""")",$$$YES)	 		
		} else {
			do ReturnError^COMUtils($$$YQMessage)
		}
		
	}

	if $$$ISOK(strStatus) {
			do GoToForm^COMUtilForm("INIssue",pidIssue,1)		//Go to Tab 1
	}

	quit strStatus

CreateUnPickTxn(pidIssue)
	;-------------------------------------------------------------------------------
	;	UnPick Lines INIssueLine (Reverse Pick)
	;	
	; Called By :
	;	(Button) UnPick (FORMSPEC) INIssue
	;		
	; Inputs : 
	;	pidIssue		-	INIssue ID
	;	
	;
	; ByRef :
	;
	; Returns :
	;
	;
	; History :
	; 17-Jun-2013	SCR		CORE-94: Clear Reservations
	; 04-Apr-2013	SCR		HEVA-811: Clear printed
	; 16-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new strStatus
	new objIssue
	new idLocn
	new idToLocn
	new idLine
	
	set strStatus = $$$OK
	set objIssue 	= $get(^INIssue(0,pidIssue,1))
	set idLocn 		= $$$INIssueFromLocn(objIssue)
	set idToLocn 	= $$$INIssueToLocn(objIssue)
	
 	if $data(^INIssueLine(0,pidIssue)) {
		
		set idLine=""
		//Clear all Pick Lines
		for {
			quit:$$$ISERR(strStatus)
			set idLine = $order(^INIssueLine(0,pidIssue,idLine))
			quit:idLine=""
			
			//SR18002.2
			if $$$ISOK(strStatus) {
				do ClearReservation^INReservation(pidIssue,11,,idLine) ; CORE-94
			}
			if $$$ISOK(strStatus) {
				do ^WWWSKILL("INIssueLine",pidIssue_$$$COMMA_idLine)
			}
		}
	}

	if $$$ISOK(strStatus) {
		set $$$INIssuePickTimestamp(objIssue)		= ""
		set $$$INIssuePickUser(objIssue)			= ""
		set $$$INIssuePickPrintedTimestamp(objIssue)= "" ; HEVA-811
		set $$$INIssuePickPrintedUser(objIssue)		= "" ; HEVA-811
		set strStatus = $$$Save("INIssue",pidIssue,objIssue,$$$YES)
	}
	
	quit strStatus
	
]]></Routine>
</Export>