<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFRAME" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFRAME(X,YFIX,YLIGHT,YID,YTABLE,APPLYBORDER=1,YHEIGHT="") ;SR17862
	
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		RAHMEN MIT RAHMEN
	;		Framework
	;
	; Inputs : 
	;	startet einen Rahmen mit den vorgegebenen rahmenbedingungen
	;	starts a fraemwork with the given conditions
	;	
	;	X	= 0      ; Start frame 100%
	;	X	= 1      ; Terminate frame
	;	X	= 2      ; Start frame without % 
	;	X	= 3      ; starten rahmen wie hintergrund z.b,grau
	;				 ; start frame like background e.g., grey
	;	X	= NN     ; Start frame with NN=% width
	;	X	= NNN    ; PICELBREITE - pixel width
	;	
	;	YFIX	= 1    	dann ist der Rand des Tables scrollbar
	;					there is a scrollbar on the edge of the table
	;	YLIGHT	= 1     =farbe an hintergrund angepasst ; color adapted to background
	;	YID		= ID 	the table id
	;	YTABLE	= 0		Normal frame
	;			= 1		table (wegen hintergrundfarbe=zeilenfarbe oder hintergrund) ;or 
	;
	; ByRef :
	;	YFORM
	;
	; Returns :
	;
	;
	; History :
	; 13-Dec-2006	JW		BR014262: Doco, clean up.
	; 27-Nov-2006	GRF		SR15232: Doco; quits; YCR
	; 11.10.1999	DT
	;-------------------------------------------------------------------------------
	SET YFIX=$GET(YFIX)
	SET X=+$GET(X)
	IF X=1 {
		DO STOP
	} else {
		DO START
	}
	QUIT
	
	;+++++++++++++++++++++++++++++++++++++++
	; YVOR		objFORM		(WWW120)
	; YVOR1		objCOMPANY	(WWW012)
	;+++++++++++++++++++++++++++++++++++++++
	
START
	;-------------------------------------------------------------------------------
	; Start frame as HTML <TABLE>, incrementing table level
	; 
	; Passed as Arguments : X,YFIX,YLIGHT,YID,YTABLE
	;
	; By Ref :
	;	YTABLEANZ	Table level
	; 
	; Returns by ref:
	;	YVOR1		objWWW012
	; 
	; History :
	; 22-Sep-2010	GRF		SR17515: "FARBE" property is not language based; Macros;
	; 							$get not required for YVOR; dot to brace format
	;-------------------------------------------------------------------------------
	new YVOR
	
	if '$data(YVOR1)   set YVOR1 = $get(^WWW012(0,YM,1))
	set YVOR=""
	if $get(YFORM)'="" set YVOR  = $get(^WWW120(0,YFORM,1))  ;FIS;08.12.04;26564
	
	;	D49		$$$WWW013EmployeeCategory()
	if $$$WWW013EmployeeCategory($get(^WWW013(0,YBED,1)))=998 new YVOR1X set YVOR1X=YVOR1 new YVOR1 do  ;USER = INTERNETLOGIN;FIS;15.12.2004
	. set YVOR1 = YVOR1X
	. set $$$WWW012BackgroundColor(YVOR1)      = 137    ; Background Colour  (White)
	. set $$$WWW012ColumnColourDarkness(YVOR1) = 137    ; Column Colour Dark (White)
	. set $$$WWW012FrameBodyColor(YVOR1)       = 137    ; Frame Body Colour  (White)
	. set $$$WWW012FrameColorForLight(YVOR1)   =   8    ; Light Frame Colour (Black)
	. set $$$WWW012FrameColorForDark(YVOR1)    = 137    ; Dark Frame Colour  (White)
	
	set YTABLEANZ = $get(YTABLEANZ)+1
	
	write YCR,"<TABLE"
	if $get(YID)'="" write " id="""_YID_""""
	
	;SR17862 vvvvvv
	;SR17862 ; Using style sheet
	;SR17862 if $$$WWW120StylesheetCSSFile(YVOR)="" {
	;SR17862 	write " border=1"
	;SR17862 } else {
	;SR17862 	write " border=0 class=""table"""
	;SR17862 }
    if $$$WWW120StylesheetCSSFile(YVOR)="" {                    
		if (APPLYBORDER = $$$YES) {
        	write " border=1"
        } else {                                  
        	write " border=0 " ;SR17871
        	set strStyle="border:1px solid gainsboro; " ;SR17871
        }
    } else {
        write " border=0 class=""table"""
    }
	;SR17862 ^^^^^
	
	write " NOWRAP"

	if YFIX=1 write YCR," STYLE=""table-layout:fixed"""
	
	; WIDTH
	;---------------------------------------
	if X>100 {
		write YCR," WIDTH="_X       ; Pixels over 100
	
	} elseif X>3 {
		write YCR," WIDTH="_X_"%"   ; Percentage to 100, special for 2 or 3
		
	} elseif (X'=2) && (X'=3) {
		write YCR," WIDTH=100%"
	}
	
	if $$$WWW120StylesheetCSSFile(YVOR)="" {
		if $$$WWW012FrameBodyColor(YVOR1)'="" {
			if $$$WWW012ColumnColourDarkness(YVOR1)="" set $$$WWW012ColumnColourDarkness(YVOR1) = $$$WWW012FrameBodyColor(YVOR1)
			if $get(YTABLE)=$$$YES {
				write YCR," bgcolor="""_$piece($get(^WWW100(0,"FARBE","EN",$$$WWW012ColumnColourDarkness(YVOR1),1)),Y,1)_""""
			} else {
				write YCR," bgcolor="""_$piece($get(^WWW100(0,"FARBE","EN",$$$WWW012FrameBodyColor(YVOR1),1)),Y,1)_""""
			}
		}
	
		if $$$WWW012FrameColorForDark(YVOR1)'="" {
			write YCR," BORDERCOLORDARK="_""""_$piece($get(^WWW100(0,"FARBE","EN",$$$WWW012FrameColorForDark(YVOR1),1)),Y,1)_""""
		}
	}
	
	
	if $get(YLIGHT)=$$$YES {
		if $$$WWW012BackgroundColor(YVOR1)'=""    write YCR," BORDERCOLORLIGHT="""_$piece($get(^WWW100(0,"FARBE","EN",$$$WWW012BackgroundColor(YVOR1),1)),Y,1)_""""
	
	} else {
		if $$$WWW012FrameColorForLight(YVOR1)'="" write YCR," BORDERCOLORLIGHT="""_$piece($get(^WWW100(0,"FARBE","EN",$$$WWW012FrameColorForLight(YVOR1),1)),Y,1)_""""
	}
	write " cellspacing=0>",YCR
	
	quit
	
oldSTART ;STARTEN RAHMEN ;launching framework 
	;-------------------------------------------------------------------------------
	; 
	; Passed as Arguments : X,YFIX,YLIGHT,YID,YTABLE
	;
	; By Ref :
	;	YTABLEANZ	Table level
	; 
	; Returns by ref:
	;	YVOR1		objWWW012
	;-------------------------------------------------------------------------------
	new YPROZ,YVOR
	
	IF '$DATA(YVOR1) SET YVOR1=$GET(^WWW012(0,YM,1))
	SET YVOR=""
	IF $GET(YFORM)'="" SET YVOR=$GET(^WWW120(0,YFORM,1))  ;FIS;08.12.04;26564
	
	;	D49		$$$WWW013EmployeeCategory()
	IF $PIECE($GET(^WWW013(0,YBED,1)),Y,49)=998 NEW YVOR1X SET YVOR1X=YVOR1 NEW YVOR1 DO  ;USER = INTERNETLOGIN;FIS;15.12.2004
	. SET YVOR1=YVOR1X
	. SET $PIECE(YVOR1,Y,5)  = 137    ; Background Colour  (White)   ;HINTERGRUNDFARBE WEISS
	. SET $PIECE(YVOR1,Y,112)= 137    ; Column Colour Dark (White)   ;HINTERGRUNDFARBE WEISS
	. SET $PIECE(YVOR1,Y,77) = 137    ; Frame Body Colour  (White)   ;HINTERGRUNDFARBE WEISS
	. SET $PIECE(YVOR1,Y,14) =   8    ; Light Frame Colour (Black)   ;RAHMENFARBE SCHWARZ
	. SET $PIECE(YVOR1,Y,15) = 137    ; Dark Fram Colour   (White)   ;RAHMENFARBE WEISS
	
	DO
	. SET YTABLEANZ=$GET(YTABLEANZ)+1
	. WRITE YCR,"<TABLE"
	. IF $GET(YID)'="" WRITE " id="""_YID_""""                                       ;TABLE ID;FIS;11.02.04;24587
	. ;
	. IF $PIECE($GET(YVOR),Y,83)=""  WRITE " BORDER=1"
	. IF $PIECE($GET(YVOR),Y,83)'="" WRITE " border=0 class=""table"""         ;FIS;CUSTOMER CSS STYLESHEET;08.12.04;26564
	. ;
	. WRITE " NOWRAP"
	. SET YPROZ=100                                                                     ;WEITE IN PROZENT ;offset within percentage 
	. IF YFIX=1 WRITE YCR," STYLE=""table-layout:fixed"""
	. IF +X>3 SET YPROZ=X
	. IF X<100.01 IF +X'=2 IF +X'=3 WRITE YCR," WIDTH="_YPROZ_"%"
	. IF X>100                      WRITE YCR," WIDTH="_YPROZ
	. ;
	. IF $PIECE(YVOR1,Y,77)'="" DO
	. . QUIT:$PIECE($GET(YVOR),Y,83)'=""
	. . WRITE YCR
	. . IF $PIECE(YVOR1,Y,112)="" SET $PIECE(YVOR1,Y,112)=$PIECE(YVOR1,Y,77)  ;FIS;06.04.04
	. . IF $GET(YTABLE)'=1 WRITE " bgcolor="""_$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,$PIECE(YVOR1,Y,77),1)),Y,1)_""""
	. . IF $GET(YTABLE)=1  WRITE " bgcolor="""_$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,$PIECE(YVOR1,Y,112),1)),Y,1)_""""  ;TYBD;FARBEN ANGEPASST;11,2,2004
	. ;
	. IF $PIECE(YVOR1,Y,15)'="" DO
	. . QUIT:$PIECE($GET(YVOR),Y,83)'=""
	. . WRITE YCR," BORDERCOLORDARK="_""""_$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,$PIECE(YVOR1,Y,15),1)),Y,1)_""""
	. ;
	. IF $GET(YLIGHT)=1  IF $PIECE(YVOR1,Y,5)'=""  WRITE YCR," BORDERCOLORLIGHT="""_$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,$PIECE(YVOR1,Y,5),1)),Y,1)_""""
	. IF $GET(YLIGHT)'=1 IF $PIECE(YVOR1,Y,14)'="" WRITE YCR," BORDERCOLORLIGHT="""_$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,$PIECE(YVOR1,Y,14),1)),Y,1)_""""
	. WRITE " cellspacing=0>",YCR
	
	QUIT
	
STOP ;ENDE DES RAHMENS        ;termination 
	write YCR,"</TABLE>"
	set YTABLEANZ = $get(YTABLEANZ)-1
	quit

]]></Routine>
</Export>