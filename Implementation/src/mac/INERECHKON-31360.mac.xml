<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INERECHKON" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INERECHKON
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUTOMATISCHES FESTLEGEN DES ERLÃ–SKONTOS
	;		AUTOMATIC ESTABLISHMENT OF THE REVENUE ACCOUNT
	;		
	; Called By :
	;		Form INERECH (Edit Supplier Invoice) - AfterSaveData
	;		
	; Inputs : 
	;   YKEY
	;   YFELD
	;
	; ByRef :
	;   YOK		Status returned on updating INSHIPINFO
	;
	; Returns :
	;
	;
	; History :
	; 03-Jul-2006	GRF		SR14471: Doco
	; 15.08.2001	FIS		Created
	;-------------------------------------------------------------------------------
	NEW LIEF,RECH,REDAT
	
	;+++++++++++++++++++++++++++++++++++++++
	;  YFELD	objINERECH ?
	;  LIEF1	objINLIEF
	;  POS1		objINAUFP
	;  TRANS	objINSHIPINFO
	;+++++++++++++++++++++++++++++++++++++++
	
	SET YFELD = $GET(YFELD)            QUIT:YFELD=""
	SET YKEY  = $GET(YKEY)
	SET LIEF  = $PIECE(YKEY,",",1)     QUIT:LIEF=""
	SET RECH  = $PIECE(YKEY,",",2)     QUIT:RECH=""
	SET REDAT = $PIECE(YKEY,",",3)     QUIT:REDAT=""
	
	DO KONTO   ;FESTLEGEN AUFWANDSKONTO (FALLS NOCH OFFEN) ;expense account yet 
	DO SKONTO  ;FESTLEGEN SKONTOBETRAG  (FALLS NOCH OFFEN) ;yet 
	
	IF $PIECE(YFELD,Y,127)'="" DO  ;TRANSPORTNUMMER
	. NEW TRANS,YFORM,YVOR,YOK
	. SET TRANS=$GET(^INSHIPINFO(YM,$PIECE(YFELD,Y,127),1))
	. SET $PIECE(TRANS,Y,19)=RECH  ;SPEICHERN RECHNUNGSNUMMER TRANSPORTDATEI ;Save 
	. SET YOK=$$^WWWSPEI("INSHIPINFO",$PIECE(YFELD,Y,127),TRANS,1)
	
	QUIT
	
KONTO ;ERMITTLUNG DES AUFWANDKONTOS ;DETERMINATION OF THE EXPENDITURE ACCOUNT 
	NEW AUF,POS,POS1,STEUERKZ,LIEF1,ART,BETRIEB,WAGR,SORTI,KONTOS
	
	QUIT:$PIECE(YFELD,Y,80)'=""  ;BEREITS EINGETRAGEN ;yet regd. 
	
	SET KONTOS=""                ;AUFWANDSKONTO ;expense account 
	;STEUERKENNZEICHEN (1=INLAND 0=AUSLAND)
	SET LIEF1=$GET(^INLIEF(YM,LIEF,1))
	SET STEUERKZ=$PIECE(LIEF1,Y,54)
	IF STEUERKZ="" DO
	. SET STEUERKZ=1  ;INLAND
	. IF $PIECE(LIEF1,Y,17)'="" IF $PIECE(LIEF1,Y,17)'=$GET(YCOUNTRY) SET STEUERKZ=0  ;KEIN DEUTSCHLAND ;not Germany 
	. IF $PIECE(LIEF1,Y,50)'="" IF '$FIND($PIECE(LIEF1,Y,50),"DE")    SET STEUERKZ=0  ;STEUERID AUSLAND ;foreign country 
	
	IF $DATA(^INERECH1s(YM,1,$$^WWWUMLAU(LIEF,1),$$^WWWUMLAU(RECH,1))) DO
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INERECH1s(YM,1,$$^WWWUMLAU(LIEF,1),$$^WWWUMLAU(RECH,1),AUF)) QUIT:AUF=""  DO  QUIT:KONTOS'=""
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INERECH1s(YM,1,$$^WWWUMLAU(LIEF,1),$$^WWWUMLAU(RECH,1),AUF,POS)) QUIT:POS=""  DO  QUIT:KONTOS'=""
	. . . SET POS1  = $GET(^INAUFP(YM,AUF,POS,1))
	. . . SET ART   = $PIECE(POS1,Y,4)  ;ARTIKEL ;item 
	. . . QUIT:ART=""
	. . . SET WAGR  = $PIECE($GET(^INART(YM,ART,1)),Y,30)  ;WARENGRUPPE
	. . . SET SORTI = $PIECE($GET(^INART(YM,ART,1)),Y,38)  ;SORTIMENT
	. . . IF WAGR=""  SET WAGR=" "
	. . . IF SORTI="" SET SORTI=" "
	. . . SET BETRIEB=$PIECE($GET(^INAUF(YM,AUF,1)),Y,6)  ;BETRIEB
	. . . IF BETRIEB="" SET BETRIEB=$GET(YLOCATION)
	. . . QUIT:BETRIEB=""
	. . . ;
	. . . SET KONTOS = $PIECE($GET(^INKSTL(YM,BETRIEB,WAGR,STEUERKZ,2,1)),Y,2)               ;AUS PARAMETER ;out of parameter 
	. . . IF KONTOS="" SET KONTOS=$PIECE($GET(^INKSTL1(YM,BETRIEB,SORTI,STEUERKZ,2,1)),Y,2)  ;AUS PARAMETER ;out of parameter 
	
	IF KONTOS'="" SET $PIECE(^INERECH(YM,LIEF,RECH,REDAT,1),Y,80)=KONTOS    ; Account
	
	QUIT
	
SKONTO ;ERRECHNEN DES SKONTOBETRAGES/MWST
	NEW SKTO,MWST
	
	IF $PIECE(YFELD,Y,14)="" IF $PIECE(YFELD,Y,48)="" DO
	. IF $PIECE(YFELD,Y,12)=""  QUIT
	. ;
	. SET SKTO=+$PIECE($GET(^INKOND(YM,$PIECE(YFELD,Y,12),1)),Y,3) 
	. IF SKTO=0 QUIT                   ;KEIN SKONTO ;no acct
	. IF +$PIECE(YFELD,Y,10)=0 QUIT    ;KEIN BETRAG ;no Sum 
	. SET SKTO=$JUSTIFY($PIECE(YFELD,Y,10)/100*SKTO,0,2)
	. ; FIXME : Why not use LIEF,RECH,REDAT? <GRF>
	. SET $PIECE(^INERECH(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),$PIECE(YKEY,",",3),1),Y,14)=SKTO
	. ;SET $PIECE(YFELD,Y,14)=SKTO
	. ;
	. ;ENTHALTENE MWST ;Tax 
	. SET MWST=$PIECE(YFELD,Y,6)  ;MEST KENNZEICHEN ;characteristic 
	. IF MWST="" SET MWST=1
	. IF +SKTO'=0 IF MWST'="" DO
	. . SET MWST=$PIECE($GET(^WWW101(0,"MWST",SPRACHE,MWST,1)),Y,1)
	. . QUIT:MWST=""
	. . SET MWST=$JUSTIFY((SKTO/(100+MWST)*MWST),0,2)
	. ; FIXME : Why not use LIEF,RECH,REDAT? <GRF>
	. . SET $PIECE(^INERECH(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),$PIECE(YKEY,",",3),1),Y,48)=MWST   ; Amount
	. . ;SET $PIECE(YFELD,Y,48)=MWST
	
	QUIT
]]></Routine>
</Export>