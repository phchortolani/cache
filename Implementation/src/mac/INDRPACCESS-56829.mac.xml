<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPACCESS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPACCESS(BET,PLANER) ;INDRPACCESS;FAN;DRP BERECHTIGUNGEN PRÜFEN ;11.12.2001
#include COMSYS
	;------------------------------------------------------------------------------
	; Description of Function :
	;		DRP BERECHTIGUNGEN PRÜFEN 
	;	BEI HINTERGRUNDJOB: ALLES ERLAUBT ;next to whatsoever permissive 
	;
	; Inputs : 
	;	BET			Location
	;	PLANER		BRP Planner (User)
	;	BGJOB		Flag $$$YES if a background job
	;	
	; ByRef :
	;
	;
	; Returns :
	;	Access		INDRPPLANNER record (D2=> set to $$$YES if no planners set up or a background job)
	;
	;						Class INDRPPLANNER
	;	 2 Alles erlaubt						; Everything Allowed
	;	 3 neuen demand erstellen				; Make Demand
	;	 4 Übersicht Supply/demand erlaubt		; Display Supply/Demand Permitted
	;	 5 bestellungen auslösen				; Create Purchase Order
	;	 6 Bestellungen automatisch freigeben	; Release Purchase Orders Automatically
	;	 7 D/S freigeben						; Edit & Release Demand/Supply Permitted
	;	 8 D/S lösen							; Delete Demand/Supply
	;	 9 Demand auf abgeschlossen setzen		; Set Demand As Finished
	;	10 Anzeige der Simulationen				; Display Simulation Demands
	;	11 lieferantenbestellung				; Purchase Order Supplier
	;	12 auftragsbearbeitung  Kunde			; Customer P.O. Access
	;	13 Erfassen von Simulationen			; Edit Simulations
	;	14 Artikelbearbeitung					; Item Master Access
	;	15 DRP Artikelbearbeitung				; DRP Item Master Access
	;	16 DEMAND SOFORT FEIGEBEN				; Accept Demands
	;	
	; History :
	; 30-Mar-2007	RPW		SRBR014346: Rewrite
	; 16-Dec-2005	GRF		SR13984 : Doco; Boolean Macros
	; 11.Dec.2001	FAN		Created
	;-----------------------------------------------------------------------------
	NEW ACCESS,blnBackground
	
	SET ACCESS=""
	
	set blnBackground=$get(BGJOB,$$$NO)
	
	SET BET=$GET(BET)  ;BETRIEB
	IF BET="" SET BET=YLOCATION
	IF BET="" QUIT ACCESS
	
	SET PLANER=$GET(PLANER)  ;PLANUNGSMITARBEITER  ;FIS, 18.12.01
	IF PLANER="" SET PLANER=YBED
	IF PLANER="" QUIT ACCESS
	
	;EIGENE BERECHTIGUNG PRÜFEN  (WENN KEIN PLANER ANGELEGT IST, HAT JEDER ALLE BERECHTIGUNGEN)
	; System Administrators are permitted full access
	; Get user permissions                              ;BERECHTIGUNGEN DES MITARBEITERS
	; All users are authorised if no planners defined   ;ALLE BERECHTIGUNGEN, WENN KEIN PLANER ANGELEGT
	IF 'blnBackground {
		IF +$$^WWWBEDBER(YBED)=1 {
			SET $PIECE(ACCESS,Y,2) = $$$YES ;SYSTEMADMIN
		} elseIF $DATA(^INDRPPLANNER(YM,BET,PLANER,1)) {
			SET ACCESS=$get(^INDRPPLANNER(YM,BET,PLANER,1))
		} elseIF '$DATA(^INDRPPLANNER(YM,BET)) {
			SET $PIECE(ACCESS,Y,2) = $$$YES
		}
	}
	
	; Set all authorisations to permitted      ;ALLES ERLAUBT
	if blnBackground || ($piece(ACCESS,Y,2)) {
		SET MAX=$ORDER(^WWW003(0,"INDRPPLANNER",""),-1)          ;ANZAHL DATENFELDER  ;FIS, 18.12.01
		IF +MAX=0 SET MAX=50
		FOR YI(1)=3:1:MAX SET $PIECE(ACCESS,Y,YI(1)) = $$$YES
	}
	
	QUIT ACCESS
]]></Routine>
</Export>