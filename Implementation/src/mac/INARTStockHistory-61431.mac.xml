<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTStockHistory" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTStockHistory
	
#include COMConst
#include INConst
#include COMView
#include %occInclude
	
oldListHistory(pidItem="")
	;-------------------------------------------------------------------------------
	;	Show COMView with "Stock History" for the Item
	; 
	; Called by : 
	;
	; History:
	; 25-Aug-2008	PPP		SR15866: Created
	;-------------------------------------------------------------------------------
	new idClass,idOperator
 
 	set idClass = "alSOH.dStockHistory"
 	
	do Initialise^COMViewCustom(idClass,"",$$$NO,$$$NO,$$$NO) 
	
	$$$ClearExternalFilter(idClass)
 
	if pidItem'="" {
		set idOperator = $$$EnumCOMVIEWCOMPARATOREquals
	} else {
		set idOperator = $$$EnumCOMVIEWCOMPARATORStartsWith
	}
 
	do AddExternalFilter^COMViewFilter(idClass,"Item->ItemId",pidItem,idOperator,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"Location->Code",YLOCATION,   $$$EnumCOMVIEWCOMPARATOREquals,     $$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"DateConfirmed",($horolog-30),$$$EnumCOMVIEWCOMPARATORGreaterThan,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"DateConfirmed",($horolog+1), $$$EnumCOMVIEWCOMPARATORLessThan,   $$$YES)
	
	do Show^COMViewCustom()
 
	quit $$$OK
 
	
ListHistory(pidItem,pstrKeys="")
	;-------------------------------------------------------------------------------
	;	Show COMView with "Stock History" for the Item
	; 
	; Called by : 
	;
	; History:
	; 13-Nov-2009	PPP		SR17023:
	;						1. Added more parameters to pstrKeys
	;							KEY5 - Movement Transaction Type List
	;							KEY6 - SubType List
	;						2. Added Filters based on new 'KEYS' #5, #6
	; 29-Apr-2009	PPP		SR16499: Updated the parameters with list, used by
	; 							Select^INSOHPeriodBalance to predefine Item,
	; 							Location, StartDate, EndDate, Transaction Type
	; 27-Apr-2009	shobby	SR16108: Display a back arrow.
	; 15-Jan-2009	DavidR	SR16297: Modified to run a routine on selection of record
	; 25-Aug-2008	PPP		SR15866: Created
	;-------------------------------------------------------------------------------
	new dteEnd,dteStart,idClass,idLocn,idOperator,idTxn
	new strMovList
	new strSubTypeList
 
	if pstrKeys'="" {
		set idLocn   = $$$KEY1(pstrKeys)
		set dteStart = $$$KEY2(pstrKeys)
		set dteEnd   = $$$KEY3(pstrKeys)
		set idTxn    = $$$KEY4(pstrKeys)
		//SR17023
		set strMovList    		= $translate($$$KEY5(pstrKeys),":",",")
		set strSubTypeList    	= $translate($$$KEY6(pstrKeys),":",",")
	}
 
 	set idClass = "alSOH.dStockHistory"
	do Initialise^COMViewCustom(idClass,"Selected^INARTStockHistory",$$$NO,$$$NO,$$$NO)  ;SR16297
	
	$$$ClearExternalFilter(idClass)
	
	if pidItem'="" {
		set idOperator = $$$EnumCOMVIEWCOMPARATOREquals
	} else {
		set idOperator = $$$EnumCOMVIEWCOMPARATORStartsWith
	}
 	if $get(idLocn)=""   set idLocn   = YLOCATION
	if $get(dteStart)="" set dteStart = $horolog-30
	if $get(dteEnd)=""   set dteEnd   = $horolog+1
	
	/*		
 	do AddExternalFilter^COMViewFilter(idClass,"SOHPeriod->StartDate",$horolog-30,$$$EnumCOMVIEWCOMPARATORGreaterThan,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"SOHPeriod->EndDate","",$$$EnumCOMVIEWCOMPARATORLessThan,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"Item->ItemId",pidItem,idOperator,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"Location->Code",YLOCATION,$$$EnumCOMVIEWCOMPARATOREquals,$$$YES)
	*/
	
	do AddExternalFilter^COMViewFilter(idClass,"Item->ItemId",pidItem,idOperator,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"Location->Code",idLocn, $$$EnumCOMVIEWCOMPARATOREquals,     $$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"DateConfirmed",dteStart,$$$EnumCOMVIEWCOMPARATORGreaterThan,$$$YES)
	do AddExternalFilter^COMViewFilter(idClass,"DateConfirmed",dteEnd,  $$$EnumCOMVIEWCOMPARATORLessThan,   $$$YES)
	
	if $get(idTxn)'="" {
		do AddExternalFilter^COMViewFilter(idClass,"TxType->TxnID",idTxn, $$$EnumCOMVIEWCOMPARATOREquals,   $$$YES)
	} 

	//SR17023
	if $get(strMovList)'="" {
		do AddExternalFilter^COMViewFilter(idClass,"MovementType_MovementTypeId",strMovList, $$$EnumCOMVIEWCOMPARATORWithin, $$$YES)
	} 
	if $get(strSubTypeList)'="" {
		do AddExternalFilter^COMViewFilter(idClass,"SubType",strSubTypeList, $$$EnumCOMVIEWCOMPARATORContains,   $$$YES)
	} 
	
	do Show^COMViewCustom($$$YES)   //16108
	
	quit $$$OK
	
	
Selected(pobjResultSet) 
	;-------------------------------------------------------------------------------
	; Open original transaction document
	;
	; Params:
	;
	; Returns:
	; The record that has been selected will redirect to the correct originating form
	; opening the record. Will alert you if there is no originating form
	;
	; History:
	; 06-Jul-2009	DWR		SR16607: If the orginating form is INREC, check for
	; 							existence of INRECSummary Record. If exists change
	; 							the form to INReceipt.
	; 12-Feb-2009	HQN		SR16341: Deprecated block
	; 04-Feb-2009	HQN		SR16341: Utilise new MovementType to identify Source Form
	; 14-Jan-2009	GRF/DavidR: SR16297 Created
	;-------------------------------------------------------------------------------
	new idForm,idItem,idRecord,idTx,idTxType,objHistory,objINREC,objTxType,sc 
	
	while pobjResultSet.Next(.sc) {
		set idRecord   = $get(pobjResultSet.Data("ID"))
		set objHistory = ##class(alSOH.dStockHistory).%OpenId(idRecord)
		
		if objHistory '= $$$NULLOREF {
			; 04-Feb-2009
			if objHistory.MovementType = $$$NULLOREF {
				$$$Alert($$$MakeStatus("IN00768"))   		// "This record has no source transaction."
				
			} else {
				set idTx = objHistory.Reference
				if idTx = "Rec without Order" {
					$$$Alert($$$MakeStatus("IN00769")) 		// "This record was received without an order."
					
				} else {
					set idItem = objHistory.Item.ItemId
					set idForm = $$GetFormViaMovementType(objHistory.MovementType.MovementTypeId)
					
					if idForm = "INREC" {     ; SR16607
						if $data(^INRECSummary(YM,idTx)) {
							set idForm = "INReceipt"
						}
					}
					
				//	if idForm'="" do RedirectForm^COMUtilForm(idForm,idTx,$get(YBACK),$get(YPARA),$get(YSEITE)) quit
					if idForm'="" do GoToForm^COMUtils(idForm,idTx,,,$$$YES,)
				}
			}
		}
	}
	quit
	
	
CallForm(pidForm,pidTx,YKEY,YBACK,YFKEY)
	;-------------------------------------------------------------------------------
	; Isolate YBACK and YKEY
	; 
	; History:
	; 14-Jan-2009	GRF		SR16297: Copied form INADJLoad
	;-------------------------------------------------------------------------------
	do GoToForm^COMUtilForm(pidForm,pidTx)
	;if pidAdj = "" {
	;	do GoToForm^COMUtilForm("INListBundles",YFKEY)
	;} else {
	;	do GoToForm^COMUtilForm(pidForm,pidTx)
	;}
	
	quit
	
	
GetFormViaMovementType(pidMovementType="")
	;-------------------------------------------------------------------------------
	; Hardcoded entry that maps a MovementTypeId to a @nm Form
	; 
	; History:
	; 03-May-2011	PPP		SR17715: Added  movment type #51 - INMOVByPallet
	; 29-Oct-2010	PPP		SR17586: Adjustment Batch for the Stocktake - #41 - INADJ
	; 28-Jun-2010	PPP		SR17297: Added  movment type #8 - INReturnToSupplier
	; 17-Apr-2009	PPP		SR16293: Added  movment type #22 - INIssueRev
	; 13-feb-2009	GRF		SR16341: doesn't need array - faster
	; 09-Feb-2009	HQN		SR16341: Corrected MovementType 7, Supplier reverse not TFR
	; 05-Feb-2009	HQN		SR16341: Created
	;-------------------------------------------------------------------------------
	quit $case(pidMovementType,0:"INAUF",
	                           1:"INREC",
	                           5:"INMOV",
	                           7:"INRECReverse",
	                           8:"INReturnToSupplier",
	                          11:"INIssue",
	                          12:"INReservation",
	                          21:"INDispenseToPatient",
	                          22:"INIssueRev",
	                          30:"INTFR",
	                          31:"INRECTFR",
	                          32:"INTFRRev",
	                          40:"INADJ",
	                          41:"INADJ",
	                          51:"INMOVByPallet",
	                  "On Build":"",
	                            :"")
	
]]></Routine>
</Export>