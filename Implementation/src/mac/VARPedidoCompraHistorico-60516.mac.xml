<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPedidoCompraHistorico" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPedidoCompraHistorico
#include COMSYS
#include INConst
#include VARConst
 
OnAfterPrimaryKey()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo OnAfterPrimaryKEY do form VARPedidoCompraHistorico
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	set VORG(1)=$piece(YKEY,YKOMMA,1)
	quit
 
GetInformacaoAfetada()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	DefaultVariableInput do Campo (7)-Informação Afetada do form VARPedidoCompraHistorico
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit $case($$$VARPedidoCompraHistoricoItem(YFELD), "":"", 0:"Documento",:"Produto")
 
GetCodigo()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	DefaultVariableInput do Campo (8)-Código do form VARPedidoCompraHistorico
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new strCodigo, objPedidoCompraLinha, objProduto
	
	set strCodigo = ""
	if ($$$VARPedidoCompraHistoricoItem(YFELD) = 0) {
		set strCodigo = "("_$$$VARPedidoCompraHistoricoItem(YFELD)_")&nbsp;"_"Pedido"
	}
	
	elseif ($length($$$VARPedidoCompraHistoricoItem(YFELD)) > 0) && ($$$VARPedidoCompraHistoricoItem(YFELD) '= 0) && ($length(VORG(1)) > 0) {
		set objPedidoCompraLinha = $get(^VARPedidoCompraLinha(0, VORG(1),$$$VARPedidoCompraHistoricoItem(YFELD), 1))
		set objProduto = $get(^INART(0, $$$VARPedidoCompraLinhaProduto(objPedidoCompraLinha), 1))
		set strCodigo = "("_$$$VARPedidoCompraLinhaProduto(objPedidoCompraLinha)_")&nbsp;"_$$$INARTSearchName(objProduto)
	}
 
	quit strCodigo 
 
RegistrarAlteracao(pidPedidoCompra, pintAcao, pintItem, pintQuantidade, pstrMotivo)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterSaveHook^VARPedidoCompra.mac.
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit $$RegistrarAlteracaoIdentificada($horolog, $get(YBED), pidPedidoCompra, pintAcao,pintItem, pintQuantidade, pstrMotivo)
 
RegistrarAlteracaoIdentificada(pdatAlt, pstrUsuario, pidPedidoCompra, pintAcao, pintItem,pintQuantidade, pstrMotivo)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função RegistrarAlteracao^VARPedidoCompraHistorico.mac.
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new objPedidoCompraHistorico, idHistAlt, strStatus, strLog
 
	kill objPedidoCompraHistorico
	set $$$VARPedidoCompraHistoricoData(objPedidoCompraHistorico) 	 	= pdatAlt
	set $$$VARPedidoCompraHistoricoUsuario(objPedidoCompraHistorico) 	= pstrUsuario
	set $$$VARPedidoCompraHistoricoAcao(objPedidoCompraHistorico) 	 	= pintAcao
	set $$$VARPedidoCompraHistoricoItem(objPedidoCompraHistorico) 	 	= pintItem
	set $$$VARPedidoCompraHistoricoQuantidade(objPedidoCompraHistorico) = pintQuantidade
	set $$$VARPedidoCompraHistoricoMotivo(objPedidoCompraHistorico) 	= pstrMotivo
 
	set idHistAlt = $$^WWWNEXT1("VARPedidoCompraHistorico", pidPedidoCompra, 2)
	set strStatus = $$$Save("VARPedidoCompraHistorico",pidPedidoCompra_YKOMMA_idHistAlt,objPedidoCompraHistorico,1)
 
	set strLog = "Data/hora='"_$zdatetime($horolog, 3, 1)_"', "_
		"usuário="_$get(YBED)_", "_
		"ação="_$$GetDescricaoAcao(pintAcao)_", "_
		"item="_pintItem_", "_
		"quantidade="_pintQuantidade_", "_
		"status="_strStatus_", "_
		"motivo='"_pstrMotivo_"'"
 
	quit strStatus
 
GetDescricaoAcao(pintAcao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função RegistrarAlteracaoIdentificada^VARPedidoCompraHistorico.mac.
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	if ('$data(^VARPARA(YM, "TIPOHISTORICO", SPRACHE, pintAcao, 1))) {
		quit "Desconhecida"
	}
	
	quit $$$VARPARAText(^VARPARA(YM, "TIPOHISTORICO", SPRACHE, pintAcao, 1))
]]></Routine>
</Export>