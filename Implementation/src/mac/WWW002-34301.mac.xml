<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW002" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW002
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SETZEN BEZEICHNUNG UND FELDLÄNGEN IN WWW002
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 18.11.1997	DT
	;-------------------------------------------------------------------------------
	NEW DATEI,LFN,YART
	
	SET DATEI = $PIECE(YKEY,",",1)         QUIT:DATEI=""
	SET LFN   = $PIECE(YKEY,",",2)         QUIT:LFN=""
	
	SET YXCHAR=""
	IF $PIECE($GET(^WWW002(0,DATEI,LFN,1)),Y,25)="" DO   ;SETZEN PROPERTY NAME ;typeset Name 
	. NEW LFN1,ATTNU,RESN,ATTX
	. KILL ^WWWSOR(YUSER)
	. FOR I=128:1:255 SET YXCHAR=YXCHAR_$CHAR(I)
	. SET ^WWWSOR(YUSER,"Mandant")=""
	. SET RESN=""
	. FOR  SET RESN=$ORDER(^WWWRES(0,RESN)) QUIT:RESN=""  DO
	. . SET ATTX=$$^WWWUMLAU($PIECE($GET(^WWWRES(0,RESN,1)),Y,1),5)
	. . QUIT:ATTX=""
	. . SET ^WWWSOR(YUSER,ATTX)=""
	. ;
	. SET LFN1=""
	. FOR  SET LFN1=$ORDER(^WWW003(0,DATEI,LFN1)) QUIT:LFN1=""  DO
	. . SET ATTX=$PIECE($GET(^WWW003(0,DATEI,LFN1,1)),Y,25)
	. . IF ATTX'="" SET ^WWWSOR(YUSER,$$^WWWUMLAU(ATTX,1))=""
	. ;
	. SET LFN1=""
	. FOR  SET LFN1=$ORDER(^WWW002(0,DATEI,LFN1)) QUIT:LFN1=""  DO
	. . SET ATTX=$PIECE($GET(^WWW002(0,DATEI,LFN1,1)),Y,25)
	. . IF ATTX'="" SET ^WWWSOR(YUSER,$$^WWWUMLAU(ATTX,1))=""
	. ;
	. DO
	. . SET ATTX=$TRANSLATE($EXTRACT($$^WWWUMLAU($PIECE($GET(^WWW002(0,DATEI,LFN,1)),Y,1),5),1,25)," .-,'`´/+*%&();:!²³_"_YXCHAR)
	. . IF $EXTRACT(ATTX)=0!(+$EXTRACT(ATTX)'=0) FOR  QUIT:$EXTRACT(ATTX)'=0&(+$EXTRACT(ATTX)=0)  SET ATTX=$EXTRACT(ATTX,2,999)  ;KEINE ZAHLEN AM ANFANG;FIS;21.04.04;25326
	. . IF ATTX'="" SET ATTXX=ATTX FOR ATTNU=1:1 DO  IF '$DATA(^WWWSOR(YUSER,$$^WWWUMLAU(ATTX,1))) SET ^WWWSOR(YUSER,$$^WWWUMLAU(ATTX,1))="" SET ^WWWSOR(YUSER,222,LFN)=ATTX QUIT
	. . . IF '$DATA(^WWWSOR(YUSER,$$^WWWUMLAU(ATTX,1))) QUIT
	. . . SET ATTX=ATTXX_ATTNU
	. ;
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,25)=ATTX
	. KILL ^WWWSOR(YUSER)
	
	;---------------------------------------
	DO ^WWWSSORT("^WWW002(0,"_""""_DATEI_""""_","_LFN_",1)")
	;---------------------------------------
	
	IF $PIECE($GET(^WWW002(0,DATEI,LFN,1)),Y,2)="" SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,2)=$PIECE($GET(^WWW002(0,DATEI,LFN,1)),Y,1)
	SET YART=$PIECE($GET(^WWW002(0,DATEI,LFN,1)),Y,3)
	IF $PIECE($GET(^WWW002(0,DATEI,LFN,1)),Y,8)'=""  DO
	. QUIT:$PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=1  ;DATUM  TYBD 20.02.2002 ;Date 
	. QUIT:$PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=7  ;UHRZEIT  ;TYBD 20.02.2002
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=6
	
	IF (YART=14)!(YART=3)!(YART=11) DO
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=6
	. SET YART=6
	
	IF YART=1 DO  ;DATUM ;Date
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)=10
	
	IF YART=2 DO  ;JA/NEIN
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)=1
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,8)="WWW100"
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,9)=""""_"JA/NEIN"_""""_",SPRACHE"
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,10)=1
	. ;SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,20)=1
	
	IF YART=7 DO  ;ZEIT ;time 
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)=5
	
	IF YART=13 DO  ;IP
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)=15
	
	IF YART=14 DO  ;STEMPEL ;timestamp 
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)=19
	
	IF YART=15 DO  ;COLLECT
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=6
	
	IF YART=16 DO  ;EMBEDED
	. SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=6
	
	SET LFFF=""
	FOR  SET LFFF=$ORDER(^WWW003(0,DATEI,LFFF)) QUIT:LFFF=""  DO
	. SET SATZ=$GET(^WWW003(0,DATEI,LFFF,1))
	. IF $PIECE(SATZ,Y,22)=1 DO   ;IST KEY          ; NOTE : $$$WWW003UniqueKey() [D22] is deprecated <GRF>
	. . SET $PIECE(^WWW003(0,DATEI,LFFF,1),Y,22)=0  ; FIXME : This should update index 2 as well
	
	IF $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)="" SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,4)=30
	IF $PIECE(^WWW002(0,DATEI,LFN,1),Y,3)="" SET $PIECE(^WWW002(0,DATEI,LFN,1),Y,3)=6
	QUIT
	
GetDescription(pidClass="",pintField="")
	;-------------------------------------------------------------------------------
	; Returns the field description taking language and customisation in to account.
	; Global variable SPRACHE is assumed.
	;
	; Params: 	pidClass - Id of the class.
	; 			pintField - Field number.
	;
	; ByRefs:
	;
	; Returns: Form Description
	;
	; History:
	; 11-Oct-2006	shobby	SRBR014207: Created
	;-------------------------------------------------------------------------------
	new strDescription,objWWW002,objWWW1201,objWWW121D,objWWW121D1,objWWW0021
	
	; FIXME: Should not be using this -- use WWWFELDNAME
	
	set strDescription=""
	if (pidClass'="")&&(pintField'="") {
		if $get(YM)'="" {
			if $get(SPRACHE)'="" {
				set objWWW121D1=$get(^WWW121D1(0,pidClass,pintField,YM,SPRACHE,1))
				set strDescription=$$$WWW121D1FieldDescription(objWWW121D1)
			}
			if strDescription="" {
				set objWWW121D=$get(^WWW121D(0,pidClass,pintField,YM,1))
				if $$$WWW121DFieldDescription(objWWW121D)'="" {
					set strDescription=$$$WWW121DFieldDescription(objWWW121D)
				}
			}
		}
		if strDescription="" {
			if $get(SPRACHE)'="" {
				set objWWW0021=$get(^WWW0021(0,pidClass,pintField,SPRACHE,1))
				set strDescription=$$$WWW0021PropertyDescription(objWWW0021)
			}
		}
		if strDescription="" {
			set objWWW002=$get(^WWW002(0,pidClass,pintField,1))
			if $$$WWW002PropertyDescription(objWWW002)'="" {
				set strDescription=$$$WWW002PropertyDescription(objWWW002)
			}
		}
	}
	quit strDescription
 
OnBeforeSave(pidKey, pobjClassKey)
	;-------------------------------------------------------------------------------
	; Prevent save if numerator being force but the primary key type is not counter
	;
	; Params:
	; pidKey		- Key of the Primary Key Class record
	; pobjClassKey	- Primary Key Class record
	;
	; Returns: Nothing
	;
	; History:
	; 09-May-2007	RPW		SR15442: Allow type 6 (TEXT) to force numerator
	; 10-Apr-2007	RPW		SR15442: Only look at customisation for the company I am in.
	; 27-Feb-2007	PO		SR15442: Created
	;-------------------------------------------------------------------------------
	new strStatus,idClass,idPrimaryKey,objCustomClassKey
	
	set strStatus = $$$OK
	
	set idClass      = $$$KEY1(pidKey)
	set idPrimaryKey = $$$KEY2(pidKey)
	
	if '(($$$WWW002InputType(pobjClassKey) = 9) || ($$$WWW002InputType(pobjClassKey) = 6)) {
		if $$$WWW002ForceNumerator(pobjClassKey) {
			set strStatus = $$$MakeStatus("WWW00064")  ;Numerator can not be forced on a primary key that is not a counter type.
		}
 
		if $$$ISOK(strStatus) {
			set objCustomClassKey = $get(^WWW002D(0,idClass,idPrimaryKey,YM,1)) // SR15442: Just use YM
			if $$$WWW002DForceNumerator(objCustomClassKey) {
				set strStatus = $$$MakeStatus("WWW00064")  ;Numerator can not be forced on a primary key that is not a counter type.
			}
		}
	}
	
	if $$$ISERR(strStatus) {
		$$$Alert(strStatus)
		set Q = $$$QDontSave
	}
	
	quit
	
]]></Routine>
</Export>