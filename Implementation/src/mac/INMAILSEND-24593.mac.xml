<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INMAILSEND" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INMAILSEND(YREF,YMAIL,YBETREFF,YPICS,YVERSAND,YHEADER,YATTACH,NOATTNAME,NOPDF)
#include COMSYS
#include INConst
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUTOMATISCHES SENDEN VON E-MAILS
	;
	; Inputs : 
	;	YREF     = KEY AUS INDMS ODER * WENN FREIER TEXT / OR "*" IF GLOBAL IS IN TEXTX()
	;	YMAIL    = E-MAIL ADRESSE EMPFÄNGER ;address recipient 
	;	YBETREFF = SUBJECT
	;	YPICS    = MIT BILDERN SENDEN (0=NEIN 1=JA) ;send with pictures (attachments?)
	;	YVERSAND = VERSANDART (0=TEXT 1=ATTACHMENT 2=text with HTML 3=HTML within the email) ;by means of HTML inside the 
	;	YHEADER  = MAILTEXT (WENN NUR ATTCHMENT ALS PDF) ; IF ONLY ATTCHMENT AS PDF
	;	YATTACH  = ATTACHMENTS 
	;	NOATTNAME  = 1 No Name of attachments
	;	NOPDF      = 1 No PDF File
	;
	; ByRef :
	;	TEXTX    = TEXTGLOGAL (BEI TEXT-VORGABE OHNE SPEICHERUNG IN INDMS) ;WITH TEXT DEFAULT WITHOUT STORAGE IN INDMS
	;
	;
	; Returns :
	;
	;
	; History :
	; 30-Aug-2005	GRF		Realign dot levels
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 22.05.2001	FIS
	;-------------------------------------------------------------------------------
	NEW OK,YZEILE,YSEITE,UUSER,TCP,TXT,TEXT,TEXT0,YI,YII,FILE,MAXSEITE,MINZEILE,MAXZEILE
	
	SET OK		 = 0
	SET YPICS	 =+$GET(YPICS)
	SET YVERSAND =+$GET(YVERSAND)
	SET YBETREFF = $GET(YBETREFF)
	SET YATTACH  = $GET(YATTACH)
	IF YBETREFF="" SET YBETREFF="E-Mail"
	
	IF $GET(YMAIL)'="" DO   ;E-MAIL
	. ;
	. ;TEXT-VORGABE (KEINE INDMS)
	. ;-------------------------------------------------------------------------
	. IF $GET(YREF)="*" IF $DATA(TEXTX) DO
	. . ;
	. . ;VERSAND ALS PLAIN TEXT ;shipping when Text 
	. . MERGE TEXT=TEXTX
	. . ;
	. . ;VERSAND ALS HTML-TEXT ;shipping when 
	. . IF YVERSAND=2 DO
	. . . SET TEXT(0)="<HTML><BODY><PRE>"_$GET(TEXT(0))
	. . . SET TEXT($ORDER(TEXT(""),-1)+1)="</PRE></BODY></HTML>"
	. . ;
	. . ;VERSAND ALS ATTACHMENT ;shipping when 
	. . IF YVERSAND=1!(YVERSAND=2) DO
	. . . NEW XTENT,strINDMS
	. . . SET VERZ=$PIECE($GET(^WWW012(0,YM,1)),Y,47)  ;BILDERVERZEICHNIS     ;BEC;23699;YM DURCH 0 ERSETZT;26.05.03
	. . . SET FILE=$PIECE($GET(^WWW012(0,YM,1)),Y,45)  ;FTP VERZEICHNIS ;tabulation 
	. . . IF FILE="" SET OK=2 QUIT
	. . . ;SET XTENT=".html"
	. . . SET XTENT=".htm"
	. . . set strINDMS=$zconvert($GET(^INDMS(YM,YREF,1,0,1)),"U")
	. . . ;IF YREF'="" IF $FIND($$^WWWUPER($GET(^INDMS(YM,YREF,1,0,1))),"XML") SET XTENT=".xml"  ;XMLFILE 
	. . . IF YREF'="" IF $FIND(strINDMS,"XML") SET XTENT=".xml"  ;XMLFILE 
	. . . ;IF YREF'="" IF $FIND($$^WWWUPER($GET(^INDMS(YM,YREF,1,0,1))),"UNH+") SET XTENT=".edi"  ;EDIFILE 
	. . . IF YREF'="" IF $FIND(strINDMS,"UNH+") SET XTENT=".edi"  ;EDIFILE 
	. . . SET FILE=FILE_$TRANSLATE($EXTRACT($$^WWWUMLAU(YBETREFF,3),1,8),".","_")_XTENT
	. . . SET %DEV=$$^WWWDEV(FILE)
	. . . IF %DEV="" SET OK=2 QUIT
	. . . ;
	. . . ;WRITE "<HTML><BODY><PRE>"
	. . . SET YI=""
	. . . FOR  SET YI=$ORDER(TEXT(YI)) QUIT:YI=""  DO  QUIT:YI>100
	. . . . IF YPICS=1 IF VERZ'="" DO
	. . . . . IF $FIND($GET(TEXT(YI)),VERZ) DO
	. . . . . . SET TEXT(YI)=$PIECE(TEXT(YI),VERZ,1)_$PIECE(TEXT(YI),VERZ,2,9999)  ;OHNE PFAD ;without track 
	. . . . ;
	. . . . WRITE $GET(TEXT(YI)),!
	. . . ;
	. . . ;WRITE "</PRE></BODY></HTML>"
	. . . ;
	. . . CLOSE %DEV
	. . . DO OPEN^WWWSTART
	. . . KILL TEXT    ;BEC;29.07.04 ZUM TETS 
	. . . SET TEXT(1)=""
	. . . SET TEXT(2)="#attach "_FILE
	. . . ;IF YVERSAND=2 SET TEXT(2)=TEXT(2)_" :notext"  ;NOTEXT=ZEIGT ATTACHMENT INHALT NUR IN MAIL AN
	. ;
	. ;TEXT/BELEG AUS INDMS SENDEN ;out of transmit 
	. ;-------------------------------------------------------------------------
	. IF $GET(YREF)'="" IF $GET(YREF)'="*" DO
	. . SET YZEILE=0
	. . SET YSEITE=""
	. . FOR  SET YSEITE=$ORDER(^INDMS(YM,YREF,YSEITE)) QUIT:YSEITE=""  DO
	. . . SET MAXSEITE=YSEITE
	. . . ;SET MINZEILE=$ORDER(^INDMS(YM,YREF,YSEITE,""),1)  ;O=<PRE>
	. . . SET MINZEILE=$ORDER(^INDMS(YM,YREF,1,""),1)  ;O=<PRE>
	. . . IF MINZEILE>1 SET MINZEILE=1
	. . . SET MAXZEILE=$ORDER(^INDMS(YM,YREF,YSEITE,""),-1)
	. . . QUIT:MAXZEILE=""
	. . . FOR LFN=MINZEILE:1:MAXZEILE DO
	. . . . IF '$DATA(^INDMS(YM,YREF,YSEITE,LFN,1)) SET ^(1)=""
	. . . . SET YZEILE=YZEILE+1
	. . . . SET TEXT0(YZEILE)=$GET(^INDMS(YM,YREF,YSEITE,LFN,1))
	. . . . ;IF LFN'=0 IF YVERSAND=0 IF $FIND(TEXT0(YZEILE),"<") SET YVERSAND=1  ;HTML IMMER ALS ATTACHMENT
	. . ;
	. . ;
	. . ;VERSAND ALS PLAIN TEXT ;shipping when Text 
	. . IF YVERSAND=0 DO
	. . . MERGE TEXT=TEXT0
	. . . IF $GET(MINZEILE)=0 IF $FIND(TEXT(1),"<") KILL TEXT(1)
	. . ;
	. . ;
	. . ;VERSAND ALS ATTACHMENT ;shipping when 
	. . IF YVERSAND=1!(YVERSAND=2) DO
	. . . SET VERZ  = $PIECE($GET(^WWW012(0,YM,1)),Y,47)  ;BILDERVERZEICHNIS   ;BEC;23699;YM DURCH 0 ERSETZT;26.05.03
	. . . SET FILE  = $PIECE($GET(^WWW012(0,YM,1)),Y,45)  ;FTP VERZEICHNIS ;tabulation 
	. . . SET VERZB = $PIECE($GET(^WWW012(0,YM,1)),Y,48)  ;BILDERVERZEICHNIS2   ;BEC;25224;27.05.04
	. . . IF FILE="" SET OK=2 QUIT
	. . . SET XTENT=".html"
	. . . ;SET XTENT=".htm"
	. . . IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") SET FILE=FILE_$TRANSLATE(YREF,".","_")_XTENT
	. . . IF $FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") SET FILE=FILE_$TRANSLATE(YREF,".","_")_".xml"
	. . . SET %DEV=$$^WWWDEV(FILE)
	. . . IF %DEV="" SET OK=2 QUIT
	. . . ;
	. . . IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") WRITE "<HTML><BODY>"
	. . . FOR YI=1:1:YZEILE DO
	. . . . IF YPICS=1 IF VERZ'="" DO
	. . . . . IF $FIND(TEXT0(YI),VERZ) DO
	. . . . . . SET TEXT0(YI)=$PIECE(TEXT0(YI),VERZ,1)_$PIECE(TEXT0(YI),VERZ,2,9999)  ;OHNE PFAD ;without track 
	. . . . ;
	. . . . WRITE $GET(TEXT0(YI)),!
	. . . ;
	. . . IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") WRITE "</BODY></HTML>"
	. . . ;
	. . . CLOSE %DEV
	. . . DO OPEN^WWWSTART
	. . . IF $GET(YVERSAND)'=2 IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 SET TEXT(1)=$$^WWWTEXT(33205)_": "_$GET(YBETREFF)  ;Attachment:
	. . . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)=1  SET TEXT(1)="<U>"_$$^WWWTEXT(33205)_":</U> <br>"_$GET(YBETREFF)_" ("_$TRANSLATE($GET(YREF),".","_")_".pdf"_")"  ;Attachment:
	. . . ;IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 SET TEXT(1)="#attach "_FILE  ;_" :notext"   ;BEC;25224;24.05.04;PDF AUFBEREITEN 
	. . . IF $GET(YVERSAND)'=2 IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 SET TEXT(1)="#attach "_FILE  ;_" :notext"   ;BEC;25806;BEI HTML KEINE ANHANG AUFBEREITEN
	. . . SET ZEILE=2
	. . . NEW YFILEB
	. . . IF YPICS=1 IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 DO
	. . . . IF $PIECE($GET(YBELEG1),Y,5)'="" DO  ;BRIEFKOPF 1.SEITE ;letterhead 
	. . . . . SET ZEILE=3
	. . . . . SET YFILEB=$PIECE($PIECE($GET(^WWW012(0,YM,1)),Y,49),VERZ,1)_VERZ_$PIECE($GET(YBELEG1),Y,5)       ;BEC;25224;24.05.04;
	. . . . . IF $$^WWWFILECHECK(YFILEB)=1 SET TEXT(3)="#attach "_YFILEB
	. . . . . ;SET TEXT(3)="#attach "_$PIECE($PIECE($GET(^WWW012(0,YM,1)),Y,49),VERZ,1)_VERZ_$PIECE($GET(YBELEG1),Y,5)
	. . . . . IF $$^WWWFILECHECK(YFILEB)'=1 SET TEXT(3)="#attach "_$PIECE($PIECE($GET(^WWW012(0,YM,1)),Y,49),VERZ,1)_VERZB_$PIECE($GET(YBELEG1),Y,5)   
	. . . . ;
	. . . . IF $PIECE($GET(YBELEG1),Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 DO  ;BRIEFKOPF 2.SEITE ;letterhead 
	. . . . . SET ZEILE=4
	. . . . . SET YFILEB=$PIECE($PIECE($GET(^WWW012(0,YM,1)),Y,49),VERZ,1)_VERZ_$PIECE($GET(YBELEG1),Y,4)
	. . . . . IF $$^WWWFILECHECK(YFILEB)=1 SET TEXT(4)="#attach "_YFILEB
	. . . . . IF $$^WWWFILECHECK(YFILEB)'=1 SET TEXT(4)="#attach "_$PIECE($PIECE($GET(^WWW012(0,YM,1)),Y,49),VERZ,1)_VERZB_$PIECE($GET(YBELEG1),Y,4)
	. . . . . ;SET TEXT(4)="#attach "_$PIECE($PIECE($GET(^WWW012(0,YM,1)),Y,49),VERZ,1)_VERZ_$PIECE($GET(YBELEG1),Y,4)
	. . ;
	. . ;VERSAND ALS HTML-TEXT ;shipping when 
	. . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 IF YVERSAND=2 DO
	. . . SET ZEILE=$GET(ZEILE)
	. . . SET ZEILE=ZEILE+1
	. . . IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"UNB+") SET TEXT(ZEILE)="<HTML><BODY>"
	. . . FOR YI=MINZEILE:1:YZEILE DO
	. . . . SET ZEILE=ZEILE+1
	. . . . SET TEXT(ZEILE)=$GET(TEXT0(YI))
	. . . ;
	. . . SET ZEILE=ZEILE+1
	. . . IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"UNB+") SET TEXT(ZEILE)="</BODY></HTML>"
	. ;
	. ;
	. ;IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 SET TEXT(1)="#attach "_FILE  ;_" :notext"   ;BEC;25224;24.05.04;PDF AUFBEREITEN 
	. ;I $G(YVERSAND)'=2 IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 SET TEXT(1)="#attach "_$G(FILE)  ;_" :notext"   ;BEC;25806;BEI HTML KEINE ANHANG AUFBEREITEN 
	. IF $GET(YVERSAND)'=2 IF $GET(YVERSAND)'=3 IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)'=1 SET TEXT(1)="#attach "_$GET(FILE)  ;_" :notext"   ;BEC;26445;BEI HTML KEINE ANHANG AUFBEREITEN 
	. ;--------------------------------------------------------------------------
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)=1 DO   ;BEC;25224;24.05.04;PDF AUFBEREITEN 
	. . QUIT:$GET(NOPDF)=1      ;bec;26445;01.10.04
	. . NEW PATH
	. . SET PATH=$TRANSLATE($PIECE($GET(^WWW012(0,YM,1)),Y,45),"/","\")         ;PFAD AUS DEN MANDANTENPARAMETERN ;track out of 
	. . IF $EXTRACT(PATH,$LENGTH(PATH))'="\" SET PATH=PATH_"\"  ;WENN KEIN VERZEICHNIS GESETZT ;when no tabulation staid 
	. . DO STOPP^WWWPDF($GET(YREF),PATH,1,1,,1)     ;PDF AUFBEREITEN
	. . SET ZEILE=$GET(ZEILE)+1
	. . SET TEXT(ZEILE)="#attach "_PATH_$TRANSLATE($GET(YREF),".","_")_".pdf"
	. ;
	. ;-------------------------------------------------------------
	. ;ZUSÄTZLICHE ATTACHMENT  ;BEC;25788;26.05.04
	. IF $GET(YATTACH)'="" DO        
	. . NEW YI
	. . FOR YI=1:1 QUIT:$PIECE(YATTACH,"|",YI,99)=""  DO
	. . . QUIT:$PIECE(YATTACH,"|",YI)=""
	. . . SET ZEILE=$GET(ZEILE)+1
	. . . SET TEXT(ZEILE)="#attach "_$PIECE(YATTACH,"|",YI)
	. . . SET ZEILE=ZEILE+1
	. . . IF $GET(NOATTNAME)'=1 SET TEXT(ZEILE)="<BR> "_$PIECE($PIECE(YATTACH,"|",YI),"\",$LENGTH($PIECE(YATTACH,"|",YI),"\"))
	. ;
	. ;
	. ;---------------------------------------------------------
	. ;
	. ;ABSENDERDATEN EINFÜGEN ;interpolate 
	. ;
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,175)=1 DO
	. . SET YHEADER=$GET(YHEADER)
	. . QUIT:$GET(YHEADER)=""
	. . SET YHEADER="|"_YHEADER
	. . SET ZEILE=$GET(ZEILE)+2
	. . NEW YI
	. . FOR YI=1:1 QUIT:$PIECE($GET(YHEADER),"|",YI,999)=""  DO 
	. . . SET ZEILE=ZEILE+1
	. . . SET TEXT(ZEILE)=$PIECE($GET(YHEADER),"|",YI)_"<BR>"
	. ;
	. ;
	. ;-------------------------------------------------------------------------
	. IF YVERSAND=1 IF OK=2 SET OK=0 QUIT
	. SET TCP=$PIECE($GET(^WWW012(0,YM,1)),Y,40)  ;z.B.: mailto.btx.dtag.de
	. IF TCP="" SET TCP="127.0.0.1"
	. SET UUSER=$PIECE($GET(^WWW012(0,YM,1)),Y,84)  ;z.B.: info@intraprend.de
	. SET TXT="TEXT"
	. IF YBETREFF="" SET YBETREFF=YREF
	. ;
	. SET OKSEND=$$^WWWMAILS(TCP,UUSER,YMAIL,YBETREFF,TXT)
	. IF OKSEND=0 SET OK=1  ;OK
	. IF $GET(YSEND)="" SET YSEND=0  ;TYBD;YSEND SOLL NICHT VERÄNDERT WERDEN;1,6,2004;25224;
	. DO OPEN^WWWSTART
	
	QUIT OK
	
Send(YREF="",pstrEmailAddr,pstrSubject,pblnInclPics=$$$NO,YVERSAND,YHEADER,YATTACH="",NOATTNAME,NOPDF)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUTOMATISCHES SENDEN VON E-MAILS
	;
	; Inputs : 
	;	YREF			KEY AUS INDMS ODER * WENN FREIER TEXT / OR "*" IF GLOBAL IS IN TEXTX()
	;	pstrEmailAddr	E-MAIL ADRESSE EMPFÄNGER ;address recipient 
	;	pstrSubject		SUBJECT
	;	pblnInclPics	MIT BILDERN SENDEN (0=NEIN 1=JA) ;send with pictures (attachments?)
	;	YVERSAND		VERSANDART (0=TEXT 1=ATTACHMENT 2=text with HTML 3=HTML within the email)
	;	YHEADER			MAILTEXT (WENN NUR ATTCHMENT ALS PDF) ; IF ONLY ATTACHMENT AS PDF
	;	YATTACH			ATTACHMENTS 
	;	NOATTNAME		= 1 No Name of attachments
	;	NOPDF			= 1 No PDF File
	;
	; ByRef :
	;	TEXTX			TEXTGLOGAL (BEI TEXT-VORGABE OHNE SPEICHERUNG IN INDMS) ;WITH TEXT DEFAULT WITHOUT STORAGE IN INDMS
	;	YBELEG1			obj?
	;
	; Returns :
	;	OK		0, 1, 2
	;
	; History :
	; 21-Mar-2007	GRF		SR15484: Copy and modify as replacement to package message in ^WWWSOR
	; 30-Aug-2005	GRF		Realign dot levels
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 22.05.2001	FIS
	;-------------------------------------------------------------------------------
	new OK,PATH,YI,idMessage,intLine,objCompany,objGeneral,XTENT,strINDMS
	
	;---------------------------------------
	; ^INDMS		Documents
	;---------------------------------------
	
	set OK = $$$NO
	quit:$get(pstrEmailAddr)="" OK
	
	set pblnInclPics = +pblnInclPics
	set YVERSAND     = +$get(YVERSAND)
	if $get(pstrSubject)="" set pstrSubject="E-Mail"
	
	set objGeneral = $get(^INVORG(YM,YM,1))
	set objCompany = $get(^WWW012(0,YM,1))
	
	set blnEmailPDF = +$$$INVORGSendPDFFileAsEMail(objGeneral)      ; D175
	
	set idMessage = $$Allocate^WWWMAILS()
	set intLine   = 0
	
	set VERZ    = $$$WWW012PictureDirectorySystem(objCompany)   ; D47  ;BILDERVERZEICHNIS
	set FILE    = $$$WWW012PhysicalFTPDirectory(objCompany)     ; D45  ;FTP VERZEICHNIS
	set VERZB   = $$$WWW012PictureDirectoryUser(objCompany)     ; D48  ;BILDERVERZEICHNIS2   ;BEC;25224;27.05.04
	set PhysDir = $$$WWW012PhysicalWWWDirectory(objCompany)     ; D49
 
	;TEXT-VORGABE (KEINE INDMS)
	;-------------------------------------------------------------------------
	if YREF="*" {
		if $data(TEXTX) {
			
			;VERSAND ALS PLAIN TEXT ;shipping when Text 
			merge ^WWWSOR(idMessage) = TEXTX
			set intLine = $order(^WWWSOR(idMessage,""),-1)
			
			; Add HTML wrapper around plain text file
			if YVERSAND=2 {
				set ^WWWSOR(idMessage,0)                   = "<HTML><BODY><PRE>"_$get(^WWWSOR(idMessage,0))
				set ^WWWSOR(idMessage,$increment(intLine)) = "</PRE></BODY></HTML>"
			}
			
			;VERSAND ALS ATTACHMENT ;shipping when 
			IF (YVERSAND=1) || (YVERSAND=2) {
				
				IF FILE="" {
					SET OK=2
				} else {
					SET XTENT=".htm"     ; ".html"
					
					set strINDMS=$zconvert($GET(^INDMS(YM,YREF,1,0,1)),"U")
					IF YREF'="" {
						IF $FIND(strINDMS,"XML")  SET XTENT=".xml"  ;XMLFILE 
						IF $FIND(strINDMS,"UNH+") SET XTENT=".edi"  ;EDIFILE
					}
					SET FILE=FILE_$TRANSLATE($EXTRACT($$^WWWUMLAU(pstrSubject,3),1,8),".","_")_XTENT
					
					SET %DEV=$$^WWWDEV(FILE)
					IF %DEV="" {
						SET OK=2
					} else {
						
						SET YI=""
						FOR  {
							SET YI=$ORDER(^WWWSOR(idMessage,YI))
							QUIT:YI=""
							
							IF pblnInclPics && (VERZ'="") {
								IF $FIND($GET(^WWWSOR(idMessage,YI)),VERZ) {
									SET ^WWWSOR(idMessage,YI)=$PIECE(^WWWSOR(idMessage,YI),VERZ,1)_$PIECE(^WWWSOR(idMessage,YI),VERZ,2,9999)  ;OHNE PFAD ;without track 
								}
							}
						;	set ^WWWSOR(idMessage,$increment(intLine)) = ""
							WRITE $GET(^WWWSOR(idMessage,YI)),!
							QUIT:YI>100
						}
						
						CLOSE %DEV
						DO OPEN^WWWSTART
						
						KILL ^WWWSOR(idMessage)
						SET ^WWWSOR(idMessage,1)=""
						SET ^WWWSOR(idMessage,2)="#attach "_FILE
					}
				}
			}
		}
	} elseif YREF'="" {
	;TEXT/BELEG AUS INDMS SENDEN ;out of transmit 
	;-------------------------------------------------------------------------
		SET YZEILE=0
		SET YSEITE=""
		FOR  {
			SET YSEITE=$ORDER(^INDMS(YM,YREF,YSEITE))      ; FIXME : There is no ^INDMS class.  <GRF>
			QUIT:YSEITE=""
			
			SET MAXSEITE=YSEITE
		;	SET MINZEILE=$ORDER(^INDMS(YM,YREF,YSEITE,""),1)  ;O=<PRE>
			SET MINZEILE=$ORDER(^INDMS(YM,YREF,1,""),1)       ;O=<PRE>     ; FIXME? : Why always from first "side"?
			IF MINZEILE>1 SET MINZEILE=1
			SET MAXZEILE=$ORDER(^INDMS(YM,YREF,YSEITE,""),-1)
			QUIT:MAXZEILE=""
			
			FOR LFN=MINZEILE:1:MAXZEILE {
				IF '$DATA(^INDMS(YM,YREF,YSEITE,LFN,1)) SET ^INDMS(YM,YREF,YSEITE,LFN,1)=""
				SET YZEILE=YZEILE+1
				SET TEXT0(YZEILE)=$GET(^INDMS(YM,YREF,YSEITE,LFN,1))
				;IF LFN'=0 IF YVERSAND=0 IF $FIND(TEXT0(YZEILE),"<") SET YVERSAND=1  ;HTML IMMER ALS ATTACHMENT
			}
		}
		
		;VERSAND ALS PLAIN TEXT ;shipping when Text 
		IF YVERSAND=0 {
			MERGE TEXT=TEXT0
			IF $GET(MINZEILE)=0 IF $FIND(TEXT(1),"<") KILL TEXT(1)
		}
		
		;VERSAND ALS ATTACHMENT ;shipping when 
		IF (YVERSAND=1) || (YVERSAND=2) {
			SET FILE  = $PIECE(objCompany,Y,45)  ;FTP VERZEICHNIS ;tabulation 
			IF FILE="" {
				SET OK=2
			} else {
				SET XTENT=".html"              ; ".htm"
				IF $FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") {
					SET FILE=FILE_$TRANSLATE(YREF,".","_")_".xml"
				} else {
					SET FILE=FILE_$TRANSLATE(YREF,".","_")_XTENT
				}
				SET %DEV=$$^WWWDEV(FILE)
				
				IF %DEV="" {
					SET OK=2
				} else {			
					IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") WRITE "<HTML><BODY>"
					FOR YI=1:1:YZEILE {
						IF pblnInclPics && (VERZ'="") {
							IF $FIND(TEXT0(YI),VERZ) {
								SET TEXT0(YI)=$PIECE(TEXT0(YI),VERZ,1)_$PIECE(TEXT0(YI),VERZ,2,9999)  ;OHNE PFAD ;without track 
							}
						}
						WRITE $GET(TEXT0(YI)),!
					}
					
					IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") WRITE "</BODY></HTML>"
					
					CLOSE %DEV
					DO OPEN^WWWSTART
					
					IF blnEmailPDF  {
						set ^WWWSOR(idMessage,$increment(intLine)) = "<U>"_$$^WWWTEXT(33205)_":</U> <br>"_pstrSubject_" ("_$TRANSLATE($GET(YREF),".","_")_".pdf"_")"  ;Attachment:
					} else {
						IF $GET(YVERSAND)'=2 {
							set ^WWWSOR(idMessage,$increment(intLine)) = $$^WWWTEXT(33205)_": "_pstrSubject  ;Attachment:
							set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_FILE  ;_" :notext"               ;BEI HTML KEINE ANHANG AUFBEREITEN
						}
					}
					NEW YFILEB
					
					IF pblnInclPics && 'blnEmailPDF {
						IF $PIECE($GET(YBELEG1),Y,5)'="" {      ;BRIEFKOPF 1.SEITE ;letterhead 
							SET YFILEB=$PIECE(PhysDir,VERZ,1)_VERZ_$PIECE($GET(YBELEG1),Y,5)       ;BEC;25224;24.05.04;
							IF $$^WWWFILECHECK(YFILEB)=1 {
								set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_YFILEB
							} else {
								set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_$PIECE($PIECE(objCompany,Y,49),VERZ,1)_VERZB_$PIECE($GET(YBELEG1),Y,5)
							}
						}
						IF ($PIECE($GET(YBELEG1),Y,4)'="") {    ;BRIEFKOPF 2.SEITE ;letterhead 
							SET YFILEB=$PIECE($PIECE(objCompany,Y,49),VERZ,1)_VERZ_$PIECE($GET(YBELEG1),Y,4)
							IF $$^WWWFILECHECK(YFILEB)=1  {
								set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_YFILEB
							} else {
								set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_$PIECE($PIECE(objCompany,Y,49),VERZ,1)_VERZB_$PIECE($GET(YBELEG1),Y,4)
							}
						}
					}
				}
			}
		}
		
		;VERSAND ALS HTML-TEXT ;shipping when 
		IF 'blnEmailPDF && (YVERSAND=2) {     ; D175
			IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"UNB+") set ^WWWSOR(idMessage,$increment(intLine)) = "<HTML><BODY>"
			FOR YI=MINZEILE:1:YZEILE {
				set ^WWWSOR(idMessage,$increment(intLine)) = $GET(TEXT0(YI))
			}
			IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"xml") IF '$FIND($GET(^INDMS(YM,YREF,1,0,1)),"UNB+") set ^WWWSOR(idMessage,$increment(intLine)) = "</BODY></HTML>"
		}
	}
	;---------------------------------------
	;	Despatch Type (0=TEXT 1=ATTACHMENT 2=text with HTML 3=HTML within the email)
	;---------------------------------------
	
	IF ($GET(YVERSAND)'=2) && ($GET(YVERSAND)'=3) && 'blnEmailPDF {
		SET TEXT(1)="#attach "_$GET(FILE)  ;_" :notext"   ;BEC;26445;BEI HTML KEINE ANHANG AUFBEREITEN 
	}
	
	IF blnEmailPDF {
		if $GET(NOPDF)'=$$$YES {
			SET PATH=$TRANSLATE($PIECE(objCompany,Y,45),"/","\")    ;PFAD AUS DEN MANDANTENPARAMETERN ;track out of 
			IF $EXTRACT(PATH,$LENGTH(PATH))'="\" SET PATH=PATH_"\"  ;WENN KEIN VERZEICHNIS GESETZT ;when no tabulation staid 
			DO STOPP^WWWPDF($GET(YREF),PATH,1,1,,1)                 ;PDF AUFBEREITEN
			set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_PATH_$TRANSLATE($GET(YREF),".","_")_".pdf"
		}
	}
	;-------------------------------------------------------------
	;ZUSÄTZLICHE ATTACHMENT
	IF YATTACH'="" {
		FOR YI=1:1 {
			QUIT:$PIECE(YATTACH,"|",YI,99)=""
			QUIT:$PIECE(YATTACH,"|",YI)=""
			
			set ^WWWSOR(idMessage,$increment(intLine)) = "#attach "_$PIECE(YATTACH,"|",YI)
			IF $GET(NOATTNAME)'=1 set ^WWWSOR(idMessage,$increment(intLine)) = "<BR> "_$PIECE($PIECE(YATTACH,"|",YI),"\",$LENGTH($PIECE(YATTACH,"|",YI),"\"))
		}
	}
	
	;---------------------------------------------------------
	
	;ABSENDERDATEN EINFÜGEN ;interpolate 
	
	IF blnEmailPDF {
		SET YHEADER=$GET(YHEADER)
		if YHEADER'="" {
			SET YHEADER="|"_YHEADER
			FOR YI=1:1 {
				QUIT:$PIECE(YHEADER,"|",YI,999)=""
				
				set ^WWWSOR(idMessage,$increment(intLine)) = $PIECE($GET(YHEADER),"|",YI)_"<BR>"
			}
		}
	}
	
	
	;---------------------------------------
	;	Despatch Type 1=ATTACHMENT
	;---------------------------------------
	IF (YVERSAND=1) && (OK=2) {
		SET OK = $$$NO
	} else {
		SET TCP=$PIECE(objCompany,Y,40)    ;z.B.: mailto.btx.dtag.de
		IF TCP="" SET TCP="127.0.0.1"
		SET UUSER=$PIECE(objCompany,Y,84)  ;z.B.: info@intraprend.de
		SET TXT="TEXT"
		IF pstrSubject="" SET pstrSubject=YREF
		
		SET OKSEND=$$^WWWMAILS(TCP,UUSER,pstrEmailAddr,pstrSubject,TXT)
	;	set strStatus = $$PackageMail^WWWMAILS(idMessage,YBETREFF  ,YMAIL       ,$$$WWW013Name(objUser),UUSER,strSenderName)
		set strStatus = $$PackageMail^WWWMAILS(idMessage,pstrSubject,pstrEmailAddr,$$$WWW013Name(objUser),"",strSenderName)
		
		; note strStatus has opposite sign to OKSEND
		IF OKSEND=0       SET OK    = $$$YES
		IF $GET(YSEND)="" SET YSEND = 0           ;TYBD;YSEND SOLL NICHT VERÄNDERT WERDEN;1,6,2004;25224;
		DO OPEN^WWWSTART
	}
	
	do Deallocate^WWWMAILS(idMessage)
	
	QUIT OK	
	
]]></Routine>
</Export>