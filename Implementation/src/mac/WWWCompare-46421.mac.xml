<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWCompare" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
    ;-------------------------------------------------------------------------------
    ; WWWCompare - comparision tool for different versions of code.
    ;-------------------------------------------------------------------------------
	#include COMSYS
	#include WWWConst
 
CompareProject(pstrPrefix="",pstrType="",pstrSites="",pstrPath="") 
    ;-------------------------------------------------------------------------------
    ; Runs a compare on multiple items.
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 07-Nov-2005	shobby		SR13748: Created 
    ;-------------------------------------------------------------------------------
	;
	new strStatus,idx
	
	if $get(Y)="" do ^WWWVAR
	set strStatus=$$$OK
	
	set pstrPrefix=$zcvt(pstrPrefix,"u")
	if pstrType=1 { ;$$$EnumWWWCOMPARETYPEClass {
		set idx=""
		for {
			quit:$$$ISERR(strStatus)
			set idx=$order(^WWW001(0,idx))
			quit:idx=""
			if pstrPrefix=$zcvt($extract(idx,1,$length(pstrPrefix)),"u") {
				w !,idx
				set strStatus=$$Compare(idx,pstrType,pstrSites,pstrPath,1)
			}
		}
	} elseif pstrType=2 { ;$$$EnumWWWCOMPARETYPEForm {
		;set strStatus=$$CopyToFile^COMClassLog("WWW120",pstrName,##Class(%File).NormalizeDirectory(pstrPath)_"Form-"_$zu(110)_"-"_$zu(5)_"-"_pstrName_".txt")
	} elseif pstrType=3 { ;$$$EnumWWWCOMPARETYPERoutine {
		;set strStatus=$$Export^%apiRTN(pstrName,##Class(%File).NormalizeDirectory(pstrPath)_"Routine-"_$zu(110)_"-"_$zu(5)_"-"_pstrName_".xml","Save for Source Control",,"-d")
	}
	
	quit strStatus
 
Compare(pstrName="",pstrType="",pstrSites="",pstrPath="",pblnSilent=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Compare Classes, forms and routines.
    ;
    ; History:
    ; 14-May-2007	GRF		Use zz global
    ; 07-Nov-2005	shobby	SR13748:Allow to run in silent mode.
    ; 01-Jul-2005	Steve S	Added $$$OK, $$$ISOK usage
    ; 23-Jun-2005	Paul K	Created SR12763
    ;-------------------------------------------------------------------------------
    new idSite,SiteLoop,strStatus
    
    set ^zzWWWCompare(0,YBED,1)=pstrPath
    set strStatus=$$$OK
    
    if pstrSites'="" {
		for SiteLoop=1:1:$length(pstrSites,",") {
			set idSite=$piece(pstrSites,",",SiteLoop)
			set strStatus=$$$ExecuteSite(idSite,"CompareType^WWWCompare",pstrName,pstrType,pstrPath)
			quit:$$$ISERR(strStatus)
		}
	}
	
	if $$$ISOK(strStatus) set strStatus=$$CompareType(pstrName,pstrType,pstrPath)
	
	if 'pblnSilent {
		if $$$ISOK(strStatus) {
			write "<script>window.open(""file:"_$translate(##Class(%File).NormalizeDirectory(pstrPath),"\","/")_""");</script>"
			do ^WWWFORM
		} else {
			do ReturnError^COMUtils(strStatus)
		}
	}
	
	quit strStatus
 
CompareType(pstrName="",pstrType="",pstrPath="")
	new strStatus,strFile
	
	set strStatus=$$$OK
	
	if pstrType=1 { ;$$$EnumWWWCOMPARETYPEClass {
		set strFile=##Class(%File).NormalizeDirectory(pstrPath)_"Class-"_$zu(110)_"-"_$zu(5)_"-"_pstrName_".txt"
		set strStatus=$$CopyToFile^COMClassLog("WWW001",pstrName,strFile)
	} elseif pstrType=2 { ;$$$EnumWWWCOMPARETYPEForm {
		set strStatus=$$CopyToFile^COMClassLog("WWW120",pstrName,##Class(%File).NormalizeDirectory(pstrPath)_"Form-"_$zu(110)_"-"_$zu(5)_"-"_pstrName_".txt")
	} elseif pstrType=3 { ;$$$EnumWWWCOMPARETYPERoutine {
		set strStatus=$$Export^%apiRTN(pstrName,##Class(%File).NormalizeDirectory(pstrPath)_"Routine-"_$zu(110)_"-"_$zu(5)_"-"_pstrName_".xml","Save for Source Control",,"-d")
	}
	
	// These functions except ^%apiRTN only return 0 or 1, so if any of them fail, then the status returned is
	// 0, and the returnerror above will return 0.
	
	quit strStatus
	
	
Resend(pstrNameSpace="TRANSLATION")
    ;-------------------------------------------------------------------------------
    ; Resends globals found not to match in the CompareGlobal(s) routines.
    ; Used to synchronise language texts between namespaces.
    ;
    ; History:
    ; 16-Feb-2006	shobby		Created:SR14281
    ;-------------------------------------------------------------------------------
	new str,strKey,strExecute,strGlobal,strData
	
	set str=""
	for {
		set str=$order(^CacheTempCompareGlobal(YUSER,str))
		quit:str=""
		set strData=$get(^CacheTempCompareGlobal(YUSER,str,pstrNameSpace))
		set strGlobal=$piece($piece(str,"(",1),"^",2)
		set strKey=$translate($piece(str,",",2,$length(str,",")-1),"""")
		set strExecute="do Save^COMUtils("""_strGlobal_""","""_strKey_""","""_strData_""",1)"
		w !,strExecute
		xecute strExecute
		set strExecute="do Save^COMUtils("""_strGlobal_""","""_strKey_""","""_strData_"."",1)"
		xecute strExecute
	}
	quit
 
 
CompareGlobals(pstrNameSpace1,pstrNameSpace2,pstrLanguage="")
    ;-------------------------------------------------------------------------------
    ; Compares globals across difference namespaces.  Compares a list, currently all the
    ; classes interesting in the context of language translations.
    ;
    ; History:
    ; 16-Feb-2006	shobby		Created:SR14281
    ;-------------------------------------------------------------------------------
	new strGlobal
 
 	if $get(Y)="" d ^WWWVAR
	kill ^CacheTempCompareGlobal(YUSER)
	for strGlobal="SALCSTGridColText","SALCSTGridText","SALCSTMenuContentText","SALCSTMenuHeaderText","SALCSTSegmentLayerText","SALCSTSegmentText","SALRuleText","SALRuleTypeText","CSTCharValue1","INDRPPARA","SALAccountText","SALCSTButtonText","WWW0011","WWW0021","WWW0031","WWW0041","WWW00411","WWW009","WWW100","WWW101","WWW1201","WWW1203","WWW1211","WWW121D1","WWW1221","WWW122D1","WWW124","WWW124D","WWW127","WWW1271","WWWEDUSP" {
	;for strGlobal="SALCSTGridColText","SALCSTGridText","SALCSTMenuContentText","SALCSTMenuHeaderText","SALCSTSegmentLayerText","SALCSTSegmentText","SALRuleText","SALRuleTypeText","CSTCharValue1","INDRPPARA","SALAccountText","SALCSTButtonText","WWW0011","WWW0021","WWW0031","WWW00411","WWW009","WWW100","WWW101","WWW1201","WWW1203","WWW1211","WWW121D1","WWW1221","WWW122D1","WWW124D","WWW1271","WWWEDUSP" {
		do CompareGlobal(strGlobal,pstrNameSpace1,pstrNameSpace2,pstrLanguage)
	}
	quit
 
 
CompareGlobal(pstrGlobal,pstrNameSpace1,pstrNameSpace2,pstrLanguage="")
    ;-------------------------------------------------------------------------------
    ; Compares globals across difference namespaces
    ;
    ; History:
    ; 03-Mar-2006	shobby		SR14281: Subscripted the passed in global
    ; 16-Feb-2006	shobby		Created:SR14281
    ;-------------------------------------------------------------------------------
	new str,strNameSpace,strCurrentNameSpace,intKey
	
 	if $get(Y)="" d ^WWWVAR
	set strCurrentNameSpace=$znspace	
	kill ^CacheTempCompareGlobal(YUSER,pstrGlobal)
	set intKey=$$GetKey^COMLanguage(pstrGlobal)+1
 
 
	for strNameSpace=pstrNameSpace1,pstrNameSpace2 {
		zn strNameSpace
		set str="^"_pstrGlobal
		for {
			set str=$query(@str)
			quit:str=""
			write !,str_","_$piece(str,",",intKey)_","_pstrLanguage
			if (pstrLanguage="")||(pstrLanguage=$translate($piece(str,",",intKey),"""")) {
				set ^[strCurrentNameSpace]CacheTempCompareGlobal(YUSER,pstrGlobal,str,strNameSpace)=@str
			}
		}
	}
	zn strCurrentNameSpace
	set str=""
	for {
		set str=$order(^CacheTempCompareGlobal(YUSER,pstrGlobal,str))
		quit:str=""
		if $get(^CacheTempCompareGlobal(YUSER,pstrGlobal,str,pstrNameSpace1))=$get(^CacheTempCompareGlobal(YUSER,pstrGlobal,str,pstrNameSpace2)) {
			kill ^CacheTempCompareGlobal(YUSER,pstrGlobal,str,pstrNameSpace1)
			kill ^CacheTempCompareGlobal(YUSER,pstrGlobal,str,pstrNameSpace2)
		}
	}
	quit
]]></Routine>
</Export>