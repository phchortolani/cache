<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMEMO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMEMO ;;EUPMEMO;Hilfsroutine fuer Memo-Felder;14.04.2004;14.04.2004;:GEAEND.:13.10.2004;riemer;$Revision: 1.1 $  ; Compiled March 10, 2005 15:06:37
#INCLUDE COMDebug
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		EUPMEMO
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| 	Hilfsroutine fuer Memo-Felder
	;|
	;\------------------------------------------------------------------/
	;
	QUIT
	
COUNTLINES(iVAL) ;Gibt Anzahl der Zeilen zurueck
	QUIT:$GET(iVAL)="" 1
	QUIT $L(iVAL,"|")
	
GETLINE(iVAL,iLINE) ;Gibt die iLINEnte Zeile zurueck
	SET iLINE=$GET(iLINE,1)
	QUIT:$GET(iVAL)="" ""
	QUIT $P(iVAL,"|",iLINE)
	
ISEMPTY(iTEXT) ;Checkt, ob Memo-Feld leer ist
	QUIT $TRANSLATE(iTEXT," "_"|")=""    
	
ADD(iMEMFELD,iSTR) ;Fuegt den String iSTR am Ende vom Memo-Feld iMEMFELD hinzu
	IF ($LENGTH(iMEMFELD)+$LENGTH(iSTR))>32000 QUIT iMEMFELD=iMEMFELD_"|"_$$^WWWTEXT("...Text was cut.")
	IF iSTR="" QUIT iMEMFELD
	IF $EXTRACT(iMEMFELD,$LENGTH(iMEMFELD))="|" QUIT iMEMFELD_iSTR
	IF iMEMFELD="" QUIT iSTR
	QUIT iMEMFELD_"|"_iSTR
	
TOP(iWERT,iZEILEN,iMAXCHAR) ;Gibt aus einem Memo-Feld in iWERT nur iZEILEN und je Zeile maximal iMAXCHAR Zeichen zureuck
	QUIT:$GET(iWERT)="" ""
	SET iZEILEN=$GET(iZEILEN,1)
	SET iMAXCHAR=+$GET(iMAXCHAR)
	NEW I,TMP
	SET iWERT=$PIECE(iWERT,"|",1,iZEILEN)
	IF iMAXCHAR FOR I=1:1:$LENGTH(iWERT,"|") SET TMP=$EXTRACT($PIECE(iWERT,"|",I),1,iMAXCHAR),$PIECE(iWERT,"|",I)=TMP
	QUIT iWERT
	
TOHTML(iWERT,iCRLF,iLINES,iTR) ;Gibt ein @net-Memo-Feld im HTML-Format zurueck
	QUIT:$GET(iWERT)="" ""
	SET iCRLF=+$GET(iCRLF)
	SET iLINES=+$GET(iLINES)
	SET iTR=$GET(iTR)
	IF iTR="" SET iTR="<br>"
	NEW LASTCHAR,I
	IF iLINES SET iWERT=$$STRIPEND(iWERT)
	FOR I=1:1:$LENGTH(iWERT,"|") SET $PIECE(iWERT,"|",I)=$$^WWWUML($PIECE(iWERT,"|",I))
	SET iWERT=$$SUBSTR^WWWTOOLS(iWERT,"|",iTR_$SELECT(iCRLF:$$GETCRLF^WWWTOOLS,1:""))
	QUIT iWERT
	
TOLATEX(iWERT,iCRLF,iLINES,iTR) ;Gibt ein @net-Memo-Feld im LaTeX-Format zurueck
	QUIT:$GET(iWERT)="" ""
	SET iCRLF=+$GET(iCRLF)
	SET iLINES=+$GET(iLINES)
	SET iTR=$GET(iTR)
	IF iTR="" SET iTR="\\"
	NEW LASTCHAR,I
	IF iLINES SET iWERT=$$STRIPEND(iWERT)
	FOR I=1:1:$LENGTH(iWERT,"|") SET $PIECE(iWERT,"|",I)=##class(MP.LATEXDOKUMENT).TRANSLATE($PIECE(iWERT,"|",I))
	SET iWERT=$$SUBSTR^WWWTOOLS(iWERT,"|",iTR_$SELECT(iCRLF:$$GETCRLF^WWWTOOLS,1:""))
	QUIT iWERT
	
TOSTREAM(iWERT) ;Gibt ein @net-Memo-Feld (iWERT) im Cache-Stream-Format zurueck
	;z. B. S OREF.StreamProperty.Write($$TOSTREAM^WWWMEMO(WERT))
	;
 
	QUIT:$GET(iWERT)="" ""
	QUIT $$SUBSTR^WWWTOOLS(iWERT,"|",$CHAR(13,10))
	
STRIPBEGIN(iSTRING) ;Entfernt Whitespace (Blank, Zeilenumbrueche, Tabs) am Anfang eines Memo-Feldes
	FOR  QUIT:(" "_"|"_$CHAR(9))'[$EXTRACT(iSTRING)  SET $EXTRACT(iSTRING)="" QUIT:iSTRING=""
	QUIT iSTRING
	
STRIPEND(iSTRING) ;Entfernt Whitespace (Blank, Zeilenumbrueche, Tabs) am Ende eines Memo-Feldes
	NEW LAENGE
	FOR  SET LAENGE=$LENGTH(iSTRING) QUIT:LAENGE=0  QUIT:(" "_"|"_$CHAR(9))'[$EXTRACT(iSTRING,LAENGE)  SET $EXTRACT(iSTRING,LAENGE)=""            ;OJR;7.05.2004;sonst f-t nicht, falls iSTRING leer wird 
	QUIT iSTRING
	
DIFFBEGIN(iMINUEND,iSUBTRAHEND) ;
	;"Subtrahiert" zwei Memo-Felder voneinander. Aber nur die ersten uebereinstimmenden Zeilen!
	;Dabei werden die Memo-Felder als Mengen betrachtet und jede einzelne Zeile als ein 
	;eindeutiges Element. Mathematisch formal beschreiben koennte man die Mengen so:
	;    Memo := {Z_1, Z_2, ..., Z_i}, i ist Element der natuerlichen Zahlen
	;             Z_1 ist die erste Zeile, Z_2 die zweite, usw...
	;
	;Beispiel:
	;
	; Memo1  -   Memo2     =      Memo3
	;--------------------------------------
	; 1. Zeile   1. Zeile         TestZeile
	; 2. Zeile   2. Zeile
	; TestZeile  Was auch immer
	;
	QUIT:$GET(iMINUEND)="" ""
	QUIT:$GET(iSUBTRAHEND)="" iMINUEND
	NEW I,QUIT
	SET QUIT=0
	FOR I=1:1:$L(iSUBTRAHEND,"|") DO  QUIT:QUIT
	.  IF $P(iMINUEND,"|",I)=$P(iSUBTRAHEND,"|",I) SET $P(iMINUEND,"|",I)="" IF 1
	.  ELSE  SET QUIT=1
	.  QUIT
	QUIT $$STRIPBEGIN(iMINUEND)
	;
	
MEMO4SUCHE(iMEMO,iLAENGE,oERSTEZEILE,oKOMPLETT)
	;Liefert Teil von der ersten Zeile von Memo (in oERSTEZEILE)
	;Falls Teil l채nger als iLAENGE, dann wird abgeschnitten und ... hinzugef체gt
	;Falls es noch Zeilen gibt, dann wird hinzugef체gt ...
	;in oKOMPLETT landet komplett Inhalt von iMEMO, falls ... hinzugef체gt wurde.
	SET oERSTEZEILE=$P(iMEMO,"|",1)
	SET oKOMPLETT=""
	IF $LENGTH(oERSTEZEILE)>iLAENGE SET oERSTEZEILE=$EXTRACT(oERSTEZEILE,1,iLAENGE)_"...",oKOMPLETT=iMEMO QUIT
	IF '$$ISEMPTY($PIECE(iMEMO,"|",2,$L(iMEMO,"|"))) SET oERSTEZEILE=oERSTEZEILE_"...",oKOMPLETT=iMEMO QUIT
	QUIT
]]></Routine>
</Export>