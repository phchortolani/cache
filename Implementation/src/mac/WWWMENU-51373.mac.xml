<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMENU" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMENU
#include COMSYS
#include WWWConst
 
#; System Param: Menu Types
#define ShowAll		0
#define Explorer	3
#define Separate	4
#define Image		5
#define Popup		7
#define None		9
#define FlexibleMenu 10
#define MegaMenu	13
#define MenuSideBar	14
	
	;-------------------------------------------------------------------------------
	; WWW Table of contents
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 20-Apr-2012	shobby	SR17998: Mega Menu.
	; 02-Aug-2010	shobby	SR17481: Corrected some invalid syntax with style
    ; 22-Apr-2010	shobby	SR17253: Cross Browser Support
	; 06-Jan-2009	FIS		don't use gradient filter with system colors
	; 16-Dec-2008	HQN		SR16240: Added new Menu Type #define
	; 						Separate menu for Menu Type 10
	; 04-Dec-2008	FIS		SR16205: Login with no menu
	; 30-Jun-2008	shobby	BR014963: If user is logged in at the change of midnight,
	; 							update the time when refreshing the menu.
	; 09-Aug-2007	shobby	BR014237: Call to CreateLoginLink so that pressing the X
	; 							button will go back to the login screen.
	; 26-Oct-2006	JW		BR014262: Rewrote, added frames.
	; 04-Sep-2006	PO		SR14420: Call UnloadEvent in frame[1]
	; 10-Aug-2006	JW		SR13836: Default context menu
	;  9-Dec-2005	JW		SR13195: Removed confirm msg
	; 02-Sep-2005	JW		SR12966: WWW120 is shared
	; 04-Aug-2005	RPW		SR13153: Needed check for YBEDBER, I believe this is set
	;							to non zero after login.  YBEDBER is the users
	;							authorisation level.
	; 05-Jul-2005	RPW		SR12230: Added namespace to ^CacheTemp global
	;  4-Jul-2005	JW		SR12807: Remove call to WWWTAG. Now done in COMHousekeeping
	; 29-Jun-2005	RPW		SR12230: If there is no menu and there is a user
	;							kill the User Session Date data and then save this
	;                           info into the form so that it displays accurately.
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 11.09.1996  	DT		(C) BY DITMAR TYBUSSEK; WWW INHALTSVERZEICHNIS
	;-------------------------------------------------------------------------------
	new (%request,%KEY,%,%ZCS,%session)
	
	;---------------------------------------
	;  YVOR		objWWW012
	;---------------------------------------
	set YNOFOOT = 1
	do ^WWWVAR
 
	set $ztrap="^WWWERROR"
	set YSCREENM="",YMFA="",YKILL="",YBACK="",YPARA="",YOPTION="",YNAME="",YKEY="",YFKEY="",YTRAKT="",YTRAKT0=0
	
	set strStatus = $$LoginCheck(YUSER)
	if $$$ISERR(strStatus) {
		set ^CacheTempFAILED($job) = $$$YES
		do ^WWWINFO($$$Text(strStatus))
		kill ^CacheTempFAILED($job)
		quit								/// ***** EARLY QUIT - INVALID LOGIN *****
	}
	
	do UpdateSessionDate()
	
	set objUser = ($get(^WWW013(0,YBED,1)))
	set YBER    = $$$WWW013UserAccess(objUser)		// These are needed in WWWMENU* calls
	set YMOD    = $$$WWW013Module1(objUser)
	set YVOR    = $get(^WWW012(0,YM,1))			// YVOR used in WWWMENU* calls
	
	set objWWWUSER = $get(^WWWUSER(0,YUSER,1))
	set $$$WWWUSERPassword1(objWWWUSER) = $$$WWW013Password1(objUser)
	if $$$WWWUSERDate1(objWWWUSER)'=+$horolog {
		set $$$WWWUSERDate1(objWWWUSER) = +$horolog
		set $$$WWWUSERTime1(objWWWUSER) = 0                ;reset only at change of day
	}
	set ^WWWUSER(0,YUSER,1) = objWWWUSER      // Direct set (May have been a reason, best not to mess with it)
	
	// Define Menu

	set YMENU = $$GetType()
	if YMENU=11 { ;Zen
	 	write "<FRAMESET >"
	 	write "<FRAME SCROLLING=""NO"" name=""ZENMENU"" SRC=""alZEN.Alphalinc.cls?"_$$WWWCGI2^WWWCGI()_"&YM="_YM_""">"
	 	write "</FRAMESET>"
	 	quit				;############  Early quit ################################
 	}
 	
	// No Menu
	if ((YMENU=$$$None) || (YMENU=$$$FlexibleMenu) || (YMENU=$$$MegaMenu) || (YMENU=$$$MenuSideBar)) && (YUSER'="") && ($$$WWWUSERFrameFormed($get(^WWWUSER(0,YUSER,1)))="") { ;SR17998
		do Separate(objUser,0)
		quit								/// ***** EARLY QUIT *****
	}
	
	// Separate Window
	if (YMENU=$$$Separate) && (YUSER'="") && ($$$WWWUSERFrameFormed($get(^WWWUSER(0,YUSER,1)))="") {
		do Separate(objUser)
		quit								/// ***** EARLY QUIT *****
	}
	
	set $$$WWWUSERFormStarted(^WWWUSER(0,YUSER,1))=""				// Direct set
	
	// Menu with frames
	if (YMENU'=$$$Popup) && ($$$WWW012TargetFrameName(YVOR)'="") && (YUSER'="") && ($$$WWWUSERFrameFormed($get(^WWWUSER(0,YUSER,1)))="") {
		do Framed(YVOR,objUser)
		quit							    /// ***** EARLY QUIT *****
	}
	
	// Delete lock files
	set YA = ""
	for {
		set YA = $order(^WWW006(0,YA))
		quit:YA=""
		
		if +YA'=+$horolog {
			kill ^WWW006(0,YA)
			kill ^WWW0061(0,YA)    ;LÖSCHEN ALTE LOCKFILE ;LÖSCHEN LOCK RÜCKHOLINFO
		}
	}
	
	;LOSCHEN ZWISCHENDATEIEN - delete buffer files
	if YUSER'="" kill ^WWWUSE(0,YUSER)
 	
	set YKOPF=$$GetHeaderText(YVOR)
	
	do ^WWWSTART(YKOPF)  ;STARTEN HTML ;launching HTML 
	
	if YMENU=$$$Popup do ^WWWFORM8  ;JAVASCRIPT WENN POPUP ;when 
	
	set blnContext = $$$CONTEXT
	if blnContext do DefaultContext^COMViewColumnMenu()
	
	write "<BODY" 
	if blnContext write " oncontextmenu=' DisplayContext(); return false; ' "
	
	write " onLoad='self.focus();document.location=""#TARGET"";'"    ;FORMULARFORMAT
	
	if $$$WWW012BackgroundPicture(YVOR)'="" write " BACKGROUND="""_YGIF_$$$WWW012BackgroundPicture(YVOR)_""""
	if $$$WWW012WatermarkEffect(YVOR) write " BGPROPERTIES=fixed"
	
	if ($$$WWW012MenuBackgroundColor(YVOR)'="") {
		write " BGCOLOR="""_$$$SysEnum("FARBE",$$$WWW012MenuBackgroundColor(YVOR))_""""
	
	} elseif ($$$WWW012BackgroundColor(YVOR)'="") {
		write " BGCOLOR="""_$$$SysEnum("FARBE",$$$WWW012BackgroundColor(YVOR))_""""
	}
	
	if $$$WWW012FontColor(YVOR)'="" write " TEXT="""_$$$SysEnum("FARBE",$$$WWW012FontColor(YVOR))_""""
	
	if $$$WWW012LinkFontColor(YVOR)'="" {
		set strColour = $$$SysEnum("FARBE",$$$WWW012LinkFontColor(YVOR))
		write " LINK="""_strColour_""""
		write " VLINK="""_strColour_""""
		write " ALINK="""_strColour_""""
	}
	
	write " topmargin=1 leftmargin=1>",YCR
	write "<FONT "
	if $$$WWW012FontFace(YVOR)'=""  write " FACE="""_$$$SysEnum("SCHRIFTART",$$$WWW012FontFace(YVOR))_""""
	if $$$WWW012FontSize(YVOR)'=""  write " SIZE="""_$$$WWW012FontSize(YVOR)_""""
	if $$$WWW012FontColor(YVOR)'="" write " COLOR="""_$$$SysEnum("FARBE",$$$WWW012FontColor(YVOR))_""""		// Duplicated above?
	write ">"
	
	;Header
	if $$$WWW012CenterFormContents(YVOR) write "<CENTER>"
	if (YMENU=$$$Popup) { 
		write "<FORM NAME=""WWW"" ACTION="""_$piece(YAKTION,"?",1)_""""
		if +$get(YHYPER)=0 write " Method=POST"
		write ">"
		do ^WWWKOPF($$$WWW012CompanyName(YVOR))
		write YCR,"</FORM>" 
		
	} else {		
		if $$$WWW012TargetFrameName(YVOR)="" {
			do ^WWWKOPF($$$WWW012CompanyName(YVOR))
		
		} else {
			new COLOR,COLORR
			set COLOR = "DIMGRAY"
			if $$$WWW012ColorCodeForHeaderLeft(YVOR)'="" {
				set COLOR = $$$SysEnum("FARBE",$$$WWW012ColorCodeForHeaderLeft(YVOR))
			}
			set COLORR=$$$WWW012ColorCodeForHeaderRight(YVOR)  ;FARBE RECHTS;FIS;11.12.03 ;tincture on the right 
			if COLORR'="" set COLORR = $$$SysEnum("FARBE",COLORR)
			if COLORR=""  set COLORR = COLOR
			write YCR
			;SR17862 do ^WWWFRAME(0)
			DO ^WWWFRAME(0,,,,,$$$NO) ;SR17862
			write "<TR><TD"
			write " bgcolor="_COLOR  ;für netscape & co. ;to 
			;SR17253 IF $$$WWW012ColorCodeForHeaderLeft(YVOR)'>256 WRITE " style=""filter:progid:DXImageTransform.Microsoft.Gradient(GradientType=1, StartColorStr="_COLOR_", EndColorStr="_COLORR_");"""  ;FIS;11.12.03  ;SR16236
			if $$$WWW012ColorCodeForHeaderLeft(YVOR)'>256 write " style="""_$$ImageTransformGradient^WWWFORMCrossBrowserSupportVisual(COLOR,COLORR) ;SR17253
			write " border-right:none;"""  ;FIS;11.12.03  ;SR17481
			;SR17481 WRITE " style=""border-right:none;"""  ;FIS;11.12.03
			write " VALIGN=TOP align=LEFT nowrap>"
			write "<FONT SIZE=3 color=white><b>"
			
			if ($get(YLOCATION)="")||('$$$WWW012ShowNameOfLocation(YVOR)) {
				write "&nbsp;"_$$$WWW012CompanyName(YVOR)   ;MANDANTENNAME
			} else {
				write "&nbsp;"_$$$WWW0121LocationName($get(^WWW0121(0,YM,YLOCATION,1)))  ;BETRIEB STATT MANDANT ANZEIGEN
			}
			write "</b></TD>"
			
			;klein
			write "<TD bgcolor="_COLORR
			write " style=""border-right:none; border-left:none;"""
			write " align=right WIDTH=10>"
			write "<IMG SRC="""_YGIF_"klein.gif"" border=0 class=link onclick=""parent.document.body.cols='6%,94%';"">"
			write "</TD>"
			
			;gross
			write "<TD bgcolor="_COLORR
			write " style=""border-right:none; border-left:none;"""
			write " align=right WIDTH=10>"
			write "<IMG SRC="""_YGIF_"gross.gif"" border=0 class=link onclick=""parent.document.body.cols='30%,70%';"">"
			write "</TD>"
			
			;ende
			write "<TD bgcolor="_COLORR
			write " style=""border-left:none;"""
			write " align=right WIDTH=10>"
			do CreateLoginLink^WWWKOPF()
			write "</TD>"
			
			write "</TR>"
			do ^WWWFRAME(1)
			write YCR
		}
	}
	if $$$WWW012CenterFormContents(YVOR) write "</CENTER>"
	write "<NOBR>"
	
	;WRITE YCR,YCR,"<!-- ************************* MENUEPUNKTE ************************* -->",YCR,YCR
	
	;VERTEILEN AUF MENUARTEN ;distribute upon 
	if YMENU'=$$$Separate if YMENU'=$$$Explorer if YMENU'=$$$Popup write YCR,"</TD></TR></TABLE>"
	
	if YMENU=$$$ShowAll {
		do ^WWWMENU1       ;NORMALES MENU
		
	} elseif YMENU=$$$Explorer {
		if '$data(^WWW004X(0,YM)) {
			do ^WWWMENU4   ;PLUS MINUS
		} else {
			do ^WWWMENU4X  ;PLUS MINUS MANDANTEN MENU
		}
	;} elseif YMENU=$$$Separate {
	} elseif ((YMENU=$$$Separate) || (YMENU=$$$FlexibleMenu)) {
	;} elseif (YMENU=$$$Separate) {
		do ^WWWMENU5  ;SEPARATES FENSTER PLUS MINUS ;window plus minus 
		
	} elseif YMENU=$$$Image {
		do ^WWWMENU6  ;IMAGE
	}
	
	if $$$WWW012MenuFormated(YVOR) write YCR,"</TABLE>"
	if $$$WWW012MenuCentered(YVOR) write "</CENTER>"
	if $$$WWW012FontFace(YVOR)=""  write "</PRE>"
	if $$$WWW012MenuFramed(YVOR)   write YCR,"</TD></TR></TABLE>"
	write YCR,"</NOBR>"
	write YCR,"<BR>"
	
	do EVENT^WWWFORM  ;APPLET FÜR EVENTGBROKER ;to 
	do ^WWWSTOP           ; close body, HTML, etc. as required
	quit
 
 
LoginCheck(&YUSER)
	;-------------------------------------------------------------------------------
	; Check login details
	;
	; Params: 	YUSER
	;
	; Returns:	status
	;
	; History:
	; 01-Nov-2007	shobby	SRBR014748: Use standard password check $$CHECK^WWWPWDCHECK
	; 03-Apr-2007	GRF		SR15492: TerminationBy => TerminationOn
	; 26-Oct-2006	JW		BR014262: Created (Encapsulated, brace syntax)
	;-------------------------------------------------------------------------------
	new strStatus,objYUSER,strUserLocs,strMenuLocs,intLoop,dteTerminated,YPWD
	new tmeStart,intAttempt,YA,objUser
	
	set strStatus = $$$OK
	
	;BEDIENER SUCHEN ;seek 
	if YUSER'="" {
		set objYUSER = $get(^WWWUSER(0,YUSER,1))
		set YPWD     = $$$WWWUSERPassword1(objYUSER)
		if $$$WWWUSERUser1(objYUSER)'=YBED {
			set strStatus = $listbuild(5)		;USER NICHT MEHR AKTIV ;Not more ENABLED 
		}
	}
	
	if $$$ISOK(strStatus) {
		;PRUEFEN BERECHTIGUNG
		if '$data(^WWW013(0,YBED)) {
			set YUSER = ""
			do ^WWWFOOL(YBED,"no such user")
			set strStatus = $listbuild(5)		;FALSCHER USER
		}
	}
	
	if $$$ISOK(strStatus) {
	 	set objUser     = $get(^WWW013(0,YBED,1))
		set strUserLocs = $$$WWW013AllowedLocations(objUser)    ;USERBERECHTIGUNG FÜR BETRIEBE
		
		if strUserLocs'="" {
			set strMenuLocs = $translate($$$WWW012LocationsWithMenu($get(^WWW012(0,YM,1))),",",";")
			if $translate(strMenuLocs,";, ")'="" {
				for intLoop=1:1 {
					quit:$piece(strMenuLocs,";",intLoop)=""
					
					if $find(";"_strUserLocs_";",";"_$piece(strMenuLocs,";",intLoop)_";") {
						do ^WWWFOOL(YBED,"wrong location")
						set strStatus = $listbuild(5)			;FALSCHER BETRIEB ;location 
						quit
					}
				}
			}
		}
		if $$$ISOK(strStatus) {
			set dteTerminated = $$$WWW013TerminationOn(objUser)
			if (dteTerminated'="") && ($$$WWW013EmployeeCategory(objUser)'=999) && ($horolog>dteTerminated) {    ; SR15492
				do ^WWWFOOL(YBED,"no longer valid")
				set strStatus = $listbuild(5)  				;GEKÜNDIGT;3,12,2003; WENN NICHT SYSTEMADMIN
			}
		}
	}
	
	if $$$ISOK(strStatus) {
		set YA = $$^WWWPWDCHECK($$$WWW013Password1(objUser))  ;TYBD; PASSWORTSCHLÜSSEL;7,1,2004
		if (YBED="INTRAPREND") && ($get(%(YQUERY,"YRANDOM"))'="") && (+YPWD'=0) {
			if YPWD=$extract(%(YQUERY,"YRANDOM")+($random(+$horolog*91)),2,6) {
				kill %(YQUERY,"YRANDOM")
				set YPWD             = YA
				set %(YQUERY,"YPWD") = YA
				set $piece(^WWWUSER(0,YUSER,1),Y,1) = $zconvert(YPWD,"U")
			}
		}
		
		; BR014748if $$$UPPER(YA)'=$$$UPPER(YPWD) {	   ;FALSCHES PASSWORD
		if '$$CHECK^WWWPWDCHECK(YA,YPWD) {
			set YUSER = ""
			do ^WWWFOOL(YBED,"wrong password") 
			set intAttempt = 0
			
			set tmeStart = $piece($horolog,",",2)-600
			for {
				set tmeStart = $order(^WWWFOOL(0,+$horolog,tmeStart))
				quit:tmeStart=""
				
				if $data(^WWWFOOL(0,+$horolog,tmeStart,YBED)) {
					set intAttempt=intAttempt+1  ; ANZAHL DER FEHLVERSUCHE ;Number the 
				}
			}
			if intAttempt<5 {
				
				set strStatus = $listbuild(5)
			} else {
				
				// FIXME: This is just stupid.
				// Needs to at least be based on user id and probably also lock out remote IP
				
				hang 30
				set strStatus = $listbuild(408)   ;ZEITSPERRE, DA ZU VIELE FEHLVERSUCHE
			}
		}
	}
	quit strStatus
	
 
UpdateSessionDate()
	;-------------------------------------------------------------------------------
	; Update User Session Date Info on FINUserSessionDate form
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:	status
	;
	; History:
	; 19-Feb-2009	FIS		SR16065: log first session event
	; 26-Oct-2006	JW		BR014262: Created (encapsulated, added status)
	;-------------------------------------------------------------------------------
	new objForm,strStatus
	
	set strStatus = $$$OK
	if (YANZ="") && (YBED'="") && (YBEDBER=0) {
		kill ^CacheTempUserSessionDate($zutil(5),YBED)
		set ^CacheTempUserSessionDate($zutil(5),YBED) = +$horolog
		
		set objForm = $get(^WWW120(0,"FINUserSessionDate",1))
		if objForm'="" {
			set $$$WWW120FormInformation(objForm) = $$$Text($listbuild("Fin00817",$$^WWWDATE(+$horolog),0))
			set strStatus = $$$Save("WWW120","FINUserSessionDate",objForm,$$$YES)
		}  ; "User Session Date: %1"
	}
	
	do LogAction^WWWUSER(YUSER)  ;Log first Action time
	quit strStatus
	
	
Separate(pobjUser,blnShowMenu=$$$YES)
	;-------------------------------------------------------------------------------
	; Create the Separate Window frame set.
	;
	; Params:	pobjUser    - WWW013 record
	;           blnShowMenu - do not load menu window if $$$NO
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 23-Jan-2009	FIS		SR16205: Change (SR16205, 4-Dec-09) incorporated into
	; 							this label, other label deleted
	; 04-Dec-2008	FIS		SR16205: new Label: do not load menu window if
	; 							blnShowMenu=$$$NO
	; 27-Oct-2006	JW		BR014262: Created
	;-------------------------------------------------------------------------------
	new startForm
	
		// Full screen mode - show COMHeader
	if $$$WWW013useFullScreenandHeader(pobjUser) {
		$$$StartFrameSet(1,"rows='49,*' framespacing=0 frameborder=no border=0")
		
		write "<frame scrolling=""NO"" noresize src=""COMHeader.cls?EP=WWWFORM"
		do ^WWWCGI
		write """>"
		
		// Normal mode
	} elseif (blnShowMenu = $$$YES) {
		$$$StartFrameSet(0,"","showMenu(0);")  //load menu window
	
	} else {
		$$$StartFrameSet(0,"","")  //no menu //SR16205
	}
	
	
	set startForm = $$$WWW013StartForm(pobjUser)
	set:startForm="" startForm = "WWWBLANK"
	
	write "<frame src="""_YAKTION_"EP=WWWFORM&YFORM="_startForm_"&YMENUROT=1"

	do ^WWWCGI
	write """>"
	
	$$$EndFrameSet
	
	quit
	
 
Framed(pobjCompany,pobjUser)
	;-------------------------------------------------------------------------------
	; Create the frame set for a menu|form set up.
	;
	; Params:	pobjCompany	- WWW012 record
	; 			pobjUser	- WWW013 record
	;
	; Returns: nil
	;
	; History:
	; 20-Oct-2009	shobby	SR16948: Need to remember the original URL.  Save later
	; 							in CacheTempURLwhen we have a value for YUSER.
	; 27-Oct-2006	JW		BR014262: Created
	;-------------------------------------------------------------------------------
	new YXTARGET,YCOLS,YROWS,strForm
	
	set YXTARGET = $$$WWW012TargetFrameName(pobjCompany)
	
	set YCOLS = $piece(YXTARGET,"/",3)    if +YCOLS=0 set YCOLS = 28
	set YROWS = $piece(YXTARGET,"/",4)
	
	if $get(YTARGET2)="" set YTARGET2 = "FRAME3"
	if $get(YTARGET)=""  set YTARGET  = "FRAME2"
	
	$$$StartFrameSet(1," cols="""_YCOLS_"%,"_(100-YCOLS)_"%""")
	write "<FRAME SRC="""_YAKTION_"EP=WWWMENU&amp;YUSER="_YUSER_"&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM_"&amp;YXURL="_$get(YXURL)_""" scrolling=yes NAME=""MENUFRAME1"">"  ;16948 (required)
	write YCR
	
	// More than 1 frame
	if YROWS {
		write "<FRAMESET ROWS="""_YROWS_"%,"_(100-YROWS)_"%"">",YCR
		write "<FRAME SRC="""
		write $$FormURL^WWWCGI("WWWBLANK")						//BR014262
		write """ scrolling=yes NAME="""_YTARGET_""">",YCR
		write "<FRAME SRC="""
		write $$FormURL^WWWCGI("WWWBLANK")						//BR014262
		write """ scrolling=yes NAME="""_YTARGET2_""">"
		write "</FRAMESET>"
		
	} else {
		write YCR,"<FRAME SRC="""
		set strForm = $$$WWW013StartForm(pobjUser)
		if strForm="" set strForm = "WWWBLANK"
		write $$FormURL^WWWCGI(strForm,,"&amp;YFORMWAIT=1")		//BR014262
		write """ scrolling=yes NAME="""_YTARGET_""">"
	}
	
	$$$EndFrameSet
	
	quit
	
	
GetHeaderText(pobjCompany)
	;-------------------------------------------------------------------------------
	; Get the Header for the menu
	;
	; Params:	pobjCompany	- WWW012 record
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Oct-2006	JW		BR014262: Encapsulated.
	;-------------------------------------------------------------------------------
	new strHeader
	
	set strHeader = $$$WWW012HeaderTextUnderPicture(pobjCompany)
	
	if (SPRACHE'="DE") && ($piece(strHeader,",",2)'="") {		// FIXME Needs to change - this is rubbish
		set strHeader = $piece(strHeader,",",2)
	} else {
		set strHeader = $piece(strHeader,",",1)
	}
	quit strHeader
 
 
GetType()
	;-------------------------------------------------------------------------------
	; Get the current menu type.
	; 
	; NOTE: ^CacheTempFAILED is used to make sure a failed login does NOT attempt to
	; 		show the obscure menu types.
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:	enum - menu type
	;
	; History:
    ; 15-Jul-2014	shobby	HEVA-1565: Moved code to MenuType^WWW013
	; 14-Dec-2006	JW		BR014262: Created
	;-------------------------------------------------------------------------------
	quit $$MenuType^WWW013()	
 
]]></Routine>
</Export>