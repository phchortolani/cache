<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARBankInvoice" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARBankInvoice
	
#include FIN
#include FINConst
	
UpdateContainer(pidField="",parrGrid,pstrHeadYFELD="",pstrHeadYMFELD="")
    ;-------------------------------------------------------------------------------
    ; Run OnAfterDataFields, OnBlur and OnDelete
    ; 
    ; Returns:
    ; 
    ; History:
	; 12-Apr-2010	GRF		SR17177: parrGrid rather than pobjGrid
    ; 05-Sep-2005	JW		SR13268: Reverted SR12759 change.
    ; 11-Jul-2005	Steve S	SR12759: Store the total payment amount
    ; 16-Jun-2005	SCR		SR12637: Show Unallocated amount (Payment - Total Payments)
    ; 24-May-2005	JW		SR12271: Created
    ;-------------------------------------------------------------------------------
	new idBank,idLine,idInvoice,objInvoice,totalPayments,totalOwing,totalDiscount
	new CustPayment
	
	set totalPayments = 0
	set totalOwing    = 0
	set totalDiscount = 0
	
	set idBank = $order(parrGrid(""))
	if idBank'="" {
		set idLine = $order(parrGrid(idBank,""))
		if idLine'="" {
			set idInvoice = ""
			for {
				set idInvoice = $order(parrGrid(idBank,idLine,idInvoice))
				quit:idInvoice=""
				
				set objInvoice    = $get(parrGrid(idBank,idLine,idInvoice))
				set totalPayments = totalPayments + $$$FINARBankInvoicePayAmount(objInvoice)
				set totalOwing    = totalOwing    + $$$FINARBankInvoiceAmountOwing(objInvoice)
				
				if $$$FINARBankInvoiceDiscountApplied(objInvoice) {
					set totalDiscount = totalDiscount + $$$FINARBankInvoiceDiscount(objInvoice)
				}
			}
		}
	}
	
	set CustPayment = $$$FINARBankLineTotal(pstrHeadYFELD) ; Customer' Payment value
	set $piece(pstrHeadYMFELD,Y,8)  = totalOwing
	set $piece(pstrHeadYMFELD,Y,9)  = totalDiscount
	set $piece(pstrHeadYMFELD,Y,11) = totalPayments
	set $piece(pstrHeadYMFELD,Y,12) = totalOwing  - totalPayments
	set $piece(pstrHeadYMFELD,Y,14) = CustPayment - totalPayments + totalDiscount
	
	quit
	
	
OnBlur(pCOL,pYFELD,pYKEY)
    ;-------------------------------------------------------------------------------
    ; Code to run when leaving a cell
    ;
    ; Returns:
    ;
    ; History:
    ; 04-Jul-2005	PO		SR12595: Display an alert if outside of discount period
    ; 21-Jun-2005	SCR		SR12698: Tidy up the pay flag setting  (use +)
    ; 16-Jun-2005	SCR		SR12637: Keep pay flag on whenever there is an PayAmount
    ; 24-May-2005	JW		SR12271: Discount now updated in UpdateContainer
    ;  8-Feb-2005	JW		SR11691: Update Pay checkbox
    ; 07-Dec-2004	Shobby	Created (SR10468)
    ;-------------------------------------------------------------------------------
	new idBank,idBankLine,idBankInvoice,idCustomer,idDiscountTerm,intDiscountDays
	new objFINARCustomer,objFINSYSTerm,dteEndOfDiscount
 
	if pCOL=5  {
		do UpdateInvoiceBalance(.pYFELD)
		; paying full amount -> check (SR11691)
		set $$$FINARBankInvoicePay(pYFELD) = (+$$$FINARBankInvoicePayAmount(pYFELD)'=0)
	
	} elseif pCOL=8 {
		if $$$FINARBankInvoiceDiscountApplied(pYFELD) {
			if $$GetKey^COMUtilClass(pYKEY,,,.idBank,.idBankLine,.idBankInvoice) {
				set idCustomer       = $$GetCustomerNumber^FINARBankLineGet(idBank,idBankLine)
				set objFINARCustomer = $get(^FINARCustomer(YM,idCustomer,1))
				set idDiscountTerm   = $$$FINARCustomerDiscountTerm(objFINARCustomer)
				if idDiscountTerm'="" {
					set objFINSYSTerm   = $get(^FINSYSTerm(YM,idDiscountTerm,1))
					set intDiscountDays = $$$FINSYSTermDiscount1Days(objFINSYSTerm)
				} else {
					set intDiscountDays = 0
				}
				;set dteEndOfDiscount = $$$FINARBankBankingDate($get(^FINARBank(YM,idBank,1)))+intDiscountDays
				set dteEndOfDiscount = $$GetDueDate^FINARCustomer(idCustomer,$$$FINARBankInvoiceDueDate(pYFELD))
				if dteEndOfDiscount < $$$FINARBankBankingDate($get(^FINARBank(YM,idBank,1))) {
					$$$Alert($listbuild("Fin00828",$zconvert($$WWWTR^COMGridEdit31S(0,1,dteEndOfDiscount),"o","JS")))  ;Discount is no longer valid. Was valid upto %1.
					;do VBConfirm^COMConst("asF",36,"Hello")
				;} else {
					;do UpdateInvoiceBalance(.pYFELD)
					;set $$$FINARBankInvoicePay(pYFELD) = (+$$$FINARBankInvoicePayAmount(pYFELD)'=0)  ; paying full amount -> check (SR11691)
				}
			}
		}
	}
	
	//} elseif (pCOL=3) || (pCOL=8) {
	//	do UpdateDiscount(pYFELD)
	//}
	
	quit
	
	 
UpdateInvoiceBalance(pYFELD)
    ;-------------------------------------------------------------------------------
    ; Updates the invoice balance for the line.
    ;
    ; Returns:
    ;
    ; History:
    ; 07-Dec-2004	Shobby	Created (SR10468)
    ;-------------------------------------------------------------------------------
	set $$$FINARBankInvoiceInvoiceBalance(pYFELD)= $$$FINARBankInvoiceAmountOwing(pYFELD)-$$$FINARBankInvoicePayAmount(pYFELD)
	quit
	
	
UpdateDiscount(pYFELD) // Not currently in use
    ;-------------------------------------------------------------------------------
    ; Updates the Discount amount on the header.
    ; 
    ; Returns:
    ;
    ; History:
	; 12-Apr-2010	GRF		SR17177: arrGrid rather than objGrid; GRID Macros
    ; 16-Dec-2004	shobby	Declared objGrid (codecheck)
    ; 07-Dec-2004	Shobby	Created (SR10468)
    ;-------------------------------------------------------------------------------
	new arrGrid,idBank,idBankLine,idInvoice,curAmount,objFINARBankInvoice
	
	set curAmount = 0
	$$$GRIDGetContents(.arrGrid)
	set idBank = ""
	for {
		set idBank = $order(arrGrid(idBank))
		quit:idBank=""
		
		set idBankLine = ""
		for {
			set idBankLine = $order(arrGrid(idBank,idBankLine))
			quit:idBankLine=""
			
			set idInvoice = ""
			for {
				set idInvoice = $order(arrGrid(idBank,idBankLine,idInvoice))
				quit:idInvoice=""
				
				set objFINARBankInvoice = $get(arrGrid(idBank,idBankLine,idInvoice))
				if $$$FINARBankInvoiceDiscountApplied(objFINARBankInvoice) {
					set curAmount = curAmount+$$$FINARBankInvoiceDiscount(objFINARBankInvoice)
				}
			}
		}
	}
	do Header^COMGridEdit31G("M9",curAmount)
	quit
 
 
OnClick(pintCol,pYFELD,pblnShift,pblnCtrl)
    ;-------------------------------------------------------------------------------
    ; Event from a click of a check box in a grid. Automatically pays off invoices.
    ;
    ; Returns:
    ;
    ; History:
	; 01-Dec-2009	GRF		SR17007: separate tags for multi-lock operations
	; 02-Sep-2009	shobby	SRAdhoc: pblnCtrl parameter
    ; 22-Jun-2006	SteveS	SR14698: See if invoice is free before ticking
    ; 24-May-2005	JW		SR12433: Look at Pay field not Pay Amount
    ; 07-Dec-2004	Shobby	Created (SR10468)
    ;-------------------------------------------------------------------------------
	new strStatus,strYKEY,strLockKey,idCustomer,idInvoice
 
	set strStatus = $$$OK
	
	if pintCol=10 {
		set strYKEY    = $$$GRIDGetCurrentYKEY
		set idCustomer = $$$FINARBankLineCustomerNumber($$$GRIDYFELDContainer)
		set idInvoice  = $$$KEY3(strYKEY)
		set strLockKey = idCustomer_","_idInvoice
 		
		if $$$FINARBankInvoicePay(pYFELD) {
			set strStatus = $$CanPayInvoice^FINARBankInvoiceCheck(strYKEY)
			if $$$ISOK(strStatus) {
			;	set strStatus = $$GetLock^WWWMultiLock("FINARBankLine","FINARBankInvoice",strLockKey) ; SR17007
				set strStatus = $$LockSet^WWWMultiLock("FINARBankLine","FINARBankInvoice",strLockKey)
				if $$$ISOK(strStatus) {
					set $$$FINARBankInvoicePayAmount(pYFELD) = $$$FINARBankInvoiceAmountOwing(pYFELD)	
				}
			}
		} else {
			do KILL^WWWMultiLock("FINARBankLine","FINARBankInvoice",strLockKey,,$$$YES) //SR14698
			set $$$FINARBankInvoicePayAmount(pYFELD) = ""
		}
		
		if $$$ISOK(strStatus) {
			do UpdateInvoiceBalance(.pYFELD)
			
		} else {
			set $$$FINARBankInvoicePayAmount(pYFELD) = ""
			set $$$FINARBankInvoicePay(pYFELD)       = ""
			$$$Alert(strStatus)
			$$$StartScript()
			write "this.event.srcElement.checked=false;"
			$$$EndScript()
		}
	}
	quit
	
	
SetInvoiceBalance(pidBank,pidBankLine,pidBankInvoice)
    ;-------------------------------------------------------------------------------
	; quick calculation to determine the balance of the invoice.
	; 
	; History:
	; 29-Jun-2003	SCR		Bug Fix
    ;-------------------------------------------------------------------------------
	new objBankInvoice,curAmount
	
	set objBankInvoice = $get(^FINARBankInvoice(YM,pidBank,pidBankLine,pidBankInvoice,1))
	
	if objBankInvoice'="" {
		set curAmount = $$$FINARBankInvoiceAmountOwing(objBankInvoice)-$$$FINARBankInvoicePayAmount(objBankInvoice)
		if curAmount'=$$$FINARBankInvoiceInvoiceBalance(objBankInvoice) {
			set $$$FINARBankInvoiceInvoiceBalance(objBankInvoice) = curAmount
			do Save^FINSYS("FINARBankInvoice",pidBank_","_pidBankLine_","_pidBankInvoice,objBankInvoice,$$$YES)
		}
	}
	quit
	
	
PayInvoice(pYPARA)
	;-------------------------------------------------------------------------------
	; Routine to Pay of a single invoice with one click
	; 
	; History
	; 16-Dec-2004	JW		Use pYPARA instead of YPARA
    ; 03-May-2004   Shobby  Replaced YKOMMA with ","
	; 18-Sep-2003	shobby	Redirected some calls to FINARBankLine
	; 05-Aug-2003	shobby	Moved from FINARBank
	; 31-Jul-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new idBank,idBankLine,idBankInvoice,objFINARBankInvoice,curAmount,blnPay
	
	set idBank        = $$$KEY1(pYPARA)
	set idBankLine    = $$$KEY2(pYPARA)
	set idBankInvoice = $$$KEY3(pYPARA)
	
	if (idBank'="") && (idBankLine'="") && (idBankInvoice'="") {
		set blnPay = $piece(pYPARA,",",4)
		
		set objFINARBankInvoice=$get(^FINARBankInvoice(YM,idBank,idBankLine,idBankInvoice,1))
		set curAmount=$$$FINARBankInvoicePayAmount(objFINARBankInvoice)
		if curAmount = $$$FINARBankInvoiceAmountOwing(objFINARBankInvoice) {
			set curAmount=0
		} else {
			set curAmount=$$$FINARBankInvoiceAmountOwing(objFINARBankInvoice)
		}
		set $$$FINARBankInvoicePayAmount(objFINARBankInvoice)=curAmount
		if objFINARBankInvoice'="" {
			do OnBeforeSave^FINARBankLine(YKEY)
			do Save^FINSYS("FINARBankInvoice",pYPARA,objFINARBankInvoice,$$$YES)
			do Allocate^FINARBankInvoice(idBank,idBankLine,idBankInvoice,curAmount,$$$YES)
			do OnAfterSave^FINARBankLine(idBank,idBankLine)
		}
	}
	quit
	
	
DoAllocate()
	;------------------------------------------------------------------------------
	; Routine to do an AutoAllocation from a button click
	; 
	; History
    ; 03-May-2004   Shobby  Replaced YKOMMA with ","
	; 19-Sep-2003	shobby	Incorrect call to OnAfterSave^FINARBankLine
	; 31-Jul-2003	shobby	Used RedirectForm technology rather than ^WWWFORM,
	; 							doesn't mess with YBACK (TR005756)
	; 30-Jul-2003	shobby	Creation
	;------------------------------------------------------------------------------
	new pidBank,pidBankLine,objFINARBankLine
	
	set pidBank     = $$$KEY1(YKEY)
	set pidBankLine = $$$KEY2(YKEY)
	
	set objFINARBankLine = $get(^FINARBankLine(YM,pidBank,pidBankLine,1))
	set $$$FINARBankLineAutoAllocate(objFINARBankLine) = $$$YES
	do Save^FINSYS("FINARBankLine",YKEY,objFINARBankLine,$$$YES)
	
	do OnAfterSave^FINARBankLine(pidBank,pidBankLine)
	
	set $$$FINARBankLineAutoAllocate(objFINARBankLine) = $$$NO
	do Save^FINSYS("FINARBankLine",YKEY,objFINARBankLine,$$$YES)
	do ReloadForm^COMUtilForm()
	quit
	
	
Allocate(pidBank,pidBankLine,pidBankInvoice,pcurPaymentRemaining,pblnAuto)
    ;-------------------------------------------------------------------------------
	; this method sets the amount paying off this invoice from how much money is left
	; 
	; History.
	; 15-Jul-2005	RPW		SR12851: Fixed the check again to be consistent with
	; 							change in SR12637 which is that any non zero amount
	; 							leaves the checkbox checked.
	; 25-May-2005	JW		SR12433: Fixed check.
	; 18-Feb-2005	JW		SR11691: Added pay field
	; 08-Jul-2004	shobby	Foreign Currency.
	; 16-Mar-2004	shobby	TR007281: Passed in pidBank and pidBankLine into
	;							CheckDueDateForDiscount to allow calculation of
	;							Supplier Terms
	; 31-Jul-2003	shobby	Don't apply discounts less than 0 (TR005766)
	; 30-Jul-2003	shobby	Reworked the Discount calculation
	; 30-Jul-2003	SCR		Take Discount amount off Payment Remaining
	; 01-Jul-2003	shobby	TR005525: Only update the applydiscount flag when
	; 							'Auto Allocate' is on.
	; 01-Jul-2003	shobby	TR005521: Upgraded Discount calculations with call to
	; 							CheckDueDateForDiscount
	; 29-Jun-2003 	SCR		Check Due Date with Discount & Bug Fixes
    ;-------------------------------------------------------------------------------
	new curAmountToPay,objBankInvoice,curAmountDiscount,blnApplyDiscount,owing
	new Header,BankingDate,DueDate,blnApplyDiscountOld
	
	set Header=$get(^FINARBank(YM,pidBank,1))
	set BankingDate=$$$FINARBankBankingDate(Header)
 
	set objBankInvoice=$get(^FINARBankInvoice(YM,pidBank,pidBankLine,pidBankInvoice,1))
	
	set curAmountDiscount=$$$FINARBankInvoiceDiscount(objBankInvoice)
	set owing = $$$FINARBankInvoiceAmountOwing(objBankInvoice)
	if pblnAuto {  ;if we are auto allocating this time around try and pay the invoice with the money remaining
		set curAmountToPay=owing
		if pcurPaymentRemaining+curAmountDiscount<curAmountToPay {
			set curAmountToPay=pcurPaymentRemaining
			set blnApplyDiscount=0
			}
		else {
			set blnApplyDiscount=$$CheckDueDateForDiscount(objBankInvoice,BankingDate,.pcurPaymentRemaining,curAmountDiscount,pidBank,pidBankLine)
		}
		set $$$FINARBankInvoiceDiscountApplied(objBankInvoice)=blnApplyDiscount
	}
	else {
		set curAmountToPay=$$$FINARBankInvoicePayAmount(objBankInvoice)
		if (curAmountToPay+curAmountDiscount<owing)||(curAmountToPay+curAmountDiscount<0) {
			set blnApplyDiscount=0
			}
		else {
			set blnApplyDiscount=$$CheckDueDateForDiscount(objBankInvoice,BankingDate,.pcurPaymentRemaining,curAmountDiscount,pidBank,pidBankLine)
		}
	}
	if ($$$FINARBankInvoiceDiscountApplied(objBankInvoice))&('blnApplyDiscount) {
		set pcurPaymentRemaining=pcurPaymentRemaining+curAmountDiscount
	}
	set pcurPaymentRemaining=$$$FCSub(pcurPaymentRemaining,curAmountToPay)  ;deduct what we are paying from the payment remaining	
	set $$$FINARBankInvoicePayAmount(objBankInvoice)=curAmountToPay	
	
	set $$$FINARBankInvoicePay(objBankInvoice) = (+curAmountToPay '= 0)
	
	if $tr(objBankInvoice,"~")'="" {
		do Save^FINSYS("FINARBankInvoice",pidBank_","_pidBankLine_","_pidBankInvoice,objBankInvoice,1)
	}
	quit pcurPaymentRemaining
	
	
CheckDueDateForDiscount(pobjBankInvoice,pdteBankingDate,pcurPaymentRemaining,pcurAmountDiscount,pidBank,pidBankLine)
	;-------------------------------------------------------------------------------
	; Check and Apply a discount if within the due date.
	; 
	; History
	; 16-Mar-2004	shobby	Call to GetDueDate to take into account customer terms
	; 							when calculating DueDate.  (TR007281)
	; 01-Jul-2003	shobby	Creation (TR005521)
	; ------------------------------------------------------------------------------
	new blnApplyDiscount,dteDueDate,idFINARCustomer
	
	set idFINARCustomer=$$GetCustomerNumber^FINARBankLineGet(pidBank,pidBankLine)
	
	set dteDueDate=$$GetDueDate^FINARCustomer(idFINARCustomer,$$$FINARBankInvoiceDueDate(pobjBankInvoice))
	set blnApplyDiscount=0
	if dteDueDate'<pdteBankingDate {
		set blnApplyDiscount=1
		set pcurPaymentRemaining=pcurPaymentRemaining+pcurAmountDiscount
	}
	quit blnApplyDiscount
	
	
OnAfterSave(pidBank,pidBankLine,pidBankInvoice)
	;-------------------------------------------------------------------------------
	;
	; History
	; 04-May-2006	SteveS	SR14597: Don't allocate -- this is done during posting
	; 02-Dec-2004	PO		Same as SR11069, carried fix across as exactly same problem
	; 01-Dec-2004	PO		SR11072 Don't double up form when it loads
	; 19-Sep-2003	shobby	included parameter pidBankInvoice
	; 05-Aug-2003	shobby	Moved from FINARBank
	; 28-Jun-2003	shobby	Disable creating new records (TrackRecord 005460)
	;-------------------------------------------------------------------------------
	new curAmountLeft
	
	set curAmountLeft = $$Allocate(pidBank,pidBankLine,pidBankInvoice,"",$$$NO)
 
	do SetInvoiceBalances(pidBank,pidBankLine)
	
	; Disable creating new records (TrackRecord 005460)
	set %("%KEY","YKEY")=YKEY
	quit
	
	
SetInvoiceBalances(pidBank,pidBankLine)
	;-------------------------------------------------------------------------------
	;
	; History
	; 05-Aug-2003	shobby	Moved from FINARBank
	;-------------------------------------------------------------------------------
	new idBank,idBankLine,idBankInvoice
 	
	set idBankInvoice=""
	if (pidBank'="")&(pidBankLine'="") {
		for {
			set idBankInvoice=$order(^FINARBankInvoice(YM,pidBank,pidBankLine,idBankInvoice))
			quit:idBankInvoice=""
			do SetInvoiceBalance^FINARBankInvoice(pidBank,pidBankLine,idBankInvoice)
		}
	}
	quit
	
	
BeforeButtonLine(pKey)
	;--------------------------------------------------------------------------------
	; Makes the form read only if the user isn't allowed to play with it.
	; 
	; History:
	; 05-Oct-2004	Paul K	Now calls editable^FINSYSReview, test for "+"
	; 06-Aug-2003	shobby	Moved from FINARBank
	; 31-Jul-2003	shobby	Call to PayInvoice
	;--------------------------------------------------------------------------------
	new idBank
	
	set idBank = $$$KEY1(pKey)
	
	if (idBank'="") && (idBank'="+") {
		if '$$Editable^FINSYSReview("FINARBank",$$$FINARBankStatus($get(^FINARBank(YM,idBank,1)))) {
			set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
		}
	}
	quit
	
]]></Routine>
</Export>