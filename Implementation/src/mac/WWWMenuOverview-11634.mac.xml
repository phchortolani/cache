<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMenuOverview" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMenuOverview
	
#include WWWConst
#include COMSYS
	
OnBeforeFormConstruction()
	;-------------------------------------------------------------------------------
	; This form cannot work correctly unless FormType is set to Manual Input (without Button)
	;-------------------------------------------------------------------------------
	;set $$$WWW120FormType(YVOR) = 5
	quit
	
	
OnBeforeButtonLine()
	;-------------------------------------------------------------------------------
	; Make sure this form is set to "No Button Line" then manually build one to look the same
	; 
	; History:
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	do DisplayStyle()
	;do DisplayButtonLine()
	quit
	
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Render Menu
	; TODO: use list of styles isntead of directly writing out background images
	; 
	; History:
	; 16-Nov-2010	PPP		SR17614: 1. Include items that have only the 'link to' defined
	;						2. Include 'Target for Output'  if defined
	; 27-Apr-2010	GRF		SR16402: Don't pass YM to Get^WWW004 - always 0
	; 28-Feb-2009	HQN		SR16402: Use $$Get^WWW004 to obtain localised record
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new arrUserMenu,idApplicn,idSection,intColCount,intMenuCount
	new loopSubmenus,objMenu,objSubmenu
	new strLink,strForm,strCSSClass,strImage,strImageDirectory,strLinkWith,strTarget
	
	do DisplayUser()
	do DisplayLocation()
	
	&html<<div class="MenuOverviewWrapper">>
	
	set intMenuCount = 0
	set intColCount  = 3
	set idApplicn    = $$$WWW013FlexibleMenuApplicationNa($get(^WWW013(0,YBED,1)))
	set strImageDirectory = $translate($piece($get(^WWW012(0,0,1)),"~",47),"\","/")
	do GetMenu^WWWMenuOverview(YBED,idApplicn,.arrUserMenu)
	set idSection = ""
	for {
		set idSection = $order(arrUserMenu(idSection))
		quit:idSection=""
		
		set objMenu = $$Get^WWW004(idApplicn,idSection)
		set strImage = $$$WWW004ImageFilegif(objMenu)
		
		&html<
			<ul class="MenuOverview">
			<div class="Section" style="background-image: url(#(strImageDirectory)#/#(strImage)#);">
				<span class="Title">#($$$WWW004MenuDescription(objMenu))#</span>
			</div>
		>
		for loopSubmenus=1:1:$length($get(arrUserMenu(idSection)),";") {
			if ($data(^WWW004(0,idApplicn,$piece(arrUserMenu(idSection),";",loopSubmenus)))){
				set objSubmenu = $$Get^WWW004(idApplicn,$piece(arrUserMenu(idSection),";",loopSubmenus))
			} else {
				set objSubmenu = ""
			}
			set strForm = $$$WWW004FormName(objSubmenu)
			set strImage = "html.gif"
			
		  	//SR17614
			set strLinkWith = $$$WWW004LinkWith(objSubmenu)
			set strTarget	= $$$WWW004TargetNameForOutput(objSubmenu)
			
			set:($$$WWW004ImageFilegif(objSubmenu)'="") strImage = $$$WWW004ImageFilegif(objSubmenu)
			
			if ($$$WWW004StartFormWithSearchFuncti(objSubmenu) = 1){
				set strLink = YAKTION_"EP=WWWSEAR&YFORM="_strForm_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER
			
			} elseif(strForm'="") {
				set strLink = YAKTION_"EP=WWWFORM&YFORM="_strForm_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YBACK="_YFORM_","
		  	//SR17614
			} elseif(strLinkWith'="") {
				set strLink = strLinkWith

			} else {
				continue ; Don't bother to display submenu
			;	set strLink = "#"
			}
			
			if ($piece(arrUserMenu(idSection),".",2) = "") {
			} else {
				if strImage = "html.gif" {
					set strCSSClass = " class=""default"""
				} else {
					set strCSSClass = " style=""list-style-image:url("_strImageDirectory_"/"_strImage_");"""
				}
				if strTarget '= "" {
					&html<<li#(strCSSClass)#><a href="#(strLink)#" target="#(strTarget)#">#($$$WWW004MenuDescription(objSubmenu))#</a></li>>
				} else {
					&html<<li#(strCSSClass)#><a href="#(strLink)#">#($$$WWW004MenuDescription(objSubmenu))#</a></li>>				
				}
			}
		}
		&html<</ul>>
		set intMenuCount = intMenuCount + 1
		if (intMenuCount#intColCount=0) {
			&html<<hr class="MenuOverviewDivider"/>>
		}
	}
	&html<<hr class="MenuOverviewDivider"/>>
	&html<</div>>
	quit
	
	
DisplayUser()
	;-------------------------------------------------------------------------------
	; History:
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new objWWW013
	
	set objWWW013 = $get(^WWW013(0,YBED,1))                ; "User"
	&html<<div class="UserDetails">#($$$Text(232))#:#($$$WWW013Name(objWWW013))#(#(YBED)#)</div>>
	quit
	
	
DisplayLocation()
	;-------------------------------------------------------------------------------
	; History:
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new objWWW0121
	
	set objWWW0121 = $get(^WWW0121(0,0,YLOCATION,1))      ;  "Location"
	&html<<div class="LocationDetails">#($$$Text(388))#:#(YLOCATION)# - #($$$WWW0121LocationName(objWWW0121))#</div>>
	quit
	
	
DisplayStyle()
	;-------------------------------------------------------------------------------
	; History:
	; 22-Apr-2010	shobby	SR17253: Cross-Browser Support
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new objWWW012,strColourLeft,strColourRight,strImageDirectory
	
	set objWWW012 = $get(^WWW012(0,0,1))
	; $$$SysEnum("FARBE",$$$WWW012ColorforWarnings(objCompany))
	set strColourLeft     = $$$SysEnum("FARBE",$$$WWW012ColorCodeForHeaderLeft(objWWW012))
	set strColourRight    = $$$SysEnum("FARBE",$$$WWW012ColorCodeForHeaderRight(objWWW012))
	set strImageDirectory = $translate($$$WWW012PictureDirectorySystem(objWWW012),"\","/")
	&html<
		<style>
		.UserDetails {
			text-align:center;
			background-color:#FFFFFF;
			font-weight:bold;
			color:#333333;
		}
		.LocationDetails {
			text-align:center;
			background-color:#FFFFFF;
			color:#333333;
		}
		.MenuOverviewWrapper {
			color:#000000;
			background-color:#FFFFFF;
		}
		UL.MenuOverview {
			list-style:none;
			width:265px;
			float:left;
			padding:0px;
			margin:0px;
			background-color:#(YWHITE)#;
		}
		UL.MenuOverview DIV.Section {
			height:45px;color:#0B48AE;
			line-height:65px;
			font-weight:bold;
			text-indent: 26px;
			font-size:14px;
		}
		UL.MenuOverview DIV.Section SPAN.Title {
			position:relative;
			top:10px;
		}
		UL.MenuOverview LI A{
			color:#000000;
			text-decoration:underline;
			padding-left:5px;
		}
		UL.MenuOverview A:Hover {
			color: #000000;
			background-color: #E0E0E0;
		}
		UL.MenuOverview LI {
			margin-left:20px;
			list-style-position: inside;
		}
		HR.MenuOverviewDivider {
			clear:left;
			width:795px;
			visibility:hidden;
		}
		.SystemToolbar .Title {
			font-size:medium;
			font-weight:bold;
			color:#(YWHITE)#;
			float:left;
		}
		.Title A:Hover {
			color:#(YWHITE)#;
		}
		.Title .SystemInformation {
			font-size:10px;
			font-style:normal;
			color:#(YWHITE)#;
		}
		.SystemToolbar .NavButton {
			position:relative;
			right:0px;
			top:2px;
			border-width:0;
			vertical-align:middle;
			float:right;
		}

		.SystemToolbar {
			position:relative;
			width:100%;
			border:2px outset;
			background-color:#(strColourLeft)#;
			#($$ImageTransformGradient^WWWFORMCrossBrowserSupportVisual(strColourLeft,strColourRight))#;
			border-right:none;
		}
		LI.default {
			list-style-image:url(#(strImageDirectory)#/html.gif);
			list-style-position:inside;
		}
		</style>
	>
	quit
	
	
DisplayButtonLine()
	;-------------------------------------------------------------------------------
	; NOTE: Not used
	; 
	; ByRef: 
	; 	YAKTION
	; 	YGIF
	; 	YUCI
	; 	YBED
	; 	YFORM
	; 	
	; History:
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new strTitle,objWWW012,strCGI
	
	set strTitle  = ""
	set objWWW012 = $get(^WWW012(0,0,1))
	set strCGI    = $$WWWCGI2^WWWCGI($$$NO)
	&html<
	<style>
		A.MenuButton {
			width:84px;
			height:27px;
			background-image:url(#(YGIF)#MenuOff.gif);
			float:right;
		}
		A.MenuButton:hover{
			background-image:url(#(YGIF)#MenuOn.gif);
		}
		A.OverviewButton {
			width:84px;
			height:27px;
			background-image:url(#(YGIF)#StartOff.gif);
			float:right;
		}
		A.OverviewButton:hover {
			background-image:url(#(YGIF)#StartOn.gif);
		}
	</style>
	<DIV class="SystemToolbar"><A NAME="start"/><span class="Title">#(strTitle)#<a href="#" class="SystemInformation" title="#($$GetSystemInfoTooltip^WWWKOPF())#">(#(YM)#/#(YBED)#)</a></span>
	<IMG class="NavButton" SRC="#(YGIF)#exit.gif" TITLE="#($$^WWWTEXT(33980))#" onClick="top.document.location.href='#($$getLoginPage^WWWLogin())#';"/>
	<a href="#(YAKTION)#EP=WWWFORM&YFORM=#(YFORM)#&YKEY=#(strCGI)#" class="OverviewButton" title="Overview"></a>
	<a href="#(YAKTION)#EP=WWWMENU#(strCGI)#" target="FRAME3#(YUSER)#" onclick="parent.showMenu(1);return false;" class="MenuButton" title="Menu"></a>
	</DIV>
	>
	;&html<<TABLE><TR><TD></TD>>
	;                                                      ; 33980 "Logout"
	quit
	
	
GetMenu(pidUser,pidRootNode,&arrUserMenu)
	;-------------------------------------------------------------------------------
	; Retrieves all menus (1 level deep) that are accessable based on a list of
	; AccessProfiles
	; 
	; ByRef:
	; 	YM		Company ID
	; History:
	; 15-Nov-2010	PPP		SR17614: Include items that have only the link to defined and not delete it
	; 27-Apr-2010	GRF		SR16402: Don't pass YM to Get^WWW004 - always 0;
	; 							variables not newed
	; 28-Feb-2009	HQN		SR16402: Use $$Get^WWW004 to obtain localised record
	; 28-Jan-2009	SCR		SR16240: Changed WWWACCESS useage
	; 12-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new blnOK,idMenu,intMenuCount,loopMenu,objMenu,strUser
	
	set arrUserMenu = ""
	
	set strUser = $get(^WWW013(0,pidUser,1))
	if strUser '= "" {
		set idMenu = ""
		if ($data(^WWW004(0,pidRootNode)) '= 0) {
			for {
				set idMenu = $order(^WWW004(0,pidRootNode,idMenu))
				quit:idMenu=""
				
				set objMenu = $$Get^WWW004(pidRootNode,idMenu)
				if ($$^WWWACCESS($$$WWW004UserAccess(objMenu),$$$WWW004Module1(objMenu),pidUser) = $$$OK) {
					if ($data(arrUserMenu($piece(idMenu,".",1)_".")) = 0) {
						set intMenuCount = 1
					} else {
						set intMenuCount = $length($get(arrUserMenu($piece(idMenu,".",1)_".")),";") + 1
					}
					if ($piece(idMenu,".",2)'="") && ($piece(idMenu,".",3)="") {
						set $piece(arrUserMenu($piece(idMenu,".",1)_"."),";",intMenuCount) = idMenu
					}
				}
			}
		}
	}
	
	; Remove invalid entries for this menu type...
	; No leaf nodes, remove
	; No sub entries allowed
	set idMenu = ""
	for {
		set idMenu = $order(arrUserMenu(idMenu))
		quit:idMenu=""
		
		set blnOK = $$$NO
		for loopMenu=1:1:$length(arrUserMenu(idMenu),";") {
			//SR17614
			set objMenu = $$Get^WWW004(pidRootNode,$piece(arrUserMenu(idMenu),";",loopMenu))
			if ($$$WWW004FormName(objMenu) '= "") || ($$$WWW004LinkWith(objMenu) '= "") {
				set blnOK = $$$YES
				quit
			}
		}
		if (blnOK = $$$NO) kill arrUserMenu(idMenu)
	}
	quit
	
	
GetMenuDescription(pidApplicn,pidSection) ; NOT USED
	quit
	
]]></Routine>
</Export>