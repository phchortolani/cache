<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FININTHCBatch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FININTHCBatch  ; FINANCE Interface Batch Health Checks.
	
#include FINHC
	
Check(Start="",End="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; Checks that no FININTBatch or FININTBatchLine are missing
    ; 
    ; ByRef : Result
    ; 
    ; Returns: strStatus
    ;
    ; History:
    ; 17-Nov-2004	GRF		Replace YKOMMA with $$$COMMA
    ; 18-May-2004	Shobby	Created
    ;-------------------------------------------------------------------------------
	new blnData,idBatch,idBatchLine,intErrorCounter,objFININTBatch,strStatus

	if $get(Y)="" do ^WWWVAR
	kill Result
	set strStatus       = $$$OK
	set intErrorCounter = 0
	
	set idBatch = ""
	for {
		set idBatch = $order(^FININTBatch(0,idBatch))
		quit:idBatch=""
		
		set blnData = $data(^FININTBatchLine(0,idBatch))
		if blnData=0 {
			set objFININTBatch = $get(^FININTBatch(0,idBatch,1))
			set Result("Hash",$$$ErrorNo(Result("Errors"))) = "Batch ^FININTBatch(0,"_idBatch_$$$COMMA_"1), BatchType='"_$$$FININTBatchBatchType(objFININTBatch)_"', does not have any FININTBatchLine."
			set strStatus = $$$NO
		}
	}
	
	set idBatch = ""
	for {
		set idBatch = $order(^FININTBatchLine(0,idBatch))
		quit:idBatch=""
		
		set blnData = $data(^FININTBatch(0,idBatch))
		if blnData=0 {
			set Result("Hash",$$$ErrorNo(Result("Errors"))) = "Batch ^FININTBatch(0,"_idBatch_$$$COMMA_"1) is missing."
			set strStatus = $$$NO
		}
	}
	
	if strStatus=$$$OK {
		set strStatus = 2  ; Because this is a Standalone Check return 2 if Ok
	}
	quit strStatus
	
]]></Routine>
</Export>