<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="VARExportarCSV" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[VARExportarCSV

	#include COMSYS
		
	;---------------------------------------------------------------------------------------------------------------------
	; Arquivo contendo rotinas para exportação de uma global para o CSV.
	;
	; Para funcionar é necessário gravar em uma global os dados que se deseja exportar para o CSV.
	; A global deve estar no formato ^NomeGlobal(YM,YUSER,YBED,Cont,1)
	;	Onde:
	;		- "Cont" deve ser um sequencial numérico começando por 0 (zero).
	;		- Na linha 0 (zero) devem estar os títulos das colunas que serão gravados na planilha.
	;		- Se não existir linha 0(zero), a planilha ficará sem títulos nas colunas.
	;
	; Depois, basta chamar a rotina ExportarCSV^VARExportarCSV("NomeGlobal") em qualquer evento.
	;	Ex:
	;	--------------------------------------------Início do Exemplo
	;	ExecuteOnClickBotao(pYKEY)
	;		set ^VARTempRelatorioCSV(YM,YUSER,YBED,0,1) = "Título1~Título2~Título3~Título4"
	;		set ^VARTempRelatorioCSV(YM,YUSER,YBED,1,1) = "Linha1Col1~Linha1Col2~Linha1Col3~Linha1Col4"
	;		set ^VARTempRelatorioCSV(YM,YUSER,YBED,2,1) = "Linha2Col1~Linha2Col2~Linha2Col3~Linha2Col4"
	;		set ^VARTempRelatorioCSV(YM,YUSER,YBED,3,1) = "Linha3Col1~Linha3Col2~Linha3Col3~Linha3Col4"
	;		do ExportarCSV^VARExportarCSV("VARTempRelatorioCSV")		;O nome da global não pode conter o caracter ^(circunflexo) no início
	;		do GoToForm^COMUtilForm("VARRelatorio",pYKEY)
	;		quit
	;	--------------------------------------------Fim do Exemplo
	;
	; Histórico
	; 06-Dez-2015	Henrique Dias	IPIRANGA-364
	; 13-Out-2011 - PTK		Criação
	;
	;---------------------------------------------------------------------------------------------------------------------
	
ExportarCSV(pGlobal,insideJavascript=0)

	if 'insideJavascript $$$StartScript()
	do WindowPrint^COMViewPrint($$$EnumDisplayTypeCSV,"Output^VARExportarCSV(.objOutput,"_""""_pGlobal_""""_")")
	if 'insideJavascript $$$EndScript()
	 
	quit    


Output(&pobjOutput,pGlobal)
	;-------------------------------------------------------------------------------
	; Rotina para gerar objData a partir da Global passada como parâmetro
	;
	; Histórico
	; 13-Out-2011 - PTK		Criação
	;-------------------------------------------------------------------------------
 	new ExistemDados,Titulos,idLinha,objLinha,objData
 	
 	quit:$get(YFORM)=""
	quit:$get(YUSER)=""
	quit:$get(YBED)=""
	quit:$get(pGlobal)=""
	xecute "set ExistemDados = $D(^"_pGlobal_"(YM,YUSER,YBED))"
	
	quit:('ExistemDados)
	
	xecute "set Titulos = $G(^"_pGlobal_"(YM,YUSER,YBED,0,1))"
	;Se tiver uma linha com os títulos monta o cabeçalho
	if (Titulos '= ""){
		do pobjOutput.WriteLine($replace(Titulos,"~",";"))
	}
	
	set idLinha = ""
	for {
		xecute "set idLinha = $order(^"_pGlobal_"(YM,YUSER,YBED,idLinha))"
		quit:idLinha=""
		
		;Não precisa exportar o cabeçalho novamente
		if idLinha=0 {
			continue
		}
		
		xecute "set objLinha = $G(^"_pGlobal_"(YM,YUSER,YBED,idLinha,1))"

		set objData(idLinha) = objLinha
		do pobjOutput.WriteLine($replace(objLinha,"~",";"))
	}

	quit]]></Routine>
</Export>