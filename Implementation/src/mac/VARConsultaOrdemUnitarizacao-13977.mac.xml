<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARConsultaOrdemUnitarizacao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARConsultaOrdemUnitarizacao
	#include COMSYS
	#include INConst
	#include VARConst
	quit

OnAfterSave()
	new pCodOrdemUnitarizacao, pNotaFiscal, pDataCriacao, pCodProduto, pLote

	kill ^VARTempOrdemUnitarizacaoFiltro(YM, $get(YBED)) // Filtro dos campos

	set pCodOrdemUnitarizacao = $get(VORG(1))
	set pNotaFiscal           = $get(VORG(2))
	set pDataCriacao          = $get(VORG(3))
	set pCodProduto           = $get(VORG(4))
	set pLote                 = $get(VORG(5))

	// Save fields in a Temp table, so the form can be load soon as the
	// user get in
	set ^VARTempOrdemUnitarizacaoFiltro(YM, $get(YBED)) =
		pCodOrdemUnitarizacao_Y_pNotaFiscal_Y_pDataCriacao_Y_pCodProduto_Y_
		pLote

	do GoToForm^COMUtilForm("VARConsultaOrdemUnitarizacao", "")
	quit

OnAfterDataFields()
	new lstHeader, strFiltros, strCodOrdemUnitarizacao, strNotaFiscal,
		datCriacao, strCodProduto, strLote, strSQL, strParam1, strParam2,
		strParam3, strParam4, strParam5, intCount, strStatus, datValidade,
		dblQuantidade, strNotaFiscal, strDescProduto, strDescStatus

	write !, "<script language=""javascript"">"
	&js<
		addEvent(window, 'load', OnLoadWindow);

		function OnLoadWindow() {
			document.#(YHTMFORM)#.#("Y"_YFORM_"M1")#.focus();
		}

		function addEvent(obj, evType, fn) {
 			if (obj.addEventListener) {
   				obj.addEventListener(evType, fn, false);
   				return true;
 			}
 			else if (obj.attachEvent) {
   				return obj.attachEvent('on' + evType, fn);
 			}
 			else {
   				return false;
 			}
		}	
	>
	write !, "</script>"

	if ('$data(^VARTempOrdemUnitarizacaoFiltro(YM, $get(YBED)))) quit

	// Monta cabeçalho da tabela
  	set lstHeader = $listbuild("Seq.", "Ordem<br/>Fracionamento",
  		"Data Criação", "Status da<br/>Ordem", "Medicamento", "Lote",
  		"Validade", "Quantidade", "Nota Fiscal")

  	if ($$Start^COMTable(lstHeader, "<div style=margin:3px;>Ordens de "_
  			"fracionamento:</div>", , , $$$NO)) {

		set strFiltros = ^VARTempOrdemUnitarizacaoFiltro(YM, $get(YBED))
		set strCodOrdemUnitarizacao = $piece(strFiltros, Y, 1)
		set strNotaFiscal           = $piece(strFiltros, Y, 2)
		set datCriacao              = $piece(strFiltros, Y, 3)
		set strCodProduto           = $piece(strFiltros, Y, 4)
		set strLote                 = $piece(strFiltros, Y, 5)

		set strSQL = "SELECT    OrdUnit.CodOrdemUnitarizacao, "_
					 "          +OrdUnit.CriadoEm AS DataCriacao, "_
					 "          OrdUnit.Status, "_
					 "          OrdUnit.Produto, "_
					 "          OrdUnit.Lote, "_
					 "          OrdUnit.Validade, "_
					 "          +OrdUnit.Quantidade AS Quantidade, "_
					 "          Rec.InvoiceNumber AS NotaFiscal "_
					 "FROM      SQLUser.VAROrdemUnitarizacao AS OrdUnit "_
					 "LEFT JOIN SQLUser.INREC AS Rec "_
					 "ON        Rec.Company = OrdUnit.Company "_
					 "AND       Rec.ReceiptNumber = $piece(OrdUnit.Recebimento,"","",1) "_
					 "WHERE     (OrdUnit.Company = 0) "
		set intCount = 1
		if ($length(strCodOrdemUnitarizacao) > 0) {
			set strSQL = strSQL_
						 "AND (OrdUnit.CodOrdemUnitarizacao = ?) "
			set @("strParam"_intCount) = strCodOrdemUnitarizacao
			set intCount = $increment(intCount)
		}
		if ($length(strNotaFiscal) > 0) {
			set strSQL = strSQL_
						 "AND (Rec.InvoiceNumber = ?) "
			set @("strParam"_intCount) = strNotaFiscal
			set intCount = $increment(intCount)
		}
		if ($length(datCriacao) > 0) {
			set strSQL = strSQL_
						 "AND (+OrdUnit.CriadoEm = ?) "
			set @("strParam"_intCount) = datCriacao
			set intCount = $increment(intCount)
		}
		if ($length(strCodProduto) > 0) {
			set strSQL = strSQL_
						 "AND (OrdUnit.Produto = ?) "
			set @("strParam"_intCount) = strCodProduto
			set intCount = $increment(intCount)
		}
		if ($length(strLote) > 0) {
			set strSQL = strSQL_
						 "AND (OrdUnit.Lote = ?) "
			set @("strParam"_intCount) = strLote
			set intCount = $increment(intCount)
		}

	 	set rs = ##class(%ResultSet).%New()
	    do rs.Prepare(strSQL)
	    do rs.Execute($get(strParam1), $get(strParam2), $get(strParam3),
	    	$get(strParam4), $get(strParam5))

		set intCount = 0

	    while (rs.Next()) {
			if ($System.Status.IsOK(rs)) { // check if this succeeded
										   // without errors		
	        	// Pegando valores da query
	        	set strCodOrdemUnitarizacao = rs.GetData(1)
	        	set datCriacao              = rs.GetData(2)
	        	set strStatus               = rs.GetData(3)
	        	set strCodProduto           = rs.GetData(4)
	        	set strLote                 = rs.GetData(5)
	        	set datValidade             = rs.GetData(6)
	        	set dblQuantidade           = rs.GetData(7)
	        	set strNotaFiscal           = rs.GetData(8)

				set strDescStatus = strStatus
				if ($length(strStatus) > 0) {
					set strDescStatus =
						$$$VARPARAText($get(^VARPARA(YM, "STATUSUNITARIZACAO", SPRACHE, strStatus, 1)))
				}
				set strDescProduto = strCodProduto_" - "_
					$$$INARTSearchName($get(^INART(YM, strCodProduto, 1)))

				set intCount = $increment(intCount)

    			do NewLine^COMTable()
    			do InsertCell^COMTable(intCount,
    				"VAROrdemUnitarizacao", strCodOrdemUnitarizacao)
			    do InsertCell^COMTable(strCodOrdemUnitarizacao,
			    	"VAROrdemUnitarizacao", strCodOrdemUnitarizacao)
			    do InsertCell^COMTable($$^WWWDATE(datCriacao),
			    	"VAROrdemUnitarizacao", strCodOrdemUnitarizacao, ,
			    	"CENTER")
			    do InsertCell^COMTable(strDescStatus,
			    	"VAROrdemUnitarizacao", strCodOrdemUnitarizacao, ,
			    	"CENTER")
			    do InsertCell^COMTable(strDescProduto,
			    	"VAROrdemUnitarizacao", strCodOrdemUnitarizacao)
			    do InsertCell^COMTable(strLote, "VAROrdemUnitarizacao",
			    	strCodOrdemUnitarizacao)
			    do InsertCell^COMTable($$^WWWDATE(datValidade),
			    	"VAROrdemUnitarizacao", strCodOrdemUnitarizacao, ,
			    	"CENTER")
			    do InsertCell^COMTable($$^WWWTR(0, 12, dblQuantidade),
			    	"VAROrdemUnitarizacao", strCodOrdemUnitarizacao, ,
			    	"RIGHT")
			    do InsertCell^COMTable(strNotaFiscal, "VAROrdemUnitarizacao",
			    	strCodOrdemUnitarizacao, , "CENTER")
		        do EndLine^COMTable()
			}
	    }

	    if (intCount = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhuma ordem de fracionamento registrada "_
				"para este medicamento.", , , , , , 9)
			do EndLine^COMTable()
	    }

	 	do Stop^COMTable()
  	}
	quit

]]></Routine>
</Export>