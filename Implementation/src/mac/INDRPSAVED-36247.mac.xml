<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPSAVED" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPSAVED(KEY,FELD) ;INDRPSAVED;DT;SPEICHERN/BERICHTIGEN SUPPLY AUS DEMAND;03.12.2001
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SPEICHERN/BERICHTIGEN SUPPLY AUS DEMAND
	;       CREATE/UPDATE SUPPLY ON DESTINATION LOCATION
	;       
	; DATEN DES DEMANDS AUS DEM BETRIEB, DER DIE ARTIKEL BETEITSTELLT WERDEN
	; WIEDER AND DEN BETRIEB GESANDT, WENN VERÄNDERUNGEN VORKOMMEN. 
	; 
	; Inputs : 
	;    KEY	: Site Locn "," Demand No
	;    FELD	: objINDRPDEMAND or instruction "KILL"
	; 
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 15-May-2007	RPW		SR15513: Do not supply blocked demands.
	; 16-Apr-2007	PO		SR15497: Use Save macro
	; 12-Jul-2006	PO		SR14833: If supply not provided with a due date then set the due date to today
	; 28-Apr-2006	PO		SR14427: Removed check of DRP/MPR Data Exchange, field 80 of INVORG
	; 04-Oct-2005	GRF		Doco
	; 08-Jun-2005	GRF		Clarify
	; 03.12.2001	DT
	;-------------------------------------------------------------------------------
	NEW DEMAND,SUPPLY,NEU,BETRIEBV,BETRIEBN,YKEY,YFELD,XKEY,YI
	
	;+++++++++++++++++++++++++++++++++++++++
	; FELD			objINDRPDEMAND		Demand
	; YFELD			objINDRPDEMAND		Demand
	; YFELD(1)		objINDRPSUPPLY
	;+++++++++++++++++++++++++++++++++++++++
	
	QUIT:$GET(KEY)=""
	QUIT:$GET(FELD)=""
	
	SET YFELD = FELD
	
	quit:$$$INDRPDEMANDBlocked(YFELD) // SR15513 Do not supply blocked demands
	
	SET KEY    = $TRANSLATE(KEY,"""")
	SET KEY(1) =$$$KEY1(KEY)		QUIT:KEY(1)=""       ;VON BETRIEB ;location 
	SET KEY(2) =$$$KEY2(KEY)		QUIT:KEY(2)=""       ;VON DEMANDNUMMER
 
	IF YFELD="KILL" DO  QUIT  ;LÖSCHEN DATENSATZ ;Delete data record 
	. SET YKEY(2) = ""
	. SET YKEY(1) = ""
	. IF KEY(1)'="" IF KEY(2)'="" DO
	. . SET YKEY(1) = $ORDER(^INDRPSUPPLYs(YM,6,$$^WWWUMLAU(KEY(1),1),$$^WWWUMLAU(KEY(2),1),YKEY(1))) IF YKEY(1)'="" SET YKEY(2)=$ORDER(^INDRPSUPPLYs(YM,6,$$^WWWUMLAU(KEY(1),1),$$^WWWUMLAU(KEY(2),1),YKEY(1),""))
	. ;
	. IF YKEY(2)'="" IF YKEY(1)'="" DO
	. . SET KEY = "^INDRPSUPPLY("_""""_YM_""""_","_""""_YKEY(1)_""""_","_""""_YKEY(2)_""""_",1)"
	. . SET YOK = $$^WWWSETK(KEY)
	. . DO ^WWWSKILL(KEY,,1)
	
	IF $PIECE(YFELD,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=1 SET ^INDRPNETCHANGE(YM,$PIECE(YFELD,Y,4),1)=""  ;FIS;29.04.05;SR12200
	SET SDART = $PIECE(YFELD,Y,70)
	
	;+++++++++++++++++++++++++++++++++++++++                              ; EARLY EXITS
	;IF SDART'=0 IF SDART'=3 QUIT  ;NUR MANUELLE UND TRANSFERAUFTRÄGE
	IF SDART'=3 QUIT               ;NUR TRANSFERAUFTRÄGE ;only 
	
	SET YKEY(1) = $PIECE(FELD,Y,1)   ;AN BETRIEB ;upon location 
	SET YKEY(2) = $PIECE(YFELD,Y,2)  ;SUPPLYNUMMER
	SET $PIECE(YFELD,Y,1) = KEY(1)   ;ALTER SCHLUESSEL  ;VON BETRIEB ;governor location 
	SET $PIECE(YFELD,Y,2) = KEY(2)   ;ALTER SCHLUESSEL;  DEMANDNUMMER
	
	IF (YKEY(1)="") || (YKEY(1)=KEY(1)) QUIT     ;INTERN KEIN AUSTAUSCH ;internal no 
	;+++++++++++++++++++++++++++++++++++++++                              ; ^^^^^
	
	
	IF YKEY(2)="" IF YKEY(1)'="" IF KEY(1)'="" IF KEY(2)'="" DO
	. IF $DATA(^INDRPSUPPLYs(YM,6,$$^WWWUMLAU(KEY(1),1),$$^WWWUMLAU(KEY(2),1),YKEY(1))) SET YKEY(2)=$ORDER(^INDRPSUPPLYs(YM,6,$$^WWWUMLAU(KEY(1),1),$$^WWWUMLAU(KEY(2),1),YKEY(1),""))
	
	IF YKEY(2)="" SET YKEY(2) = $$^WWWNEXT("INDRPSUPPLY")  ;NEUE NUMMER
	
	FOR NEU=6,9,72,101,120 SET $PIECE(YFELD,Y,NEU) = ""
	
	NEW YFORM,YVOR,YOK
	SET YKEY     = YKEY(1)_","_YKEY(2)
	SET YFELD(2) = ""
	SET YFELD(1) = YFELD
	SET XKEY     = "^INDRPSUPPLY("_""""_YM_""""_","_""""_YKEY(1)_""""_","_""""_YKEY(2)_""""_",1)"
	IF $DATA(@XKEY) SET YFELD(1) = @XKEY SET YFELD(2) = YFELD(1)
	SET $PIECE(YFELD(1),Y,9) = "S"
	IF ($PIECE(YFELD(1),Y,6)="")  || ($get(VORG(101))'="") SET $PIECE(YFELD(1),Y,6)  = $get(VORG(3),+$horolog)  ; Date
	IF ($PIECE(YFELD(1),Y,17)="") || ($get(VORG(101))'="") SET $PIECE(YFELD(1),Y,17) = +$PIECE($HOROLOG,",",2)  ; Time
 
	set $$$INDRPSUPPLYDisabled(YFELD(1))         = $case(+$$$INDRPDEMANDBlocked(YFELD),0:"",:1)
	set $$$INDRPSUPPLYReadyForDelivery(YFELD(1)) = $case(+$$$INDRPDEMANDReadyForDelivery(YFELD),0:"",:1)
	set $$$INDRPSUPPLYDeletedOrder(YFELD(1))     = $case(+$$$INDRPDEMANDDeleteOrder(YFELD),0:"",:1)
	if ($get(VORG(101)) = "") && ($$$INDRPDEMANDTransfer(YFELD) = "") {
		set $$$INDRPSUPPLYClosed(YFELD(1))       = $case(+$$$INDRPDEMANDDemandClosed(YFELD),0:"",:1)
	} else {
		set $$$INDRPSUPPLYClosed(YFELD(1)) = ""
	}
 
	FOR NEU=3,4,5,8,12,13,15,16,17,22,26,40,42,46,50,61,64,66,67,71,100 IF $PIECE(YFELD,Y,NEU)'=""  SET $PIECE(YFELD(1),Y,NEU) = $PIECE(YFELD,Y,NEU)  ;DATEN ÜBERNEHMEN
	IF YFELD(1)'=YFELD(2) SET YOK = $$$Save("INDRPSUPPLY",YKEY,YFELD(1),$$$YES)
	set VORG(204) = $get(YOK)
	set VORG(205) = YKEY
	QUIT
	
	/*
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; TODO : Proposed Replacement code - need to consider later changes
	; 
	; History :
	; 08-Jun-2005	GRF		Clarify; ! => ||; only single calls to $$^WWWUMLAU;
	; 						Don't need to check if $data(^INDRPSUPPLYs) before $order
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	NEW BETRIEBN,BETRIEBV,DEMAND,NEU,objINVORG,SUPPLY,umKEY,XKEY,YFELD,YI,YKEY
	
	set objINVORG = $GET(^INVORG(YM,YM,1))
	IF $$$INVORGDRPMRPDataExchange(objINVORG)'=2 QUIT  ;KEINE DIREKTE DATENÜBERTRAGUNG ;no data transmission 
	QUIT:$GET(KEY)=""
	QUIT:$GET(FELD)=""
	
	SET YFELD	 = FELD
	SET KEY		 = $TRANSLATE(KEY,"""")
	SET KEY(1)   = $PIECE(KEY,",",1)   QUIT:KEY(1)=""  				;VON BETRIEB ;location 
	SET KEY(2)   = $PIECE(KEY,",",2)   QUIT:KEY(2)=""  				;VON DEMANDNUMMER
	set umKEY(1) = $$^WWWUMLAU(KEY(1),1)
	set umKEY(2) = $$^WWWUMLAU(KEY(2),1)
	
	;LÖSCHEN DATENSATZ ;Delete data record 
	IF YFELD="KILL" {
		SET YKEY(1)=""
		SET YKEY(2)=""
		IF (umKEY(1)'="")&&(umKEY(2)'="") {
			SET YKEY(1)=$ORDER(^INDRPSUPPLYs(YM,6,umKEY(1),umKEY(2),YKEY(1)))
			IF YKEY(1)'="" SET YKEY(2)=$ORDER(^INDRPSUPPLYs(YM,6,umKEY(1),umKEY(2),YKEY(1),""))
		}
		IF (YKEY(1)'="")&&(YKEY(2)'="") {
			SET KEY="^INDRPSUPPLY("_""""_YM_""""_","_""""_YKEY(1)_""""_","_""""_YKEY(2)_""""_",1)"
			SET YOK=$$^WWWSETK(KEY)
			DO ^WWWSKILL(KEY,,1)
		}
	} else {
		IF ($PIECE(YFELD,Y,4)'="")&&($$$INVORGDRPOnlyForNetChange(objINVORG)=$$$YES) {
			SET ^INDRPNETCHANGE(YM,$PIECE(YFELD,Y,4),1)=""  ;FIS;29.04.05;SR12200
		}
		if $piece(YFELD,Y,70)=3 {
			; Only Transfer Orders     ;NUR TRANSFERAUFTRÄGE
			SET YKEY(1)=$PIECE(FELD,Y,1)   ;AN BETRIEB ;upon location 
			SET YKEY(2)=$PIECE(YFELD,Y,2)  ;SUPPLYNUMMER
			SET $PIECE(YFELD,Y,1)=KEY(1)   ;ALTER SCHLUESSEL  ;VON BETRIEB ;governor location 
			SET $PIECE(YFELD,Y,2)=KEY(2)   ;ALTER SCHLUESSEL  ;DEMANDNUMMER
			
			if (YKEY(1)'="")&&(YKEY(1)'=KEY(1)) {
				if (YKEY(2)="")&&(YKEY(1)'="")&&(umKEY(1)'="")&&(umKEY(2)'="") {
					SET YKEY(2)=$ORDER(^INDRPSUPPLYs(YM,6,umKEY(1),umKEY(2),YKEY(1),""))
				}
				
				IF YKEY(2)="" SET YKEY(2)=$$^WWWNEXT("INDRPSUPPLY")  ;NEUE NUMMER
			;	FOR NEU=6,9,72,101 SET $PIECE(YFELD,Y,NEU)=""
				FOR NEU=6,9,72,101,120 SET $PIECE(YFELD,Y,NEU)=""  ;FIS;25.04.05;SR12200
				
				set YOK=$$Save(.YKEY,.YFELD)
			}
		}
	}
	quit
	*/
	
Save(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	;  Isolate variables for WWWSPEI call
	; 
	; ByRef : YKEY, YFELD
	; 
	; Returns : YOK
	; 
	; History :
	; 08-Jun-2005	GRF		Extracted from single routine
	;-------------------------------------------------------------------------------
	new NEU,XKEY,YFORM,YOK,YVOR,blnValue	
	
	SET YKEY     = YKEY(1)_","_YKEY(2)
	SET YFELD(2) = ""
	SET YFELD(1) = YFELD
	SET XKEY     = "^INDRPSUPPLY("_""""_YM_""""_","_""""_YKEY(1)_""""_","_""""_YKEY(2)_""""_",1)"
	IF $DATA(@XKEY) SET YFELD(1) = @XKEY SET YFELD(2) = YFELD(1)
	SET $PIECE(YFELD(1),Y,9) = "S"
	IF $PIECE(YFELD(1),Y,6)=""  SET $PIECE(YFELD(1),Y,6)  = +$HOROLOG
	IF $PIECE(YFELD(1),Y,17)="" SET $PIECE(YFELD(1),Y,17) = +$PIECE($HOROLOG,",",2)
	;IF $PIECE(YFELD(1),Y,7)="" SET $PIECE(YFELD(1),Y,7)  = +$HOROLOG  ;DATUM ;Date;FIS;27.04.05:SR12200
	
	; Boolean Values - store $$$NO as null
	FOR NEU=19,39,98,99 {
		set blnValue = +$PIECE(YFELD,Y,NEU)
		set $piece(YFELD(1),Y,NEU) = $select(blnValue:blnValue,1:"")
	}
	FOR NEU=3,4,5,8,12,13,15,16,17,22,26,40,42,46,50,61,64,66,67,71,100 {
		IF $PIECE(YFELD,Y,NEU)'="" SET $PIECE(YFELD(1),Y,NEU) = $PIECE(YFELD,Y,NEU)  ;DATEN ÜBERNEHMEN ;assume 
	}
	IF YFELD(1)'=YFELD(2) SET YOK = $$^WWWSPEI("INDRPSUPPLY",YKEY,YFELD(1),$$$YES)
	quit YOK
	
]]></Routine>
</Export>