<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEAUFVORS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEAUFVORS(YKEY)   ; DEPRECATED with removal of INWEAUF*
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SPEICHERN VORERFASSUNG ALS WARENEINGANG
	;
    ; Called By: Form INWEAUFVOR Button 1 : "Save Goods Receipt"
    ;                            Button 2 : "Save All Goods Receipts"
    ;            OldReverseTxn^INRECReverse  [DEPRECATED]
    ; 
	; Inputs : 
	;	YKEY
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 14-Dec-2006	GRF		Doco; macros
	; 03-Nov-2005	JW		SR13222: Added parameters to SAVE
	; 17.05.2004	FIS		Created
	;-------------------------------------------------------------------------------
	NEW MESSAGE
	
	SET MESSAGE=32292  ;BITTE PRÜFEN ;Check Please!
	IF $GET(YKEY)'="" DO
	. IF YKEY'=0 DO SAVE(YKEY) QUIT
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INWEAUFVOR(YM,AUF)) QUIT:AUF=""  DO
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INWEAUFVOR(YM,AUF,POS)) QUIT:POS=""  DO
	. . . SET BET=""
	. . . FOR  SET BET=$ORDER(^INWEAUFVOR(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . . SET LAP=""
	. . . . FOR  SET LAP=$ORDER(^INWEAUFVOR(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . SET WED=""
	. . . . . FOR  SET WED=$ORDER(^INWEAUFVOR(YM,AUF,POS,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . . . SET YKEY=AUF_","_POS_","_BET_","_LAP_","_WED
	. . . . . . DO SAVE(YKEY)
	
	NEW YKEY,YI
	SET YI=YAKTION_"EP=WWWFORM&YFORM=INWEAUFVOR&YKEY="
	DO VAR^WWWCGI
	DO ^WWWINFO($$^WWWTEXT(MESSAGE),,YI,2)  ;BITTE PRÜFEN / ÜBERTRAGUNG ABGESCHLOSSEN ;please check
	
	QUIT
	
SAVE(YKEY,blnREVERSE=$$$NO,blnV2Processing=$$$NO)   ; DEPRECATED with removal of INWEAUF*
    ;-------------------------------------------------------------------------------
    ; SPEICHERN DER INWEAUF ;Save the INWEAUF record
    ; 
    ; Inputs :
    ; 	YKEY
    ;	blnREVERSE    : passed through to INWEAUF & used there; don't add "p" prefix
    ; 
    ; Called By: ReverseTxn^INRECReverse()
    ; 
    ; Returns :
    ;   MESSAGE     Implicit
    ; 
    ; History:
    ; 27-Aug-2008	HQN		SR15829: Added additional parameter to break out Order
    ; 							processing to refactored code
    ; 03-Nov-2005	JW		SR13222: Added parameters, return status
    ;-------------------------------------------------------------------------------
	new strStatus
	
	QUIT:$PIECE(YKEY,",",1)=""       ; 1 : Order Number
	QUIT:$PIECE(YKEY,",",2)=""       ; 2 : Order Line Item
	QUIT:$PIECE(YKEY,",",3)=""       ; 3 : Site Location
	QUIT:$PIECE(YKEY,",",4)=""       ; 4 : Storage
	QUIT:$PIECE(YKEY,",",5)=""       ; 5 : Incoming Goods Date
	
	IF $DATA(^INWEAUF(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),$PIECE(YKEY,",",3),$PIECE(YKEY,",",4),$PIECE(YKEY,",",5))) QUIT  ;BEREITS ANGELEGT ;yet 
	SET YFELD=$GET(^INWEAUFVOR(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),$PIECE(YKEY,",",3),$PIECE(YKEY,",",4),$PIECE(YKEY,",",5),1))
	QUIT:YFELD=""
	
	; D108 not defined in INWEAUF
	SET $$$INWEAUFVORItem(YFELD)=""  ; ARTIKEL NUR IN VORERFASSUNG ;item only within 
	DO
	. NEW YFORM,YVOR,YOK
	. SET YOK=$$^WWWSPEI("INWEAUF",YKEY,YFELD,1)
	. set strStatus = YOK
	. IF YOK'=$$$SaveOkay QUIT
	. if 'blnV2Processing DO ^INWEAUF        ;BUCHEN WARENEINGANG (UND LÖSCHEN VORERFASSUNG) ;receiving Delete ; SR15829 27-Aug-2008 HQN
	. if blnV2Processing && blnREVERSE DO Reverse^INWEAUFV2(YKEY,YFELD) ; SR15829 27-Aug-2008 HQN
	. SET MESSAGE=30007  ;ÜBERTRAGUNG ABGESCHLOSSEN ; Transfer Finished!
	
	QUIT strStatus
	
CHECK(YKEY)   ; DEPRECATED with removal of INWEAUF*
	;-------------------------------------------------------------------------------
	; Execute To Check For Disabled
	; 
	; Called By: Form INWEAUFVOR Button 1 : "Save Goods Receipt"
	;-------------------------------------------------------------------------------
	QUIT:$PIECE(YKEY,",",1)=""
	QUIT:$PIECE(YKEY,",",2)=""
	QUIT:$PIECE(YKEY,",",3)=""
	QUIT:$PIECE(YKEY,",",4)=""
	QUIT:$PIECE(YKEY,",",5)=""
	
	IF $DATA(^INWEAUF(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),$PIECE(YKEY,",",3),$PIECE(YKEY,",",4),$PIECE(YKEY,",",5))) DO  ;BEREITS ANGELEGT ;yet 
	. SET YQ="1"_$$^WWWTEXT(33871)  ; "This Receipt Has Already Been Entered (INWEAUF). Cancel!" 
	
	QUIT
]]></Routine>
</Export>