<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARDebug" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARDebug
	;-----------------------------------------------------------------------------------
	; Descrição:
	;	Rotina para depurar os resultados de funções
	;-----------------------------------------------------------------------------------
	#include VARConst
	#define ISDEBUG (pDebug = 1)
	quit
	
	
	;-----------------------------------------------------------------------------------
	; Descrição:
	;	Copiada da função GetCMM^VARReposicao
	;
	; Parâmetros:
	;	pItem - Item da INART
	;	pLocal	- Local da WWW0121
	;	pDebug	- Exibe os valores da execução da função GetCMM
	; 	pDebugConsumo - Exibe os valores da execução da função GetCMMYearMonth
	; Uso:
	;	Ex: do GetCMM^VARDebug(20003,"",1,1)
	;-----------------------------------------------------------------------------------
GetCMM(pidItem,pidLocal, pDebug=0,pDebugConsumo=0)
	$$$VAR
	set calculatedCMM=0
	set months=0
	set currentmonth=$extract($ZD($H,1),1,2)
	set currentyear=$extract($ZD($H,1),7,10)
	set months = $$GetMonthsForAverage^VARReposicao(pidItem,pidLocal="")
	
	if $$$ISDEBUG write !,"Meses para media da função GetMonthsForAverage " _ months
	
	if months="" set months=12
	
	if $$$ISDEBUG write !,"Meses para media considerado para o calculo " _ months
	
	for x=1:1:months {
		if $$$ISDEBUG write !
		set GetCMMYearMonth=0
		set currentmonth=currentmonth-1
		if currentmonth<1 {
			set currentyear=currentyear-1
			set currentmonth=12
		}
		if $$$ISDEBUG write !,"Mes " _ currentmonth _ " ano " _ currentyear
		set GetCMMYearMonth=$$GetCMMYearMonth(pidItem,pidLocal,currentyear,currentmonth,pDebugConsumo)
		if $$$ISDEBUG write " Consumo para o mes " _ GetCMMYearMonth
		if GetCMMYearMonth=0 set months=months-1 //Ignore months with no movement
		set calculatedCMM=calculatedCMM+GetCMMYearMonth
		if $$$ISDEBUG write !,"Acumulado - Mes: " _ months _ " Consumo " _ calculatedCMM
	}
	if $$$ISDEBUG write !
	if $$$ISDEBUG write !,"Final"
	if $$$ISDEBUG write !," Total Consumo " _ calculatedCMM _ " Total Mes " _ months
	
	if ((calculatedCMM>0) && (calculatedCMM'="") && (months>0)) set calculatedCMM=calculatedCMM\months
	if $$$ISDEBUG write " Media " _ calculatedCMM
	
	quit
	
	;-----------------------------------------------------------------------------------
	; Descrição:
	;	Copiada da função GetCMMYearMonth^VARReposicao
	;
	; Parâmetros:
	;	pItem 	- Item da INART
	;	pLocal	- Local da WWW0121
	; 	currentyear - Ano a ser pesquisado
	;	currentmonth - Mês a ser pesquisado
	;	pDebug	- Exibe os valores da execução da função GetCMMYearMonth
	; Uso:
	;	Ex: W $$GetCMMYearMonth^VARReposicao(20003,100001,2016,9)
	;-----------------------------------------------------------------------------------
GetCMMYearMonth(pidItem,pidLocal="",currentyear,currentmonth,pDebug=0)
	$$$VAR
	if pidItem="" q 0
	set qtyCMMLocation = 0
	if pidLocal'="" {

		if $order(^VARCMMLinha(YM,pidItem,currentyear,currentmonth,pidLocal,""))'="" {
				
			$$$Order6(^VARCMMLinha,YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction)
					
				set qty = 0
				set qty = $piece($get(^VARCMMLinha(YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction,1)),Y,1)
				set qtyCMMLocation = qtyCMMLocation + qty
				if $$$ISDEBUG write !,"Local: " _ pidLocal _ " Doc: " _ idTransaction _ " Qtde: " _ qty
			$$$End
		}
	}else {
	
		$$$Order5(^VARCMMLinha,YM,pidItem,currentyear,currentmonth,pidLocal)
				
			$$$Order6(^VARCMMLinha,YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction)
					
				set qty = 0
				set qty = $piece($get(^VARCMMLinha(YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction,1)),Y,1)
				set qtyCMMLocation = qtyCMMLocation + qty
				if $$$ISDEBUG write !,"Local: " _ pidLocal _ " Doc: " _ idTransaction _ " Qtde: " _ qty
			$$$End
		$$$End
	}
	if qtyCMMLocation<0 set qtyCMMLocation=qtyCMMLocation*(-1)
	if $$$ISDEBUG write !,"Total: " _ qtyCMMLocation
	q qtyCMMLocation
]]></Routine>
</Export>