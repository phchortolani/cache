<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLFCAdjustPost" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLFCAdjustPost
 
#include COMSYS
#include FINConst
 
CreateBatch(pdteDate,pidHistory,pstrClass,&pidBatch,&pidBlock)
	;-------------------------------------------------------------------------------
	; Create and Post a Batch to generate a FINGLTran for FINGLBankReconLine entries
	;
	; Params:
	; pdteDate  : The date to post the record to
	; pidHistory: The id of the History Object
	; pstrClass : The class name
	; pidBatch  : The id the FININTBatch
	; pidBlock  : The id the FININTBatchBlock
	;
	; ByRefs:
	; pidBatch
	; pidBlock
	;
	; Returns:
	; strStatus: $$$OK no errors, anything else are errors
	;
	; History:
	; 13-Jun-2006	RPW		SR14642: Created
	;-------------------------------------------------------------------------------
 
	new strStatus,strDetail,idBatch,idBlock
	new objJournal,blnInterCompany
		
	set strDetail     = "Fin01041"_$$$COMMA_pidHistory_$$$COMMA_$$^WWWDATE(pdteDate)  ;Foreign Currency Variation History %1 for Date %2
	set strStatus     = $$Allocate^FININTBatch(.pidBatch,.pidBlock,"GLFCV",,pdteDate,,YBED,,pstrClass,pidHistory,strDetail) ; SR13087
	
	quit strStatus
	
CreateLine(pidAccount,pcurDebit,pcurCredit,pstrRef,pidBatch,pidBlock,pdteDate)
	;-------------------------------------------------------------------------------
	; Add the Adjustment lines to the Batch
	;
	; Params:
	; pidAccount: The id of the GL Account
	; pcurDebit : The amount to debit
	; pcurCredit: The amount to credit
	; pstrRef   : The reference for the Adjustment
	; pidBatch  : The id of the FININTBatch
	; pidBlock  : The id of the FININTBatchBlock
	; pdteDate  : The date to post to.
	;
	; ByRefs:
	;
	; Returns:
	; strStatus: $$$OK no errors, anything else are errors
	;
	; History:
	; 13-Jun-2006	RPW		SR14642: Created
	;-------------------------------------------------------------------------------
 
	new curAmount,objParameters,strStatus
	
	set curAmount=$select(+pcurCredit:$$$FCSign(pcurCredit),1:pcurDebit)
	set $piece(objParameters,",",8)=pstrRef
	
	set strStatus=$$AddLine^FININTBatch(pidBatch,pidBlock,pidAccount,curAmount,"FC",objParameters,"",pstrRef)
	
	quit strStatus
 
Post(pidBatch,pidBlock,&pidTran)
 
	;-------------------------------------------------------------------------------
	; Post the Batch and Block
	;
	; Params:
	; pidBatch: The id of the FININTBatch
	; pidBlock: The id of the FININTBatchBlock
	; pidTran : The id of the FINGLTran
	;
	; ByRefs:
	; pidTran
	;
	; Returns:
	; strStatus: $$$OK no errors, anything else are errors
	;
	; History:
	; 13-Jun-2006	RPW		SR14642: Created
	;-------------------------------------------------------------------------------
 
	new strStatus
	
	set strStatus = $$PostBlock^FININTBatch(pidBatch,pidBlock)
	if $$$ISOK(strStatus) {
		set strStatus = $$PostBatch^FININTBatch(pidBatch,"FC",.pidTran)
	}
	
	quit strStatus
]]></Routine>
</Export>