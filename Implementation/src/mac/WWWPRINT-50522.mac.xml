<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWPRINT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWPRINT
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		DRUCKEN DATEI-INHALTE AUS FORMULAR
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 13.02.2002	FIS
	;-------------------------------------------------------------------------------
	new YDATEI,YMAXKEY,YI,YQ,YDATEI1,LFN,YFELD,YTYP,YINHALT,YNAME,YDDSATZ,MAXWIDTH
	
	set YQ=0
	do  ;PRÜFEN ;sift 
	. set YKEY=$get(YKEY)
	. if YKEY="" set YQ=1 quit
	. set YFORM=$get(YFORM)
	. if YFORM="" set YQ=1 quit
	. ;
	. set YDATEI=$piece($get(^WWW120(0,YFORM,1)),Y,11)  ;BASISKLASSE
	. if YDATEI="" set YQ=1 quit
	. ;
	. set YMAXKEY=$order(^WWW002(0,YDATEI,""),-1)  ;ANZAHL KEYS ;Number 
	. if +YMAXKEY=0 set YMAXKEY=1
	. for YI(1)=1:1:YMAXKEY set:$piece(YKEY,",",YI(1))="" YQ=1
	. quit:YQ=1
	. ;
	. set YDATEI(1)="^"_YDATEI_"("_""""_0_""""  ;ZUSAMMENBAU DATENSATZ ;data record 
	. for YI(2)=1:1:YMAXKEY  set YDATEI(1)=YDATEI(1)_","_""""_$piece(YKEY,",",YI(2))_""""
	. set YDATEI(1)=YDATEI(1)_",1)"
	. if '$data(@YDATEI(1)) set YQ=1 quit
	. ;
	. set YFELD=@YDATEI(1)  ;GESPEICHERTE DATEN
	. ;IF $GET(^WWWDATEN(YM,+$H,YUSER,YFORM,"D",1))'="" SET YFELD=^(1)  ;GGF. ÄNDERUNGEN
	. if YFELD="" set YQ=1
	
	if YQ=1 do ^WWWINFO($$^WWWTEXT(32310)) quit  ;KEINE AUSWAHL GETROFFEN ;no Selection 
	if $extract(YFORM,1,5)'="INAUF" if YFORM'="INWERBKONVOR" do ^WWWBACK   ;KEINE BUTTON FÜR "Auftrags Information drucken"  FAN;05.05.03 
	set YDDSATZ=1
	do START100^WWWTAB
	;ÜBERSCHRIFT ;superscription 
	do NL^WWWTAB
	write "<TH VALIGN=TOP NOWRAP ALIGN=LEFT"
	write " BGCOLOR="_YDARKGRAY
	write " COLSPAN=2>"
	write "<FONT SIZE=2>"
	write YKOPF
	write "<BR>"
	for YI(3)=1:1:YMAXKEY do
	. new YNAME,YDATEI1,YTYP
	. set YNAME=$piece($get(^WWW002(0,YDATEI,YI(3),1)),Y,2)
	. if $piece($get(^WWW0021(0,YDATEI,YI(3),SPRACHE,1)),Y,1)'="" set YNAME=$piece(^(1),Y,1)  ;SPRACHENABHÄNGIG
	. if $piece($get(^WWW121D(0,YDATEI,YI(3),YM,1)),Y,6)'="" set YNAME=$piece($get(^(1)),Y,6)  ;AUS CUSTOMIZING ;out of 
	. if $piece($get(^WWW121D1(0,YDATEI,YI(3),YM,SPRACHE,1)),Y,6)'="" set YNAME=$piece($get(^(1)),Y,1)  ;AUS CUSTOMIZING ;out of 
	. set YDATEI1=$get(^WWW002(0,YDATEI,YI(3),1))  ;DATENFELD ;data item 
	. set YTYP=$piece(YDATEI1,Y,3)  ;FELDTYP
	. if YTYP="" set YTYP=6  ;TEXTFELD
	. if YTYP=0!(YTYP=5)!(YTYP=11)!(YTYP=13)!(YTYP=15)!(YTYP=16) quit  ;DIESE FELDFORMATE NICHT DRUCKEN ;this Not print 
	. set YINHALT=$piece(YKEY,",",YI(3))  ;INHALT IN KEY ;purport within KEY 
	. do FORMAT
	. write YNAME_": "_YINHALT
	. write "<BR>",YCR
	
	do EH^WWWTAB
	do EL^WWWTAB
	do
	. new SEITE,ZEILE,TAB,EXECVOR
	. set SEITE=""
	. for  set SEITE=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),SEITE)) quit:SEITE=""  do
	. . set ZEILE=""
	. . for  set ZEILE=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),SEITE,ZEILE)) quit:ZEILE=""  do
	. . . set TAB=""
	. . . for  set TAB=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),SEITE,ZEILE,TAB)) quit:TAB=""  do
	. . . . set FNR=""
	. . . . for  set FNR=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),SEITE,ZEILE,TAB,YFORM,FNR)) quit:FNR=""  do
	. . . . . set LFN=$piece($get(^WWW122(0,YFORM,FNR,1)),Y,1)
	. . . . . quit:+LFN=0
	. . . . . quit:$get(blnPutAway)&&(YFORM="INSHIPINFO")&&((LFN=17)||(LFN=18)) ;SR10951: Steve S - skip costs
	. . . . . quit:$get(blnPutAway)&&(YFORM="INSHIPINFO")&&(LFN'=1)&&(LFN'=2)&&(LFN'=3)&&(LFN'=21) ;SR12378
	. . . . . set YDATEI1=$get(^WWW003(0,YDATEI,LFN,1))  ;DATENFELD ;data item 
	. . . . . quit:YDATEI1=""
	. . . . . set YNAME=$piece(YDATEI1,Y,1)  ;FELDNAME
	. . . . . if $piece(YDATEI1,Y,2)'="" set YNAME=$piece(YDATEI1,Y,2)  ;FELDNAME IM FORMULAR ;form 
	. . . . . if $data(^WWW0031(0,YDATEI,LFN,SPRACHE,1)) set YNAME=$piece(^(1),Y,1)  ;FELDNAME IN SPRACHE ;within Language 
	. . . . . if $piece($get(^WWW0031(0,YDATEI,LFN,SPRACHE,1)),Y,2)'="" set YNAME=$piece(^(1),Y,2)  ;FELDNAME IN SPRACHE ;within Language 
	. . . . . if $piece($get(^WWW122D(0,YDATEI,LFN,YM,1)),Y,6)'="" set YNAME=$piece($get(^(1)),Y,6)  ;AUS CUSTOMIZING ;out of 
	. . . . . if $piece($get(^WWW122D1(0,YDATEI,LFN,YM,SPRACHE,1)),Y,1)'="" set YNAME=$piece($get(^(1)),Y,1)  ;AUS CUSTOMIZING ;out of 
	. . . . . if $extract(YNAME,1,5)="_FREE"!($extract(YNAME,1,6)="_BLANK") quit
	. . . . . set YTYP=$piece(YDATEI1,Y,3)  ;FELD ;field 
	. . . . . if YTYP="" set YTYP=6  ;TEXTFELD
	. . . . . if YTYP=0!(YTYP=5)!(YTYP=9)!(YTYP=11)!(YTYP=13)!(YTYP=15)!(YTYP=16) quit  ;DIESE FELDFORMATE NICHT DRUCKEN ;this Not print 
	. . . . . ;
	. . . . . set YINHALT=$piece(YFELD,Y,LFN)  ;INHALT IN DATENFELD ;purport within data item 
	. . . . . set EXECVOR=$piece($get(^WWW122(0,YFORM,FNR,1)),Y,92)  ;EXECUTE VOR DATENFELD  ;FIS;26533;12.10.04
	. . . . . if EXECVOR'="" if $find(EXECVOR,"SET YINHALT=") xecute EXECVOR  ;ANDERER INHALT  ;FIS;26533;12.10.04
	. . . . . ;SET MAXWIDTH=$PIECE(YDATEI1,Y,4)  ;MAX. DRUCKBREITE
	. . . . . ;IF MAXWIDTH'="" IF $FIND(MAXWIDTH,"-") SET MAXWIDTH=$PIECE(MAXWIDTH,"-",2)
	. . . . . ;IF +MAXWIDTH=0 SET MAXWIDTH=50 ;table-mat 
	. . . . . do FORMAT
	. . . . . do DRUCK
	
	do STOP^WWWTAB
	quit
	
FORMAT ;TEXTFORMATIERUNG
	new YQ,YI
	set YQ=0
	do  ;IF YINHALT'="" DO  ;UMWANDLUNG PARAMETER-NUMMER IN PARAMETER-NAME AUS RELATIONSDATEI
	. new YDAT,YKE,YFE,YSAT,YSPR,YDATA
	. set YDAT=$piece(YDATEI1,Y,8)   ;RELATIONS-DATEI
	. quit:YDAT=""
	. set YKE=$piece(YDATEI1,Y,9)    ;RELATION-PRIMÄRSCHLÜSSEL
	. if YKE'="" if $extract(YKE)'="""" if '$find(YKE,",")  quit:'$data(@(YKE))  quit:$get(@(YKE))=""
	. if YDAT="WWW100" if YKE'="" if $find(YKE,""""_"JA/NEIN"_"""") if $translate(YINHALT," ")="" set YINHALT=0
	. quit:YINHALT=""
	. set YFE=+$piece(YDATEI1,Y,10)   ;RELATIONS DATENFELD NR. ;data item 
	. if +YFE=0 set YFE=1           ;WENN KEINE AUSGEWÄHLT DANN IST 1 ;when no select 
	. set YSPR=$piece($get(^WWW001(0,YDAT,1)),Y,7)  ;SPRACHENDATEI FÜR RELATION ;to 
	. if YSPR'="" set YDAT=YSPR,YFE=1
	. ;
	. if '$find(YINHALT,";") do  quit
	. . set YSAT="^"_YDAT_"("_$$^WWWYM(YDAT,1)  ;              YSAT = ^DATEINAME(MANDANT, ...
	. . set YDATA=$get(^WWW001(0,YDAT,1))  ;KLASSENDEFINITION
	. . if YKE'="" if $extract(YKE)'="," set YSAT=YSAT_YKE_","  ;                                     ... RELATIONS-PRIMÄRSCHLÜSSEL, ...
	. . set YSAT=YSAT_""""_$translate(YINHALT,"""")_""""   ;                    ... FELDINHALT ...
	. . if YSPR'="" set YSAT=YSAT_",SPRACHE"  ;                                 ... ,SPRACHE ...
	. . if $piece(YDATA,Y,8)'=4 set YSAT=YSAT_",1)"  ;                          ... ,1)
	. . ;ALTERNATIVE DATENSPEICHERUNG PRÜFUNG  ;option quiz 
	. . if $piece(YDATA,Y,8)=4 set YSAT=YSAT_")"  ;                        ODER ... )
	. . set YSAT(1)=$piece($$^WWWSETL(YSAT),Y,YFE)
	. . ;IF $EXTRACT(YKE)="""" IF YSAT(1)'="" SET YINHALT=$EXTRACT($TRANSLATE(YSAT(1),"|"," "),1,30) SET YQ=1 QUIT
	. . if YSAT(1)'="" if YDAT="WWW100" if YKE'="" if $find(YKE,""""_"JA/NEIN"_"""") set YINHALT=$extract($translate(YSAT(1),"|"," "),1,30) set YQ=1 quit
	. . if YSAT(1)'="" set YINHALT="("_YINHALT_") "_$extract($translate(YSAT(1),"|"," "),1,30) set YQ=1
	. ;
	. if $find(YINHALT,";") do  ;MULTISELECT
	. . set YSAT(1)=""
	. . for YI=1:1  quit:$piece(YINHALT,";",YI,999)=""  do
	. . . quit:$translate($piece(YINHALT,";",YI)," ")=""
	. . . set YSAT="^"_YDAT_"("_$$^WWWYM(YDAT,1)  ;              YSAT = ^DATEINAME(MANDANT, ...
	. . . set YDATA=$get(^WWW001(0,YDAT,1))  ;KLASSENDEFINITION
	. . . if YKE'="" if $extract(YKE)'="," set YSAT=YSAT_YKE_","  ;                                     ... RELATIONS-PRIMÄRSCHLÜSSEL, ...
	. . . set YSAT=YSAT_""""_$translate($piece(YINHALT,";",YI),"""")_""""   ;     ... FELDINHALT ...
	. . . if YSPR'="" set YSAT=YSAT_",SPRACHE"  ;                                 ... ,SPRACHE ...
	. . . if $piece(YDATA,Y,8)'=4 set YSAT=YSAT_",1)"  ;                          ... ,1)
	. . . ;ALTERNATIVE DATENSPEICHERUNG PRÜFUNG  ;option quiz 
	. . . if $piece(YDATA,Y,8)=4 set YSAT=YSAT_")"  ;                        ODER ... )
	. . . set YSAT(1)=YSAT(1)_$piece($$^WWWSETL(YSAT),Y,YFE)_"|"
	. . ;
	. . if YSAT(1)'="" set YINHALT=YSAT(1) set YQ=1
	
	if YQ=0 set YINHALT=$$GetLiteral^WWWTR(YTYP,YINHALT)  ;FORMAT JE FELDTYP ;format once 
	quit
	
DRUCK ;TEXT DRUCKEN ;Text print 
	do NL^WWWTAB
	do NFB2^WWWTAB
	write "<FONT SIZE=2>"
	write YNAME
	write YCR
	do EF^WWWTAB
	do NFW^WWWTAB
	write "<FONT SIZE=2>"
	if YINHALT'="" do
	. ;BILDER ;imagery 
	. if $find(YINHALT,".gif")!$find(YINHALT,".GIF") do  quit
	. . write "<img src="_YGIF_YINHALT_" HEIGHT=80 border=0>"  ;BILD ;portrait 
	. ;
	. ;MEMO-FELD
	. if $find(YINHALT,"|") do  quit
	. . new A,TEXT1
	. . for YI(4)=1:1  quit:$piece(YINHALT,"|",YI(4),999)=""  set TEXT1=$piece(YINHALT,"|",YI(4))  do  ;SCHREIBEN FELDINHALT (WENN PARAMETER DANN PARAMETERNAME) ;write parameter 
	. . . write TEXT1
	. . . if $piece(YINHALT,"|",YI(4)+1,999)'="" write "<BR>",YCR
	. . . ;SET A=""
	. . . ;FOR YI(5)=1:1 QUIT:$PIECE(TEXT1," ",YI(5),999)=""  DO  SET A=A_$PIECE(TEXT1," ",YI(5))_" "
	. . . ;. IF $LENGTH(A_$PIECE(TEXT1," ",YI(5)))>MAXWIDTH WRITE A,"<BR>",YCR SET A="" QUIT
	. . . ;IF $TRANSLATE(A," ")'="" WRITE A,"<BR>",YCR
	. . . ;IF $TRANSLATE(A," ")="" IF YI(5)=1 WRITE A,"<BR>",YCR
	. ;
	. ;SONSTIGES
	. write YINHALT
	
	write "&nbsp;"
	write YCR
	do EF^WWWTAB
	do EL^WWWTAB
	write YCR
	quit
	
]]></Routine>
</Export>