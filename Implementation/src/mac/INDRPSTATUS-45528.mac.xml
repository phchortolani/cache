<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPSTATUS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPSTATUS(YTYP,YBET,YID,YTERM,YBES)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERMITTELN STATUS DEMAND/SUPPLY
	;
	; Inputs : 
	;	YTYP= AUFTRAGSART   0=KD-AUFTRAG ..
	;                       ..  01=WH=BESTELLUNG
	;                       ..  02=WH=LAGERBESTAND
	;                       ..  03=WH=FERTIGUNG
	;                       ..  04=WH=DIENSTLEISTUNG
	;                       ..  05=WH=LAGERBESTELLUNG
	;                       1=EIGENAUFTRAG
	;                       2=LF-BESTELLUNG
	;                       D=MANUELLER DEMAND
	;                       S=MANUELLER SUPPLY
	;	YBET= BETRIEB ;location 
	;	YID=  AUFTRAG,POS ODER DEMAND/SUPPLY-ID
	;	YTERM=BEDARFSTERMIN
	;	YBES= VERFÜGBARER BESTAND
	;
	; ByRef :
	;
	; Returns :
	;	RETURNVALUE	=	STATUS_#_OK
	;	
	;	OK: 1=KEINE ÜBERFÄLLIGKEITEN IN DEN LAUFZEITEN  ;within 
	;	    0=TERMIN KANN NICHT EINGEHALTEN WERDEN ;Not will 
	;
	;	STATUS: 90=GESPERRT ;Closed
	;	        91=GELÖSCHT ;Deleted
	;	        92=STORNIERT ;Cancelled
	;	        93=ERLEDIGT
	;	
	;	        10=AUFTRAGS-/PRÜFUNG, ANLAGE UND FREIGABE
	;	        20=BESTELLVORGANG/FERTIGUNGSVORBEREITUNG
	;	        30=LIEFERANTEN-AUFTRAGSBESTRÄTIGUNG
	;	        40=WARENEINGANG/FERTIGUNGSENDE ;Incoming goods/manufacturing
	;	        50=VERSANDPLANUNG ;dispatch planning
	;	        60=BEREITSTELLUNG ;supply
	;	        70=AUSLIEFERUNG ;distrubution
	;
	; History :
	; 12-Oct-2007	GRF		Doco; !=||; naked reference
	; 13.11.2001	FIS
	;-------------------------------------------------------------------------------
	NEW STATUS,OK,AUF,AUF1,POS,POS1,ART1,YI,RESTDAUER,ART
	
	SET STATUS=""
	SET OK=0
	
	SET YTYP=$GET(YTYP)         QUIT:YTYP="" STATUS
	SET YBET=$GET(YBET)         QUIT:YBET="" STATUS
	SET YTERM=+$GET(YTERM)
	IF YTERM="" SET YTERM=+$HOROLOG
	
	SET YID=$GET(YID)
	IF (YTYP'="D") && (YTYP'="S") {
		SET AUF=$PIECE(YID,"-",1)
		SET POS=$PIECE(YID,"-",2)
	}
	
	IF YTYP'="D" IF YTYP'="S" IF (AUF="") || (POS="") QUIT STATUS
	IF YTYP="04" QUIT STATUS  ;LEISTUNG ;performance 
	
	;IF YTYP="05" SET YTYP="02"  ;LAGERBESTELLUNG
	
	;STATUSPRÜFUNG
	;=============
	
	;AUFTRÄGE
	;--------
	IF (YTYP'="D") && (YTYP'="S") DO
	. IF '$DATA(^INAUFP(YM,AUF,POS))           SET STATUS=91 QUIT  ;GELÖSCHT
	. SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. IF $PIECE(POS1,Y,4)=""                   SET STATUS=91 QUIT  ;KEIN ARTIKEL=GELÖSCHT ;no 
	. SET ART=$PIECE(POS1,Y,4)
	. IF $PIECE(POS1,Y,9)=1                    SET STATUS=92 QUIT  ;STORNIERT
	. IF $PIECE(POS1,Y,60)=1                   SET STATUS=93 QUIT  ;ABGESCHLOSSEN
	. IF $PIECE(POS1,Y,205)=1                  SET STATUS=90 QUIT  ;NOCH NICHT FREIGEGEBEN ;yet Not 
	. IF $PIECE($GET(^INAUF(YM,AUF,1)),Y,38)=1 SET STATUS=90 QUIT  ;NOCH NICHT FREIGEGEBEN ;yet Not 
	. ;
	. ;KUNDENAUFTRAG ;customer´s order 
	. IF $EXTRACT(YTYP)=0 DO
	. . IF $PIECE(POS1,Y,92)'="" SET STATUS=70 QUIT  ;LIEFERSCHEIN GEDRUCKT ;packing slip 
	. . IF $PIECE(POS1,Y,87)'="" SET STATUS=60 QUIT  ;BEREITSTELLUNGSSCHEIN GEDRUCKT
	. . IF YTYP="0" DO  ;KEINE WH BESTIMMT ;no sure 
	. . . ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. . . ;;IF YBES'<$PIECE(POS1,Y,5) DO  QUIT  ;BESTAND NICHT BERÜCKSICHTIGEN
	. . . ;. IF $PIECE(POS1,Y,76)'="" SET STATUS=50 QUIT  ;AUSLIEFERUNGSDATUM GEPLANT
	. . . ;. SET STATUS=40 QUIT  ;AN LAGER ;upon stock location 
	. . . ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	. . . IF $PIECE(POS1,Y,90)=1 SET STATUS=40 QUIT  ;AUSLIEFERUNGSBEREIT
	. . . SET STATUS=10
	. . ;
	. . IF YTYP="02" DO  ;LAGERBESTAND
	. . . IF YBES>$PIECE(POS1,Y,5) DO  QUIT
	. . . . IF $PIECE(POS1,Y,76)'="" SET STATUS=50 QUIT  ;AUSLIEFERUNGSDATUM GEPLANT
	. . . . SET STATUS=40 QUIT  ;AN LAGER ;upon stock location 
	. . . IF $PIECE(POS1,Y,90)=1 SET STATUS=40 QUIT  ;AUSLIEFERUNGSBEREIT
	. . . SET STATUS=10
	. . ;
	. . IF (YTYP="01")||(YTYP="05") DO  ;BESTELLUNG ;sales order 
	. . . IF $PIECE(POS1,Y,109)'<$PIECE(POS1,Y,5) DO  QUIT
	. . . . IF $PIECE(POS1,Y,76)'="" SET STATUS=50  ;AUSLIEFERUNGSDATUM GEPLANT
	. . . . SET STATUS=40 QUIT  ;WE ERFOLGT ;In 
	. . . ;
	. . . IF $PIECE(POS1,Y,90)=1         SET STATUS=40 QUIT  ;AUSLIEFERUNGSBEREIT
	. . . IF $DATA(^INAUFPA(YM,AUF,POS)) SET STATUS=30 QUIT  ;AB ERFASST ;Confirm. 
	. . . IF $PIECE(POS1,Y,83)'=""       SET STATUS=20 QUIT  ;BESTELLUNG GEDRUCKT ;sales order 
	. . . IF YTYP="05"                   SET STATUS=20 QUIT  ;SOURCED
	. . . SET STATUS=10
	. . ;
	. . IF YTYP="03" DO  ;FERTIGUNG
	. . . IF $PIECE(POS1,Y,90)=1 DO  QUIT
	. . . . IF $PIECE(POS1,Y,76)'="" SET STATUS=50 QUIT  ;AUSLIEFERUNGSDATUM GEPLANT
	. . . . SET STATUS=40 QUIT                           ;FERTIGUNG BEENDET
	. . . ;
	. . . IF $PIECE(POS1,Y,90)=1 SET STATUS=40 QUIT      ;AUSLIEFERUNGSBEREIT
	. . . IF $PIECE(POS1,Y,165)'="" DO  QUIT
	. . . . IF $DATA(^INPROSTEMP(YM,$PIECE(POS1,Y,165))) || $DATA(^INPROSTEMP1(YM,$PIECE(POS1,Y,165))) SET STATUS=30 QUIT  ;FERTIGUNG GESTARTET
	. . . . SET STATUS=20 QUIT  ;FERTIGUNGS VORBEREITET
	. . . ;
	. . . SET STATUS=10
	. ;
	. ;EIGENFERTIGUNG
	. IF YTYP="1" DO
	. . IF $PIECE(POS1,Y,90)=1 SET STATUS=93 QUIT  ;FERTIGUNG BEENDET=ERLEDIGT
	. . IF $PIECE(POS1,Y,165)'="" DO  QUIT
	. . . IF $DATA(^INPROSTEMP(YM,$PIECE(POS1,Y,165))) || $DATA(^INPROSTEMP1(YM,$PIECE(POS1,Y,165))) SET STATUS=30 QUIT  ;FERTIGUNG GESTARTET
	. . . SET STATUS=20 QUIT  ;FERTIGUNGS VORBEREITET
	. . ;
	. . SET STATUS=10
	. ;
	. ;LF-BESTELLUNGEN
	. IF YTYP="2" DO
	. . IF $PIECE(POS1,Y,109)'<$PIECE(POS1,Y,5) SET STATUS=93 QUIT  ;WE ERFOLGT=ERLEDIGT ;In 
	. . IF $DATA(^INAUFPA(YM,AUF,POS))          SET STATUS=30 QUIT  ;AB ERFASST ;Confirm. 
	. . IF $PIECE(POS1,Y,83)'=""                SET STATUS=20 QUIT  ;BESTELLUNG GEDRUCKT ;sales order 
	. . SET STATUS=10
	
	
	;MANUELLE
	;--------
	;DEMANDS
	IF YTYP="D" DO
	. IF '$DATA(^INDRPDEMAND(YM,YBET,YID)) SET STATUS=91 QUIT  ;GELÖSCHT
	. SET POS1=$GET(^INDRPDEMAND(YM,YBET,YID,1))
	. IF $PIECE(POS1,Y,4)="" SET STATUS=91 QUIT  ;KEIN ARTIKEL=GELÖSCHT ;no 
	. SET ART=$PIECE(POS1,Y,4)
	. IF $PIECE(POS1,Y,98)=1 SET STATUS=91 QUIT  ;STORNIERT
	. IF $PIECE(POS1,Y,99)=1 SET STATUS=93 QUIT  ;ABGESCHLOSSEN
	. IF $PIECE(POS1,Y,19)=1 SET STATUS=90 QUIT  ;NOCH NICHT FREIGEGEBEN ;yet Not 
	. ;
	. IF $PIECE(POS1,Y,61)'="" SET STATUS=70 QUIT  ;GELIEFERT
	. IF ($PIECE(POS1,Y,42)'="") || ($PIECE(POS1,Y,52)'="") SET STATUS=60 QUIT  ;BEREITSGESTELLT
	. IF $PIECE(POS1,Y,5)>0 IF YBES>$PIECE(POS1,Y,5) DO  QUIT  ;GENUG AN LAGER ;sufficiently upon stock location 
	. . IF $PIECE(POS1,Y,40)'="" SET STATUS=50 QUIT            ;AUSLIEFERUNGSDATUM GEPLANT
	. . SET STATUS=40 QUIT                                     ;AN LAGER ;upon stock location 
	. ;
	. SET STATUS=30 QUIT
	
	
	;SUPPLIES
	IF YTYP="S" DO
	. IF '$DATA(^INDRPSUPPLY(YM,YBET,YID)) SET STATUS=91 QUIT  ;GELÖSCHT
	. SET POS1=$GET(^INDRPSUPPLY(YM,YBET,YID,1))
	. IF $PIECE(POS1,Y,4)=""   SET STATUS=91 QUIT  ;KEIN ARTIKEL=GELÖSCHT ;no Item No
	. SET ART=$PIECE(POS1,Y,4)
	. IF $PIECE(POS1,Y,98)=1   SET STATUS=91 QUIT  ;STORNIERT Deleted Order
	. IF $PIECE(POS1,Y,99)=1   SET STATUS=93 QUIT  ;ABGESCHLOSSEN Closed
	. IF $PIECE(POS1,Y,19)=1   SET STATUS=90 QUIT  ;NOCH NICHT FREIGEGEBEN ;yet Not Blocked
	. ;
	. IF $PIECE(POS1,Y,64)'="" SET STATUS=93 QUIT  ;GELIEFERT=ERLEDIGT Date Received
	. IF $PIECE(POS1,Y,40)'="" SET STATUS=40 QUIT  ;READY FOR DELIVERY  ;FIS:25.04.05;SR12200 Planned Ship Date
	. IF $PIECE(POS1,Y,20)'="" SET STATUS=30 QUIT  ;BESTÄTIGT  ;FIS:25.04.05;SR12200 Firmed
	. IF $PIECE(POS1,Y,7)'=""  SET STATUS=20 QUIT  ;BESTELLUNG ÜBERGELEITET Confirmed In-Date
	. SET STATUS=10 QUIT
	
	
	;LAUFZEITPRÜFUNG ;Run Time Check
	;===============
	DO
	. IF STATUS=93 SET OK=1 QUIT  ;ERLEDIGT
	. IF (STATUS=91) || (STATUS=92) SET OK=0  QUIT  ;NICHT OK ;Not 
	. SET ART1=""
	. IF $GET(ART)'="" DO
	. . NEW WG
	. . IF $DATA(^INDRPITEM(YM,YBET,ART,1)) SET ART1=^INDRPITEM(YM,YBET,ART,1) QUIT       ; naked ref
	. . SET WG=$PIECE($GET(^INART(YM,ART,1)),Y,30)                                        ;WARENGRUPPE
	. . IF WG="" SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)                               ;DFLT. WARENGRUPPE
	. . IF WG'="" IF $DATA(^INDRPITEMS(YM,YBET,WG,1)) SET ART1=^INDRPITEMS(YM,YBET,WG,1)  ;STANDARD VORGABEN  ; naked ref
	. ;
	. QUIT:ART1=""
	. SET RESTDAUER=0
	. IF (YTYP="1") || (YTYP="2") DO
	. . IF STATUS=90 FOR YI(1)=60,63,66,69 SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=10 FOR YI(1)=63,66,69    SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=20 FOR YI(1)=66,69       SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=30 SET RESTDAUER=+$PIECE(ART1,Y,69)
	. ;
	. IF (YTYP="01") || (YTYP="03") || (YTYP="05") || (YTYP="0") DO
	. . IF STATUS=90 FOR YI(1)=60,63,66,69,72,75,78,79 SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=10 FOR YI(1)=63,66,69,72,75,78,79    SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=20 FOR YI(1)=66,69,72,75,78,79       SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=30 FOR YI(1)=69,72,75,78,79          SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=40 FOR YI(1)=72,75,78,79             SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=50 FOR YI(1)=75,78,79                SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=60 FOR YI(1)=78,79                   SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=70 SET RESTDAUER=+$PIECE(ART1,Y,79)
	. ;
	. IF YTYP="02" DO
	. . IF STATUS=90 FOR YI(1)=72,75,78,79 SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=40 FOR YI(1)=72,75,78,79 SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=50 FOR YI(1)=75,78,79    SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=60 FOR YI(1)=78,79       SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=70 SET RESTDAUER=+$PIECE(ART1,Y,79)
	. ;
	. IF YTYP="D" DO
	. . IF STATUS=90 FOR YI(1)=60,63,66,69,72,75,78,79 SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=30 FOR YI(1)=69,72,75,78,79          SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=40 FOR YI(1)=72,75,78,79             SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=50 FOR YI(1)=75,78,79                SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=60 FOR YI(1)=78,79                   SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=70 SET RESTDAUER=+$PIECE(ART1,Y,79)
	. ;
	. IF YTYP="S" DO
	. . IF STATUS=90 FOR YI(1)=60,63,66,69 SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=10 FOR YI(1)=63,66,69    SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=20 FOR YI(1)=66,69       SET RESTDAUER=RESTDAUER+$PIECE(ART1,Y,YI(1))
	. . IF STATUS=30 SET RESTDAUER=+$PIECE(ART1,Y,69)
	. ;
	. SET RESTDAUER=RESTDAUER+$$^INTAGEPLUS(+$HOROLOG,(+$HOROLOG+RESTDAUER),YBET,1)  ;PLUS FREIE TAGE (WOCHENENDE+FEIERTAGE)
	. IF ($HOROLOG+RESTDAUER)'>YTERM SET OK=1  ;OK
	
	QUIT STATUS_"#"_OK
	
]]></Routine>
</Export>