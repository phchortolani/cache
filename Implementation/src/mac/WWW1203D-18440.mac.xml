<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW1203D" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW1203D
	
 	#include COMSYS
#include COMConst
#include WWWConst
	
OnBeforeSave(pYKEY="",YFELD) // This code is currently not in use.
    ;-------------------------------------------------------------------------------
    ; Execute before save
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Oct-2006	RPW		SRBR014072: Change $lb to $listbuild
    ; 12-Jul-2006	shobby	SRBR014072: Created.  
    ;-------------------------------------------------------------------------------
	new strStatus,strForm,strLanguage,strPosition
	
	set strStatus = $$$OK
	
	if $$$WWW1203DHide(YFELD) {
		set strForm     = $$$KEY1(pYKEY)
		set strLanguage = $$$KEY2(pYKEY)
		set strPosition = $$$KEY3(pYKEY)
		if strPosition = $order(^WWW1203(0,strForm,strLanguage,"")) {
			;First Tab in sequence will show as the 'Primary' or 'Selected' tab when entering a
			;form even if not the first one in the display sequence.
			set strStatus = $listbuild("34431") ; "Error!!!, Primary tab can not be hidden."
		}
	}
 
	if $$$ISOK(strStatus) {
		set Q=$$$QSave
	} else {
		do ReturnError^COMUtils(strStatus)
		set Q=$$$QDontSave
	}
	quit
	
	
OnBeforeSaveHook()
	;-------------------------------------------------------------------------------
	; On before save hook for WWW1203D.
	; 	
    ; Returns:
    ; 	strStatus for the save action.
	;
	; History:
	; 27-Jun-2007	FRANK	SRBR014503: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$UpdateCoreTab(YFELD, $$$KEY1(YKEY)_$$$COMMA_$$$KEY2(YKEY)_$$$COMMA_$$$KEY3(YKEY))
	quit strStatus
	
	
FirstCustom() ; 0-49 core.  50+ Custom ;SR17846
	quit 101
	
	
OnBeforeDeleteHook(pYKEY)
	;-------------------------------------------------------------------------------
	; On before delete hook for WWW1203D.
	; 
	; pYKEY = return the keys
	; 	
    ; Returns:
    ; 	strStatus for the delete action.
    ; 	
	; History:
	; 08-Nov-2011	shobby	SR17846: Changed limits.  Core 0-49, Custom 50+
	; 22-Jul-2011	GRF		-: Order macros
	; 05-Oct-2007	GM		SR15596: Delete only Customisation Tabs 
	; 27-Jun-2007	FRANK	SRBR014503: Created
	;-------------------------------------------------------------------------------
	new strStatus, idCompany, idPage, intCountComp
	
	$$$VAR
	set strStatus = $$$OK
	set intCountComp = 0
	set idPage = $$$KEY3(pYKEY)
 
	;set strStatus = $$DeleteCoreTab($$$KEY1(YKEY)_$$$COMMA_$$$KEY2(YKEY)_$$$COMMA_$$$KEY3(YKEY))	
	; Frank's code, modified by GM 
 
	if (idPage '< $$FirstCustom()) {				;SR17846
		; below 50 this is a core tab 
		; 50+ it's just a customisation
	
		set idCompany = ""
		for {
			set idCompany = $order(^WWW1203D(0,$$$KEY1(pYKEY),$$$KEY2(pYKEY),idPage,idCompany))
			quit:idCompany=""
			
			set intCountComp = intCountComp + 1		
		}	
		
		if (intCountComp < 2) {
			set strStatus = $$DeleteCoreTab($$$KEY1(pYKEY)_$$$COMMA_$$$KEY2(pYKEY)_$$$COMMA_idPage)
		}			
	} 
	quit strStatus	
	
	
DeleteCoreTab(pidCoreTab)
	;-------------------------------------------------------------------------------
	; Delete a core tab.
	;
	; 	pidCoreTab - the id of the core tab to be deleted.
	; 	
    ; Returns:
    ; 	strStatus for the delete action.
	;
	; History:
	; 27-Jun-2007	FRANK	SRBR014503: Created
	;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus = $$$Kill("WWW1203",pidCoreTab)
 
	quit strStatus
	
UpdateCoreTab(pobjCustomTab, pidCoreTab)
	;-------------------------------------------------------------------------------
	; Update/create the core tab related to the Custom tab created/updated.
	;
	; 	pobjCustomTab - the custom tab record.
	; 	pidCoreTab - the id of the core tab to be created/updated.
	; 	
    ; Returns:
    ; 	strStatus for the save action of the core tab.
	;
	; History:
	; 03-Sep-2013	shobby	CORE-241: Fixed test for existing tab.
	; 08-Nov-2011	shobby	SR17846: Changed limits.  Core 0-49, Custom 50+
	; 26-Oct-2007	shobby	SR15596: Cannot use $$$Save here because we want access
	; 							to the pblnDoOnBeforeSave flag to prevent a recursive
	; 							call as WWW1203 can update WWW1203D and the reverse
	; 							also applies.
	; 05-Oct-2007	GM		SR15596: Save page numbers greater than 99 in WWW1203D
	; 27-Jun-2007	FRANK	SRBR014503: Created
	;-------------------------------------------------------------------------------
	new objTab, strStatus, intPageNumber
 
	set strStatus     = $$$OK
	set intPageNumber = $$$KEY3(pidCoreTab)
 
	// If we are saving a custom tab with id 50+, we have to create/update a core tab.
	if intPageNumber '<$$FirstCustom() {						;SR17846
		set $$$WWW1203PictureFileForPageTag(objTab)   = $$$WWW1203DPictureFileForPageTag(pobjCustomTab)
		set $$$WWW1203NextLine(objTab)                = $$$WWW1203DNextLine(pobjCustomTab)
		set $$$WWW1203ToolTip(objTab)                 = $$$WWW1203DToolTip(pobjCustomTab)
		set $$$WWW1203DataFieldSearchFunction(objTab) = $$$WWW1203DDataFieldSearchFunction(pobjCustomTab)
		set $$$WWW1203AccessForModule(objTab)         = $$$WWW1203DAccessForModule(pobjCustomTab)
		set $$$WWW1203UsersAccess(objTab)             = $$$WWW1203DUsersAccess(pobjCustomTab)
		set $$$WWW1203DisplayPosition(objTab)         = $$$WWW1203DDisplayPosition(pobjCustomTab)
		
		; ******  Do not use $$$Save here!!!! SR15596
		;set strStatus = $$$Save("WWW1203",pidCoreTab,objTab,$$$YES)
		set strStatus = $$^WWWSPEI("WWW1203",pidCoreTab,objTab,$$$YES,,,,$$$NO)
	} else {
		if '$data(^WWW1203(YM,$$$KEY1(pidCoreTab),$$$KEY2(pidCoreTab),$$$KEY3(pidCoreTab))) {
			set strStatus=$$$MakeStatus(34360,$$FirstCustom()) ;"Page numbers less than %1 must correspond to an existing core tab."
		}
	}
 	;set strStatus = $$$OK
	quit strStatus
	
	
CreateCoreTabs(pidLog)
	;-------------------------------------------------------------------------------
	; Loops through all the custom tabs, and creates the related core tab.
	;
	; 	pidLog - the id log of the update process.
	; 	
    ; Returns:
    ; 	strStatus for the save action of the core tab.
	;
	; History:
	; 08-Nov-2011	shobby	SR17846: Changed limits.  Core 0-49, Custom 50+
	; 22-Jul-2011	GRF		-: Order macros
	; 27-Jun-2007	FRANK	SRBR014503: Created
	;-------------------------------------------------------------------------------
	$$$VAR
	new idPage,idForm,idLanguage,objCustomTab,strStatus,idCompany,idCustomTab
	
	set idPage = $$FirstCustom()-1					;SR17846
	set strStatus = $$$OK
	
	for {
		set idPage = $order(^WWW1203Ds(0,2,idPage))
		quit:idPage=""
		
		set idForm = ""
		for {
			set idForm = $order(^WWW1203Ds(0,2,idPage,idForm))
			quit:idForm=""
			
			set idLanguage = ""
			for {
				set idLanguage = $order(^WWW1203Ds(0,2,idPage,idForm,idLanguage))
				quit:idLanguage=""
				
				set idCompany = ""
				for {
					set idCompany = $order(^WWW1203Ds(0,2,idPage,idForm,idLanguage,idPage,idCompany))
					quit:idCompany=""
					
					set objCustomTab = $get(^WWW1203D(0,idForm,idLanguage,idPage,idCompany,1))
					set idCustomTab  = idForm_$$$COMMA_idLanguage_$$$COMMA_idPage_$$$COMMA_idCompany
					set strStatus    = $$$Save("WWW1203D",idCustomTab,objCustomTab,$$$YES)
					
					if $$$ISERR(strStatus) {
						do AddMessage^COMLog(pidLog,"Data update failure: "_$$$Text(strStatus))
					}
				}
			}
		}
	}
	quit strStatus
	
]]></Routine>
</Export>