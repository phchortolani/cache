<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARHistoricoMOVDetalhe" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARHistoricoMOVDetalhe
#include COMSYS
#include MEDConst
 
OnBeforeFormConstruction
	
	if ( (YPARA = "") && ($length(YAUSWAHL) = 1) ) {
		set YPARA = YAUSWAHL
	}

	if (YKEY '= "") {
		set YOPTION = YKEY
	} elseif ( (YOPTION = "") && (YPARA '= "") ) {
		set YOPTION = YPARA
	} else {
		set YOPTION = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,10)
	}
 	
 	if $extract(YAUSWAHL,1,1) = "D" {
	 	set VORG(1) = $piece(YAUSWAHL,",",2)  ;mesInicio
	 	set VORG(2) = $piece(YAUSWAHL,",",3)  ;anoInicio
	 	set VORG(3) = $piece(YAUSWAHL,",",4)  ;mesFinal
	 	set VORG(4) = $piece(YAUSWAHL,",",5)  ;anoFinal
	 	set VORG(5) = $piece(YAUSWAHL,",",6)  ;local
	 	set VORG(6) = $piece(YAUSWAHL,",",7)  ;centroDeCusto
	 	set VORG(7) = $piece(YAUSWAHL,",",8)  ;paciente
	 	set VORG(8) = $piece(YAUSWAHL,",",9)  ;produto(s)
	 	set VORG(9) = $piece(YAUSWAHL,",",10) ;grupo(s)
	 	set VORG(11) = $piece(YAUSWAHL,",",11) ;grupo(s)
		set ^VARTempHistMOVDetalhe(YBED) = VORG(1)_Y_VORG(2)_Y_VORG(3)_Y_VORG(4)_Y_VORG(5)_Y_VORG(6)_Y_VORG(7)_Y_VORG(8)_Y_VORG(9)_Y_""_Y_VORG(11)
 	}
 	
 	quit
 
 
OnAfterSave
	new mesInicio, anoInicio, mesFinal, anoFinal, local, centroDeCusto, paciente, produto, grupo, screenSetUp,
		visualizar
	
	set mesInicio 	  = $get(VORG(1))
	set anoInicio 	  = $get(VORG(2))
	set mesFinal  	  = $get(VORG(3))
	set anoFinal  	  = $get(VORG(4))
	set local     	  = $get(VORG(5))
	set centroDeCusto = $get(VORG(6))
	set paciente   	  = $get(VORG(7))
	set produto   	  = $get(VORG(8))
	set grupo     	  = $get(VORG(9))
	set screenSetUp   = YOPTION
	set visualizar 	  = $get(VORG(11))
	
	if ((mesFinal '= "") && (anoFinal = "")) {	
		$$$Alert("Favor selecionar o ano para o mês indicado.")
	}
	elseif ((mesFinal = "") && (anoFinal '= "")) {	
		$$$Alert("Favor selecionar o mês para o ano indicado.")
	}
	
	set ^VARTempHistMOVDetalhe(YBED) = mesInicio_Y_anoInicio_Y_mesFinal_Y_anoFinal_Y_local_Y_centroDeCusto_Y_paciente_Y_produto_Y_grupo_Y_screenSetUp_Y_visualizar
	
	do GoToForm^COMUtilForm("VARHistoricoMOVDetalhe",YKEY)
 	
 	quit
 
OnAfterDataFields
	
 	//Parâmetros que vem da tela 	
	new mesInicio, anoInicio, mesFinal, anoFinal, local, centroDeCusto, paciente, produto, grupo, screenSetUp,
		visualizar
		
 	//Parâmetros obrigatórios
	set mesInicio = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,1)		
	set anoInicio = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,2)
	
	if mesInicio = "" set mesInicio = $get(VORG(1))
	if anoInicio = "" set anoInicio = $get(VORG(2))
 
	quit:((mesInicio = "") || (anoInicio = ""))
	
	//Parâmetros opcionais
	set mesFinal 	  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,3)
	set anoFinal 	  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,4)
	set local 		  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,5)
	set centroDeCusto = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,6)
	set paciente 	  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,7)
	set produto 	  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,8)
	set grupo 		  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,9)
	set screenSetUp   = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,10)
	set visualizar	  = $piece($get(^VARTempHistMOVDetalhe(YBED)),Y,11)
 
	if mesFinal = "" set mesFinal = $get(VORG(3))
	if anoFinal = "" set anoFinal = $get(VORG(4))
	if local = "" set local = $get(VORG(5))
	if grupo = "" set grupo = $get(VORG(9))
 
	set objTemp = mesInicio_Y_anoInicio_Y_mesFinal_Y_anoFinal_Y_local_Y_centroDeCusto_Y_paciente_Y_produto_Y_grupo_Y_screenSetUp_Y_visualizar
	
	set ^VARTempHistMOVDetalhe(YBED) = objTemp
	
	if paciente '= "" {
		do ImprimeHistoricoMOVPaciente(objTemp)	
	}
	
	elseif centroDeCusto '= "" {		
		do ImprimeHistoricoMOVCentroCusto(objTemp)
	}
	
	elseif local '= "" {		
		do ImprimeHistoricoMOVLocal(objTemp)	
	} 
	
	else {
		$$$Alert("É necessário refinar a pesquisa.")
		quit
	}
 	
 	quit

ImprimeHistoricoMOVCentroCusto(pobjTemp)
	new mesInicio, anoInicio, mesFinal, anoFinal, filterCC, filterItem, filterGroup, minimumDate, maximumDate,
		date, objConsumoSetor, custoTotal, custoGeral
	
	set mesInicio   = $piece(pobjTemp,Y,1)
	set anoInicio   = $piece(pobjTemp,Y,2)
	set mesFinal    = $piece(pobjTemp,Y,3)
	set anoFinal    = $piece(pobjTemp,Y,4)
	set filterCC    = $piece(pobjTemp,Y,6)  //CC = Centro de Custo
	set filterItem  = $piece(pobjTemp,Y,8)
	set filterGroup = $piece(pobjTemp,Y,9)
	
	set count = 0
 	set custoFinal = 0
 
 	new lstHeader
 
 	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Seq.","Código","Descrição","Grupo","Qtde","UN","Valor")
 
	set idItem = ""
 
	if $$Start^COMTable(lstHeader,"Histórico de Movimentação para o Centro de Custo "_filterCC_" - "_$piece($get(^WWW0121(YM,YM,filterCC,1)),Y,1)) {
 		 	
 		$$$Order4(^VARMOVLinhas,YM,1,filterCC,idItem)
 		
			//Se filtrou por produto..
			/*
			if filterItem '= "" {
				set fitemFind = ";"_filterItem_";"
				set idItemFind = ";"_idItem_";"
				continue:('$find(fitemFind,idItemFind))	
			}
			*/			
			if (filterItem '= "") {
				continue:(idItem '= filterItem)
			}
			
			//Se filtrou por grupo...			
			if filterGroup '= "" {
				set itemGroup = $piece($get(^INART(YM,idItem,1)),Y,30)					
				;continue:('$find(filterGroup,itemGroup))
				if $piece(filterGroup,".",2) = "" {
					continue:( $piece(itemGroup,".",1) '= filterGroup )
				} elseif $piece(filterGroup,".",2) '= "" {
					continue:(itemGroup '= filterGroup)
				}				
			}
		
			set ano = ""			
			set qtyItem = 0		
			set custoTotal = 0
						
			$$$Order5(^VARMOVLinhas,YM,1,filterCC,idItem,ano)
 
				$$$Order6(^VARMOVLinhas,YM,1,filterCC,idItem,ano,mes)
					
					//Se o usuário preencheu os campos De e Ate
					if (mesFinal '= "")&&(anoFinal '= "") {
						set thisDate = $zdh("01/"_mes_"/"_ano,4)
						set minimumDate = $zdh("01/"_mesInicio_"/"_anoInicio,4)					
						set maximumDate = $zdh("01/"_mesFinal_"/"_anoFinal,4)						
						continue:(thisDate < minimumDate)||(thisDate > maximumDate)
					}
						
					//Se o usuário preencheu somente os campos De
					else {
						continue:(ano '= anoInicio)||(mes '= mesInicio)
					}
	 
					set local = ""
										
					for {
						set local = $order(^VARMOVLinhas(0,1,filterCC,idItem,ano,mes,local))
						quit:(local = "")
					
						set idTransaction = ""
					
						for {
							set idTransaction = $order(^VARMOVLinhas(0,1,filterCC,idItem,ano,mes,local,idTransaction))
							quit:(idTransaction = "")

							set objConsumoSetor = $get(^INIssue(YM,idTransaction,1))

							set centroDeCustoTrans = $piece(objConsumoSetor,Y,2)
							continue:(filterCC '= centroDeCustoTrans)

							set date = $piece(objConsumoSetor,Y,9)
							if date = "" set date = $piece(objConsumoSetor,Y,7)

							set qty = 0
							set qty = $piece($get(^VARMOVLinha(YM,idItem,ano,mes,local,idTransaction,1)),Y,1)
							set qtyItem = qtyItem + qty

							set custo = $$GetCost^INItemCost(idItem)
							set custoDoc = custo * qty

							set custoTotal = custoTotal + custoDoc
							set custoFinal = custoFinal + custoDoc
						}
					$$$End
				$$$End
 			$$$End

			continue:(qtyItem = 0)
		
			set count = $i(count)
 			
 			do PrintTableHistoricoMOVCC(count,idItem,qtyItem,custoTotal)
 		}
 	
 		do NewLine^COMTable("lightsteelblue")
 		do InsertCell^COMTable("<strong>Total:",,,,"RIGHT",,6)
 		do InsertCurrencyCell^COMTable(custoFinal)
 		do EndLine^COMTable()
	}
 
	do Stop^COMTable()
	
	quit

PrintTableHistoricoMOVCC(pCount,pidItem,qtyItem,custoTotal)
	new objItem, itemName, itemUnit, itemGroup
 
	set objItem = $get(^INART(YM,pidItem,1))
	
	set itemName  = $piece(objItem,Y,1)
	set itemUnit  = $piece($get(^WWW101(YM,"EINHEIT","PT",$piece(objItem,Y,40),1)),"~",1)
 	set itemGroup = $piece(objItem,Y,30)
 
	do NewLine^COMTable("")
	do InsertCell^COMTable(pCount)
	do InsertCell^COMTable(pidItem)
	do InsertCell^COMTable(itemName,,,,,,,,,1)
	do InsertCell^COMTable(itemGroup,,,,"CENTER")
	do InsertCell^COMTable($$^WWWTR(0,12,qtyItem),,,,"RIGHT")
	do InsertCell^COMTable($extract(itemUnit,1,2))
	do InsertCurrencyCell^COMTable(custoTotal)
	do EndLine^COMTable()
 
	quit
 
ImprimeHistoricoMOVPaciente(pobjTemp)
	new mesInicio, anoInicio, mesFinal, anoFinal, local, centroDeCusto, paciente, produto, grupo, date,
		objConsumoPaciente, objTransaction, tipoTransaction, objDevolucao, form, totalCostYear
	
	set mesInicio 	  = $piece(pobjTemp,Y,1)
	set anoInicio 	  = $piece(pobjTemp,Y,2)
	set mesFinal  	  = $piece(pobjTemp,Y,3)
	set anoFinal  	  = $piece(pobjTemp,Y,4)
	set local 	  	  = $piece(pobjTemp,Y,5)
	set centroDeCusto = $piece(pobjTemp,Y,6)
	set paciente  	  = $piece(pobjTemp,Y,7)
	set produto  	  = $piece(pobjTemp,Y,8)
	set grupo 	  	  = $piece(pobjTemp,Y,9)
	set count = 0
 
 	new lstHeader
 	set lstHeader = ""
  	//set lstHeader = lstHeader_$listbuild("Seq.","Código","Descrição","Grupo","No. Documento","Tipo","Local de Retirada","Data","Qtde","UN")
  	set lstHeader = lstHeader_$listbuild("Seq.","Código","Descrição","Grupo","No. Documento","Local de Retirada / Devolução","Data","Qtde","UN")
 	
 	set idItem = ""
 
   	set objPaciente = $get(^MEDPatient(YM,paciente,1))
    
	set nome    = $$$MEDPatientName(objPaciente)
	set dteNasc = $$$MEDPatientDOB(objPaciente) 
 
	if dteNasc {
		set idade = $System.SQL.DATEDIFF("year",dteNasc,$horolog)
		set titleHeader = "Paciente: "_nome_" (Cód. "_paciente_")<br> Idade: "_idade_" anos ("_$zdate(dteNasc,4)_")"
 
	} else {
    	set titleHeader = "Paciente: "_nome_" (Cód. "_paciente_")"
	} 
 
	if $$Start^COMTable(lstHeader,titleHeader) {
 		
	 	for {
			set idItem = $order(^VARMOVLinhas(0,1,paciente,idItem))
			quit:(idItem = "")
 
			//Se filtrou por produto..
			/*
			if produto '= "" {
				set produtoFind = ";"_produto_";"
				set idItemFind = ";"_idItem_";"
				continue:('$find(produto,idItem))
			}*/
			if (produto '= "") {
				continue:(idItem '= produto)
			}
								
			set objItem = $get(^INART(YM,idItem,1))
			set itemGrupo = $piece(objItem,Y,30)
			
			//Se filtrou por grupo...
			/*if grupo '= "" {
				continue:('$find(grupo,itemGrupo))
			}
			*/
			
			if (grupo '= "") {
				if $piece(grupo,".",2) = "" {
					continue:( $piece(itemGrupo,".",1) '= grupo )				
				} elseif $piece(grupo,".",2) '= "" {
					continue:(itemGrupo '= grupo)
				}				
			}
			
			set itemDescricao   = $piece(objItem,Y,1)
			set itemUnidade     = $piece(objItem,Y,40)			
			set itemDescUnidade = $piece($get(^WWW101(YM,"EINHEIT","PT",itemUnidade,1)),"~",1)			
 			
			set ano = ""			
			
			set qtyItem = 0
			
	 		for {
				set ano = $order(^VARMOVLinhas(0,1,paciente,idItem,ano))
				quit:(ano = "")
				
				set mes = ""
				
				for {
					set mes = $order(^VARMOVLinhas(0,1,paciente,idItem,ano,mes))
					quit:(mes = "")
					
					//Se o usuário preencheu os campos De e Ate
					if (mesFinal '= "") && (anoFinal '= "") {
						set thisDate = $zdh("01/"_mes_"/"_ano,4)
						set minimumDate = $zdh("01/"_mesInicio_"/"_anoInicio,4)						
						set maximumDate = $zdh("01/"_mesFinal_"/"_anoFinal,4)
						continue:(thisDate < minimumDate)||(thisDate > maximumDate)
					}
						
					//Se o usuário preencheu somente os campos De
					else {
						continue:(ano '= anoInicio)||(mes '= mesInicio)
					}
					
					set local = ""
					
					for {
						set local = $order(^VARMOVLinhas(0,1,paciente,idItem,ano,mes,local))
						quit:(local = "")
						
						set idTransaction = ""
						
						for {
							set idTransaction = $order(^VARMOVLinhas(0,1,paciente,idItem,ano,mes,local,idTransaction))
							quit:(idTransaction = "")
							
							set objTransaction  = $get(^VARMOVLinha(0,idItem,ano,mes,local,idTransaction,1))
							set tipoTransaction = $piece(objTransaction,Y,3)
							
							if (tipoTransaction = 21) {
								set objConsumoPaciente = $get(^INIssue(YM,idTransaction,1))
								set pacienteTrans = $piece(objConsumoPaciente,Y,23)
								continue:(paciente '= pacienteTrans)
							
								set date = $piece(objConsumoPaciente,Y,7)			
								if date = "" set date = $piece(objConsumoPaciente,Y,9)
								
								set form = "INDispenseToPatient"
							
							} elseif (tipoTransaction = 22) {								
								set objDevolucao = $get(^INIssueRev(YM,idTransaction,1))
								set idConsumo = $piece(objDevolucao,Y,1)
								
								set objConsumoPaciente = $get(^INIssue(YM,idConsumo,1))
								set pacienteTrans = $piece(objConsumoPaciente,Y,23)
								continue:(paciente '= pacienteTrans)
								
								set date = $piece($piece(objDevolucao,Y,5),",",1)
								
								set form = "INIssueRev"
							
							} else {
								set form = "INDispenseToPatient"	
							}
							
							;set tipoPaciente     = $piece(objConsumoPaciente,Y,13)
							;set descTipoPaciente = $piece($get(^INPARA(YM,"VARSESTIPOPACIENTE","PT",tipoPaciente,1)),Y,1)
							
							set count = $i(count)
							
							set qty = 0
							set qty = $piece($get(^VARMOVLinha(YM,idItem,ano,mes,local,idTransaction,1)),Y,1)
							set qtyItem = qtyItem + qty
							
							do NewLine^COMTable()
							do InsertCell^COMTable(count)
							do InsertCell^COMTable(idItem)
							do InsertCell^COMTable(itemDescricao,,,,,,,,,1)
							do InsertCell^COMTable(itemGrupo,,,,"CENTER")
							do InsertCell^COMTable(idTransaction,form,idTransaction,,,,,,"_BLANK")
							;do InsertCell^COMTable(descTipoPaciente)
							do InsertCell^COMTable($piece($get(^WWW0121(YM,YM,local,1)),Y,1))
							do InsertCell^COMTable($zdate(date,4))							
							do InsertCell^COMTable($$^WWWTR(0,12,qty),,,,"RIGHT")
							do InsertCell^COMTable($extract(itemDescUnidade,1,2))							
							do EndLine^COMTable()
						}
					}
				}	 		
	 		}
 			
 			if (qtyItem '= 0) { 			
				do NewLine^COMTable("lightsteelblue")
				do InsertCell^COMTable("<b>Total do produto</b>",,,,"RIGHT",,7)
				do InsertCell^COMTable("<b>"_$$^WWWTR(0,12,qtyItem)_"</b>",,,,"RIGHT")
				do InsertCell^COMTable($extract(itemDescUnidade,1,2))
				do EndLine^COMTable()	 		
 			}
	 	}
	
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum registro encontrado para o período pesquisado.",,,,,,9)
			do EndLine^COMTable()
		}
	
		do Stop^COMTable()
	}
 
	set lstHeader2 = ""
  	set lstHeader2 = lstHeader2_$listbuild("Seq.","Código","Descrição","Jan","Fev","Mar","Abr","Mai","Jun","Jul","Ago","Set","Out","Nov","Dez","Total","Valor")
		
	set ano = ""
	
	for {
		set ano = $order(^VARPARA(YM,"ANO","PT",ano))
		quit:(ano = "")
		
		set totalCostYear = 0
 
		if $$Start^COMTable(lstHeader2,"Histórico mensal em "_ano_"") {
	
			set count2 = 0
 	
	 		for {
				set idItem = $order(^VARMOVLinhas(0,1,paciente,idItem))
				quit:(idItem = "")
 
				//Se filtrou por produto..
				/*if produto '= "" {
					set produtoFind = ";"_produto_";"
					set idItemFind = ";"_idItem_";"
					continue:('$find(produto,idItem))	
				}*/
				
				if (produto '= "") {
					continue:(idItem '= produto)
				}				
				
				set objItem = $get(^INART(YM,idItem,1))
				set itemGrupo 	  = $piece(objItem,Y,30)
				set itemDescricao = $piece(objItem,Y,1)
				
				//Se filtrou por grupo...
				/*if grupo '= "" {					
					continue:('$find(grupo,itemGrupo))
				}*/

				if (grupo '= "") {
					if $piece(grupo,".",2) = "" {
						continue:( $piece(itemGrupo,".",1) '= grupo )				
					} elseif $piece(grupo,".",2) '= "" {
						continue:(itemGrupo '= grupo)
					}				
				}
			
				set count2 = $i(count2)
				set qtyItem = 0
				
				set mes = ""
				
				do NewLine^COMTable()
				do InsertCell^COMTable(count2)
				do InsertCell^COMTable(idItem)
				do InsertCell^COMTable(itemDescricao,,,,,,,,,1)		
				
				for {
					set mes = $order(^WWW101(YM,"COMMONTHSOFYEAR","PT",mes))
					quit:(mes = "")
					
					set qtyMes = $$GetQtyMes(paciente,idItem,mes,ano)
					set qtyItem = qtyItem + qtyMes
					
					do InsertCell^COMTable($$^WWWTR(0,12,qtyMes),,,,"RIGHT")				
				}
				
				do InsertCell^COMTable($$^WWWTR(0,12,qtyItem),,,,"RIGHT")
				
				set itemCost = $$GetCost^INItemCost(idItem)
				set totalCostItem = qtyItem * itemCost
				
				set totalCostYear = totalCostYear + totalCostItem
				
				do InsertCurrencyCell^COMTable(totalCostItem)
				
				;do InsertCell^COMTable(,,,,"RIGHT")
				do EndLine^COMTable()
			}
			
			do NewLine^COMTable("lightsteelblue")
			do InsertCell^COMTable("<strong>Total no ano:</strong>",,,,"RIGHT",,16)
			do InsertCurrencyCell^COMTable(totalCostYear)
			do EndLine^COMTable()
		}
		
		if (count2 = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum registro encontrado para o período pesquisado.",,,,,,17)
			do EndLine^COMTable()
		}
					
		do Stop^COMTable()	
		
	}
 
	quit
	
GetQtyMes(pPaciente,pidItem,pMonth,pYear)
	new local, idTransaction, qty, qtyItem
 
	set local = ""	
	set qtyItem = 0
	
	for {
		set local = $order(^VARMOVLinhas(0,1,pPaciente,pidItem,pYear,pMonth,local))
		quit:(local = "")
 
		set idTransaction = ""
		
		for {
			set idTransaction = $order(^VARMOVLinhas(0,1,pPaciente,pidItem,pYear,pMonth,local,idTransaction))
			quit:(idTransaction = "")		
			
			set qty = 0
			set qty = $piece($get(^VARMOVLinha(YM,pidItem,pYear,pMonth,local,idTransaction,1)),Y,1)
			set qtyItem = qtyItem + qty
		}
	}
 
	quit qtyItem
	
ImprimeHistoricoMOVLocal(pobjTemp)	
	new mesInicio, anoInicio, mesFinal, anoFinal, filterLocal, filterGroup, count, custoFinal, date,
		idItem, filterGroup, qtyItem, custoTotal, custo, custoItem
	
	set mesInicio 	  = $piece(pobjTemp,Y,1)
	set anoInicio 	  = $piece(pobjTemp,Y,2)
	set mesFinal  	  = $piece(pobjTemp,Y,3)
	set anoFinal  	  = $piece(pobjTemp,Y,4)
	set filterLocal	  = $piece(pobjTemp,Y,5)
	set filterGroup	  = $piece(pobjTemp,Y,9)
	
	set count = 0
 	set custoFinal = 0

 	new lstHeader
 
 	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Seq.","Código","Descrição","Grupo","Qtde","UN","Valor")

	if $$Start^COMTable(lstHeader,"Histórico de Movimentação para o local "_filterLocal_" - "_$piece($get(^WWW0121(YM,YM,filterLocal,1)),Y,1)) {
	
		$$$Order2(^VARMOVLinha,YM,idItem)
			
			set qtyItem = 0		
			set custoTotal = 0
			
			//Se filtrou por grupo...
			if filterGroup '= "" {
				set itemGroup = $piece($get(^INART(YM,idItem,1)),Y,30)					
				/*continue:('$find(filterGroup,itemGroup))*/
				if $piece(filterGroup,".",2) = "" {
					continue:( $piece(itemGroup,".",1) '= filterGroup )
				} elseif $piece(filterGroup,".",2) '= "" {
					continue:(itemGroup '= filterGroup)
				}				
			}
			
			$$$Order3(^VARMOVLinha,YM,idItem,ano)				
				
				$$$Order4(^VARMOVLinha,YM,idItem,ano,mes)
				
					//Se o usuário preencheu os campos De e Ate
					if (mesFinal '= "")&&(anoFinal '= "") {
						set thisDate = $zdh("01/"_mes_"/"_ano,4)
						set minimumDate = $zdh("01/"_mesInicio_"/"_anoInicio,4)					
						set maximumDate = $zdh("01/"_mesFinal_"/"_anoFinal,4)						
						continue:(thisDate < minimumDate)||(thisDate > maximumDate)
					}
						
					//Se o usuário preencheu somente os campos De
					else {
						continue:(ano '= anoInicio)||(mes '= mesInicio)
					}
					
					$$$Order6(^VARMOVLinha,YM,idItem,ano,mes,filterLocal,idTransaction)
						
						set objConsumoSetor = $get(^INIssue(YM,idTransaction,1))

						set date = $piece(objConsumoSetor,Y,9)
						if date = "" set date = $piece(objConsumoSetor,Y,7)

						set qty = 0
						set qty = $piece($get(^VARMOVLinha(YM,idItem,ano,mes,filterLocal,idTransaction,1)),Y,1)
						set qtyItem = qtyItem + qty
					
					$$$End
				$$$End				
			$$$End
			
			continue:(qtyItem = 0)
			
			set custo = $$GetCost^INItemCost(idItem)
			set custoItem = custo * qtyItem
			
			set custoFinal = custoFinal + custoItem			

			set count = $i(count) 			
 			do PrintTableHistoricoMOVLocal(count,idItem,qtyItem,custoItem)			
				
		$$$End
		
		 do NewLine^COMTable("lightsteelblue")
 		do InsertCell^COMTable("<strong>Total:",,,,"RIGHT",,6)
 		do InsertCurrencyCell^COMTable(custoFinal)
 		do EndLine^COMTable()
		
		do Stop^COMTable()
					
	}
	
	quit
	
PrintTableHistoricoMOVLocal(pCount,pidItem,qtyItem,custoTotal)
	new objItem, itemName, itemUnit, itemGroup
 
	set objItem = $get(^INART(YM,pidItem,1))
	
	set itemName  = $piece(objItem,Y,1)
	set itemUnit  = $piece($get(^WWW101(YM,"EINHEIT","PT",$piece(objItem,Y,40),1)),"~",1)
 	set itemGroup = $piece(objItem,Y,30)
 
	do NewLine^COMTable("")
	do InsertCell^COMTable(pCount)
	do InsertCell^COMTable(pidItem)
	do InsertCell^COMTable(itemName,,,,,,,,,1)
	do InsertCell^COMTable(itemGroup,,,,"CENTER")
	do InsertCell^COMTable($$^WWWTR(0,12,qtyItem),,,,"RIGHT")
	do InsertCell^COMTable($extract(itemUnit,1,2))
	do InsertCurrencyCell^COMTable(custoTotal)
	do EndLine^COMTable()
 
	quit
]]></Routine>
</Export>