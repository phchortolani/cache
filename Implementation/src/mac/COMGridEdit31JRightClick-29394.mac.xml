<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31JRightClick" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31JRightClick
	
#include COMGridEdit31
	
Show(pstrParameters="")
    ;-------------------------------------------------------------------------------
    ; Populates the Column menu popup.
    ; 
    ; Inputs:
    ; 	pstrParameters		Comma delimited list
    ; 							tdY[row]_[col],GridForm,"Grid Content"
    ; 							???_YFORM,???                ???
    ; 
    ; History:
    ; 
    ; 03-Sep-2013	shobby	CORE-233.6: ID of record to be opened comes with additional characters in Firefox.
    ; 13-Aug-2013	shobby	CORE-233.5: Allow sorting on fixed primary keys
	; 20-Jul-2012	shobby	SR18063: Additional menu options to sort by relation.
    ; 16-Jul-2012	shobby	SR18064: Patching of SR17886 (Internatationalize FORM name & 'Master File')
    ; 26-Mar-2012	shobby	SR17724.52: New menu option for aligning columns
    ; 02-Mar-2012	shobby	SR17724.21: Don't crash if clicking on primary key of
    ;							expanded line.
    ; 02-Sep-2011	PPP		SR17886: Internatationalize FORM name & 'Master File'
    ; 30-Nov-2009	DWR		SR17063: Use the forms descriptive title instead of key
    ; 04-Mar-2009	DavidR	SR16399: modified logic of check of relational class
    ; 							indexes and passing them in to the redirected form.
    ; 27-Feb-2009	DavidR	SR16399: if the relation class is INART, pull apart
    ; 							piece 3 of teh paramaters passed in to get the key
    ; 							to pass to the GoToForm call.
    ; 26-Feb-2009	DavidR	SR16399: added field to link to relating to the class
    ; 							used.
	; 11-Dec-2006	shobby	SRBR014286:Corrected GIF names.
	; 11-Dec-2006	shobby	SRBR014286:Corrected GIF names.
    ; 04-Dec-2006	Steve S	SR15230: Check YFORM, set blnEnable
    ; 21-Oct-2005	JW		SR11573: Set YFORM, and _ not + . Only sort if one page.
    ; 22-Feb-2005	shobby	Support menu creation when clicking in a cell in the
    ; 							body of the grid.
    ; 17-Jan-2005	Paul K	Created
    ;-------------------------------------------------------------------------------
	new count,intLine,lstNewMenuItems,i,idKey,idKeyBuild,idRelation,idRelationKey
	new lstNewMenuItem,idField,YFORM,blnEnable,lstFormLink,strFormName,strGrid,idCol
	new blnExpandCell
 
	set idField = $piece(pstrParameters,",",1)
	set intLine=""
	
	if $extract(idField,1,3)="tdY" {
		set YFORM   = $piece(pstrParameters,",",2)		//SR11573
		set intLine = $piece($piece(idField,"tdY",2),"_",1)
		set idCol   = $piece(idField,"_",2)
		; allows sorting on column headers when clicking into a cell in the body of the grid.
		//set idField="THfld_"_YFORM_"_"+$piece(idField,"_",2)
		set idField="THfld_"_YFORM_"_"_$piece(idField,"_",2)		//SR11573
	} elseif $extract(idField,1,9)="tdGridRow" {		;SR17724.21
		set YFORM   = $piece(pstrParameters,",",2)							; CORE-233.5
		set idCol   = 2														; CORE-233.5
		if $$GridVersion^COMGridEdit31V2() {								; CORE-233.5 
			set intLine = $piece($piece(idField,"tdGridRow",2),"Key",1)		; CORE-233.5
		} else {															; CORE-233.5
			set intLine = $piece(idField,"tdGridRow",2)						; CORE-233.5
		}																	; CORE-233.5
		set idField="THkey_"_YFORM_"_"_2									; CORE-233.5
	} else {
		set YFORM = $piece(idField,"_",2)
		set idCol = " "
	}
	
	do 
	//SR15230
	set blnExpandCell=(idField="THkey_Expand")||(idField["KeyExp")
	set blnEnable = 'blnExpandCell
	if YFORM="" set YFORM=$$$GRIDName

	do StartMenu^COMViewColumnMenu()
	if (idField'="") {
		;SR18063 if ($$$GRIDNumPages=1) {
			do AddRow^COMViewColumnMenu("SortAsc.gif",$$$Text("Com00154") ,"cgeSortColumnAscending("""_idField_""",""gridbody"")",,blnEnable)  ;Sort Ascending
			do AddRow^COMViewColumnMenu("SortAsc.gif", $$$Text("Com00343"),"cgeSortColumnAscending("""_idField_""",""gridbody"",1)",,blnEnable)  ;Sort Ascending By Relation ;SR18063
			do AddRow^COMViewColumnMenu("SortDsc.gif",$$$Text("Com00155") ,"cgeSortColumnDescending("""_idField_""",""gridbody"")",,blnEnable)   ;Sort Descending
			do AddRow^COMViewColumnMenu("SortDsc.gif", $$$Text("Com00344"),"cgeSortColumnDescending("""_idField_""",""gridbody"",1)",,blnEnable)   ;Sort Descending By Relation ;SR18063
		;SR18063 }
		if blnExpandCell do AddRow^COMViewColumnMenu("property.gif","Align Columns","cgeAlignColumns()",,$$$YES)  ;Align columns

		;vvvvvvvvvvvvvvvvvvvvvvSR16399:vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
		set idRelation = $$GetRelation(YFORM,idCol)
		
		if (idRelation'="") && ($extract(idRelation,1,7)'="COMTemp") {
			set strGrid = $piece(pstrParameters,",",3)
			if strGrid [ ")" {
				set strGrid = $piece(strGrid,")",1)
				;CORE-233.6 set strGrid = $extract(strGrid,2,99)
				set strGrid=$piece(strGrid,"(",2) ;CORE-233.6
			} else {
				set strGrid = $zstrip($piece(strGrid,",",1),">W")
			}
			
			set idRelationKey = $$GetRelationKey(YFORM,idCol)
			if idRelationKey = "" {
				set idRelationKey = strGrid
			} else {
				set idKeyBuild=""
				for count=1:1 {
					set idKey = $piece(idRelationKey,",",count)
					quit:idKey=""
					
					if idKey '[ $char(34) {
						if $data(@idKey){
							set idKey= @idKey
						} else {
							quit
						} 
					}
					set idKeyBuild = idKeyBuild_","_idKey
				}
				set idKeyBuild = idKeyBuild_","_strGrid
				set idKeyBuild = $extract(idKeyBuild,2,99999)
				set idRelationKey= $$^WWWKEYBUILD(idKeyBuild)
				set idRelationKey= $translate(idRelationKey,"""")
			}	
 			
 			//set strFormName = $$$WWW120FormHeaderOrImageFile($get(^WWW120(0,idRelation,1)))  ;SR17063
 			set strFormName = $$^WWWFORMNAME(idRelation)  ;SR17886
 			
			do AddSeparator^COMViewColumnMenu()
		;	do AddRow^COMViewColumnMenu("g.gif","Master File"_" ["_idRelation_"]","CallBack(""GoToForm^COMUtilForm"","""_idRelation_""","""_$piece($get(YFELD),Y,1)_""",1,1,1,1,1)",,$$$YES)
		;	do AddRow^COMViewColumnMenu("g.gif","Master File"_" ["_idRelation_"]","CallBack(""GoToForm^COMUtilForm"","""_idRelation_""","""_idRelationKey_""",1,1,1)",,$$$YES)
			//SR17886 - Materfile
			do AddRow^COMViewColumnMenu("g.gif",$$$Text("IN01320")_" ["_strFormName_"]","CallBack(""GoToForm^COMUtilForm"","""_idRelation_""","""_idRelationKey_""",1,1,1)",,$$$YES)   ;SR17063
		}
		;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
		set lstNewMenuItems=$$$OnDisplayRightClickMenu(YFORM,intLine)
		if $listlength(lstNewMenuItems)>0 {
			do AddSeparator^COMViewColumnMenu()
			for i=1:1:$listlength(lstNewMenuItems) {
				set lstNewMenuItem=$listget(lstNewMenuItems,i)
				if lstNewMenuItem="Separator" {
					do AddSeparator^COMViewColumnMenu()		
				} else {
					do AddRow^COMViewColumnMenu($listget(lstNewMenuItem,1),
					                            $listget(lstNewMenuItem,2),
					                            $listget(lstNewMenuItem,3),
					                            $listget(lstNewMenuItem,4),
					                            $listget(lstNewMenuItem,5)  )   ;Sort Descending
				}
			}
		}
	}
	
	do EndMenu^COMViewColumnMenu()
	;do AddRow^COMViewColumnMenu("sort.gif",$$$Text("Com00157"),"ChooseShow()")    ;Field Chooser
	quit
	
GetRelation(pidForm,pidFormField)
	;-------------------------------------------------------------------------------
	;
	; History:
	; 26-Feb-2009	DavidR	SR16399: Created
	;------------------------------------------------------------------------------- 
	new objFormField
							
	set objFormField = $$Get^WWW122(pidForm,pidFormField)
	quit $$$WWW122RelationClass(objFormField)
	
GetRelationKey(pidForm,pidFormField)
	;-------------------------------------------------------------------------------
	;
	; History:
	; 04-Mar-2009 	DavidR	SR16399: Created
	;-------------------------------------------------------------------------------
	new objFormField								
	set objFormField = $$Get^WWW122(pidForm,pidFormField)
	quit $$$WWW122RelationalPrimaryKey(objFormField)
 
 
CreateJavascript()
 	;-------------------------------------------------------------------------------
 	;
 	; History:
 	; 29-Jul-2010	shobby	SR17445: pageX/Y for firefox positioning.
 	; 12-Jul-2010	shobby	SR17411: Replaced call to getInnerText
 	; 05-Jul-2010	GRF		SR17413: Right Mouse Button menu fix
 	; 27-Feb-2009	DavidR	SR16399: added third element (srcElement.innerText) to 
 	; 							the callback array sent for right click on the grid,
 	; 							this reads the content of the cell that has been 
 	; 							right-clicked on
 	;-------------------------------------------------------------------------------
	&js<
	//<script language="javascript">
	//<!--
 
	function cgeRightClick(event) {
		if (!event) event=window.event
		var srcElement=findObjectTagName(event.srcElement,'TH');
		if (srcElement==null) {
			srcElement=findObjectTagName(event.srcElement,'TD');
		}
		
		if ((srcElement!=null)&&(srcElement.id.substring(0,7)!='tdExCon')) {
			CallBackNow("Show^COMGridEdit31JRightClick",srcElement.id+','+srcElement.parentNode._Form+','+srcElement.innerText.replace('\n',' '));  //SR17253    
			if (Columns.document.body.innerHTML != "") {
				//Columns.show(event.pageX ? event.pageX : event.screenX,event.pageY ? event.pageY : event.screenY,200,Columns.document.body.children[0].offsetHeight);
				Columns.show(event.pageX ? event.pageX : event.screenX,event.pageY ? event.pageY : event.screenY,200,0);
				Columns.show(event.pageX ? event.pageX : event.screenX,event.pageY ? event.pageY : event.screenY,200,Columns.document.body.children[0].offsetHeight);
			}
			event.returnValue=false;
			event.cancelBubble=false;
		}
		return false;
	}
 	//-->
	//</script>
	>
	quit
	
	/*	
OldCreateJavascript()
	&js<
	//<script language="javascript">
	//<!--
 
function cgeRightClick() {
	var srcElement;
	srcElement=findObjectTagName(event.srcElement,'TH');
	if (srcElement==null) {
		srcElement=findObjectTagName(event.srcElement,'TD');
	}
	if (srcElement!=null) {
	//if (srcElement!=null) {
		CallBackNow("Show^COMGridEdit31JRightClick",srcElement.id+','+srcElement.parentNode._Form+','+srcElement.innerText);		//SR17253	
		if (Columns.document.body.children.length>0) {
			Columns.show(event.screenX,event.screenY,200,0);
			window.setTimeout('Columns.show('+event.screenX+','+event.screenY+',200,Columns.document.body.children[0].offsetHeight);',50);
		}
		event.returnvalue=false;
		event.cancelBubble=false;
	}
	return false;
}
 
 
 	//-->
	//</script>
	>
	quit
	*/
	
]]></Routine>
</Export>