<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARTCIAndar" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARTCIAndar

#include COMSYS
#include VARConst
#include VARLog

	quit
	
	
ProcessarAlteracoesAndar(pYKEY)
	;do ProcessarAlteracoesAndar^VARTCIAndar(pYKEY)
	quit:(pYKEY="")
	
	new strStatus
	
	set strStatus = $$GravarAutoria(pYKEY)
	
	set:$$$ISOK(strStatus) strStatus = $$IsBloqueadoDesabilitado(pYKEY)
			
	if $$$ISERR(strStatus) {
		$$$Alert("Erro ao gravar dados das alterações do registro.")
		$$$VARLOGERROR("Erro ao gravar dados das alterações do registro.")
	}
	else {
		$$$Alert("Alterações aplicadas com sucesso!")	
	}
		
	do ReloadForm^COMUtilForm()
	
	quit
	
	
PodeProcessarAlteracoesAndar(pYKEY)
	;do PodeProcessarAlteracoesAndar^VARTCIAndar(pYKEY)
	quit:(pYKEY="")
	
	new strStatus,idLocal,idModulo,idModulo,idRua,idPredio,idAndar,objAndar
	
	set strStatus = $$$OK
	
	;Separa a chave em seus respectivos valores
	set idLocal  =	$$$KEY1(pYKEY)
	set idModulo =	$$$KEY2(pYKEY)
	set idRua	 =	$$$KEY3(pYKEY)
	set idPredio =	$$$KEY4(pYKEY)
	set idAndar  =  $$$KEY5(pYKEY)
	
	if ((idLocal="") || (idModulo="") || (idRua="") || (idPredio="") || (idAndar = "")) {
		set strStatus = "Dados incompletos"
	}
	
	if $$$ISOK(strStatus) {		
		set objAndar = $get(^VARTCIAndar(YM,idLocal,idModulo,idRua,idPredio,idAndar,1))		
		if (objAndar = ""){
			set strStatus = "Registro não encontrado em VARTCIPredio"
		}			
	}
	
	$$$YQHandler(strStatus)
	 
	quit


GetStatusAndar(pLocal,pModulo,pRua,pPredio,pAndar)
	;-------------------------------------------------------------------------------
	; Retorna o status bloqueado, desabilitado ou ambos para o endereço informado, 
	; sendo que para cada um fora designado os seguintes números. 
	; 
	; strStatus: 1- Desbloqueado e habilitado
	;			 2-	Somente bloqueado
	;			 3-	Somente desabilitado
	;			 4- Bloqueado e desabilitado
	;            
	; History:
	; 18-Apr-2011	SLV		Criado
	;-------------------------------------------------------------------------------
	quit:((pLocal="") || (pModulo="") || (pRua="") || (pPredio="") || (pAndar = ""))
	
	new idStatus,objAndar,stDesabilitado,stBloqueado,stExcluido
	
	set idStatus 	= 1 // Sem restrições
	set objAndar	= $get(^VARTCIAndar(YM,pLocal,pModulo,pRua,pPredio,pAndar,1))
	
	set stDesabilitado = $$$VARTCIAndarAndarDesabilitado(objAndar)
	set stBloqueado	   = $$$VARTCIAndarAndarBloqueado(objAndar)
	set stExcluido	   = $$$VARTCIAndarAndarExcluido(objAndar)
	
	if (stExcluido '= "") {
		set idStatus = 5
	}
	elseif ((stDesabilitado '= "") && (stBloqueado '= "")){
		set idStatus = 4	//Desabilitado e bloqueado
	}
	elseif (stBloqueado '= "") {
		set idStatus = 2	//Somente bloqueado
	}
	elseif (stDesabilitado '= "") {
		set idStatus = 3	//Somente desabilitado
	}
	
	quit idStatus	
	
	
IsBloqueadoDesabilitado(pYKEY)
	;-------------------------------------------------------------------------------
	; Reproduz o Status "Bloqueado" e "Desabilitado" para as classes Filhas           
	; 
	;
	; History:
	; 18-Apr-2011	SLV		Criado
	;-------------------------------------------------------------------------------
	quit:(pYKEY="")
	
	new strStatus,idStatus,idLocal,idModulo,idRua,idPredio,idAndar,idApartamento,
		objApartamento,idKeyAp

	set strStatus = $$$OK

	set idLocal  =	$$$KEY1(pYKEY)
	set idModulo =	$$$KEY2(pYKEY)
	set idRua	 =	$$$KEY3(pYKEY)
	set idPredio =	$$$KEY4(pYKEY)
	set idAndar  =  $$$KEY5(pYKEY)
	
	set idStatus = $$GetStatusAndar(idLocal,idModulo,idRua,idPredio,idAndar)
	
	if $$$ISOK(idStatus) {
		$$$Order7(^VARTCIApartamento,YM,idLocal,idModulo,idRua,idPredio,idAndar,idApartamento)
			set idKeyAp = idLocal_$$$COMMA_idModulo_$$$COMMA_idRua_$$$COMMA_idPredio_$$$COMMA_idAndar_$$$COMMA_idApartamento
			set objApartamento = $get(^VARTCIApartamento(YM,idLocal,idModulo,idRua,idPredio,idAndar,idApartamento,1))
			if (idStatus = 2) { 	 //Somente bloqueado
				set $$$VARTCIApartamentoAptoBloqueado(objApartamento)		= 1
				set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= ""		
			}
			elseif (idStatus = 3) { //Somente desabilitado
				set $$$VARTCIApartamentoAptoBloqueado(objApartamento)		= ""
				set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= 1
			}
			elseif (idStatus = 4) { //Desabilitado e bloqueado
				set $$$VARTCIApartamentoAptoBloqueado(objApartamento) 	 	= 1
				set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= 1
			}
			elseif (idStatus = 5) { //Excluido
				set $$$VARTCIApartamentoAptoBloqueado(objApartamento) 	 	= 1
				set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= 1
				set $$$VARTCIApartamentoAptoExcluido(objApartamento)		= 1
			}
			else { //Liberado
				set $$$VARTCIApartamentoAptoBloqueado(objApartamento) 	 	= ""
				set $$$VARTCIApartamentoAptoDesabilitado(objApartamento)  	= ""
			}
			set strStatus = $$$Save("VARTCIApartamento",idKeyAp,objApartamento,$$$YES)
			set:$$$ISOK(strStatus) strStatus = $$GravarAutoria^VARTCIApartamento(idKeyAp)
			
		$$$EndErr(strStatus)	; Apartamento
	}

	quit strStatus
	

GravarAutoria(pYKEY)
	quit:(pYKEY="")
	
	new strStatus,idStatus,idLocal,idModulo,idRua,idPredio,idAndar,objAndar
	
	set:(YBED="") YBED = "UNKNOWN"
	
	set idLocal  = $$$KEY1(pYKEY)
	set idModulo = $$$KEY2(pYKEY)
	set idRua	 = $$$KEY3(pYKEY)
	set idPredio = $$$KEY4(pYKEY)
	set idAndar	 = $$$KEY5(pYKEY)
	
	set strStatus = $$$OK
	
	set objAndar = $get(^VARTCIAndar(YM,idLocal,idModulo,idRua,idPredio,idAndar,1))
	
	if (objAndar '=""){
		set $$$VARTCIAndarAlteracoesAplicadasEm(objAndar)  = $Horolog
		set $$$VARTCIAndarAlteracoesAplicadasPor(objAndar) = YBED			
		set strStatus=$$$Save("VARTCIAndar",pYKEY,objAndar,$$$YES)
	}
					
	if $$$ISERR(strStatus) $$$VARLOGERROR("Erro ao gravar dados de autoria do registro.")
	
	quit strStatus
		

OnBeforeSave(pYKEY)
	quit:(pYKEY="")
	
	new idLocal,idModulo,idModulo,idRua,idPredio,idAndar,blnAndar
	
	set idLocal  =	$$$KEY1(pYKEY)
	set idModulo =	$$$KEY2(pYKEY)
	set idRua	 =	$$$KEY3(pYKEY)
	set idPredio =	$$$KEY4(pYKEY)
	set idAndar  =  $$$KEY5(pYKEY)
	
	set blnAndar = $data(^VARTCIAndar(YM,idLocal,idModulo,idRua,idPredio,idAndar,1))
	
	if ('blnAndar) {
		$$$Alert("Registro inexistente!")
		set $$$KEY5(pYKEY) = ""
		do GoToForm^COMUtils("VARTCIAndar",pYKEY)
	}

	quit		
]]></Routine>
</Export>