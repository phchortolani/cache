<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewColumnGroup" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
    ;-------------------------------------------------------------------------------
    ; COMViewColumnGroup - Controls The selection of groups from columns.
    ; 
    ; Note. this routine is not in use yet!!!	<--- NOTE
    ;-------------------------------------------------------------------------------
	#include COMConst
	#include COMSYS
 
 
GetGroups(pintLevel=0,pblnAll=0)
	new lstFields,idClass,idView,idColumn,idGroup,objColumn,arrFields
	
	set lstFields=""
	set idClass=$get(^CacheTempView(YUSER,"Class"))	
	set idView=$$GetCurrentView^COMView(idClass)
	set idColumn=""
	for {
		set idColumn=$order(^COMViewColumnUser(0,idClass,idView,YBED,idColumn))
		quit:idColumn=""
		set objColumn=$get(^COMViewColumnUser(0,idClass,idView,YBED,idColumn))
		if ($$$COMViewColumnUserGroup1(objColumn)>pintLevel)&&('$$$COMViewColumnUserExpandGroup(objColumn)||pblnAll) {
			set arrFields($$$COMViewColumnUserGroup1(objColumn),idColumn)=""
		}
	}
	
	set idGroup=""
	for {
		set idGroup=$order(arrFields(idGroup))
		quit:idGroup=""
		set idColumn=""
		for {
			set idColumn=$order(arrFields(idGroup,idColumn))
			quit:idColumn=""
			set lstFields=lstFields_$listbuild(idColumn)
		}
	}
	quit lstFields
 
AddGroup(pidField,pintLevel="")
	new idClass,idView,lstGroups,objColumn
	
	set idClass=$get(^CacheTempView(YUSER,"Class"))	
	set idView=$$GetCurrentView^COMView(idClass)
 
	set lstGroups=$$GetGroups(0,1)
	if pintLevel="" {
		set objColumn=$get(^COMViewColumnUser(0,idClass,idView,YBED,pidField))
		set $$$COMViewColumnUserGroup1(objColumn)=$listlength(lstGroups)+1
		set ^COMViewColumnUser(0,idClass,idView,YBED,pidField)=objColumn
	}
	do DisplayHeader^COMViewFilterColumn()
	do DisplayGrid^COMViewFilter()
	
	quit
	
 
ExpandGroup(pidField,pblnExpand)
	new idClass,idView,objColumn
	set idClass=$get(^CacheTempView(YUSER,"Class"))	
	set idView=$$GetCurrentView^COMView(idClass)
	set objColumn=$get(^COMViewColumnUser(0,idClass,idView,YBED,pidField))
	set $$$COMViewColumnUserExpandGroup(objColumn)=pblnExpand
	set ^COMViewColumnUser(0,idClass,idView,YBED,pidField)=objColumn
	do DisplayGrid^COMViewFilter()
	quit
 
RemoveGroup(pidField)
	new idClass,idView,objColumn,intLevel,lstGroups,LevelLoop,idField
	
	set idClass=$get(^CacheTempView(YUSER,"Class"))	
	set idView=$$GetCurrentView^COMView(idClass)
	set objColumn=$get(^COMViewColumnUser(0,idClass,idView,YBED,pidField))
    set intLevel=$$$COMViewColumnUserGroup1(objColumn)
	set $$$COMViewColumnUserGroup1(objColumn)=""
	set ^COMViewColumnUser(0,idClass,idView,YBED,pidField)=objColumn    
    set lstGroups=$$GetGroups(intLevel,1)
    for LevelLoop=1:1:$listlength(lstGroups) {
	    set idField=$listget(lstGroups,LevelLoop)
	    set objColumn=$get(^COMViewColumnUser(0,idClass,idView,YBED,idField))
	    set $$$COMViewColumnUserGroup1(objColumn)=$$$COMViewColumnUserGroup1(objColumn)-1
	    set ^COMViewColumnUser(0,idClass,idView,YBED,idField)=objColumn
    }
   	do DisplayHeader^COMViewFilterColumn()
	do DisplayGrid^COMViewFilter()
    quit
	
	
]]></Routine>
</Export>