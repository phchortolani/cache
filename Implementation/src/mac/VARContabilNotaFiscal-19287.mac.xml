<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContabilNotaFiscal" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContabilNotaFiscal
	#include INConst
	#include WWWConst
	quit
	
OnAfterSave
	new Local, Grupo, Mes, Ano
	
	kill ^VARTempContabilNotaFiscal(YM,YBED)
	
	set Local 	= $get(VORG(1))
	set Grupo 	= $get(VORG(2))
	set Mes 	= $get(VORG(3))
	set Ano		= $get(VORG(4))
	
	set $piece(^VARTempContabilNotaFiscal(YM,YBED,1),Y,1) = Local
	set $piece(^VARTempContabilNotaFiscal(YM,YBED,1),Y,2) = Grupo
	set $piece(^VARTempContabilNotaFiscal(YM,YBED,1),Y,3) = Mes
	set $piece(^VARTempContabilNotaFiscal(YM,YBED,1),Y,4) = Ano
	
	if ((Mes = "")||(Ano = "")){
		$$$Alert("Preencha todos os campos obrigatórios.")
		do GoToForm^COMUtilForm("VARContabilNotaFiscal","",,,,,)
		quit
	} elseif ($$CheckFechamentoContabil^VAREntradaNotaFiscal(Mes,Ano) = $$$NO){
		$$$Alert("Este relatório está disponível somente para os meses/anos que já foi gerado o fechamento contábil.")
		do GoToForm^COMUtilForm("VARContabilNotaFiscal","",,,,,)
		quit
	}
	
	do RunReportContabilNotaFiscal^VARJasperRunReport(Local,Grupo,Mes,Ano)
	
	quit
	
OnBeforeButtonLine(pYFELD)	
	do LoadListaLocaisDeEstoque
	quit
 	
LoadListaLocaisDeEstoque
	new CodLocal, NomeLocal, objLista, objUsuario, perfilUsuario,
		locaisPermitidos
	
	set objLista = ""
	
	set objUsuario = $get(^WWW013(YM,YBED,1))		
    set perfilUsuario = $$$WWW013UserAccess(objUsuario)
	set locaisPermitidos = $$$WWW013AllowedLocations(objUsuario)
	
 	kill ^COMTempList(YM,YUSER,"VARLocais")
 
	&sql(
		DECLARE CursorLocais CURSOR FOR 
		SELECT Location, LocationName
		INTO :CodLocal, :NomeLocal
		FROM WWW0121
 		WHERE COMPANY = :YM
 		AND StorageLocn = 1
 		ORDER BY to_number(Location) )
 		
	
	&sql(OPEN CursorLocais)
    IF (SQLCODE) {
		QUIT
    }
    
   	FOR { &sql(FETCH CursorLocais)
		QUIT:SQLCODE
		
		; Caso o usuário não seja administrador e o local não esteja na
		; lista de locais permitidos ao usuário, não popula
		if ((perfilUsuario '= 1) &&
			('$find(";"_locaisPermitidos_";", ";"_CodLocal_";"))) {
			continue
		}

		set $piece(objLista, Y, 1) = NomeLocal
		set ^COMTempList(YM,YUSER,"VARLocais",CodLocal,1)=objLista
   	}
 
   	&sql(CLOSE CursorLocais)
 
	quit
]]></Routine>
</Export>