<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPInvChargeSplit" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPInvChargeSplit
	
#include COMSYS
#include FINConst
#include COMConst
	
OnBeforeButtonLine(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Disable grid if not editable
    ;
    ; Returns:
    ;
    ; History:
    ; 01-Mar-2005	JW		SR11405: Created
    ;-------------------------------------------------------------------------------
	new idInvoice,objInvoice
	
	set idInvoice = $piece($get(pYKEY),",",1)
	if idInvoice'="" {
		set objInvoice = $get(^FINAPInv(YM,idInvoice,1))
		if '$$Editable^FINSYSReview("FINAPInv",$$$FINAPInvStatus(objInvoice)) {
			set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
		}
	}	
	quit
 
 
OnAfterDataFields(pYKEY,pYFELD)
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Jun-2005	JW		Removed call to GetKeys
    ; 25-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new idInvoice,idCharge,objInvoice
	
	do LoadGrid(pYKEY)
	
	if $get(pYKEY)'="" {
		;do GetKeys(pYKEY,.idInvoice,.idCharge)
		set idInvoice = $piece(pYKEY,",",1)
		set idCharge = $piece(pYKEY,",",2)
		
		if idInvoice'="" {
			do ShowCharges(idInvoice,idCharge)
		}
	}
	
	quit
	
 
LoadGrid(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Load the FINAPInvChargeDivision grid.
    ;
    ; Returns:
    ;
    ; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
    ; 22-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
	
	set YFORM="FINAPInvChargeDivision"
	set YFELD=""
	set YKEY=$get(pYKEY)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 200
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterClearFields(YAUSWAHL)   = "1,2;2,1"
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "FINAPInvChargeSplit"
	do ^COMGridEdit31
	
	quit
 
 
ShowCharges(pidInvoice,pidCharge="")
    ;-------------------------------------------------------------------------------
    ; Show Invoice Charges
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new idLine,objLine,lstHeader,accountGroup,strForm,description
 	
 	set strForm = "FINAPInvChargeSplit"
 	
	set lstHeader=$listbuild($$$StrFINAPInvChargeLine,
							$$$StrFINAPInvChargeReference,
							$$$StrFINAPInvChargeAccountGroup,
							$$$StrFINAPInvChargeAmount,
							$$$Text("Fin00737"))  ;Apportioned?
	
	do Start^COMTable(lstHeader,$$$Text("Fin00688"))   ;Charges
 	
	if (pidInvoice'="") {
		set idLine=""
		for {
			set idLine=$order(^FINAPInvCharge(YM,pidInvoice,idLine))
			quit:idLine=""
			
			do NewLine^COMTable($select(idLine=pidCharge:"lightgreen",1:""))
			set objLine=$get(^FINAPInvCharge(YM,pidInvoice,idLine,1))
			set accountGroup = $$$FINAPInvChargeAccountGroup(objLine)
			
			if accountGroup'="" {
				set description = $$$FINSYSAccountGroupDescription($get(^FINSYSAccountGroup(YM,accountGroup,1)))
				if description'="" set accountGroup=accountGroup_" - "_description
				
				do InsertCell^COMTable(idLine,strForm,pidInvoice_","_idLine)
				do InsertCell^COMTable($$$FINAPInvChargeReference(objLine),strForm,pidInvoice_","_idLine)
				do InsertCell^COMTable(accountGroup,strForm,pidInvoice_","_idLine)
				do InsertCurrencyCell^COMTable($$$FINAPInvChargeAmountTaxEx(objLine),$$$NO,strForm,pidInvoice_","_idLine)
				do InsertCellBoolean^COMTable($$IsComplete^FINAPInvCharge(pidInvoice,idLine),strForm,pidInvoice_","_idLine,,"center")
				do EndLine^COMTable()
			}
		}
	}
	do Stop^COMTable()
	
	quit
	
 
OnBeforeSave(pYKEY,pYFELD)
    ;-------------------------------------------------------------------------------
    ; Get the grid to save its contents
	;
    ; Returns:
    ; 
    ; History:
    ; 24-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$GRIDSave(pYKEY)
	
	if $$$ISOK(strStatus) {
		set Q=$$$QSave
	} else {
		set Q=$$$QDontSave
	}
	quit	
	
	
	
 
	
	
	
]]></Routine>
</Export>