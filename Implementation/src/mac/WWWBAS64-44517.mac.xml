<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWBAS64" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWBAS64(SRC,DES,P,TYP) ;DATENUMSETZUNG ASCII-BASE64 UND ZURUECK [ 04/05/00  9:09 AM ] ;And to the   ; Compiled February 9, 2005 13:39:56
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		And to the   
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| 	 Compiled February 9, 2005 13:39:56
	;|
	;\------------------------------------------------------------------/
	;
	;SRC = QUELLGLOBAL(KNOTEN) Z.B. "^TEMP(KNOTEN1,KNOTEN2)"
	;DES =  ZIELGLOBAL(KNOTEN)
	;P   = BLOCKLAENGE: <0: ASCII->BAS64, ZIELBLOCKLAENGE FEST, SIEHE TYP
	;                   >0: BAS64->ASCII, ZIELBLOCKLAENGE FEST
	;                   =0: BAS64->ASCII, ZIELBLOCKLAENGE VARIA, SIEHE TYP
	;TYP = DATENTYP  "BIN": KNOTEN WERDEN ENDLOS ANEINANDERGEREIHT
	;                "TXT": TRENNZEICHEN IST CRLF (ERZEUGT)
	;                       ODER CRLF ODER CR ODER LF (GELESEN) ;Or Or Or 
	;                "GLB": SATZLAENGE WIRD ERZEUGT/GELESEN (LAENGE~SATZ)
	NEW CR,DE2,I,I1,I2,I3,I4,L,K,M,PTR,PTW,SR2,V,X 
	SET CR=""
	SET M="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"
	SET:$GET(TYP)="" TYP="BIN" 
	SET:'$DATA(P) P=-64
	SET:$EXTRACT(SRC,$LENGTH(SRC))'=")" SRC=SRC_"()"
	SET:$EXTRACT(DES,$LENGTH(DES))'=")" DES=DES_"()"
	SET $EXTRACT(SRC,$LENGTH(SRC))=$SELECT($EXTRACT(SRC,$LENGTH(SRC)-1)="(":"PTR)",1:",PTR)")
	SET $EXTRACT(DES,$LENGTH(DES))=$SELECT($EXTRACT(DES,$LENGTH(DES)-1)="(":"PTW)",1:",PTW)")
	SET PTR="",PTR=$ORDER(@SRC) 
	QUIT:PTR=""
	SET PTW="",PTW=+$ORDER(@DES,-1)
	SET SR2=SRC,DE2=DES
	SET:$DATA(@SR2)\10 $EXTRACT(SR2,$LENGTH(SR2))=",1)",$EXTRACT(DE2,$LENGTH(DE2))=",1)"
	IF P<0 SET P=-P DO  QUIT
	. SET (X,K,PTR)="" 
	. FOR  SET PTR=$ORDER(@SRC) QUIT:PTR=""  DO
	. . SET K=K_$SELECT(TYP="BIN":"",TYP="GLB":$LENGTH(@SR2)_"~",1:"")_@SR2_$SELECT(TYP="TXT":$CHAR(13,10),1:"")
	. . FOR I=1:3:$LENGTH(K)-2 SET X=X_$EXTRACT(M,$ASCII(K,I)\4+1)_$EXTRACT(M,$ASCII(K,I)#4*16+($ASCII(K,I+1)\16)+1)_$EXTRACT(M,$ASCII(K,I+1)#16*4+($ASCII(K,I+2)\64)+1)_$EXTRACT(M,$ASCII(K,I+2)#64+1)
	. . SET $EXTRACT(K,1,$LENGTH(K)\3*3)=""
	. . QUIT
	. IF $LENGTH(K) SET K=K_$CHAR(0),X=X_$EXTRACT(M,$ASCII(K,1)\4+1)_$EXTRACT(M,$ASCII(K,1)#4*16+($ASCII(K,2)\16)+1) SET:$LENGTH(K)>2 X=X_$EXTRACT(M,$ASCII(K,2)#16*4+($ASCII(K,3)\64)+1)
	. FOR  QUIT:'$LENGTH(X)  SET PTW=PTW+1,@DE2=$EXTRACT(X,1,P),$EXTRACT(X,1,P)=""
	. QUIT
	SET (K,X)=""
	SET PTR="" 
	FOR  SET PTR=$ORDER(@SRC) QUIT:PTR=""  SET K=K_@SR2 DO
	. FOR I=1:4:$LENGTH(K)-3 SET I1=$FIND(M,$EXTRACT(K,I))-2,I2=$FIND(M,$EXTRACT(K,I+1))-2,I3=$FIND(M,$EXTRACT(K,I+2))-2,I4=$FIND(M,$EXTRACT(K,I+3))-2 SET X=X_$CHAR(I1*4+(I2\16),I2#16*16+(I3\4),I3#4*64+I4)
	. SET $EXTRACT(K,1,$LENGTH(K)\4*4)="" 
	. FOR  QUIT:'$$WRI()
	. QUIT
	IF $LENGTH(K) SET K=K_$CHAR(0,0) S I1=$FIND(M,$EXTRACT(K,1))-2 S I2=$FIND(M,$EXTRACT(K,2))-2 S I3=$FIND(M,$EXTRACT(K,3))-2 SET X=X_$CHAR(I1*4+(I2\16)) SET:$LENGTH(K)>4 X=X_$CHAR(I2#16*16+(I3\4))
	SET:P P=$LENGTH(X) 
	SET I=$$WRI()
	QUIT
	
WRI() ;
	IF P QUIT:$LENGTH(X)<P 0 SET PTW=PTW+1 S @DE2=$EXTRACT(X,1,P) S $EXTRACT(X,1,P)="" QUIT 1
	IF TYP="TXT" DO:CR=""  QUIT:CR="" 0 QUIT:$LENGTH(X,CR)=1 0 SET V=$FIND(X,CR) S PTW=PTW+1 S @DE2=$EXTRACT(X,1,V-$LENGTH(CR)-1),$EXTRACT(X,1,V-1)="" QUIT 1
	. DO TYP
	. QUIT
	IF TYP="GLB" QUIT:$LENGTH(X,"~")=1 0 SET V=$FIND(X,"~") QUIT:$LENGTH(X)<($PIECE(X,"~",1)+V-1) 0 SET PTW=PTW+1,@DE2=$EXTRACT(X,V,V+$PIECE(X,"~",1)-1),$EXTRACT(X,1,V+$PIECE(X,"~",1)-1)="" QUIT 1
	QUIT 0
	
TYP ;
	NEW I
	SET I=$FIND(X,$CHAR(13,10)) 
	IF I SET CR=$CHAR(13,10) QUIT
	SET I=$FIND(X,$CHAR(13)) 
	IF I,I'>$LENGTH(X) SET CR=$CHAR(13) QUIT
	SET I=$FIND(X,$CHAR(10)) 
	IF I,I'>$LENGTH(X) SET CR=$CHAR(10) QUIT
	QUIT
]]></Routine>
</Export>