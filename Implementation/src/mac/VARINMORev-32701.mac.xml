<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINMORev" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINMORev
#include INConst
#include VARLog

	quit

OnAfterSaveHook(pidRev,objRev)

	if ((pidRev = "") || (objRev = "")) {
		quit $$$OK
	}
		
	new strStatus,idRevLine,objRevLine,objIMOLine,fltRevQty
	
	if ('$$$INMORevStatus(objRev)) quit $$$OK //Early quit: Not processed
	
	set strStatus = $$$OK
	
	set idRevLine = ""
	for {
		set idRevLine = $order(^INMORevLine(0,pidRev,idRevLine))
		if ((idRevLine="") || ('strStatus)) quit
		
		set objRevLine = $get(^INMORevLine(0,pidRev,idRevLine,1))
		
		if (objRevLine '= "") {
			set objIMOLine = $get(^INMOLine(0,pidRev,idRevLine,1))
		
			set fltRevQty  = +$$$INMORevLineReversedQuantity(objRevLine)
			if (fltRevQty > 0) {
				set $$$INMOLineQtyReversed(objIMOLine) = fltRevQty
				set strStatus = $$$Save("INMOLine",pidRev_","_idRevLine,objIMOLine,$$$YES)
				if $$$ISERR(strStatus) {
					$$$VARLOGERROR("Registro INMOLine "_pidRev_","_idRevLine_" nÃ£o foi salvo com sucesso: "_strStatus)
				}
			}
		}
	}
	
	quit strStatus
]]></Routine>
</Export>