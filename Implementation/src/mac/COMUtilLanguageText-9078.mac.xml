<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilLanguageText" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilLanguageText
	;-------------------------------------------------------------------------------
	; Load LanguageText.txt to replace WWW009 for PT
	; 
	; History:
	; 21-Nov-2008	CRH		SR15939: Created
	;-------------------------------------------------------------------------------
#include %occInclude
#include COMSYS
#include WWWConst
#include COMConst
#Define Tab $char(9)
#Define DoTheSave 1
#Define TraceLevel 2
	; 0=No trace; 1=Control; 2=Detail; 3=Verbose
#Define IFTrace(%Level)    if (($$$TraceLevel) >= %Level)
#Define Trace(%Level,%Msg) if (($$$TraceLevel) >= %Level) {w $zdt($h),$$$Tab,%Msg,!}
 
Start(pstrFile="\\orion\temp\ufh\LanguageText.txt") public
	new strStatus
 
	do ^WWWVAR				; initialise @NM variables
	
	$$$Trace(1,"# COMUtilLanguageText version 19-Nov-2008")
	
	;***************************************
	tstart
	;***************************************
	do $ZUTIL(68,40,1)   	; end of file handling
	
	$$$Trace(1,"# Loading Phrases from "_pstrFile)
	set strStatus=$$ProcessFile(pstrFile)
 
	;***************************************
	if $$$ISERR(strStatus) {
		$$$Trace(0,"# Completed with ERROR.")
		write !,"ERROR: ",strStatus,!
		write "ROLLBACK",!
		trollback
	}
	else {
		$$$Trace(0,"# Completed Successfully.")
		write !,"Completed Successfully.",!
		write "Test, then TCOMMIT or TROLLBACK",!
	}
	;***************************************
 
	quit strStatus
 
	;-------------------------------------------------------------------------------
	; ProcessFile
	; Load text file and replace WWW009 
	; Note:
	; 1. Text file expected has Text Number <tab> PT <tab> EN
	;-------------------------------------------------------------------------------
ProcessFile(pstrFile) private
	new strStatus,intLine,strLine,strX
	new strTextNumber,strPT,strEN
 
	set strStatus=$$$OK
	open pstrFile:"R":10 else  set strStatus="Can not open file "_pstrFile quit strStatus
	for intLine=1:1 {
		quit:$zeof
		quit:strStatus'=$$$OK
		continue:(intLine<=2)	// Ignore the first line as it contain the column headings
								// 1:1 gives 2 in the first pass (believe it or not)
		use pstrFile
		read strLine
		use 0
		quit:$zstrip(strLine,"<>W")=""
		
		set strTextNumber=$zstrip($piece(strLine,$$$Tab,1),"<>W")
		set strPT=$zstrip($piece(strLine,$$$Tab,2),"<>W")
		set strEN=$zstrip($piece(strLine,$$$Tab,3),"<>W")
		// Some strings have annoying enclosing quotes, and double quotes inside
		// Strip by executing a set on the command line
		if (($extract(strTextNumber,1,1) = $c(34)) && ($extract(strTextNumber,$length(strTextNumber)) = $c(34))) {
			set strX = "set strTextNumber = " _ strTextNumber
			x strX
		}
		if (($extract(strPT,1,1) = $c(34)) && ($extract(strPT,$length(strPT)) = $c(34))) {
			set strX = "set strPT = " _ strPT
			x strX
		}
		if (($extract(strEN,1,1) = $c(34)) && ($extract(strEN,$length(strEN)) = $c(34))) {
			set strX = "set strEN = " _ strEN
			x strX
		}
		
		set strStatus=$$LanguageText(strTextNumber,"PT",strPT)
		quit:strStatus'=$$$OK
		set strStatus=$$LanguageText(strTextNumber,"EN",strEN)
		quit:strStatus'=$$$OK
	} 
	close pstrFile
	$$$Trace(2,"Loaded "_(intLine - 1)_" phrases")
	quit strStatus
 
 
 	;-------------------------------------------------------------------------------
 	; LanguageText
 	; Replace www009 PT text
 	;-------------------------------------------------------------------------------
LanguageText(pstrTextNumber,pLanguage1,pStr) private
	new strStatus,strKey
	new objText
	new strText
	
	set strStatus=$$$OK
	set objText = $get(^WWW009(0,pLanguage1,pstrTextNumber,1))
	if (objText="") {
		// TextNumber not found
		$$$Trace(2,"  Not found"_$$$Tab_pstrTextNumber_$$$Tab_pLanguage1)
		quit strStatus
	}
	set strText	= $$$WWW009Text(objText)
	
	if (strText '= pStr) {
		$$$Trace(2,"  Replace"_$$$Tab_pstrTextNumber_$$$Tab_pLanguage1_$$$Tab_strText_$$$Tab_pStr)
	 
		if ($$$DoTheSave) {
			set $$$WWW009Text(objText) = pStr
			set strKey = pLanguage1_","_pstrTextNumber			
			set strStatus=$$$Save("WWW009",strKey,objText,1)
		}
	}
			 
	quit strStatus
 
]]></Routine>
</Export>