<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilLocale" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilLocale
    ;-------------------------------------------------------------------------------
    ; 	Routines relating to Locality-based settings
    ;-------------------------------------------------------------------------------
#include WWWConst
#include COMSYS
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMUtilLocale("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;+++++++++++++++++++++++++++++++++++++++
	;  ^WWW100(0,"FELDFORMAT",Language,Type,1)  over-ridden by
	;  ^WWW101(0,"FELDFORMAT",Language,Type,1)  over-ridden by   <<< This should be deprecated
	;  ^INPARA(0,"FELDFORMAT",Language,Type,1)
	;  
	;     Type
	;		0		""
	;		1		"DD/MM/YYYY"		Date
	;		2		"X"
	;		3		"XXXXXXXXXX"
	;		4		"NNNN.NN"
	;		5		""
	;		6		"XXXXXXXXXX"
	;		7		"NN:NN"				Time
	;		8		"NN,NNN.NN"			Thousands/Decimals
	;		9		"NNNN"
	;+++++++++++++++++++++++++++++++++++++++
	
GetNumberDelimiters(pidLanguage)
	;-------------------------------------------------------------------------------
	; Common code to identify delimiters for thousands and decimal place
	; either Comma-Dot (English) or Dot-Comma (European)
	; 
	; Called By :	^INWEAUFD4, Initialise^WWWEVENT, AdjustCurrencyAmount^WWWEVENT,
	;				Convert^WWWTR, ^WWWVORG, ^WWWZAHL
	; 
	; History:
	; 20-Jul-2011	shobby	SR17807: During an upgrade it is possible that INPARA is not
	;  								 compiled yet. Unfortunate situation with a COM routine
	;								 calling a IN routine.
	; 05-Sep-2007	GRF		SR13729: Need to pass Language through to INPARA rather
	; 							than relying on SPRACHE.
	; 20-Aug-2007	GM		SR13729: Change "FELDFORMAT" parameter to $$GetFormat^INPARA()
	; 23-May-2007	GRF		SR15525: created
	;-------------------------------------------------------------------------------
	new strDelim
	
	$$$LogR("GetNumberDelimiters",pidLanguage)
	
	if pidLanguage'="" {
		;SR17807 set strDelim = $$GetFormat^INPARA(8,".,",pidLanguage)	; 17807
		set strDelim = $$GetFormat^WWW100(8,".,",pidLanguage)	; 17807
		set strDelim = $translate(strDelim,"nNxX")
	}
	
	if $length($get(strDelim))'=2 {
    ;	European override at system level, Default to English format
		set strDelim = $select($$$WWW012DecimalSigns($get(^WWW012(0,0,1)))=",":".,",1:",.")
	}
	
	quit strDelim
	
	
GetDateFormat(&pstrFormat,&pstrDelim,pidLanguage="")
	;-------------------------------------------------------------------------------
	;	Common code to identify date format
	;	based on user's (or nominated) language as Locale identifier
	; 
	;     e.g. DD.MM.YYYY   or   MM/DD/YYYY   or variation
	; 
	; Japanese YYYYMMDD - delimiter = ""   Okay here - check where used
	; 
	; Inputs:
	;	pidLanguage		Optional Language code - defaults to SPRACHE
	; 
	; Returns (By Ref):
	;	pstrFormat		DD.MM.YYYY
	;	pstrDelim		Delimiter from format (usually . / - or ,)
	;					                      Could be null for YYYYMMDD
	;	
	; 
	; History:
 	; 09-Aug-11	shobby	SR17807:	GetFormat has moved.
 	; 07-May-2009	GRF		SR16522: Pass override language to INPARA call
	; 20-Aug-2007	GM		SR13729: Change "FELDFORMAT" parameter to $$GetFormat^INPARA()
	; 13-Jun-2007	GRF		SR15525: created
	;-------------------------------------------------------------------------------
	set:pidLanguage="" pidLanguage=$get(SPRACHE,"EN")
	
	set pstrDelim  = ""
	;SR17807 set pstrFormat = $$GetFormat^INPARA(1,"DD.MM.YYYY",pidLanguage)
	set pstrFormat = $$GetFormat^WWW100(1,"DD.MM.YYYY",pidLanguage) ;SR17807 
	
 
	set pstrFormat = $translate(pstrFormat,"AJT","YYD")            ; TT.MM.JJJJ or DD.MM.AAAA => DD.MM.YYYY
	
	if $length(pstrFormat)=10 {                                    ; since fails YYYYMMDD
		set pstrDelim = $extract(pstrFormat,3)
		if pstrDelim="Y" set pstrDelim = $extract(pstrFormat,5)    ; YYYY.MM.DD?
	}
	if pstrFormat="" set pstrDelim = "."
	
	quit
	
]]></Routine>
</Export>