<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPInvCharge2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPInvCharge2
	
#include COMSYS
#include FINConst
 
#define FORM	"FINAPInvCharge2"
	
#define LogR(%1,%2)		;
#define LogRx(%1)		;
#;define LogR(%1,%2)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1_"^FINAPInvCharge2("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
OnAfterDelete(YKEY,YFORM,YFELD)
    ;-------------------------------------------------------------------------------
    ;
    ; Returns:
    ;
    ; History:
    ; 05-Apr-2005	shobby	Code to run after a charge line has been deleted.
    ;-------------------------------------------------------------------------------
	set YKEY = $$$KEY1(YKEY)_","_$$$FINAPInvChargeParent(YFELD)
	
	do UpdateTotals(YKEY,YFELD)
	quit
 
 
GetFactor(YFELD)
    ;-------------------------------------------------------------------------------
    ; Determine the discount factor if possible - for rate/percentage/amount
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jul-2005	shobby	Doesn't compile TODO (Property Removed?)
    ; 06-Apr-2005	shobby	Created SR:11983
    ;-------------------------------------------------------------------------------
	new fltFactor
	
	set fltFactor=0
	quit fltFactor
 
	;new objINDISCOUNT,idINDISCOUNT,fltFactor
	;set fltFactor=$$$FINAPInvChargeFactor(YFELD)
	;set idINDISCOUNT=$$$FINAPInvChargeType(YFELD)
	;if idINDISCOUNT'="" {
	;	set objINDISCOUNT=$get(^INDISCOUNT(YM,idINDISCOUNT,1))
	;	if objINDISCOUNT'="" {
	;		if $$$INDISCOUNTMinimumAmount(objINDISCOUNT)=$$$INDISCOUNTMaximumAmount(objINDISCOUNT) {
	;			set fltFactor=$$$INDISCOUNTMinimumAmount(objINDISCOUNT)
	;		}
	;	}
	;}
	;quit fltFactor
	
	
OnBlur(pCol,YKEY,YFELD)
    ;-------------------------------------------------------------------------------
    ; To execute on blur of line cells.
    ;
    ; Returns:
    ;
    ; History:
    ; 12-Sep-2005	PO		SR13457: Call UpdateTotals with YKEY
    ; 09-Sep-2005	RPW		SR13456: Restored missing code for handling change the
    ; 							Amount and Amount Tax Inc fields.
    ; 02-Sep-2005	RPW		SR11983: Copied here to handle the discounts/surcharges.
    ; 23-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
    new fltDiscount,strYKEY,intReference,idInvoice,strChargeKey
	
	set YKEY      = $translate(YKEY,"""")
	set idInvoice = $$$KEY1(YKEY)
	set strYKEY   = idInvoice_","_$$$FINAPInvChargeParent(YFELD)
	
	set intReference = $$ReferenceRow^COMGridEdit31Interface("FINAPInvCharge2",$$$KEY2(YKEY))
	set strChargeKey = idInvoice_","_intReference
	
	if (pCol=$$$FldFINAPInvChargeAmount) || (pCol=$$$FldFINAPInvChargeAmountTaxEx) {  ; Amount, AmountTaxEx
		; This whole block put back in for SR13456
		if pCol=$$$FldFINAPInvChargeAmount {
			set $$$FINAPInvChargeAmountTaxEx(YFELD) = $$$FINAPInvChargeAmount(YFELD)
		} else {
			set $$$FINAPInvChargeAmount(YFELD)      = $$$FINAPInvChargeAmountTaxEx(YFELD)
		}
		
		do OnBlurForTax(idInvoice,.YFELD)
		do UpdateTotals(strYKEY,YFELD,pCol=$$$FldFINAPInvChargeAmount,YKEY) ; SR13457
	
	} elseif (pCol=14) || (pCol=12) || (pCol=9) {    ; NOTE : pCol is form not class field number - sometimes the same
 		
		if (pCol=14) && ($$$FINAPInvChargeSupplierDiscount(YFELD)'="") set $$$FINAPInvChargeBuyingDiscount(YFELD)   = ""
		if (pCol=12) && ($$$FINAPInvChargeBuyingDiscount(YFELD)'="")   set $$$FINAPInvChargeSupplierDiscount(YFELD) = ""
		if pCol'=9 do PopulateFields^FINAPInvDiscount(.YFELD)
		set fltDiscount = $$GetDiscountAmount(strChargeKey,YFELD)
		set $$$FINAPInvChargeAmount(YFELD)      = fltDiscount
		set $$$FINAPInvChargeAmountTaxEx(YFELD) = fltDiscount
		
		do OnBlurForTax(idInvoice,.YFELD)
		do UpdateTotals(strYKEY,YFELD,,YKEY) ; SR13457
		
	} elseif pCol=4 {
		do GetDiscounts(YKEY,YFELD)	
	}
	
	quit
	
	
OnBlurForTax(pidInvoice,pYFELD)
    ;-------------------------------------------------------------------------------
    ; To execute on blur of line cells.
    ;
    ; Returns:
    ;
    ; History:
    ; 29-Aug-2005	RPW		SR11983: Copied from FINAPInvCharge
    ; 12-Aug-2005	JW		Use LineIncludeTax check
    ; 23-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new objInv,blnIncTax
	
	// Update Tax, headers
	set objInv = $$$GRIDYFELDContainer
 
	;do CalculateTax(.pYFELD,objInv, pCol=$$$FldFINAPInvChargeAmount )
	do CalculateTax^FINAPInvDiscount(pidInvoice,.pYFELD,objInv, $$$FINAPInvLinesIncludeTax(objInv) )
	
	quit
	
	
GetDiscountAmount(YKEY,YFELD) 
    ;-------------------------------------------------------------------------------
    ; Find the Discount amount.
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 27-Jul-2005	shobby	SR11983 Created
    ;-------------------------------------------------------------------------------
	new idFINAPInv,objFINAPInv,idSupplier
 
	set objFINAPInv = $$$GRIDYFELDContainer
	set idSupplier  = $$$FINAPInvSupplier(objFINAPInv)
	set idFINAPInv  = $$$KEY1(YKEY)
	quit $$GetDiscountForChargeLineByYFELD^FINAPInvDiscount(YFELD,idSupplier,idFINAPInv,$$$KEY2(YKEY))
	
	
GetDiscounts(YKEY,YFELD)
	new fltAmount,objDiscounts
	
	set fltAmount=0
	;do UpdateHeaderField^COMGridEdit31Interface("M44",15)
	quit fltAmount
	
	
UpdateTotals(YKEY,YFELD,pblnTaxInc=$$$NO,pChargeYKEY="")
    ;-------------------------------------------------------------------------------
    ; Recalculate Additional Charges and Total after Discounts and Surcharges
    ; 
    ; Returns:
    ; 
    ; Parameters:
    ; YKEY      : The key or the inv match line.
    ; YFELD     : The current charge line
    ; pblnTaxInc: Is this tax inc or not?
    ;
    ; History:
    ; 28-Sep-2005	PO		SR13559: Add instead of subtract.
    ; 12-Sep-2005	PO		SR13457: Pass in pChargeYKEY and pass across YFELD,
    ; 							pChargeYKEY to GetAdditionalCharges
    ; 22-Aug-2005	RPW		SR11983:Added pblnTaxInc
    ; 05-Apr-2005	shobby	Created (SR11983)
    ;-------------------------------------------------------------------------------
	new fltExtraCharges,fltTotalAfterDS,idLine,objFINAPInvLine
 
	// Update Tax, headers
	;do CalculateTax^FINAPInvCharge(.pYFELD,objInv, pCol=$$$FldFINAPInvChargeAmount )
	set fltExtraCharges = $$GetAdditionalCharges(YKEY,YFELD,pblnTaxInc,pChargeYKEY) ; SR11983, SR13457
	set idLine          = $$GetLineId^COMGridEdit31G(YKEY,"FINAPInvLine")
	
	set objFINAPInvLine = $$GetYFELDEX^COMGridEdit31Interface("FINAPInvLine",YKEY)
	set fltTotalAfterDS = $$$FCAdd($$$FINAPInvLineInvoiceTotal(objFINAPInvLine),fltExtraCharges)
	
	$$$GRIDUpdateManualField(idLine,25,fltExtraCharges,$$$FORM)
	$$$GRIDUpdateManualField(idLine,35,fltTotalAfterDS,$$$FORM)
	
	do UpdateContainer^COMGridEdit31Interface()
	quit
	
	
OldGetAdditionalCharges(pYKEY,pYFELD,pblnTaxInc=$$$NO,pChargeYKEY="")  ; DEPRECATED - DO NOT USE
    ;-------------------------------------------------------------------------------
    ; Returns:
    ; 
    ; Parameters:
    ; pYKEY     : The key or the inv match line.
    ; pYFELD    : The current charge line
    ; pblnTaxInc: Is this tax inc or not?
    ;
    ; History:
    ; 21-May-2009	DWR		SR16363: calling new code for test with new receipts in mind
    ; 12-Sep-2005	PO		SR13457: For line changed use in memory details and perform FCUpdate since summing up
    ; 29-Aug-2005	RPW		SR11983: Return a -ve amount.
    ; 22-Aug-2005	RPW		SR11983:Added pblnTaxInc
    ; 19-Aug-2005	shobby	SR11983:Don't get idSupplier if YKEY1=""
    ; 18-Aug-2005	RPW		SR11983: Correctly calculate the total.s
    ; 04-Apr-2005	shobby	Loop through and sum up the additional charges (SR11983)
    ;-------------------------------------------------------------------------------
	
	;quit $$NewGetAdditionalCharges($get(pYKEY),$get(pYFELD),pblnTaxInc,pChargeYKEY)
	
	; vvv DEPRECATED CODE
	
	new objGrid,idLine,objFINAPInvCharge,fltTotal,YKEY1,YKEY2,fltDiscount,idSupplier,idFINAPInvCharge
	new fltRate
	
	$$$LogR("OldGetAdditionalCharges",$get(pYKEY)_","_pYFELD_","_pblnTaxInc_","_pChargeYKEY)
	
	set fltTotal = 0
	
	set YKEY1 = $$$KEY1(pYKEY)
	set YKEY2 = $$$KEY2(pYKEY)
	set idFINAPInvCharge = $$$KEY2($get(pChargeYKEY))
	
	if (YKEY1'="")&&(YKEY2'="") {
		set idSupplier=$$$FINAPInvSupplier($get(^FINAPInv(YM,YKEY1,1)))
		
		do GetObject^COMGridEdit31Interface(.objGrid,$$$YES,"FINAPInvCharge2") ; returns both simple variable and array
		set fltRate = $$$FINAPInvExchangeRate(objGrid)
		set idLine  = ""
		for {
			set idLine = $order(objGrid(YKEY1,idLine))
			quit:idLine=""
			
			set objFINAPInvCharge = $get(objGrid(YKEY1,idLine))
			if $$$FINAPInvChargeParent(objFINAPInvCharge)=YKEY2 {
				if idLine = idFINAPInvCharge { ; if pChargeYKEY not provided then assignment doesn't take place
					set objFINAPInvCharge = pYFELD
				}
				if pblnTaxInc {
					set fltDiscount = $$$FINAPInvChargeAmount(objFINAPInvCharge)
				} else {
					set fltDiscount = $$$FINAPInvChargeAmountTaxEx(objFINAPInvCharge)
				}
				set fltTotal = $$$FCAdd(fltDiscount,fltTotal)
				set fltTotal = $$$FCUpdate(fltTotal,fltRate,1)
			}
		}
	}
	quit fltTotal
	;quit $$$FCSign(fltTotal)
	
	
GetAdditionalCharges(pYKEY,pYFELD,pblnTaxInc=$$$NO,pChargeYKEY="")
    ;-------------------------------------------------------------------------------
    ; modified routine from above, call the record directly, it has been saved
    ;
    ; Returns:
    ; 
    ; Parameters:
    ; pYKEY     : The key or the inv match line.
    ; pYFELD    : The current charge line
    ; pblnTaxInc: Is this tax inc or not?
    ;
    ; History:
    ; 14-Jul-2009	GRF		SR16689: Rename and call directly
    ; 22-May-2009	GRF		SR16363: rate not being set; variable prefixes
    ; 21-May-2009	DWR		SR16363: Created
    ;-------------------------------------------------------------------------------
	new fltDiscount,fltRate,fltTotal,idChargeNo,idInvLine,idInvoice,idLine,idSupplier
	new objCharge,objInvoice
	
	$$$LogR("GetAdditionalCharges",$get(pYKEY)_","_pYFELD_","_pblnTaxInc)
	
	set fltTotal = 0
	
	set idInvoice = $$$KEY1(pYKEY)
	set idInvLine = $$$KEY2(pYKEY)
	
	set objInvoice = $get(^FINAPInv(0,idInvoice,1))
	set fltRate    = $$$FINAPInvExchangeRate(objInvoice)
	;set fltRate = $$$FINAPInvExchangeRate(objGrid)
	
	set idChargeNo = ""
	for {
		set idChargeNo = $order(^FINAPInvCharge(YM,idInvoice,idChargeNo))
		quit:idChargeNo=""
		
		set objCharge = $get(^FINAPInvCharge(YM,idInvoice,idChargeNo,1))
	$$$LogRx("#"_objCharge_"#"_$$$FINAPInvChargeParent(objCharge)_"<"_idInvLine)
		continue:$$$FINAPInvChargeParent(objCharge)'=idInvLine
		
		if pblnTaxInc {
			set fltDiscount = $$$FINAPInvChargeAmount(objCharge)
		} else {
			set fltDiscount = $$$FINAPInvChargeAmountTaxEx(objCharge)
		}
		set fltTotal = $$$FCAdd(fltDiscount,fltTotal)
		set fltTotal = $$$FCUpdate(fltTotal,fltRate,1)
	;	set fltTotal = $select((fltDiscount["@")||(fltTotal["@"):$$Add^COMSYSFC(fltDiscount,fltTotal)   ,1:fltDiscount+(fltTotal))
	;	set fltTotal = $select(fltTotal["@"                     :$$FCUpdate^COMSYSFC(fltTotal,fltRate,1),1:+(fltTotal))
	}
	
	quit fltTotal
	
	
OnLineAdded(pintRowCount="",pstrKey="") 
    ;-------------------------------------------------------------------------------
    ; When a discount line is added, check if it's dirty, ie the user was the last
    ; to modify the value, even when last saved.
    ;
    ; Params:
    ; pintRowCount: The row number added
    ; pstrKey     : The YKEY or the row and owner
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Aug-2005	RobertW		SR11983: Created
    ;-------------------------------------------------------------------------------
	new objCharge,objFINAPInv
	
	;do SetDirty^FINAPInvDiscount(pstrKey)
	set objFINAPInv = $$$GRIDYFELDContainer
 	
 	// FIXME JW - i think we can put pintRowCount==1 here ??
 	
	if objFINAPInv'="" {
		do HideHeaders^FINAPInvDiscount(+$$$FINAPInvLinesIncludeTax(objFINAPInv))
	}
 		
	quit
	
	
OnBeforeFormat(pYFIELDNAME,pYTEXT,pYFELD) 
    ;-------------------------------------------------------------------------------
    ; Currently does nothing except stop and error.
    ;
    ; Params:
    ; pYFIELDNAME: The grid field name, ie Y5_12
    ; pYTEXT     : The text of the the field
    ; pYFELD     : The data of the row.
    ;
    ; Returns:
    ; The original text
    ;
    ; History:
    ; 12-Sep-2005	PO		SR13480: Lock the Discount or Surcharges field if in
    ; 							base mode for a FC supplier
    ; 02-Sep-2005	RPW		SR11983: Created
    ;-------------------------------------------------------------------------------
	new intRow,intCol
	
	set intRow = $piece($piece(pYFIELDNAME,"_",1),"Y",2)
	set intCol = $piece(pYFIELDNAME,"_",2)
	
	if (intCol=2) || (intCol=1) {
		;set YBACKGROUNDCOLOR=YRED
		
	} elseif (intCol=12) && ($$$CurrencyToUse="Base") {
		set YENABLED = $$$NO
		set YBACKGROUNDCOLOR = YGRAY
	}
	
	quit
	
]]></Routine>
</Export>