<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvChild" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvChild
	;--------------------------------------------------------------------------------------
	; SR: 11837 - Created as a replacement for FINARRecurringInvoiceChild.mac
	;--------------------------------------------------------------------------------------
	#Include FIN
	#Include FINConst
	#Include COMConst
	#Include COMSYS
	
 
GetListChildren(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, return a list of all its children.
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new lstChildren,idInvoice,idChild
	
	set lstChildren=""
	set idInvoice=$get(pidInvoice)
	
	if (idInvoice'="")&&(idInvoice'="+") {
		set idInvoice=$$$Index(idInvoice)
		set idChild=""
		for {
			set idChild=$order(^FINARInvs(YM,3,idInvoice,idChild))
			quit:idChild=""
			set lstChildren=lstChildren_$listbuild(idChild)	
		}
	}
 
	quit lstChildren
	
	
GetNumberChildren(pstrYKEY,pDaysAgo=-1)
    ;-------------------------------------------------------------------------------
    ; Returns the number of children that an invoice has.
    ; 
    ; If an invoice is a child itself, or not a repeat invoice, returns zero.
    ;
    ; pDaysAgo: -1 = Get TOTAL number of children posted
    ; 			 0 = Get number of children posted 0 days ago (today)
    ; 			 1 = Get number of children posted 1 day ago (yesterday)
    ; 			 2 = Get number of children posted 2 days ago
    ; 			 3 = Get number of children posted 3 days ago... etc.
    ; 
    ; Returns: integer (intNumberChildren)
    ;
    ; History:
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new idInvoice,intNumberChildren,lstChildren,dteToday,idChild,objChild
	new dtePosted,idx
	
	set idInvoice=$get(pstrYKEY)
	set intNumberChildren=0
	
	if (idInvoice'="")&&(idInvoice'="+") {
		set lstChildren=$$GetListChildren(idInvoice)
		if pDaysAgo=-1 {
			set intNumberChildren=$listlength(lstChildren)
		} else {
			set dteToday=+$H
			for idx=1:1:$listlength(lstChildren) {
				set idChild=$listget(lstChildren,idx)
				set objChild=$get(^FINARInv(YM,idChild,1))
				set dtePosted=$$$FINARInvPostedDate(objChild)
				if ((dteToday-pDaysAgo)<=dtePosted)&&(dtePosted<=dteToday) {
					set intNumberChildren=intNumberChildren+1	
				}
			}	
		}	
	}
	
	quit intNumberChildren
	
	
DisplayChildren(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Routine to display all child invoices of a parent in a COM table.
    ;
    ; Returns:
    ;
    ; History:
    ; 14-Mar-2005		Steve S			Display 'Pending' if no posted date
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new idInvoice,lstHeader,idInvoiceChild,objChildInvoice,dtePosted
	new lstChildren,idx,idCustomer,fltSubTotal,fltSubTotalFormatted,fltTaxAmt
	new fltTaxAmtFormatted,fltTotal,fltTotalFormatted,strCustomer
	new strCreditText,idStatus
	
	set idInvoice=$get(pidInvoice)
	set lstChildren=$$GetListChildren(idInvoice)
	
	if ($listlength(lstChildren)>0) {	
		set lstHeader=""
		set lstHeader=lstHeader_$listbuild($$$Text("Fin00595"))			;Child Invoice
		set lstHeader=lstHeader_$listbuild($$$StrFINARInvPostedDate)	;Posted Date
		set lstHeader=lstHeader_$listbuild($$$Text("Fin00557"))  		;Customer
		set lstHeader=lstHeader_$listbuild($$$StrFINARInvCredit) 	    ;Credit
		set lstHeader=lstHeader_$listbuild($$$Text("Fin00609"))  		;Sub Total
		set lstHeader=lstHeader_$listbuild($$$StrFINARInvTax)  			;Tax
		set lstHeader=lstHeader_$listbuild($$$Text("Fin00071"))  		;Total
 
		do Start^COMTable(lstHeader)
	
		for idx=1:1:$listlength(lstChildren) {
			set idInvoiceChild=$listget(lstChildren,idx)
			set objChildInvoice=$get(^FINARInv(YM,idInvoiceChild,1))
			set idStatus=$$$FINARInvStatus(objChildInvoice)
		
			do NewLine^COMTable()
		
			; CHILD INVOICE CELL
			do InsertCell^COMTable(idInvoiceChild,"FINARInv",idInvoiceChild,"","",1)
		
			; POSTED DATE CELL
			set dtePosted=$$$FINARInvPostedDate(objChildInvoice)
			if dtePosted="" {
				do InsertCell^COMTable($$$AppEnum("FINSYSSTATUS",idStatus))
			} else {
				do InsertCell^COMTable($$^WWWDATE(dtePosted))
			}
 
			; CUSTOMER NUMBER CELL
			set idCustomer=$$$FINARInvCustomer(objChildInvoice)
			set strCustomer=$$$FINARCustomerCustomerName($get(^FINARCustomer(YM,idCustomer,1)))
			do InsertCell^COMTable(idCustomer_" ("_strCustomer_")","FINARCustomer",idCustomer)
			
			; CREDIT CELL
			if $$$FINARInvCredit(objChildInvoice) {
				set strCreditText=$$$Text("Fin00598")  ;Yes	
			} else {
				set strCreditText=$$$Text("Fin00608")  ;No	
			}
			do InsertCell^COMTable(strCreditText)
			
			
			; SUB TOTAL CELL
			set fltSubTotal=$$GetSubTotal^FINARInv(idInvoiceChild)
			set fltSubTotalFormatted=$$FormatCurrency^COMTable(fltSubTotal)
			do InsertCell^COMTable(fltSubTotalFormatted)
			
			; TAX CELL
			set fltTaxAmt=$$$FINARInvTax(objChildInvoice)
			set fltTaxAmtFormatted=$$FormatCurrency^COMTable(fltTaxAmt)
			do InsertCell^COMTable(fltTaxAmtFormatted)
			
			; TOTAL CELL
			set fltTotal=fltSubTotal+fltTaxAmt
			set fltTotalFormatted=$$FormatCurrency^COMTable(fltTotal)
			do InsertCell^COMTable(fltTotalFormatted)
			
			; END THE ROW
			do EndLine^COMTable()
		}
		
		do Stop^COMTable()
	
	}
	
	quit
	
	
GetSubTotalPosted(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, determine the total amount posted over its children,
    ; NOT INCLUDING TAX.
    ;
    ; Returns: float
    ;
    ; History:
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new idInvoice,fltSubTotal,lstChildren,idx,idChildInvoice
	
	set idInvoice=$get(pidInvoice)
	set fltSubTotal=0
	set lstChildren=$$GetListChildren(idInvoice)
	
	for idx=1:1:$listlength(lstChildren) {
		set idChildInvoice=$listget(lstChildren,idx)
		set fltSubTotal=fltSubTotal+$$GetSubTotal^FINARInv(idChildInvoice)	
	}
	
	quit fltSubTotal
	
	
	
GetTaxPosted(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, determine the total amount of tax posted over its
    ; children.
    ;
    ; Returns: float
    ;
    ; History:
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new idInvoice,fltTax,lstChildren,idx,idChildInvoice,objChildInvoice
	
	set idInvoice=$get(pidInvoice)
	set fltTax=0
	set lstChildren=$$GetListChildren(idInvoice)
	
	for idx=1:1:$listlength(lstChildren) {
		set idChildInvoice  =$listget(lstChildren,idx)
		set objChildInvoice =$get(^FINARInv(YM,idChildInvoice,1))
		set fltTax			=fltTax+$$$FINARInvTax(objChildInvoice)
	}
	
	quit fltTax
	
	
GetTotalPosted(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, return the total posted over its children
    ; (INCLUDING TAX)
    ;
    ; Returns: float
    ;
    ; History:
    ; 21-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new idInvoice,fltTax,fltSubTotal,fltTotal
	
	set idInvoice=$get(pidInvoice)
	
	set fltTax		=$$GetTaxPosted(idInvoice)
	set fltSubTotal =$$GetSubTotalPosted(idInvoice)
	set fltTotal	=fltTax+fltSubTotal
	
	quit fltTotal
	
 
GetChildrenPostedToday(pidInvoice,pDatePosted="")
    ;-------------------------------------------------------------------------------
    ; Given an invoice ID, does this invoice have any children posted today?
    ; 
    ; If so, return the list of children.
    ; 
    ; Parameters: 
    ;
    ; Returns: - pidInvoice:  The id of the parent invoice.
    ; 		   - pDatePosted: Check for children posted on this day (defaults to $H)
    ;
    ; History:
    ; 24-Jan-2005		Steve S			Created
    ;-------------------------------------------------------------------------------
 
	new lstChildren,objInvoice,idParentInvoice,idChildInvoice,objChildInvoice
	
	set lstChildren=""
	if pDatePosted="" set pDatePosted=+$H 
	
	if ($get(pidInvoice)'="")&&($get(pidInvoice)'="+") {
		set objInvoice=$get(^FINARInv(YM,pidInvoice,1))
		if $$$FINARInvRepeat(objInvoice) {
			set idParentInvoice=$$$Index(pidInvoice)
			
			set idChildInvoice=""
			for {
				set idChildInvoice=$order(^FINARInvs(YM,3,idParentInvoice,idChildInvoice))
				quit:idChildInvoice=""
				
				set objChildInvoice=$get(^FINARInv(YM,idChildInvoice,1))
				if $$$FINARInvPostedDate(objChildInvoice)=pDatePosted {
					set lstChildren=lstChildren_$listbuild(idChildInvoice)		
				}		
			}
		}
	}
	
	quit lstChildren
	
	
ShowParent(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Show link to parent
    ;
    ; Returns:
    ;
    ; History:
    ; 26-May-2005	Steve S		Created SR:12340
    ;-------------------------------------------------------------------------------
	new objInvoice,idParent,strLinkText
	
	if '$$$NoKey(pidInvoice) {
		set objInvoice=$get(^FINARInv(YM,pidInvoice,1))
		set idParent=$$$FINARInvParentInvoice(objInvoice)
		if idParent'="" {
			set strLinkText=$$$StrFINARInvParentInvoice_": "_idParent
			do InsertLink^COMTable("FINARInv",idParent,strLinkText)
		}
	}
 
	quit
]]></Routine>
</Export>