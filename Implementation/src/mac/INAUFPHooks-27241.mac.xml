<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPHooks" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPHooks
	
#include FINConst
#include INConst
#include COMSYS
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^INAUFPHooks("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
OnBeforeSaveHook(YKEY,&YFELD)
	;-------------------------------------------------------------------------------
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 
	; 03-Feb-2010	shobby	SR17138: Only update Quantity if Ordered Quantity is
	; 							not empty so as not to upset orders coming from DRP.
	; 02-Feb-2010	shobby	SR17138: Created
	;-------------------------------------------------------------------------------
	new idItem,strStatus
	
	$$$LogR("OnBeforeSaveHook",$get(YKEY))
	
	set strStatus = $$$OK
	
	if '$$$NoKey(YKEY) {
		set idItem = $$$INAUFPItemNumber(YFELD)
		if $$$INAUFPOrderedQuantity(YFELD)'="" {
			set $$$INAUFPQuantity(YFELD) = $$$INAUFPOrderedQuantity(YFELD)*$$GetUOMConversion^INUOMConversion(idItem,$$$INAUFPOrderedUnit(YFELD))
		}
	}
	set strStatus = $$Update(YKEY,YFELD)
	
	quit strStatus
	
	
Update(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 30-May-2011	GRF		SR17697: Remove superfluous tests where already know
	;							INARTK doesn't exist
	; 29-Jan-2010	shobby	SR17138: Created
	;-------------------------------------------------------------------------------
	new idItem,idSupplier,objINAUF,objINARTK,strStatus
	
	set strStatus=$$$OK
	
	if '$$$NoKey(YKEY) {
		set objINAUF = $get(^INAUF(0,$$$KEY1(YKEY),1))
		set idSupplier = $$$INAUFSupplierNumber(objINAUF)
		set idItem     = $$$INAUFPItemNumber(YFELD)
		if (idSupplier'="") && (idItem'="") {
			if '$data(^INARTK(0,idItem,idSupplier)) {
				set objINARTK = ""
				if ($$$INAUFPOrderedUnit(YFELD)'="") {
					set $$$INARTKBasicPurchasePrice(objINARTK)   = $$Convert^INUOMConversion(idItem,$$$INARTKBasicPurchasePrice(objINARTK),$$$INAUFPOrderedUnit(YFELD),$$$INARTKDeliveryQuantityUnit(objINARTK))
					set $$$INARTKDeliveryQuantityUnit(objINARTK) = $$$INAUFPOrderedUnit(YFELD)
					
					set strStatus = $$$Save("INARTK",idItem_","_idSupplier,objINARTK,$$$YES)
				}
			}
		}
	}
	quit strStatus
	
	
OnAfterSaveHook(pidOrdKeys)
	;-------------------------------------------------------------------------------
	; OnAfterSaveHook Hook - Called by INAUFP - OnAfterSave
	;
	; Inputs: Order Keys - ID & Line
	;
	; Returns: strStatus
	;
	; History:
	; 23-Oct-2009	PPP		SR16979: Created (Replaces COMDCMEvent : INAUFPSave)
	;-------------------------------------------------------------------------------
	new idOrder,objOrder,strStatus
	
	$$$LogR("OnAfterSaveHook",$get(YKEY))
	
	set strStatus = $$$OK
	set idOrder   = $$$KEY1(pidOrdKeys)
	
	if idOrder'="" {
		set objOrder = $get(^INAUF(0,idOrder,1)) ; 2 : Purchase Order
		if ($$$INAUFOrderType(objOrder)=2) && ($$$INAUFBlockPurchaseOrder(objOrder)'=$$$YES) {
			set strStatus = $$OrderLineSave^FINAPInvDCM(pidOrdKeys)
		}
	}
	quit strStatus
	
	
OnBeforeDeleteHook(pidOrdKeys)
	;-------------------------------------------------------------------------------
	; OnBeforeDelete Hook - Called by INAUFP - OnBeforeDelete
	;
	; Inputs: Order Keys - ID & Line
	;
	; Returns: strStatus
	;
	; History:
	; 23-Oct-2009	PPP		SR16979: Created (Replaces COMDCMEvent : INAUFPDelete)
	;-------------------------------------------------------------------------------
	new strStatus
	
	$$$LogR("OnBeforeDeleteHook",$get(pidOrdKeys))
	
	set strStatus = $$OrderLineDelete^FINAPInvDCM(pidOrdKeys)
	quit strStatus
	

]]></Routine>
</Export>