<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARInvLineIC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARInvLineIC
	#include COMSYS
	#include FINConst
 
 
OnBlur(pRow,pCol,&pobjInvoiceLine,pValue)
	;-------------------------------------------------------------------------------
	; Code to run on-blur of IC fields.
	;
	; Params:
    ; pRow           		: The row in the grid
    ; pCol           		: The cell in the grid
    ; pobjInvoiceLine		: The data for the grid line
    ; pValue         		: The value in this cell.
    ;
    ; ByRefs:
    ; pobjInvoiceLine
	;
	; Returns:
	;
	; History:
	; 02-Nov-2006	Steve S		SR14243: Created
	;-------------------------------------------------------------------------------
	new idCompany,objICSetup
	
	if pCol=$$$FldFINARInvLineCompany1 {
		set idCompany=pValue
		if idCompany'="" {
			set objICSetup=$get(^FINICSetup(YM,idCompany,1))
			set $$$FINARInvLineCompanyAccount(pobjInvoiceLine) = $$$FINICSetupDefaultDistributionAccoun(objICSetup)
			set $$$FINARInvLineGLAccount(pobjInvoiceLine) = $$$FINICSetupToAccount(objICSetup)
		} else {
			set $$$FINARInvLineCompanyAccount(pobjInvoiceLine)=""
			set $$$FINARInvLineGLAccount(pobjInvoiceLine)=""
		}
		do UpdateManualField^COMGridEdit31Interface(pRow,9,$$GetAccountStatus(pobjInvoiceLine))
	} elseif pCol=$$$FldFINARInvLineCompanyAccount {
		if $$$FINARInvLineCompany1(pobjInvoiceLine)="" {
			set $$$FINARInvLineCompanyAccount(pobjInvoiceLine)=pValue
		}
		do UpdateManualField^COMGridEdit31Interface(pRow,9,$$GetAccountStatus(pobjInvoiceLine))
	}
 
	
	quit
	
	
GetAccountStatus(pobjLine) 
	;-------------------------------------------------------------------------------
	; Intercompany status for an invoice line
	;
	; Params: pobjLine: Line object
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 02-Nov-2006	Steve S		SR14243: Created
	;-------------------------------------------------------------------------------
	quit $$GetAccountStatus^FINICSetup($$$FINARInvLineCompany1(pobjLine),$$$FINARInvLineCompanyAccount(pobjLine))
 
 
AccountSearch(pstrLine)
	;-------------------------------------------------------------------------------
	; Start the account search
	;
	; Params:	pstrLine : Line ref
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 02-Nov-2006	Steve S		SR14243: Created
	;-------------------------------------------------------------------------------
	new idLine,objLine
	
	set idLine=$piece($piece(pstrLine,"Y",2),"_",1)
 
	if (idLine'="") {
		set objLine=$$GetYFELD^COMGridEdit31G("FINARInvLine",idLine)
		if $$$FINARInvLineCompany1(objLine)'="" {
			do Initialise^COMViewCustom("FINICAccount")
			do AddFilter^COMViewCustom("P1","=",$$$FINARInvLineCompany1(objLine))
			do AddFilter^COMViewCustom("D"_$$$FldFINICAccountBlockedReason,"=","")
		} else {
			do Initialise^COMViewCustom("FINGLAccount")
			do AddFilter^COMViewCustom("D"_$$$FldFINGLAccountAccountUsage,"=",$$$EnumFINGLACCOUNTUSAGETransaction)
			do AddFilter^COMViewCustom("D"_$$$FldFINGLAccountEnablepostingsecurity,"=","")
		}
		do Start^COMViewCustom("WWW2.Y"_idLine_"_8")
	}
	
	quit
]]></Routine>
</Export>