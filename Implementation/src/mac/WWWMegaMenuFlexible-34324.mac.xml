<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMegaMenuFlexible" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMegaMenuFlexible
	#include COMConst

Flexibles()
	;-------------------------------------------------------------------------------
	; Looks for any Menus under the 'Mega' menu.
	; 
	; History:
	; 27-May-2013	shobby	CORE-110.1: MegaMenu application is user defined now.
	; 17-Apr-2012	shobby	SR17998: Created..
	;-------------------------------------------------------------------------------
	new idMenu,intCounter,objWWW013,idMega ;CORE-110.1
	
	set intCounter=0
	
	set objWWW013=$get(^WWW013(0,YBED,1))
	set idMega=$$GetMegaMenuApplicationName^WWWMegaMenu()
	set idMenu="" for { set idMenu=$order(^WWW004(0,idMega,idMenu)) quit:idMenu=""
		continue:$piece(idMenu,".",2)'=""
		continue:$piece($order(^WWW004(0,idMega,idMenu)),".",1)'=$piece(idMenu,".",1)
		if ((";"_$$$WWW013MegaMenus(objWWW013)_";")[(";"_idMenu_";"))||($$$WWW013MegaMenus(objWWW013)="") {
			set intCounter=intCounter+1
			do Flexible(idMenu,10+intCounter)
		}
	}
	quit
	
	
Flexible(pidMenu,pintMenu)
	;-------------------------------------------------------------------------------
	; Render Menu
	; TODO: use list of styles isntead of directly writing out background images
	; 
	; History:
	; 04-Jun-2014	shobby	CORE-385.11: Corrected problem if more than 4 columns
	; 31-Mar-2014	shobby	CORE-202.1: Don't pass YBACK when selecting from a menu.
	; 02-Jan-2014	shobby	CORE-302.2: onmousemove
	; 26-Dec-2013	shobby	CORE-291.2: Don't UMLAU the YNAME
	; 18-Dec-2013	shobby	CORE-291: YUSENAME
	; 13-Jun-2013	shobby	CORE-128: Transfer trigger for flexible menu.
	; 06-Jun-2013	shobby	Adhoc: Removed height:100% on overviewwrapper
	; 31-May-2013	shobby	CORE-112.7: Improve layout for menus with > or < 3 columns (from Gustavo)
	; 27-May-2013	shobby	CORE-110.1: MegaMenu application is user defined now.
	; 22-May-2013	shobby	CORE-107.3: Don't show if no items.
	; 20-May-2013	shobby	CORE-81.3.2: Fixes icon and item text vertical alignment
	; 20-May-2013	shobby	CORE-81.3.1: Fixes text vertical alignment
	; 17-Apr-2012	shobby	SR17998: Based on flexible menu.
	;-------------------------------------------------------------------------------
	new arrUserMenu,idApplicn,idSection,intColCount
	new loopSubmenus,objMenu,objSubmenu
	new strLink,strForm,strCSSClass,strImage,strImageDirectory,strLinkWith,strTarget
	new strMenuName,strLeft
	new numberOfColumns,idColumn,styleNoColumns ;CORE-112.7
	new strTrigger ;CORE-128
		
	set idApplicn=$$GetMegaMenuApplicationName^WWWMegaMenu() ;SR17998 ;CORE-110.1
	do GetMenu(YBED,idApplicn,pidMenu,.arrUserMenu) ;CORE-107.3

	if $data(arrUserMenu) {							;CORE-107.3
        //--Count the number of columns under a main folder and define area width    ;CORE-112.7 vvvv
        set numberOfColumns = 0
        set idColumn = ""       
        for {
              set idColumn = $order(arrUserMenu(idColumn))
              quit:(idColumn = "")
              
              set numberOfColumns = $increment(numberOfColumns)
        }
        
        set styleNoColumns = $case(numberOfColumns,1:"column1",2:"columns2",3:"columns3",4:"columns4",:"columns4") ;CORE-385.11
        set:(styleNoColumns > 4) styleNoColumns = "columns4"
        set:(styleNoColumns = "") styleNoColumns = "columns3"
        
        //-------																	;CORE-112.7 ^^^^
   		set strMenuName=$$$WWW004MenuDescription($$Get^WWW004(idApplicn,pidMenu))

		set strLeft=""
		if pintMenu=11 set strLeft="margin-left:0px;"
		&html<
	    <li id='menuLi_#(pintMenu)#' class='li' hclass='lihover' zclass='li' style='#(strLeft)#'><a id='menuA_#(pintMenu)#' href="#" class="drop"><span style='text-align:center;'>#(strMenuName)#</span></a><!-- Begin 4 columns Item -->
	        <div id='menuDiv_#(pintMenu)#' onmousemove='menucloseClearTimeout();' class="dropdown_flexible #(styleNoColumns)#"  zclass="dropdown_flexible" ><!-- Begin 4 columns container -->
		>
		;CORE-81
		&html<<div class="OverviewWrapper" onmouseover='window.event.returnValue=false; window.event.cancelBubble=true; return false;'>>
	
		set intColCount  = 3
		set strImageDirectory = $translate($piece($get(^WWW012(0,0,1)),"~",47),"\","/")
		;CORE-107.3 do GetMenu(YBED,idApplicn,pidMenu,.arrUserMenu)
		set idSection = ""
		for {
			set idSection = $order(arrUserMenu(idSection))
			quit:idSection=""
		
			set objMenu = $$Get^WWW004(idApplicn,idSection)
			set strImage = $$$WWW004ImageFilegif(objMenu)
			if strImage="" set strImage="Mega_empty.png" ;CORE-107.3
			;CORE-81, CORE-81.3.1
			;##W3C
			&html<
				<ul class="Overview" style='padding:5px; padding-right:0px; width:265px; float:left; height:100%; clear:right;'>
					<li>
					<div class="Section" style="margin-left:-36px; left:-15px; margin-right:0px; width:265px; height:65px; background-image: url(#(strImageDirectory)#/#(strImage)#); padding-top:8px; background-repeat:no-repeat;">
						<span>#($$$WWW004MenuDescription(objMenu))#</span>
					</div>
					</li>
			>
			for loopSubmenus=1:1:$length($get(arrUserMenu(idSection)),";") {
				if ($data(^WWW004(0,idApplicn,$piece(arrUserMenu(idSection),";",loopSubmenus)))){
					set objSubmenu = $$Get^WWW004(idApplicn,$piece(arrUserMenu(idSection),";",loopSubmenus))
				} else {
					set objSubmenu = ""
				}
				set strForm = $$$WWW004FormName(objSubmenu)
				set strImage = "html.gif"
			
			  	//SR17614
				set strLinkWith = $$$WWW004LinkWith(objSubmenu)
				set strTarget	= $$$WWW004TargetNameForOutput(objSubmenu)
     			set strTrigger = $piece(objSubmenu,Y,7) ;CORE-128
      			;set strTrigger = $$$WWW004TransferTriggerForForm(objSubMenu) ;CORE-128
			
				set:($$$WWW004ImageFilegif(objSubmenu)'="") strImage = $$$WWW004ImageFilegif(objSubmenu)
			
				if ($$$WWW004StartFormWithSearchFuncti(objSubmenu) = 1){
					set strLink = YAKTION_"EP=WWWSEAR&YFORM="_strForm_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER
			
				} elseif(strForm'="") {
					new YNAME,YUSENAME ;CORE-291
					
					set YNAME=$$$WWW004MenuDescription(objSubmenu) ;CORE-291
					set YUSENAME=$$$WWW004UseMenuDescriptionForForm(objSubmenu) ;CORE-291
					;CORE-128 set strLink = YAKTION_"EP=WWWFORM&YFORM="_strForm_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YBACK="_YFORM_","
				  	;CORE-202.1 set strLink = YAKTION_"EP=WWWFORM&YFORM="_strForm_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YPARA="_strTrigger_"&YUSER="_YUSER_"&YBACK="_YFORM_",&YNAME="_YNAME_"&YUSENAME="_YUSENAME ;CORE-128
				  	set strLink = YAKTION_"EP=WWWFORM&YFORM="_strForm_"&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YPARA="_strTrigger_"&YUSER="_YUSER_"&YNAME="_YNAME_"&YUSENAME="_YUSENAME ;CORE-128 ;CORE-202.1
					
			  	//SR17614
				} elseif(strLinkWith'="") {
					set strLink = strLinkWith

				} else {
					continue ; Don't bother to display submenu
				;	set strLink = "#"
				}
			
				if ($piece(arrUserMenu(idSection),".",2) = "") {
				} else {
					if strImage = "html.gif" {
						set strCSSClass = " class=""default"""
					} else {
						;CORE-81.3.2 set strCSSClass = " style=""list-style-type:square; list-style-image:url("_strImageDirectory_"/"_strImage_"); """
						set strCSSClass = " style=""list-style-type:none;list-style-position:outside;padding-left:17px;background-image:url("_strImageDirectory_"/"_strImage_");background-repeat:no-repeat;background-position: left 5px; """ ;CORE-81.3.2
					}
					if strTarget '= "" {
						&html<<li#(strCSSClass)#><a href="#(strLink)#" target="#(strTarget)#">#($$$WWW004MenuDescription(objSubmenu))#</a></li>>
					} else {
						&html<<li#(strCSSClass)#><a href="#(strLink)#">#($$$WWW004MenuDescription(objSubmenu))#</a></li>>				
					}
				}
			}
			&html<</ul>>
		}
		&html<<hr class="OverviewDivider"/>>
		&html<
			</div>
	    	</div><!-- End 2 columns container -->   
	    </li><!-- End Home Item -->
	    >
	} ;CORE-107.3
	quit
	

GetMenu(pidUser,pidRootNode,pidNode,&arrUserMenu)
	;-------------------------------------------------------------------------------
	; Retrieves all menus (2 level deep) that are accessable based on a list of
	; AccessProfiles
	; 
	; ByRef:
	; 	YM		Company ID
	; History:
	; 19-Apr-2012	shobby	SR17998: Based on code from Flexible menu
	;-------------------------------------------------------------------------------
	new blnOK,idMenu,intMenuCount,loopMenu,objMenu,strUser
	
	kill arrUserMenu
	
	set strUser = $get(^WWW013(0,pidUser,1))
	if strUser '= "" {
		set idMenu = ""
		if ($data(^WWW004(0,pidRootNode)) '= 0) {
			for {
				set idMenu = $order(^WWW004(0,pidRootNode,idMenu))
				quit:idMenu=""
				continue:$extract(idMenu,1,$length(pidNode))'=pidNode
				continue:idMenu=pidNode
				
				set objMenu = $$Get^WWW004(pidRootNode,idMenu)
				if ($$^WWWACCESS($$$WWW004UserAccess(objMenu),$$$WWW004Module1(objMenu),pidUser) = $$$OK) {
					;break
					if ($data(arrUserMenu($piece(idMenu,".",1,2)_".")) = 0) {
						set intMenuCount = 1
					} else {
						set intMenuCount = $length($get(arrUserMenu($piece(idMenu,".",1,2)_".")),";") + 1
					}
					if ($piece(idMenu,".",3)'="") && ($piece(idMenu,".",4)="") {
						set $piece(arrUserMenu($piece(idMenu,".",1,2)_"."),";",intMenuCount) = idMenu
					}
				}
			}
		}
	}

	; Remove invalid entries for this menu type...
	; No leaf nodes, remove
	; No sub entries allowed
	set idMenu = ""
	for {
		set idMenu = $order(arrUserMenu(idMenu))
		quit:idMenu=""
		
		set blnOK = $$$NO
		for loopMenu=1:1:$length(arrUserMenu(idMenu),";") {
			//SR17614
			set objMenu = $$Get^WWW004(pidRootNode,$piece(arrUserMenu(idMenu),";",loopMenu))
			if ($$$WWW004FormName(objMenu) '= "") || ($$$WWW004LinkWith(objMenu) '= "") {
				set blnOK = $$$YES
				quit
			}
		}
		if (blnOK = $$$NO) kill arrUserMenu(idMenu)
	}
	quit

]]></Routine>
</Export>