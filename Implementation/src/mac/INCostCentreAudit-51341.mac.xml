<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INCostCentreAudit" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INCostCentreAudit
	
#include COMSYS
#include INConst
	
	
	;---------------------------------------------------------------------
	;   Location   |   Original Cost Centre   |   Assigned Cost Centre   |
	;---------------------------------------------------------------------
	;              |                          |                          |
	;---------------------------------------------------------------------
	;              |                          |                          |
	;---------------------------------------------------------------------
	
ShowTable(pidSeq)
	;-------------------------------------------------------------------------------
	; 
	; Called By : Form INCostCentreAudit : OnAfterData
	; 
	; History:
	; 03-Dec-2009	GRF		SR16871: created
	;-------------------------------------------------------------------------------
	new idForm,idKey,idLink,idLocn,lstHeader,objAudit,objLocation,strPara,strText
	
	quit:$get(pidSeq)=""
	
	set idForm = "INCostCentreAuditLocn"
	
	set lstHeader =""
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst(idForm,,2))   ; "Location"
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst("WWW0121",1)) ; "Location Name"
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst(idForm,1))    ; "Original Cost Centre"
	set lstHeader = lstHeader_$listbuild($$GetPropertyName^COMConst(idForm,2))    ; "Assigned Cost Centre"
	
	set idLink  = ""
	set idKey   = ""
	set strPara = ""
	set strText = ""
	
	if $data(^INCostCentreAuditLocn(0,pidSeq)) {
		do Start^COMTable(lstHeader,strText,1)
		
		set idLocn = ""
		for {
			set idLocn = $order(^INCostCentreAuditLocn(0,pidSeq,idLocn))
			quit:idLocn=""
			
			set objAudit    = $get(^INCostCentreAuditLocn(0,pidSeq,idLocn,1))
			set objLocation = $get(^WWW0121(0,0,idLocn,1))
			
			do NewLine^COMTable()
			do InsertCell^COMTable(idLocn                                 ,idLink,idKey,strPara)
			do InsertCell^COMTable($$$WWW0121LocationName(objLocation)    ,idLink,idKey,strPara)
			do InsertCell^COMTable($$$INCostCentreAuditLocnOldCC(objAudit),idLink,idKey,strPara)
			do InsertCell^COMTable($$$INCostCentreAuditLocnNewCC(objAudit),idLink,idKey,strPara)
		}
		do Stop^COMTable()
	}
	
	quit
	
]]></Routine>
</Export>