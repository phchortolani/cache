<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPITEMLOCATION" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPITEMLOCATION
	
#include COMSYS
#include INConst
	
OnBeforeSave(pKey,pobjRecord)
    ;-------------------------------------------------------------------------------
    ; Prevent save if any of the day offsets are negative
    ; 
    ; If we can access the stock from other locations, we must be able to see the
    ; stock from these locations as well
    ;
    ; Returns:
    ;
    ; History:
    ; 19-Mar-2010	PPP		SR17202: Invalid Supply Location for current record
	; 24-Feb-2009	GRF		SR16394: Clarify idClassField rather than idField
	; 12-Jan-2007	PO		SR15344: Created
    ;-------------------------------------------------------------------------------
	new idClassField,strStatus
 
	set strStatus = $$$OK
 
	for idClassField = $$$FldINDRPITEMFirm,$$$FldINDRPITEMSource,$$$FldINDRPITEMRaise,$$$FldINDRPITEMPlace,$$$FldINDRPITEMConfirm,$$$FldINDRPITEMSupplier {
		if $piece(pobjRecord,Y,idClassField) < 0 {
			set strStatus = $$$MakeStatus("IN00638",$$^WWWFELDNAME(YFORM,"D",idClassField))
		} ; "%1 number of days must be greater than or equal to zero."
	}
	
	if $$$ISOK(strStatus) {
		for idClassField = $$$FldINDRPITEMDispatchPlanning,$$$FldINDRPITEMPicking,$$$FldINDRPITEMDistribution,$$$FldINDRPITEMDeliveryTime {
			if $piece(pobjRecord,Y,idClassField) < 0 {
				set strStatus = $$$MakeStatus("IN00638",$$^WWWFELDNAME(YFORM,"D",idClassField))
			//	quit
			} ; "%1 number of days must be greater than or equal to zero."
		}
	}
	
	if $$$ISOK(strStatus) {   //SR17202
		if $$$KEY1(pKey) = $$$INDRPITEMSupplyLocation(pobjRecord) {
			set strStatus = $$$MakeStatus("IN01215")
		} ; "Invalid 'Supply Location' for current record"
	}
		
	if $$$ISERR(strStatus) {
		$$$DecomposeStatus(strStatus)
		$$$Alert(strStatus)
		set Q = $$$QDontSave
	}
	quit
	
	
OnBlur(pidClassField,pobjRecord)
	;-------------------------------------------------------------------------------
	; Validate data entry on blur
	;
	; Params:
	; pidClassField - Field Id
	; pidRecord     - Record
	; 
	; Returns: Nothing
	;
	; History:
	; 25-Jan-2010	shobby	SR15753: Removed redundant code (core rules)
	; 24-Feb-2009	GRF		SR16394: Clarify pidClassField rather than pidField;
	; 							leave idField as is to better differentiate
	; 12-Jan-2007	PO		SR15344: Created
	;-------------------------------------------------------------------------------
	new idField,intDays

	set intDays = 0
 
	for idField = $$$FldINDRPITEMFirm,$$$FldINDRPITEMSource,$$$FldINDRPITEMRaise,$$$FldINDRPITEMPlace,$$$FldINDRPITEMConfirm,$$$FldINDRPITEMSupplier {
		set intDays = intDays + $piece(pobjRecord,Y,idField)
	}
 
	if intDays = 0 set intDays = ""
	set $$$INDRPITEMSOrderTimeProductionTime(pobjRecord)=intDays
 
	for idField = $$$FldINDRPITEMDispatchPlanning,$$$FldINDRPITEMPicking,$$$FldINDRPITEMDistribution,$$$FldINDRPITEMDeliveryTime {
		set intDays = intDays + $piece(pobjRecord,Y,idField)
	}
 
	if intDays = 0 set intDays = ""
	set $$$INDRPITEMSEntireRunTime(pobjRecord)=intDays
	
	quit
	
]]></Routine>
</Export>