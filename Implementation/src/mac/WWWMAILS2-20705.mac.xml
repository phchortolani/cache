<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMAILS2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMAILS2(SRVR,SNDR,EMPF,SUBJ,ZUG1,ENCD,FNAM,ZUG2) ;WWWMAILS;FUH;dieses Programm ist ein SMTP Mailer 20.03.2000  ;;:GEAEND.:10.04.2000 ;this Program is uni- 
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		dieses Programm ist ein SMTP Mailer 20.03.2000  
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FUH	dieses Programm ist ein SMTP Mailer 20.03.2000  
	;|
	;\------------------------------------------------------------------/
	;
	;Peter Fuhrmann, FUH 5.4.2000
	;zuletzt geändert: FUH 6.4.2000
	;;SRVR = TCP/IP Adresse des e-Mail Servers, z.B.: "172.16.0.2"
	;;SNDR = e-Mail Adresse des Absenders, z.B.: "fuhrmann@bewidata.de" 
	;;EMPF = e-Mail Adresse des Empfängers, z.B.: "schoenberger@dmv.de" 
	;;SUBJ = Subject/Betreff, z.B.: "Hallo"
	;;ZUG1 = Name des Arrays, der die Nachricht enthält, z.B.: "LOCAL" oder "^GLOBAL(""ABC"")" 
	;;;der Inhalt kann in ARRAY(1..N) oder in ARRAY(1..N,1) enthalten sein
	;;ENCD = Encoding des Anhangs: "7BIT", "8BIT", "BASE64"
	;;FNAM = Filename, den der Anhang bekommen soll, z.B.: "ANHANG01.TXT"
	;;ZUG2 = Wenn die Nachricht als Anhang verschickt wird, der Name des Arrays, der eine textliche Zusatzinformation enthält. z.B.: "TEXT" oder "^GLOBAL(""XYZ"")" 
	NEW A,BOUNDARY,DEBUG,DEV,FLAG,GLO1,GLO2,I,KOPF,TIME,UTC
	SET DEBUG=0
	IF $GET(SRVR)="" QUIT "kein SMTP Server"
	IF $GET(SNDR)="" QUIT "anonym ist verboten"
	IF $GET(EMPF)="" QUIT "ohne Empfänger ist Unsinn" 
	;;I SUBJ="" ;kein Betreff ist nicht schlimm
	IF $GET(ZUG1)="" SET ZUG1="TEXT" ;nimm Local Array TEXT, wenn keine Angabe
	SET A=$QUERY(@ZUG1) 
	IF A'[$PIECE(ZUG1,")") QUIT "Nachricht ist leer"
	SET FLAG=$LENGTH(A,",")-$LENGTH(ZUG1,",")-(ZUG1["(") ;1=TEXT(1,1),TEXT(2,1) oder 0=TEXT(1),TEXT(2)
	;Wenn kein genehmigtes Encoding, dann schick gar nichts
	SET ENCD=$GET(ENCD)
	IF ENCD'="" DO  IF ENCD="" QUIT "kein base64, 7Bit oder 8Bit Encoding"
	. IF $GET(FNAM)="" SET FNAM="FILE0001.DAT"
	. SET ENCD=$TRANSLATE(ENCD,"abest","ABEST")
	. IF ENCD="BASE64" SET ENCD="base64" QUIT
	. IF ENCD="8BIT" QUIT
	. IF ENCD="7BIT" QUIT
	. SET ENCD=""
	. QUIT 
	IF '$DATA(YSYST) SET YSYST="CACHE"
	SET UTC="+0000" ;Universal Time
	;hier Caché spezifisch ;here is Caché specific 
	IF $PIECE(YSYST,",")="CACHE" DO
	. SET DEV="|TCP|25"
	. OPEN DEV:(SRVR:25) 
	. SET UTC=$JUSTIFY(($PIECE($H,",",2)-$PIECE($ZTS,",",2))/3600,0,0)*100
	. SET UTC="+"_$EXTRACT(10000+UTC,2,5)
	. IF UTC<0 SET UTC="-"_$EXTRACT(10000-UTC,2,5)
	. SET TIME=$ZDATE($H,11)_", "_$ZDATETIME($H,2)_" "_UTC
	. QUIT 
	DO PREPARE
	;Verbindungsaufbau
	SET A=$$READ IF A'["220 " QUIT A
	DO SEND("RSET") 
	SET A=$$READ() 
	IF A'["250 " QUIT A
	DO SEND("HELO "_$PIECE(SNDR,"@",2)) 
	SET A=$$READ() 
	IF A'["250 " QUIT A
	DO SEND("MAIL FROM: "_SNDR) 
	SET A=$$READ() IF A'["250 " QUIT A
	DO SEND("RCPT TO: "_EMPF) 
	SET A=$$READ() 
	IF A'["250 " QUIT A
	DO SEND("DATA") 
	SET A=$$READ() 
	IF A'["354 " QUIT A
	;Zuerst allgemeiner Header
	FOR I=1:1 QUIT:'$DATA(KOPF(0,I))  DO SEND(KOPF(0,I))
	;normaler Textteil
	IF GLO1'="" IF $DATA(@GLO1)>1 DO
	. FOR I=1:1 QUIT:'$DATA(KOPF(1,I))  DO SEND(KOPF(1,I))
	. FOR I=1:1 QUIT:'$DATA(@GLO1@(I))  DO SEND($SELECT(FLAG=0:@GLO1@(I),1:@GLO1@(I,1)))
	. QUIT
	;Anhang ;supporters 
	IF GLO2'="" IF $DATA(@GLO2)>1 DO
	. FOR I=1:1 QUIT:'$DATA(KOPF(2,I))  DO SEND(KOPF(2,I))
	. FOR I=1:1 QUIT:'$DATA(@GLO2@(I))  DO SEND($SELECT(FLAG=0:@GLO2@(I),1:@GLO2@(I,1)))
	. QUIT
	;Ende ;Termination 
	IF BOUNDARY'="" DO SEND("--"_BOUNDARY_"--")
	DO SEND($CHAR(13,10)_".") SET A=$$READ() IF A'["250 " QUIT A
	DO SEND("QUIT") SET A=$$READ()
	CLOSE DEV
	IF ENCD="base64" KILL @GLO2 ;lösche ^WWWSOR("BASE64","$H,$R(10000)")
	QUIT 1 
	
SEND(A) ;
	USE DEV
	WRITE A,$CHAR(13,10),!
	IF DEBUG USE 0 WRITE A,!
	QUIT  
	
READ() ;
	USE DEV
	READ A:3
	IF DEBUG USE 0 WRITE A
	QUIT A 
	
PREPARE ;
	SET BOUNDARY=""
	IF ENCD'="" SET BOUNDARY="-----?<<###____next_part____###>>!-----"
	SET GLO1=ZUG1,GLO2=""
	SET KOPF(0,1)="From: "_SNDR
	SET KOPF(0,2)="To: "_EMPF
	SET KOPF(0,3)="Subject: "_SUBJ
	SET KOPF(0,4)="X-Mailer: WWWMAILS "
	SET KOPF(0,5)="MIME-Version: 1.0"
	SET KOPF(0,6)="Content-Type: multipart/mixed; boundary="""_BOUNDARY_""""
	IF BOUNDARY="" SET KOPF(0,6)="Content-Type: text/plain; charset=ISO-8859-1"
	SET KOPF(0,7)="Date: "_TIME
	SET KOPF(0,8)="Message-ID: <"_$H_">"
	SET KOPF(0,9)=""
	SET KOPF(0,10)=""
	IF BOUNDARY'="" DO 
	. ;hier Header für Bodypart 1 plain Text ;here is to Text 
	. SET KOPF(1,1)="--"_BOUNDARY
	. SET KOPF(1,2)="Content-Type: text/plain; charset=ISO-8859-1"
	. SET KOPF(1,3)="Content-Transfer-Encoding: 8BIT" 
	. SET KOPF(1,4)=""
	. ;
	. ;hier Header für Bodypart 2 Anhang ;here is to supporters 
	. SET KOPF(2,1)="--"_BOUNDARY
	. SET KOPF(2,2)="Content-Type: application/octet-stream; name="_FNAM
	. SET KOPF(2,3)="Content-Transfer-Encoding: "_ENCD
	. SET KOPF(2,4)=""
	. SET GLO2=ZUG1
	. SET GLO1=$GET(ZUG2)
	. QUIT
	IF ENCD="base64" DO
	. NEW KNO
	. ;finde einen Unique Knoten für diese Aufgabe ;snarl to this task 
	. FOR  SET KNO=$H_","_$RANDOM(100000) SET GLO2="^WWWSOR(""MAIL"","""_KNO_""")" QUIT:'$DATA(@GLO2)  hang 2
	. DO ^WWWBAS64(ZUG1,GLO2)
	. QUIT
	QUIT
]]></Routine>
</Export>