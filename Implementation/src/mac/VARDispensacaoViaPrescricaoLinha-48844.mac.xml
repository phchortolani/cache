<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARDispensacaoViaPrescricaoLinha" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARDispensacaoViaPrescricaoLinha
	#include COMSYS
	#include COMConst
	#include MEDConst
	#include INConst
	#include %occInclude
	
	#define Verde 1
	#define Amarelo 2
	#define Vermelho 3
	
	quit
	
OnBeforeFormat(pYFIELDNAME,pYTEXT,pYFELD)
	;-------------------------------------------------------------------------------
	; OnBeforeFormat
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 27-Oct-2014	SCR		HEVA-1580: Calculate Status based on Time
	;-------------------------------------------------------------------------------
	new dtEnd,dtStart,idDispSta,idField,idKey,intCol,intRow,linhaValidadas,objMEDPrescription
	new status,strTimes

	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	
	set idField = $$$GetClassField(YFORM,intCol)
    set idKey  = $$ReferenceKey^COMGridEdit31Interface(YFORM,intRow)

    set status = $$CheckUrgent(idKey)
    set linhaValidadas = $$CheckLinhasValidadas(idKey)
    set objMEDPrescription = $get(^MEDPrescription(YM,idKey,1))
	
	if (idField = 1) {
		if (status = $$$Vermelho) {
			set YBACKGROUNDCOLOR = "RED"
		} 
		else {
			if ($$$MEDPrescriptionStatus(objMEDPrescription) = 2) {
				set YBACKGROUNDCOLOR = "LIGHTGREEN"
			}
			else {
				if (linhaValidadas = $$$Verde) {
					set YBACKGROUNDCOLOR = "LIGHTGREEN"
				} elseif (linhaValidadas = $$$Amarelo) {
					set YBACKGROUNDCOLOR = "YELLOW"
				}
			}
		}
		set strTimes	= $get(^WWWDATEN(0,+$h,YUSER,"VARDispensacaoViaPrescricao","M",1))
		set dtStart		= $piece(strTimes,Y,1)
		set dtEnd		= $piece(strTimes,Y,2)
		if idKey'="" {
			if (dtStart'="")&&(dtEnd'="") {
			    if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
					do SetTime^VARDispensacaoViaPrescricaoLinha(.dtStart,.dtEnd) ; Convert Time to Date and Time
		    	}
				do DoseStatusTime^MEDPrescription(idKey,dtStart,dtEnd,.idDispSta)
				set:'idDispSta idDispSta=1 ; Default to awaiting validation
				set YTEXT	= $$GetDescription^WWWStatus("MEDPrescription",+idDispSta,$get(SPRACHE))
				
			}
		}
		; set YTEXT="xxx"
	}
	
	quit
	
CheckUrgent(pNumPrescricao)
	new objPrescLine, idLinha, flagUrgente
	set flagUrgente = $$$NO
	set idLinha = ""
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,pNumPrescricao,idLinha))
		quit:idLinha=""
		
		set objPrescLine = $get(^MEDPrescriptionLine(YM,pNumPrescricao,idLinha,1))
		
		if ($$$MEDPrescriptionLineUrgent(objPrescLine) > 0){
			set flagUrgente = $$$Vermelho
			quit
		}
	}
	
	quit flagUrgente
	
CheckLinhasValidadas(pNumPrescricao)
	new objPrescLine, idLinha, flagValidacao
	set flagValidacao = $$$Verde
	set idLinha = ""
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,pNumPrescricao,idLinha))
		quit:idLinha=""
		
		set objPrescLine = $get(^MEDPrescriptionLine(YM,pNumPrescricao,idLinha,1))
		
		if ($$$MEDPrescriptionLineStatus(objPrescLine) '= 2){
			set flagValidacao = $$$Amarelo
			quit
		}
	}
	
	quit flagValidacao
	
ValidarPrescricao()
	;-------------------------------------------------------------------------------
	; Validate Prescription
	;
	; Inputs: 
	;
	; Returns:
	;
	; History:
	; 14-Oct-2014	SCR		HEVA-1580: Pass in Times
	;-------------------------------------------------------------------------------
	new strStatus,NumPrescricao,strTimes,dtStart,dtEnd
	
	set NumPrescricao = $$$GRIDGetCurrentYKEY
	
	set strStatus = $$CanValidate^MEDPrescriptionHosp(NumPrescricao, $get(^MEDPrescription(YM,NumPrescricao,1)))

 	if $$$ISOK(strStatus) {
		set YBACK 	= $replace(YBACK,"VARDispensacaoViaPrescricaoLinha","VARDispensacaoViaPrescricao")
		set YFORM 	= "VARDispensacaoViaPrescricao"
		
		set strTimes	= $get(^WWWDATEN(0,+$h,YUSER,"VARDispensacaoViaPrescricao","M",1))
		set dtStart		= $piece(strTimes,Y,1)
		set dtEnd		= $piece(strTimes,Y,2)
		do SetTime(.dtStart,.dtEnd)
		set strStatus 	= $$Validate^MEDPrescriptionHosp(NumPrescricao,dtStart,dtEnd)
		
		if ($$$ISERR(strStatus)){
			$$$JS("alert('"_$$DecodeError^COMUtilError(strStatus,$$$YES)_"')")
			do GoToForm^COMUtilForm("VARDispensacaoViaPrescricao","")
			quit
		}
		
 	} elseif ($$$ISERR(strStatus)){
		$$$JS("alert('A prescrição não pode ser validada.')")
		do GoToForm^COMUtilForm("VARDispensacaoViaPrescricao","")
		quit
	} 

	quit
	
SetTime(&pdtStart,&pdtEnd,pdtCur="")
	;-------------------------------------------------------------------------------
	; Add Date to Times based on current time
	;
	; Inputs: 
	;
	; Returns:
	;
	; History: Created Pass in Times
	;-------------------------------------------------------------------------------
	new dtCurDate,dtCurTime,dtEndtDate,dtStartDate,intCurSecs,intEndSecs,intStartSecs
	new intWin
	
	set:pdtCur="" pdtCur	= $h
	set dtCurTime	= $piece(pdtCur,",",2)
	set dtCurDate	= $piece(pdtCur,",",1)
	set dtStartDate	= dtCurDate		; Asuume Current Day
	set dtEndtDate	= dtCurDate		; Assume Current Day
	set intCurSecs	= dtCurDate*86400+dtCurTime
	set intStartSecs= dtStartDate*86400+pdtStart
	set intEndSecs	= dtEndtDate*86400+pdtEnd
	set intWin		= 12*60*60
	
	if intStartSecs+intWin<intCurSecs  { ; If Start Time is Less than 12 Hours use next day
		set dtStartDate	=	dtCurDate+1
	} 
	if intStartSecs-intWin>intCurSecs  { ; If Start Time is more than 12 Hours use last day
		set dtStartDate	=	dtCurDate-1
	}
	
	
	if intEndSecs+intWin<intCurSecs  { ; If End Time is Less than 12 Hours use next day
		set dtEndtDate	=	dtCurDate+1
	} 
	if intEndSecs-intWin>intCurSecs  { ; If End Time is more than 12 Hours use last day
		set dtEndtDate	=	dtCurDate-1
	}
	set intStartSecs= dtStartDate*86400+pdtStart
	set intEndSecs	= dtEndtDate*86400+pdtEnd
	
	if intStartSecs>intEndSecs  { ; 
		set dtEndtDate	=	dtEndtDate+1
	}
	
	set:pdtStart'="" pdtStart	= dtStartDate_","_pdtStart
	set:pdtEnd'="" pdtEnd		= dtEndtDate_","_pdtEnd

	quit

DispensarPrescricao()
	new NumPrescricao,idDispense
	set NumPrescricao = $$$GRIDGetCurrentYKEY
	set idDispense = $order(^MEDDispenses(YM,1,$$$Index(NumPrescricao),""),-1)
	
	if (idDispense = ""){
		$$$JS("alert('Primeiro valide a dispensação.')")
		do GoToForm^COMUtilForm("VARDispensacaoViaPrescricao","")
		quit
	} elseif ((idDispense '= "") && ($$$MEDDispenseStatus($get(^MEDDispense(YM,idDispense,1))) = 1) ){
		$$$JS("alert('Primeiro valide a dispensação.')")
		do GoToForm^COMUtilForm("VARDispensacaoViaPrescricao","")
		quit
	} 
	
	set YBACK = $replace(YBACK,"VARDispensacaoViaPrescricaoLinha","VARDispensacaoViaPrescricao")
	set YFORM = "VARDispensacaoViaPrescricao"
	
	if ('$data(^MEDDispensePickLine(YM,idDispense))) {
		do RedirectForm^COMUtilForm("MEDDispense",$get(idDispense),YBACK,"",1)
		quit
	} else {
		do RedirectForm^COMUtilForm("MEDDispense",$get(idDispense),YBACK,"",2)
	}
	
	quit
	
VisualizarPrescricao()
	new NumPrescricao
	
	set YBACK = $replace(YBACK,"VARDispensacaoViaPrescricaoLinha","VARDispensacaoViaPrescricao")
	set YFORM = "VARDispensacaoViaPrescricao"
	set NumPrescricao = $$$GRIDGetCurrentYKEY
	do RedirectForm^COMUtilForm("MEDPrescriptionHosp",NumPrescricao,YBACK,YPARA,1)
	;do GoToForm^COMUtilForm("MEDPrescriptionHosp",NumPrescricao)
	quit
	
GetDispensacaoEmAberto()
	new NumPrescricao,idDispense,idSta,objDispense

	set NumPrescricao = $get(YKEY)
	set idDispense = ""
	
	for{
		set idDispense = $order(^MEDDispenses(YM,1,$$$Index(NumPrescricao),idDispense),-1)
		quit:idDispense=""
		
		set objDispense = $get(^MEDDispense(YM,idDispense,1))
		set idSta = $$$MEDDispenseStatus(objDispense)
	
		quit:idSta'=3
	}
	
	quit idDispense
	
GetMedPrescritos()
	;@$$GetMedPrescritos^VARDispensacaoViaPrescricaoLinha()
	new NumPrescricao,count,idLinha
	set count = 0
	set NumPrescricao = $get(YKEY)
	set idLinha = ""
	quit:NumPrescricao="" 0
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,NumPrescricao,idLinha))
		quit:idLinha=""
		
		set count = $i(count)
	}
	
	quit count
	
GetMedSemValidacao()
	new objPrescLine, idLinha, count, NumPrescricao
	set count = 0
	set NumPrescricao = $get(YKEY)
	set idLinha = ""
	quit:NumPrescricao="" 0
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,NumPrescricao,idLinha))
		quit:idLinha=""
		
		set objPrescLine = $get(^MEDPrescriptionLine(YM,NumPrescricao,idLinha,1))
		
		if ($$$MEDPrescriptionLineStatus(objPrescLine) = 1){
			set count = $i(count)
		}
	}
	
	quit count
	
GetMedAguardDispensacao()
	new objPrescLine, idLinha, count, NumPrescricao
	set count = 0
	set NumPrescricao = $get(YKEY)
	set idLinha = ""
	quit:NumPrescricao="" 0
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,NumPrescricao,idLinha))
		quit:idLinha=""
		
		set objPrescLine = $get(^MEDPrescriptionLine(YM,NumPrescricao,idLinha,1))
		
		if ($$$MEDPrescriptionLineStatus(objPrescLine) = 2){
			set count = $i(count)
		}
	}
	
	quit count

GetMedDispensados()
	new objPrescLine, idLinha, count, NumPrescricao
	set count = 0
	set NumPrescricao = $get(YKEY)
	set idLinha = ""
	quit:NumPrescricao="" 0
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,NumPrescricao,idLinha))
		quit:idLinha=""
		
		set objPrescLine = $get(^MEDPrescriptionLine(YM,NumPrescricao,idLinha,1))
		
		if ($$$MEDPrescriptionLineStatus(objPrescLine) = 9){
			set count = $i(count)
		}
	}
	
	quit count

GetRejectedLines()
	;-------------------------------------------------------------------------------
	; Rejected Lines from the Prescription
	;
	;
	; History:
	; 04-Jan-2013	PPP		HEVA-738: Created
	;-------------------------------------------------------------------------------
	new objPrescLine, idLinha, count, NumPrescricao
	set count = 0
	set NumPrescricao = $get(YKEY)
	set idLinha = ""
	quit:NumPrescricao="" 0
	
	for{
		set idLinha = $order(^MEDPrescriptionLine(YM,NumPrescricao,idLinha))
		quit:idLinha=""
		
		set objPrescLine = $get(^MEDPrescriptionLine(YM,NumPrescricao,idLinha,1))
		
		if ($$$MEDPrescriptionLineStatus(objPrescLine) = 10){
			set count = $i(count)
		}
	}
	
	quit count

]]></Routine>
</Export>