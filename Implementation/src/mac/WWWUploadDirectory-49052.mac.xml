<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWUploadDirectory" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWUploadDirectory
	
#include WWWConst
#include COMSYS
	
OnBlur(pstrValue,pidField) 
    ;-------------------------------------------------------------------------------
    ; Clear form/app if other is entered into
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 02-Feb-2010	shobby	SR15753: Redundant (Now with core rule)
    ; 24-Aug-2005	JW		SR12876: Created
    ;-------------------------------------------------------------------------------
	quit
	
	
OnBeforeSave(pYKEY="",pYFELD="") 
    ;-------------------------------------------------------------------------------
    ; Execute before save. Check whether:
    ; 	- directory exists
    ; 	- directory is already defined for the form/app
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 24-Aug-2005	JW		SR12876: Created
    ;-------------------------------------------------------------------------------
	new idField,idKey,strStatus,strValue
	
	set strStatus = $$$OK
	
	if '##class(%File).DirectoryExists($$$WWWUploadDirectoryDirectory(pYFELD)) {
		set strStatus = $listbuild("WWW00029",$$$WWWUploadDirectoryDirectory(pYFELD))
	 ; "Directory '%1' does not exist:"
	
	} else {
		for idField=1:1:3 {
			set strValue = $piece(pYFELD,Y,idField)
			if (strValue'="") && (strValue'=0) {
				
				set idKey = $order(^WWWUploadDirectorys(0,idField,$$$Index(strValue),""))	// Index is same as field num
				
				if (idKey'="") && (idKey'=pYKEY) {
					if idField = $$$FldWWWUploadDirectoryDefault1 {
						set strStatus = $listbuild("WWW00030",idKey)
					  ; "A default directory is already defined (see Code %1)"
					
					} else {
						set strStatus = $listbuild("WWW00031",idKey,strValue)
					} ; "A directory is already defined for %2 (see Code %1)"
				}
			}
			quit:$$$ISERR(strStatus)
		}
	}
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	quit
	
	
GetDirectory(pidForm="") 
    ;-------------------------------------------------------------------------------
    ; Get the upload directory for the current form
    ;
    ; Params: pidForm - current form
    ;
    ; Returns: string
    ;
    ; History:
    ; 04-Jun-2007	GRF		BR014417: Normalise path returned.
    ; 23-Aug-2005	JW		SR12876: Created
    ;-------------------------------------------------------------------------------
	new strDirectory,idKey,idModule
	
	set idKey=""
	
	if pidForm'="" {
		set pidForm = $$$Index(pidForm)
		set idKey = $order(^WWWUploadDirectorys(0,2,pidForm,""))			// Look up form name
		
		if idKey="" {
			set idModule = " "
			for {
				set idModule = $order(^WWWUploadDirectorys(0,1,idModule))	// Look up module
				quit:idModule=""
				
				if idModule = $extract(pidForm,1,$length(idModule)) {	        // Found
					set idKey = $order(^WWWUploadDirectorys(0,1,idModule,""))
				}
				quit:idKey'=""
			}
		}
	}
	
	if idKey="" {
		set idKey = $order(^WWWUploadDirectorys(0,3,$$$YES,""))			// Look up default
	}
	
	if idKey'="" {
		set strDirectory = $$$WWWUploadDirectoryDirectory($get(^WWWUploadDirectory(0,idKey,1)))
	} else {
		set strDirectory = $$$WWW012PhysicalFTPDirectory($get(^WWW012(0,0,1)))	// Physical FTP directory
	}
	
	quit ##Class(%File).NormalizeDirectory(strDirectory)
	
]]></Routine>
</Export>