<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARTFRReq" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARTFRReq
#include COMSYS
#include INConst
#include COMGridEdit31
#include %occInclude 


OnAfterPrimaryKey(pidReq,pobjReq,&pobjForm)
    ;-------------------------------------------------------------------------------
    ;
    ; History:
    ; 17-Oct-2013	SCR		SESDF-669: Created
    ;-------------------------------------------------------------------------------
	do DefinirVigencia(YFELD,YUSER)
	do OnAfterPrimaryKey^VARReq(pidReq,pobjReq,.pobjForm)
	set ^VARSESLocalAbastecedor(YUSER) =  $$$INReqToLocn(pobjReq)
	quit
	
CanProcess(pidReq,pobjReq)
    ;-------------------------------------------------------------------------------
    ;
    ; History:
    ; 18-Oct-2013	SCR		SESDF-669: Created
    ;-------------------------------------------------------------------------------
	new strStatus,idLine,objLine,idItem,idxItem,idToLoc,idFromLoc
	new idxToLoc,idxFromLoc,idReqSearch,objReqSearch,idMonth,idPriority
	
	set strStatus	= $$IsFirmable^INReq(YM,YFORM,pidReq,pobjReq)
	if $$$ISOK(strStatus) {
		set idToLoc		= $$$INReqToLocn(pobjReq)
		set idFromLoc	= $$$INReqFromLocn(pobjReq)
		set idMonth		= $$$INReqFREE16(pobjReq)
		set idPriority	= $$$INReqPriority(pobjReq)
		if idToLoc '= YLOCATION {
			set strStatus= $$$MakeStatus("VAR8894",$$$INReqToLocn(pobjReq))  ;Must be at Receiving Location %1 to Process this Requisition
		}
	}
	if $$$ISOK(strStatus) && '$Data(^INReqLine(YM,pidReq)) {
		set strStatus= $$$MakeStatus("VAR8895")  ;The request must have at least 1 item.
	}
	if $$$ISOK(strStatus) && (pidReq'="") {
		set idxToLoc	= $$$Index(idToLoc)
		set idxFromLoc	= $$$Index(idFromLoc)
		set idLine	= ""
		for {
			set idLine		= $order(^INReqLine(YM,pidReq,idLine))
			quit:idLine=""
			quit:'$$$ISOK(strStatus)
			set objLine		= $get(^INReqLine(YM,pidReq,idLine,1))
			if '$$$INReqLineQtyOrdered(objLine) {
				 set strStatus= $$$MakeStatus("VAR8893",idLine)  ;Quantity required for line %1
				 quit
			}
			continue:((idFromLoc'=5)&&(idFromLoc'=1)) ; Dont Look at Other Locs
			set idItem 		= $$$INReqLineItem(objLine)
			set idxItem		= $$$Index(idItem)
			set idReqSearch	= ""
			for {
				set idReqSearch		= $order(^INReqLines(YM,5,idxToLoc,idxItem,2,idxFromLoc,idReqSearch))
				quit:idReqSearch=""
				set objReqSearch	= $get(^INReq(YM,idReqSearch,1))
				if $$$INReqPriority(objReqSearch) = idPriority {
					if idPriority = 1 {
						//This validation has been removed as per SESDF-941
						//set strStatus	= $$$MakeStatus("VAR8896",idReqSearch,idItem)  ;There is already another request EMERGENCY number %1 still waiting for the product %2. Please remove this product such request or cancel the old order
						quit
					} else {
						if $$$INReqFREE16(objReqSearch) = idMonth {
							set strStatus	= $$$MakeStatus("VAR8897",idPriority,idMonth,idReqSearch,idItem)  ;There is already another request %1 effective in period %2, number %3, still waiting for the product %4. Please remove this product such request or cancel the old request
						}
					}
				}
			}
		}
	}
	
	$$$YQHandler(strStatus)
 	quit strStatus

CanCreateTransfer(pidReq)
    ;-------------------------------------------------------------------------------
    ;
    ; History:
    ; 22-Oct-2013	SCR		SESDF-669: Created
    ;-------------------------------------------------------------------------------
	new objReq,strStatus,idTransfer
	
	set strStatus	= $$$OK
	if pidReq = "" {
		set YQ = $$$YQDisable("")
		quit
	}
	
	set objReq	= $get(^INReq(YM,pidReq,1))
	if $$$INReqStatus(objReq)<2 {
		set strStatus	= $$$MakeStatus("VAR8898")  ;This request has not been processed
	} elseif $$$INReqStatus(objReq)=8 {
		set strStatus	= $$$MakeStatus("VAR8899") ;This request can not be executed because it was rejected by the supplier.
	} elseif $$$INReqStatus(objReq)=8 {
		set strStatus	= $$$MakeStatus("VAR8900")  ;This request can not be executed because it is already closed
	} else {
		if $$$INReqFromLocn(objReq) '= YLOCATION {
			set idTransfer = $order(^INTFRs(YM, 11, $$$Index(pidReq), "")) // Index 11 for field 26 - No. Solicitação
			if (idTransfer '= "") {
				set YQ = $$$YQEnable
			} else {
				set strStatus	= $$$MakeStatus("VAR8901")  ;Your Location not the place supplying Location and this request has not been effected.
			}
		}
	}
	$$$YQHandler(strStatus)
	quit strStatus
	
CreateTransfer(pidReq)
    ;-------------------------------------------------------------------------------
    ;
    ; History:
    ; 22-Oct-2013	SCR		SESDF-669: Created
    ;-------------------------------------------------------------------------------
	do FulfilRequisition^INReqIssueAsReq(pidReq,YFORM)
	quit
	
DefinirVigencia(YFELD,YUSER)
    ;-------------------------------------------------------------------------------
    ;
    ; Superseded , USE DefinirVigencia^VARReq
    ; History:
    ; 17-Oct-2013	SCR		SESDF-669: Created
    ;-------------------------------------------------------------------------------
	new currentdate,nextmonth,nextyear,nextdate,months
	kill ^VARSESSolicTransfVigencia(YM,YUSER)
	set months=$Listbuild("JAN","FEV","MAR","ABR","MAI","JUN","JUL","AGO","SET","OUT","NOV","DEZ")
	if YKEY="" {
		set currentdate=$piece($H,",",1)
		set nextmonth=$piece($H,",",1)
	} else {
		set currentdate=$piece(YFELD,Y,20)
		set nextmonth=$piece(YFELD,Y,20)
	}
		set currentdate=$ZD(currentdate,1)
		set nextmonth=$extract($ZD(nextmonth,1),1,2)
		if nextmonth=12 {
			set nextyear=$extract(currentdate,7,10)+1
			set nextmonth="01"
		} else {
			set nextyear=$extract(currentdate,7,10)
			set nextmonth=nextmonth+1
			IF $LENGTH(nextmonth)=1 set nextmonth="0"_nextmonth
		}
		set nextdate=nextyear_nextmonth_"01"
		set nextdate=$ZDH(nextdate,8)
		set nextdate=$ZD(nextdate,1)
		set nextdate=$LIST(months,($extract(nextdate,1,2)))_"/"_$extract(nextdate,7,10)
		set currentdate=$LIST(months,($extract(currentdate,1,2)))_"/"_$extract(currentdate,7,10)
		set ^VARSESSolicTransfVigencia(YM,YUSER,currentdate)=currentdate
		set ^VARSESSolicTransfVigencia(YM,YUSER,nextdate)=nextdate
	
	quit

]]></Routine>
</Export>