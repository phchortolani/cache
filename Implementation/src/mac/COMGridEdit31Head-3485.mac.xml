<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31Head" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31Head
	
#include WWWConst
#include COMConst
#include COMGridEdit31
#def1arg WWWDATEN(%args) ^WWWDATEN(0,+$horolog,YUSER,YFORM,"V",%args) 
	
#define LogR(%1,%2)	;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMGridEdit31Head("_%2_") : "_$zh $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Key Name : Key Value         [optional - ^CacheTemp(YUSER,"Grid","ShowKeys") ]    HEAD
	; Per Page: [   ]   Pages: .....                        [  ][  ][  ][  ][  ][  ]    Pages / Buttons
	;-------------------------------------------------------------------------------
	
HEAD(YDATEI,YKEY,YMAXKEY)	  ;TABLE HEADER
    ;-------------------------------------------------------------------------------
    ; Displays primary keys for this class along top of grid area.
    ; 
    ; Params:	YDATEI	- parent class
    ;			YKEY	- parent key
    ;			YMAXKEY	- number of grid keys
    ; 
    ; Returns:
    ;
    ; History:
    ; 04-Mar-2013	shobby	CORE-69.1: Additional DIV
    ; 12-Feb-2012	shobby	SR17724: Changes to support fixed columns
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 05-Jan-2010	GRF		SR15525: Call to $$Convert^WWWTR rather than $$^WWWTR
    ; 09-Aug-2009	GRF		Doco
    ; 25-May-2009	PPP		SR16565: Scanning Prompt for Grid, Only if grid is not read-only
    ; 28-Nov-2008	HQN		SR16194: Added support for OnBeforeButtonLine for 
    ; 							GridEdit forms
    ; 31-Oct-2006	JW		BR014279: Added parameters
    ; 31-Aug-2006	JW		SR14907: Switch for key display. Removed excess TH elements
    ; 19-Oct-2005	JW		SR11573: Paging
    ; 16-Dec-2004	shobby	Change parameter PARA to @PARA in $$KEYBUILD. (To evaluate
    ;							YM for example, rather than use "YM")
    ; 15-Dec-2004	Shobby	Put all primary keys on one line to save space.
    ;-------------------------------------------------------------------------------
	new YTEXT,YSEI,YZEILE,YSPL,YBBN,YTXT,YKLFN,YWIDTH,YINHALT,YTYP,REL,PARA,RELF,SUCH
	new YHEIGHT,YFONT,YFONTSIZE,YV
	
	$$$LogR("HEAD",YDATEI_";"_YKEY_";"_YMAXKEY)
	
	;write YCR,"<div>" ;CORE-69.1
	write YCR,"<table cellspacing=0 cellpadding=0 style='width:100%;'>"
	;write YCR,"<table cellspacing=0 cellpadding=0 style=' border:3px solid green;'>"   ; test

	set YHEIGHT   = 14
	set YFONT     = "arial"
	set YFONTSIZE = 8
	
	;---------------------------------------
	
	if $get(^CacheTemp(YUSER,"Grid","ShowKeys")) {		//SR14907
		write YCR,"<tr>"
		write YCR,"<th nowrap class='TH' style=' font:"_YFONTSIZE_"pt arial; height:"_YHEIGHT_"pt;'>"
		for YKLFN=1:1:YMAXKEY {
			quit:$piece(YKEY,",",YKLFN)=""
			
			write "<B>"_$$^WWWFELDNAME(YFORM,"P",YKLFN)_" : </B>"
			
			set YINHALT = $piece(YKEY,",",YKLFN)
			
			set YDAT  = $get(^WWW002(0,YDATEI,YKLFN,1))  ;DATENFELDDEFINITION
			set YTYP  = $piece(YDAT,Y,3)                 ; D3		$$$WWW002InputType()
			set REL   = $piece(YDAT,Y,8)                 ; D8		$$$WWW002RelationClass()
			set PARA  = $piece(YDAT,Y,9)                 ; D9		$$$WWW002RelationalPrimaryKeys()
			if PARA'="" if '$find(PARA,",") if $extract(PARA)'="""" if $get(@(PARA))="" write YTEXT quit
			if PARA'="" if $find(PARA,",") for YV=1:1  quit:$piece(PARA,",",YV,99)=""  if $extract($piece(PARA,",",YV))'="""" set:$get(@($piece(PARA,",",YV)))="" PARA=""  if PARA="" write YTEXT quit  ;KEY VORDATEI MIT MEHREREN VARIABLEN
			set RELF  = $piece(YDAT,Y,10)                ; D10		$$$WWW002RelationalDisplayItems()
			set YTEXT = $$GetLiteral^WWWTR(YTYP,YINHALT)
			
			if (REL="") || (+RELF=0) { ; 29-Mar-2005 JW - don't show if no relation
				write YTEXT
			
			} else {
				set SUCH = "^"_REL_"("""_YM_""""  ;ZUSAMMENBAU DER GLOBALREFERENZ
				if PARA'="" set SUCH = SUCH_","_$$^WWWKEYBUILD(@PARA)
				set SUCH = SUCH_","""_$translate(YTEXT,"""")_""",1)"
				if ($data(@SUCH)#2)=0 {
					write YTEXT
				} else {
					write YTEXT_"&nbsp;("_$piece(@SUCH,Y,RELF)_")"
				}
			}
			write YCR," &nbsp; &nbsp; &nbsp; &nbsp;"		//SR14907
		}
		write YCR,"</th></tr>"		//SR14907
	}
	
	;---------------------------------------
	
	write YCR,"<tr>"
 
	//SR16565 : Only if grid is not read-only
	if $get(^CacheTemp(YUSER,"Grid","Enabled")) {
		if $$IsScanReq^WWW120Scan(YFORM) {
			do ScanData(YFONT,YFONTSIZE)
		}
	}
	
	do Pages(YFONT,YFONTSIZE)		             // SR11573
	do OnBeforeButtonLine^COMGridEdit31Events()  // SR16194
	do Buttons(YKEY,YFORM,YFONTSIZE)			 // BR014279
	write YCR,"</tr>"
	
	;---------------------------------------
	
	write YCR,"</TABLE>"
	;write YCR,"</div>" ;CORE-69.1

	if '$$GridVersion^COMGridEdit31V2() write $$HeadStart()						;SR17724
	if $$GridVersion^COMGridEdit31V2() do HeadStart^COMGridEdit31V2()  	  		;SR17724
	quit 
	
	
ScanData(pstrFont,pintSize)
	;-------------------------------------------------------------------------------
	; Displays the Scan input field
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 17-Aug-2012	shobby	SR18084: Call to enterScan so scan will be triggered
	;								 by enter key.
    ; 25-May-2009	PPP		SR16565: Scanning Prompt for Grid
	;-------------------------------------------------------------------------------
	new strDefaultColor,strDisabledColor,strRequiredColor,strWarningColor
	
	$$$LogR("ScanData","")
	
	write YCR,"<th align=left width=200 nowrap style=' font:"_pintSize_"pt "_pstrFont_";'>"
	write "<b id='inputdataheader' title='"_"Scan"_"'>&nbsp;"_"Scan"_":&nbsp</b>" 
	write YCR,"<input id='inputdata' type=text size=25 style='font:"_pintSize_"pt "_pstrFont
 
	set strDefaultColor = $$CellColours^COMGridEditUtil(.strDisabledColor,.strRequiredColor,.strWarningColor)
    write ";' onkeydown='enterScan(event,this,"""_strDefaultColor_Y_strWarningColor_""")"
    write ";' onFocus='this.select();SetFocus("""");' onBlur='InputData(this,"""_strDefaultColor_Y_strWarningColor_""");' value="_""_">"  ;
	write YCR,"</th>"
 
 	//Create the Search Button for Manual Access
	do CreateSearchButton^COMGridEdit31Scan("INART",148,"search1")		//Search Class - INART, 148	- Search, "search1" - binoculors
	quit
	
	
Pages(pstrFont,pintSize)
	;-------------------------------------------------------------------------------
	; Displays the 'per page' input field and pages text
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 01-May-2009	shobby	SR16507: Give the perpageheader object an id.
	; 05-Dec-2008	HQN		SR16026: replaced GRIDPerPage macro usage
	; 08-Oct-2008	GRF		Split lines for readability
	; 14-May-2008	shobby	SRBR014943: GRIDPerPage (use $get)
	; 31-Aug-2006	JW		SR14907: Removed excess TH elements. Added pstrFont param
	; 20-Oct-2005	JW		SR11573: Created
	;-------------------------------------------------------------------------------
	$$$LogR("Pages","")
	
	write YCR,"<th align=left nowrap style=' font:"_pintSize_"pt "_pstrFont_";'>"
	write "<b id='perpageheader' title='"_$$$Text("Com00325")_"'>&nbsp;"_$$$Text("Com00230")_":&nbsp</b>"    ; "Per Page"
	write YCR,"<input id='perpage' type=text size=1 style='font:"_pintSize_"pt "_pstrFont
	write ";' onFocus='SetFocus("""");' onBlur='PerPage(this);' value="_$$GetRowsPerPage^COMGridEdit31(YBED,YFORM)_">"
	write YCR,"<B>&nbsp; "_$$$Text("Com00229")_"</b>:"                                ; "Pages"
	write YCR,"<span id='pages'></span></th>"		            // This is where the page numbers are displayed
	quit
	
	
Buttons(pstrKey,pYFORM,pYFONTSIZE)
    ;-------------------------------------------------------------------------------
    ; Displays standard buttons as part of the grid.
    ; 
    ; Params:	pstrKey		- parent key
    ;			pYFORM		- Grid (child) form
    ;			pYFONTSIZE	- font size
    ; 
    ; Returns:
    ;
    ; History:
    ; 01-Mar-2013	shobby	CORE-69.2 Still require StoreFocus for buttons on forms such as
    ;							INReqIssue or VARDispensacaoViaPrescricao
	; 19-Feb-2013	shobby	CORE-69: Don't set focus to grid, it will cause scrolling with
	;							long grids that causes click events to be lost.
    ; 23-Feb-2012	shobby	SR17724: Missing single quote from previous change.
    ; 14-Feb-2012	shobby	SR17724: Reduce the amount of space that buttons occupy
    ; 06-Apr-2011	GRF		SR17687: remove "private" setting since called externally
    ; 06-Jul-2010	FIS		SR17402: get focus for Firefox when button click
    ; 15-May-2010	shobby	SR17253: Firefox alignment of buttons in grid.
    ; 21-Oct-2009	shobby	SR16969: Put in the ability to hide the popup (Grid Layout) button.
    ; 21-Jul-2009	shobby	SR16730: Button to start popup form.
    ; 02-Apr-2009	GRF		Fix prefixes
    ; 08-Apr-2008	shobby	SRBR014446: ExportToExcel button should always be
    ; 							enabled regardless of whether form is readonly.
    ; 25-Feb-2008	shobby	SRBR014446: Export To Excel button.
    ; 13-Dec-2007	shobby	SRBR014601: Moved call to DoNotDisplay into WWWFORMX
    ; 12-Dez-2007	GM		SRBR014601: Call routine that checks characters in Form
    ; 							and Customisation
    ; 30-Mar-2007	RPW		SRBR014416: Create the new Toogle Search button for
    ; 							grids that require it.
    ; 31-Oct-2006	JW		BR014279: Added parameters
    ; 14-Sep-2006	JW		SR14960: Pass YVOR into Buttons
    ; 31-Aug-2006	JW		SR14907: Removed excess TH
    ; 10-Apr-2006	JW		SR14421: Added copy button
    ; 10-Apr-2006	JW&RW	SR14457: Reverted SR14093
    ; 03-Feb-2006	RPW		SR14093: Allow delete if the grid is locked when required.
    ; 21-Oct-2005	JW		SR11573: Added search
    ; 15-Mar-2005	shobby	passed YVOR parameter into CreateButton.
    ; 01-Mar-2005	JW		Added 'deleteable' hidden field
    ; 28-Jan-2005	PO		SR11328: added table data tags around hidden input field
    ; 01-Nov-2004	JW		SR11066: Added custom buttons (from subform)
    ; 29-Oct-2004	Shobby	Created
    ;-------------------------------------------------------------------------------
	new blnAddLines,blnCopy,blnDelLines,blnEnabled,blnSearch,blnSeparator
	new strHideButtonList,YWIDTH,YHEIGHT,YVOR
	
	$$$LogR("Buttons",pstrKey_";"_pYFORM)
	
	if pYFORM'="" {
		set YHEIGHT = " 1pt "
		set YWIDTH  = YHEIGHT
		write YCR,"<th nowrap class='THbtns'>"
		write YCR,"<table align='right' style='border-collapse:collapse;'>"			;SR17253 ;SR17724
		write YCR,"<tr onMouseDown='StoreFocus();'>"  ;CORE-69.2
		;CORE-69 (Disabled) write YCR,"<tr onMouseDown=""document.getElementById('gridDIV').focus(); StoreFocus();"">" //SR17402
		
		;Buttons can be configured on or off.
		set YVOR = $$$GRIDYVOR(pYFORM)
		set strHideButtonList = ";"_$$$WWW120DoNOTDisplayStandardButto(YVOR)_";"
	 	
		set blnEnabled  = $$Enabled^COMGridEdit31G(YVOR,"","")
		set blnAddLines = ($find(strHideButtonList,";1;")=0)
		set blnDelLines = ($find(strHideButtonList,";5;")=0)
		set blnSearch   = ($find(strHideButtonList,";9;")=0)
		set blnCopy     = ($find(strHideButtonList,";15;")=0)
				
		if blnAddLines  do CreateButton("New",   YVOR,blnEnabled)
		if blnDelLines  do CreateButton("Delete",YVOR,blnEnabled)
		if blnSearch    do CreateButton("Search",YVOR,$$$YES)
		if blnCopy      do CreateButton("Copy",  YVOR,blnEnabled)
		if $get(^CacheTemp(YUSER,"Grid","COMView")) {
			do CreateButton("ToggleSearch",YVOR,$$$YES)
		}
 		if '$find(strHideButtonList,";17;")           do CreateButton("Popup",  YVOR,blnEnabled)	;16730 ;16969
		if $$AddExportToExcel(YVOR,strHideButtonList) do CreateButton("ExportToExcel",YVOR,$$$YES)
		
		; Are add / delete lines allowed?
		write YCR,"<TD>"
		write "<input type=hidden id=""addnew"" value="_(blnAddLines && blnEnabled)_">"
		write "</TD>"
		
		write YCR,"<TD>"
		write "<input type=hidden id=""delete"" value="_(blnDelLines && blnEnabled)_">"
		write "</TD>"
		
		set blnSeparator = (blnAddLines || blnDelLines || blnSearch || blnCopy)
 
		do CustomButtons(pstrKey,blnSeparator,pYFORM,YVOR)
		
		write YCR,"</tr>"
		write YCR,"</table>"
		write YCR,"</th>"
	}
	quit
	
	
AddExportToExcel(pYVOR,pstrHideButtonList)
	;-------------------------------------------------------------------------------
	; Can we add the 'Export To Excel' button to a grid form?
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 08-Apr-2008	GRF		SRBR014446: Fixed logic in $find
	; 25-Feb-2008	shobby	SRBR014446: Created
	;-------------------------------------------------------------------------------
	new blnResult
	
	set blnResult = $$$NO
	if $$$WWW120FormType(pYVOR)=12 {        ; Only applies to Edit Grids
		if '$find(pstrHideButtonList,";16;") set blnResult = $$$YES
	}
	quit blnResult
	
	
CustomButtons(pstrKey,pblnAddSep,pidForm,pobjForm) private
    ;-------------------------------------------------------------------------------
    ; Copied/modified from WWWFORM1
    ; Prints buttons from grid form to screen.
    ; 
    ; Param:	pstrKey		- parent key
    ;			pblnAddSep	- Whether or not there should be a separator before the custom buttons
    ;			pidForm		- Grid (child) form id
    ;			pobjForm	- Grid (child) form object
    ;
    ; Returns: nil
    ;
    ; History:
    ; 31-Oct-2006	JW		BR014279: Added parameters, removed extra 1. Moved check
    ;							to WWWFORMB
    ; 14-Sep-2006	JW		SR14960: Set YFOART, so never display extra buttons
    ; 24-Jun-2005	JW&SH	SR12779: Always display buttons on the grid. Never a menu.
    ; 22-Jun-2005	shobby	SR12655: Create a fake valid looking YKEY so that 
    ;							WWWFORMC will not display the only when data record
    ;							selected message for grid buttons. 
    ; 30-Nov-2004	JW		Created (SR11066)
    ;-------------------------------------------------------------------------------
	new YKEY,YFOART,YFELD
	
	$$$LogR("CustomButtons",pstrKey)
	
	set YFELD = ""		// Not sure if this is required - JW 
	set YKEY  = pstrKey
	
	if $data(^WWW124s(0,2,$$$Index(pidForm),SPRACHE)) {
		if $$MenuType^WWW013()'=14{
			if pblnAddSep {
				write "<TD WIDTH=3>"
				write "<IMG SRC="""_YGIF_"delimiter.gif"">"
				write "</TD>"
			}
		}
		set YFOART = $$$WWW120FormType(pobjForm)
		do ^WWWFORMC  ;BUTTON
	}
	quit
	
	
CreateButton(pstrType,pYVOR,pblnEnabled=$$$YES)
    ;-------------------------------------------------------------------------------
    ; Displays the standard buttons on the grid.
    ;
    ; History:
    ; 13-Sep-2010	SH(GRF)	SR17523: objDIV not objDiv
    ; 21-Jul-2009	shobby	SR16730:	New button to start the popup properties form.
    ; 08-Apr-2008	GRF		SRBR014446: property.gif rather than grid.gif - AGAIN!!!
    ; 07-Apr-2008	shobby	SRBR014446: pblnEnabled test was missed for 'Export To
    ; 							Excel' button
    ; 07-Apr-2008	GRF		SRBR014446: property.gif rather than grid.gif
    ; 25-Feb-2008	shobby	SRBR014446: Create the 'Export To Excel' function.
    ; 30-Mar-2007	RPW		SRBR014416: Add new internal button Toggle Search
    ; 10-Apr-2006	JW		SR14421: Added copy button
    ; 03-Feb-2006	RPW		SR14093: Created disnew.gif to show new as disabled.
    ; 21-Oct-2005	JW		SR11573: Added search
    ; 15-Mar-2005	shobby	Created (Wrapper for CreateButtonEx).
    ;-------------------------------------------------------------------------------
	$$$LogR("CreateButton",pstrType_";"_pYVOR_";"_pblnEnabled)
	
	if pstrType=      "New"           { do CreateButtonEx(pYVOR,"BUTTON_GRID_NEW",33946,45,$select(pblnEnabled:"new",1:"disnew"),pblnEnabled)
	} elseif pstrType="Delete"        { do CreateButtonEx(pYVOR,"BUTTON_GRID_DEL",33945,46,$select(pblnEnabled:"del",1:"disdel"),pblnEnabled)
	} elseif pstrType="Search"        { do CreateButtonEx(pYVOR,"BUTTON_GRID_SEARCH",148,-1,"search",pblnEnabled)
	} elseif pstrType="Copy"          { do CreateButtonEx(pYVOR,"BUTTON_GRID_COPY",39,-2,"copy",pblnEnabled)
	} elseif pstrType="ToggleSearch"  { do CreateButtonEx(pYVOR,"BUTTON_GRID_TOGGLE","Com00291","","format",pblnEnabled,"objDIV")
	} elseif pstrType="ExportToExcel" { do CreateButtonEx(pYVOR,"BUTTON_GRID_EXPORTTOEXCEL",$$$SysEnum("BUTTON",16),-3,"property",pblnEnabled)
	} elseif pstrType="Popup" 		  { do CreateButtonEx(pYVOR,"BUTTON_GRID_POPUP","Popup",-4,"data",$$$YES)        ;SR16730
	}
	quit
	
	
CreateButtonEx(pYVOR,pid,pidText,pintpruef,pstrGIF,pblnEnabled,pstrToggle)
    ;-------------------------------------------------------------------------------
    ; Displays the standard buttons on the grid.
    ;
    ; History:
    ; 14-Aug-2013	shobby	CORE-233.7: SetFocus to grid when button pressed.
    ; 06-Jun-2013	shobby	CORE-122: Manage the cursor when mouse is over the buttons.
    ; 02-Feb-2011	shobby	SR17086.1: pruef has become pruefg in grids because we
    ;							want to keep pruef from the main form.
    ; 02-Apr-2009	GRF		use blnButtonPictures - single check
    ; 19-Nov-2007	shobby	SRBR014790: No longer necessary to do the CallBackNow
    ; 16-Oct-2007	shobby	SR15237: Breadcrumb so that COMView knows which form is
    ;							relevant when popping out of an Edit Grid.
    ; 30-Mar-2007	RPW		SRBR014416: Modified to create correct javascript to
    ;							toggle search button
    ; 21-Jun-2005	shobby	SR12409: Put an ID on the button.  Used later to determine
    ;							if a button has been pressed that caused the grid to
    ;							receive focus.
    ; 15-Mar-2005	shobby	Replace CreateNewButton, CreateDeleteButton & CreateFCButton
    ; 23-Nov-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
	new blnButtonPictures
	
	set blnButtonPictures = (+$$$WWW120PicturesAsButtons(pYVOR)=$$$YES)  ; D45
	
	if YBED="SHOBBY" {
		write $$OpenTD^WWWFORMCOMMON($$$NO)
	} else {
		if $$MenuType^WWW013()=14{
			write "<TD WIDTH=30>"
		}else{
			write "<TD WIDTH=10"
			if pblnEnabled && blnButtonPictures write " class=""coolButton"""  ; MOUSEEFFECT
			write ">"
		}
	}
	
	if blnButtonPictures {
		write "<A ID=""CLICK_"_pid_""" HREF='#'"
	} else {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(pidText))_""""
	}
	
	if pblnEnabled {
		if pintpruef'="" {
			write " onClick='JavaScript:"
			write " SetFocus(""Grid""); pruefg("_pintpruef_");'" ;17086.1 ;CORE-233.7
		} else {
			write " onClick='javascript:ToggleHeader("""_pstrToggle_""");'"
		}
	}
	
	write ">"
	if blnButtonPictures {  ; NEUANLAGE
		;CORE-122 vvvv
		set icone=""
		if $$MenuType^WWW013()=14{
			set icone = $$traducaoIcone^WWWFORMCOMMON(pstrGIF_".gif")
		}
		if icone'=""{
			if $extract(icone,1,2)'="fa"&&($length(icone,"-")<2){
				if 'pblnEnabled set corBotao="secondary"
				write "   <div class='btn btn-outline-"_corBotao_" btn-sm "_$select(pblnEnabled'=1:"disabled",1:"")_"' id="""_pid_""""
				write " style='padding: 2px 5px 0px 5px;height:23px;vertical-align:middle;text-align:' "
				write " title="""_$$^WWWTEXT(pidText)_""" border=0>"
				write icone
				write "</div>"
			}else{
				if 'pblnEnabled set corBotao="secondary"
				write "   <div class='btn btn-outline-"_corBotao_" btn-sm "_$select(pblnEnabled'=1:"disabled",1:"")_"' ID="""_pid_""""
				write " style='width:28px; "
				if pblnEnabled {
					write "cursor:pointer;"
				} else {
					write "cursor:default;"
				}
				write "' "
				write "TITLE="""_$$^WWWTEXT(pidText)_""" border=0>"
				write "<i class='"_icone_"'></i>"
				write "</div>"
			}
		}else{			
			write YCR,"<IMG"
			if pblnEnabled {
				write " style='cursor:pointer;'"
			} else {
				write " style='cursor:default;'"
			}
			write " ID="""_pid_""" SRC="""_YGIF_pstrGIF_".gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(pidText)_""" border=0 enabled=false></A>"
			;write YCR,"<IMG ID="""_pid_""" SRC="""_YGIF_pstrGIF_".gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(pidText)_""" border=0 enabled=false></A>"
			;CORE-122 vvvv
		}
	}
	if YBED="SHOBBY" {
		write $$CloseTD^WWWFORMCOMMON()
	} else {
		write YCR,"</TD>"
	}
	quit
	
	
HeadStart()

	;-------------------------------------------------------------------------------
	; Start table
	;
	;  DIV:        gridheadDIV
	; 		TABLE:  gridhead
	; 		TABLE:  gridhead2		- additional charges
	;
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 06-Jul-2013	shobby	CORE-142.1: Float left is not good in Firefox.  Causes the header to refuse
	;							to expand past the end of the screen.
	; 27-Jun-2013	shobby	CORE-126.2: Float the grid left to fix an issue with column dragging.
	; 16-Mar-2012	shobby	SR17724.29: Formatting issues.
	; 08-Mar-2012	shobby	SR17724.26: Place for storage of the number of fixed columns
	;							on the expanded lines.
	; 19-Apr-2011	shobby	SR17724: Removed some old comments.
	; 02-Mar-2011	shobby	SR17673: Removed unused nonexistent property 'resizing'
	; 28-Feb-2011	shobby	SR17628: Removed overflow:hidden.  Header would be cut-off.
	; 30-Jun-2010	SHOBBY	SR17325.2: changed DIV to "table-layout:auto" with
	;							"display:-moz-box;"
	; 29-Jun-2010	GRF		SR17377: Apply "table-layout:fixed" to div and table
	; 06-Sep-2006	JW		SR14484: Moved 2nd header from Head^COMGridEdit31Body
	;-------------------------------------------------------------------------------
	new strHTML,idFORMEXPAND ;SR17724.29
	
	$$$LogR("HeadStart","")
	
	set idFORMEXPAND=$get(^WWWDATEN(0,+$h,YUSER,YFORM,"V","LINKS"))
	set strHTML = ""
	set strHTML = strHTML_"<DIV id='gridheadDIV' class='gDIVhd' style='"_$select(YUSERAGENT'="MSIE":"",1:"float:left;")_" display:-moz-box; table-layout:auto; position:relative; wiqdth:gridbodyDIV.clientWidth; z-index:0;'>" ;SR17628 ;SR17724 ;CORE-126.2 ;CORE-142.1
	write "						<INPUT id='GridVersion' TYPE='HIDDEN' value="_$$GridVersion^COMGridEdit31V2()_">",!
	write "						<INPUT id='FixedColumns' TYPE='HIDDEN' value='"_$$FixedColumns^COMGridEdit31V2(YFORM)_"'>",!
	if idFORMEXPAND'="" {	;SR17724.12
		write "						<INPUT id='FixedColumnsExpanded' TYPE='HIDDEN' value='"_$$FixedColumns^COMGridEdit31V2(idFORMEXPAND)_"'>",! ;SR17724.12
	}														;SR17724.12
	set strHTML = strHTML_"   <TABLE cellspacing=0 cellpadding=0 id='gridhead' class='TABLEhd' style='position:relative; zfloat:right; table-layout:fixed;'>" ; SR17377 ;SR17673
	set strHTML = strHTML_"      <thead>"
	set strHTML = strHTML_"         <tr id='gridheadRow'>"
	set strHTML = strHTML_"         </tr>"
	set strHTML = strHTML_"      </thead>"
	set strHTML = strHTML_"   </table>"
	
	;-----
	
	if $get($$$WWWDATEN("LINKS"))'="" {		//SR14484
		set strHTML = strHTML_"<TABLE cellspacing=0 cellpadding=0 id='gridhead2' class='TABLEhd' style='position:relative; zfloat:right;'>" ;column resizing ;SR17673 ;SR17724.29
		set strHTML = strHTML_"   <thead id='ELBody'>"
		set strHTML = strHTML_"      <tr id='gridheadRow2'>"
		set strHTML = strHTML_"      </tr>"
		set strHTML = strHTML_"   </thead>"
		set strHTML = strHTML_"</table>"
	}
	
	;-----
	
	set strHTML = strHTML_"</DIV>"
	quit strHTML
	
	; --------------------------------------
	
SetYFORM(pstrYForm="") ; DEPRECATED - no longer in use
	;-------------------------------------------------------------------------------
	; Leaves a breadcrumb so that COMView forms that popup from a grid know the
	; name of the calling form.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Nov-2007	shobby	SRBR014790:  No longer required.
	; 16-Oct-2007	shobby	SR15237: Created
	;-------------------------------------------------------------------------------
	;if ($get(YUCI)'="")&&($get(YUSER)'="") {
	;	if pstrYForm="" {
	;		;kill ^CacheTempPopupForm(YUCI,YUSER)
	;		;kill ^CacheTempFixedField(YUSER,"Popup","Form")
	;	} else {
	;		;set ^CacheTempPopupForm(YUCI,YUSER)=pstrYForm
	;		;set ^CacheTempFixedField(YUSER,"Popup","Form")=pstrYForm
	;	}
	;}
	$$$LogR("SetYFORM",pstrYForm)
	
	quit
		
]]></Routine>
</Export>