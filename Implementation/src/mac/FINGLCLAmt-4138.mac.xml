<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLCLAmt" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLCLAmt ; G/L Consolidated Ledger Amount Processing
	
#include COMSYS
#include FINConst
	
MakeConsolidation(Company,Totals)
	;-------------------------------------------------------------------------------
	; Make Consolidation from the Period Details
	;
	; Returns:
	;
	; History:
	; 19-Feb-2004	SCR		Creation
	;-------------------------------------------------------------------------------
	new Status,Rec,Line,Balance,Site
	new FromAccount,Destinations,DestLine
	new ToSite,ToCompany,ToAccount

	set Status=$$$OK
	
	set Line=0
	set Balance=0
	set Rec=$get(^FINGLCLSetup(0,0,1))
	set Site=$$getCurrentLocation^COMDCMUtilities()
	if Status=$$$OK {
		set $$$FINGLCLSetupSourceActive(Rec)=1
		if '$$$FINGLCLSetupSourceActive(Rec) {
			set Status=$$$Text("Fin00241")  ;Ledger Consolidation not Active
		}
	}
	if Status=$$$OK {
		for {
			set Line=$order(@Totals@("Save",Line))
			quit:(Line="")!(Status'=$$$OK)
			
			set FromAccount=$get(@Totals@("Save",Line,"Account"))
			if FromAccount="" set FromAccount=" "
			set Rec=$get(^FINGLAccount(0,FromAccount,1))
			; Check for Transaction Accounts
			if $$$FINGLAccountAccountUsage(Rec)=$$$EnumFINGLACCOUNTUSAGETransaction { 
				kill Destinations
				set Status=$$AccountDestinations^FINGLCLRule(FromAccount,.Destinations)
				set DestLine=""
				set ToSite=""
				for {
					set ToSite=$order(Destinations(ToSite))
					quit:ToSite=""
					quit:Status'=$$$OK
					set ToCompany=""
					for {
						set ToCompany=$order(Destinations(ToSite,ToCompany))
						quit:ToCompany=""
						quit:Status'=$$$OK
						set ToAccount=$get(Destinations(ToSite,ToCompany))
						set Status=$$MakeRecord(Site,Totals,Line,ToSite,ToCompany,ToAccount)
					}
				}
				
			}
		}
	}	
	if Status'=$$$OK set Status="0,"_Status
	quit Status
	
	
MakeRecord(FromSite="",Totals="",Line="",ToSite="",ToCompany="",ToAccount="") 
	;-------------------------------------------------------------------------------
	; Make a Consolidation Record
	;
	; Returns:
	;
	; History:
	; 20-Feb-2004	SCR		Creation
	;-------------------------------------------------------------------------------
 	new Status,FromAccount,FromType,FromPeriod,PeriodRec
 	new Rec,FromAmount,CompanyRec,FromNameSpace,FromDate,FromCurrency
 	
 	set Status=$$$OK
	if Totals="" set Status=$$$Text($listbuild("Fin00253","Total"))  ;Incorrect Parameter '%1' in Consolidation Amount processing

	; Get From Details
	if Status=$$$OK {
		set Line=+Line
		set FromAccount=$get(@Totals@("Save",Line,"Account"))
		set FromAmount=$get(@Totals@("Save",Line,"Amount"))
		set FromPeriod=$get(@Totals@("Save",Line,"Period"))
		set FromType=$get(@Totals@("Save",Line,"Type"))
		if (FromPeriod="")&(Status=$$$OK) {
			set Status=$$$Text($listbuild("Fin00254",Line))  ;Period does not exist for Consolidated Accounts Line %1
		} else {
			set PeriodRec = $get(^FINGLPeriod(0,FromPeriod,1))
			set FromDate  = $$$FINGLPeriodEndDate(PeriodRec)
		}
		set FromNameSpace=$znspace
	}
	
	; Get Currency
	if Status=$$$OK {
		set CompanyRec=$get(^WWW012(0,0,1))
		set FromCurrency=$piece(CompanyRec,Y,75)
	}
	
	; Build the Record
	if Status=$$$OK {
		set Rec = ""
		set $$$FINGLCLAmtFromSite(Rec)      = FromSite
		set $$$FINGLCLAmtFromNamespace(Rec) = FromNameSpace
		set $$$FINGLCLAmtFromCompany(Rec)   = 0
		set $$$FINGLCLAmtFromAccount(Rec)   = FromAccount
		set $$$FINGLCLAmtFromPeriod(Rec)    = FromPeriod
		set $$$FINGLCLAmtFromDate(Rec)      = FromDate
		set $$$FINGLCLAmtFromAmount(Rec)    = FromAmount
		set $$$FINGLCLAmtFromCurrency(Rec)  = FromCurrency
		set $$$FINGLCLAmtToSite(Rec)        = ToSite
		set $$$FINGLCLAmtToCompany(Rec)     = ToCompany
		set $$$FINGLCLAmtToAccount(Rec)     = ToAccount
		set Status = $$SaveRec(Rec)
	}
	quit Status
	
	
SaveRec(Rec="") 
	;-------------------------------------------------------------------------------
	; Save Record
	;
	; Returns:
	;
	; History:
	; 23-Feb-2004	SCR		Creation
	;-------------------------------------------------------------------------------
 	new Status
 	new FromSite,FromCompany,FromAccount,FromPeriod,Entry
 	new FromSiteI,FromCompanyI,FromAccountI,FromPeriodI,EntryI
 	new Done,Ok,OldRec
 	
 	; Get Indexes
 	set Status = $$$OK
	
	set FromSite    = $$$FINGLCLAmtFromSite(Rec)
	set FromCompany = $$$FINGLCLAmtFromCompany(Rec)
	set FromAccount = $$$FINGLCLAmtFromAccount(Rec)
	set FromPeriod  = $$$FINGLCLAmtFromPeriod(Rec)
	
 	; Build Indexes variables
 	; Check for an existing record else create a new entry
	if Status=$$$OK {
		set Entry=""
		set FromSiteI    = $$Index^FINSYS(FromSite)
		set FromCompanyI = $$Index^FINSYS(0)
		set FromAccountI = $$Index^FINSYS(FromAccount)
		set FromPeriodI  = $$Index^FINSYS(FromPeriod)
		
		set Done=$$$NO
		set EntryI=""
		for {
			set EntryI = $order(^FINGLCLAmts(0,2,FromSiteI,FromCompanyI,FromAccountI,FromPeriodI,EntryI))
			quit:EntryI=""
			quit:Done=$$$YES
			
			set OldRec = $get(^FINGLCLAmt(0,EntryI,1))
			set Ok     = $$$YES
			; double check indexes
	 		if FromSite'=$$$FINGLCLAmtFromSite(OldRec)       set Ok = $$$NO
	 		if FromCompany'=$$$FINGLCLAmtFromCompany(OldRec) set Ok = $$$NO
	 		if FromAccount'=$$$FINGLCLAmtFromAccount(OldRec) set Ok = $$$NO
	 		if FromPeriod'=$$$FINGLCLAmtFromPeriod(OldRec)   set Ok = $$$NO
	 		
	 		if Ok=$$$YES {
		 		set Entry = EntryI
		 		set Done  = $$$YES
	 		}
		}
		if Entry="" {
			for {
				set Entry = $$^WWWNEXT("FINGLCLAmt")
				quit:'$data(^FINGLCLAmt(0,Entry,1))
			}
		}
	}
	
	if Status=$$$OK {
		set Status = $$$Save("FINGLCLAmt",Entry,Rec,$$$YES)
	}
	quit Status
	
	
Amt(Buffer,Totals="",Line="",ToSite="",ToCompany="",ToAccount="") 
	;-------------------------------------------------------------------------------
	; Load Consolidation Amount
	;
	; Returns:
	;
	; History:
	; 20-Feb-2004	SCR		Creation
	;-------------------------------------------------------------------------------
 	new Status,Period,PeriodRec,EndDate,NewLine
 	new Account,Amount,Type,CompanyRec,Currency
 	
 	set Status=$$$OK
	if Totals="" set Status=$$$Text($listbuild("Fin00253","Total"))  ;Incorrect Parameter '%' in Consolidation Amount processing
	if Status=$$$OK {
		set Line    = +Line
		set Account = $get(@Totals@("Save",Line,"Account"))
		set Amount  = $get(@Totals@("Save",Line,"Amount"))
		set Period  = $get(@Totals@("Save",Line,"Period"))
		set Type    = $get(@Totals@("Save",Line,"Type"))
		if (Period="") && (Status=$$$OK) {
			set Status = $$$Text($listbuild("Fin00254",Line))  ;Period does not exist for Consolidated Accounts Line %1
		
		} else {
			set PeriodRec = $get(^FINGLPeriod(0,Period,1))
			set EndDate   = $$$FINGLPeriodEndDate(PeriodRec)
		}
	}
	if Status=$$$OK {
		set CompanyRec = $get(^WWW012(0,0,1))
		set Currency   = $piece(CompanyRec,Y,75)
	}
	
	if Status=$$$OK {
		set NewLine=$order(@Buffer@("Save",""),-1)+1
		set @Buffer@("Save",NewLine,"From Account")  = Account
		set @Buffer@("Save",NewLine,"From Amount")   = Amount
		set @Buffer@("Save",NewLine,"From Currency") = Currency
		set @Buffer@("Save",NewLine,"From Date")     = EndDate
		set @Buffer@("Save",NewLine,"From Period")   = Period
		set @Buffer@("Save",NewLine,"From Type")     = Type
		set @Buffer@("Save",NewLine,"To Account")    = ToAccount
		set @Buffer@("Save",NewLine,"To Company")    = ToCompany
		set @Buffer@("Save",NewLine,"To Site")       = ToSite
	}
	quit Status
	
]]></Routine>
</Export>