<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescriptionCommon" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescriptionCommon
	quit
	
#include COMSYS 
#include MEDConst
#include INConst
#include WWWConst


GetPatientData(pidPrescription, &plstPatData ,pdteCreated="")
	;-------------------------------------------------------------------------------
	; Get Patient and Admission data in a formatted list 
	;
	; Inputs: 	Prescription ID , optional created date time
	;
	; Returns: Data to Display
	;
	; History:
	; 21-Nov-2012	SCR		SR18209: Use optional passed in Timestamp (Used for Dispense)
	; 05-Nov-2012	SCR		SR18141: Use Rx data & time
	; 12-Oct-2012	SCR		SR18141: Retain Admission details
	; 15-May-2012	PPP		SR18002: Split into GetPatientData & GetPatientDataViaID
	; 30-Apr-2012	PPP		SR18002: Created 
	;-------------------------------------------------------------------------------
	new objPrescription
	new idPatient
		
	set plstPatData = ""
	if $get(pidPrescription)'="" {

		set objPrescription=$get(^MEDPrescription(0,pidPrescription,1))
		set idPatient= $$$MEDPrescriptionPatientID(objPrescription)

		set:pdteCreated="" pdteCreated = $$$MEDPrescriptionCreatedOn(objPrescription) ; SR18209
		if idPatient'="" {
			do GetPatientDataViaID(idPatient, .plstPatData, pdteCreated ) ; SR18209
			
			;do GetPatientDataViaID(idPatient, .plstPatData, $$$MEDPrescriptionCreatedOn(objPrescription) ) ; SR18141
			;do GetPatientDataViaID(idPatient, .plstPatData, $$$MEDPrescriptionDateCreated(objPrescription) )
			;do GetPatientDataViaID(idPatient, .plstPatData)
		}
	
	}

	quit $$$OK


GetPatientDataViaID(pidPatient, &plstPatData, pdtRx)
	;-------------------------------------------------------------------------------
	; Get Patient and Admission data in a formatted list
	;
	; Inputs: 	Patient ID
	;
	; Returns: Data to Display
	;
	; History:
	; 20-Nov-2013	shobby	HEVA-1148.39: Subroutined the code that finds details based on admission
	; 21-Nov-2012	shobby	SR18020: Include Admission id. Year/Years
	; 08-Nov-2012	SCR		SR18192: Used Alternate Name field
	; 12-Oct-2012	SCR		SR18141: Retain Admission details (Use RxDate)	
	; 16-May-2012	PPP		SR18002.1: Remove Seconds on Admitted Time
	; 15-May-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
#dim objPatient,objADM as %String
	
	new idAdmissionLoc,strDOB,objPatient,idADM,objADM,objLoc
	new objPrescription
	new strPatData
	new strPatName
	new strAddLoc
	new dteAdmit
	new intAge ;SR18020

	set plstPatData = ""
	set (objPatient,objADM)= ""
	
	if pidPatient'="" {

		//Admission Data
		set idADM=$$GetAdmission^MEDAdmission(pidPatient,pdtRx) ; SR18141 ;HEVA-1148.39
		do GetPatientDataViaAdmission(idADM,pidPatient,.plstPatData)
	}
	quit $$$OK
		
GetPatientDataViaAdmission(pidADM,pidPatient, &plstPatData) ;HEVA-1148.39
	;-------------------------------------------------------------------------------
	; Get Patient and Admission data in a formatted list
	;
	; Inputs: 	Patient ID
	;
	; Returns: Data to Display
	;
	; History:
	; 29-Nov-2013	shobby		CORE-290:     Subroutine DOB calculation.
	; 20-Nov-2013	shobby		HEVA-1148.39: Created (Subroutined from GetPatientViaID)
	;-------------------------------------------------------------------------------
	if pidADM'="" { 
		set objADM=$get(^MEDAdmission(0,pidADM,1))

		set idAdmissionLoc=  $$$MEDAdmissionLocation(objADM)
		if idAdmissionLoc'="" {
			set objLoc= $get(^WWW0121(0,0,idAdmissionLoc,1))
			set strAddLoc = $$$WWW0121LocationName(objLoc) ; Admission Location
		}

		// Admission Date & Time
		set dteAdmit =  $$^WWWTR(0,1,$$$MEDAdmissionDateAdmitted(objADM)) _ " " _
									  $piece($$^WWWTR(0,7,$$$MEDAdmissionTimeAdmitted(objADM)),":",1,2)	//SR18002.1

	}

	//Patient Data
	set objPatient=$get(^MEDPatient(0,pidPatient,1))
	; SR18192 vvvv
	set strPatName = $$$MEDPatientOtherNames(objPatient)_" "_$$$MEDPatientSurname(objPatient) ; Name
	;set strPatName = $zcvt($$$MEDPatientOtherNames(objPatient)_" "_$$$MEDPatientSurname(objPatient),"U") ; Name
	if $translate(strPatName," ")="" {
		set strPatName = $$$MEDPatientName(objPatient)
	}
	set strPatName = $zcvt(strPatName,"U")
	; SR18192 ^^^^
	

	set strDOB= $$$MEDPatientDOB(objPatient)
	set strDOB=$$GetDOB^MEDPatient(strDOB,$h) ;CORE-290

	
	// 1. Admission Location Key
	// 2. 
	// 3. Patient Name
	// 4. Patient ID
	// 5. Patient DOB
	// 6. Admission Location Name
	// 7. Admission Room
	// 8. Admission Bed
	// 9. Admission Date
	// 10. Patient Allergies
	// 11. Admission ID ;SR18020

	set plstPatData = $listbuild($get(idAdmissionLoc),"",$get(strPatName),idPatient,$get(strDOB),$get(strAddLoc),$$$MEDAdmissionRoom(objADM),$$$MEDAdmissionBed(objADM),$get(dteAdmit),$$$MEDPatientAllergies(objPatient),pidADM) ;SR18020
	
	quit $$$OK


GetPrescriber(pidPrescription)
	;-------------------------------------------------------------------------------
	; Get Prescriber
	;
	; Inputs: 	Prescription #
	;
	; Returns: Prescriber
	;
	; History:
	; 30-Apr-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new strPrescriber
	new objPrescription
	
	set strPrescriber=""
	if (pidPrescription'="") && (pidPrescription'="+") {
		set objPrescription=$get(^MEDPrescription(0,pidPrescription,1))
		set strPrescriber = $$$MEDPrescriptionProviderNumber(objPrescription) 
	}

	quit $get(strPrescriber)

GetStatus(pidPrescription)
	;-------------------------------------------------------------------------------
	; Get Status
	;
	; Inputs: 	Prescription #
	;
	; Returns: Prescriber
	;
	; History:
	; 16-May-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new idStatus
	new strDispenseStatus
	new objPrescription
	
	set idStatus=""
	if '$$$NoKey(pidPrescription) {
		set objPrescription=$get(^MEDPrescription(0,pidPrescription,1))
		set idStatus = +$$$MEDPrescriptionStatus(objPrescription) 
		set strDispenseStatus = $$GetDescription^WWWStatus("MEDPrescription",idStatus,$get(SPRACHE))

	}

	quit $get(strDispenseStatus)

RoundDose(pfltDose)
	;-------------------------------------------------------------------------------
	; Round Dose to 2 dp and if the value is less than .01 round up to .01
	;
	; Inputs: 	Dose
	;
	; Returns: Rounded Dose
	;
	; History:
	; 07-Feb-2013	SCR		HEVA-800: Created
	;-------------------------------------------------------------------------------
	quit:pfltDose="" "" ; Fast exit
	if (pfltDose && (pfltDose<.01)) {
		set pfltDose = .01
	}
	set pfltDose	= +$justify(pfltDose,0,2)

	quit pfltDose
	

]]></Routine>
</Export>