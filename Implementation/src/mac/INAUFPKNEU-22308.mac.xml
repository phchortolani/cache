<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPKNEU" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPKNEU(AUF,POS,NEU)
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ARTIKELKONDITIONEN ÜBERTRAGEN
	;
	; Inputs : 
	;	AUF		ORDER
	;	POS		LINE ITEM
	;	NEU		(bln) New Calculation of discounts and conditions
	;			RABATTE UND KONDITIONEN NEU RECHNEN JA/NEIN
	;			=1 CALCULATE TERMS
	;
	; ByRef :
	;
	;
	; Returns :
	;
	; History :
	; 09-Feb-2010	shobby	SR17138: Some rearrangement to handle Ordered Quantity and ordered Unit fields.
	; 28-Sep-2006	GRF		Doco; macros; remove unnecessary DO Level; quits
	; 07.08.2003	FIS
	;-------------------------------------------------------------------------------
	NEW LIEF,ART,POS1,YFELD,objINARTK ;SR17138
	
	;+++++++++++++++++++++++++++++++++++++++
	;	POS1		objINAUFP
	;	SATZ		objINAUFPK   (1)
	;	YFELD		objINAUFPK   (2)
	;	RABATT1		objINARTKR
	;+++++++++++++++++++++++++++++++++++++++

	SET AUF =  $GET(AUF)         QUIT:AUF=""
	SET POS =  $GET(POS)         QUIT:POS=""
	SET NEU = +$GET(NEU)
	
	SET POS1 = $GET(^INAUFP(YM,AUF,POS,1))
	IF $PIECE(POS1,Y,254)=1 SET NEU = 0        ;KEINE NEURECHNUNG ;no 
	SET ART  = $PIECE(POS1,Y,4)
	SET LIEF = $PIECE(POS1,Y,12)
	
	IF (LIEF="") || (ART="") DO    ;BEC;NEURECHNER DER EK WERTE  ;the Planned Cost 
	. NEW SATZ,YOK
	. SET SATZ=$GET(^INAUFPK(YM,AUF,POS,1))
	. SET $PIECE(SATZ,Y,16)=$PIECE(POS1,Y,5)  ;TYBD; IMMER BEZUGSMENGE ;24530; 27,11,2003
	. SET $PIECE(SATZ,Y,12)=+$PIECE(SATZ,Y,16)*$PIECE(SATZ,Y,47)/$$^INQTYUNIT(,AUF,POS)
	. SET $PIECE(SATZ,Y,12)=$JUSTIFY($PIECE(SATZ,Y,12),0,2)
	. IF +$PIECE(SATZ,Y,12)'=0 SET $PIECE(SATZ,Y,13)=$$^INNETTO(SATZ)   ;RECHEN NETTO AUS BRUTTO FELD ;out of gr. field 
	. SET YOK=$$^WWWSPEI("INAUFPK",AUF_","_POS,SATZ,1) 
	
	QUIT:ART=""
	
	IF (ART'="") && (LIEF'="") DO
	. IF '$DATA(^INARTK(YM,ART,LIEF)) IF '$DATA(^INAUFPK(YM,AUF,POS))  QUIT
	. IF $DATA(^INARTK(YM,ART,LIEF)) SET ^INAUFPK(YM,AUF,POS,1)=$GET(^INARTK(YM,ART,LIEF,1))
	. SET YFELD=$GET(^INAUFPK(YM,AUF,POS,1))
	. ;IF '$DATA(^INARTK(YM,ART,LIEF)) SET $PIECE(YFELD,Y,12)=$$^INARTKOST(ART)
	. IF $DATA(^INARTK(YM,ART,LIEF)) DO
	. . set objINARTK=$get(^INARTK(YM,ART,LIEF,1))
	. . SET $PIECE(YFELD,Y,12)=$$Convert^INUOMConversion(ART,$$$INARTKUnitPrice(objINARTK),$$$INAUFPOrderedUnit(POS1),$$$INARTKDeliveryQuantityUnit(objINARTK))	;SR17138
	. IF '$DATA(^INARTK(YM,ART,LIEF)) IF +$$^INARTKOST(ART)'=0 SET $PIECE(YFELD,Y,12)=$$^INARTKOST(ART)     ;Bec;27.11.03;24530;damit keine nullwerte
	. ;
	. ;MENGENRABATTE USW
	. IF NEU=$$$YES IF $DATA(^INARTKR(YM,ART,LIEF)) DO
	. . NEW RABATT,RABATT1
	. . SET RABATT1=""
	. . SET RABATT=$ORDER(^INARTKR(YM,ART,LIEF,+$PIECE(POS1,Y,5)),-1)  ;VORIGE STUFE ;step 
	. . IF RABATT'="" SET RABATT1=$GET(^INARTKR(YM,ART,LIEF,RABATT,1))
	. . IF RABATT=""  SET RABATT1=$GET(^INARTKR(YM,ART,LIEF,+$PIECE(POS1,Y,5),1))  ;GENAUE MENGEN ;shuffle 
	. . QUIT:RABATT1=""                                                            ;KEIN RABATT ;no deduction 
	. . IF $PIECE(RABATT1,Y,2)'="" QUIT:$PIECE(RABATT1,Y,2)>$HOROLOG               ;AB GÜLTIG ;Confirm. valuable 
	. . IF $PIECE(RABATT1,Y,3)'="" QUIT:$PIECE(RABATT1,Y,3)<$HOROLOG               ;BIS GÜLTIG ;until valuable 
	. . IF +$PIECE(RABATT1,Y,1)'=0 SET $PIECE(^INAUFPK(YM,AUF,POS,1),Y,4)=$PIECE(RABATT1,Y,1)   ;3.RABATT=MENGENRABATT
	. . SET $PIECE(^INAUFPK(YM,AUF,POS,1),Y,53)=$$^WWWTEXT(32223)      ; "Quantity Discount"    ;3.RABATT=MENGENRABATT
	. . IF +$PIECE(RABATT1,Y,4)'=0 SET $PIECE(^INAUFPK(YM,AUF,POS,1),Y,12)=$PIECE(RABATT1,Y,4)  ;WENN BETRAG ALS RABATT ;when Sum when deduction 
	. ;
	. set $$$INAUFPKConversionFactorQuantity(YFELD) = $$Convert^INUOMConversion(ART,1,$$$INAUFPOrderedUnit(POS1),$$$INAUFPQuantityUnit(POS1)) ;SR17138
	. set $$$INAUFPKPurchaseQuantity(YFELD)         = $$$INAUFPOrderedQuantity(POS1)
	. set $$$INAUFPKDeliveryQuantityFullLot(YFELD)  = $$$INAUFPOrderedUnit(POS1)
	. IF NEU=$$$YES DO
	. . SET $PIECE(YFELD,Y,47)=$JUSTIFY($PIECE(YFELD,Y,12),0,2)    ;EINZEL-EK IN SELBSTKOSTENFELD ;within 
	. . ;SR17138 SET $PIECE(YFELD,Y,16)=$PIECE(POS1,Y,5)                    ;STANDARDMENGE
	. . ;IF +$PIECE(YFELD,Y,41)'=0 SET $PIECE(YFELD,Y,16)=+$JUSTIFY($PIECE(POS1,Y,5)*$PIECE(YFELD,Y,41),0,2)  ;UMRECHNUNGSFAKTOR ;TYBD;9.5.2003
	. . ;SR17138 SET $PIECE(YFELD,Y,12)=$JUSTIFY($PIECE(YFELD,Y,47)*$PIECE(YFELD,Y,16),0,2)/$$^INQTYUNIT(,AUF,POS)    ;MENGE * PREIS ;quantity * price 
	. . SET $PIECE(YFELD,Y,12)=$JUSTIFY($PIECE(YFELD,Y,47)*$PIECE(YFELD,Y,16),0,2)/$$^INQTYUNIT(,AUF,POS) ;SR17138    ;MENGE * PREIS ;quantity * price 
	. . SET $PIECE(YFELD,Y,13)=$$^INNETTO(YFELD)                   ;NETTOPREIS ERRECHNE  ; Net-Original Price
	. . set $$$INAUFPKDeliveryQuantityFullLot(YFELD)=$$$INAUFPOrderedUnit(POS1)
	. . SET ^INAUFPK(YM,AUF,POS,1)=YFELD                           ;SPEICHERN PREISE ;Save
	. . SET $PIECE(^INAUFP(YM,AUF,POS,1),Y,47)=$PIECE(YFELD,Y,13)  ;NEURECHNEN POSITIONS-EK
	. ;
	. IF NEU'=$$$YES DO
	. . SET $PIECE(YFELD,Y,13)=$JUSTIFY($PIECE(POS1,Y,47),0,2)     ;GESAMT IN AUFTRAG ;total within order 
	. . ;SR17138 SET $PIECE(YFELD,Y,16)=$PIECE(POS1,Y,5)                    ;STANDARDMENGE
	. . SET $PIECE(YFELD,Y,12)=$JUSTIFY($$^INBRUTTO(YFELD),0,2)    ;BRUTTO AUS NETTOPREIS ERRECHNEN
	. . SET $PIECE(YFELD,Y,47)=$PIECE(YFELD,Y,12)                  ;MENGE * PREIS ;quantity * price 
	. . ;IF $PIECE(POS1,Y,254)'=1 IF +$PIECE(POS1,Y,5)'=0 SET $PIECE(YFELD,Y,47)=$JUSTIFY($PIECE(YFELD,Y,12)/$PIECE(POS1,Y,5)/$$^INQTYUNIT(,AUF,POS),0,2) ;EINZELPREIS
	. . IF +$PIECE(POS1,Y,5)'=0 SET $PIECE(YFELD,Y,47)=$JUSTIFY($PIECE(YFELD,Y,12)/$PIECE(POS1,Y,5)/$$^INQTYUNIT(,AUF,POS),0,2) ;EINZELPREIS
	. . SET ^INAUFPK(YM,AUF,POS,1)=YFELD                           ;SPEICHERN PREISE ;Save
	
	; FIXME : direct save of INAUFPK (not indexed at present) and
	;         INAUFP D47 (not an indexed field) - Low urgency to change <GRF>
	
	QUIT
]]></Routine>
</Export>