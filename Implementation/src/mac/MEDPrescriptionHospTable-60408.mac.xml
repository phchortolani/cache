<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescriptionHospTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescriptionHospTable

#include COMSYS
#include MEDConst
#include INConst
#include %occInclude
#include COMView
#include COMConst


ShowLines(pidMEDPres)
	;-------------------------------------------------------------------------------
	; Show the Issue for the Production Order
	;     Not Used - Replaced with Grid
	;
	; Params:	pidIssue - INIssue id
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new arrQuery,intCol
	quit:pidMEDPres=""
	set arrQuery("Class") = "MEDPrescriptionLine"
	set arrQuery("Loop")  = "LineQuery^MEDPrescriptionHospTable"
	set arrQuery("Get")   = "set id=$$LineData^MEDPrescriptionHospTable(pidMEDPres,id(1))"
	set arrQuery("Link")  = "@set Form=""MEDPrescriptionHosp"",Key="""_pidMEDPres_",""_$get(id(1))~P~2" 
	set arrQuery("Title") = $$^WWWFORMNAME("MEDPrescriptionHopsLine")
	
	set arrQuery("Col",1) = "D~1~L~~~~~"_$$$Text( "MED01249") 	;Drug
	;set arrQuery("Col",1,"DoInstead") = "LineData^MEDPrescriptionHopsLine"
	set arrQuery("Col",2) = "D~13~L~~~~~"_$$$Text("MED01251")	;Route
	set arrQuery("Col",3) = "D~4~L~~~~~"_$$$Text("MED01250")	;Dose
	set arrQuery("Col",4) = "D~5~C~~~~~"_$$$Text("MED01252")	;Frequency
	set arrQuery("Col",5) = "D~28~C~~~~~"_$$$Text("MED01253")_"~1"	;Start Date
	set arrQuery("Col",6) = "D~6~C~~~~~"_$$$Text("MED01254")_"~1"	;Duration
	set arrQuery("Col",7) = "D~30~L~~~~~"_$$$Text(32781)		;Status
	
	
	do Query^COMQuery(.arrQuery,pidMEDPres)
	quit

LineData(pidRx,pidRxLine)
	;-------------------------------------------------------------------------------
	; Convert Data.
	;
	; Params:
	; pidCol  - Col Number.
	; pstrData - Cu
	;
	; ByRefs: None
	;
	; Returns: Data to Display 
	;
	; History:
	; 19-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
#Dim pidRx,pidRxLine,objLine,strField,strNew As %String
	new objLine,strField,strNew
	set objLine=$get(^MEDPrescriptionLine(0,pidRx,pidRxLine,1))
	; Item
	set strField= $$$MEDPrescriptionLineItem(objLine)
	if (strField'="") && ($data(^INART(YM,strField))) {
		set $$$MEDPrescriptionLineItem(objLine)=$piece($get(^INART(YM,strField,1)),Y,1)
	}
	; Dose
	set strField=$$$MEDPrescriptionLineDoseQty(objLine)_" "_ $$$WWW101Text($get(^WWW101(0,"EINHEIT",LANGUAGE, $$$MEDPrescriptionLineDoseUOM(objLine),1)))
	set $$$MEDPrescriptionLineDoseUOM(objLine) = strField
	; Frequency
	set $$$MEDPrescriptionLineFrequency(objLine) =  $$$MEDFrequencyFrequency($get(^MEDFrequency(YM, $$$MEDPrescriptionLineFrequency(objLine),1)))
	; Start Date
	set $$$MEDPrescriptionLineFromDate(objLine) = $$^WWWTR(0,1,$$$MEDPrescriptionLineFromDate(objLine))_"<BR>"_$$^WWWTR(0,7, $$$MEDPrescriptionLineFromTime(objLine))
	; Duration
	set strField  = $$$MEDPrescriptionLineDuration(objLine)
	if strField>1 {
		set strField = strField _" "_$$$Text("MED01256")  ;Days
	} else {
		set strField = strField _" "_$$$Text("MED01257")  ;Day
	}
	set $$$MEDPrescriptionLineDuration(objLine) = strField
	quit objLine
	
	
LineQuery(parrQuery,&parrStorage,pidMEDPres)
	;-------------------------------------------------------------------------------
	; Loop for the COMQuery above - show related receipts
	;
	; Params:	parrQuery	- COMQuery info
	; 			parrStorage	- COMQuery data
	;			pidMEDPres	- ID of the MED Prescription (D27)
	; ByRefs:
	;
	; Returns:	int - row count
	;
	; History:
	; 19-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new idRxLine,intCount
	
	kill ^mtemp("Query",$job)
	//set idIssue = parrQuery("Parameters","pidIssue")
	
	set intCount = 0
	
	set idRxLine = ""
	if pidMEDPres'="" {
		for {
			set idRxLine = $order(^MEDPrescriptionLine(YM,pidMEDPres,idRxLine))
			quit:idRxLine=""
		
			set @parrStorage@($increment(intCount),1) = idRxLine
			set intCount=intCount+1
		}
	}
	quit intCount

ShowDrugs(pidPatient)
	;-------------------------------------------------------------------------------
	; Show current Drugs for a patien
	;
	; Params:	PID - MEDPatient
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 16-Mar-2012	SCR		SR17993: Added Header
	; 28-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	

	Do Header($$$Text("MED01261"))

	new arrQuery,intCol
	quit:pidPatient=""
	set arrQuery("Class") = "MEDPrescriptionLine"
	set arrQuery("Loop")  = "DrugQuery^MEDPrescriptionHospTable"
	set arrQuery("Get")   = "set id=$$DrugData^MEDPrescriptionHospTable(id(1),id(2))"
	;set arrQuery("Link")  = "@set Form=""MEDPrescriptionHosp"",Key=""""_$get(id(1))_"",""_$get(id(2))~P~2" 
	set arrQuery("Link")  = "@do DrugForm^MEDPrescriptionHospTable(.id,.Form,.Key)~P~2" 
	set arrQuery("Title") = $$^WWWFORMNAME("MEDPrescriptionHopsLine")
	
	set arrQuery("Col",1) = "D~1~L~~~~~"_$$$Text( "MED01249")_"~1" 	;Drug
	set arrQuery("Col",2) = "D~43~L~~~~~"_$$$Text(32781)_"~1"		;Status
	
	
	do Query^COMQuery(.arrQuery,pidPatient)
	quit

DrugData(pidRx,pidRxLine)
	;-------------------------------------------------------------------------------
	; Convert Data.
	;
	; Params:
	; pidCol  - Col Number.
	; pstrData - Cu
	;
	; ByRefs: None
	;
	; Returns: Data to Display 
	;
	; History:
	; 09-Apr-2013	SCR		HEVA-875: Correct Infusion Details (Was using last line prescription instead of last line of Solution)
	; 13-Mar-2013	shobby	HEVA-883: corrected syntax of &nbsp; (not &nbsp)
	; 08-Feb-2013	SCR		HEVA-800: Convert number based on region
	; 07-Feb-2013	SCR		HEVA-800: Round to 2 dp
	; 22-Jan-2013	shobby	HEVA-775.2: Format decimals according to language setup
	; 15-Jan-2013	SCR		HEVA-751: Use Solution Status function
	; 21-Jun-2012	shobby	SR18026: Undefined variable
	; 26-May-2012	SCR		SR17993: Truncated Item Name to 50 characters
	; 16-May-2012	SCR		SR17993: Add Status Icons
	; 07-May-2012	SCR		SR17993: Added Solution display
	; 28-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
#Dim pidRx,pidRxLine,objLine,strField,strNew,idSol,idItem,strDesc,idUnit,idStatus,strImage,objRx As %String
#dim intSol,intSolLine As %Integer
	new objLine,strField,strNew,intSol,idSol,intSolLine,idItem,strDesc,idUnit,idStatus,strImage,objRx
	new objLast
	set objRx	= $get(^MEDPrescription(0,pidRx,1))
	set objLine	= $get(^MEDPrescriptionLine(0,pidRx,pidRxLine,1))
	set strField= $$$MEDPrescriptionLineItem(objLine)
	set intSol	= $$$MEDPrescriptionLineSolution(objLine)
	set idStatus= +$$$MEDPrescriptionLineStatus(objLine)
	set strNew="" ;SR18026
	if intSol {
		set strNew=$$$Text("MED01291")  ;Solution of
		set idSol=""
		set intSolLine=0
		for {
			set idSol=$order(^MEDPrescriptionLine(0,pidRx,idSol))
			quit:idSol=""
			set objLine	= $get(^MEDPrescriptionLine(0,pidRx,idSol,1))
			set idItem	= $$$MEDPrescriptionLineItem(objLine)
			set strDesc	= $$$INARTSearchName($get(^INART(YM,idItem,1)))
			continue:intSol'=$$$MEDPrescriptionLineSolution(objLine)
			set intSolLine=intSolLine+1
			set objLast = objLine  ; HEVA-875
			set strNew	= strNew_"&nbsp;"_intSolLine_"&nbsp;-&nbsp;"
			set strNew	= strNew_"<b><u>"_$extract(strDesc,1,50)_"</u></b>"
			set:$length(strDesc)>40 strNew=strNew_"<br>&nbsp;&nbsp;"
			set strNew	= strNew_"&nbsp;&nbsp;&nbsp;&nbsp;"_$$$Text("MED01250")	;Dose
			; HEVA-800 vvvv
			set strNew	= strNew_"&nbsp;"_$$Convert^WWWTR(0,12,$$$MEDPrescriptionLineDoseQty(objLine))_"&nbsp;"
			set strNew	= strNew_$$$WWW101Text($get(^WWW101(0,"EINHEIT",LANGUAGE, $$$MEDPrescriptionLineDoseUOM(objLine),1)))
			;set strNew	= strNew_"&nbsp;"_$$$MEDPrescriptionLineDoseQty(objLine)_"&nbsp;"_ $$$WWW101Text($get(^WWW101(0,"EINHEIT",LANGUAGE, $$$MEDPrescriptionLineDoseUOM(objLine),1)))
			; HEVA-800 ^^^^
			set strNew	= strNew_"<br>"
			
		}
		; Adde Infusion Rate & Tim based on last Line
		set strNew	= strNew_"&nbsp;&nbsp;&nbsp;&nbsp;"_$$$Text("MED01258")  ;Infusion Time
		set strNew	= strNew_"&nbsp;"_$$Convert^WWWTR(0,12,$$$MEDPrescriptionLineInfusionSpeed(objLast))_"&nbsp;" ; HEVA-875
		;set strNew	= strNew_"&nbsp;"_$$Convert^WWWTR(0,12,$$$MEDPrescriptionLineInfusionSpeed(objLine))_"&nbsp;" ; HEVA-800
		set idUnit	= $$$MEDPrescriptionLineInfusionUOM(objLast) ; HEVA-875
		;set idUnit	= $$$MEDPrescriptionLineInfusionUOM(objLine)
		set:idUnit'="" strNew=strNew_"&nbsp;"_$$$INPARAText($get(^INPARA(0,"MEDINFUSEUNIT",LANGUAGE,idUnit,1)))
		set idStatus=+$$SolutionStatus^MEDPrescriptionAutoClose(pidRx,intSol) ; HEVA-751
		
	} else { 
		if (strField'="") && ($data(^INART(YM,strField))) {
			set strNew	= "<b><u>"_$extract($piece($get(^INART(YM,strField,1)),Y,1),1,50)_"</u></b><br>"
			set strNew	= strNew_"&nbsp;&nbsp;&nbsp;&nbsp;"_$$$Text("MED01250")	;Dose
			set strNew	= strNew_"&nbsp;"_$$Convert^WWWTR(0,12,$$$MEDPrescriptionLineDoseQty(objLine))_"&nbsp;"_ $$$WWW101Text($get(^WWW101(0,"EINHEIT",LANGUAGE, $$$MEDPrescriptionLineDoseUOM(objLine),1)))_"-" ; HEVA-775.2
			if $$$MEDPrescriptionLineRouteOfAdministration(objLine)'="" {
				set strNew	= strNew_$piece($get(^INPARA(YM,"MEDROUTEADMIN",LANGUAGE,$$$MEDPrescriptionLineRouteOfAdministration(objLine),1)),Y,1)_"&nbsp;&nbsp;-&nbsp;&nbsp;"
			}
			;
			if $$$MEDPrescriptionLineFrequency(objLine)'="" {
				set strNew	= strNew_$$$MEDFrequencyFrequency($get(^MEDFrequency(YM, $$$MEDPrescriptionLineFrequency(objLine),1)))
			}
			set strNew	= strNew_"<br>"
			set strNew	= strNew_"&nbsp;&nbsp;&nbsp;&nbsp;"_$$$Text("MED01253")_"&nbsp;"  ;Start Date
			set strNew	= strNew_$$^WWWTR(0,1,$$$MEDPrescriptionLineFromDate(objLine))_"&nbsp;&nbsp;-&nbsp;&nbsp;"
			set strNew	= strNew_$$$Text("MED01254")_"&nbsp;" ; Duration
			set strField= $$Convert^WWWTR(0,12,$$$MEDPrescriptionLineDuration(objLine),2) ; HEVA-775.2
			set strNew	= strNew_strField
			if strField>1 {
				set strNew = strNew _"&nbsp;"_$$$Text("MED01256")  ;Days
			} else {
				set strNew = strNew _"&nbsp;"_$$$Text("MED01257")  ;Day
			}
		}
	}
	set $$$MEDPrescriptionLineItem(objLine)=strNew
	set strImage=  $$$WWWStatusImage($get(^WWWStatus(0,"MEDPrescription",idStatus,1)))
	set $$$MEDPrescriptionLineStatus(objLine)="<IMG SRC='"_YGIF_strImage_"'  TITLE='"_$$GetDescription^WWWStatus("MEDPrescription",idStatus,SPRACHE)_"' />"

	set strField=$$$MEDPrescriptionLineDoseQty(objLine)_" "_ $$$WWW101Text($get(^WWW101(0,"EINHEIT",LANGUAGE, +$$$MEDPrescriptionLineDoseUOM(objLine),1)))
	quit objLine
	
	
DrugQuery(parrQuery,&parrStorage,pidPatient)
	;-------------------------------------------------------------------------------
	; Loop for the COMQuery above - show related receipts
	;
	; Params:	parrQuery	- COMQuery info
	; 			parrStorage	- COMQuery data
	;			pidPatient	- ID of Patient
	; ByRefs:
	;
	; Returns:	int - row count
	;
	; History:
	; 14-Feb-2013	SCR		HEVA-751: Show Soluton when all lines are Rejected
	; 23-Jan-2013	SCR		HEVA-751: Whole solution not shown when a single line is closed
	; 28-May-2012	SCR		SR17993: Ensure Rx is in date order
	; 16-May-2012	SCR		SR17993: Limit Number of Lines to 6
	; 16-May-2012	SCR		SR17993: Most Recent Drug 1st
	; 07-May-2012	SCR		SR17993: Show Solutions as a single line
	; 28-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
#dim idRx,objRx,idxPat,idLine,objLine,strKey,dtCreated As %String
#dim intCount,intSol,intSolLine,idStatus As %Integer 
#dim arrLine,arrRx As %ArrayOfDataTypes
	new idRx,objRx,idxPat,idLine,objLine,intCount,arrLine,intSol,intSolLine,arrRx,strKey,dtCreated,idStatus
	
	kill ^mtemp("Query",$job)
	//set idIssue = parrQuery("Parameters","pidIssue")
	
	set intCount = 0
	set idRx=""
	
	set idxPat=$$$Index(pidPatient)
	for {
		set idRx=$order(^MEDPrescriptions(YM,1,idxPat,idRx))
		quit:idRx=""
		set objRx = $get(^MEDPrescription(YM,idRx,1))
		set dtCreated = $$$MEDPrescriptionCreatedOn(objRx)
		set strKey = $piece(dtCreated,",",1)_","_$justify($piece(dtCreated,",",2),5)_","_idRx ; ensure Rx is in date order
		set arrRx(strKey) = ""
	}
	set strKey="" 
	for {
		set strKey=$order(arrRx(strKey),-1)
		quit:strKey=""
		quit:intCount>5
		set idRx=$piece(strKey,",",3)
		set objRx=$get(^MEDPrescription(YM,idRx,1))
		;quit:$$$MEDPrescriptionStatus(objRx)=5 
		set idLine = ""
		for {
			set idLine = $order(^MEDPrescriptionLine(YM,idRx,idLine))
			quit:idLine=""
			quit:intCount>5
			set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
			; HEVA-751 vvvv			
			set idStatus=$$$MEDPrescriptionLineStatus(objLine)
			set:idStatus=10 idStatus=2.5
			continue:idStatus>7 ; HEVA-751
			;continue:$$$MEDPrescriptionLineStatus(objLine)>7 ; HEVA-751
			; HEVA-751 ^^^^			
			;quit:$$$MEDPrescriptionLineStatus(objLine)>7
			set intSol= $$$MEDPrescriptionLineSolution(objLine)
			if 'intSol {
				set intCount=intCount+1
				set arrLine("l",intCount,1) = idRx
				set arrLine("l",intCount,2) = idLine
			} else {
				set intSolLine=$get(arrLine("s",idRx,intSol))
				if 'intSolLine {
					set intCount=intCount+1
					set intSolLine=intCount
					set arrLine("s",idRx,intSol)=intSolLine
					set arrLine("l",intSolLine,1) = idRx
					set arrLine("l",intSolLine,2) = idLine
				}
				set arrLine("s",idRx,intSol,idLine)=""
			}
		}
	}
	merge @parrStorage=arrLine("l")
	quit intCount

DrugForm(&parrId,&pstrForm,&pstrKey)
	;-------------------------------------------------------------------------------
	; Select form MEDPrescriptionHosp or MEDPrescriptionSol
	;		
	; Params: Query ID ((1)=Presciption,(2)=Line)
	;		  Form pass back
	;		  Key pass back	
	;
	;
	; Returns: Form & Key pass back
	;
	; History :
	; 07-May-2012	SCR		SR17993:Created
	;--------------------------------------- ----------------------------------------
#dim YKEY,objLine,strForm,pstrForm,pstrKey As %String
#dim intSol As %Integer
#dim parrId As %ArrayOfDataTypes
	new strForm,objLine,intSol
	set pstrForm="MEDPrescriptionHosp"
	if $get(parrId(2))'="" {
		set objLine = $get(^MEDPrescriptionLine(YM,parrId(1),parrId(2),1))
		set intSol = $$$MEDPrescriptionLineSolution(objLine)
		if intSol'="" {
			set pstrForm="MEDPrescriptionSol"

		}
		
	}
	;set pstrKey=parrId(1)_","_parrId(2)_Y_"GridLine,"_parrId(2)
	set pstrKey=parrId(1)_","_parrId(2)_Y ;_"GridLine,"_parrId(2)
	quit
	
ShowHist(pidPatient)
	;-------------------------------------------------------------------------------
	; Show current Drugs for a patien
	;
	; Params:	PID - MEDPatient
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 29-Nov-2012	shobby	HEVA-669: Include link on 2nd column
	; 16-May-2012	SCR		SR17993: Added Header	
	; 10-May-2012	PPP		SR17993: Added 'DoInstead' code
	; 08-May-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------

	do Header($$$Text("MED01262"))  ;Prescibed Drugs

	new arrQuery,intCol
	quit:pidPatient=""
	set arrQuery("Class") = "MEDPrescriptionLine"
	set arrQuery("Loop")  = "HistQuery^MEDPrescriptionHospTable"
	set arrQuery("Get")   = "set id=$$HistData^MEDPrescriptionHospTable(id(1),id(2))"
	//set arrQuery("Link")  = "@set Form=""MEDPrescriptionHosp"",Key=""""_$get(id(1))_"",""_$get(id(2))~P~2" 
	//set arrQuery("Link")  = "@do HistForm^MEDPrescriptionHospTable(.id,.Form,.Key)~P~2" 
	set arrQuery("Title") = $$^WWWFORMNAME("MEDPrescriptionHopsLine")
	
	set arrQuery("Col",1) = "D~1~C~~~~~"_$$$Text("MED01211")_"~1"	;Date
	set arrQuery("Col",2) = "D~2~C~~~~~"_$$$Text("MED01259")_"~1"  ;Drugs Total
	set arrQuery("Col",1,"DoInstead") = "HistoryLink^MEDPrescriptionHospTable"
	set arrQuery("Col",2,"DoInstead") = "HistoryLink^MEDPrescriptionHospTable"
	
	do Query^COMQuery(.arrQuery,pidPatient)
	
	quit


HistoryLink(pidCol,pstrData)
	;-------------------------------------------------------------------------------
	; Return hyperlink that will be displayed in the column listing the site locations.
	;
	; Params:
	; pidCol  - Not used.
	; pidLocn - Sending Site Location
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 31-Nov-2012	shobby	HEVA-669: Get the data from the 'id' global.  We may have
	;								  clicked on the 2nd column but want the reference
	;								  from the first.
	; 10-May-2012	PPP		SR17993:Created
	;-------------------------------------------------------------------------------
	new strArgs,strOutput
	
	new strData																			
	set strData=$piece(id,Y,1)
	
	set strArgs = "'ListHistory^MEDPrescriptionHospTable','"_pidCol_"','"_strData_"'"

	set strOutput = "<a href=""#"" onclick=""javascript:CallBack("_strArgs_");"">"
	set strOutput = strOutput_pstrData
	set strOutput = strOutput_"</a>"
	
	quit strOutput

HistData(pidDate,pintCount)
	;-------------------------------------------------------------------------------
	; Convert Data.
	;
	; Params:
	; pidCol  - Col Number.
	; pstrData - Cu
	;
	; ByRefs: None
	;
	; Returns: Data to Display 
	;
	; History:
	; 07-May-2012	SCR		SR17993: Added Solution display
	; 28-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	set objLine=$$^WWWTR(0,1,pidDate)_Y_pintCount
	quit objLine
	
	
HistQuery(parrQuery,&parrStorage,pidPatient)
	;-------------------------------------------------------------------------------
	; Loop for the COMQuery above - show related receipts
	;
	; Params:	parrQuery	- COMQuery info
	; 			parrStorage	- COMQuery data
	;			pidPatient	- ID of Patient
	; ByRefs:
	;
	; Returns:	int - row count
	;
	; History:
	; 
	; 01-Jun-2012	SCR		SR17993: Changed to ^CacheTempMedRx(YUSER,"HospTable") 
	; 18-May-2012	SCR		SR17993: Limit Number of Lines to 7
	; 10-May-2012	PPP		SR17993: Saved the prescription details in ^CacheTempHospTable
	; 07-May-2012	SCR		SR17993: Show Solutions as a single line
	; 28-Mar-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
#dim idRx,objRx,idxPat,idLine,objLine As %String
#dim intCount,intSol,intSolLine As %Integer 
#dim arrLine As %ArrayOfDataTypes
	new idRx,objRx,idxPat,idLine,objLine,intCount,arrLine,intSol,intSolLine
	
	kill ^mtemp("Query",$job)
	//set idIssue = parrQuery("Parameters","pidIssue")
	kill ^CacheTempMedRx(YUSER,"HospTable")
	
	set intCount = 0
	set idRx=""
	set idxPat=$$$Index(pidPatient)
	for {
		set idRx=$order(^MEDPrescriptions(YM,1,idxPat,idRx))
		quit:idRx=""
		set objRx=$get(^MEDPrescription(YM,idRx,1))
		set idLine = ""
		for {
			set idLine = $order(^MEDPrescriptionLine(YM,idRx,idLine))
			quit:idLine=""
			set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
			set dtDate= $$$MEDPrescriptionLineFromDate(objLine)
			if 'dtDate {
				set dtDate= $$$MEDPrescriptionDateCreated(objRx)
			}
			set:'dtDate dtDate = +$h
			set arrLine("l",dtDate,1) = dtDate
			set arrLine("l",dtDate,2) = $get(arrLine("l",dtDate,2))+1
			set ^CacheTempMedRx(YUSER,"HospTable",dtDate) = $get(^CacheTempMedRx(YUSER,"HospTable",dtDate))_"|"_idRx   //PPP
		}
	}
	set dtDate=""
	for intCount=1:1:7 {
		set dtDate=$order(arrLine("l",dtDate),-1)
		quit:dtDate=""
		merge @parrStorage@(intCount)=arrLine("l",dtDate)

	}
	quit intCount

ListHistory(pidCol, pdtePrescription)
	;-------------------------------------------------------------------------------
	; Show COMView with "History on MEDPrescription Lines"
	; 
	; Called by : 
	;
	; History:
	; 24-Sep-2012	SCR		SR17993:Changed Date Calculation
	; 10-May-2012	PPP		SR17993:Created
	;-------------------------------------------------------------------------------
	new strPrescriptions
	new dtePrescription
 
 	;set dtePrescription = $zdh(pdtePrescription)
 	set dtePrescription = $$^WWWTR(1,1,pdtePrescription)
	
	set strPrescriptions = $get(^CacheTempMedRx(YUSER,"HospTable",dtePrescription))
	set strPrescriptions = $extract(strPrescriptions,2,$length(strPrescriptions))	
 	
	do Initialise^COMViewCustom("MEDPrescriptionLine","",$$$NO,$$$NO,$$$YES) 
	do AddFilter^COMViewCustom("P1",$$$EnumCOMVIEWCOMPARATORFindIn,strPrescriptions)
	do Start^COMViewCustom()
 	
	quit $$$OK

Header(pstrText)
	;-------------------------------------------------------------------------------
	; Display Table Header
	; 
	; Called by :
	;
	; History:
	; 16-May-2012	SCR		SR17993:Created
	;-------------------------------------------------------------------------------
	write "<BR><B><U>",pstrText,"</U></B><BR><BR>"  ;List of Current Drugs
	quit


]]></Routine>
</Export>