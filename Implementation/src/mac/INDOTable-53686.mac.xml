<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDOTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDOTable

#include COMSYS
#include INConst
#include %occInclude

ShowHeader(pidEvent,pidParameters)
	;-------------------------------------------------------------------------------
	; show the Delivery Order Header the Delivery Order Line belongs to if present
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 04-Jan-2010	DWR		SR17074: Created
	;-------------------------------------------------------------------------------
	set strStatus = $$$OK
	set idGrid    = $$$DYNTABLEGridLineFocusGrid(pidParameters)
	
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dINDOHeader")
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dINDOHeaderOther")
 
	set idKey = $$ReferenceKey^COMGridEdit31Interface(idGrid,$$$DYNTABLEGridLineFocusRow(pidParameters)) 

	set idINPO	    = $$$KEY1(idKey)
	set idxPurOrd   = $$$Index(idINPO)
	;set idSupplier  = $$$INPOSupplierCode($get(^INPO(0,idINPO,1)))
	set idINDOLine  = $$$KEY2(idKey)
	set objINDOLine = $$$GRIDGetYFELD(idINDOLine)
	set idDOHeader  = ""
	
	if objINDOLine '=$$$NULLOREF {
		set idLineDOHeader = $$$INDOLineDOHeader(objINDOLine)
	}
	if idLineDOHeader = "" {
		do DrawTable^WWW120DynTable($listbuild($$$Text("IN01164")),"dINDOHeader",$listbuild($listbuild("left","display")),pidParameters,"",pidEvent)
	
	} else {
		set idDOHeader=""
		set idTableLine = ""
		for{
			set idDOHeader = $order(^INPOs(0,2,idxPurOrd,idDOHeader))
			quit:idDOHeader=""
			
			set objDOHeader = $get(^INPO(0,idDOHeader,1))
			if objDOHeader'=""{
				set enumDOStatus   = $$$INPOStatus(objDOHeader)
				set strDOStatus    = $$GetDescription^WWWStatus("INPOORDERSTATUS",enumDOStatus) 
				set idShipTo       = $$$INPOShipToLocation(objDOHeader)
				set dteDelivery    = $$^WWWDATE($$$INPOSupplierDeliveryDate(objDOHeader))	
				set idSupplier     = $$$INPOSupplierCode(objDOHeader)
				set fltOrdVal      = $$$INPOOrderValue(objDOHeader) 
			
				if idDOHeader = idLineDOHeader {
					set ^CacheTempDynamicTable(YUCI,YUSER,"dINDOHeader",$increment(idTableLine),"data") = idDOHeader
					set ^CacheTempDynamicTable(YUCI,YUSER,"dINDOHeader",idTableLine) = idDOHeader_Y_"("_enumDOStatus_") "_strDOStatus_Y_idShipTo_Y_dteDelivery_Y_fltOrdVal_Y_idSupplier
				} else {
					set ^CacheTempDynamicTable(YUCI,YUSER,"dINDOHeaderOther",$increment(idTableLine),"data") = idDOHeader
					set ^CacheTempDynamicTable(YUCI,YUSER,"dINDOHeaderOther",idTableLine) = idDOHeader_Y_"("_enumDOStatus_") "_strDOStatus_Y_idShipTo_Y_dteDelivery_Y_fltOrdVal_Y_idSupplier
				}
			}
		}
		
	}
	
	set strStatus = $$BuildDynTableHeader(.lstHeaders,.lstColTypes)
	
				; setup JS Callback
	set strFunction   = "FillLine"  
	if $$$ISERR(strStatus) {

		write "function "_strFunction_"() {"
		write "alert('"_$$$JSText($$$Text(strStatus))_"');"
		write "}"
	} else {
		write "function "_strFunction_"() {"
		write "CallBack(""GoToHeader^INDOTable"", getRowNum(getFocusField()), this.DiscData[0]);"
		write "}"
	}
	
	do DrawTable^WWW120DynTable(lstHeaders,"dINDOHeader",lstColTypes,pidParameters,strFunction,pidEvent,"GoToHeader^INDOTable",$$$Text("IN01185"))
	do DrawTable^WWW120DynTable(lstHeaders,"dINDOHeaderOther",lstColTypes,pidParameters,strFunction,pidEvent,"GoToHeader^INDOTable",$$$Text("IN01185"))
	 	
	quit strStatus
	
BuildDynTableHeader(&plstHeaders,&plstColTypes)
	;-------------------------------------------------------------------------------
	; Setup Table Headers
	; 
	;                         
	; 
	; History:
	; 06-Jan-2010	DWR	 SR17074: Created 
	;-------------------------------------------------------------------------------
	
	;---------------------------------------  1
	set plstHeaders  = $listbuild($$$Text("IN01185"))       					; "Delivery Order"
	set plstColTypes = $listbuild($listbuild("left","display"))
	
	;---------------------------------------  2
	set plstHeaders  =  plstHeaders_$listbuild($$$Text(32781))       			; "Status"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  3
	set plstHeaders  = plstHeaders_$listbuild($$$Text("IN01186"))             	; "Ship to Location"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  4     
	set plstHeaders  = plstHeaders_$listbuild($$$Text(32168))             		; "Delivery Date"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))

	;---------------------------------------  4     
	set plstHeaders  = plstHeaders_$listbuild($$$Text("IN01189"))             	; "Order Value"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  5     
	set plstHeaders  = plstHeaders_$listbuild($$$Text("IN00870"))             	; "Supplier"
	set plstColTypes = plstColTypes_$listbuild($listbuild("right","display"))
	

	quit $$$OK

GoToHeader(pidRow,pidKey)
	;-------------------------------------------------------------------------------
	; go to header Form	
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Jan-2010	DWR		SR17074: Created
	;-------------------------------------------------------------------------------
	new idForm,YPARA
	set YPARA = ""

	set idForm = "INDOHeader"
	set idParentPO = $piece(pidKey,"-",1)
	;set idSupplier = $$GetOrderSupplier(idParentPO)
	
	set YPARA = idParentPO
	do GoToForm^COMUtilForm(idForm,pidKey,,YPARA)
	
	quit
	
GetOrderSupplier(pidPurOrd)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Jan-2010	DWR		SR17074: Created
	;-------------------------------------------------------------------------------
	new idSupplier
	set idSupplier=""
	
	if '$$$NoKey(pidPurOrd) {
		set idSupplier = $$$INPOSupplierCode($get(^INPO(0,pidPurOrd,1)))
	}
	quit idSupplier
]]></Routine>
</Export>