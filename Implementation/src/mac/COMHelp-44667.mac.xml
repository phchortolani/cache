<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHelp" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHelp
	
#include COMSYS
#include COMConst
#include WWWConst
	
OnBeforeFormConstruction(pYKEY)
	;-------------------------------------------------------------------------------
	; When loading the screen, clear and populate the COMHelp* globals
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 09-Sep-2008	shobby	BR014980: Created
	;-------------------------------------------------------------------------------
	new idForm,idLang
	
	set idForm = $$$KEY1(pYKEY)
	set idLang = $$$KEY2(pYKEY)
 
	quit:(idForm="")||(idLang="")
 
 	if '$$IsLocked^WWW006(YUSER,YFORM,pYKEY) {
	 	; If someone else already has it open then don't regenerate.  Form will open "readonly"
	 	do DeleteAll(idForm)
		do LoadIn(idForm,idLang)
 	}
	quit
	
	
OnAfterSave(pYKEY)
	;-------------------------------------------------------------------------------
	; Code to run after saving a record.
	;
	; Params:	pYKEY		: The YKEY value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 16-Nov-2009	DWR		SR16925: PEER changes in consult with shobby.
	; 10-Sep-2008	shobby	BR014980: Changes to support the fact that COMHelp* are
	; 							temporary data
	; 23-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idForm,idLang,objCOMHelp,strHelp,intSeq,objWWW127,strStatus,strText
 
	set idForm = $$$KEY1(pYKEY)
	set idLang = $$$KEY2(pYKEY)
	
	quit:(idForm="")||(idLang="")
 
	;set intSeq = $$GetHeaderHelpIndex(idForm)  ;SR16925
	set intSeq = 0
	
	set objWWW127  = $get(^WWW127(0,idForm,"M",intSeq,idLang,1))
	set objCOMHelp = $get(^COMHelp(0,idForm,idLang,1))
	
	set strHelp = $$$COMHelpHelpText(objCOMHelp)
	
	if (strHelp'="") { // merge over
		set $$$WWW127HelpText(objWWW127) = $$$FormatParagraph(strHelp)
		set strStatus = $$$Save("WWW127",idForm_","_"M"_","_intSeq_","_idLang,objWWW127,$$$YES)
	}
	
	do MergeButtons^COMHelpButton(idForm,idLang)
	do UpdateFieldHelp^COMHelpField(idForm,idLang)
 	do DeleteAll(idForm)
	quit	
	
	
LoadIn(pidForm,pidLang)
	;-------------------------------------------------------------------------------
	; Load in any existing help text data.
	;
	; Params:	pidForm		: The form name
	; 			pidLang		: The language to translate in
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 16-Nov-2009	DWR		SR16925: PEER changes in consult with shobby.
	; 09-Sep-2008	shobby	BR014980: Add created/changed information
	; 23-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idSeq,objCOMHelp,objWWW127,strHelp,strStatus
 
	set strStatus = $$$OK
 
	if '$data(^COMHelp(0,pidForm,pidLang)) { // load in
	;	set idSeq      = $$GetHeaderHelpIndex(pidForm)  ;SR16925
		set idSeq      = 0
		set objCOMHelp = ""
		set objWWW127  = $get(^WWW127(0,pidForm,"M",idSeq,pidLang,1))
		set strHelp    = $$$WWW127HelpText(objWWW127)
		set $$$COMHelpHelpText(objCOMHelp)    = $$$FormatParagraph(strHelp)
		set $$$COMHelpDateCreated(objCOMHelp) = $$$WWW127DateCreated(objWWW127)
		set $$$COMHelpCreatedBy(objCOMHelp)   = $$$WWW127CreatedBy(objWWW127)
		set $$$COMHelpDateChanged(objCOMHelp) = $$$WWW127DateChanged(objWWW127)
		set $$$COMHelpChangedBy(objCOMHelp)   = $$$WWW127ChangedBy(objWWW127)
		set strStatus = $$$Save("COMHelp",pidForm_","_pidLang,objCOMHelp,$$$YES)
	}
	
	if $$$ISOK(strStatus) {
		set idSeq=""
		for {
			set idSeq = $order(^WWW124(0,pidForm,pidLang,idSeq))
			quit:(idSeq="")||($$$ISERR(strStatus))
			
			set strStatus = $$LoadInButton^COMHelpButton(pidForm,pidLang,idSeq)
		}
	}
	if $$$ISOK(strStatus) {
		set strStatus = $$LoadFields^COMHelpField(pidForm,pidLang)
	}
	
	quit strStatus
	
	
GetHeaderHelpIndex(pidForm)  ;  DEPRECATED Not required as SR16925
	;-------------------------------------------------------------------------------
	; Return the index to use for the header help text for this form.
	;
	; Params:	pidForm		: The form id
	;
	; ByRefs:
	;
	; Returns: intSeq
	;
	; History:
	; 26-oct-2009	shobby	SR16925: Header help text only in Manual Field 0.
	; 23-Oct-2006	Steve S	SR14915: Created
	;-------------------------------------------------------------------------------
	new blnManual,intSeq,objWWW122
 
	;set intSeq=""
	
	;if ($get(pidForm)'="") {
	;	set intSeq = +$order(^WWW127(0,pidForm,"M",""))
	;	if (intSeq>0) {
	;		set objWWW122 = $get(^WWW122(0,pidForm,intSeq,1))
	;		set blnManual = ($$$WWW122SequenceNumber(objWWW122)="")
	;		if blnManual && (objWWW122'="") set intSeq=0
	;	}
	;}
 
	;quit intSeq
	quit 0
	
OnBeforeSave(pYKEY)
	;-------------------------------------------------------------------------------
	; Routine to run before saving a data record
	;
	; Params:	pYKEY		: The YKEY value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 24-Oct-2006	Steve S	SR14915: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		$$$Error(strStatus)	
	}
 
	quit
	
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
	; Routine to run after data fields
	;
	; Params:	pYKEY		: The YKEY value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 24-Oct-2006	Steve S	SR14915: Created
	;-------------------------------------------------------------------------------
	if YSEITE = 2 {                         // buttons
		do LoadGrid(pYKEY,"COMHelpButton")
		do PreviewArea()
	
	} elseif YSEITE = 3 {                   // fields
		do LoadGrid(pYKEY,"COMHelpField")
		do PreviewArea()
	}
	
	quit
	
	
LoadGrid(pYKEY,pidForm)
	;-------------------------------------------------------------------------------
	; Load the edit grid
	;
	; Params:	pYKEY		: The YKEY value
	; 			pidForm		: The form for the grid
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 04-Dec-2009	GRF		Show all 20 lines (default)
	; 03-Nov-2006	SteveS	Don't Store/Don't Update macros
	; 24-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new idForm,YAUSWAHL
	
	set idForm = pidForm
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 600    ;400
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = idForm
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "COMHelp"
	
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$YES
	set $$$COMGridEditParameterDontStoreAll(YAUSWAHL)        = $$$YES
	
	$$$GRIDStart(idForm,pYKEY)
 
	quit
		
	
PreviewArea()
	;-------------------------------------------------------------------------------
	; Show the preview area
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 24-Oct-2006	Steve S	SR14915: Created
	;-------------------------------------------------------------------------------
	write "<br><B>"_$$$Text("WWW00049")_"</B><hr>"         ; "Rendered Help Text"
	write "<table style='font-size:14px;width:100%;visibility:hidden' border=1>"
	write "<tr>"
	write "<td id='Preview'></td>"
	write "</tr>"
	write "</table>"
 
	quit
	
	
View(pidForm,pidLang)
	;-------------------------------------------------------------------------------
	; View help text for the current record
	;
	; Params:	pidForm		: The form name
	; 			pidLang		: The language to translate in
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 20-Dec-2006	JW			SR14235: Added reference to child window
	; 25-Oct-2006	Steve S		SR14915: Created
	;-------------------------------------------------------------------------------
	new SPRACHE,strURL
	
	quit:(pidForm="")||(pidLang="")
	
	set SPRACHE = pidLang
	//set blnPOPUP = $$$YES
	//set strURL = strURL_$$WWWCGI2^WWWCGI()
	//write "window.open('"_strURL_"');"
	
	set strURL=YAKTION_"EP=WWWHELP&amp;YFORM="_pidForm_"&amp;SPRACHE="_pidLang
	set strURL = strURL_$$WWWCGI2^WWWCGI($$$YES)	//SR14235
	write "subWindow('"_strURL_"');"
	
	quit
	
	
Delete()
	;-------------------------------------------------------------------------------
	; Just clean all of this stuff.  Indices are probably not necessary because they
	; have all been removed from the class definition but it won't hurt to clean
	; up any that still remain on a destination namespace.
	; 
	; Call from a scheduled task.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 10-Sep-2008	shobby	BR014980: Created
	;-------------------------------------------------------------------------------
	new idClass,arrLocks,idLanguage,intLoop
	
	set idClass = ""
	for {
		set idClass = $order(^COMHelp(0,idClass))
		quit:idClass=""
		
		set idLanguage = ""
		for {
			set idLanguage = $order(^COMHelp(0,idClass,idLanguage))
			quit:idLanguage=""
			
			; a bit of extra trickery for the unlikely case that anyone is editing help text
			; when the cleanup routine runs.
			if $$IsLocked^WWW006("","COMHelp",idClass_","_idLanguage) {
				set arrLocks(idClass,idLanguage) = ""
			
			} else {
				kill ^COMHelp(0,idClass)
				kill ^COMHelpField(0,idClass)
				kill ^COMHelpButton(0,idClass)
			}
		}
	}
	if '$data(arrLocks) {
		;if no locks at all kill everything including any left over indexes from earlier builds
		kill ^COMHelp
		kill ^COMHelps
		kill ^COMHelpField
		kill ^COMHelpFields
		kill ^COMHelpButton
		kill ^COMHelpButtons
	}
	quit
	
DeleteAll(pidForm)
	;-------------------------------------------------------------------------------
	; Delete all help entries for a form
	;
	; Params:	pidForm		: The form id
	;
	; ByRefs:
	;
	; Returns: strStatus
	;
	; History:
	; 10-Sep-2008	shobby	BR014980: Just blow away the ^COMHelp.  Temporary data
	; 							without index
	; 26-Oct-2006	SteveS	SR14915: Created
	;-------------------------------------------------------------------------------
	new strStatus,idSeq,idField,idLang
 
	set strStatus = $$$OK
	
	if ($get(pidForm)="") {   ; FIXME : $$$MakeStatus? <GRF>
		set strStatus = $listbuild("Com00207")  ; "Please Select A Data Record First."
		
	} else {
		kill ^COMHelp(0,pidForm)
		kill ^COMHelpField(0,pidForm)
		kill ^COMHelpButton(0,pidForm)
		;set idLang=""
		;for {
			;set idLang = $order(^COMHelp(0,YUSER,pidForm,idLang))
			;quit:(idLang="")||$$$ISERR(strStatus)
			
			;set strStatus = $$$Kill("COMHelp",YUSER_","_pidForm_","_idLang)
			
			;if $$$ISOK(strStatus) {
			;	set idField=""
			;	for {
			;		set idField = $order(^COMHelpField(0,YUSER,pidForm,idLang,idField))
			;		quit:(idField="")||($$$ISERR(strStatus))
			;		
			;		set strStatus=$$$Kill("COMHelpField",YUSER_","_pidForm_","_idLang_","_idField)
			;	}
			;}	
			
			;if $$$ISOK(strStatus) {
			;	set idSeq=""
			;	for {
			;		set idSeq = $order(^COMHelpButton(0,pidForm,idLang,idSeq))
			;		quit:(idSeq="")||($$$ISERR(strStatus))
			;
			;		set strStatus=$$$Kill("COMHelpButton",pidForm_","_idLang_","_idSeq)			
			;	}
			;}
		;}
	}
	
	quit strStatus
	
]]></Routine>
</Export>