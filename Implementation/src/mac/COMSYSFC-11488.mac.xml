<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMSYSFC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMSYSFC ; Foreign Currency Code
	#include COMSYS
	
	;-------------------------------------------------------------------------------
	; Set of Foreign Currency Utilities
	; Foreign Currency extends the Currency in this format
	; BaseAmount@Currency_ForeignAmount@Rate  (The Rate is optional)
	; 
	; History
	; 24-Apr-2006	GRF		Doco
	; 23-Jun-2005	shobby	Moved from Finance.
	; 25-May-2004	SCR		Change format from BaseAmount@ForeignAmount@Currency@Rate
	; 
	; For Addition and Subtraction procedures
	; - If Foreign Amount comes to zero then all values are to equal zero
	; - If Base Amount comes to zero then rate is to equal zero
	; - Calculate rate from base and foreign amounts
	; 
	;-------------------------------------------------------------------------------
 
GetRounding(pfltAmount)
	;-------------------------------------------------------------------------------
	; Rounding method.
	; 
	; NOTE: Not currently in use.
	;
	; Returns: number of decimal places to round to
	;
	; History:
	; 14-Jun-2005	PK/PO		Created SR:12589
	;-------------------------------------------------------------------------------
    new intSignificant
 
	set intSignificant=""
	if +pfltAmount'=0 {
		set intSignificant=$zlog(1/pfltAmount)\1+4
	}
	if intSignificant<2 set intSignificant=2
	quit intSignificant
 
Add(Amount1="",Amount2="")
	;-------------------------------------------------------------------------------
	; Add FC amounts
	;
	; Returns: Amount
	;
	; History:
	; 24-Apr-2006	GRF			! => ||
	; 24-May-2005	sh/PK/PO	Base should not be set to 0 if Foreign1 is zero.
	; 03-May-2005	PO			SR12256 Do FC side if one of the bases is zero
	; 29-Mar-2005	PO			SR11349 Generate consistent amount, do not have contradictory details
	; 24-May-2004   SCR		  	Created
	;-------------------------------------------------------------------------------
	new Base1,Foreign1,Currency1,Rate1
	new Base2,Foreign2,Currency2,Rate2
	
	do Split(Amount1,.Base1,.Foreign1,.Currency1,.Rate1)
	do Split(Amount2,.Base2,.Foreign2,.Currency2,.Rate2)
	
	set Base1 = Base1+Base2
	if (Currency1'="") || (Base1=0) || (Currency2'="") || (Base2=0) {
		set Foreign1 = Foreign1+Foreign2
		if Currency2'=""                set Currency1 = Currency2      	; Copy Currency
		if (Foreign1'=0) && (Rate2'="") set Rate1     = Base1/Foreign1
	}
	
	if Foreign1=0 {
	;	set Base1 = 0  ;PK doesn't like this.
		set Rate1 = 0
	} elseif Base1=0 {
		set Rate1 = 0
	}
	
	set Amount1 = $$Join(Base1,Foreign1,Currency1,Rate1)
	quit Amount1
 
Sub(Amount1="",Amount2="") 
	;-------------------------------------------------------------------------------
	; Subtract FC amounts
	;
	; Returns: Amount
	;
	; History:
	; 14-Jun-2005	PK/PO		SR:12589 Do not round exchange rate
	; 03-May-2005	PO			SR12256 Do FC side if one of the bases is zero
	; 29-Mar-2005	PO			SR11349 Generate consistent amount, do not have contradictory details
	; 24-May-2004   SCR			Created
	;-------------------------------------------------------------------------------
	new Base1,Foreign1,Currency1,Rate1
	new Base2,Foreign2,Currency2,Rate2
	
	do Split(Amount1,.Base1,.Foreign1,.Currency1,.Rate1)
	do Split(Amount2,.Base2,.Foreign2,.Currency2,.Rate2)
	
	set Base1=Base1-Base2
	if (Currency1'="") || (Base1=0) || (Currency2'="") || (Base2=0) {
		set Foreign1 = Foreign1-Foreign2
		if Currency2'=""                set Currency1 = Currency2                        ; Copy Currency
	;	if (Foreign1'=0) && (Rate2'="") set Rate1     = $fnumber(Base1/Foreign1,"",2)
		if (Foreign1'=0) && (Rate2'="") set Rate1     = Base1/Foreign1
	}
	
	if Foreign1=0 {
		set Rate1 = 0
	} elseif Base1=0 {
		set Rate1 = 0
	}
	
	set Amount1 = $$Join(Base1,Foreign1,Currency1,Rate1)
	quit Amount1
 
 // !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 // Mult NOT TO BE USED, DO NOT WRITE NEW CODE THAT USES THIS FUNCTION. NEED TO CHECK AND CONVERT OVER ALL EXISTING CODE THAT USES THIS
 // HOWEVER THERE MAY BE SOME CASES WHERE JUST SWITCHING OVER TO Mult2 WILL CAUSE ERRORS
 // 
 // Problem with Mult is that it calculates the base amount from the base amount instead of from the foreign
 // !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Mult(Amount="",By="") 
	;-------------------------------------------------------------------------------
	; Multiply FC amounts
	;
	; Returns: Amount
	;
	; History:
	; 24-Apr-2006	GRF		Replace 0 with $$$NEVER for clarity
	; 14-Jun-2005	PK/PO	SR:12589 Do not use FCUpdate
	; 17-Aug-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	set Base=Base*By
	if (Currency'="") {
		set Foreign=Foreign*By
	}
	set Amount=$$Join(Base,Foreign,Currency,Rate)
	
	if $$$NEVER {
		set Amount=$$FCUpdate(Amount,,1)
	}
	
	quit Amount
	
Mult2(Amount="",By="") 
	;-------------------------------------------------------------------------------
	; Multiply FC amounts
	;
	; Returns: Amount
	;
	; History:
	; 24-Apr-2006	GRF		Replace 0 with $$$NEVER for clarity
	; 14-Jun-2005	PK/PO	SR12589 Do not use FCUpdate
	; 17-Aug-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	set Base=Base*By
	
	if (Currency'="") {
		set Foreign=Foreign*By
		set Base=Foreign*Rate
	}
	set Amount=$$Join(Base,Foreign,Currency,Rate)
	if $$$NEVER {
		set Amount=$$FCUpdate(Amount,,1)
	}
	quit Amount
	
Divd(Amount="",By="",pblnRound=$$$NO,pidForm="",pidField="") 
	;-------------------------------------------------------------------------------
	; Divide FC amounts
	; 
	; Inputs:
	;	pblnRound	Flag to indicate amounts returned are to be rounded
	;	pidForm		Optional : Form name
	;	pidField	Optional : Field on form as source of number of decimals
	; Returns: Amount
	;
	; History:
	; 15-Jan-2007	GRF		SR15357: Add pblnRound, pidForm and pidField so $$Divd can
	; 						return a string with rounded amounts rather than having to
	; 						Split and Join again for separate rounding operation.
	; 23-Feb-2005	PO		SR11349 Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	set Base = Base/By
	set:pblnRound Base = $$Round^COMUtilNum(Base,pidForm,pidField)
	; TODO : use WWW012 D75 - Base Currency - and then WWWWAE D8 for Base Currency decimals <GRF>
	
	if (Currency'="") {
		set Foreign = Foreign/By
		set:pblnRound Foreign = $$Round^COMUtilNum(Foreign,pidForm,pidField)
	
	; TODO : use ^WWWWAE D8 - Number of Decimals - for Currency        <GRF>
	}
	set Amount = $$Join(Base,Foreign,Currency,Rate)
	quit Amount
	
Divd2(Amount="",By="") 
	;-------------------------------------------------------------------------------
	; Divide FC amounts
	;
	; Returns: Amount
	;
	; History:
	; 23-Feb-2005	PO		SR11349 Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	if +By=0 set By=1
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	set Base=Base/By
	if (Currency'="") {
		set Foreign=Foreign/By
		set Base=Foreign*Rate
	}
	set Amount=$$Join(Base,Foreign,Currency,Rate)
	quit Amount
 
Sign(Amount)
	;-------------------------------------------------------------------------------
	; Sign Change FC amount
	;
	; Returns: Amount
	;
	; History:
	; 24-May-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	set Base = -Base
	if (Currency'="") {
		set Foreign = -Foreign
	}
	set Amount = $$Join(Base,Foreign,Currency,Rate)
	
	quit Amount
	
Cur(Amount)
	;-------------------------------------------------------------------------------
	; Return Currency Code
	;
	; History:
	; 24-May-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Currency
	
FCBase(Amount)
	;-------------------------------------------------------------------------------
	; Return Base Amount
	;
	; History:
	; 04-Jun-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Base
	
FCAmount(Amount)
	;-------------------------------------------------------------------------------
	; Return Foreign Amount
	;
	; History:
	; 24-May-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Foreign
 
FCRate(Amount)
	;-------------------------------------------------------------------------------
	; Return Conversion Rate
	;
	; History:
	; 24-May-2004   SCR		  Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Rate
 
FCZero(Amount)
	;-------------------------------------------------------------------------------
	; Test for Zero
	;
	; Returns: If Base Currency & Foreign Currency is Zero return 1 else 0
	;
	; History:
	; 02-Jun-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit 'Foreign
 
FCGTZero(Amount)
	;-------------------------------------------------------------------------------
	; Test for greater than zero
	;
	; Returns: If Base Currency & Foreign Currency is greater than Zero return 1 else 0
	;
	; History:
	; 04-Mar-2005	PO		SR11349 Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
 
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
 
	quit Foreign>0
 
FCLTZero(Amount)
	;-------------------------------------------------------------------------------
	; Test for less than zero
	;
	; Returns: If Base Currency & Foreign Currency is less than Zero return 1 else 0
	;
	; History:
	; 04-Mar-2005	PO		SR11349 Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
 
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
 
	quit Foreign<0
	
Round(Amount,pidForm="",pidField="")	
	;-------------------------------------------------------------------------------
	; Round Amount
	;
	; Returns:FCAmount
	;
	; History:
	; 19-Apr-2005	Paul K	Round to the required number of digits for that field. SR:5
	; 24-May-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
    
    ; FIXME: in what cases does the base amount need to be rounded? will this end up like Mult and Divd?
	set Base = $$Round^COMUtilNum(Base,pidForm,pidField)
	
	; Since non FC amounts get into this proc, rounding will need to occur if amount not FC
	if Currency'="" {
		set Foreign = $$Round^COMUtilNum(Foreign,pidForm,pidField)
	}
	set Amount = $$Join(Base,Foreign,Currency,Rate)
	
	quit Amount
	
FCUpdate(Amount="",NewRate="",Mode="")	
	;-------------------------------------------------------------------------------
	; Update Amount
	; 
	; Set the Rate if it is not null
	; if Mode=1 Recalc & Round the Base amount
	; if Mode=2 Recalc & Round the Foreign amount - PO: NOT A NORMAL THING TO DO, POSSIBLY SHOULD NOT BE ALLOWED
	; if Mode=3 Recalc rate from base and FC amounts
	;
	; Returns: Amount
	;
	; History:
	; 10-Jun-2005	SCR/PO		SR12050: Mode 3
	; 25-May-2004   SCR			Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
 
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	if NewRate'="" set Rate=NewRate
	
	if Mode=1 {
	; FIXME: in what cases does the base amount need to be rounded? will this end up like Mult and Divd?
	
	; FIXME: NOTE - This is hard coded to 2dp.  Should probably base on Currency setting instead. <GRF>
	
		set Base=$$Round^COMUtils(Foreign*Rate)
	} elseif (Mode=2)&&(Rate) {
		set Foreign=$$Round^COMUtils(Base/Rate) 
	} elseif (Mode=3)&&(Foreign) {
		set Rate=+$justify(Base/Foreign,0,5)
	}
	
	set Amount=$$Join(Base,Foreign,Currency,Rate)
 
	quit Amount
	
Split(Amount,Base,Foreign,Currency,Rate)	
	;-------------------------------------------------------------------------------
	; Split FC Amount to Base Amount, Foreign Amount, Currency Code, Rate
	; 
	; Inputs :
	;	Amount		Base@FC@Rate   or    Base
	;	            Where FC has form "123ABC", Currency_Foreign
	; 
	; By Ref : Base,Foreign,Currency,Rate
	; Returns: nothing
	;
	; History:
	; 25-May-2004   SCR		Created
	;-------------------------------------------------------------------------------
    new FC
    
    set Base = $piece(Amount,"@",1)
    set FC   = $piece(Amount,"@",2)
    set Rate = $piece(Amount,"@",3)
    set Foreign  = $translate(FC,"abcdefghijklmnopqrstuvwxyz$ABCDEFGHIJKLMNOPQRSTUVWXYZ")
    set Currency = $translate(FC," -1234567890,.")
    
    quit
    
Join(Base,Foreign,Currency="",Rate="")	
	;-------------------------------------------------------------------------------
	; Join FC amount from Base Amount, Foreign Amount, Currency Code, Rate
	;
	; Returns: Amount
	;
	; History:
	; 25-May-2004   SCR		Created
	;-------------------------------------------------------------------------------
	new Amount
 
	set Amount = Base
	if Currency'="" {
		set $piece(Amount,"@",2) = Currency_Foreign
		if Rate'="" {
			set $piece(Amount,"@",3) = Rate
		}
	}
	quit Amount
 
FCConvRate(pstrFC,ptmeStamp)
	;-------------------------------------------------------------------------------
	; Returns the most up to date conversion rate for given time stamp.
	;
	; History:
	; 23-Mar-2005	RobertW	SR11959 Moved to WWWZAL routine FindConversionRate
	; 04-Mar-2005	PO		SR11349 Created
	;-------------------------------------------------------------------------------
    quit $$FindConversionRate^WWWZAHL(pstrFC,$get(ptmeStamp))
 
AmountIsFC(pAmount)
	;-------------------------------------------------------------------------------
	; To determine whether an amount is in Foreign Currency
	;
	; Returns: 1 if amount is in FC else 0
	;
	; History:
	; 07-Mar-2005	PO		Created
	;-------------------------------------------------------------------------------
	quit $$CurIsFC($$Cur(pAmount))
 
CurIsFC(pCur)
	;-------------------------------------------------------------------------------
	; To determine whether a currency is Foreign or not
	;
	; Returns: 1 if currency is Foreign else 0
	;
	; History:
	; 08-Mar-2005	PO		Created
	;-------------------------------------------------------------------------------
	quit (pCur'="")&&(pCur'=YWHR)
 
FCIsEqual(pAmountLft,pAmountRht)
	;-------------------------------------------------------------------------------
	; Determine whether two Foreign Currency amounts are equal
	;
	; Returns: 1 if amounts are equal, else 0
	;
	; History:
	; 22-Mar-2005	PO		Created
	;-------------------------------------------------------------------------------
	quit +$$$FCAmount(pAmountLft)=+$$$FCAmount(pAmountRht)
	
FCMake(pfltBase,pstrCurrency,pfltRate)
	;-------------------------------------------------------------------------------
	; Reverse create a foreign currency string from a base amount, the conversion 
	; rate and the currency rate
	;
	; Returns:
	; 	String: The join and calculated foreign currency string
	;
	; History:
	; 08-Apr-2005	RobertW		Created SR11836
	;-------------------------------------------------------------------------------
	if +pfltRate=0 set pfltRate=1
	quit $$Join(pfltBase,(pfltBase/pfltRate),pstrCurrency,pfltRate)
 
GetAmountText(pAmount)
	;-------------------------------------------------------------------------------
	; Get the Amount in Text either $100.00 for Base currency or USD $304.23 for foreign Curreny
	;
	; Params: 
	; pAmount = Amount to convert
	;
	; Returns: Text
	;
	; History:
	; 06-Nov-2005	SCR		SR13578: Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate,strAmount
	
	do Split(pAmount,.Base,.Foreign,.Currency,.Rate)
	
	set strAmount = "$"_$justify(Base,0,2)
	if Currency'="" {
		set strAmount = Currency_" $"_$justify(Foreign,0,2)
	}
	
	quit strAmount
	
]]></Routine>
</Export>