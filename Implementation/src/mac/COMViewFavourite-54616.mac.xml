<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewFavourite" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewFavourite
	#include COMSYS
	#include COMConst
	#include COMView
	; 23-Dec-2013	shobby	Resubmit after failed compile due to missing COMConst for COMViewD

 
GetAvailableView(pidClass)
	;-------------------------------------------------------------------------------
	; Return the first available view (favourite) for searching on this class.
	;
	; Params:	pidClass	: The class id
	;
	; Returns:	idView
	;
	; History:
	; 22-May-2007	RPW		SR<place SR Number here>: 
	; xx-Feb-2007	Steve S		SR15431: Use Save macro to save COMView entry
	; 20-Feb-2007	Steve S		SR15440: Created
	;-------------------------------------------------------------------------------
	new idView,arrViews,blnInForm,idViewLoop,objView,strStatus,idOldView
 
	set idView = $$GetCurrentView^COMView(pidClass)
	
	do GetViews(pidClass,YLOCATION,YBED,.arrViews)
	
 	if '$data(arrViews(idView)) {
		set blnInForm=$get(^CacheTempView(YUSER,YUCI,"InForm"))
		if blnInForm {
			set idOldView=idView
			$$$Order1(arrViews,idViewLoop) // get first view which is 'locked'
				set objView = $get(^COMView(0,pidClass,idViewLoop,1))
				if $$$COMViewLock(objView) {
					set idView=idViewLoop
					quit
				}
			$$$End
			
			if idView=idOldView {
				set idView=""
			}
			
		} else { // pick up the first (default)
			set idView=$order(arrViews(""))
		}
 	}
 	
 	if (idView="")||'$data(^COMView(0,pidClass,idView,1)) {
	 	// Create default view
		set idView=0
		if '$data(^COMView(0,pidClass,idView,1)) {
			set objView=""
			set $$$COMViewDescription(objView)=$$$Text("Com00119")   ;Default
			set strStatus = $$$Save("COMView",pidClass_","_idView,objView,1) //SR15431
		}
	}
 
	quit idView
	
	
GetViews(pidClass,pidLocation,pidUser,&parrView)
    ;-------------------------------------------------------------------------------
    ; Loop through all the views for this class and find those that are for this
    ; location and user.
    ;
    ; Params:		pidClass	: The class we're searching on
    ; 				pidLocation	: Location drill-down
    ; 				pidUser		: User id drill-down
    ; 				parrView	: (By ref) Array of views
    ; 
    ; Returns:
    ;
    ; History:
    ; 20-Dec-2012	shobby	CORE-299: Don't show favourites that have been customised as 'Hide'
    ; 02-Apr-2009	shobby	SR16462: Created a default 'In form' view if none is available.
    ; 26-Mar-2007	PO		NO SR: Use blnLock variable
    ; 19-Feb-2007	SteveS	SR15440: Lock drill-down, moved from COMView.mac
    ; 14-Feb-2007	SteveS	SR15431: Form-specific favourite support
    ; 							 Pass views back in an array by ref
    ; 							 Order macro, 'continue' instead of nested 'if'
    ; 30-Nov-2004	Paul K	Created
    ;-------------------------------------------------------------------------------
    new idView,idUser,idLocn,objView,idViewForm,blnInForm,blnLock,strStatus
    
    ; TODO: This routine seems to only be called using YLOCATION / YBED as
    ; 	    pidLocation and pidUser. Do we need these parameters?
    
    set blnInForm = $get(^CacheTempView(YUSER,YUCI,"InForm")) //SR15440
    
	set idView = "" 
	for {
		set idView = $order(^COMView(0,pidClass,idView)) 
		quit:idView=""

		set objView    = $get(^COMView(0,pidClass,idView,1))
		set idLocn     = $$$COMViewLocation(objView)
		set idUser     = $$$COMViewUser1(objView)
		set idViewForm = $$$COMViewForm(objView)
		set blnLock    = +$$$COMViewLock(objView)
		
		continue:(idLocn'="")&&(pidLocation'=idLocn)               // location drill-down
		continue:(idUser'="")&&(idUser'=pidUser)                   // user drill-down
		continue:(idViewForm'="")&&(idViewForm'=$$$CallingForm)    // Form drill-down	//SR15431
		continue:blnInForm&&'blnLock                               // Lock drill-down SR15440   PO Changed
		
		if '$$$COMViewDHide($get(^COMViewD(YM,pidClass,idView,YM,1))) set parrView(idView)="" // SR15431 ;CORE-299
	}
	
 	if blnInForm && '$data(parrView) {        ;16462
	 	set idView  = $$^WWWNEXT("COMView")
	 	set objView = ""
		set $$$COMViewDescription(objView) = $$$Text("Com00322")   ;In form default
		set $$$COMViewLock(objView)        = $$$YES
		set strStatus = $$$Save("COMView",pidClass_","_idView,objView,$$$YES) //SR15431
		set parrView(idView) = ""
 	}
	
	quit
]]></Routine>
</Export>