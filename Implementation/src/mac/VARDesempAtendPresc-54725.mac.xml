<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARDesempAtendPresc" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARDesempAtendPresc
	#include COMSYS
	#include WWWConst
	#include MEDConst
	#include VARConst
	quit
	
OnAfterSave

	set $piece(^VARTempDesempAtend(YM,YBED,1),Y,1) = $get(VORG(1)) ; Data Inicial
	set $piece(^VARTempDesempAtend(YM,YBED,1),Y,2) = $get(VORG(2)) ; Data Final
	set $piece(^VARTempDesempAtend(YM,YBED,1),Y,3) = $get(VORG(3)) ; Und. Atendimento
	set $piece(^VARTempDesempAtend(YM,YBED,1),Y,4) = $get(VORG(4)) ; Prescrição Urgente
	set $piece(^VARTempDesempAtend(YM,YBED,1),Y,5) = $get(VORG(5)) ; Atendimento
	
	if ($get(VORG(6)) = ""){		
		set $piece(^VARTempDesempAtend(YM,YBED,1),Y,6) = 2 ; Formato
	}
	else {
		set $piece(^VARTempDesempAtend(YM,YBED,1),Y,6) = $get(VORG(6)) ; Formato
	}
	
	do VARLogAfterSave^VARUtil(YBED,YFORM)	
 	do GoToForm^COMUtilForm("VARDesempAtendPresc",1,,,,,)

	quit
	
OnAfterDataFields

	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	set objTemp = $get(^VARTempDesempAtend(YM,YBED,1))
	
	set pDataInicial		 = $piece(objTemp,Y,1)
	set pDataFinal 		  	 = $piece(objTemp,Y,2)
	set pUnAtendimento     	 = $piece(objTemp,Y,3)
	set pPrescUrgente	 	 = $piece(objTemp,Y,4)
	set pAtendimento  		 = $piece(objTemp,Y,5)
	set pFormato			 = $piece(objTemp,Y,6)
	
	write "<br>"
	write "<b>Observação</b>: os medicamentos manipulados (Farmacotécnica) não são contemplados no relatório."
	write "<br><br>"
	
	if (pFormato = 2){
		do MontaConsultaSimplificada(pDataInicial,pDataFinal,pUnAtendimento,pPrescUrgente,pAtendimento,pFormato)
	}
	else {
		do MontaConsultaCompleta(pDataInicial,pDataFinal,pUnAtendimento,pPrescUrgente,pAtendimento,pFormato)
		//do MontaConsultaSimplificada(pDataInicial,pDataFinal,pUnAtendimento,pPrescUrgente,pAtendimento,pFormato)
	}
	
	quit
	
MontaConsultaSimplificada(pDataInicial,pDataFinal,pUnAtendimento,pPrescUrgente,pAtendimento,pFormato)
      					  		
	set meuSQL = ""
	set meuSQL = meuSQL_" SELECT "
	set meuSQL = meuSQL_" admission.Location as Location,"
	set meuSQL = meuSQL_" $$SQLGetLocationName^VARSQL(Location) AS AdmissionLocationDesc, "
	set meuSQL = meuSQL_" count(DISTINCT presc.PrescriptionNumber) AS countPresc,"
	set meuSQL = meuSQL_" count(line.Line) AS countLines,"
	set meuSQL = meuSQL_" SUM($$GetCountDispensacoes^VARDesempAtendPresc(line.PrescriptionNumber,line.Line)) AS countDisp,"
	set meuSQL = meuSQL_" SUM($$GetQtyConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line)) AS countConform,"
	set meuSQL = meuSQL_" SUM($$GetQtyNConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line)) AS countNConform"
	set meuSQL = meuSQL_" FROM MEDPrescription presc"
	set meuSQL = meuSQL_" JOIN MEDPrescriptionLine line ON presc.PrescriptionNumber = line.PrescriptionNumber"
	set meuSQL = meuSQL_" JOIN MEDAdmission admission ON presc.ReasonForAdmission = admission.AdmissionID"
	set meuSQL = meuSQL_" WHERE line.Status IS NOT NULL "
	set meuSQL = meuSQL_" AND (line.Status NOT IN ('6', '8', '10') OR line.DispensedQty IS NOT NULL)"
	set meuSQL = meuSQL_" AND $$CheckDoseExists^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) = 1"
	set meuSQL = meuSQL_" AND $$CheckAtendimentoEmAtraso^VARDesempAtendPresc($$GetQtyConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line),"_
						" $$GetQtyNConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line)) = 1"
	set meuSQL = meuSQL_" AND $$hasLineBeenDispensedByUDO^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) = 0"
	set meuSQL = meuSQL_" AND line.FromDate >= "_+pDataInicial_ ""
	set meuSQL = meuSQL_" AND line.ToDate <= "_+pDataFinal_ ""
	
	if (pUnAtendimento '= ""){
		set meuSQL = meuSQL_" AND Location = "_pUnAtendimento_ ""
	}
	
	if (pPrescUrgente '= ""){
		if (pPrescUrgente = 1){
			set meuSQL = meuSQL_" AND line.Urgent = "_$$$YES_ ""
		}
		else {
			set meuSQL = meuSQL_" (AND line.Urgent <> "_$$$YES_ " or line.Urgent is null)"
		}
	}
	
	set meuSQL = meuSQL_" GROUP BY Location"
	set meuSQL = meuSQL_" ORDER BY Location"

	do MontaGridSimplificado(meuSQL)
		
	quit

MontaConsultaCompleta(pDataInicial,pDataFinal,pUnAtendimento,pPrescUrgente,pAtendimento,pFormato)
  					  		
	set columnsClause = ""
	set columnsClause = columnsClause_" admission.Location as Location,"
	set columnsClause = columnsClause_" $$SQLGetLocationName^VARSQL(Location) AS AdmissionLocationDesc, "
	set columnsClause = columnsClause_" line.PrescriptionNumber AS Prescricao,"
	set columnsClause = columnsClause_" line.Line AS Linha,"
	set columnsClause = columnsClause_" line.Item AS Codigo,"
	set columnsClause = columnsClause_" $$SQLGetDescricaoProduto^VARSQL(line.Item) AS Descricao,"
	set columnsClause = columnsClause_" $$GetDispensacoes^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) AS Dispensacao,"
	set columnsClause = columnsClause_" $$GetQtyConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) AS Conforme,"
	set columnsClause = columnsClause_" $$GetQtyNConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) AS NConforme"


	set sql = ""
	set sql = sql_" FROM MEDPrescription presc"
	set sql = sql_" JOIN MEDPrescriptionLine line ON presc.PrescriptionNumber = line.PrescriptionNumber"
	set sql = sql_" JOIN MEDAdmission admission ON presc.ReasonForAdmission = admission.AdmissionID"
	set sql = sql_" WHERE line.Status IS NOT NULL "
	set sql = sql_" AND (line.Status NOT IN ('6', '8', '10') OR line.DispensedQty IS NOT NULL)"
	set sql = sql_" AND $$CheckDoseExists^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) = 1"
	set sql = sql_" AND $$CheckAtendimentoEmAtraso^VARDesempAtendPresc($$GetQtyConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line),"_
						" $$GetQtyNConforme^VARDesempAtendPresc(line.PrescriptionNumber,line.Line)) = 1"

	set sql = sql_" AND $$hasLineBeenDispensedByUDO^VARDesempAtendPresc(line.PrescriptionNumber,line.Line) = 0"

	set sql = sql_" AND line.FromDate >= "_+pDataInicial_ ""
	set sql = sql_" AND line.ToDate <= "_+pDataFinal_ ""
	;set sql = sql_" AND presc.PrescriptionNumber = ""13/068970"""
	
	if (pUnAtendimento '= ""){
		set sql = sql_" AND Location = "_pUnAtendimento_ ""
	}
	
	if (pPrescUrgente '= ""){
		if (pPrescUrgente = 1){
			set sql = sql_" AND line.Urgent = "_$$$YES_ ""
		}
		else {
			set sql = sql_" AND (line.Urgent <> "_$$$YES_ " or line.Urgent is null)"
		}
	}
	
	set orderBy = ""
	set orderBy = orderBy_" ORDER BY Location, line.PrescriptionNumber, line.Line "
	
	set completeSQL = "SELECT "_columnsClause_sql_orderBy
	set countSQL = "SELECT COUNT(*)"_sql
	
	do MontaGridCompleto(completeSQL, countSQL)
		
	quit
	
MontaGridSimplificado(pMeuSQL)

	#dim rsDesempAtendSimpl = ##class(%ResultSet).%New() 
	do rsDesempAtendSimpl.Prepare(pMeuSQL)
	set queryStatus = rsDesempAtendSimpl.Execute() 
	
	new lstHeader
 	
 	set lstHeader =  $listbuild("Local","Prescrições","Itens","Dispensações","Doses <br/> &nbsp; Conforme","Doses <br/> &nbsp; Não Conforme")
 	set seq = 0
 	set totalCountPresc    = 0
 	set totalCountItens    = 0
 	set totalCountDisp     = 0
 	set totalCountConform  = 0
 	set totalCountNConform = 0
 	
  	if $$Start^COMTable(lstHeader) {
	  			
		while (rsDesempAtendSimpl.Next()) {
			set unAtendimento			= rsDesempAtendSimpl.Data("Location")
			set unAtendimentoDesc		= rsDesempAtendSimpl.Data("AdmissionLocationDesc")
			set countPresc				= rsDesempAtendSimpl.Data("countPresc")
			set countItens				= rsDesempAtendSimpl.Data("countLines")
			set countDisp				= rsDesempAtendSimpl.Data("countDisp")
			set countConform			= rsDesempAtendSimpl.Data("countConform")
			set countNConform			= rsDesempAtendSimpl.Data("countNConform")

			set seq = $increment(seq)
 			set totalCountPresc    = totalCountPresc 	+ countPresc
 			set totalCountItens    = totalCountItens 	+ countItens
 			set totalCountDisp     = totalCountDisp 	+ countDisp
 			set totalCountConform  = totalCountConform 	+ countConform
 			set totalCountNConform = totalCountNConform + countNConform
			
			do NewLine^COMTable()
			do InsertCell^COMTable(unAtendimento_" - "_unAtendimentoDesc)
			do InsertCell^COMTable($$^WWWTR(0,4,countPresc),,,,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,4,countItens),,,,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,4,countDisp),,,,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,4,countConform),,,,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,4,countNConform),,,,"RIGHT")
			do EndLine^COMTable()	
			
		}
		
		if (seq = 0){
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum resultado encontrado para a pesquisa.",,,,,,6)
			do EndLine^COMTable()	
		}
		elseif (pUnAtendimento = "") {
			do NewLine^COMTable("lightsteelblue")
			do InsertCell^COMTable("<b>TOTAL GERAL </b>",,,,,,)
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalCountPresc)_"</b>",,,,"RIGHT")
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalCountItens)_"</b>",,,,"RIGHT")
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalCountDisp)_"</b>",,,,"RIGHT")
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalCountConform)_"</b>",,,,"RIGHT")
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalCountNConform)_"</b>",,,,"RIGHT")
			do EndLine^COMTable()			
		}
		
		do Stop^COMTable()	
  	}
	do rsDesempAtendSimpl.Close()

	quit
	
MontaGridCompleto(pMeuSQL, pCountSQL)

	set qtdLinhas = +$$$VARParametroClienteQuantidadedelinhasnapagin(^VARParametroCliente(YM,YM,1))
	set pagination = ##Class(VAR.infra.util.Pagination).%New(20)
	#dim rsDesempAtendCompl As PaginationResultSet
	set rsDesempAtendCompl = pagination.ExecuteQuery(,pCountSQL,pMeuSQL)

	new lstHeader
 	
 	set lstHeader =  ""
 	
 	do pagination.PrintNavigationBar(rsDesempAtendCompl,"SAVE")
 	if $$Start^COMTable(lstHeader) {
		
		set seq = 0
		set unAtendimentoAntes    = ""
		;set totalPrescricoes 	  = 0
		;set totalConformeUnidade  = 0
		;set totalNConformeUnidade = 0
		;set totalGeralConformeUnidade  = 0
		;set totalGeralNConformeUnidade = 0
		
		while (rsDesempAtendCompl.Next()) {
			set unAtendimento			= rsDesempAtendCompl.Data("Location")
			set unAtendimentoDesc		= rsDesempAtendCompl.Data("AdmissionLocationDesc")
			set prescricao				= rsDesempAtendCompl.Data("Prescricao")
			set linha					= rsDesempAtendCompl.Data("Linha")
			set item					= rsDesempAtendCompl.Data("Codigo")
			set descItem				= rsDesempAtendCompl.Data("Descricao")
			set dispensacoes			= rsDesempAtendCompl.Data("Dispensacao")
			set conforme				= rsDesempAtendCompl.Data("Conforme")
			set nConforme				= rsDesempAtendCompl.Data("NConforme")
			
			set seq = $increment(seq)
			
			if (unAtendimentoAntes '= unAtendimento){
				
				if (unAtendimentoAntes '= ""){
					;do NewLine^COMTable("palegoldenrod")
					;do InsertCell^COMTable("<b>Total Local</b>",,,,,,5)
					;do InsertCell^COMTable("<b>"_totalConformeUnidade_"</b>",,,,"RIGHT")
					;do InsertCell^COMTable("<b>"_totalNConformeUnidade_"</b>",,,,"RIGHT")
					;do EndLine^COMTable()
					do PrintBlankLine()	
					;set totalConformeUnidade  = 0
					;set totalNConformeUnidade = 0
				}
				
				
				do NewLine^COMTable("lightsteelblue")
				do InsertCell^COMTable("<b>Local "_unAtendimento_" - "_unAtendimentoDesc_"</b>",,,,,,7)
				do PrintColumnHeaderCompleto
				do EndLine^COMTable()	
			}
							
			do NewLine^COMTable()
			do InsertCell^COMTable(prescricao,"MEDPrescriptionHosp",prescricao,,"CENTER",,,,1)
			do InsertCell^COMTable(linha,"MEDPrescriptionHosp",prescricao,,"CENTER",,,,1)
			do InsertCell^COMTable(item,,,,"CENTER")
			do InsertCell^COMTable(descItem,,,,"LEFT")
			do InsertCell^COMTable(dispensacoes,,,,"LEFT")
			do InsertCell^COMTable($$^WWWTR(0,4,conforme),,,,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,4,nConforme),,,,"RIGHT")
			do EndLine^COMTable()	
			
			;set totalConformeUnidade  = totalConformeUnidade  + conforme
			;set totalNConformeUnidade = totalNConformeUnidade + nConforme
			
			;set totalGeralConformeUnidade  = totalGeralConformeUnidade  + conforme
			;set totalGeralNConformeUnidade = totalGeralNConformeUnidade + nConforme
			
			if (unAtendimentoAntes '= unAtendimento){
				set unAtendimentoAntes = unAtendimento
			}
						
		}
		
		if (seq = 0){
			do PrintColumnHeaderCompleto
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum resultado encontrado para a pesquisa.",,,,,,7)
			do EndLine^COMTable()	
		} /*
		else {
			do NewLine^COMTable("palegoldenrod")
			do InsertCell^COMTable("<b>Total Local</b>",,,,,,5)
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalConformeUnidade)_"</b>",,,,"RIGHT")
			do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,totalNConformeUnidade)_"</b>",,,,"RIGHT")
			do EndLine^COMTable()
		}*/
		
		do Stop^COMTable()	
  	}
	do rsDesempAtendCompl.Close()

	quit

PrintBlankLine()
	do NewLine^COMTable("gainsboro")
	do InsertCell^COMTable("",,,,,,7)
	do EndLine^COMTable()	
	
	quit
	
PrintColumnHeaderCompleto

	do NewLine^COMTable("darkgray")
	do InsertCell^COMTable("<b>Prescrição				</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Linha					</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Código					</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Descrição				</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Dispensação				</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Doses Conforme		    </b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Doses Não Conforme	    </b>",,,,"CENTER")
	do EndLine^COMTable()
	
	quit
	
PrintColumnHeaderSimplificado

	do NewLine^COMTable("darkgray")
	do InsertCell^COMTable("<b>Local					</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Prescrições				</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Itens					</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Dispensações				</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Doses Conforme			</b>",,,,"CENTER")
	do InsertCell^COMTable("<b>Doses Não Conforme		</b>",,,,"CENTER")
	do EndLine^COMTable()
	
	quit
	
PrintTotalGeral(pTotalGeralConforme,pTotalGeralNConforme)

	do NewLine^COMTable("lightsteelblue")
	do InsertCell^COMTable("<b>TOTAL GERAL</b>",,,,,,5)
	do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,pTotalGeralConforme)_"</b>",,,,"RIGHT")
	do InsertCell^COMTable("<b>"_$$^WWWTR(0,4,pTotalGeralNConforme)_"</b>",,,,"RIGHT")
	do EndLine^COMTable()
	
	quit
		
GetDispensacoes(pPrescricao,pPrescLine,&arrDispensacoes)
	new str, disp
	set pPrescricao = $$$Index(pPrescricao)
	set str = ""
	&sql(
		DECLARE dispensacoes CURSOR FOR
		SELECT Dispensation 
		INTO :disp
		FROM MEDDispense disp 
		WHERE Status = '3' AND %upper(Prescription) = :pPrescricao 
		ORDER BY Dispensation
	)
	&sql(OPEN dispensacoes)
	&sql(FETCH dispensacoes)
		
	set arrDispensacoes = ""

	while (SQLCODE = 0){

		if ('$$IsDispensacaoLineUDO(disp,pPrescLine)) {
			set arrDispensacoes(disp) = ""
			if (str = ""){
				set str = disp
			} else {
				set str = str_","_disp
			}
		}
		&sql(FETCH dispensacoes)	
	}
	&sql(CLOSE dispensacoes)
	
	quit str
	
CountDispensacoes(pPrescricao,pPrescLine)

	new count
	set count=0
	
	set idDispense = ""
	for {
		set idDispense = $order(^MEDDispenses(0,4,$$$Index(pPrescricao),3,idDispense))
		quit:idDispense=""
		
		if ('$$IsDispensacaoLineUDO(idDispense,pPrescLine)) {
			
			set count = count + 1
		}
	}

	quit count
	
hasLineBeenDispensedByUDO(pPrescricao,pPrescLine)

	new dispensedByUDO, idDispense
	
	set idDispense = ""
	set dispensedByUDO = 0
	
	for {
		set idDispense = $order(^MEDDispenses(0,1,$$$Index(pPrescricao),idDispense))
		
		quit:(idDispense="")
		
		if ($$IsDispensacaoLineUDO(idDispense,pPrescLine)) {
			
			set dispensedByUDO = 1
			quit
		}
	}
	
	quit dispensedByUDO
	
IsDispensacaoLineUDO(pDispensacao, pLinha)

	new objDispLine, udo

	set objDispLine = $get(^MEDDispenseLine(0,pDispensacao,pLinha,1))
	set udo = $$$MEDDispenseLineUDO(objDispLine)

	quit +udo
	
GetCountDispensacoes(pPrescricao,pPrescLine)

	new countDispensacoes, objConformidade

	set objConformidade = $get(^VARConformidade(pPrescricao, pPrescLine))
	set countDispensacoes = $piece(objConformidade, "~", 2)
	
	if (countDispensacoes = "") {
		
		set countDispensacoes = $$CountDispensacoes(pPrescricao, pPrescLine)
	}
	
	quit countDispensacoes


CheckDoseExists(pPrescricao,pPrescLine)
	if ($data(^VARDose(0,pPrescricao,pPrescLine))) quit $$$OK
	quit $$$NO
	
CountTotalDoses(pPrescricao,pPrescLine)
	new count
	set count = 0
	&sql(
		SELECT COUNT(*) INTO :count FROM VARDose WHERE Prescricao = :pPrescricao AND Linha = :pPrescLine
	)
	quit count

GetQtyConforme(pPrescricao,pPrescLine)

	new conformidade

	set conformidade = $$GetConformidade(pPrescricao, pPrescLine)

	quit $piece(conformidade,";",1)
	
GetQtyNConforme(pPrescricao,pPrescLine)

	new conformidade
	
	set conformidade = $$GetConformidade(pPrescricao, pPrescLine)

	quit $piece(conformidade,";",2)

GetQtyConformidade(pPrescricao,pPrescLine)
	new cConformes, cNConformes, arrDispensacoes, conformidades, listaDisp,
		disp, objDisp, objDispLine, prazoPrevisto, dtHDispensacao,
		dose, dosePresc, objPrescLine
	
	set cConformes    = 0
	set cNConformes   = 0
	set conformidades = cConformes_";"_cNConformes
	
	set objPrescSetup = $get(^MEDPrescriptionSetup(0,0,1))
	set objPrescLine  = $get(^MEDPrescriptionLine(0,pPrescricao,pPrescLine,1))

	quit:($$$MEDPrescriptionLineDoseQty(objPrescLine)=0) conformidades
	quit:($$hasLineBeenDispensedByUDO(pPrescricao,pPrescLine)=1) conformidades
	
	set arrDispensacoes = ""
	
	set listaDisp = $$GetDispensacoes(pPrescricao,pPrescLine, .arrDispensacoes)
	
	set qtyDosesTotal = $$CountTotalDoses^VARDesempAtendPresc(pPrescricao,pPrescLine)
	set qtyTotalDosesDispensadas = 0
	
	if ($data(arrDispensacoes) = 11){
		
		k ^VARTempDoseChecked
		set disp = ""	
		for {
			set disp = $order(arrDispensacoes(disp))
			quit:(disp = "")
			
			set objDisp = $get(^MEDDispense(0,disp,1))
			
			set objDispLine    		= $get(^MEDDispenseLine(0,disp,pPrescLine,1))
			
			set dtHDispensacao 		= $$$MEDDispenseDateProcessed(objDisp)
			
			set qtyIssueConverted = $$Convert^INUOMConversion($$$MEDDispenseLineItemName(objDispLine),$$$MEDDispenseLineIssueQuantity(objDispLine),$$$MEDDispenseLineIssueUOM(objDispLine),$$$MEDDispenseLineDoseUOM(objDispLine))
			
			set qtyDosesDispensadas = (qtyIssueConverted / $$$MEDPrescriptionLineDoseQty(objPrescLine))
			set qtyTotalDosesDispensadas = qtyTotalDosesDispensadas + qtyDosesDispensadas

			for dose=1:1:qtyTotalDosesDispensadas {
				quit:(dose > qtyDosesTotal)
				continue:($data(^VARTempDoseChecked(dose)))

								
				set objDose       = $get(^VARDose(0,pPrescricao,pPrescLine,dose,1))
				set aprazamento   = $$$VARDosePrevisaodeAdministracao(objDose)
				
				if ($$$MEDPrescriptionLineUrgent(objPrescLine) '= ""){
					set prazoPrevisto  = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("mi",($$$MEDPrescriptionSetupFREE1(objPrescSetup)/60),aprazamento),3,1)
				} else {
					set prazoPrevisto  = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("mi",-($$$MEDPrescriptionSetupFREE2(objPrescSetup)/60),aprazamento),3,1)
				}
				
				set ^VARTempDoseChecked(dose) = "checked"	
				
				if (dtHDispensacao = "") set dtHDispensacao = $horolog
				do ##class(VAR.infra.util.DateTime).getHorologDiff("mi",dtHDispensacao,prazoPrevisto,.diff)

				if (diff>=0){
					set cConformes  = $i(cConformes)
				} else {
					set cNConformes = $i(cNConformes)
				}	
			}
		}
		
	} else {
	
		quit:'$$$MEDPrescriptionLineFromDate(objPrescLine) conformidades
		set aprazamento = $$$MEDPrescriptionLineFromDate(objPrescLine)_","_$$$MEDPrescriptionLineFromTime(objPrescLine)
		
		if ($$$MEDPrescriptionLineUrgent(objPrescLine) '= ""){
			set prazoPrevisto  = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("mi",($$$MEDPrescriptionSetupFREE1(objPrescSetup)/60),aprazamento),3,1)
		} else {
			set prazoPrevisto  = $zdatetimeh(##class(%SYSTEM.SQL).DATEADD("mi",-($$$MEDPrescriptionSetupFREE2(objPrescSetup)/60),aprazamento),3,1)
		}

		set dtHDispensacao = $horolog

		do ##class(VAR.infra.util.DateTime).getHorologDiff("mi",dtHDispensacao,prazoPrevisto,.diff)

		if (diff>=0){
			set cConformes  = 0
		} else {
			set cNConformes = qtyDosesTotal
		}		
	
	}
	
	set conformidades = cConformes_";"_cNConformes
	
	quit conformidades
			
CheckAtendimentoEmAtraso(pQtyConforme,pQtyNConforme)
	if ((pQtyConforme = 0) && (pQtyConforme = pQtyNConforme)) quit 0
	quit 1

Imprimir

	set objTemp = $get(^VARTempDesempAtend(YM,YBED,1))
	
	set pDataInicial		 = $piece(objTemp,Y,1)
	set pDataFinal 		  	 = $piece(objTemp,Y,2)
	set pUnAtendimento     	 = $piece(objTemp,Y,3)
	set pPrescUrgente	 	 = $piece(objTemp,Y,4)
	set pAtendimento  		 = $piece(objTemp,Y,5)
	set pFormato			 = $piece(objTemp,Y,6)	
	
	set pFiltro = ""
		
	set pFiltro = pFiltro_" AND line.FromDate >= "_+pDataInicial_ ""
	set pFiltro = pFiltro_" AND line.ToDate <= "_+pDataFinal_ ""
	
	if (pUnAtendimento '= ""){
		set pFiltro = pFiltro_" AND Location = "_pUnAtendimento_ ""
	}
	
	if (pPrescUrgente '= ""){
		if (pPrescUrgente = 1){
			set pFiltro = pFiltro_" AND line.Urgent = "_$$$YES_ ""
		}
		else {
			set pFiltro = pFiltro_" AND (line.Urgent <> "_$$$YES_ " or line.Urgent is null)"
		}
	}
	do RunReportDesempAtendPresc^VARJasperRunReportContinuacao(pDataInicial,pDataFinal,pUnAtendimento,pPrescUrgente,pAtendimento,pFormato,pFiltro)
	
	quit
	
GetConformidade(pIdPrescription, pIdline)	

	new conformidades, countDispensacoes, lineStatusConformidade, objConformidade

	set objConformidade = $get(^VARConformidade(pIdPrescription, pIdline))
	set lineStatusConformidade = $piece(objConformidade,"~",3)
	
	if ($$IsLineWaitingForDispensation(lineStatusConformidade)) {
		
		set conformidades = $$CalculateAndSaveConformityAndDipensationCount(pIdPrescription, pIdline)
						
	} else {
		set conformidades = $piece(objConformidade, "~", 1)
	}
	
	quit conformidades
	
CalculateAndSaveConformityAndDipensationCount(pIdPrescription, pIdline)

	new conformidades, countDispensacoes, lineStatus

	set conformidades = $$GetQtyConformidade(pIdPrescription, pIdline)
	set countDispensacoes = $$CountDispensacoes(pIdPrescription, pIdline)

	;write "Calculando conformidade para: "_pIdPrescription_","_pIdline_" -> "
	;write conformidades_" -> Dispensações: "_countDispensacoes,!

	set lineStatus = $piece(^MEDPrescriptionLine(0,pIdPrescription, pIdline, 1), "~", 43)
	set ^VARConformidade(pIdPrescription, pIdline)=conformidades_"~"_countDispensacoes_"~"_lineStatus_"~"_$horolog

	quit conformidades
	
CalculateAllConformities()

	new idPrescription, line
	set idPrescription = ""

	for {
		set idPrescription = $order(^MEDPrescription(0,idPrescription),-1)
		quit:idPrescription=""
		
		set line = ""
		
		for {
			set line = $order(^MEDPrescriptionLine(0,idPrescription, line))
			quit:line=""
			
			do CalculateAndSaveConformityAndDipensationCount(idPrescription, line)
		}		
	}

	quit
	
IsLineWaitingForDispensation(pLineStatus)

	quit (pLineStatus = "") || (pLineStatus = 1) || (pLineStatus = 2)

CheckAndCalcConformidades()

	new idPrescription, line
	set idPrescription = ""

	set antes = $piece($h,",",2)
	for {
		set idPrescription = $order(^MEDPrescription(0,idPrescription),-1)
		quit:idPrescription=""
		
		set line = ""
		
		for {
			set line = $order(^MEDPrescriptionLine(0,idPrescription, line))
			quit:line=""
			
			do GetConformidade(idPrescription, line)
		}		
	}
	
	set depois = $piece($h,",",2)
	write "Tempo de execução: "_(depois - antes)_" segundos"

	quit

ResetConformityCalculation()

	set antes = $piece($h,",",2)
	kill ^VARConformidade
	do CalculateAllConformities()
	set depois = $piece($h,",",2)
	
	write "Tempo de execução: "_(depois - antes)_" segundos"

	quit
]]></Routine>
</Export>