<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INANGSUCH1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INANGSUCH1(YFORM)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SUCHEN ANGEBOTE
	;	AUFRUF VIA FORMULARGENERATOR BEI SAVE =    #D ^INANGSUCH1("INANGSUCH1")
	;	AUFRUF NACH DATENFELD (VORG(1)=SUCHSTRING)  D SUCH^INANGSUCH1("INANG")
	;
	; Called By : Form INANGSUCH1 : OnAfterSave
	; 
	; Inputs : 
	;
	;
	; ByRef :
	; VORG(1)=SUCHFUNKTION  (AUS FORMDEFINITION)
	; YARTS=FORMULAR, DAS AUFGERUFEN WIRD
	;
	; Returns :
	;
	;
	; History :
	; 04-Jun-2010	GRF		SR17146: allow "." or "/" date delimiters - doesn't
	; 							recognise YYYYMMDD versions at this stage
	; 08.07.2002	FAN
	;-------------------------------------------------------------------------------
	SET %("VAR","YBACK")="INANGBANZ,"
	SET %("VAR","YAUSWAHL")="#"_VORG(1)_"#"_VORG(2)
	SET %("VAR","YFORM")=YFORM
	DO ^WWWFORM
	QUIT
	
SUCH(YARTS)
	;-------------------------------------------------------------------------------
	;	ANZEIGE SUCHFUNKTION ;Show 
	;	YARTS=DATEI DIE DURCHSUCHT WIRD ;who 
	;
	; Called By : Form INANGSUCH1 : OnAfterDataFields
	;
	;-------------------------------------------------------------------------------
	NEW YFELD,ACCVK,ACCAL,ACCKD,ACCEI,ACCLE,ACCRE,DATUM,SCH,ABS
	
	SET VORG(1) = $PIECE(YAUSWAHL,"#",2)
	SET VORG(2) = $PIECE(YAUSWAHL,"#",3)
	
	SET YDDSATZ=0   ;VARIABEL ZUR HELL DUNKELSCHALTUNG ;bright 
	WRITE YCR
	
	; "Search-concept Please With At Least 3 Character Input!"     ; "Suchbegriff bitte mit mindestens 3 Zeichen eingeben!"
	IF VORG(1)'="" IF $LENGTH(VORG(1))<3 WRITE "<br>"_"&nbsp;"_"<B>"_$$^WWWTEXT(32517)_"</B>"_"<BR>&nbsp;" QUIT
	IF VORG(2)'="" IF VORG(1)'="" WRITE "&nbsp;"_$$^WWWTEXT(148)_": "_"<B>"_VORG(1)_"</B>"       ; "Search"
	IF VORG(2)'="" IF VORG(1)'="" WRITE "&nbsp;&nbsp;"_$$^WWWTEXT(32091)_"&nbsp;&nbsp;"_$PIECE($GET(^WWW101(0,"INANGSUCH1",SPRACHE,VORG(2),1)),Y,1) ; "Within"   ;Z.B. IN OFFENE ANGEBOTE
	
	WRITE "<BR>"
	SET MINDAUFSCHLAG = +$PIECE($GET(^INVORG(YM,YM,1)),Y,11)  ;MINDESAUFSCHLAG DER ERREICHT WERDEN SOLL ;the will 
	SET KREDITLIMIT   = +$PIECE($GET(^INVORG(YM,YM,1)),Y,4)  ;AUSLIEFERUNG NUR MIT KREDITLIMIT DES KUNDEN ;handover only by means of 
	SET YDDSATZ=0
	;----------------------------------BERECHTIGUNGEN----------------------------------
	SET YAUSWAHL=3
	SET ACCVK=$$^WWWACCESS("",$PIECE($GET(^INVORG(YM,YM,1)),Y,22))      ;BERECHTIGUNGEN VON VK-PREISE
	SET ACCAL=$$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INANGBANZ",SPRACHE,3,1)),Y,23))      ;BERECHTIGUNGEN VON ALLE AUFTRAG ;mandate 
	SET ACCKD=$$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INANGBANZ",SPRACHE,4,1)),Y,23))      ;BERECHTIGUNGEN VON KUNDE ANGTRAG ;lore 
	IF ACCKD=1 SET ACCKD=$$^WWWACCESS("",$PIECE($GET(^INVORG(YM,YM,1)),Y,35))                           ;BERECHTIGUNGEN VON KUNDE ANGTRAG ;lore 
	SET ACCEI=$$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INANGBANZ",SPRACHE,8,1)),Y,23))      ;BERECHTIGUNGEN VON EIGEN ANGTRAG  ;EINGESCHALTET;FIS;11.06.03;23763;
	SET ACCLE=$$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INANGBANZ",SPRACHE,6,1)),Y,23))      ;BERECHTIGUNGEN VON LIEF ANGTRAG
	SET ACCRE=$$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INANGBANZ",SPRACHE,7,1)),Y,23))      ;BERECHTIGUNGEN VON RE ANGTRAG
	;----------------------------------KOPF--------------------------------------------
	SET YSORTBY=+$GET(%(YQUERY,"YSORTBY"))  ;1= NORMAL , -1 = AB, 2=NAME AUF 3=NAME UP, 4=DATUM AUF, 5=DATUM UP
	IF YSORTBY=0 SET YSORTBY=-1
	SET ARTIKEL=""  ;AUS ARTIKELANLAGE ;out of 
	SET NAMESORT=0  ;NICHT NACH NAMEN ;Not within 
	IF YSORTBY=2 SET NAMESORT=1,YSORTBY=1   ;NACH NAME AUFW. ;within Name 
	IF YSORTBY=3 SET NAMESORT=1,YSORTBY=-1  ;NACH NAME ABW. ;within Name 
	IF YSORTBY=4 SET NAMESORT=2,YSORTBY=1   ;NACH DATUM AUFW. ;within Date 
	IF YSORTBY=5 SET NAMESORT=2,YSORTBY=-1  ;NACH DATUM ABW. ;within Date 
	DO ^INANGBANZ2(0)
	;--------------------------------------------------------------------------------------SUCHEN
	SET ANZM=0
	SET YLEAUF=$PIECE($GET(^WWW126(YM,"INANG",YBED,1,1)),Y,1)  ;LETZTE ANGBOTESNUMMER ;last 
	SET YMAX=0
	SET SUCH="ALL"  
	IF (VORG(1)?1.2N1"."1.2N1".".4N) ||  (VORG(1)?1.2N1"/"1.2N1"/".4N) { ; Date  ; SR17146
		SET VORG(1)=$$^WWWDATE1(VORG(1))    ; DD.MM.YYYY or MM.DD.YYYY to match FELDFORMAT (can have . or /)
	}
	;------------------------SUCHEN NACH ALLE OFFENE ANGBOTE---------------""oder1
	;Q:VORG(2)="" 
	IF VORG(2)'="" IF VORG(2)+10#10=0 QUIT      ;TRENNEN -------
	IF VORG(2)=1 IF VORG(1)'="" DO
	. SET SUCH="OFFEN"
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INANG1(YM,AUF),-1) QUIT:AUF=""  DO  QUIT:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO
	. . . IF $FIND(AUF,VORG(1)) DO BERECHTIGUNG 
	. . . IF '$FIND(AUF,VORG(1)) IF $FIND($$^WWWUMLAU(AUF1,1),$$^WWWUMLAU(VORG(1),1)) DO BERECHTIGUNG 
	
	;------------------------SUCHEN NACH ANGBOTSNUMMER---------------------------2
	IF VORG(2)=2 IF VORG(1)'="" DO
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INANG(YM,AUF),-1) QUIT:AUF=""  DO  QUIT:ANZM>100
	. . IF AUF'="" IF $FIND(AUF,VORG(1)) DO
	. . . SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG 
	
	;------------------------SUCHEN NACH ANGBOTSDATUM----------------------------3
	IF VORG(2)=3 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INANGs(YM,2,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INANGs(YM,2,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  QUIT:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG 
	
	;------------------------SUCH NACH KOMMISSION--------------------------------5
	IF VORG(2)=5 IF VORG(1)'="" DO
	. SET YVAR=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET YVAR=$ORDER(^INANGs(YM,14,YVAR)) QUIT:YVAR=""  QUIT:'$FIND(YVAR,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(YVAR,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)      ;SSOK
	. . QUIT:YVAR=""
	. . SET AUF=""
	. . FOR  SET AUF=$ORDER(^INANGs(YM,14,YVAR,AUF)) QUIT:AUF=""  DO  QUIT:ANZM>100     ;SSOK
	. . . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG
	
	;------------------------SUCH NACH ZUSTÄNDIGER MITARBEITER-------------------7
	IF VORG(2)=7 IF VORG(1)'="" DO
	. SET MIT=""
	. FOR  SET MIT=$ORDER(^WWW013(0,MIT)) QUIT:MIT=""  DO  QUIT:ANZM>100
	. . IF MIT'="" DO
	. . . IF $FIND(MIT,$$^WWWUMLAU(VORG(1),1)) DO
	. . . . QUIT:'$DATA(^INANGs(YM,15,$$^WWWUMLAU(MIT,1)))    ;SSOK
	. . . . SET AUF=""
	. . . . FOR  SET AUF=$ORDER(^INANGs(YM,15,$$^WWWUMLAU(MIT,1),AUF)) QUIT:AUF=""  DO  QUIT:ANZM>100     ;SSOK
	. . . . . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG 
	. . . ;
	. . . IF '$FIND(MIT,$$^WWWUMLAU(VORG(1),1)) IF $FIND($$^WWWUMLAU($PIECE($GET(^WWW013(0,MIT,1)),Y,1),1),$$^WWWUMLAU(VORG(1),1)) DO
	. . . . QUIT:'$DATA(^INANGs(YM,15,$$^WWWUMLAU(MIT,1)))    ;SSOK
	. . . . SET AUF=""
	. . . . FOR  SET AUF=$ORDER(^INANGs(YM,15,$$^WWWUMLAU(MIT,1),AUF)) QUIT:AUF=""  DO  QUIT:ANZM>100     ;SSOK
	. . . . . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG 
	
	;------------------------SUCH NACH ANSPRECHPARTNER---------------------------------8------VOLLTEXT SUCHEN;FAN;24.08.04;26127
	IF VORG(2)=8 IF VORG(1)'="" DO
	 .NEW I,SUCH
	. SET YVAR=""
	. FOR  SET YVAR=$ORDER(^INANGs(YM,19,YVAR)) QUIT:YVAR=""  DO  QUIT:ANZM>100
	. . ;FOR I=1:1 QUIT:$PIECE(VORG(1)," ",I,999)=""  SET SUCH=$PIECE(VORG(1)," ",I) IF SUCH'="" IF $FIND($$^WWWUMLAU(YVAR,2),$$^WWWUMLAU(SUCH,2)) DO
	. . SET SUCH=$$^WWWUMLAU(VORG(1),1)
	. . IF $FIND($$^WWWUMLAU(YVAR,2),$$^WWWUMLAU(SUCH,2)) DO
	. . . SET AUF=""
	. . . FOR  SET AUF=$ORDER(^INANGs(YM,19,YVAR,AUF)) QUIT:AUF=""  DO
	. . . . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG
	
	;------------------------SUCHEN NACH KUNDENNUMMER----------------------------12
	IF VORG(2)=12 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INANGs(YM,1,$$^WWWUMLAU(VORG(1),1)))
	. SET DATUM=""
	. FOR  SET DATUM=$ORDER(^INANGs(YM,1,$$^WWWUMLAU(VORG(1),1),DATUM),-1) QUIT:DATUM=""  DO  QUIT:ANZM>100
	. . SET SCH=""
	. . FOR  SET SCH=$ORDER(^INANGs(YM,1,$$^WWWUMLAU(VORG(1),1),DATUM,SCH)) QUIT:SCH=""  DO  QUIT:ANZM>100
	. . . SET AUF=""
	. . . FOR  SET AUF=$ORDER(^INANGs(YM,1,$$^WWWUMLAU(VORG(1),1),DATUM,SCH,AUF)) QUIT:AUF=""  DO  QUIT:ANZM>100
	. . . . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG

	;------------------------SUCHEN NACH LIEFERANTENNUMMER------------------------14
	IF VORG(2)=14 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INANGs(YM,17,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INANGs(YM,17,$$^WWWUMLAU(VORG(1),1),AUF)) QUIT:AUF=""  DO  QUIT:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG 
	
	;------------------------SUCHEN NACH KUNDEN/LIEFERRANT BEZEICHNUNG------------16
	IF VORG(2)=16 IF VORG(1)'="" DO
	. SET YVAR=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET YVAR=$ORDER(^INANGs(YM,13,YVAR)) QUIT:YVAR=""  QUIT:'$FIND(YVAR,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(YVAR,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1) 
	. . QUIT:YVAR=""
	. . SET AUF=""
	. . FOR  SET AUF=$ORDER(^INANGs(YM,13,YVAR,AUF)) QUIT:AUF=""  DO  QUIT:ANZM>100    ;SSOK
	. . . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG
	
	;------------------------SUCHEN NACH ARTIKELNUMMER-----------------------------21
	IF VORG(2)=21 IF VORG(1)'="" DO
	. FOR ABS="",0,1  DO
	. . SET ART=$$^WWWUMLAU(VORG(1),1)
	. . DO  FOR  SET ART=$ORDER(^INANGPs(YM,5,$$^WWWUMLAU(ABS,1),ART)) QUIT:ART=""  QUIT:'$FIND(ART,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(ART,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)
	. . . QUIT:ART=""
	. . . SET AUF=""
	. . . SET YVAR=""
	. . . FOR  SET YVAR=$ORDER(^INANGPs(YM,5,$$^WWWUMLAU(ABS,1),ART,YVAR)) QUIT:YVAR=""  DO  QUIT:ANZM>100    ;SSOK
	. . . . IF YVAR'="" IF YVAR'=AUF SET AUF=YVAR SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG
	
	;------------------------SUCHEN NACH ARTIKELNAME-------------------------------23
	IF VORG(2)=23 IF VORG(1)'="" DO
	. SET ART=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET ART=$ORDER(^INANGPs(YM,1,ART)) QUIT:ART=""  QUIT:'$FIND(ART,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(ART,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)
	. . QUIT:ART=""
	. . SET AUF=""
	. . SET YVAR=""
	. . FOR  SET YVAR=$ORDER(^INANGPs(YM,1,ART,YVAR)) QUIT:YVAR=""  DO  QUIT:ANZM>100      ;SSOK
	. . . IF YVAR'="" IF YVAR'=AUF SET AUF=YVAR SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG     ;GLEICH ART IN EINEN ANGTRAG,ANGTRAG NUR EINMAL LISTE
	
	;------------------------SUCH NACH Gesamt ANGEBOTE GEDRUCKT AM---------------------31
	IF VORG(2)=31 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INANGs(YM,10,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INANGs(YM,10,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  QUIT:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INANG(YM,AUF,1)) DO BERECHTIGUNG 
	
	;--------------------------------------------------------------------------------------
	;DO  ;IF ANZM=0 DO
	IF (ANZM=0) || ($GET(YDDSATZ)<20) DO
	. NEW YBLANKLINE
	. SET YBLANKLINE=$GET(YDDSATZ)
	. FOR YBLANKLINE=YBLANKLINE:1:20 DO
	. . WRITE "<TR>"
	. . SET YDDSATZ=YDDSATZ+1
	. . FOR YI=1:1:10 DO
	. . . WRITE "<TD NOWRAP VALIGN=TOP"
	. . . IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . . WRITE ">"
	. . . WRITE "<FONT SIZE=2>&nbsp;</FONT>"
	. . . WRITE "</TD>",YCR
	. . ;
	. . WRITE YCR
	. . IF ACCVK=1 DO   ;nur bei BERECHTIGUNG ;solely next to 
	. . . WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	. . . IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . . WRITE ">"
	. . . WRITE "<FONT SIZE=2>&nbsp;</FONT>"
	. . . WRITE "</TD>"
	. . ;
	. . WRITE "</TR>"
	
	IF ANZM>100 SET YMAX=1
	IF YMAX=1 WRITE "&nbsp;"_$$^WWWTEXT(32504)
	DO ^WWWFRAME(1)   ;TABLE AUS ;out of 
	QUIT
	
BERECHTIGUNG          ;BERECHTIGUNG PRÜFUNG  ;quiz 
	IF ANZM>100 SET YMAX=1 QUIT
	IF ACCAL=1 IF ACCKD=1 DO ^INANGBANZ1                      ;ALLE                     ;ALLE
	IF ACCAL=1 IF ACCKD'=1 DO
	. IF $PIECE(AUF1,Y,14)=YBED DO ^INANGBANZ1 
	. IF +$PIECE(AUF1,Y,169)'=0 IF ACCRE=1 DO ^INANGBANZ1 ;REKLAMATIONEN
	. IF +$PIECE(AUF1,Y,169)=0 DO
	. . IF $PIECE(AUF1,Y,2)="" SET $PIECE(AUF1,Y,2)=0
	. . IF $PIECE(AUF1,Y,2)=0 IF ACCKD=1 DO ^INANGBANZ1    ;KUNDE ANGTRAG ;lore 
	. . IF $PIECE(AUF1,Y,2)=1 IF ACCEI=1 DO ^INANGBANZ1    ;EIGENE ANGTRAG  ;EINGESCHALTET;FIS;11.06.03;23763;
	. . IF $PIECE(AUF1,Y,2)=2 IF ACCLE=1 DO ^INANGBANZ1    ;LIEFERRANTEN-BESTELLUNGEN
	
	IF ACCAL'=1 DO
	. IF $PIECE(AUF1,Y,14)=YBED DO ^INANGBANZ1 
	. IF +$PIECE(AUF1,Y,169)'=0 IF ACCRE=1 DO ^INANGBANZ1 ;REKLAMATIONEN
	. IF +$PIECE(AUF1,Y,169)=0 DO
	. . IF $PIECE(AUF1,Y,2)="" SET $PIECE(AUF1,Y,2)=0
	. . IF $PIECE(AUF1,Y,2)=0 IF ACCKD=1 DO ^INANGBANZ1    ;KUNDE ANGTRAG ;lore 
	. . IF $PIECE(AUF1,Y,2)=1 IF ACCEI=1 DO ^INANGBANZ1    ;EIGENE ANGTRAG  ;EINGESCHALTET;FIS;11.06.03;23763;
	. . IF $PIECE(AUF1,Y,2)=2 IF ACCLE=1 DO ^INANGBANZ1    ;LIEFERRANTEN-BESTELLUNGEN
	
	QUIT
	
PROJEKT     ;AUFRUF VON INANGBANZ: LINKS VON PROJEKT ;FAN;11.05.2004;LINK ZU ALLE ANG ZUM DIESE PROJEKT;25655
	SET VORG(1)=YKEY
	SET VORG(2)=5
	SET YFORM="INANGSUCH1"
	SET YBACK="INANGBANZ,"
	DO ^INANGSUCH1(YFORM)
	QUIT
	
]]></Routine>
</Export>