<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORML" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORML
	;------------------------------------------------------------------------
	; Description of Function :
	;		LISTGENERATOR
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 22-Feb-2008	GRF		Doco; quits; Naked References
	; 05.08.1997	DT		Created
	;------------------------------------------------------------------------
	NEW YDATEI,YDATSO,YDNUM,YI
	
	IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,30)=1 DO
	. SET $PIECE(^WWW120(0,YFORM,1),Y,30)=0
	. KILL ^WWW131(0,YFORM),^WWW132(0,YFORM),^WWW133(0,YFORM)
	. IF '$DATA(^WWW131(0,YFORM)) SET ^WWW131(0,YFORM,1,1)=$PIECE(^WWW120(0,YFORM,1),Y,11)_Y_Y_Y_Y_Y_Y_Y_1_Y_Y_Y_Y_Y_Y_Y_Y_1
	. SET YI(3)=""
	. FOR  SET YI(3)=$ORDER(^WWW131(0,YFORM,YI(3))) QUIT:YI(3)=""  DO
	. . SET YI(1)=$PIECE($GET(^WWW131(0,YFORM,YI(3),1)),Y,1)
	. . IF YI(1)'="" DO
	. . . SET YI(2)=""
	. . . FOR  SET YI(2)=$ORDER(^WWW002(0,YI(1),YI(2))) QUIT:YI(2)=""  DO
	. . . . IF '$DATA(^WWW132(0,YFORM,YI(3),YI(2),1)) SET ^WWW132(0,YFORM,YI(3),YI(2),1)=YI(2)_Y_Y_1_Y_1_Y_Y_Y_1_Y
	. . . SET YI(2)=""
	. . . FOR  SET YI(2)=$ORDER(^WWW003(0,YI(1),YI(2))) QUIT:YI(2)=""  DO
	. . . . IF '$DATA(^WWW133(0,YFORM,YI(3),YI(2),1)) SET ^WWW133(0,YFORM,YI(3),YI(2),1)=YI(2)_Y_Y_1_Y_1_Y_Y_Y_1_Y
	
	;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	;PRUEFEN VORGABEN
	;DO
	. SET YSELECTION=0  ;KEINE SELECTION ;no 
	. SET YI(3)="" FOR  SET YI(3)=$ORDER(^WWW131(0,YFORM,YI(3))) QUIT:YI(3)=""  DO  QUIT:YSELECTION=1
	. . SET YI(1)=$PIECE($GET(^WWW131(0,YFORM,YI(3),1)),Y,1)
	. . IF YI(1)'="" DO
	. . . SET YI(2)=""
	. . . FOR  SET YI(2)=$ORDER(^WWW002(0,YI(1),YI(2))) QUIT:YI(2)=""  DO  QUIT:YSELECTION=1
	. . . . IF +$PIECE($GET(^WWW132(0,YFORM,YI(3),YI(2),1)),Y,3)'=0 SET YSELECTION=1
	. . . QUIT:YSELECTION=1
	. . . SET YI(2)="" FOR  SET YI(2)=$ORDER(^WWW003(0,YI(1),YI(2))) QUIT:YI(2)=""  DO  QUIT:YSELECTION=1
	. . . . IF +$PIECE($GET(^WWW132(0,YFORM,YI(3),YI(2),1)),Y,3)'=0 SET YSELECTION=1
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	
	WRITE YCR,"</TABLE>"
	;WRITE "<CENTER>"
	WRITE YCR
	;W YCR,"<TABLE CELLSPACING=0 BORDER="_+$P(YVOR,Y,13)_">"
	;I YSELECTION=1 D   ;WENN KEINE AUSWAHL,DANN KEINE FRAGE
	IF '$DATA(^WWW122(0,YFORM)) IF +$PIECE(YVOR,Y,13)=1 DO  ;WRITE YCR,"<FIELDSET STYLE="_""""_"border-color:"_YLIGHTGREY_""""_">"
	. IF $GET(YDREID)'=1 WRITE YCR,"<FIELDSET STYLE="_""""_"border-color:"_YLIGHTGREY_""""_">"
	. IF +$GET(YDREID)=1 WRITE YCR,"<FIELDSET STYLE="_""""_"border-color-dark:"_YLIGHTGREY_"; border-color-light:"_YDARKGRAY_";"_""""_">"  ;FIS;31.03.04;25445;RAHMEN 2-FARBIG
	
	DO
	. WRITE YCR,"<TABLE CELLSPACING=0 BORDER=0>"
	. NEW YDATEI,YDATSO,YDNUM
	. SET YDATSO=""
	. FOR  SET YDATSO=$ORDER(^WWW131(0,YFORM,YDATSO)) QUIT:YDATSO=""  DO
	. . SET YLISTDAT=$GET(^WWW131(0,YFORM,YDATSO,1))
	. . SET YDATEI=$PIECE(YLISTDAT,Y,1)
	. . SET YSORT=$PIECE(YLISTDAT,Y,2)
	. . ;WRITE YCR
	. . ;WRITE "<TR>"
	. . ;WRITE "<TD>"
	. . ;WRITE YCR,"<FONT SIZE="_""""_$PIECE(YVOR,Y,7)_""""_">"
	. . DO SUCH       
	. . ;WRITE YCR,"</FONT></TD>"
	. . ;WRITE YCR,"</TR>"
	. . WRITE YCR,"<TR>"
	. . WRITE YCR,"<TD ALIGN=RIGHT NOWRAP VALIGN=TOP>"
	. . WRITE "<FONT SIZE="_""""_$PIECE(YVOR,Y,7)_""""_">"
	. . WRITE $$^WWWTEXT(382)          ; "Print Orientation"
	. . WRITE "&nbsp;"
	. . WRITE YCR,"</FONT></TD>"
	. . DO RICHT
	. . WRITE YCR,"</TR>"
	. . ;;W YCR,"</TABLE>"
	. . ;;WRITE YCR,"<TABLE CELLSPACING=0 BORDER=0>"
	. . ;W "<TR>"
	. . ;W "<TD>&nbsp;"
	. . ;W YCR,"</TD>"
	. . ;W YCR,"</TR>"
	. . ;SET YHID=""
	. . ;SET YHIDDSE=0  ;TYBD;WENN AUS ;table-mat when out of 
	. . DO PRIM
	. . DO FELD
	. ;
	. WRITE YCR,"</TABLE>"
	. ;WRITE "</CENTER>"
	
	IF '$DATA(^WWW122(0,YFORM)) IF +$PIECE(YVOR,Y,13)=1 WRITE YCR,"</FIELDSET>"
	QUIT
	
SUCH ;AUSWAHL SORTKEY ;Selection 
	QUIT:YDATEI=""
	NEW YSKEY,YA,YI,YX
	
	IF '$DATA(YSORT) SET YSORT = ""
	IF YSORT="" IF $PIECE(YLISTDAT,Y,3)'="" SET YSORT = +$PIECE(YLISTDAT,Y,3)
	
	SET YSKEY(0)=""
	SET YLFN = ""
	FOR  SET YLFN = $ORDER(^WWW002(0,YDATEI,YLFN)) QUIT:YLFN=""  DO   ;neue selection; tybd;4,07,2003
	. SET YNAME = ""
	. IF YFORM'="" IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,11)=YDATEI SET YNAME=$$^WWWFELDNAME(YFORM,"P",YLFN)
	. IF YNAME="" DO
	. . SET YNAME = $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,2)
	. . IF $DATA(^WWW0021(0,YDATEI,YLFN,SPRACHE,1)) SET YNAME=$PIECE(^WWW0021(0,YDATEI,YLFN,SPRACHE,1),Y,1)     ; Naked Ref
	. ;
	. SET $PIECE(YSKEY(0),"/",YLFN)=YNAME
	. ;
	. SET YSKEY = $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,6)
	. FOR YI=1:1 SET YA = $PIECE(YSKEY,",",YI) QUIT:YA=""  DO
	. . SET YA(1) = +$PIECE(YA,".",1)
	. . SET YA(2) = +$PIECE(YA,".",2)
	. . QUIT:+YA(1)=0
	. . IF +YA(2)=0 SET YA(2)=1
	. . SET YNAME=""
	. . IF YFORM'="" IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,11)=YDATEI SET YNAME=$$^WWWFELDNAME(YFORM,"P",YLFN)
	. . IF YNAME="" DO
	. . . SET YNAME = $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,2)
	. . . IF $DATA(^WWW0021(0,YDATEI,YLFN,SPRACHE,1)) SET YNAME=$PIECE(^WWW0021(0,YDATEI,YLFN,SPRACHE,1),Y,1)     ; Naked Ref
	. . ;
	. . SET $PIECE(YSKEY(YA(1)),"/",YA(2))=YNAME
	
	SET YLFN = ""
	FOR  SET YLFN = $ORDER(^WWW003(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. SET YSKEY = $PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,6)
	. FOR YI=1:1 SET YA = $PIECE(YSKEY,",",YI) QUIT:YA=""  DO
	. . SET YA(1) = +$PIECE(YA,".",1)
	. . SET YA(2) = +$PIECE(YA,".",2)
	. . QUIT:+YA(1)=0
	. . IF +YA(2)=0 SET YA(2)=1
	. . SET YNAME=""
	. . IF YFORM'="" IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,11)=YDATEI DO
	. . . NEW YLFN1
	. . . SET YLFN1=$ORDER(^WWW122s(0,4,YLFN,YFORM,""))
	. . . QUIT:YLFN1=""
	. . . SET YNAME=$$^WWWFELDNAME(YFORM,"D",YLFN1)
	. . ;
	. . IF YNAME="" DO
	. . . SET YNAME=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,2)
	. . . IF $DATA(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)) SET YNAME=$PIECE(^WWW0031(0,YDATEI,YLFN,SPRACHE,1),Y,1)     ; Naked Ref
	. . ;
	. . SET $PIECE(YSKEY(YA(1)),"/",YA(2))=YNAME
	
	DO
	. IF $ORDER(YSKEY(0))="" DO  QUIT
	. . ;WRITE YCR
	. . ;WRITE "&nbsp;"
	. . ;WRITE YCR,"</TD>"
	. . ;WRITE YCR,"<TD>"
	. . ;WRITE "&nbsp;"
	. . WRITE "<INPUT TYPE=HIDDEN NAME="_""""_"Y"_YFORM_"S"_YDATSO_""""_" VALUE=0>"
	. ;
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;DO
	. . IF $DATA(^WWW0011(0,YDATEI,SPRACHE,1)) WRITE $$^WWWUML($PIECE(^WWW0011(0,YDATEI,SPRACHE,1),Y,1))_" " QUIT     ; Naked Ref
	. . WRITE $$^WWWUML($PIECE($GET(^WWW001(0,YDATEI,1)),Y,1))_" "
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	. WRITE "<TR>"
	. WRITE "<TD ALIGN=RIGHT>"
	. WRITE YCR,"<FONT SIZE="_""""_$PIECE(YVOR,Y,7)_""""_">"
	. WRITE $$^WWWTEXT(65)
	. WRITE "&nbsp;"
	. WRITE YCR,"</FONT></TD>"
	. ;
	. WRITE YCR,"<TD>"
	. WRITE "<select NAME="_""""_"Y"_YFORM_"S"_YDATSO_""""_">"
	. IF +YSORT=0  WRITE "<option value="_""""_0_""""_" SELECTED=SELECTED>"_$$^WWWUML(YSKEY(0))_"</option>"
	. IF +YSORT'=0 WRITE "<option value="_""""_0_""""_">"_$$^WWWUML(YSKEY(0))_"</option>"
	. SET YA="0"
	. FOR  SET YA = $ORDER(YSKEY(YA)) QUIT:YA=""  DO 
	. . QUIT:YSKEY(YA)="_FREE"
	. . IF YSORT'=YA WRITE YCR,"<option value="_""""_YA_""""_">"_$$^WWWUML(YSKEY(YA))_"</option>"
	. . IF YSORT=YA  WRITE YCR,"<option value="_""""_YA_""""_" SELECTED=SELECTED>"_$$^WWWUML(YSKEY(YA))_"</option>"
	. ;
	. WRITE "</select>"
	. WRITE YCR,"</TD>"
	. WRITE YCR,"</TR>"
	
	QUIT
	
RICHT ;ORIENTIERUNG AUF ODER ABSTEIGEND? ;upon Or 
	IF '$DATA(YORIENT) SET YORIENT=0
	IF YORIENT=""      SET YORIENT=0
	DO
	. WRITE YCR,"<TD NOWRAP>"
	. WRITE "<FONT SIZE=2>",YCR
	. IF $GET(YDREID)'=1 WRITE "<FIELDSET STYLE="_""""_"border-color:"_YLIGHTGREY_""""_">"
	. IF +$GET(YDREID)=1 WRITE "<FIELDSET STYLE="_""""_"border-color-dark:"_YLIGHTGREY_"; border-color-light:"_YDARKGRAY_";"_""""_">"  ;FIS;31.03.04;25445;RAHMEN 2-FARBIG
	. WRITE YCR
	. ;WRITE "<LEGEND>"_$$^WWWTEXT(382)_"</LEGEND>"    ; "Print Orientation"
	. ;W $$^WWWTEXT(110)_" "
	. FOR YI=0,1 DO
	. . IF YI=1 WRITE YCR,"<BR>"
	. . WRITE YCR,"<FONT SIZE="_""""_$PIECE(YVOR,Y,7)_""""_">"
	. . SET YA=$PIECE($$^WWWTEXT(112)_"#"_$$^WWWTEXT(111),"#",YI+1)    ; "Ascending"  "Descending"
	. . WRITE "<INPUT TYPE="_""""_"RADIO"_""""_" NAME="_""""_"Y"_YFORM_"LR"_YDATSO_""""_" VALUE="_""""_YI_""""
	. . IF YI=YORIENT WRITE " CHECKED="_""""_"CHECKED"_""""
	. . WRITE "> "_YA
	. . WRITE "</INPUT></FONT>"
	. ;
	. WRITE YCR,"</FIELDSET>"
	. WRITE YCR,"</FONT></TD>"
	
	QUIT
	
PRIM
	SET YLFD = ""
	FOR  SET YLFD = $ORDER(^WWW132(0,YFORM,YDATSO,YLFD)) QUIT:YLFD=""  DO
	. SET YLISTLF = $GET(^WWW132(0,YFORM,YDATSO,YLFD,1))
	. SET YLFN    = $PIECE(YLISTLF,Y,1)
	. QUIT:+$PIECE(YLISTLF,Y,2)'=0
	. QUIT:YLFN=""
	. QUIT:+$PIECE(YLISTLF,Y,3)=0
	. SET YSATZ = $GET(^WWW002(0,YDATEI,YLFN,1))
	. IF $DATA(^WWW0021(0,YDATEI,YLFN,SPRACHE,1)) SET $PIECE(YSATZ,Y,2)=$PIECE(^WWW0021(0,YDATEI,YLFN,SPRACHE,1),Y,1)     ; Naked Ref
	. SET $PIECE(YSATZ,Y,88)=$PIECE(YLISTLF,Y,88)   ;FELDLÄNGE GEÄNDERT ;field size 
	. QUIT:YSATZ=""
	. SET YFOART = 2
	. SET YART   = "LP"
	. IF '$DATA(^WWW127(0,YFORM,YART,YLFN,SPRACHE,1)) SET ^WWW127(0,YFORM,YART,YLFN,SPRACHE,1) = ""
	. SET YLFN = +YDATSO_$EXTRACT(100+YLFN,2,3)
	. DO ^WWWFORM9
	
	QUIT
	
FELD
	SET YLFD = ""
	FOR  SET YLFD = $ORDER(^WWW133(0,YFORM,YDATSO,YLFD)) QUIT:YLFD=""  DO
	. SET YLISTLF = $GET(^WWW133(0,YFORM,YDATSO,YLFD,1))
	. SET YLFN    = $PIECE(YLISTLF,Y,1)
	. QUIT:YLFN=""
	. QUIT:+$PIECE(YLISTLF,Y,3)=0
	. SET YSATZ=$GET(^WWW003(0,YDATEI,YLFN,1))
	. IF $DATA(^WWW0031(0,YDATEI,YLFN,SPRACHE,1)) SET $PIECE(YSATZ,Y,2)=$PIECE(^WWW0031(0,YDATEI,YLFN,SPRACHE,1),Y,1) ; Naked Ref
	. SET $PIECE(YSATZ,Y,88)=$PIECE(YLISTLF,Y,88)   ;FELDLÄNGE GEÄNDERT ;field size 
	. QUIT:YSATZ=""
	. SET YFOART = 2
	. SET YART   = "LD"
	. IF '$DATA(^WWW127(0,YFORM,YART,YLFN,SPRACHE,1)) SET ^WWW127(0,YFORM,YART,YLFN,SPRACHE,1) = ""
	. SET YLFN = +YDATSO_$EXTRACT(100+YLFN,2,3)
	. DO ^WWWFORM9
	
	QUIT
	
]]></Routine>
</Export>