<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSHIPINFOSUCH" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSHIPINFOSUCH   ;INSHIPINFOSUCH;WEM;SUCHFUNKTION ALLER AUFTRÄGE UND POS MIT DER TRANSPORTNUMMER;24.03.2004  ; Compiled January 11, 2005 14:06:27
	#include COMSYS
	#include INConst
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		SUCHFUNKTION ALLER AUFTRÄGE UND POS MIT DER TRANSPORTNUMMER
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| WEM	24.03.2004  
	;|
	;\------------------------------------------------------------------/
	;
	; DO ^INSHIPINFOSUCH - EXEC NACH DATENFELDERN IN INSHIPINFO
	; TPN = TRANSPORTNUMMER
	NEW YKEYX
	SET YKEYX=$GET(YKEY)
	IF YKEYX'="" DO DisplayLineItems(YKEYX) ;SR10949:Inserted
	
	;SR10949 - NOT IN USE: Use a COMTable for this data.
	;IF YKEYX'="" DO
	;	.NEW YSUCH1,YNOSORT,YDATEI,YBACK,YHEADONLY,YKEY,YMAXKEY
	;	.SET YSUCH1=""
	;	.SET $PIECE(YSUCH1,";",1)="INAUFPA"              ; FORMULAR
	;	.SET $PIECE(YSUCH1,";",2)="INAUFPA"              ; DATEI
	;	.SET $PIECE(YSUCH1,";",3)=2                      ; SORTKEY
	;	.SET $PIECE(YSUCH1,";",17)=YKEYX                 ; VORGABE SUCHKEY
	;	.SET $PIECE(YSUCH1,";",5)=1_","_2_","_4          ; ANZEIGE KEY
	;	.SET $PIECE(YSUCH1,";",6)=16_","_23_","_19       ; ANZAHL COLLI
	;	.SET $PIECE(YSUCH1,";",7)=2                     ; STD SORT
	;	.SET $PIECE(YSUCH1,";",8)=1                      ; ANZEIGEERGEBNIS
	;	.SET $PIECE(YSUCH1,";",9)=0                      ; ORIENTIERUNG
	;	.SET $PIECE(YSUCH1,";",10)=100                   ; ANZAHL
	;	.SET $PIECE(YSUCH1,";",11)=""                    ; FIXKEY
	;	.SET $PIECE(YSUCH1,";",12)=""                    ; FELDER MIT SUMMENBILDUNG
	;	.SET $PIECE(YSUCH1,";",13)=""                    ; WELCHEN KEY ÜBERGEBEN
	;	.SET $PIECE(YSUCH1,";",20)=$$^WWWTEXT(33816)     ; HEADER - IMPORTIERTE AUFTRAGSPOSITIONEN FÜR DIESE TRANSPORTNUMMER
	;	.SET YNOSORT=1
	;	.SET YBACK="INSHIPINFO,"
	;	.DO ^WWWSUCH1
	;	.QUIT
	;SET YKEY=YKEYX
	QUIT
	
	
	
DisplayLineItems(pidImport) ;SR10949 - added
	;-------------------------------------------------------------------------------
	; Display Import Shipment Line Items in a COM Table.
	;
	; Returns:
	;
	; History:
	; 19-Dec-2006	RPW			SR15233: Removed uneccessary concatenation
	; 01-Dec-2006	Steve S		SR15233: Cleaned up/macro usage
	; 22-Mar-2005	Steve S		Created (SR:10949)
	;-------------------------------------------------------------------------------
	new lstHeader,objImport,idSupplier,idOrder,idLine,objINAUFP,idConfNumber
	new objINAUFPA,fltConfQty,intPackages,dteDelivery,objINAUF,idConfKey
	new idxImport,strLineKey,idItem,fltItemQty
	
	#define AddHeader(%1)	set lstHeader = lstHeader_$listbuild(%1)
	
	set lstHeader=""
	
	//SR15233 Add Header macro
	$$$AddHeader($$^WWWFELDNAME("INAUFPA","P",1)) 	;Order Number
	$$$AddHeader($$^WWWFELDNAME("INAUFPA","P",2)) 	;Line Item
	$$$AddHeader($$^WWWFELDNAME("INAUFP","D",4)) 	;Item Number
	$$$AddHeader($$^WWWTEXT(34003)) 				;Order Quantity
	$$$AddHeader($$^WWWFELDNAME("INAUFPA","P",4)) 	;Order Confirmation Number
	$$$AddHeader($$^WWWFELDNAME("INAUFPA","D",16)) 	;Order Confirmation Qty
	$$$AddHeader($$^WWWFELDNAME("INAUFPA","D",23)) 	;Delivery Date
	$$$AddHeader($$^WWWFELDNAME("INAUFPA","D",19)) 	;No of Packages
		
	set idxImport=$$$Index(pidImport)
	
	if $data(^INAUFPAs(YM,2,idxImport)) {
		do Start^COMTable(lstHeader,$$^WWWTEXT(33816)) ;Import Shipment Line Items
		
		set idOrder=""
		for {
			set idOrder=$order(^INAUFPAs(YM,2,idxImport,idOrder))
			quit:(idOrder="")
			
			set objINAUF=$get(^INAUF(YM,idOrder,1))
			set idSupplier=$$$INAUFSupplierNumber(objINAUF)
			
			set idLine=""
			for {
				set idLine=$order(^INAUFPAs(YM,2,idxImport,idOrder,idLine))
				quit:(idLine="")
				
				set objINAUFP		= $get(^INAUFP(YM,idOrder,idLine,1))
				set idConfNumber	= $order(^INAUFPAs(YM,2,idxImport,idOrder,idLine,idSupplier,""),1) ; 31-Mar-2005	RW/PO/SS	Missed Supplier on $order
				set objINAUFPA		= $get(^INAUFPA(YM,idOrder,idLine,idSupplier,idConfNumber,1))
				
				//SR15233: Use macros
				set fltConfQty		= $$$INAUFPAOrderConfirmationQuantity(objINAUFPA)
				set intPackages		= $$$INAUFPANumberOfColli(objINAUFPA)
				set dteDelivery		= $$$INAUFPADeliveryDate(objINAUFPA)
				
				set idItem			= $$$INAUFPItemNumber(objINAUFP)
				set fltItemQty		= $$$INAUFPQuantity(objINAUFP)
				
				set strLineKey 		= idOrder_","_idLine // SR15233 - Two less concatenations this way.
				set idConfKey		= strLineKey_","_idSupplier_","_idConfNumber
				
				//SR15233: Align variables
				
				do NewLine^COMTable()
				
				do InsertCell^COMTable(		idOrder			,"INAUF"		,idOrder)
				do InsertCell^COMTable(		idLine			,"INAUFP"		,strLineKey)
				do InsertCell^COMTable(		idItem			,"INAUFP"		,strLineKey)
				do InsertCell^COMTable(		fltItemQty		,"INAUFP"		,strLineKey)
				do InsertCell^COMTable(		idConfNumber	,"INAUFPA"		,idConfKey)
				do InsertCell^COMTable(		fltConfQty		,"INAUFPA"		,idConfKey)
				do InsertCellDate^COMTable(	dteDelivery		,"INAUFPA"		,idConfKey)
				do InsertCell^COMTable(		intPackages		,"INAUFPA"		,idConfKey)
				
				do EndLine^COMTable()
			}
		}
		
		do Stop^COMTable() 
		
	}
	
	quit
]]></Routine>
</Export>