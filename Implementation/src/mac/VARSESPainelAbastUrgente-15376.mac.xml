<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPainelAbastUrgente" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESPainelAbastUrgente
#include COMSYS
#include VARSESPainelAbastecimento
 
PrintSituacaoUrgente(pfilterTipoPadrao,pfilterZerados,pfilterOrdenacao,pfilterCobertura)
	new count, meuSQL, rsSQL, idProduto, tipoPadrao, SituacaoProcessoRP
 
 	if '$data(^VARSESPainelAbastecimentoRun(0,0,1)) {
	 	do PrintMsgEmExecucao^VARSESPainelAbastUtils(1)
	 	quit
 	} 
	
	set count = 0
 
	do PrintHeader(pfilterTipoPadrao,pfilterZerados,pfilterCobertura)
	
	write "<br />" 
	write "<table cellspacing='1' style='margin:right:20px'>"
	write "	<tr style='background-color:#2c6c7f;color:white'>"
	write "		<th>#</th>"
	write " 	<th>Código</th>"
	write "		<th>Produto</th>"
	write "		<th>Tipo</th>"
	write "		<th>Grupo</th>"
	write "		<th>Estoque Rede</th>"
	write "		<th>Estoque Central</th>"
	write "		<th>CMM</th>"
	write "		<th width='90'>Cobertura de Estoque (Rede)</th>"
	write "		<th width='150'>Situação Processo RP</th>"
	write "		<th width='130'>Situação de Fornecimento</th>"
	write "	</tr>"
 	
 	
	set meuSQL = "SELECT Produto,"
	set meuSQL = meuSQL_" TipodePadrao,"
	set meuSQL = meuSQL_" EstoqueRede,"
	set meuSQL = meuSQL_" EstoqueCentrais,"
	set meuSQL = meuSQL_" CMM,"
	set meuSQL = meuSQL_" CoberturaRede,"
	set meuSQL = meuSQL_" SituacaoCompras,"
	set meuSQL = meuSQL_" SituacaoProcessoRP"
	set meuSQL = meuSQL_" FROM VARSESPainelAbastecimento"
	set meuSQL = meuSQL_" WHERE CoberturaRede <= "_pfilterCobertura
	set meuSQL = meuSQL_" AND Situacao <> "_$$$SemDadosPlanejamento
	
	
	if ( (pfilterTipoPadrao '= "") && (pfilterTipoPadrao '= "todos") ) {
		set meuSQL = meuSQL_" AND TipodePadrao = "_pfilterTipoPadrao
	}
	
	if (pfilterZerados = 1) {
		set meuSQL = meuSQL_" AND ZeradoRede = 1"
	}
	
	if (pfilterZerados = 2) {
		set meuSQL = meuSQL_" AND Zerado = 1"
	}
	
	if (pfilterOrdenacao = 2) {		
		set meuSQL = meuSQL_" ORDER BY SituacaoProcessoRP desc, CoberturaRede, TipodePadrao asc"
	
	} elseif (pfilterOrdenacao = 3) {
		set meuSQL = meuSQL_" ORDER BY SituacaoCompras desc, CoberturaRede, TipodePadrao asc"	
	
	} elseif (pfilterOrdenacao = 4) {
		set meuSQL = meuSQL_" ORDER BY TipodePadrao asc, CoberturaRede, SituacaoCompras desc"	
	
	} else {
		set meuSQL = meuSQL_" ORDER BY CoberturaRede, SituacaoCompras desc, TipodePadrao asc"
	}
	
	set rsSQL = ##class(%ResultSet).%New()  ;Create Result Set Object
	do rsSQL.Prepare(meuSQL) ;Prepare Query
	do rsSQL.Execute()  	 ;Execute Query
 	
	while (rsSQL.Next()) {		
		set idProduto          = rsSQL.GetData(1)
		set tipoPadrao         = rsSQL.GetData(2)
		set EstoqueRede        = rsSQL.GetData(3)
		set EstoqueCentral     = rsSQL.GetData(4)
		set CMMRede		       = rsSQL.GetData(5)
		set CoberturaRede      = rsSQL.GetData(6)
		set SituacaoCompras    = rsSQL.GetData(7)
		set SituacaoProcessoRP = rsSQL.GetData(8)
		
		set count = $increment(count)
 
	  	if (count#2=0) set bkgColor = "#F0F0F0"
	  	if (count#2=1) set bkgColor = "#E0E0E0"
		
		//Para poder usar a função WWWTR		
		set:($get(SPRACHE) = "") SPRACHE = "PT"
		set:($get(YBED) = "") YBED = "UNKNOWN"
		//
		
		write "	<tr style='background-color:"_bkgColor_"'>"
		write " <td>"_count_"</td>"
		write " <td><a href='VARSESPainelAbastProdutoRede.csp?fProd="_idProduto_"&fOrig=1' class='link'>"_idProduto_"</td>"
		write " <td><a href='VARSESPainelAbastProdutoRede.csp?fProd="_idProduto_"&fOrig=1' class='link'>"_$$SQLGetDescricaoProduto^VARSQL(idProduto)_"</td>"		
		write " <td nowrap='nowrap'>"_$$GetDescTipoPadraoFromID^VARSESPainelAbastUtils(tipoPadrao)_"</td>"
		write " <td>"_$$SQLGetGrupoItem^VARSQL(idProduto)_"</td>"		
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,EstoqueRede)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,EstoqueCentral)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,CMMRede)_"</td>"
		write " <td style='text-align:right'>"_CoberturaRede_" dia(s)</td>"
		write " <td width='150'>"_$$GetSituacaoProcessoRPFromID^VARSESPainelAbastUtils(SituacaoProcessoRP)_"</td>"
		write " <td width='130'>"_$$GetSituacaoComprasFromID^VARSESPainelAbastUtils(SituacaoCompras)_"</td>"
		
		write "</tr>"
		
	}	
	
	if (count = 0) {
		write "<tr style='background-color:white'>"
		write "	<td colspan='12'>Nenhum registro encontrado.</td>"
		write "</tr>"
	}
 
	write "</table>"
	do rsSQL.Close()
 
	quit
	
PrintHeader(pidTipoPadrao,pZerados,pfilterCobertura)
	new descTipoProduto, descSituacao	
 
	set descTipoProduto = $$GetDescCompTipoPadraoFromID^VARSESPainelAbastUtils(pidTipoPadrao)
	
	set descSituacao = ""
	
	if (pZerados = 1) {
		set descSituacao = " zerados na rede e sem cobertura de estoque"
	
	} elseif (pZerados = 2) {
		set descSituacao = " zerados nas centrais e "	
	
	}
	
	if (pZerados '= 1) {	
		if (pfilterCobertura > 1) {
			set descSituacao = descSituacao_" com cobertura de estoque de até "_pfilterCobertura_" dias na rede"
		} elseif (pfilterCobertura = 1) {
			set descSituacao = descSituacao_" com cobertura de estoque de 1 dia na rede"
		} else {
			set descSituacao = descSituacao_" sem cobertura de estoque na rede"
		}	
	}
	
	write "<div style='margin-top:20px;padding-top:10px;padding-bottom:10px;padding-left:15px;padding-right:15px;background-color:"_$$$corVermelho_";'>" 	 	
	write "	<strong>"_descTipoProduto_" "_descSituacao_"</strong>"
	write "</div>"
	
	quit
]]></Routine>
</Export>