<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPROPANZDETAIL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPROPANZDETAIL	;INPROPANZDETAIL;FIS;PRINT LIST OF MANUFACTURING ORDERS WITH DETAILS;25.04.2005
	;
	;SR12200
	;
	NEW ANZM,AUF,POS,POS1,POSN,AUF1,YI,YQ,STATUS,ARTIKEL,PDATUM,FDATUM,SATZ,ANZAHL,SEITE,AKTIV,PLAN1,VORG,ANZEIGE
	;
	SET VORG(5)=$PIECE($GET(^INUSER(YM,YBED,1)),Y,65)  ;SELECTED LOCATION
	SET VORG(6)=$PIECE($GET(^INUSER(YM,YBED,1)),Y,66)  ;SELECTED PRIORITY
	SET ANZEIGE=+$PIECE($GET(^INUSER(YM,YBED,1)),Y,21)    ;0=ALL 1=NOT PLANNED 2=PLANNED, NOT SAVED  3=PLANNED AND SAVED
	;
	DO ^WWWBACK
	;
	KILL ^WWWSOR(YUSER,1)
	NEW EINSPRUNG
	SET EINSPRUNG=1
	DO AUSWAHL^INPROPANZAUF  ;GET ORDERS
	;
	SET ANZM="" FOR  SET ANZM=$ORDER(^WWWSOR(YUSER,1,ANZM)) QUIT:ANZM=""  DO
	. SET SATZ=$GET(^WWWSOR(YUSER,1,ANZM))
	. SET POS1=""
	. IF $PIECE(SATZ,Y,1)'="" IF $PIECE(SATZ,Y,2)'="" SET POS1=$GET(^INAUFP(YM,$PIECE(SATZ,Y,1),$PIECE(SATZ,Y,2),1))
	. WRITE YCR,"<br>"
	. WRITE YCR,"------------------------------------------------------------------------------------------------------"
	. WRITE YCR,"<BR>"
	. ;
	. W "<B>"
	. WRITE $$^WWWFELDNAME("INAUFP","P",1)_": "_$PIECE(SATZ,Y,1)_"-"_$PIECE(SATZ,Y,2)
	. WRITE "&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"
	. WRITE $$^WWWFELDNAME("INAUFP","D",5)_": "
	. WRITE $PIECE(POS1,Y,5)
	. IF $PIECE(POS1,Y,40)'="" WRITE "&nbsp;"_$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(POS1,Y,40),1)),Y,1)
	. WRITE YCR,"<BR>"
	. WRITE $$^WWWFELDNAME("INAUFP","D",4)_": "
	. WRITE $PIECE(POS1,Y,4)
	. WRITE "&nbsp;&nbsp;"
	. WRITE "("_$PIECE(POS1,Y,1)_")"
	. WRITE "</B>"
	. WRITE YCR,"<BR>"
	. WRITE $$^WWWFELDNAME("INAUFP","D",168)_": "
	. WRITE $$^WWWDATE($PIECE(SATZ,Y,5))
	. WRITE YCR,"<BR>"
	. WRITE YCR,"<BR>"
	. IF $PIECE(POS1,Y,256)'="" DO  ;ORDER REFERENCE (LINK TO CUSTOMER ORDER NO)
	. . WRITE $$^WWWFELDNAME("INAUFP","D",256)_":"
	. . WRITE "&nbsp;"_$PIECE(POS1,Y,256)
	. . QUIT:$PIECE($PIECE(POS1,Y,256),"-",1)=""
	. . QUIT:$PIECE($PIECE(POS1,Y,256),"-",2)=""
	. . QUIT:'$DATA(^INAUFP(YM,$PIECE($PIECE(POS1,Y,256),"-",1),$PIECE($PIECE(POS1,Y,256),"-",2)))  ;NO ORDER (DELETED ?)
	. . QUIT:$PIECE($GET(^INAUF(YM,$PIECE($PIECE(POS1,Y,256),"-",1),1)),Y,2)'=0  ;ONLY IF CUSTOMER ORDER
	. . NEW CUST
	. . SET CUST=$PIECE($GET(^INAUF(YM,$PIECE($PIECE(POS1,Y,256),"-",1),1)),Y,1)  ;CUSTOMER
	. . QUIT:CUST=""
	. . WRITE YCR,"<BR>"
	. . ;WRITE $$^WWWFELDNAME("INAUF","D",1)_": "_CUST
	. . WRITE $$^WWWTEXT(32231)_": "_CUST  ;CUSTOMER
	. . WRITE YCR,"<BR>"
	. . WRITE $$^WWWTRANSLATE($$^INADRES($GET(^INKUNDE(YM,CUST,1)),,3),"|","<BR>")
	. . WRITE YCR,"<BR>"
	. . WRITE YCR,"<BR>"
	. . ;
	. . ;OTHER MANUFACTURING ORDERS FOR THIS ITEM
	. . QUIT:$PIECE(POS1,Y,4)=""
	. . WRITE $$^WWWTEXT(34164)_":<br>"
	. . NEW OPEN,AUFX,POSX
	. . FOR OPEN=" ",0 SET AUFX="" FOR  SET AUFX=$ORDER(^INAUFPs(YM,5,OPEN,$$^WWWUMLAU($PIECE(POS1,Y,4),1),AUFX)) QUIT:AUFX=""  DO
	. . . QUIT:$PIECE($GET(^INAUF(YM,AUFX,1)),Y,2)'=1  ;ONLY MANUFACTURING ORDER
	. . . QUIT:AUFX=$PIECE(SATZ,Y,1)  ;ITSELF
	. . . SET POSX="" FOR  SET POSX=$ORDER(^INAUFPs(YM,5,OPEN,$$^WWWUMLAU($PIECE(POS1,Y,4),1),AUFX,POSX)) QUIT:POSX=""  DO
	. . . . QUIT:$PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,7)'=3  ;ONLY MANUFACTURING
	. . . . QUIT:$PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,90)=1  ;ALREADY FINISHED
	. . . . IF $PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,256)'="" Q  ;NO ORDER WITH REFERENCE
	. . . . WRITE AUFX_"-"_POSX
	. . . . WRITE "&nbsp;&nbsp;"
	. . . . WRITE $PIECE($GET(^INAUFP(YM,AUFX,POSX,1)),Y,5)
	. . . . IF $PIECE(POS1,Y,40)'="" WRITE " "_$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(POS1,Y,40),1)),Y,1)
	. . . . WRITE YCR,"<BR>"
	. . . .QUIT
	. . . QUIT
	. . QUIT
	. WRITE YCR,"<BR>"
	. WRITE YCR,"------------------------------------------------------------------------------------------------------"
	. WRITE YCR
	. QUIT
	;
	WRITE YCR,"<BR>"
	KILL ^WWWSOR(YUSER,1)
	QUIT
]]></Routine>
</Export>