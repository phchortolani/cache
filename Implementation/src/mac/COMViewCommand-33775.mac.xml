<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewCommand" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewCommand
    ;-------------------------------------------------------------------------------
    ; optional commands for rows. Setup is in COMViewCustom
    ;-------------------------------------------------------------------------------
#include COMView
#include COMConst
#include WWWConst
	
GetCommands()
    ;-------------------------------------------------------------------------------
    ; Get the commands that are attached to buttons.
    ;
    ; Returns:String (HTML)
    ;
    ; History:
    ; 12-Apr-2005	Paul K	Created
    ;-------------------------------------------------------------------------------
    new strHTML,idCommand,objCommand
    
	set strHTML=""
	set idCommand=""
	for {
		set idCommand=$order(^CacheTempView(YUSER,"Command",idCommand))
		quit:idCommand=""
		
		set objCommand=$get(^CacheTempView(YUSER,"Command",idCommand))
		set strHTML=strHTML_"<img src="""_YGIF_$$$COMViewCommandImage(objCommand)_""" "
		set strHTML=strHTML_"title="""_$zcvt($$$COMViewCommandDescription(objCommand),"o","HTML")_""" "
		set strHTML=strHTML_"class=""coolButton"" "
		set strHTML=strHTML_"onclick=""CommandClick("_idCommand_",'"_$zcvt($$$COMViewCommandConfirmText(objCommand),"o","JS")_"');"">"
	}
	if strHTML'="" set strHTML=strHTML_Y
	
	quit strHTML
	
CallBack(pidKey="",pidCommand="")
    ;-------------------------------------------------------------------------------
    ; Used for callbacks of commands. If command returns 1, refresh the grid
    ; 
    ; Called by JS: CommandClick()
    ; 
    ; History:
    ; 09-Sep-2008	PP		SR15866:Update COMView to Objects
    ; 14-Sep-2006	RPW		SR15041: If we need to refresh, then we MUST kill the
    ; 							SQL as well.
    ; 29-Aug-2006	JW		SR14763: Set status
    ; 12-Apr-2005	Paul K	Created
    ;-------------------------------------------------------------------------------
	new idClass,strSQL,strCallBack,objResult,blnRefresh
	new blnObj
	
	set blnObj=+$get(^CacheTempObj(YUSER,"Object"))		//SR15866
	
	if (pidKey'="") && (pidCommand'="") {
		set idClass=$get(^CacheTempView(YUSER,"Class"))	
		if $$Group^COMViewSQL() {
			set strSQL=$$GenerateSQL^COMViewSQL(idClass,"",pidKey)
		} else {
			if 'blnObj {	
				set strSQL="select ID from "_idClass_" where ID='"_$select($$$WWW001SharedFile($get(^WWW001(0,idClass,1))):"",1:"0||")_$$Replace^COMUtilStr(pidKey,",","||")_"'"
			} else {
				set strSQL="select ID from "_idClass_" where ID='"_$$Replace^COMUtilStr(pidKey,",","||")_"'"
			}
		
		}
		set strCallBack = $$$COMViewCommandCode($get(^CacheTempView(YUSER,"Command",pidCommand)))
		set objResult = ##class(%Library.ResultSet).%New()
		do objResult.Prepare(strSQL)
		do objResult.Execute()
		set blnRefresh=$$$NO
		
		xecute "set blnRefresh=$$"_strCallBack_"(objResult)"
		
		if blnRefresh {
			kill ^CacheTempSQL($$$SQLID)         // SR15041
			do DisplayGrid^COMViewFilter()
		} else {
			$$$SetStatus                         //SR14763
		}
	}
	
	quit
	
]]></Routine>
</Export>