<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPPaymentGroup" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPPaymentGroup ; SR11191: Intermediate class between FINAPPayment ~Line and ~Account
	
#include COMConst
#include FINConst
#include FIN
	
AfterDataFields(pidKey)
    ;-------------------------------------------------------------------------------
    ; After Data Fields
    ;
    ; Returns:
    ;
    ; History:
    ; 22-Dec-2004	JW		Created
    ;-------------------------------------------------------------------------------
	do LoadGrid(pidKey)
	quit
 
 
LoadGrid(pidKey)
    ;-------------------------------------------------------------------------------
    ; Show the Edit Grid
    ;
    ; Returns: nil
    ;
    ; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
    ; 22-Dec-2004	JW		Created
    ;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
	
	set YFORM = "FINAPPaymentAccount"
	set YKEY  = $get(pidKey)
	set YFELD = ""
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "FINAPPaymentGroup"
	do ^COMGridEdit31
	
	quit
	
	
OnBeforeSave(pYKEY)
    ;-------------------------------------------------------------------------------
    ; On Before Save - save grid.
    ;
    ; Returns: nil
    ;
    ; History:
    ; 22-Dec-2004	JW		Created
    ;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$GRIDSave(pYKEY)
	if $$$ISERR(strStatus) {
		set Q = 1
		do ReturnError^COMUtils(strStatus)
	}
	quit
	
	
UpdateTotal()	// 24-May-2005 JW - No longer in use
				// Replaced with UpdateContainer^FINAPPaymentAccount
    ;-------------------------------------------------------------------------------
    ; Update Total header field
    ;
    ; Returns: nil
    ;
    ; History:
	; 10-Apr-2010	GRF		SR17177: arrGrid rather than objGrid; GRID Macros
    ; 03-Feb-2005	shobby	Redirected call to GetGridContents.
    ; 22-Dec-2004	JW		Created
    ;-------------------------------------------------------------------------------
	new arrGrid,idPayment,idPaymentLine,idGroup,idAccount,totalPayment
	new objFINAPPaymentAccount
	
	$$$GRIDGetContents(.arrGrid)
	
	set totalPayment = 0
	
	set idPayment    = $order(arrGrid(""))
	if idPayment'="" {
		set idPaymentLine = $order(arrGrid(idPayment,""))
		quit:idPaymentLine=""
		
		set idGroup = ""
		for {
			set idGroup = $order(arrGrid(idPayment,idPaymentLine,idGroup))
			quit:idGroup=""
			
			set idAccount = ""
			for {
				set idAccount = $order(arrGrid(idPayment,idPaymentLine,idGroup,idAccount))
				quit:idAccount=""
				
				set objFINAPPaymentAccount = $get(arrGrid(idPayment,idPaymentLine,idGroup,idAccount))
				set totalPayment = $$$FCAdd(totalPayment,$$$FINAPPaymentAccountTotal(objFINAPPaymentAccount))
			}
		}
	}
	do Header^COMGridEdit31G("D1",$justify(totalPayment,0,2))
	quit
	
	
	
]]></Routine>
</Export>