<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARDCM" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARDCM  ; Customers DCM
	
#include COMSYS
#include FINConst
#include COMConst
	
CusLoad(Number,Name,Control,BankNumber,BankBSB,BankName,Term,Name1,Name2,Name3,Street,Zipcode,
		POBox,POZip,City,State,Country,Fax,Phone,IsCreditEntity,IsBillToEntity,CustomerGroup,
		TaxCode,TaxLocation,DefaultTaxCode,CustomerReceivesAllInvoices,CustomerReceivesAllStatements,
		dteInactive,pstrInvoiceText,pstrEmail,pstrDocumentsVia,
		pstrInvoicesVia,pstrStatementsVia,pstrInvoiceEmail,pstrStatementEmail)
	;-----------------------------------------------------------------------------------------
	; Load Customers from Back Office
	; 
	; History
	; 21-Jun-2006	SteveS	SR14514: Added statement/invoice email/send via
	; 25-Nov-2005	SteveS	SR12974: Added Email Address
	; 09-Nov-2005	SteveS	SR12482: Added Invoice Text
	; 30-Sep-2005	JW		SR13447: Inactive date
	; 27-Jun-2005	shobby	SR12517: CustomerReceivesAllInvoices,CustomerReceivesAllStatements
	; 15-Jun-2005	SteveS	SR12636: Swapped BankNumber/BSBNumber Parameters
	; 09-May-2005	SteveS	SR12304: Store Country Description, NOT the code.
	; 06-May-2005	SteveS	SR12192: Don't add name to address field
	; 30-Mar-2005	SteveS	SR11937: Added Tax Location
	; 17-Feb-2005	JW		SR11667: Added tax code
	; 04-Aug-2004	shobby	Included Customer Group (SR10095) / Used standard macros
	; 17-Jun-2004	shobby	Finance only interested in Credit Entitities. (SR7792)
	; 08-Aug-2003	SCR		Added Error Decode
	; 03-Jun-2003	shobby	Included formatted Address Field
	; -----------------------------------------------------------------------------------------
	new YFORM,Rec,Status,strAddress,strPostCode
	
	set Status=$$$OK
	set Rec=""
	
	if Number="" set Status=('$$$OK)_Y_$lb("Fin00161")  ; No Number
	if IsCreditEntity||IsBillToEntity {
		if $$$ISOK(Status),$Get(Control)="" {
			set Control=$$DefaultControl^FINARCustomer(Number)
			if Control="" {
				set Status=('$$$OK)_Y_$$$Text($listbuild("Fin00080",Number))  ; No customer control account for customer %1
			}
		}
		if $$$ISOK(Status) {
			set Rec=$get(^FINARCustomer(YM,Number,1))
			set $$$FINARCustomerCustomerName(Rec)=$get(Name)
			set $$$FINARCustomerControlAccount(Rec)=$Get(Control)
			
			set $$$FINARCustomerBankBSB(Rec)=$Get(BankBSB)
			set $$$FINARCustomerBankAccountNumber(Rec)=$get(BankNumber)
			set $$$FINARCustomerBankName(Rec)=$Get(BankName)
			
			set $$$FINARCustomerDiscountTerm(Rec)=$Get(Term)
			;
			; Address String
			set strAddress=""
			
			;SR12192 -- Don't add name to address string
			;do AddPiece(.strAddress,Name1,$$$YES)
			;do AddPiece(.strAddress,Name2,$$$YES)
			;do AddPiece(.strAddress,Name3,$$$YES)
			
			if (Street'="") {
				do AddPiece(.strAddress,Street,$$$YES)
				set strPostCode=Zipcode
			}
			else {
				do AddPiece(.strAddress,POBox,$$$YES)
				set strPostCode=POZip
			}
			
			do AddPiece(.strAddress,City_", ",$$$NO)
			do AddPiece(.strAddress,State_" ",$$$NO)
			do AddPiece(.strAddress,strPostCode,$$$YES)
			
			;SR12304 -- Store country description
			do AddPiece(.strAddress,$$$SysEnum("LAND",Country),$$$NO)
 
			set $$$FINARCustomerAddress(Rec)	   =$get(strAddress)
			set $$$FINARCustomerPhone(Rec)		   =$get(Phone)
			set $$$FINARCustomerFax(Rec)		   =$get(Fax)
			set $$$FINARCustomerCustomerGroup(Rec) =$get(CustomerGroup)
 
			;SR11937 - commented
			if +$get(TaxCode)'=0 set $$$FINARCustomerTaxIncluded(Rec)=$$$YES
			;set $$$FINARCustomerTaxCode(Rec) = $get(TaxCode) ;SR11667
			
			set $$$FINARCustomerTaxLocation(Rec)	 = $get(TaxLocation) ;SR11937
			set $$$FINARCustomerDefaultTaxCode(Rec)  = $get(DefaultTaxCode)
			set $$$FINARCustomerPrintInvoices(Rec)   =$get(CustomerReceivesAllInvoices)
			set $$$FINARCustomerPrintStatements(Rec) =$get(CustomerReceivesAllStatements)
			
			set $$$FINARCustomerInactiveDate(Rec) = $get(dteInactive)	//SR13447
			set $$$FINARCustomerInvoiceText(Rec)  = $get(pstrInvoiceText) ;SR12482
			set $$$FINARCustomerEmail(Rec)		  = $get(pstrEmail) //SR12974
			
			
			//SR14514: Specific emails/send-vias for invoices/statements
			set pstrDocumentsVia=$get(pstrDocumentsVia)
			if (pstrDocumentsVia=0)||(pstrDocumentsVia="") { // printer in alphalinc
				set pstrDocumentsVia=$$$EnumCOMDOCUMENTOUTPUTPrinter	
			} elseif (pstrDocumentsVia=1) { // email in alphalinc
				set pstrDocumentsVia=$$$EnumCOMDOCUMENTOUTPUTEmail
			}
			
			if $get(pstrInvoicesVia)="" {
				set pstrInvoicesVia=pstrDocumentsVia	
			}
			
			if $get(pstrStatementsVia)="" {
				set pstrStatementsVia=pstrDocumentsVia
			}
			
			if $get(pstrInvoiceEmail)="" {
				set pstrInvoiceEmail=$get(pstrEmail)
			}
			
			if $get(pstrStatementEmail)="" {
				set pstrStatementEmail=$get(pstrEmail)
			}
			
			set $$$FINARCustomerSendInvoicesVia(Rec)	=pstrInvoicesVia
			set $$$FINARCustomerSendStatementsVia(Rec)	=pstrStatementsVia
			set $$$FINARCustomerEmailforInvoices(Rec)	=pstrInvoiceEmail
			set $$$FINARCustomerEmailforStatements(Rec)	=pstrStatementEmail
			
			set YFORM="FINARCustomer"
						
			set Status=$$Save^FINSYS(YFORM,Number,Rec,1)
		}
	}
	
	quit Status
	
AddPiece(pstrText,pstrPiece,pblnCRLF)
	;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Returns:
	;
	; History:
	; 03-Nov-2003	shobby	Changed delimiter to |, @netManager forms interpret this as crlf
	;-------------------------------------------------------------------------------
 
 	; TODO: Make this a macro -- avoid the extra function call.
 
	if $get(pstrPiece)'="" {
		set pstrText=pstrText_pstrPiece
		if pblnCRLF {
			;set pstrText=pstrText_$$$CRLF
			set pstrText=pstrText_"|"
		}
	}
	quit
 
CusRemove(pYKEY="")
    ;-------------------------------------------------------------------------------
    ; When deleted from Back Office remove from finance if appropriate
    ;
    ; Returns: always successful
    ;
    ; History:
    ; 27-Jun-2005	JW		Removed surplus code
    ; 08-Apr-2005	PO		SR11814 return successful every time
    ; 03-Mar-2005	PO		Created
    ;-------------------------------------------------------------------------------
	new strStatus
	;set strStatus=$$$OK //JW
	
	if '$$$NoKey(pYKEY) {
		
		set strStatus=$$CheckRecordsLinks^FINSYS(pYKEY,$lb("FINARInvoice","FINARADJLine","FINARBankLine"),"FINARCustomer")
	
		;if strStatus'=$$$OK {
		;	set strStatus=$$$NO  //JW
		;}
	
		if $$$ISOK(strStatus) {
			do KILL^FINSYS("FINARCustomer",pYKEY)
		}
	}
 
	quit $$$OK ; Don't want event to block in queue if unsuccessful
	
]]></Routine>
</Export>