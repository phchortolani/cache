<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEINCYCLE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEINCYCLE(YKEY)
#include %occInclude
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		CYCLE COUNTING INVENTUR
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 30-Sep-2008	GRF		SR15810: Quits; doco; boolean macros
	; 07.05.2003	FIS
	;-------------------------------------------------------------------------------
	NEW YQ
	
	SET YKEY=$GET(YKEY)
	IF YKEY="" DO ^WWWINFO($$^WWWTEXT(144)) QUIT
	SET YQ = $$$NO
	JOB START(YM,YKEY,YBED,SPRACHE)::1 IF '$TEST SET YQ=$$$YES
	IF YQ=$$$YES {
		DO ^WWWINFO($$^WWWTEXT(144))     ; "Not Possible"
	} else {
		DO ^WWWINFO($$^WWWTEXT(383))     ; "Program Has Started In Background"
	}
	QUIT
	
START(YM,YKEY,YBED,SPRACHE)
	NEW ART,ART1,ARTMAX,DAT,KLASSE,REPEAT,YI,YQ,ZAEHLMAX
	
	SET $ZTRAP="^WWWERROR"
	
	DO LOW^%PRIO
	DO
	. NEW YM,YKEY,YBED,SPRACHE,YUSER
	. DO ^WWWVAR
	
	QUIT:$GET(YKEY)=""
	SET YFELD=$GET(^INWEINCYCLE(YM,YKEY,1))
	QUIT:YFELD=""
	
	IF $PIECE(YFELD,Y,1)<0  SET $PIECE(YFELD,Y,1) = 0
	IF $PIECE(YFELD,Y,2)<0  SET $PIECE(YFELD,Y,2) = 0
	IF $PIECE(YFELD,Y,3)<0  SET $PIECE(YFELD,Y,3) = 0
	IF $PIECE(YFELD,Y,4)'>0 SET $PIECE(YFELD,Y,4) = 1     ; Not included in class defn
	
	SET REPEAT("A")=$PIECE(YFELD,Y,1)                     ; Most Important
	SET REPEAT("B")=$PIECE(YFELD,Y,2)                     ; Important
	SET REPEAT("C")=$PIECE(YFELD,Y,3)                     ; Less Important
	
	DO ^INWEINCYCLEKILL  ;MUSS $D(YKEY) !  ;LÖSCHEN ALTE DATEN ; Backup and Delete 
	
	SET ARTMAX   = 0
	SET ZAEHLMAX = 0
	
	;	D26		$$$INARTItemType()
	;	D177	$$$INARTItemClassification()
	
	SET ART=""
	FOR  SET ART=$ORDER(^INART(YM,ART)) QUIT:ART=""  DO
	. SET ART1=$GET(^INART(YM,ART,1))
	. QUIT:$PIECE(ART1,Y,26)=2  ;KEINE LEISTUNG ;no performance 
	. QUIT:$PIECE(ART1,Y,26)=5  ;KEINE PHANTOMARTIKEL ;no 
	. QUIT:$PIECE(ART1,Y,26)=6  ;KEINE PAUSCHALE ;no 
	. QUIT:$PIECE(ART1,Y,26)=7  ;KEINE KONFIGURATIONSARTIKEL ;no 
	. QUIT:$PIECE(ART1,Y,29)=1  ;OHNE BESTANDSFÜHRUNG ;without 
	. ;QUIT:$PIECE(ART1,Y,30)'=13  ;  ;TEST !!!!!!!!!!!!!!!!!!
	. ;
	. ; Don't continue if No Transactions within the last year when there is currently no stock on hand
	. SET YQ=$$$NO
	. IF $$^INARTMENGE(ART,,,1)=0 DO
	. . set DAT = $$GetLastTrn(ART)
	. . IF $$^WWWYEAR(DAT+365)<YKEY SET YQ=$$$YES
	. ;
	. QUIT:YQ=$$$YES
	. ;
	. SET KLASSE=$PIECE(ART1,Y,177)
	. IF KLASSE="" SET KLASSE="C"
	. ;
	. SET ^INWEINCYCLE1(YM,YKEY,0,+$GET(REPEAT(KLASSE)),ART,1)=""
	. SET ARTMAX   = ARTMAX   + 1                     ;ANZAHL ARTIKEL   ;Number of items
	. SET ZAEHLMAX = ZAEHLMAX + $GET(REPEAT(KLASSE))  ;ANZAHL ZÄHLUNGEN ;Number 
	
	
	DO
	. NEW YOK,YFORM,YVOR
	. SET $PIECE(YFELD,Y,10)=ARTMAX         ; Number of Items         ; ANZAHL VERSCHIEDENE ARTIKEL
	. SET $PIECE(YFELD,Y,11)=ZAEHLMAX       ; Total Number To Count   ; ARTIKELZÄHLUNGEN INSGESAMT 
	. SET $PIECE(YFELD,Y,12)=0              ; Counted                 ; BEREITS GEZÄHLT
	. SET $PIECE(YFELD,Y,13)=ZAEHLMAX       ; Open to Count           ; OFFENE ZÄHLUNGEN
	. SET YOK=$$^WWWSPEI("INWEINCYCLE",YKEY,YFELD,1)
	
	QUIT
	

GetLastTrn(pidItem)
	;-------------------------------------------------------------------------------
	; Get the Date of the Last Transaction for an Item
	;
	; Inputs:pidItem, pidLocation
	;
	; Returns: %Date
	;
	; History:
	; 08-Sep-2009	PP		SR<>: Created
	;-------------------------------------------------------------------------------
	new SQLCODE
	new dteTrnDate
	
	set dteTrnDate=""
	
	&SQL(
		SELECT Top 1 DateConfirmed INTO :dteTrnDate
		FROM alSOH.dStockHistory
		WHERE Item = :pidItem
		ORDER BY ID DESC
	)
	
	quit dteTrnDate
]]></Routine>
</Export>