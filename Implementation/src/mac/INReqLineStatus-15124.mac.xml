<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReqLineStatus" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReqLineStatus
	
#include COMSYS
#include COMConst
#include INConst
#include %occInclude
	 
GetOutLineForReq(pidReq,penumType=0)
	;-------------------------------------------------------------------------------
	; Get the no of Outstanding Lines
	;
	; Inputs: pidReq - ID Req
	;         penumType : 0 - Total Outstanding
	;                     1 - Can be Fullfilled
	;                     2 - Partially Fulfilled
	;                     3 - No Stock
	;
	; Returns: Number of lines for type code specified
	;
	; History:
	; 17-Aug-2010	PPP		SR17502:Corrected logic to exclude -ve outstanding qty
	; 22-Feb-2010	PPP		SR17182:Redid this subroutine, removed Cached data
	; 18-Feb-2010	PPP		SR17182:Created (to get no. of Lines status)
	;-------------------------------------------------------------------------------
	new arrCount,fltAvail,fltReq,idItem,enumType,intLine,objReqLine
	
	quit:'$data(^INReqLine(0,pidReq)) 0          ; No Lines
	
	set intLine = ""
	for {
		set intLine = $order(^INReqLine(0,pidReq,intLine))
		quit:intLine=""
		
		set objReqLine = $get(^INReqLine(0,pidReq,intLine,1))
		continue:(($$$INReqLineStatus(objReqLine)=8) || ($$$INReqLineStatus(objReqLine)=9))   //SR17502
		
		set idItem = $$$INReqLineItem(objReqLine)
		set fltReq = $$$INReqLineQtyRequired(objReqLine) - $$$INReqLineQtyIssued(objReqLine)
		
		continue:fltReq'>0
		
		// Lines Outstanding
		set arrCount(0) = $get(arrCount(0))+1
		
		// Only do the extra processing if needed		
		if penumType>0 {
			set fltAvail = $$GetQuantityOnHand^INARTMENGE(idItem,$$$INReqLineFromStockLocn(objReqLine))   ; [IU]
			
			if fltAvail>=fltReq {
				set enumType = 1		             ; Lines Fulfilled
			
			} elseif fltAvail && (fltReq>fltAvail) {
				set enumType = 2		             ; Lines Partially Fulfilled
			
			} else {
				set enumType = 3		             ; Lines Out of Stock
			}
			set arrCount(enumType) = $get(arrCount(enumType))+1
		}
	}
	quit +$get(arrCount(penumType))
	
]]></Routine>
</Export>