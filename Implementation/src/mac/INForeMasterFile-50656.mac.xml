<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INForeMasterFile" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INForeMasterFile  ; Masterfile related processing
#include %occInclude
#include COMSYS
#include COMConst
#include INConst
#include COMView
	
FormList(pstrClass)
	;-------------------------------------------------------------------------------
	; 		Store a COMTempList List of form that are used by a class based on
	; 		the Index ^WWW120s(0,1)
	;
	; Params:
	;	pstrClass=Class (Can be null)
	;
	; Returns: 
	;	status
	;
	; History:
	; 01-Feb-2008	STEVER	SRBR014376 Added Form Name
	; 29-Jan-2008	STEVER	SRBR014376: Created
	;-------------------------------------------------------------------------------
	new strStatus,strForm,objForm
	
	set strStatus=$$$OK
	set strForm=""
	
	kill ^COMTempList(YM,YUSER,"Forms")
	if pstrClass'="" {
		for  {
		;	FIXME : Need to use $$$Index on pstrClass; need to consider possibility of
		;	multiple classes with same $$$Index value (e.g. INART and INArt) - double
		;	check class match after obtaining form ID <GRF>
			set strForm=$order(^WWW120s(0,1,pstrClass,strForm))
			quit:strForm=""
			
			set objForm=$get(^WWW120(0,strForm,1))
			do Save^FINSYS("COMTempList",YUSER_YKOMMA_"Forms"_YKOMMA_strForm,$$$WWW120FormHeaderOrImageFile(objForm),1)
		}
	}
	
	quit strStatus
	
FormDropDown(pstrClass,pintField)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Update Forms if the Class changes
	;
	; Inputs :
	;	pstrClass=Class
	;	pintField=Field to add list to
	;
	;
	; History :
	; 29-Jan-2008	STEVER	SRBR014376: Created
	;-------------------------------------------------------------------------------
	new strField,strForm
	
	do FormList(pstrClass)
		
	set pintField=+$get(pintField)
	set strField="Y"_YFORM_YART_pintField
	set %TXT(1)=""
	
	set %TXT(1)=%TXT(1)_"#"_strField_"~REMOVE"
	;set %TXT(1)=%TXT(1)_"#"_strField_"~"_$$$NULLID_"~"_$$$SPACE_"~"_0
	
	set strForm=""
	
	for {
		set strForm=$order(^COMTempList(YM,YUSER,"Forms",strForm))
		quit:strForm=""
		
		set %TXT(1)=%TXT(1)_"#"_strField_"~"_strForm_"~"_strForm
	}
	
	quit
	
	
MakeCode(pstrRule,pintLine,pstrLocation,pstrSupplier,pstrMessage,pstrStatus) 
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Return code to either get or set a masterfile field
	;
	; Inputs :
	;	pstrRule=Rule
	;	pintLine=RuleLine
	;	pstrLocation=Loction
	;	pstrSupplier=Supplier
	;	pstrMessage=Message (Pass by Ref)
	;	pstrStatus=Status (Pass by Ref)
	;
	;
	; History :
	; 02-Apr-2008	STEVER	SRBR014376: Changed get code to use a common function
	; 04-Feb-2008	STEVER	SRBR014376: Created
	;-------------------------------------------------------------------------------
 	new blnChange,intFormLine,intMasterfileField,objForm,objFormLine,objLine,objMethod
 	new strCode,strForm,strMasterfile,strMasterfileLocation,strMethod,strPrimaryKey,strStatus
 	
 	set strStatus = $$$OK
 	set strCode   = ""
 	set objLine   = $get(^INForeLine(YM,pstrRule,pintLine,1))
	set blnChange = $$$INForeLineMasterfileChange(objLine)
	set intMasterfileField = +$$$INForeLineItemMasterFileData(objLine)
	if 'intMasterfileField {
		set strStatus="IN00826"  ; No Field     ; FIXME : Use $$$MakeStatus? <GRF>
	}
	
	set strMethod = $$$INForeLineMethod(objLine)
	if strMethod="" set strMethod = " "
	
	set objMethod     = $get(^INForeCalc(0,strMethod,1))
	set strMasterfile = $$$INForeCalcMasterfile(objMethod)		; Masterfile (Class)
	set strPrimaryKey = $$$INForeCalcPrimaryKey(objMethod)
	
	;  The Masterfile method can get or set data.
	; 	The get is simple and requires no validation
	;   The set is complex and requires validation based on a form. 
	;		The form is used to for readonly, hidden, required, data type etc
	;		This process has to also consider customization
	
	if 'blnChange && (strStatus=$$$OK) {    ; Get Masterfile
		set strCode=$$GetBuild(strMasterfile,strPrimaryKey,intMasterfileField)
	}
	
	if blnChange && (strStatus=$$$OK) {     ; Set Masterfile
		; Step 1 Find the 1st field in the form
		set strForm=$$$INForeCalcFormValidation(objMethod)
		if strForm="" {
			set strStatus="IN00827"  ;Form Required for Validation
		}
		if strStatus=$$$OK {
			set objForm=$get(^WWW120(0,strForm,1))
			if $$$WWW120ClassUsedInForm(objForm)'=strMasterfile {
				set strStatus=$listbuild("IN00828",strForm,strMasterfile)  ;Form %1 is not based on Materfile %2
			}
		}
		; Scan the lines of the form and stop at the first line that matches
		if strStatus=$$$OK {
			set intFormLine="" 
			for  {
				set intFormLine=$order(^WWW122(0,strForm,intFormLine))
				quit:intFormLine=""
				
				set objFormLine=$get(^WWW122(0,strForm,intFormLine,1))
				if $$$WWW122SequenceNumber(objFormLine)=intMasterfileField {
					quit   ; found a match, stop looking!
				}
			}
			if intFormLine="" {
				set strStatus=$listbuild("IN00829",strForm,intMasterfileField)  ;Form %1 does not have the Masterfile Item %2
			}
		}
		
		; Step 2 Create Set Code code based on the line
		if strStatus=$$$OK {
			set pstrMessage=pstrMessage_" "_$$$Text("IN00838")  ;Set Value
			set strCode=$$MakeSetCode(pstrRule,pintLine,pstrLocation,pstrSupplier,strForm,intFormLine,.pstrMessage,.pstrStatus)
		}
	}
	quit strCode
	
	
MakeSetCode(pstrRule,pintLine,pstrLocation,pstrSupplier,pstrForm,pintFormLine,pstrMessage,pstrStatus)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Return code to set a field in a masterfile based on a form line
	;
	; Inputs :
	;	pstrRule		= Rule
	;	pintLine		= RuleLine
	;	pstrLocation	= Loction
	;	pstrSupplier	= Supplier
	;	pstrLocation	= Location
	;	pstrSupplier	= Supplier
	;	pstrForm		= Form
	;	pintFormLine	= Form Line
	;	pstrMessage		= Message (Pass by Ref)
	;	pstrStatus		= Status (Pass by Ref)
	;
	;
	; History :
	; 26-Mar-2008	STEVER	SRBR014376: Changed code to use late checking via a
	; 							standard function (WWWFieldValidation)
	; 25-Mar-2008	STEVER	SRBR014376: Added Verbose
	; 19-Feb-2008	STEVER	SRBR014376: Added Calc Code
	; 11-Feb-2008	STEVER	SRBR014376: Created
	;-------------------------------------------------------------------------------
	new objFormLine,strStatus,strCode,objLine,strMethod,objMethod,strMasterfile,blnChange,intMasterfileField,strCalcCode
	new strPrimaryKey,strCalc
	
	set strCode   = ""
	set strStatus = $$$OK
	
	; Get Rule/Method Related info
 	set objLine   = $get(^INForeLine(YM,pstrRule,pintLine,1))
	set blnChange          = $$$INForeLineMasterfileChange(objLine)
	set strCalc            = $$$INForeLineCalculation(objLine)
	set intMasterfileField = +$$$INForeLineItemMasterFileData(objLine)
	set strMethod          = $$$INForeLineMethod(objLine)
	if strMethod="" set strMethod = " "
	
	set objMethod = $get(^INForeCalc(0,strMethod,1))
	set strMasterfile = $$$INForeCalcMasterfile(objMethod)		; Masterfile (Class)
	set strPrimaryKey = $$$INForeCalcPrimaryKey(objMethod) 		; Location of Masterfile
	
	set strCalcCode = $$Calculation^INForeRule(strCalc,"",.pstrMessage)
	
	; Create the line of code, This will in turn call the SetRequest which will create a request entry on the Result Data Record
	if strStatus=$$$OK {
	;	xecute "set strPrimaryKey="_strPrimaryKey				; Resolve expressions
	;	set strPrimaryKey=$$^WWWKEYBUILD(strPrimaryKey)			; Add Quotes
		set strCode = "$$SetRequest^INForeMasterFile(.Data"
		set strCode = strCode_","""_pstrRule_""""
		set strCode = strCode_","""_pintLine_""""
		set strCode = strCode_","""_strMasterfile_""""			; Masterfile
		set strCode = strCode_","""_strPrimaryKey_""""			; Masterfile Location
		set strCode = strCode_","_intMasterfileField			; Masterfile Field
		set strCode = strCode_","_strCalcCode					; Value
		set strCode = strCode_","""_pstrForm_""""				; Form
		set strCode = strCode_","_pintFormLine_""				; Form Line													
		set strCode = strCode_",Verbose"
		set strCode = strCode_")"
	}
	
	quit strCode
	
SetRequest(parrData,pstrRule,pintLine,pstrMasterfile,pstrPrimaryKey,pintMasterfileField,pstrValue,pstrForm,pintFormLine,pblnVerbose=0)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Set a Request in the Data structure for a Masterfile change.
	;
	; Inputs :
	;	pstrData=Data Location for Forecast result
	;	pstrRule=Rule
	;	pintLine=RuleLine
	; 	pstrMasterfile=Masterfile to be changed
	;	pstrMasterfileLocation=Actual Location of the Masterfile
	;	pintMasterfileField=Field number to be changed
	;	pstrValue=New Value
	;	pstrForm
	;	pintFormLine
	;	pblnVerbose=Verbose flag
	;
	;
	; History :
	; 02-Apr-2008	STEVER	SRBR014376: Set Global for faster access and resolve
	; 							primary keys
	; 26-Mar-2008	STEVER	SRBR014376: Changed code to use late checking via a
	; 							standard function (WWWFieldValidation)
	; 25-Mar-2008	STEVER	SRBR014376: Added Verbose Message
	; 11-Feb-2008	STEVER	SRBR014376: Created
	;-------------------------------------------------------------------------------
	new intNext,objSet,strPrimaryKey,strGlobal
	new strCurrentValue
	;for i=1:1:100 h 1
 
	; Build the Record
	
	set objSet=""
	xecute "set strPrimaryKey="_pstrPrimaryKey
	set strPrimaryKey = $$^WWWKEYBUILD(strPrimaryKey)		; Add Quotes
	set strGlobal = "^"_pstrMasterfile_"("_$$^WWWYM(pstrMasterfile)_","_strPrimaryKey_",1)"  ; Build Global
 
	set $$$INForeRestSetRule(objSet)            = pstrRule
	set $$$INForeRestSetRuleLine(objSet)        = pintLine
	set $$$INForeRestSetMasterfile(objSet)      = pstrMasterfile
	set $$$INForeRestSetPrimaryKey(objSet)      = strPrimaryKey
	set $$$INForeRestSetMasterfileField(objSet) = pintMasterfileField
	set $$$INForeRestSetValue1(objSet)          = pstrValue
	set $$$INForeRestSetForm(objSet)            = pstrForm
	set $$$INForeRestSetFormLine(objSet)        = pintFormLine
	set $$$INForeRestSetGlobal1(objSet)         = strGlobal
	set $$$INForeRestSetOldValue(objSet)		= $$GetCurrentValue(objSet)
	
	; Get the Next Number, There could be more than one databse entry for a rule
	set intNext=$order(parrData("Masterfile",""),-1)+1
	
	; Set the Record
	set parrData("Masterfile",intNext)=objSet
	
	if pblnVerbose {
		set strCurrentValue=$$$INForeRestSetOldValue(objSet)
		do Message^INForeItem($$$Text("IN00839") ,""""_strCurrentValue_"""",$$$Text("IN00376"),"")
	}
		
	quit pstrValue
	

	
Set(pobjSet,pintType)
    ;-------------------------------------------------------------------------------
    ; Set the new value after passing valiadtion
    ;
    ; Parameters:
    ;  pobjSet=Masterfile Set Object
    ;  pintType= Type of Set, 1=From Test Screen
    ;
    ;  return Status
    ;
    ; History:
	; Mar-26-2008	STEVER	SRBR014376: Created
    ;-------------------------------------------------------------------------------
	new strStatus,blnValid,strRule,intRuleLine,strMasterfile,strPrimaryKey,intMasterfileFeild
	new strValue,intMasterfileField,strForm,intFormLine,strReason
	new strGlobal,objRecord
	
	set strStatus = $$$OK
	set strRule            = $$$INForeRestSetRule(pobjSet)
	set intRuleLine        = $$$INForeRestSetRuleLine(pobjSet)
	set strMasterfile      = $$$INForeRestSetMasterfile(pobjSet)
	set strPrimaryKey      = $$$INForeRestSetPrimaryKey(pobjSet)
	set intMasterfileField = $$$INForeRestSetMasterfileField(pobjSet)
	set strValue           = $$$INForeRestSetValue1(pobjSet)
	set strForm            = $$$INForeRestSetForm(pobjSet)
	set intFormLine        = $$$INForeRestSetFormLine(pobjSet)
	
	set blnValid = $$ValidRelation^WWWFieldValidation("D",strMasterfile,strForm,intFormLine,strValue,$$$NO,.strReason)
	
	set $$$INForeRestSetValid(pobjSet)  = blnValid
	set $$$INForeRestSetReason(pobjSet) = $get(strReason)
	if blnValid {
		set strGlobal = $$$INForeRestSetGlobal1(pobjSet)
		xecute "set objRecord=$get("_strGlobal_")"
		set $piece(objRecord,Y,intMasterfileField) = strValue
		set $$$INForeRestSetSet1(pobjSet) = $$$Save(strMasterfile,strPrimaryKey,objRecord,$$$YES)
	}
	
	quit
	
	
GetCurrentValue(pobjSet)
    ;-------------------------------------------------------------------------------
    ; Get the current value of the masterfile
    ;
    ; Parameters:
    ;  pobjSet=Masterfile Set Object
    ;
    ; History:
	; Mar-25-2008	STEVER	SRBR014376: Created
    ;-------------------------------------------------------------------------------
	new strCurrent,strPrimaryKey,intField,strGlobal,strMasterfile,strCode
	
	set strPrimaryKey = $$$INForeRestSetPrimaryKey(pobjSet)
	set strMasterfile = $$$INForeRestSetMasterfile(pobjSet)
	set intField      = $$$INForeRestSetMasterfileField(pobjSet)
	set strCurrent    = ""
	
	if ((strPrimaryKey'="")||('intField)) {
		set strCode=$$GetBuild(strMasterfile,strPrimaryKey,intField)
		xecute "set strCurrent="_strCode		; Get Value
	}
	
	quit strCurrent
	
GetBuild(pstrGlobal,pstrPrimaryKey,pintField)
    ;-------------------------------------------------------------------------------
    ; Build up the code to get a value
    ; 
    ; pstrGlobal = Class (Global)
    ; pstrPrimaryKey = Primary key(s) as an expression
    ; pintField = the Field number
    ;
    ; History:
	; 02-Apr-2008	STEVER	SRBR014376: Created
    ;-------------------------------------------------------------------------------
    new strCode,strPrimaryKey,strGlobal
    
    set strPrimaryKey=pstrPrimaryKey
	set strGlobal="^"_pstrGlobal_"("_$$^WWWYM(pstrGlobal)_","_strPrimaryKey_",1)"  ; Build Global
	set strCode="$piece($get("_strGlobal_"),Y,"_pintField_")"	; A Piece Function
 
	quit strCode
	
MasterfileSearch(pidLocation)
    ;-------------------------------------------------------------------------------
    ; Shows a Comview for the Masterfile changes for Item & Location
    ;
    ; History:
	; 20-Feb-2008	STEVER	SRBR014376: Created
    ;-------------------------------------------------------------------------------
    if pidLocation'="" {
		do Initialise^COMViewCustom("INForeRestSet","","",1)
	;	do AddFilter^COMViewCustom("P"_$$$FldINForeRestSetLocation,"=",pidLocation)
		do Start^COMViewCustom()
    }
	quit $$$OK
	
	; ==============================================================================
	; Support code for Masterfile preview
	
OnAfterDataFields(pstrCompany, pstrForm)
	;-------------------------------------------------------------------------------
	; Description : Show pending Masterfile changes
	;			
	; Called By : From INForeRestMast
	;		
	; Inputs : Coy, Form
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 04-Dec-2009	SCR		SRBR014376: Created
	;-------------------------------------------------------------------------------
	
	
	do LoadGrid(pstrCompany, pstrForm)
 
	quit
	
LoadGrid(pstrCompany=0,pstrForm,pblnLocked=$$$NO)
	;-------------------------------------------------------------------------------
	; Description : Load up the Masterfile changes
	;			
	; Called By : See Above
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns : Status
	;
	; History :
	; 04-Dec-2009	SCR		SRBR014376: Created
	;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
 
	kill ^CacheTempLocked(YUCI,YUSER)
	do ClearOld^WWWMultiLock(YUSER)
 
	$$$ClearExternalFilter("INForeRestMast") 
	
	//YFORM is used as a global within COMGridEdit31
	set YFORM = "INForeRestSet"
	

	set YFELD	= ""
	set YKEY	= ""		// No header
	;do SetYFORM^COMGridEdit31Head(pstrForm)
 
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)          = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)       = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)            = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)             = $$$YES
	set $$$COMGridEditParameterContainer(YAUSWAHL)           = pstrForm
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$YES
	set $$$COMGridEditParameterCOMViewGrid(YAUSWAHL)         = $$$YES
 
	do Start^COMGridEdit31(YFORM,YKEY)
	
	do LoadCOMViewGrid^COMViewFilter(pstrForm,"INForeRestSet",1)
 
	quit $$$YES

]]></Routine>
</Export>