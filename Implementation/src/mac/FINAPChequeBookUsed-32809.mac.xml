<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPChequeBookUsed" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPChequeBookUsed
	#include FINConst
	#include COMSYS
 
GoToSupplierPayment()
    /*-------------------------------------------------------------------------------
    ; Modified from GoToAccount^FINGLJournalLine.  Selects the ChequeBookUsed record from Grid,
    ; and loads the Supplier Payments form, with the record containing the selected cheque. 
    ;
    ; Params: none
    ;
    ; ByRefs:
    ;
    ; Returns: none
    ;
    ; History:
    ;  7-Aug-2006	JW		SR14730: Popup
    ; 25-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new objChequeBookUsed
	set objChequeBookUsed = $$GetYFELDEX^COMGridEdit31Interface() // Get current line
	
	do GoToForm^COMGridEdit31Interface("FINAPPaymentLine",$$$FINAPChequeBookUsedPaymentNumber(objChequeBookUsed)_","_$$$FINAPChequeBookUsedPaymentLine(objChequeBookUsed),$$$YES) //SR14730
 
	quit
	
 
 
GetChequeNumber(pobjChequeBookUsed) ;FIX...?? can use FIELD param to combine all these procedures.
    /*-------------------------------------------------------------------------------
    ; Gets Cheque Number from the corresponding FINAPPaymentLine record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: string
    ;
    ; History:
    ; 13-Feb-2006 RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new idPaymentNumber, idPaymentLine, objPaymentLine
	
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	set idPaymentLine = $$$FINAPChequeBookUsedPaymentLine(pobjChequeBookUsed)
	
	set objPaymentLine=""
	
	if (idPaymentNumber'="")&&(idPaymentLine'="") {
		set objPaymentLine=$get(^FINAPPaymentLine(YM,idPaymentNumber,idPaymentLine,1))
	}
	
	quit $$$FINAPPaymentLineChequeNumber(objPaymentLine)
	 
 
GetPaymentDate(pobjChequeBookUsed)
    /*-------------------------------------------------------------------------------
    ; Gets Payment Date from the corresponding FINAPPayment record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: Payment Date
    ;
    ; History:
    ; 13-Feb-2006 RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new idPaymentNumber,idPaymentLine,objPayment
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	
	set objPayment=""
	
	if idPaymentNumber'="" {
		set objPayment=$get(^FINAPPayment(YM,idPaymentNumber,1))
	} 
	
	quit $$$FINAPPaymentPaymentDate(objPayment)
	 
 
GetSupplierNumber(pobjChequeBookUsed)
    /*-------------------------------------------------------------------------------
    ; Gets Supplier Number from the corresponding FINAPPaymentLine record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: Supplier Number 
    ;
    ; History:
    ; 13-Feb-2006 RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new idPaymentNumber, idPaymentLine, objPaymentLine
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	set idPaymentLine = $$$FINAPChequeBookUsedPaymentLine(pobjChequeBookUsed)
	
	set objPaymentLine=""
	
	if (idPaymentNumber'="")&&(idPaymentLine'="") {
		set objPaymentLine=$get(^FINAPPaymentLine(YM,idPaymentNumber,idPaymentLine,1))
	}
	
	quit $$$FINAPPaymentLineSupplierNumber(objPaymentLine)
 
 
GetSupplierName(pobjChequeBookUsed)
    /*-------------------------------------------------------------------------------
    ; Supplier Name from the corresponding FINAPSupplier record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: Supplier Name
    ;
    ; History:
    ; 13-Feb-2006 RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/ 
	new idPaymentNumber, idPaymentLine, objPaymentLine, idSupplier, objSupplier
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	set idPaymentLine = $$$FINAPChequeBookUsedPaymentLine(pobjChequeBookUsed)
	
	set objPaymentLine=""
	
	if (idPaymentNumber'="")&&(idPaymentLine'="") {
		set objPaymentLine=$get(^FINAPPaymentLine(YM,idPaymentNumber,idPaymentLine,1))
	}
	
	set idSupplier=$$$FINAPPaymentLineSupplierNumber(objPaymentLine)
	set objSupplier=""
	
	if idSupplier'="" {
		set objSupplier=$get(^FINAPSupplier(YM,idSupplier,1))
	}
 
	quit $$$FINAPSupplierSupplierName(objSupplier)
 
 
GetPayment(pobjChequeBookUsed)
    /*-------------------------------------------------------------------------------
    ; Gets Payment Amount from the corresponding FINAPPaymentLine record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: Payment Amount
    ;
    ; History:
    ; 13-Feb-2006 RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new idPaymentNumber, idPaymentLine, objPaymentLine
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	set idPaymentLine = $$$FINAPChequeBookUsedPaymentLine(pobjChequeBookUsed)
	
	set objPaymentLine=""
	
	if (idPaymentNumber'="")&&(idPaymentLine'="") {
		set objPaymentLine=$get(^FINAPPaymentLine(YM,idPaymentNumber,idPaymentLine,1))
	}
	
	quit $$$FINAPPaymentLineTotal(objPaymentLine)
 
 
GetPaymentStatus(pobjChequeBookUsed)
    /*-------------------------------------------------------------------------------
    ; Gets Payment Status from the corresponding FINAPPaymentLine record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: Payment Status 
    ;
    ; History:
    ; 27-Feb-2006	JW		SR13975: Newed variables, defined strPaymentStatus
    ; 14-Feb-2006	SC		SR13975: Show Cancelled status for manually cancelled cheques.
    ; 13-Feb-2006 	RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new idPaymentNumber, idPaymentLine, objPaymentLine,idCancelledIndex,strPaymentStatus
	
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	set idPaymentLine = $$$FINAPChequeBookUsedPaymentLine(pobjChequeBookUsed)
	set idCancelledIndex = $$$FINAPChequeBookUsedCancelledIndex(pobjChequeBookUsed)
	
	set objPaymentLine=""
	
	set strPaymentStatus=""
	
	if (idPaymentNumber'="")&&(idPaymentLine'="") {
		set objPaymentLine=$get(^FINAPPaymentLine(YM,idPaymentNumber,idPaymentLine,1))
		set strPaymentStatus = $$$AppEnum("FINSYSSTATUS",$$$FINAPPaymentLineStatus(objPaymentLine))
	} elseif idCancelledIndex '="" {
		set strPaymentStatus = $$$AppEnum("FINSYSSTATUS",$$$EnumFINSYSSTATUSCancelled)
	}
	
	quit strPaymentStatus 
 
 
GetPaymentMethod(pobjChequeBookUsed)
    /*-------------------------------------------------------------------------------
    ; Gets Payment Method from the corresponding FINAPPayment record.
    ;
    ; Params: ChequeBookUsed Record
    ;
    ; ByRefs:
    ;
    ; Returns: Payment Method
    ;
    ; History:
    ; 13-Feb-2006 RPW		SR14106: Modded for build errors.
    ; 18-Jan-2006	SC		SR14016: Created
    ;-------------------------------------------------------------------------------*/
	new idPaymentNumber, idPaymentLine, objPayment
	set idPaymentNumber = $$$FINAPChequeBookUsedPaymentNumber(pobjChequeBookUsed)
	
	set objPayment=""
	if idPaymentNumber'="" {
		set objPayment=$get(^FINAPPayment(YM,idPaymentNumber,1))
	}
	
	quit $$$AppEnum("FINARBANKLINEMETHOD",$$$FINAPPaymentPaymentMethod(objPayment))
	
	
	
	
InsertCheque(pidBankAccount,pidCheque,pidPayment,pidLine) 
	;-------------------------------------------------------------------------------
	; Insert a used cheque number into a cheque book
	;
	; Params: bank account, cheque number, payment and payment line ids
	;
	; ByRefs:
	;
	; Returns: status
	;
	; History:
	; 20-Mar-2006	JW		SR14444: Moved from Post^FINAPPaymentPost
	;-------------------------------------------------------------------------------
	new strStatus,idFirstCheque,blnBothNums,blnBothSameLength,idIndex,objBook
	
	if ($order(^FINAPChequeBook(YM,pidBankAccount,"")) = "") { ;SR14026
		set strStatus = $listbuild("Fin00971",pidBankAccount) ;  No Cheque Books exist for Bank Account %1.
		
	} else {
		set idFirstCheque=$order(^FINAPChequeBook(YM,pidBankAccount,$$IncTextCheque^FINAPChequeBook(pidCheque,1)),-1) ;SR14036
		
		//FirstCheque and ChequeNumber both nums?
		set blnBothNums = ((+idFirstCheque=idFirstCheque) && (+pidCheque=pidCheque))
		
		//FirstCheque and ChequeNumber both same length?
		set blnBothSameLength = ($length(idFirstCheque)=$length(pidCheque))
		
		;if Cheque Number is ok for ChequeBook.
		if (idFirstCheque '="") && (blnBothNums || blnBothSameLength) { ;SR14026
			
			set idIndex=$order(^FINAPChequeBookUsed(YM,pidBankAccount,idFirstCheque,""),-1)+1
			
			set $$$FINAPChequeBookUsedPaymentNumber(objBook) = pidPayment
			set $$$FINAPChequeBookUsedPaymentLine(objBook) = pidLine
			set $$$FINAPChequeBookUsedChequeNumber(objBook) = pidCheque
			
			;SR14026
			set strStatus = $$$Save("FINAPChequeBookUsed",pidBankAccount_","_idFirstCheque_","_idIndex,objBook,1)
 
		} else {
			set strStatus = $listbuild("Fin00351",pidCheque,pidBankAccount) ;Cheque number ´%1´ was not found in any chequebook for account ´%2´.
		}
	}
	
	quit strStatus
	
	
	
]]></Routine>
</Export>