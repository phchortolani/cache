<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINDAPSupplier" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINDAPSupplier
 
#include COMSYS
#include INSupStatus
#include FINConst
 
IsUsable(pidSupplier,pidForm)
    ;-------------------------------------------------------------------------------
    ; Whether supplier can be used at this time.
    ; Check status permissions.
    ;
    ; Params:	pidSupplier
    ;
    ; Returns: status
    ;
    ; History:
    ; 23-Feb-2007	JW		SR15453: Moved from FINAPSupplier. Inactive Date -> Status
    ; 15-Aug-2005	JW		SR12290: Created
    ;-------------------------------------------------------------------------------
	new idStatus,strStatus,intField
	
	set strStatus = $$$OK
	
	if ($get(pidSupplier)'="") && ($extract(pidForm,1,3) = "FIN") {		// Ignore REPorts etc for now
		set idStatus = $$$FINAPSupplierStatus($get(^FINAPSupplier(YM,pidSupplier,1)))
		if idStatus="" {
			set strStatus = $$$AppError("P0060000014")	// Mandatory field should not be blank.
		} else {
			set intField = $case(pidForm, 	"FINAPPaymentLine"	: $$$FldINSupStatusPayments,
											"FINAPADJLine"		: $$$FldINSupStatusInvoices,
											"FINAPInv"			: $$$FldINSupStatusInvoices,
											"FINAPVoucher"		: $$$FldINSupStatusInvoices,
																: $$$FldINSupStatusOther)
			set strStatus = $$$HasPermission(pidSupplier,idStatus,intField)
		}
	}
	
	quit strStatus
	
	
	/*	SR15453 - redefined above
	if $get(pidSupplier)'="" {
		set dteInactive = $$$FINAPSupplierInactiveDate($get(^FINAPSupplier(YM,pidSupplier,1)))
		if (dteInactive'="") && (+$horolog >= dteInactive) {
			set strStatus = $listbuild("Com00226",pidSupplier) ; Supplier %1 is inactive
		}
	}
	quit strStatus
	*/
	
CanInvoice(pidSupplier)
	;-------------------------------------------------------------------------------
	; Can we create invoices for this supplier?
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 27-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idStatus
	set idStatus = $$$FINAPSupplierStatus($get(^FINAPSupplier(YM,pidSupplier,1)))
	set strStatus = $$$HasPermission(pidSupplier,idStatus,$$$FldINSupStatusInvoices)
	
	quit strStatus
 
 
CanPay(pidSupplier)
	;-------------------------------------------------------------------------------
	; Can we create payments for this supplier?
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 27-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idStatus
	set idStatus = $$$FINAPSupplierStatus($get(^FINAPSupplier(YM,pidSupplier,1)))
	set strStatus = $$$HasPermission(pidSupplier,idStatus,$$$FldINSupStatusPayments)
	
	quit strStatus
]]></Routine>
</Export>