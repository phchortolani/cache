<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHCGeneral" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHCGeneral	; COM General Health Checks
	#include COMConst
	#include WWWConst
	#include COMSYS
 
 
RelationalCheck(Start="",End="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; All classes whose IDs refer to other classes must have valid instances of data
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Nov-2005	RPW			SR13916: Do not check the relationship if the No Relation Validation checkbox is checked
    ; 06-Jun-2005	shobby/pk	Improved relationship check. SR7576
    ; 15-May-2005	Paul K		Added macro usage
    ; 12-May-2004   lw   		Created
    ;-------------------------------------------------------------------------------
    ;
	new strStatus,intErrorCounter,class,id,ref,classYM,Global,objProperty
	
	set strStatus=1
	set intErrorCounter=0
	kill Result
 
	; Loop through all class definitions
	set class=$get(Level)
	for  {
		set class=$order(^WWW001(0,class))
		quit:(class="")
		quit:($extract(class,1,$length(Level))'=Level)
		set Global="^"_class
		
		; loop through all primary id definitions
		set id=""
		for  {
			set id=$order(^WWW002(0,class,id))
			quit:(id="")
			
			set objProperty=$get(^WWW002(0,class,id,1)) ; SR13916
			
			continue:+$$$WWW002NoRelationValidation(objProperty) ; SR13916
			
			set ref=$$$WWW002RelationClass(objProperty) ; SR13916
			continue:(ref="")
			continue:(ref="COMTempList")	; ignore these references as they are non-persistant
 
			if ($data(@Global)) {
				set strStatus=strStatus*$$RelDataCheck(class,ref,id,.Result,.intErrorCounter)
			}
		}
	}
	if strStatus=1 {
		set strStatus=2  ; Because this is a Standalone Check return 2 if Ok
	}
 
	quit strStatus
 
RelDataCheck(class,ref,id,Result,intErrorCounter)
    ;-------------------------------------------------------------------------------
	; Check all the data in a global
    ;
    ; Returns:
    ;
    ; History:
    ; 29-Nov-2005	RPW		SR13916: Modified to handle nulls and ()'s in the data
    ; 08-Jul-2005	RW	Code Check
    ; 12-May-2004   lw   Created
    ;-------------------------------------------------------------------------------
    ;
	new strStatus,refShared,refGlobal,GlobalNode,YKEY,refYM,refYKEY,test,Global,classYM,%i
	new RefLoop,strKey,strFirst,intLength,strPrimary
	
	set $ZTRAP="RelDataCheckErr"
 
	set strStatus=1
	set Global="^"_class
	set refGlobal="^"_ref
	set refYKEY=$$$WWW002RelationalPrimaryKeys($get(^WWW002(0,class,id,1)))
	set refShared=$$$WWW001SharedFile($get(^WWW001(0,ref,1)))		
	
	; Loop through all nodes
	set GlobalNode=$QUERY(@Global)
	while (GlobalNode'="") {
		; Get the YKEY from the global node id
		set strFirst=$piece(GlobalNode,")",1,$length(GlobalNode,")")-1) ; SR13916
		set intLength=$length(strFirst,"(") ; SR13916
		;if intLength=1 set intLength=$length(strFirst,"(")
		set YKEY=$piece(strFirst,"(",2,intLength) ; SR13916
		;set YKEY=$piece($piece(GlobalNode,")",1,$length(GlobalNode,")")-1),"(",2)
		
		; ignore partial matches
		if ((ref'="COMDCMEvent") || (YKEY'["*")) {
			set refYKEY=$$$WWW002RelationalPrimaryKeys($get(^WWW002(0,class,id,1))) ;todo shobby
			set classYM=$piece(YKEY,",",1)
			if (refShared) {
				set refYM=0
			} else {
				set refYM=classYM
			}
			;if class="SALCSTButtonText" if refGlobal="^SALCSTButton" if YKEY="0,""Exhibition"",8,""ES"",1" for l=1:1:100 hang 1
			if (refYKEY'="")&&($extract(refYKEY)'="$") { ; SR13916
				for RefLoop=1:1:$length(refYKEY,",") {
					set strKey=$piece(refYKEY,",",RefLoop)
					if '$find(strKey,"""") {
						if '$data(@strKey) {
							set $piece(refYKEY,",",RefLoop)=$piece(YKEY,",",RefLoop+1)
						}
					}
				}
				set refYM=refYM_","_refYKEY
			}
			set test=0
			for %i=id+1:-1:2 {
				; check to see if multi-part key
				set strPrimary=$piece(YKEY,",",%i,id+1) ; SR13916
				if (strPrimary="")&&(class="INPROPJOB") continue ; SR13916
 
				if strPrimary'="" {
					xecute "set test=$data("_refGlobal_"("_refYM_","_strPrimary_"))"
				}
				quit:(test)		; found one
			}
			if ('test) {
				set intErrorCounter=intErrorCounter+1
				set Result("Hash","Error No. "_$translate($justify(intErrorCounter,3)," ","0"))="Node '^"_class_"("_YKEY_")' has an invalid relation to '"_ref_"' in primary key "_id
				set strStatus=0
				quit:(intErrorCounter>=100)
			}
		}
 
		set GlobalNode=$QUERY(@GlobalNode) 
	}
 
	quit strStatus
 
RelDataCheckErr   ;Internal Tag
	; Runtime error
	new txtlist
	set $ZTRAP=""
	set txtlist=$listbuild("Com00096",class,ref,$ZERROR)
	set intErrorCounter=intErrorCounter+1
	set Result("Hash","Error No. "_intErrorCounter)=$$DecodeError^COMUtils(txtlist)
	set strStatus=0
	quit strStatus
	
]]></Routine>
</Export>