<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPChequeBookCancelled" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPChequeBookCancelled
#include FINConst
#include COMSYS
	
OnBeforeSave(YKEY="",YFELD="")
    ;-------------------------------------------------------------------------------
    ; Validate the record before saving
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 27-Jun-2006	JW		SR13755: Removed $$$Text
    ;  3-Mar-2006 	JW&SC	SR14036: Removed last change.
    ; 23-Feb-2006	SC		SR14036: Extra checking, enforce cancelled cheques same
    ; 							length as cheque book. ie. 0002 not in 001:009
    ; 14-Feb-2006	SC		SR13975: Modified error checking. Prevent save for
    ; 							posted cheques, allow for cancelled.
    ; 28-Jan-2006	SC		SR13975: Pass YKEY, allow AlreadyCancelled to skip if
    ; 							we're modifying a record.
    ; 27-Jan-2006	SC		SR13975: Add additional checking + error msgs for posted
    ; 							and cancelled cheques.
    ; 27-Jan-2006	SC		SR13975: Use TextCheque functions to handle alphanumeric
    ; 							cheques.
    ; 11-Jul-2005	shobby	SR12678: Created
    ;-------------------------------------------------------------------------------
    new idCheque,idPayment,objFINAPChequeBook,strStatus
	
	set objFINAPChequeBook = $$$GRIDYFELDContainer
	set strStatus = $$$OK	
 	set YRETVAL   = ""
	
	set idCheque=$$$FINAPChequeBookCancelledChequeNumber(YFELD)
	
	if $$TextChequeLessThan^FINAPChequeBook(idCheque,$piece(YKEY,",",2))                                    ||
	   $$TextChequeLessThan^FINAPChequeBook($$$FINAPChequeBookEndChequeNumber(objFINAPChequeBook),idCheque)    {
		set YRETVAL=$listbuild("Fin00835",idCheque)  ;%1 not in Cheque Book."
	
	} elseif '$$IsChequeUsed^FINAPChequeBook($piece(YKEY,",",1),idCheque) {
		;see if cheque num in FINAPChequeBookCancelled. 
		do IsChequeAlreadyCancelled(idCheque,YKEY) ;SR13975
		;see if cheque num free, so we can add to cancelled.
		;do IsChequeAlreadyUsed(idCheque,YKEY,"posted")
		
 		set idPayment = $$IsChequeAlreadyUsed(idCheque,YKEY,$$$EnumFINSYSSTATUSPosted) 
 		if idPayment '= "" {
	 		set YRETVAL=$listbuild("Fin00959",idCheque,idPayment)
 		}  ; "Cheque %1 posted in Supplier Payment batch %2.  Cancel must be performed from the Supplier Payment batch."
	}
 
	quit
 	
	
CancelCheque(pidFINAPPayment="",pintChequeNumber="")
    ;-------------------------------------------------------------------------------
    ; Cancels the check that was used in the Supplier Payment.
    ;
    ; Params:
    ;
    ; Returns: strStatus
    ;
    ; History:
    ; 10-Feb-2006	JW&SC	SR14036 Inc text cheque
    ; 11-Jul-2005	shobby	SR12678 Created
    ;-------------------------------------------------------------------------------
    new idAccount,idFirst,idIndex,objFINAPChequeBookCancelled,objFINAPPayment,strStatus
    
    set strStatus = $$$OK
        
	if (pidFINAPPayment'="") && (pintChequeNumber'="") {
		set objFINAPPayment = $get(^FINAPPayment(0,pidFINAPPayment,1))
		if $$$FINAPPaymentPaymentMethod(objFINAPPayment)=$$$EnumFINARBANKLINEMETHODCheque {
			set idAccount = $$$FINAPPaymentBankAccount(objFINAPPayment)
			if idAccount'="" {
				set idFirst = $order(^FINAPChequeBook(0,idAccount,$$IncTextCheque^FINAPChequeBook(pintChequeNumber,1)),-1)
				if idFirst'="" {
					if '$data(^FINAPChequeBookCancelleds(0,1,pintChequeNumber,idAccount,idFirst)) {
						set idIndex=$order(^FINAPChequeBookCancelled(0,idAccount,idFirst,""),-1)+1
						set $$$FINAPChequeBookCancelledChequeNumber(objFINAPChequeBookCancelled)=pintChequeNumber
						set strStatus = $$$Save("FINAPChequeBookCancelled",idAccount_","_idFirst_","_idIndex,objFINAPChequeBookCancelled,$$$YES)
					}
				}
			}
		}	
	}
	quit strStatus
	
	
IsChequeAlreadyUsed(pidCheque,pYKEY,penumStatus)
 
    ;-------------------------------------------------------------------------------
    ; Checks if Cheque Number is already used in a FIN AP Payment Line.
    ; If so, we can't add it to the cancel list. Sets error message for the grid if neccessary.
    ;
    ; Params:
    ; 	Cheque Number,
    ; 	ChequeBookCancelled key,
    ; 	enum Cheque Status ("cancelled" or "posted").
    ;
    ; ByRefs:
    ;
    ; Returns: null if not used, FINAPPayment Number if posted or cancelled.
    ; 
    ;
    ; History:
    ; 14-Feb-2006	SC		SR13975: Created new index to speed up searching.
    ; 27-Jan-2006	SC		SR13975: Created
    ;-------------------------------------------------------------------------------
    new idPayment,idPaymentLine,idBankAccount,idFirstCheque,idIndex,objChequeBookUsed
	
	set idPayment=""
	
	set idBankAccount = $$$KEY1(pYKEY)
	set idFirstCheque = $$$KEY2(pYKEY)
	
	set idIndex = $order(^FINAPChequeBookUseds(0,1,pidCheque,idBankAccount,idFirstCheque,""),-1)
	
	if idIndex '= "" {
		set objChequeBookUsed = $get(^FINAPChequeBookUsed(0,idBankAccount,idFirstCheque,idIndex,1))
		set idPayment = $$$FINAPChequeBookUsedPaymentNumber(objChequeBookUsed)
		set idPaymentLine = $$$FINAPChequeBookUsedPaymentLine(objChequeBookUsed)		
	}	
	
	if idPayment'="" {
		;cheque number is already used  
		if ($$$FINAPPaymentLineStatus($get(^FINAPPaymentLine(0,idPayment,idPaymentLine,1)))=penumStatus) {
			;used & posted
		} else {
			set idPayment=""	// incorrect status
		}
	} 
	
	;cheque not used
	quit idPayment
	
	
		;is cheque used or cancelled?
		;if ($$$FINAPPaymentStatus($get(^FINAPPayment(0,idPayment,1)))=$$$EnumFINSYSSTATUSPosted) && ($$$FINAPPaymentLineStatus($get(^FINAPPaymentLine(0,idPayment,idPaymentLine,1)))=$$$EnumFINSYSSTATUSPosted) {
			;on save;set YRETVAL=$$$Text($listbuild("Fin00959",pidCheque,idPayment)) ;Cheque %1 posted in AP Payment batch %2.  Cancel must be perfromed from the AP Payment batch. 
		;} 
	
IsChequeAlreadyCancelled(pstrChequeNum,pYKEY)
    ;-------------------------------------------------------------------------------
    ; Checks to see if passed pintChequeNum is already manually cancelled.
    ; Sets error msg for the grid if neccessary.
    ;
    ; Params: Cheque Number, YKEY for updated ChequeBookCancelled line.
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 14-Feb-2006	SC		SR13975: Fix error checking from 2-Feb-06 below, match
    ; 							keys with pYKEY
    ; 02-Feb-2006	SC		SR13975: skip error if we find a match on the record
    ; 							we're updating.
    ; 30-Jan-2006	SC		SR13975: Created
    ;-------------------------------------------------------------------------------
 	new idBankAccount, idFirstCheque, idIndex, objChequeBookCancelled 
  
	set idBankAccount = $piece(pYKEY,",",1) 
	set idFirstCheque = $piece(pYKEY,",",2) 
	set idIndex = ""
	for {
		set idIndex = $order(^FINAPChequeBookCancelled(0,idBankAccount,idFirstCheque,idIndex))
		quit:idIndex=""
		
		set objChequeBookCancelled = $get(^FINAPChequeBookCancelled(0,idBankAccount,idFirstCheque,idIndex,1))
		
		if ($$$FINAPChequeBookCancelledChequeNumber(objChequeBookCancelled)=pstrChequeNum) && (idBankAccount_","_idFirstCheque_","_idIndex '= pYKEY){ ;SR13975 
			set YRETVAL=$$$Text($listbuild("Fin00961",pstrChequeNum)) ; "Cheque %1 already set to cancelled."
			quit
		}
	}
	
	quit
 
	
OnBlurChequeNumber(pstrCheque)
	;-------------------------------------------------------------------------------
	; Perform on blur from cheque number cell. If ChequeAlreadyUsed, and "cancelled" 
	; alert a warning.  Save still allowed.
	;
	; Params: Cheque Number.
	;
	; ByRefs:
	;
	; Returns: none
	;
	; History:
	; 14-Feb-2006	SC		SR13975: Created
	;-------------------------------------------------------------------------------
	new idPayment
 
 	if pstrCheque '="" {
 		set idPayment = $$IsChequeAlreadyUsed(pstrCheque,YKEY,$$$EnumFINSYSSTATUSCancelled) 
 		if idPayment '= "" {
	 		$$$Alert($listbuild("Fin00960",pstrCheque,idPayment)) ; "Cheque %1 cancelled in Supplier Payment batch %2."
 		}
 	}
 
	quit
 
	
OnAfterSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; After ChequeBookCancelled written, creates a record of the cheque in ChequeBookUsed.
	;
	; Params: ChequeBookCancelled key, field
	;
	; ByRefs:
	;
	; Returns: none
	;
	; History:
	; 21-Feb-2006	SC		SR14016: Validation checking on ChequeBookUsed write.
	; 14-Feb-2006	SC		SR13975: Created
	;-------------------------------------------------------------------------------
	new idBankAccount, idFirstCheque, idIndex, objBook, strStatus, idPayment
 
 	set idPayment = $$IsChequeAlreadyUsed($$$FINAPChequeBookCancelledChequeNumber(pYFELD),YKEY,$$$EnumFINSYSSTATUSCancelled)
	
	;don't need to populate ChequeBookUsed if already used in a Payment, or modifying a manually cancelled cheque.
	if (idPayment = "") && ($$$FINAPChequeBookCancelledCreatedDate(pYFELD)="") { 
 
		set $$$FINAPChequeBookUsedPaymentNumber(objBook) = ""
		set $$$FINAPChequeBookUsedPaymentLine(objBook) = ""
		set $$$FINAPChequeBookUsedChequeNumber(objBook) = $$$FINAPChequeBookCancelledChequeNumber(pYFELD)
		set $$$FINAPChequeBookUsedCancelledIndex(objBook) = $piece(pYKEY,",",3)
	 
		set idBankAccount = $piece(pYKEY,",",1)
		set idFirstCheque = $piece(pYKEY,",",2)
		set idIndex = $order(^FINAPChequeBookUsed(0,idBankAccount,idFirstCheque,""),-1) + 1
		
		set strStatus = $$$Save("FINAPChequeBookUsed",idBankAccount_","_idFirstCheque_","_idIndex,objBook,1)
	}
 
	quit
 
 
OnAfterDelete(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; delete FINAPChequeBookUsed entries if they exist
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Oct-2006	JW		SR15134: Look at kill status
	; 23-Feb-2006	SC		SR14036: Use Kill Wrapper to maintain correct indexes.
	; 23-Feb-2006	SC		SR13975: Created
	;-------------------------------------------------------------------------------
	new idBankAccount,idFirstCheque,strChequeNumber,idIndex,objUsed,strStatus

	set idBankAccount = $piece(pYKEY,",",1)
	set idFirstCheque = $piece(pYKEY,",",2)
	set strChequeNumber = $$$FINAPChequeBookCancelledChequeNumber(pYFELD)
	
	set strStatus = $$$OK
	
	if strChequeNumber '="" {
		set idIndex = $order(^FINAPChequeBookUseds(0,1,strChequeNumber,idBankAccount,idFirstCheque,""),-1)
		if idIndex '="" {
			set objUsed = $get(^FINAPChequeBookUsed(0,idBankAccount,idFirstCheque,idIndex,1))
			if $$$FINAPChequeBookUsedCancelledIndex(objUsed) = $piece(pYKEY,",",3) {
				set strStatus = $$$Kill("FINAPChequeBookUsed",idBankAccount_","_idFirstCheque_","_idIndex)
			}
		}
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontDelete
	}
	
	quit strStatus
	
]]></Routine>
</Export>