<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPDCM" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPDCM  ; Suppliers DCM
	; 08-Jul-2003	SCR			Bug Fixes from Code Checking
 	#Include FINConst
	#Include FIN
	
Supplier(pidSupplier		,pstrName			,pidControl,		// 1-3
		pstrBankBSB			,pstrBankNumber		,pstrBankName,		// 4-6
		pstrTerm			,pintGrace			,pstrStreet,		// 7-9
		pstrPOBox			,pstrZipCodeStreet	,pstrZipCodePOBox,	// 10-12
		pstrCity			,pstrCountry		,pstrState,			// 13-15
		pstrCurrency		,pstrPaymentMethod	,pstrGroup,			// 16-18
		pidTaxCode			,pfltExchangeRate	,pstrTaxLocation,	// 19-21
		pidDefaultTaxCode	,pstrTaxReporting	,pstrTaxGroup,		// 22-24
		pidCompanyNumber	,pstrTaxNumber		,pidStatus,			// 25-27		SR15453
		pstrContact			,pidDiscAccount		,pidRemitTo)		// 28-30
	;-------------------------------------------------------------------------------
	; Save Suppplier from DCM
	;
	; Returns:
	;
	; History:
	; 23-Feb-2007	JW			SR15453: Changed inactive date to status
	; 30-Nov-2006	JW			SR14945: Removed dodgy location calculation
	; 20-Nov-2006	JW			SR14945: Added check for COMTaxLocation data
	; 25-Sep-2006	JW			SR15038: Added idRemitTo
	; 20-Sep-2006	JW			SR14612: Added PaymentMethod, cleaned up.
	; 30-Jan-2006	Steve S		SR13497: Added Discount GL Account, clean up
	; 12-Dec-2005	PO			SR13749: Use standard variable names
	; 11-Nov-2005	RPW			SR13749: Added Contact
	; 15-Aug-2005	JW			SR12290: Added inactive date
	; 21-Jun-2005	Steve S		SR10421: Added Tax Reporting/Tax Group/CompanyNumber/TaxNumber
	; 06-Apr-2005	JW			SR10722: Added tax location
	; 05-Apr-2005	RobertW		SR12008: Conversion Rate -> Invoice Matching Fixed Conversion Rate.
	; 30-Mar-2005	JW			SR11405: tax code -> Invoices include tax. Added location.
	; 17-Feb-2005	JW			SR11405: Added tax code
	; 19-Oct-2004	MS			SR10417 : Added Supplier Group (store in p20 of Fin Supplier)
	; 07-Oct-2004	Steve S		SR10423 : Added Payment on Hold check
	; 21-May-2004	SCR			Added Currency
	; 21-May-2004	SCR			Check for an existing Control account and keep it, if there is no new one.
	; 21-May-2004	SCR			Reinstated Terms and Grace days
	; 30-mar-2004	LW			Removed update to Supplier terms as BO & FIN not compatible
	; 13-Feb-2004	shobby		Street -> Country
	;-------------------------------------------------------------------------------
 	;
	; Save Suppplier from DCM
	new YFORM,objRec,strStatus
	set strStatus=$$$OK
	set objRec=""
	
	; <FIXME> Language text?
	if $get(pidSupplier)="" set strStatus=('$$$OK)_Y_"No Number"
	
	if $$$ISOK(strStatus) {
		set objRec=$get(^FINAPSupplier(YM,pidSupplier,1))
	
		if ($get(pidControl)="") {
			set pidControl=$$DefaultControl^FINAPSupplier(pidSupplier)
			if pidControl="" {
				set pidControl=$$$FINAPSupplierControlAccount(objRec)
			}
			if pidControl="" {
				set strStatus=('$$$OK)_Y_$$$Text($listbuild("Fin00198",pidSupplier)) ;Control Required for Supplier ´%1´
			}
		}
		if $$$ISOK(strStatus) {
			set $$$FINAPSupplierSupplierName(objRec)				=$get(pstrName)
			set $$$FINAPSupplierControlAccount(objRec)				=$get(pidControl)
			set $$$FINAPSupplierBankBSB(objRec)						=$get(pstrBankBSB)
			set $$$FINAPSupplierBankAccountNumber(objRec)			=$get(pstrBankNumber)
			set $$$FINAPSupplierBankName(objRec)					=$get(pstrBankName)
			set $$$FINAPSupplierDiscountTerm(objRec)				=$get(pstrTerm)
			set $$$FINAPSupplierGraceDays(objRec)					=$get(pintGrace)
			set $$$FINAPSupplierStreet(objRec)						=$get(pstrStreet)
			set $$$FINAPSupplierPOBox(objRec)						=$get(pstrPOBox)
			set $$$FINAPSupplierZipCodeStreet(objRec)				=$get(pstrZipCodeStreet)
			set $$$FINAPSupplierZipCodePOBox(objRec)				=$get(pstrZipCodePOBox)
			set $$$FINAPSupplierState(objRec)						=$get(pstrState)
			set $$$FINAPSupplierCity(objRec)						=$get(pstrCity)
			set $$$FINAPSupplierCountry(objRec)						=$get(pstrCountry)
			set $$$FINAPSupplierCurrency(objRec)					=$get(pstrCurrency)
			set $$$FINAPSupplierSupplierGroup(objRec)				=$get(pstrGroup)  		; SR10417
			set $$$FINAPSupplierExchangeRate(objRec)				=$get(pfltExchangeRate) ; SR11349
			//set $$$FINAPSupplierIssuesTaxInvoices(objRec)			=($get(pidTaxCode)'=0)	SR14945
			
			set $$$FINAPSupplierTaxGroup(objRec)					=$get(pstrTaxGroup) 	;SR10421
			set $$$FINAPSupplierTaxReportingRequired(objRec)		=$get(pstrTaxReporting) ;SR10421
			set $$$FINAPSupplierCompanyNumber(objRec)				=$get(pidCompanyNumber) ;SR10421
			set $$$FINAPSupplierTaxNumber(objRec)					=$get(pstrTaxNumber) 	;SR10421
			set $$$FINAPSupplierStatus(objRec)						=$get(pidStatus) 		;SR12290	SR15453
			set $$$FINAPSupplierContactPerson(objRec)				=$get(pstrContact)      ;SR13749
			set $$$FINAPSupplierDiscountGLAccount(objRec)			=$get(pidDiscAccount) 	;SR13497
			set $$$FINAPSupplierRemittancetoDifferentSupp(objRec)	=$get(pidRemitTo)		;SR15038
			
			/* SR14945
			if $get(pstrTaxLocation)="" {  // SR10722
				set pstrTaxLocation = $$GetTaxLocation^COMTAXLocation($get(pstrCountry),$get(pstrState),$get(pstrZipCodeStreet))
			}
			*/
			set $$$FINAPSupplierIssuesTaxInvoices(objRec)			=(pstrTaxLocation'="")&&($get(pidTaxCode)'=0)&&$data(^COMTAXLocation(YM,pstrTaxLocation))	//SR14945
			
			set $$$FINAPSupplierTaxLocation(objRec) = pstrTaxLocation
			set $$$FINAPSupplierDefaultTaxCode(objRec) = $get(pidDefaultTaxCode)
			
			/* SR14612	vvvvvvv
			; SR10423
			if $get(pstrPaymentMethod)=0 { ;supplier is on payment hold
				set $$$FINAPSupplierOnHold(objRec)=$$$YES	
			} else { ;not on payment hold
				set $$$FINAPSupplierOnHold(objRec)=$$$NO
			} */
			set $$$FINAPSupplierOnHold(objRec) = ($get(pstrPaymentMethod)=0)		 ; whether supplier is on payment hold
			set $$$FINAPSupplierMethodofPayment(objRec) = $get(pstrPaymentMethod)
			// SR14612	^^^^^^^
			
			set strStatus=$$$Save("FINAPSupplier",pidSupplier,objRec,$$$YES)
 
		}
	}
	
	quit strStatus
	
 
SupplierGroupConditions(SuppliersNumber,Conditions)
    ;-------------------------------------------------------------------------------
    ; Transfers the Suppliers Discount terms from Back Office to Finance.
    ;
    ; Returns:
    ;
    ; History:
    ; 30-Aug-2004		shobby			Disabled.  Discount Term should come from Standard condition on INLIEF not INLIEFK
    ; 29-Aug-2004		shobby			Created (SR7432)
    ;-------------------------------------------------------------------------------
	;
	new strStatus,objFINAPSupplier,idFINAPSupplier
	
	set strStatus=$$$OK
	set idFINAPSupplier=$piece(SuppliersNumber,YKOMMA,1)
	if idFINAPSupplier="" set strStatus=('$$$OK)_Y_"No Number"
	if strStatus {
		set objFINAPSupplier=$get(^FINAPSupplier(YM,idFINAPSupplier,1))
		set $$$FINAPSupplierDiscountTerm(objFINAPSupplier)=$get(Conditions)
		set strStatus=$$$Save("FINAPSupplier",idFINAPSupplier,objFINAPSupplier,$$$YES)
	}
 
	quit strStatus
	
	
DeleteSupplier(pidSupplier)
    ;-------------------------------------------------------------------------------
    ; Routine to attempt to delete a supplier in Finance when removed 
    ; from back office.
    ; 
    ; (Copied from FINARDCM)
    ;
    ; Returns:
    ;
    ; History:
    ; 27-Jun-2005	JW			(Peer review) Removed surplus code
    ; 20-Jun-2005	Steve S		Created SR12648
    ;-------------------------------------------------------------------------------
	new strStatus
	;set strStatus=$$$OK // JW
	
	if '$$$NoKey(pidSupplier) {
		set strStatus=$$CheckRecordsLinks^FINSYS(pidSupplier,$listbuild("FINAPPaymentLine","FINAPInvoice","FINAPADJLine"),"FINAPSupplier")
		
		;if strStatus'=$$$OK set strStatus=$$$NO // JW
		
		if $$$ISOK(strStatus) do KILL^FINSYS("FINAPSupplier",pidSupplier)
	}
 
	quit $$$OK ; Don't want event to block in queue if unsuccessful
]]></Routine>
</Export>