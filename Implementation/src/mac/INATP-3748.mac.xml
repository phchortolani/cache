<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INATP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INATP(YART) ;INATP;FIS;VORAUSSCHAU LAGERBESTAND EINES ARTIKELS (AVAILABLE TO PROMISE);04.05.2001  ; Compiled February 7, 2005 12:06:26
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		VORAUSSCHAU LAGERBESTAND EINES ARTIKELS (AVAILABLE TO PROMISE)
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 03-Jun-2010	GRF		SR17146: call "DD.MM.YYYY" wrapper for WWWDATE1
	; 21-Feb-2007	GRF		SR15234: Doco; cleanup; if-else; YCR
	; 28-Aug-2006	GRF		SR14948: Naked Reference; strBGCOLOR
	; 07.02.2005	FIS
	; 26.07.2004	FAN		25822;ATP BUTTON IN AUFTRAGPOSITON FORM
	; 04.05.2001	FIS
	;-------------------------------------------------------------------------------
	QUIT:YART=""
	; "Check Please! (Phantom Item)"      ;KEINE PHANTOM-ARTIKEL
	IF $PIECE($GET(^INART(YM,YART,1)),Y,26)=5 DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$PIECE($GET(^WWW101(0,"ARTIKEL-ART",SPRACHE,5,1)),Y,1)_")") QUIT
	
	SET %(YQUERY,"YKEY")=YART
	DO MANU^INDRPRUN
	QUIT
	
AUFP         //26.07.2004;FAN;25822;ATP BUTTON IN AUFTRAGPOSITON FORMULAR ;buff ATP within form 
	NEW AUF,POS,ART,ZEIT,ZWISCHEN
	
	QUIT:YKEY=""
	SET AUF=$PIECE(YKEY,",",1)                         QUIT:AUF=""
	SET POS=$PIECE(YKEY,",",2)                         QUIT:POS=""
	SET ART=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,4)    QUIT:ART=""
	QUIT:'$DATA(^INART(YM,ART,1))
	
	SET ZEIT     = $PIECE($GET(^INDRPITEM(YM,YLOCATION,ART,1)),Y,9)     ;ZEITVORSCHAU
	SET ZWISCHEN = $PIECE($GET(^INDRPITEM(YM,YLOCATION,ART,1)),Y,34)    ;ZWISCHENSUMME     ;subtotal 
	SET ^INDRPUSER(YM,YUSER,1)=Y_ART_Y_Y_1_Y_Y_ZEIT_Y_0_Y_ZWISCHEN      ;SPEICHERN AUSWAHL ;Save Selection 
	DO INATP(ART)
	QUIT 
	
	;  **** ALT ****
	
START ;
	NEW ACCESS,ARTMENGE,AUFMENGE,BESMENGE,RESMENGE,ABGMENGE,ATPMENGE,ANZWOCHE,ANZDURCH,WOCHE,TEXT
	
	SET ART=$GET(%(YQUERY,"YPARA"))
	QUIT:$GET(ART)=""
	QUIT:'$DATA(^INART(YM,ART))
	SET YTARGET=$GET(YTARGET)
	;SET YTARGET=$PIECE(YTARGET,"/",1)
	IF YTARGET="" SET YTARGET="FRAME2"
	SET ACCESS=$$^WWWACCESS($PIECE($GET(^WWW001(0,"INLIEF",1)),Y,2),$PIECE($GET(^WWW001(0,"INLIEF",1)),Y,5))
	SET $PIECE(YVOR,Y,24)=1
	
	;---------------------------------------
	;	D44			$$$INVORGNoOfMonthsForLeavingAver()
	;	D45			$$$INVORGWeeksForATPDisplay()
	;---------------------------------------
	
	SET ANZWOCHE=$PIECE($GET(^INVORG(YM,YM,1)),Y,45)  ;ANZAHL DER WOCHEN DIE ANGEZEIGT WERDEN SOLLEN ;Number the weekly who will should 
	IF +ANZWOCHE=0 SET ANZWOCHE=5
	SET ANZDURCH=$PIECE($GET(^INVORG(YM,YM,1)),Y,44)  ;ANZAHL MONATE FÜR BERECHNUNG DES DURCHSCHNITTLICHEN ABGANGS ;Number to 
	IF +ANZDURCH=0 SET ANZDURCH=6
	SET WOCHE=$$^WWWWEEK(+$HOROLOG,2)                 ;AKTUELLE WOCHE ;week 
	;DO ^WWWBACK
	;IF $PIECE($GET(^WWW012(0,YM,1)),Y,13)=1 DO RAHMEN^WWWTAB   ;BEC;23699;YM DURCH 0 ERSETZT;26.05.03
	SET TEXT=$$^WWWTEXT(32024)_" "_ART_" - "_$PIECE($GET(^INART(YM,ART,1)),Y,1)
	DO UEBER($$^WWWTEXT(32483)_" "_TEXT)
	KILL ^WWWSOR(YUSER)
	;SET ^WWWSOR(YUSER,WOCHE) = SATZ: 1=GESAMTBESTAND, 2=BESTELLT, 3=RESERVIERT, 4=AUFTRÄGE/ABGÄNGE
	SET ANZM=0
	SET YDDSATZ=0
	DO SORT
	DO ^WWWFRAME(2)  ;TABLE AN ;upon 
	DO KOPF
	IF $PIECE($GET(^INART(YM,ART,1)),Y,26)'=2         DO LIST
	IF ANZM=0  SET ^WWWSOR(YUSER,WOCHE)=0_Y_0_Y_0_Y_0 DO LIST
	DO ^WWWFRAME(1)  ;TABLE AUS ;out of 
	;WRITE "<BR>"
	DO UEBER($$^WWWTEXT(32496))
	SET YDDSATZ=1
	DO ^WWWFRAME(2)  ;TABLE AN ;upon 
	DO LIEF
	DO ^WWWFRAME(1)  ;TABLE AUS ;out of 
	;IF $PIECE($GET(^WWW012(0,YM,1)),Y,13)=1 DO RAHMENE^WWWTAB   ;BEC;23699;YM DURCH 0 ERSETZT;26.05.03
	KILL ^WWWSOR(YUSER)
	QUIT
	
SORT ;DATEN UND TERMINE AUFBEREITEN ;And 
	NEW AUF,POS,POS1,AUF1,LIEF,LFDAT,ABMENGE
	
	;+++++++++++++++++++++++++++++++++++++++
	;	AUF1		objINAUF		Order
	;	POS1		objINAUFP		Order Line
	;	WE			objINWEAUF		Orders Received?
	;	WE			objINWEAUFALT	
	;	AB1			objINAUFPA		
	;	POS01		objINRESERVIERT
	;+++++++++++++++++++++++++++++++++++++++
	
	SET ARTMENGE=$$^INARTMENGE(ART)
	SET AUFMENGE=$$^INAUFMENGE(ART)
	SET $PIECE(^WWWSOR(YUSER,WOCHE),Y,1)=ARTMENGE+AUFMENGE
	SET $PIECE(^WWWSOR(YUSER,WOCHE),Y,4)=AUFMENGE
	;OFFENE AUFTRÄGE SUCHEN ;seek 
	SET AUF=""
	FOR  SET AUF=$ORDER(^INAUF1(YM,AUF)) QUIT:AUF=""  DO
	. SET AUF1=$GET(^INAUF(YM,AUF,1))                      ;AUFTRAG ;order 
	. QUIT:$PIECE(AUF1,Y,2)=1                              ;EIGENAUFTRAG
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . ;
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))  ;AUFTRAGSPOSITION
	. . QUIT:POS1=""
	. . QUIT:$PIECE(POS1,Y,4)'=ART  ;NICHT DER GESUCHTE ARTIKEL ;Not the item 
	. . QUIT:$PIECE(POS1,Y,60)=1    ;QUIT WENN AUFTRAGSPOSITION AGESCHLOSSEN IST
	. . ;
	. . ;BESTELLUNGEN
	. . ;------------------------------------
	. . IF $PIECE(POS1,Y,7)=1 DO
	. . . IF $PIECE(POS1,Y,83)="" QUIT     ;KEINE BESTELLUNG GEDRUCKT ;no sales order 
	. . . ;
	. . . SET LIEF=$PIECE(POS1,Y,12)       ;LIEFERANT ;supplier 
	. . . QUIT:LIEF=""
	. . . QUIT:'$DATA(^INLIEF(YM,LIEF))
	. . . SET BESMENGE=$PIECE(POS1,Y,5)    ;BESTELLMENGE ;ordering quantity 
	. . . ;
	. . . ;SUCHEN BEREITS GELIEFERTE MENGE ;seek yet quantity 
	. . . IF $DATA(^INWEAUF(YM,AUF,POS)) DO
	. . . . NEW BET,LAP,DAT,WE
	. . . . SET BET=""
	. . . . FOR  SET BET=$ORDER(^INWEAUF(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . . . SET LAP=""
	. . . . . FOR  SET LAP=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . . SET DAT=""
	. . . . . . FOR  SET DAT=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP,DAT)) QUIT:DAT=""  DO
	. . . . . . . SET WE = $GET(^INWEAUF(YM,AUF,POS,BET,LAP,DAT,1))
	. . . . . . . SET BESMENGE=BESMENGE-$PIECE(WE,Y,4)
	. . . ;
	. . . IF BESMENGE'>0 QUIT  ;KEINE BESTELLUNG OFFEN ;no sales order vulnerable 
	. . . ;
	. . . ;SUCHEN BEREITS GELIEFERTE MENGE ;seek yet quantum 
	. . . IF $DATA(^INWEAUFALT(YM,AUF,POS)) DO
	. . . . NEW BET,LAP,DAT,WE
	. . . . SET BET=""
	. . . . FOR  SET BET=$ORDER(^INWEAUFALT(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . . . SET LAP=""
	. . . . . FOR  SET LAP=$ORDER(^INWEAUFALT(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . . . SET DAT=""
	. . . . . . FOR  SET DAT=$ORDER(^INWEAUFALT(YM,AUF,POS,BET,LAP,DAT)) QUIT:DAT=""  DO
	. . . . . . . SET WE=$GET(^INWEAUFALT(YM,AUF,POS,BET,LAP,DAT,1))
	. . . . . . . SET BESMENGE=BESMENGE-$PIECE(WE,Y,4)
	. . . ;
	. . . IF BESMENGE'>0 QUIT  ;KEINE BESTELLUNG OFFEN ;no sales order vulnerable 
	. . . ;
	. . . ;LIEFERDATUM
	. . . SET LFDAT=""
	. . . SET ABMENGE=0
	. . . IF $DATA(^INAUFPA(YM,AUF,POS,LIEF)) DO
	. . . . NEW AB,AB1
	. . . . SET AB=$ORDER(^INAUFPA(YM,AUF,POS,LIEF,""),-1) QUIT:AB=""  DO
	. . . . . SET AB1=$GET(^INAUFPA(YM,AUF,POS,LIEF,AB,1))
	. . . . . SET LFDAT=$PIECE(AB1,Y,23)               ;ANLIEFERTERMIN
	. . . . . IF LFDAT<$HOROLOG SET LFDAT=+$HOROLOG    ;BEREITS ÜBERFÄLLIG ;yet past due 
	. . . . . SET ABMENGE=ABMENGE+$PIECE(AB1,Y,16)     ;AB-MENGE
	. . . . . SET LFDAT=$$^WWWWEEK(LFDAT,2)
	. . . . . IF LFDAT>(WOCHE+ANZWOCHE) QUIT
	. . . . . IF '$DATA(^WWWSOR(YUSER,LFDAT))  SET $PIECE(^WWWSOR(YUSER,LFDAT),Y,2)=$PIECE(AB1,Y,16) QUIT
	. . . . . SET $PIECE(^WWWSOR(YUSER,LFDAT),Y,2)=$PIECE(^WWWSOR(YUSER,LFDAT),Y,2)+$PIECE(AB1,Y,16)  ;AB-MENGE SPEICHERN ;Save 
	. . . ;
	. . . IF ABMENGE<BESMENGE DO
	. . . . SET LFDAT=$PIECE(POS1,Y,83)
	. . . . IF LFDAT<$HOROLOG SET LFDAT=+$HOROLOG      ;BEREITS ÜBERFÄLLIG ;yet past due 
	. . . . IF $DATA(^INAUFPK(YM,AUF,POS,1)) SET LFDAT=$PIECE(POS1,Y,83)+$PIECE($GET(^INAUFPK(YM,AUF,POS,1)),Y,23)
	. . . . SET LFDAT=$$^WWWWEEK(LFDAT,2)
	. . . . IF LFDAT>(WOCHE+ANZWOCHE) QUIT
	. . . . IF '$DATA(^WWWSOR(YUSER,LFDAT))  SET $PIECE(^WWWSOR(YUSER,LFDAT),Y,2)=BESMENGE-ABMENGE QUIT
	. . . . SET $PIECE(^WWWSOR(YUSER,LFDAT),Y,2)=$PIECE(^WWWSOR(YUSER,LFDAT),Y,2)+BESMENGE-ABMENGE    ;BESTELLMENGE SPEICHERN ;ordering quantity Save 
	. . . ;
	. . . ;RESERVIERUNGEN
	. . . IF $DATA(^INRESERVIERT(YM,AUF,POS)) DO
	. . . . NEW AUF0,POS0,POS01
	. . . . SET AUF0=""
	. . . . FOR  SET AUF0=$ORDER(^INRESERVIERT(YM,AUF,POS,AUF0)) QUIT:AUF0=""  DO
	. . . . . SET POS0=""
	. . . . . FOR  SET POS0=$ORDER(^INRESERVIERT(YM,AUF,POS,AUF0,POS0)) QUIT:POS0=""  DO
	. . . . . . SET POS01=$GET(^INRESERVIERT(YM,AUF,POS,AUF0,POS0,1))
	. . . . . . SET RESMENGE=$PIECE(POS01,Y,1)-$PIECE(POS01,Y,2)
	. . . . . . IF LFDAT>(WOCHE+ANZWOCHE) QUIT
	. . . . . . IF '$DATA(^WWWSOR(YUSER,LFDAT))  SET $PIECE(^WWWSOR(YUSER,LFDAT),Y,3)=RESMENGE QUIT
	. . . . . . SET $PIECE(^WWWSOR(YUSER,LFDAT),Y,3)=$PIECE(^WWWSOR(YUSER,LFDAT),Y,3)+RESMENGE  ;RESERVIERTE MENGE SPEICHERN ;quantum Save 
	
	QUIT
	
LIST ;AUFBAU DER LISTE ;the list 
	NEW SATZ,GESMENGE,BESMENGE,RESMENGE,AUFMENGE,ATPMENGE,SUM,VON,BIS
	
	FOR XWOCHE=WOCHE:1:WOCHE+ANZWOCHE  DO
	. SET SATZ=$GET(^WWWSOR(YUSER,XWOCHE))
	. SET GESMENGE=$PIECE(SATZ,Y,1)
	. SET BESMENGE=$PIECE(SATZ,Y,2)
	. SET RESMENGE=$PIECE(SATZ,Y,3)
	. SET AUFMENGE=$PIECE(SATZ,Y,4)
	. SET ATPMENGE=((+GESMENGE)+(+BESMENGE))-((+RESMENGE)+(+AUFMENGE))
	. DO DRUCK
	. ;
	. ;BERECHNUNG DURCHSCHNITTLICHER ABGÄNGE
	. SET (SUM(1),SUM(2))=0
	. DO
	. . NEW MONAT,JAHR
	. . SET JAHR  = $$^WWWYEAR($HOROLOG)
	. . SET MONAT = $$^WWWMONTH($HOROLOG)
	. . FOR YII=1:1:ANZDURCH DO
	. . . SET MONAT=MONAT-1
	. . . IF MONAT<1 SET MONAT=12 SET JAHR=JAHR-1
	. . ;
	. . SET VON = $$DMY^WWWDATE1("01."_MONAT_"."_JAHR)  ; SR17146
	. ;
	. DO
	. . NEW MONAT,JAHR,TAG
	. . SET JAHR  = $$^WWWYEAR($HOROLOG)
	. . SET MONAT = $$^WWWMONTH($HOROLOG)-1
	. . IF MONAT<1 SET MONAT = 12 SET JAHR = JAHR-1
	. . SET TAG = $$^WWWMONTHDAYS($$DMY^WWWDATE1("01."_MONAT_"."_JAHR))  ; SR17146
	. . SET BIS = $$DMY^WWWDATE1(TAG_"."_MONAT_"."_JAHR)                 ; SR17146
	. ;
	. SET SUM(1)=$$^INUMS(VON,BIS,2,ART,2,1,,)
	. FOR YI=1:1      SET SUM(2)=SUM(2)+$PIECE(SUM(1),",",YI) QUIT:$PIECE(SUM(1),",",YI,999)=""
	. IF +ANZDURCH'=0 SET SUM(2)=$JUSTIFY(SUM(2)/ANZDURCH,0,0)
	. ;
	. SET $PIECE(^WWWSOR(YUSER,XWOCHE+1),Y,4)=SUM(2)
	. SET $PIECE(^WWWSOR(YUSER,XWOCHE+1),Y,1)=ATPMENGE
	
	QUIT
	
DRUCK ;ANZEIGEN DER POSITIONEN ;display the 
	;============================================
	new strBGCOLOR
	
	SET ANZM=ANZM+1
	WRITE "<TR>"
	WRITE YCR
	SET YDDSATZ=YDDSATZ+1
	; SR14948 28-Aug-2006
	set strBGCOLOR = ""
	if YDDSATZ#2=1 set strBGCOLOR = " BGCOLOR="_YWHITE
	
	;KALENDERWOCHE ;calendar week 
	;---------------------------------------
	WRITE "<TD NOWRAP VALIGN=TOP ALIGN=CENTER"
	;IF XWOCHE=WOCHE WRITE " BGCOLOR=LIGHTGOLDENRODYELLOW"
	;IF XWOCHE'=WOCHE IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	if XWOCHE=WOCHE {
		WRITE " BGCOLOR=LIGHTGOLDENRODYELLOW"
	} else {
		WRITE strBGCOLOR
	}
 
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $EXTRACT(XWOCHE,5,6)_"/"_$EXTRACT(XWOCHE,1,4)
	WRITE "&nbsp;"
	WRITE "</TD>"
	
	;GESAMTBESTAND
	;---------------------------------------
	WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	;IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	;WRITE ">"
	WRITE strBGCOLOR_">"       ; 28-Aug-2006
	WRITE "<FONT SIZE=2>"
	if GESMENGE<0 {
		WRITE "0"
	} else {
		WRITE $$^WWWZAHL(GESMENGE,0,$LENGTH($PIECE(GESMENGE,".",2)))
	}
	
	WRITE "&nbsp;"
	WRITE "</TD>"
	
	;BESTELLUNGEN
	;---------------------------------------
	WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	WRITE strBGCOLOR_">"       ; 28-Aug-2006
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWZAHL(BESMENGE,0,$LENGTH($PIECE(BESMENGE,".",2)))
	WRITE "&nbsp;"
	WRITE "</TD>"
	
	;RESERVIERT ;undemonstrative 
	;---------------------------------------
	WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	WRITE strBGCOLOR_">"       ; 28-Aug-2006
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWZAHL(RESMENGE,0,$LENGTH($PIECE(RESMENGE,".",2)))
	WRITE "&nbsp;"
	WRITE "</TD>"
	
	;AUFTRÄGE / DURCHSCHN. ABGÄNGE
	;---------------------------------------
	WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	WRITE strBGCOLOR_">"       ; 28-Aug-2006
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWZAHL(AUFMENGE,0,$LENGTH($PIECE(AUFMENGE,".",2)))
	WRITE "&nbsp;"
	WRITE "</TD>"
	
	;ATP MENGE ;ATP quantity
	;---------------------------------------
	WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	WRITE strBGCOLOR_">"       ; 28-Aug-2006
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWZAHL(ATPMENGE,0,$LENGTH($PIECE(ATPMENGE,".",2)))
	WRITE "&nbsp;"
	WRITE "</TD>"
	
	WRITE "</TR>",YCR
	QUIT
	
KOPF ;
	;KOPF ZEILE ;heading 
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=CENTER"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(385)  ;KALENDERWOCHE ;calendar week 
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=RIGHT"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32214)  ;GESAMTBESTAND
	IF $PIECE($GET(^INART(YM,ART,1)),Y,40)'="" WRITE "<BR>",$GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE($GET(^INART(YM,ART,1)),Y,40),1))
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP align=center"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32481)  ;BESTELLT
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP align=center"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32480)  ;RESERVIERT ;undemonstrative 
	WRITE " "
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP align=center"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32021)_"/<BR>"_"&Oslash;"_"-"_$$^WWWTEXT(32482)  ;AUFTRÄGE/ABGÄNGE
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP align=center"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(31407)_" "_$$^WWWTEXT(32483)  ;ATP
	WRITE "</TH>",YCR
	
	QUIT
	
ZEILE ;
	new strBGCOLOR
	
	WRITE "<TR>"
	SET YDDSATZ=YDDSATZ+1
	; SR14948 28-Aug-2006
	set strBGCOLOR = ""
	IF YDDSATZ#2=1 set strBGCOLOR = " BGCOLOR="_YWHITE
	
	FOR YI(3)=1:1:6 DO
	. WRITE "<TD NOWRAP VALIGN=TOP"
	. WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. WRITE "<FONT SIZE=2>"
	. WRITE "&nbsp;"
	. WRITE "</TD>",YCR
	
	WRITE "</TR>",YCR
	QUIT
	
LIEF ;LIEFERANTEN-LISTE
	NEW LIEF,LIEF1,LIEFERANT,LFKOND
	
	;+++++++++++++++++++++++++++++++++++++++
	;	LIEFERANT		objINLIEF		Supplier
	;	LFKOND			objINARTK		Supplier Conditions
	;+++++++++++++++++++++++++++++++++++++++
	
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=LEFT"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32022)  ;LIEFERANT ;supplier 
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=LEFT" 
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(371)    ;NAME
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=LEFT"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32493)  ;LIEFERZEIT ;purchasing lead time 
	WRITE "</TH>"
	
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=LEFT"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	DO             ;QM-PUNKTE
	. WRITE $$^WWWFELDNAME("INLIEF","D",121)
	. ;IF $DATA(^WWW0031(0,"INLIEF",121,SPRACHE,1)) IF $PIECE($GET(^WWW0031(0,"INLIEF",121,SPRACHE,1)),Y,1)'="" WRITE $PIECE($GET(^ (1)),Y,1)  QUIT
	. ;IF $DATA(^WWW003(0,"INLIEF",121,1))          IF $PIECE($GET(^WWW003(0,"INLIEF",121,1)),Y,1)'=""          WRITE $PIECE($GET(^ (1)),Y,1)  QUIT
	
	WRITE "</TH>",YCR
	
	SET LIEF=""
	SET LIEF=$$^INARTLIEF(ART,4)
	FOR YI=1:1 QUIT:$PIECE(LIEF,Y,YI,99)=""  DO
	. SET LIEF1=$PIECE(LIEF,Y,YI)
	. IF LIEF1="" SET LIEF="" QUIT
	. ;
	. WRITE "<TR>"
	. WRITE "<TD NOWRAP VALIGN=TOP"
	. IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. IF ACCESS=1 DO
	. . NEW YKEY,YFORM
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INLIEF&YKEY="_LIEF1
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""        ; "Select Data Record"
	. . WRITE " TARGET="_YTARGET
	. . WRITE ">"
	. ;
	. WRITE LIEF1
	. IF ACCESS=1 WRITE "</A>"
	. WRITE "&nbsp;",YCR
	. WRITE "</TD>"
	. ;
	. WRITE "<TD NOWRAP VALIGN=TOP"
	. IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. SET LIEFERANT=""
	. IF $DATA(^INLIEF(YM,LIEF1,1)) SET LIEFERANT=$GET(^INLIEF(YM,LIEF1,1))
	. IF ACCESS=1 DO
	. . NEW YKEY,YFORM
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INLIEF&YKEY="_LIEF1
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""
	. . WRITE " TARGET="_YTARGET
	. . WRITE ">"
	. ;
	. IF $PIECE(LIEFERANT,Y,8)'="" WRITE $PIECE(LIEFERANT,Y,8)_", "_$PIECE(LIEFERANT,Y,16)
	. IF ACCESS=1 WRITE "</A>"
	. WRITE "&nbsp;",YCR
	. WRITE "</TD>"
	. ;
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	. IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. SET LFKOND=""
	. IF $DATA(^INARTK(YM,ART,LIEF1,1)) SET LFKOND=$GET(^INARTK(YM,ART,LIEF1,1)) 
	. IF ACCESS=1 IF $PIECE(LFKOND,Y,1)'="" DO  ;INFO ATIKEL BEI LIEFEANT ;next to 
	. . NEW YARTIKEL,YURL
	. . SET YURL=$PIECE($GET(^INLIEF(YM,LIEF1,1)),Y,119)  ;URL DER ARTIKELBESTÄNDE ;URL the 
	. . QUIT:YURL=""
	. . SET YARTIKEL=$TRANSLATE($PIECE($PIECE(LFKOND,Y,1),"|",1)," ")
	. . WRITE YCR
	. . SET YURL=YURL_YARTIKEL
	. . WRITE "&nbsp;"
	. . WRITE "<A HREF="
	. . WRITE "http://"_YURL
	. . WRITE " TARGET=ARTIKELINFO"
	. . WRITE ">",YCR
	. . WRITE "<IMG SRC="_""""_YGIF_"left.gif"_""""_" BORDER=0 TITLE='"_$$^WWWTEXT(32196)_"'></A>"
	. . WRITE "&nbsp;&nbsp;"
	. ;
	. IF +$PIECE(LFKOND,Y,23)'=0 WRITE $PIECE(LFKOND,Y,23)_" "_$$^WWWTEXT(32283) ;TAGE
	. WRITE "&nbsp;"
	. WRITE "</TD>",YCR
	. ;
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	. IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. WRITE $PIECE(LIEFERANT,Y,121)  ;QM-PUNKTE
	. WRITE "&nbsp;"
	. WRITE "</TD>",YCR,"</TR>",YCR
	. ;
	. SET YDDSATZ=YDDSATZ+1
	
	IF LIEF="" DO
	. WRITE "<TR BGCOLOR="_YWHITE_">"
	. WRITE "<TD>&nbsp;</TD>"
	. WRITE "<TD>"
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWTEXT(46)          ;KEINE DATEN VORHANDEN ; "No Data Available"
	. WRITE "</FONT>"
	. WRITE "&nbsp;</TD>"
	. WRITE "<TD>&nbsp;</TD>"
	. WRITE "<TD>&nbsp;</TD>"
	. WRITE "</TR>",YCR
	
	QUIT
	
UEBER(TEXT) ;ÜBERSCHRIFT ;superscription 
	WRITE "<B>"
	WRITE "<NOBR>"
	WRITE "<FONT SIZE=2>"
	WRITE TEXT
	WRITE "</FONT>"
	WRITE "</NOBR>"
	WRITE "</B>"
	WRITE "<BR>",YCR
	QUIT
	
ARTHIST(YART)
	;-------------------------------------------------------------------------------
	;ARTIKELHISTORY  ;ACHTUNG EINSPRUNG ! ;estimation 
	;-------------------------------------------------------------------------------
	NEW idxItem,KUNDE,strBGCOLOR,YBIS,YVON
	
	QUIT:$GET(YART)=""
	
	SET YBIS=+$HOROLOG
	SET TAGE=+$PIECE($GET(^INVORG(YM,YM,1)),Y,88)  ;ANZAHL DER TAGE FÜR UMSÄTZE ; $$$INVORGNumberOfDaysToShowItemSal() 
	IF TAGE=0 SET TAGE=364
	SET YVON=+$HOROLOG-TAGE
	SET YBACK="INART,"
	SET YKEY=YART
	DO ^WWWBACK
	DO UEBER($$^WWWTEXT(32024)_" "_YART_" "_$PIECE($GET(^INART(YM,YART,1)),Y,1)_" ("_$$^WWWDATE(YVON)_" - "_$$^WWWDATE(YBIS)_")")
	DO ^WWWFRAME(2)
	DO KOPF2
	
	SET ANZM    = 0
	SET SUM     = 0
	SET YDDSATZ = 1
	
	SET ACCESS(1)=$$^WWWACCESS($PIECE($GET(^WWW001(0,"INAUF",1)),Y,2),$PIECE($GET(^WWW001(0,"INAUF",1)),Y,5))
	SET ACCESS(2)=$$^WWWACCESS($PIECE($GET(^WWW001(0,"INKUNDE",1)),Y,2),$PIECE($GET(^WWW001(0,"INKUNDE",1)),Y,5))
	SET YTARGET=$GET(YTARGET)
	;SET YTARGET=$PIECE(YTARGET,"/",1)
	IF YTARGET="" SET YTARGET="FRAME2"
	
	set idxItem = $$^WWWUMLAU(YART,1)
	IF YVON'=""   SET DAT=$ORDER(^INAUFPs(YM,6,idxItem,YVON),-1)   ;TYBD 21.08.2002
	
	FOR  SET DAT=$ORDER(^INAUFPs(YM,6,idxItem,DAT)) QUIT:DAT=""  QUIT:DAT]]YBIS  DO
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFPs(YM,6,idxItem,DAT,AUF))  QUIT:AUF=""  DO
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INAUFPs(YM,6,idxItem,DAT,AUF,POS))  QUIT:POS=""  DO
	. . . SET YFELD=$GET(^INAUFP(YM,AUF,POS,1))
	. . . QUIT:$PIECE($GET(^INAUF(YM,AUF,1)),Y,2)'=0       ;NUR KUNDENAUFTRÄGE ;only 
	. . . ;
	. . . ; SR14948 28-Aug-2006
	. . . set strBGCOLOR = ""
	. . . IF YDDSATZ#2=1 set strBGCOLOR = " BGCOLOR="_YWHITE
	. . . ;-----------------------------------------------
	. . . SET ANZM=ANZM+1
	. . . WRITE "<TR>"
	. . . ;-----------------------------------------------
	. . . ;AUFTRAGSDATUM
	. . . WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. . . ;IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . . ;WRITE ">"
	. . . WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. . . WRITE "<FONT SIZE=2>"
	. . . WRITE $$^WWWDATE(DAT)
	. . . WRITE "&nbsp;"
	. . . WRITE "</TD>"
	. . . ;
	. . . ;-----------------------------------------------
	. . . ;AUFTRAG ;order 
	. . . WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. . . WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. . . WRITE "<FONT SIZE=2>"
	. . . IF ACCESS(1)=1 DO
	. . . . NEW YKEY,YFORM,YBACK
	. . . . SET YBACK="INAUF,"
	. . . . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INAUFP&YKEY="_AUF_","_POS
	. . . . DO ^WWWCGI
	. . . . WRITE """"
	. . . . WRITE " TITLE="_""""_$$^WWWTEXT(374)_""""
	. . . . WRITE " TARGET="_YTARGET
	. . . . WRITE ">"
	. . . ;
	. . . WRITE AUF_"-"_POS
	. . . IF ACCESS(1)=1 WRITE "</A>"
	. . . WRITE "&nbsp;"
	. . . WRITE "</TD>"
	. . . ;
	. . . ;-----------------------------------------------
	. . . ;KUNDE ;customer 
	. . . WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. . . WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. . . WRITE "<FONT SIZE=2>"
	. . . SET KUNDE=$PIECE($GET(^INAUF(YM,AUF,1)),Y,1)
	. . . IF KUNDE'="" DO
	. . . . IF ACCESS(2)=1 DO
	. . . . . NEW YKEY,YFORM,YBACK
	. . . . . SET YBACK=""
	. . . . . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INKUNDE&YKEY="_KUNDE
	. . . . . DO ^WWWCGI
	. . . . . WRITE """"
	. . . . . WRITE " TITLE="_""""_$$^WWWTEXT(374)_""""
	. . . . . WRITE " TARGET="_YTARGET
	. . . . . WRITE ">"
	. . . . . QUIT
	. . . . WRITE KUNDE
	. . . . WRITE " "
	. . . . WRITE $PIECE($GET(^INKUNDE(YM,KUNDE,1)),Y,8)
	. . . . WRITE ", "
	. . . . WRITE $PIECE($GET(^INKUNDE(YM,KUNDE,1)),Y,16)
	. . . . IF ACCESS(2)=1 WRITE "</A>"
	. . . ;
	. . . WRITE "&nbsp;"
	. . . WRITE "</TD>"
	. . . ;
	. . . ;-----------------------------------------------
	. . . ;MENGE ;quantity 
	. . . WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. . . WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. . . WRITE "<FONT SIZE=2>"
	. . . WRITE $PIECE(YFELD,Y,5)
	. . . WRITE " "
	. . . IF $PIECE(YFELD,Y,40)'="" WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(YFELD,Y,40),1)),Y,1)
	. . . IF $PIECE(YFELD,Y,40)="" IF $PIECE($GET(^INART(YM,YART,1)),Y,40)'="" WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE($GET(^(1)),Y,40),1)),Y,1)
	. . . SET SUM=SUM+(+$PIECE(YFELD,Y,5))
	. . . WRITE "&nbsp;"
	. . . WRITE "</TD>"
	. . . ;
	. . . WRITE "</TR>",YCR
	. . . SET YDDSATZ=YDDSATZ+1
	
	;SUMMENZEILE
	; SR14948 28-Aug-2006
	set strBGCOLOR = ""
	IF YDDSATZ#2=1 set strBGCOLOR = " BGCOLOR="_YWHITE
	
	DO  ;IF SUM'=0 DO
	. WRITE "<TR>"
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. ;IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. ;WRITE ">"
	. WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. WRITE "<FONT SIZE=2>"
	. WRITE "&nbsp;"
	. WRITE "</TD>"
	. ;
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. WRITE "<FONT SIZE=2>"
	. IF SUM=0 WRITE $$^WWWTEXT(46)  ;KEINE DATEN VORHANDEN ;no on hand 
	. WRITE "&nbsp;"
	. WRITE "</TD>"
	. ;
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=LEFT"
	. WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. WRITE "<FONT SIZE=2>"
	. WRITE "&nbsp;"
	. WRITE "</TD>"
	. ;
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	. WRITE strBGCOLOR_">"       ; 28-Aug-2006
	. WRITE "<FONT SIZE=2><B>"
	. IF SUM'=0 WRITE $$^WWWZAHL(SUM,0,$LENGTH($PIECE(SUM,".",2)))
	. WRITE "&nbsp;"
	. WRITE "</B></TD>",YCR
	
	DO ^WWWFRAME(1)
	WRITE "<BR>"
	QUIT
	
KOPF2
	;-------------------------------------------------------------------------------
	;   KOPF ZEILE ;heading
	;-------------------------------------------------------------------------------
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=CENTER"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(124)     ; "Date "            ;DATUM
	WRITE "</TH>",YCR
	
	WRITE "<TH VALIGN=TOP NOWRAP ALIGN=RIGHT"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32234)   ; "Customer Order"   ;KUNDENAUFTRAG
	WRITE "</TH>",YCR
	
	WRITE "<TH VALIGN=TOP NOWRAP align=center"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32231)   ; "Customer"         ;KUNDE
	WRITE "</TH>",YCR
	
	WRITE "<TH VALIGN=TOP NOWRAP align=center"
	WRITE " BGCOLOR="_YDARKGRAY
	WRITE ">"
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(31407)   ; "Quantity"         ;MENGE
	WRITE " "
	WRITE "</TH>",YCR
	
	QUIT
]]></Routine>
</Export>