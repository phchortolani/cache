<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWACCESS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWACCESS(pstrChkAccess,pstrChkModule,pidUser,pblnNoAdmin)
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ZUGANG ERLAUBT ODER NICHT - Access permissions
	;	Does the user have authorisation to access X and Y
	;	
	;	Q:$$^WWWACCESS(BERECHTIGUNG,MODULLISTE,MITARBEITER)'=1
	;
	; Inputs : 
	;	pstrChkAccess		Authorisation list to check
	;	pstrChkModule		Module list to check
	;	pidUser				User (optional) defaults to YBED
	;	pblnNoAdmin			Normally sys-admin has special access. If this option is set
	;						to yes - they do not.
	;						NOTE: This special access only applies if the user is a
	;						SysAdmin and nothing else!
	;
	; ByRef :
	;
	;
	; Returns :		boolean - whether user has access
	;
	; History :
	; 24-Jun-2008	shobby	SRAdhoc:    Put $get around check of pstrChkAccess
	; 14-Aug-2007	shobby	SRBR014321: System Administrators who also have other permissions would lose the sysadmin priveleges
	; 02-Aug-2007	GM		SRBR014601: Created condition for new user access level("0 - Not Available").
	; 12-Jul-2007	shobby	SRBR014604: ReducedModuleAuthorisation would fail if nothing selected.
	; 									<space> does not equal empty string.
	; 28-Feb-2007	JW		SR15457: Fixed rewrite error.
	; 29-Nov-2006	JW		SR15205: Doco. Rewritten
	; 26-Aug-2005	GRF		Doco
	; 17-APR-2005 	TYBD 	NEW PARAMETER "NOADMIN" TO OVERWRITE THE ADMINRIGHTS FOR ALL OK
	; 15.11.2000	DT
	;-------------------------------------------------------------------------------
	new blnAccess,strUserAccess,strUserModules,strCompanyModule
 
	quit:($get(pstrChkAccess)=0) $$$NO
	
	set blnAccess = $$$YES
	
		// Check version validity
	if $data(^WWW012V(0)) set blnAccess = $$CheckValidity()
	
	if blnAccess {
		set pstrChkAccess = $translate($get(pstrChkAccess),";",",")   ;Access levels check
		set pstrChkModule = $translate($get(pstrChkModule),";",",")  	;Module access check
		
		if $translate(pstrChkAccess,",")="" set pstrChkAccess = ""
		if $translate(pstrChkModule,",")="" set pstrChkModule = ""
		
		if $get(pidUser)="" set pidUser = YBED
		
		if (pstrChkAccess'="") || (pstrChkModule'="") { 	// Something to check
			set strUserAccess = $$^WWWBEDBER(pidUser)   	// Get user access levels
			
			if '$get(pblnNoAdmin) && (+strUserAccess=1) {	// Sys Admin override
				set blnAccess = $$$YES
				
			} else { // Check module authorisation
				if (pstrChkModule'="") {	// For company
					set strCompanyModule = $$$TRIMWS($translate($$$WWW012ReducedModuleAuthorizatio($get(^WWW012(0,YM,1))),";",","))
					if (strCompanyModule'="") {
						set blnAccess = $$MultiFind^COMUtilStr(pstrChkModule,strCompanyModule,",",",")
					}
					if blnAccess {			// For user
						set strUserModules=$$^WWWBEDMOD(pidUser)   ;Get user module access
						set blnAccess = $$MultiFind^COMUtilStr(pstrChkModule,strUserModules,",",",")
					}
				}
				
					// Check user access level authorisation
				if blnAccess && (pstrChkAccess'="") && ($translate(pstrChkAccess,",")'=99) {
					set blnAccess = $$MultiFind^COMUtilStr(pstrChkAccess,strUserAccess,",",",")
				}
			}
		}
	}
	quit blnAccess
 
 
CheckValidity()
	;-------------------------------------------------------------------------------
	; Check whether the current company's version has expired
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:	boolean
	;
	; History:
	; 01-Dec-2006	JW		SR15205: Encapsulated
	;-------------------------------------------------------------------------------
	new blnAccess,dteValidUntil,objVersion
	
	set blnAccess  = $$$YES
	set objVersion = $get(^WWW012V(0,YM,1))
	
	if $$$WWW012VActive1(objVersion) {							// Company has invalid version
		set blnAccess = $$$NO
		
	} else {
		set dteValidUntil = $$$WWW012VValidUntil(objVersion)
		if (dteValidUntil'="") && (dteValidUntil<$horolog) {	// Company's version has expired
			set blnAccess                           = $$$NO
			set $$$WWW012VActive1(^WWW012V(0,YM,1)) = $$$YES	// Set to invalid
		}
	}
	quit blnAccess
	
]]></Routine>
</Export>