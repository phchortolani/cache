<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW001" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW001
	
#include COMSYS
#include COMConst
#include WWWConst
	
GetClassDescription(pidClass="")
	;-------------------------------------------------------------------------------
	; Returns the class description taking language in to account.
	; Global variable SPRACHE is assumed.
	;
	; Params: pidClass - Id of the class.
	;
	; ByRefs:
	;
	; Returns: Class Description
	;
	; History:
	; 10-Oct-2006	shobby	SRBR014207: Created 
	;-------------------------------------------------------------------------------
	new strDescription,objWWW0011,objWWW001
	
	set strDescription = ""
	if pidClass'="" {
		if $get(SPRACHE)'="" {
			set objWWW0011     = $get(^WWW0011(0,pidClass,SPRACHE,1))
			set strDescription = $$$WWW0011ClassDescription(objWWW0011)
		}
		if strDescription="" {
			set objWWW001      = $get(^WWW001(0,pidClass,1))
			set strDescription = $$$WWW001ClassDescription(objWWW001)
		}
	}
	quit strDescription
	
 	
OnAfterDataFields(YKEY)
	;-------------------------------------------------------------------------------
	; OnAfterDataFields - called by the form.
	;
	; Params: key
	;
	; ByRefs:
	;
	; Returns: none
	;
	; History:
	; 08-Nov-2007	GRF		SR15611: Added ShowForms
	; 22-Jan-2007	PO		SR15316: Show hooks on callback page
	; 10-Apr-2006	SC		SR14414: Show callbacks on tab 3. 
	;-------------------------------------------------------------------------------
	if YSEITE=1 {
		do ShowCalcFields^WWW001CalcFields(YKEY)	;general tab
		do ShowForms(YKEY)
	
	} elseif YSEITE=3 {
		do ShowHooks(YKEY)
		do ShowVARHooks(YKEY)
	}
	quit
	
	
ShowHooks(pidClass)
	;-------------------------------------------------------------------------------
	; Displays all hooks for a given class.
	;
	; Params: pidClass
	;
	; Returns: Nothing
	;
	; History:
	; 22-Jan-2007	PO		SR15316: Created
	;-------------------------------------------------------------------------------
	new idHookEvent, objHook
	if '$$$NoKey(pidClass) {
		do Start^COMTable($listbuild($$$StrWWW001HookEvent,$$$StrWWW001HookCode,$$$StrWWW001HookDescription),
		                  $$^WWWFORMNAME("WWW001Hook")_":")
		set idHookEvent = ""
		for {
			set idHookEvent = $order(^WWW001Hook(0,pidClass,idHookEvent))
			quit:idHookEvent=""

			do NewLine^COMTable()
			set objHook = $get(^WWW001Hook(0,pidClass,idHookEvent,1))
			do InsertCell^COMTable($$$WWW101Text($get(^WWW101(0,"WWWEVENTTYPE",SPRACHE,idHookEvent,1))),"WWW001Hook",pidClass_","_idHookEvent)
			do InsertCell^COMTable($$$WWW001HookCode(objHook),"WWW001Hook",pidClass_","_idHookEvent)
			do InsertCell^COMTable($$$WWW001HookDescription(objHook),"WWW001Hook",pidClass_","_idHookEvent)		
			do EndLine^COMTable()
		}
		do Stop^COMTable()
	}
	quit
	
	
ShowVARHooks(pClass) // 
	;-------------------------------------------------------------------------------
	; Displays all client callbacks (VARHooks) for a given class.
	;
	; Params: pClass - Class Name. Show callbacks for this class.
	;
	; ByRefs:
	;
	; Returns: none.
	;
	; History:
	; 22-Jan-2007	PO		SR15316: Renamed from ShowCallbacks
	; 10-Apr-2006	SC		SR14414: Created
	;-------------------------------------------------------------------------------
	new VARHookEvent, objVARHook
	if pClass '= "" {
		do Start^COMTable($listbuild($$$StrWWW001VARHooksEvent,$$$StrWWW001VARHooksCode,$$$StrWWW001VARHooksDescription),$$^WWWFORMNAME("WWW001VARHooks")_":")
		set VARHookEvent = ""
		for {
			set VARHookEvent = $order(^WWW001VARHooks(0,pClass,VARHookEvent))
			quit:VARHookEvent=""
			
			do NewLine^COMTable()
			set objVARHook = $get(^WWW001VARHooks(0,pClass,VARHookEvent,1))
			do InsertCell^COMTable($$$WWW101Text($get(^WWW101(0,"WWWEVENTTYPE",SPRACHE,VARHookEvent,1))),"WWW001VARHooks",pClass_","_VARHookEvent)
			do InsertCell^COMTable($$$WWW001VARHooksCode(objVARHook),"WWW001VARHooks",pClass_","_VARHookEvent)
			do InsertCell^COMTable($$$WWW001VARHooksDescription(objVARHook),"WWW001VARHooks",pClass_","_VARHookEvent)		
			do EndLine^COMTable()
		}
		do Stop^COMTable()
	}
	quit
	
	
ShowForms(pidClass)
	;-------------------------------------------------------------------------------
	; Displays forms linked to a given class.
	;
	; Params: pidClass
	;
	; Returns: Nothing
	;
	; History:
	; 08-Nov-2007	GRF		SR15611: Created
	;-------------------------------------------------------------------------------
	new idForm,idxClass,objForm,strFormName
	
	if '$$$NoKey(pidClass) {
		set idxClass = $$$Index(pidClass)
		do Start^COMTable($listbuild($$$StrWWW120FormName,$$$StrWWW120FormHeaderOrImageFile),$$^WWWFORMNAME("WWW120")_":")
		set idForm = ""
		for {
			set idForm = $order(^WWW120s(0,1,idxClass,idForm))
			quit:idForm=""
		
			set objForm = $get(^WWW120(0,idForm,1))
			continue:$$$WWW120ClassUsedInForm(objForm)'=pidClass     ; distinguish between classes "INABC" and "INAbc"
			
			set strFormName = $$^WWWFORMNAME(idForm)
			
			do NewLine^COMTable()
			do InsertCell^COMTable(idForm,"WWW120",idForm)		
			do InsertCell^COMTable(strFormName,"WWW120",idForm)		
			do EndLine^COMTable()
		}
		do Stop^COMTable()
	}
	quit
	
	
IsUsable(YKEY,YFORM)
	;-------------------------------------------------------------------------------
	; Should a given Class show in a COMView for COMQuickSearchSetup
	; 
	; Params:	YKEY	- FINGLAccount id
	; 			YFORM	- Current form
	;
	; ByRefs:	nil
	;
	; Returns:	boolean - whether can use
	;
	; History:
	; 23-Jun-2006	RPW		SR12522: Created
	;-------------------------------------------------------------------------------
	new blnValid
	
	set blnValid = $$$YES
	
	if YFORM="COMQuickSearchSetup" {
		set blnValid = ($order(^WWW002(0,YKEY,""),-1)=1)
	}
	quit blnValid
 
  
OnBeforeDataAccess(YKEY="",YFORM="")
	;-------------------------------------------------------------------------------
	; Restricts the list of classes displayed in the list
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 10-Jul-2008	shobby	SRBR014965: Included $$$KEY1
	; 08-Jul-2008	shobby	SRBR014965: Defaults for parameters.
	; 03-Jul-2008	shobby	SRBR014965: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	if '$$$NoKey(YKEY) {
		if YFORM="WWW128Form" {
			set strStatus = $$$OK
			if YKEY = $$$KEY1(YKEYCONTAINER) {
				set strStatus = $$$NO
			
			} elseif '$data(^WWW128(0,$$$KEY1(YKEY))) {
				set strStatus = $$$NO
			}
		}
	}
	quit strStatus
	
]]></Routine>
</Export>