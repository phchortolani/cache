<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFBES" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFBES
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		DRUCKEN/WIEDERHOLEN ALLE BESTELLUNGEN
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 19-Jan-2007	GRF		SR12027: Dot levels; doco; quits
	; 28.07.2000	DT
	;-------------------------------------------------------------------------------
	NEW POS,AUFTRAG,WH,YLIEFERX,SATZ,YQ,YGESTSENDART
	
	SET VORG(1)=$GET(VORG(1))
	SET VORG(2)=$GET(VORG(2))
	SET VORG(3)=$GET(VORG(3))
	SET VORG(4)=$GET(VORG(4))  ;NUR FÜR VERSANDART AUSGEBEN ;only to 
	SET VORG(4)=$TRANSLATE(VORG(4),";",",")
	SET VORG(6)=$GET(VORG(6))    ;NO PRINT ;BEC;24.11.04
	IF $GET(VORG(1))="" IF $GET(VORG(2))=""  WRITE $$^WWWTEXT(32310) QUIT  ;KEINE AUSWAHL GETROFFEN ;no Selection 
	
	KILL ^WWWSOR(YUSER)
	;KILL WWWSOR(YUSER_"-SUM")   ;KILL TEMPFILE
	KILL ^WWWSOR(YUSER_"-SUM")   ;KILL TEMPFILE ;SR13246
	
	SET AUFTRAG=""
	FOR  SET AUFTRAG=$ORDER(^INAUF1(YM,AUFTRAG)) QUIT:AUFTRAG=""  DO  ;ALLE AUFTRÄGE
	. SET SATZ=$GET(^INAUF(YM,AUFTRAG,1))
	. SET YQ=0
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;BEC:PRÜFUNG SPÄTER IN DER INDRUCK
	. ;IF VORG(4)'="" DO   ;WEM;23713;15.03.2004;NUR ANGEGEBENE VERSANDARTEN AUSGEBEN
	. . IF $PIECE(SATZ,Y,15)'="" DO  QUIT
	. . . IF '$FIND(","_VORG(4)_",",","_$PIECE(SATZ,Y,15)_",") SET YQ=1
	. . ;
	. . SET YLIEFER=$PIECE(SATZ,Y,12) IF YLIEFER'="" IF '$FIND(","_VORG(4)_",",","_+$PIECE($GET(^INLIEF(YM,YLIEFER,1)),Y,15)_",") SET YQ=1
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	. ;
	. ;QUIT:YQ=1
	. QUIT:$PIECE(SATZ,Y,2)=1  ;KEINE EIGENAUFTRÄGE ;no 
	. ;QUIT:$PIECE(SATZ,Y,2)=0  ;KEINE KUNDENAUFTRÄGE;TYBD/BEC;WAR FALSCH;25856;4,6,2004
	. IF $GET(VORG(3))'="" IF $PIECE(SATZ,Y,6)'="" IF '$FIND(","_$TRANSLATE(VORG(3),";",",")_",",","_$PIECE(SATZ,Y,6)_",") QUIT  ;NICHT FÜR BETRIEB ;02.07.2002
	. ;PRUEFEN GEPLANTES BESTALLDATUM
	. IF $PIECE(SATZ,Y,248)'="" QUIT:$PIECE(SATZ,Y,248)>$HOROLOG  ;NOCH NICHT GEPLANT ;yet Not 
	. ;PRUEFEN WH
	. SET WH=""
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFP(YM,AUFTRAG,POS)) QUIT:POS=""  DO  QUIT:WH="X"
	. . IF +$PIECE($GET(^INAUFP(YM,AUFTRAG,POS,1)),Y,7)=0 SET WH="X"
	. ;
	. ;IF WH="X" QUIT  ;NICHT ALLE WH ERFASST
	. DO POSITIONEN
	
	DO DRUCK
	DO SUMMERY^INANGANF
	;KILL WWWSOR(YUSER_"-SUM")   ;KILL TEMPFILE
	KILL ^WWWSOR(YUSER_"-SUM")   ;KILL TEMPFILE ;SR13246
	QUIT
	
POSITIONEN      ;SUCHEN DER AUFTRAGSPOSITIONEN         ;seek the 
	;-------------------------------------------------------------------------------
	; Check order lines to see if they've been printed or not
	;
	; History:
	; 16-Oct-2006	Steve S		SR15077: Re-wrote to brace form
	;-------------------------------------------------------------------------------
	new POS,objINAUFP,objINAUF,idSupplier
	
	// NOTE: This code is called from the Print Order button, however it only really
	// 		 seems to be valid for Purchase Orders. So why is the button available
	//		 for customer orders?
	
	kill YAUFTRAG
	
	set objINAUF = $get(^INAUF(YM,AUFTRAG,1))
	
	set POS=""
	for {
		set POS = $order(^INAUFP(YM,AUFTRAG,POS))
		quit:(POS="")
		
		set objINAUFP = $get(^INAUFP(YM,AUFTRAG,POS,1))
		
		continue:+$$$INAUFPSource(objINAUFP)'=1		// not from order
		
		if ($get(VORG(1))=1)&&($$$INAUFPOrderPrintedDate(objINAUFP)="") {
			set YAUFTRAG(POS)=""
		}
		
		if ($get(VORG(2))'="")&&(+$$$INAUFPOrderPrintedDate(objINAUFP)=VORG(2)) {
			set YAUFTRAG(POS)=""
		}
	}
	
	if $data(YAUFTRAG) {	
		set POS=""
		for {
			set POS = $order(YAUFTRAG(POS))
			quit:(POS="")
			
			set idSupplier=""
			
			if $$$INAUFOrderType(objINAUF) = 2 { // should be 2 (Purchase Order)
				set idSupplier = $$$INAUFSupplierNumber(objINAUF)
			} else {
				set idSupplier = $$$INAUFPSupplier($get(^INAUFP(YM,AUFTRAG,POS,1)))
			}
			
			if idSupplier="" set idSupplier=" "
			
			set ^WWWSOR(YUSER,idSupplier,AUFTRAG,POS)=""
		}
	}
	
	quit
	
	/* SR15077: Commented -- rewrote above
	KILL YAUFTRAG
	SET POS="" FOR  SET POS=$ORDER(^INAUFP(YM,AUFTRAG,POS)) QUIT:POS=""  DO
	. QUIT:+$PIECE($GET(^INAUFP(YM,AUFTRAG,POS,1)),Y,7)'=1   ;keine Bestellung ;None Sales Order 
	. IF $GET(VORG(1))=1 IF +$PIECE($GET(^INAUFP(YM,AUFTRAG,POS,1)),Y,83)=0 SET YAUFTRAG(POS)=""           ;GESAMTDRUCK
	. IF $GET(VORG(2))'="" IF +$PIECE($GET(^INAUFP(YM,AUFTRAG,POS,1)),Y,83)=VORG(2) SET YAUFTRAG(POS)=""   ;WIEDERHOLUNGSDRUCK
	
	QUIT:'$DATA(YAUFTRAG)  ;KEINE Position ;no Line Item 
	;SORT NACH LIEFERANTEN ;within 
	SET POS="" FOR  SET POS=$ORDER(YAUFTRAG(POS)) QUIT:POS=""  DO
	. SET YLIEFER=""
	. IF $PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,2)=3 DO  ;fester lieferant ;provider 
	. . SET YLIEFER=$PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,12)  ;FESTER LIEFERANT aus auftragskopf  ;supplier out of 
	. ;
	. IF YLIEFER=""  DO
	. . SET YLIEFER=$PIECE($GET(^INAUFP(YM,AUFTRAG,POS,1)),Y,12)  ;aus auftragsposition ;out of 
	. ;
	. IF YLIEFER="" SET YLIEFER=" "
	. SET ^WWWSOR(YUSER,YLIEFER,AUFTRAG,POS)=""  ;SAVE NACH LIEFERANT ;within supplier 
	
	QUIT
	*/
	
DRUCK 
	;-------------------------------------------------------------------------------
	;	DRUCK DER BESTELLUNG ;printing the sales order
	;
	; History:
	; 16-Oct-2006	Steve S		SR15077: Show meaningful error for Purchase Orders
	;-------------------------------------------------------------------------------
	;Druckaufbereitung ;editing for printing 
	NEW YMULTIPLE,DRUCKOK
	SET DRUCKOK=0
	SET YCOPY=0
 
	SET YLIEFERX=""
	FOR  SET YLIEFERX=$ORDER(^WWWSOR(YUSER,YLIEFERX)) QUIT:YLIEFERX=""  DO
	. KILL YAUFTRAG1
	. SET YAUFTRAG1=""
	. FOR  SET YAUFTRAG1=$ORDER(^WWWSOR(YUSER,YLIEFERX,YAUFTRAG1)) QUIT:YAUFTRAG1=""  DO
	. . KILL YAUFTRAG
	. . SET YAUFTRAG=YAUFTRAG1
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^WWWSOR(YUSER,YLIEFERX,YAUFTRAG,POS)) QUIT:POS=""  DO
	. . . SET YAUFTRAG(POS)=""
	. . ;
	. . SET INFO=""
	. . IF YAUFTRAG'="" IF $PIECE($GET(^INAUF(YM,YAUFTRAG1,1)),Y,38)=1 SET INFO=$$^WWWTEXT(32047)_" "_$$^WWWTEXT(32285)_" "_$$^WWWTEXT(10004)
	. . SET YLIEFER=YLIEFERX
	. . SET YSEND=+$PIECE($GET(^INLIEF(YM,YLIEFER,1)),Y,15)  ;VERSANDART
	. . IF YSEND=1 IF $PIECE($GET(^INLIEF(YM,YLIEFER,1)),Y,24)="" SET YSEND=0
	. . IF YSEND=2 IF $PIECE($GET(^INLIEF(YM,YLIEFER,1)),Y,22)="" SET YSEND=0
	. . IF $GET(XSEND)'="" SET YSEND=XSEND
	. . SET YBELEG=3
	. . SET YMULTIPLE=$GET(YMULTIPLE)+1         ;MEHRERE AUFTRÄGE ;divers 
	. . IF YMULTIPLE>1 IF YSEND=0 DO FF^WWWW()  ;SEITENVORSCHUB
	. . ;
	. . IF VORG(2)'="" DO  ;WIEDERHOLUNGSDRUCK
	. . . SET YCOPY=1
	. . . SET YSEND=0
	. . ;
	. . IF VORG(2)="" SET YCOPY=0
	. . NEW YCOPY1
	. . SET YCOPY1=YCOPY
	. . IF $GET(VORG(4))'="" DO  QUIT     ;BEC;25783;25.05.04;BELEGART
	. . . NEW YISU,YSENDART
	. . . FOR YISU=1:1 QUIT:$PIECE(VORG(4),",",YISU,999)=""  DO
	. . . . SET YCOPY=YCOPY1
	. . . . SET YSTOP=""
	. . . . SET VORG(99)=""
	. . . . SET YSENDART=$PIECE(VORG(4),",",YISU)
	. . . . QUIT:YSENDART=""
	. . . . ;
	. . . . DO
	. . . . . SET YGESTSENDART=1       ;CHECK SENDART
	. . . . . DO ^INDRUCK
	. . . . . IF YCOPY'=1 SET YSEND=YGESTSENDART
	. . . . . SET YGESTSENDART=""
	. . . . ;
	. . . . IF +YSENDART'=0 IF VORG(6)=1 IF YCOPY'=1 IF YSEND'=0 SET VORG(99)=1    ;NO PRINT
	. . . . ;
	. . . . DO ^INDRUCK     ;DRUCK DER ANFRAGE ;printing the 
	. . . . DO ^INDRUCKZHD  ;DRUCK KOPIE ZU ANSPRECHENPATNER     ;FAN;13.03.2003;23059
	. . . . IF $GET(DRUCKOK)'=1 IF $GET(YSTOP)'=1 SET DRUCKOK=1    ;BEC;24.11.04;26820
	. . . . IF $GET(YSTOP)'=1 SET ^WWWSOR(YUSER_"-SUM",YAUFTRAG,1)=$GET(YAUFTRAG)_Y_$GET(YSEND)_Y_$GET(INFO)
	. . ;
	. . DO ^INDRUCK     ;DRUCK  DER BESTELLUNG ;printing the sales order 
	. . DO ^INDRUCKZHD  ;DRUCK KOPIE ZU ANSPRECHENPATNER     FAN;13.03.2003;23059 ;printing copy within buff 
	. . SET DRUCKOK=1
	. . IF $GET(YSTOP)'=1 SET ^WWWSOR(YUSER_"-SUM",YAUFTRAG,1)=$GET(YAUFTRAG)_Y_$GET(YSEND)
 
	if DRUCKOK=0 { // no order lines in ^WWWSOR
		new YFORM 
		do ^WWWBACK
		if ($$$INAUFOrderType($get(^INAUF(YM,AUFTRAG,1)))=2)&&($data(^INAUFP(YM,AUFTRAG))) { //Purchase order: SR15077
			write $$$Text($listbuild("IN00551",AUFTRAG)),"<BR>"  ;Order %1 has already been printed
		} else {	// old code
			write $$^WWWTEXT(46),"<BR>" //	no data available
		}
	}
	
 
	quit
	
AUFTRAG ; EINSPRUNG AUS AUFTRAG ;out of order 
	;-------------------------------------------------------------------------------
	; Print an order (checks for line items being open first)
	;
	; History:
	; 16-Oct-2006	Steve S		SR15077: Re-wrote to brace form
	;-------------------------------------------------------------------------------
	new AUFTRAG,WH,POS
	
	set AUFTRAG=$$$KEY1(YKEY)
	quit:AUFTRAG=""
	
	quit:$$$INAUFOrderType($GET(^INAUF(YM,AUFTRAG,1)))=1  //manufacturing
	
	set WH=""
	set POS="" 
	for {
		set POS=$ORDER(^INAUFP(YM,AUFTRAG,POS))
		quit:(POS="")
		quit:(WH="X")
		
		if +$$$INAUFPSource($GET(^INAUFP(YM,AUFTRAG,POS,1)))=0 {
			set WH="X"
		}
	}
	
	if WH="X" {
		write $$^WWWTEXT(32076)     ; "Some Line Items Open!"
	} else {
		set VORG(1)=1
		set VORG(2)=""
		set VORG(3)=""
		
		new XSEND
		set XSEND=0
		do POSITIONEN
		do DRUCK
	}
	
	quit
	
	/* SR15077: Commented -- rewrote above
	FOR  SET POS=$ORDER(^INAUFP(YM,AUFTRAG,POS)) QUIT:POS=""  DO  QUIT:WH="X"
	. IF +$PIECE($GET(^INAUFP(YM,AUFTRAG,POS,1)),Y,7)=0 SET WH="X"
	
	IF WH="X" WRITE $$^WWWTEXT(32076) QUIT  ;NICHT ALLE WH ERFASST ;Not 
	SET VORG(1)=1
	SET VORG(2)=""
	SET VORG(3)=""
	DO
	. NEW XSEND
	. SET XSEND=0
	. DO POSITIONEN
	. DO DRUCK
	
	QUIT
	*/
]]></Routine>
</Export>