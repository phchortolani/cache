<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMHOTKEY" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMHOTKEY
	
#include WWWConst
	;-------------------------------------------------------------------------------
	;		112		F1
	;		113		F2
	;		114		F3 ...
	;                   F5  (116)  Note : IE refresh overrides
	;                   F6  (117)  Note : Also performs IE operation (shift between frames and URL field)
	;  11 Prev Record   F7  (118)
	;  12 Next Record   F8  (119)
	;   9 Search        F9  (120)
	;   3 Save          F12 (123)  Note : Conflict with IE8
	;-------------------------------------------------------------------------------
	
CallBack()
	;-------------------------------------------------------------------------------
	; This routine is call when the javascript is created and creates javascript 
	; commands to build an array of codes to handle keydown events.  It is a bit 
	; indirect but ensures that every keypress does not call back to cache to determine
	; whether it should be handled or not.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-Jan-2011	shobby	SR17086: Created
	;-------------------------------------------------------------------------------
	new idCommand,intLine,objWWWFORMHOTKEY,strCommands
	
	set strCommands = ""
	set intLine = 0
	
	set idCommand = ""
	for {
		set idCommand = $order(^WWWFORMHOTKEY(0,idCommand))
		quit:idCommand=""
		
		set objWWWFORMHOTKEY = $get(^WWWFORMHOTKEY(0,idCommand,1))
		if $data(^WWWFORMHOTKEYD(0,idCommand,1)) {
			set objWWWFORMHOTKEY = $get(^WWWFORMHOTKEYD(0,idCommand,1))
		}
		set strCommands=strCommands_"arr['"_+$$$WWWFORMHOTKEYKey1(objWWWFORMHOTKEY)_"-"_
					+$$$WWWFORMHOTKEYAlt(objWWWFORMHOTKEY)_"-"_
					+$$$WWWFORMHOTKEYCtrl(objWWWFORMHOTKEY)_"-"_
					+$$$WWWFORMHOTKEYShift(objWWWFORMHOTKEY)_"']='"_idCommand_"';"_$$$CRLF
	}
	quit strCommands
	
	
JS()
	;-------------------------------------------------------------------------------
	; js : pruef()  : Check certain key presses - includes testing for disabled
	; Standard Buttons
	; 
	; Called By: WWWFORM8
	;
	; Returns:
	;
	; History:
	; 14-Feb-2011	shobby		SR17660:   Scope YPARA here.  We don't want to use
	;								it.  Only show popup help window if no field has
	;								the focus.
	; 01-Feb-2011	shobby		SR17086.1: Rework so that it can work with forms
	;								that have grids.
	; 28-Jan-2011	shobby		SR17086:   Some tweaks for Firefox.
	; 27-Jan-2011	shobby		SR17086:   Created ( Rewrite of code in WWWFORM8)
	;-------------------------------------------------------------------------------
	new blnReadOnly,strButtons,YPARA	;SR17660
	
	quit:((YFORM'="")    && ($$$WWW120NoEventkeyCheck($get(^WWW120(0,YFORM,1))) = $$$YES))
	quit:(($get(YM)'="") && ($$$WWW012NoEventkeyCheck($get(^WWW012(0,YM,1)))    = $$$YES))
	
	if ($get(YNOEVENTKEY)'=1) && ($$$WWW120FormType(YVOR)'=11) && ($$$WWW120FormType(YVOR)'=9) {
		$$$StartScript()
			write " window.onhelp= function() {return false;}; "
			
			;SR17086.1
			write "function mapKey(keyCode,altKey,ctrlKey,shiftKey) {",!
			write "  var arr=new Array();",!
			write $$CallBack^WWWFORMHOTKEY(),!
			write "	 return arr[(keyCode+0)+'-'+(altKey+0)+'-'+(ctrlKey+0)+'-'+(shiftKey+0)];",!
			write "}",!
			
			write "function pruef(wert) {",!
			write "  var blnResult=false;",!
			write "  wert=mapKey(window.event.keyCode,window.event.altKey,window.event.ctrlKey,window.event.shiftKey);",!
			write "  if (wert == 'BACK') { window.history.back(); }",!
			write "  else if ((wert == 'HELP')&&(window.event.srcElement.id=='')) { subWindow('"_YAKTION_"EP=WWWHELP&amp;YFORM="_YFORM_$$WWWCGI2^WWWCGI(1)_"&amp;YSEITE="_YSEITE_"','HELP"_YTARGET_"');}",! ;SR17660

			set strButtons=","_$translate($$$WWW120DoNOTDisplayStandardButto(YVOR),";",",")_","
			set blnReadOnly= (($get(YBEARB)=4) || ($$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly) || ($find(strButtons,",3,")))
			if 'blnReadOnly {
				write "  else if (wert == 'SAVE') {window.focus();document.WWW.YBUTTON.value='';document.WWW.YOPEN.value='0'; window.setTimeout('SAVENOW()',100);}",!
			} else {
				write "  else if (wert == 'SAVE') {window.alert(""F12 "_$$^WWWTEXT(144,,1)_"!"");}",!   ; "Not Possible"   ; NICHT MÃ–GLICH
			}

			if (YFORM'="")&&(($$$WWW120FormType($get(YVOR))=1) || ($$$WWW120FormType($get(YVOR))=3)) {
				if '$find(strButtons,",9,") write "  else if (wert == 'SEARCH') { ShowSearch(); }"		// COMView now always used.
				if ($$$WWW120FormType($get(YVOR))=1)&&('$get(YTIMEFORM)) {    ; Standard Form
					if '$find(strButtons,",12,") write "  else if (wert == 'PREV RECORD')  { window.location='"_$$DirectionURL2^WWWFORMF("BACK")_"'; }",!	
					if '$find(strButtons,",11,") write "  else if (wert == 'NEXT RECORD')  { window.location='"_$$DirectionURL2^WWWFORMF("NEXT")_"'; }",!
					if '$find(strButtons,",10,") write "  else if (wert == 'FIRST RECORD') { window.location='"_$$DirectionURL2^WWWFORMF("FIRST")_"'; }",!	
					if '$find(strButtons,",13,") write "  else if (wert == 'LAST RECORD')  { window.location='"_$$DirectionURL2^WWWFORMF("LAST")_"'; }",!
				}
			}
			write "  else { blnResult=true;",!
			write "    if (typeof(pruefg)=='function') {"
			if $get(YUSERAGENT)="CHROME" {
				write "       	pruefg(window.event.keyCode);"
			}else{
				write "       var data = new Date();"
				write "       var seg = data.getSeconds();"
				write "       if (!document.getElementById('focusfield').dataHora) {"
				write "       	pruefg(window.event.keyCode);"
				write "       }else{"
				write "       	var objData = document.getElementById('focusfield').dataHora;"
				write "       	if (objData!=seg) {"
				write "    		   	pruefg(window.event.keyCode);"
				write "       	}"
				write "       }"
				write "       document.getElementById('focusfield').dataHora = seg;"
			}
			write "    }",!
			write " }"
			write " return blnResult;"
			write " };"
			$$$EndScript()

		;if $get(YUSERAGENT)="MSIE" {
			$$$StartScript("for=document event='onkeydown()'")
			write " pruef(window.event.keyCode); "
		
		;} else {
			;$$$StartScript()
		;}
		
		/*&JS<
		function keyEvent(e) {
		   if (!(document.all)) {pruef(e.which)}
		   return true;
		}
		document.onkeyup = keyEvent;
		>*/
		$$$EndScript()
	}
	quit
	
	
OnBeforeSave(pidFunction,pobjHotKeys)
	;-------------------------------------------------------------------------------
	; Called By : Form WWWFORMHOTKEYD (Customisation) - to show main configuration
	; 
	; ByRef : Q
	; 
	; History:
	; 03-Mar-2011	GRF		SR17086: Created
	;-------------------------------------------------------------------------------
	new blnFound,idCommand,idxAlt,idxCtrl,idxKey,idxShift,strStatus
	
	set Q = $$$QSave
	
	set idxAlt    = $$$Index($piece(pobjHotKeys,Y,2))
	set idxCtrl   = $$$Index($piece(pobjHotKeys,Y,3))
	set idxShift  = $$$Index($piece(pobjHotKeys,Y,4))
	set idxKey    = $$$Index($piece(pobjHotKeys,Y,5))
	
	set blnFound  = $$$NO
	set idCommand = ""
	for {
		set idCommand = $order(^WWWFORMHOTKEYDs(0,1,idxAlt,idxCtrl,idxShift,idxKey,idCommand))
		quit:idCommand=""
		continue:idCommand=pidFunction
		
		set blnFound = $$$YES
	}
	if 'blnFound {
		set idCommand = ""
		for {
			set idCommand = $order(^WWWFORMHOTKEYs(0,1,idxAlt,idxCtrl,idxShift,idxKey,idCommand))
			quit:idCommand=""
			continue:idCommand=pidFunction
		
			set blnFound = $$$YES
		}
	}
	
	if blnFound {
		set strStatus = $$$MakeStatus("WWW00137")  ; "This key combination already exists"
		do ReturnError^COMUtilError(strStatus)
		set Q = $$$QDontSave		
	}
	quit
	
]]></Routine>
</Export>