<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASEncerrar" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASEncerrar
#include COMSYS

OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Carrega as informações para encerrar o processo, conforme a modalidade da compra.
    ; 
    ; History:
    ; 30-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new idModalidade
	set idModalidade = $$GetModalidade^VARSESCOMPRAS(pYKEY)
	
	if (idModalidade = 10) { ;10-PDPAS
		do OnAfterDataFieldsPDPAS(pYKEY)
	
	} else {
		do OnAfterDataFieldsRegular(pYKEY)
	}
	
	
	quit

OnAfterDataFieldsRegular(pYKEY)
	;-------------------------------------------------------------------------------
    ; Carrega os itens da compra para que possam ser encerrados
    ; 
    ; History:
    ; 30-Oct-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new idLinha, chave, objLinha, PAM, Produto, blnEncerrado, Motivo, Encerrado, strMotivo, readOnly,
		isChecked, beginStyle, endStyle, idFornecedorVencedor
 
	//Tabela
   	new lstHeader
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild(,"Item","Produto","PAM","Situação","Motivo do Encerramento")
  	
	set beginStyle = "<font style='line-height:24px'>"
	set endStyle = "</font>"  	
 	
 	kill ^VARSESTempDispEncerrarItem(YM,YBED)
 	
  	if $$Start^COMTable(lstHeader,"Selecione os itens da compra a serem encerrados:") {
 
		$$$Order3(^VARSESCOMPRASLinha,YM,pYKEY,idLinha)
			set chave = pYKEY_$$$COMMA_idLinha
			
			set objLinha     = $get(^VARSESCOMPRASLinha(YM,pYKEY,idLinha,1))
			set PAM	         = $piece(objLinha,Y,1)
			set Produto      = $piece(objLinha,Y,2)			
			set blnEncerrado = $piece(objLinha,Y,16)
			set Motivo		 = $piece(objLinha,Y,17)
			
			if blnEncerrado = "" set Encerrado = "Em Andamento"
			if blnEncerrado = 1  set Encerrado = "<font color=red>Encerrado</font>"
			
			set readOnly  = $$$NO
			set isChecked = $$$NO
			
			if (blnEncerrado = $$$YES) {
				set readOnly = $$$YES
				set isChecked = $$$YES
			}
			
			set idFornecedorVencedor = $$GetVencedorFornecedor^VARSESCOMPRASParecer(pYKEY,idLinha)
			if (idFornecedorVencedor '= "") {				
				set Encerrado = "<font color=blue>Vencedor Definido</font>"
				set readOnly = $$$YES
				set isChecked = $$$NO
			}			
			
			set strMotivo = $$GetMotivoDescricao(Motivo)
			
			do NewLine^COMTable()
			do InsertCellCheckbox^COMTable(chave,"SelecionarItem^VARSESCOMPRASEncerrar",isChecked,readOnly)
			do InsertCell^COMTable(beginStyle_idLinha_endStyle,"VARSESCOMPRASLinha",chave)
	    	do InsertCell^COMTable(beginStyle_Produto_" - "_$extract($$SQLGetDescProduto^VARSQL(Produto),1,50)_endStyle,"VARSESCOMPRASLinha",chave,,,,,,,1)
	    	do InsertCell^COMTable(beginStyle_PAM_endStyle,"VARPedidoCompra",PAM)
	    	do InsertCell^COMTable(beginStyle_Encerrado_endStyle)
	    	do InsertCell^COMTable(beginStyle_"<font color=red>"_strMotivo_"</font>"_endStyle)
			do EndLine^COMTable()
		$$$End
		
		do Stop^COMTable()
  	}
 
	quit


OnAfterDataFieldsPDPAS(pYKEY)
	;-------------------------------------------------------------------------------
    ; Carrega os itens da compra PDPAS para que possam ser encerrados
    ; 
    ; History:
    ; 30-Apr-2011 Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new idLinha, chave, objLinha, Produto, blnEncerrado, Motivo, Encerrado, strMotivo, readOnly,
		isChecked, beginStyle, endStyle, idFornecedorVencedor
 
	//Tabela
   	new lstHeader
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild(,"Item","Produto","Situação","Motivo do Encerramento")

	set beginStyle = "<font style='line-height:24px'>"
	set endStyle = "</font>"
 	
 	kill ^VARSESTempDispEncerrarItem(YM,YBED)
 	
  	if $$Start^COMTable(lstHeader,"Selecione os itens da  compra a serem encerrados:") {
 
		$$$Order3(^VARSESCOMPRASLinha,YM,pYKEY,idLinha)
			set chave = pYKEY_$$$COMMA_idLinha
						
			set objLinha     = $get(^VARSESCOMPRASLinha(YM,pYKEY,idLinha,1))
			set Produto      = $piece(objLinha,Y,2)			
			set blnEncerrado = $piece(objLinha,Y,16)
			set Motivo		 = $piece(objLinha,Y,17)
			
			if blnEncerrado = "" set Encerrado = "Em Andamento"
			if blnEncerrado = 1  set Encerrado = "<font color=red>Encerrado</font>"
			
			set readOnly  = $$$NO
			set isChecked = $$$NO
			
			if (blnEncerrado = $$$YES) {
				set readOnly = $$$YES
				set isChecked = $$$YES
			}
			
			set idFornecedorVencedor = $$GetVencedorFornecedor^VARSESCOMPRASParecer(pYKEY,idLinha)
			if (idFornecedorVencedor '= "") {				
				set Encerrado = "<font color=blue>Vencedor Definido</font>"
				set readOnly = $$$YES
				set isChecked = $$$NO
			}
			
			set strMotivo = $$GetMotivoDescricao(Motivo)
			
			do NewLine^COMTable()
			do InsertCellCheckbox^COMTable(chave,"SelecionarItem^VARSESCOMPRASEncerrar",isChecked,readOnly)
			do InsertCell^COMTable(beginStyle_idLinha_endStyle)
	    	do InsertCell^COMTable(beginStyle_Produto_" - "_$extract($$SQLGetDescProduto^VARSQL(Produto),1,50)_endStyle,,,,,,,,,1)
	    	do InsertCell^COMTable(beginStyle_Encerrado_endStyle)
	    	
	    	if (strMotivo '= "") {	    	
	    		do InsertCell^COMTable(beginStyle_"<font color=red>"_strMotivo_"</font>"_endStyle)
	    	} else {
		    	do InsertCell^COMTable("")
	    	}
	    	
			do EndLine^COMTable()
			
		$$$End
		
		do Stop^COMTable()
  	}
 
	quit

SelecionarItem(pYKEY="",pChecked="")
	;-------------------------------------------------------------------------------
    ; Salva a marcação ou desmarcação do usuário no checkbox com os itens da compra
    ; a serem encerrados.
    ; 
    ; History:
    ; 30-Oct-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	if ($piece(pChecked,",",1) = "true") {		
		set ^VARSESTempDispEncerrarItem(YM,YBED,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1) = ""
	
	} elseif ($piece(pChecked,",",1) = "false") {
		kill ^VARSESTempDispEncerrarItem(YM,YBED,$$$KEY1(pYKEY),$$$KEY2(pYKEY))
	}
	
	quit $$$YES	
	
OnAfterSave(pYKEY)
	quit:(pYKEY = "")
	
	new strStatus, motivo, idModalidade
	set motivo = $get(VORG(1))	
	
	set strStatus = $$$OK
	
	set idModalidade = $$GetModalidade^VARSESCOMPRAS(pYKEY)
	
	if (idModalidade = 10) { ;10-PDPAS
		set strStatus = $$Transaction^COMTransaction("OnAfterSaveTxnPDPAS^VARSESCOMPRASEncerrar("""_pYKEY_""","""_motivo_""")",$$$YES)
		
	} else {
		set strStatus = $$Transaction^COMTransaction("OnAfterSaveTxnRegular^VARSESCOMPRASEncerrar("""_pYKEY_""","""_motivo_""")",$$$YES)
	}
	
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	}
	
	quit
 
OnAfterSaveTxnRegular(pYKEY,pMotivo)
	if ((pYKEY = "") || (pMotivo = "")) set strStatus = "É necessário definir um processo de compra e/ou o motivo do encerramento"
	
	new idItem, chave, objLinha, PAM, PAMLinha, chavePAMCOMPRAS, objPAMCOMPRAS, isCompraEncerrada, objCompra
	
	$$$Order4(^VARSESTempDispEncerrarItem,YM,YBED,pYKEY,idItem)
		
		set chave = pYKEY_","_idItem
		
		set objLinha = $get(^VARSESCOMPRASLinha(YM,pYKEY,idItem,1))
		set $piece(objLinha,Y,16) = 1 			;Encerrado
		set $piece(objLinha,Y,17) = pMotivo 	;Motivo do Encerramento
		set $piece(objLinha,Y,18) = $horolog	;Encerrado Em
		set $piece(objLinha,Y,19) = YBED		;Encerrado Por
		
		set strStatus = $$$Save("VARSESCOMPRASLinha",chave,objLinha,1)
		
		set PAM 	 = $piece(objLinha,Y,1)
		set PAMLinha = $piece(objLinha,Y,20)
		
		set chavePAMCOMPRAS = PAM_","_PAMLinha_","_pYKEY_","_idItem
		
		set objPAMCOMPRAS = $get(^VARSESPAMCOMPRAS(YM,PAM,PAMLinha,pYKEY,idItem,1))
		set $piece(objPAMCOMPRAS,Y,2) = 2		;Status
 
		set strStatus = $$$Save("VARSESPAMCOMPRAS",chavePAMCOMPRAS,objPAMCOMPRAS,1)
	
	$$$End

	if $$$ISOK(strStatus) {					
		set isCompraEncerrada = $$checkCompraEncerrada(pYKEY)
		
		if (isCompraEncerrada = $$$YES)  {
			set objCompra = $get(^VARSESCOMPRAS(YM,pYKEY,1))
			;set $piece(objCompra,Y,7) = 12 ;PositionHere -> 12 - Processo Encerrado
			set $piece(objCompra,Y,7) = 13
			set strStatus = $$$Save("VARSESCOMPRAS",pYKEY,objCompra,1)			
		}
	}
	
	quit strStatus
	
OnAfterSaveTxnPDPAS(pYKEY,pMotivo)
	if ((pYKEY = "") || (pMotivo = "")) set strStatus = "É necessário definir um processo de compra e/ou o motivo do encerramento"
	
	new idItem, chave, objLinha, isCompraEncerrada, objCompra, isCompraFinalizada, isParticipantesDefinidos
	
	$$$Order4(^VARSESTempDispEncerrarItem,YM,YBED,pYKEY,idItem)
		
		set chave = pYKEY_","_idItem
		
		set objLinha = $get(^VARSESCOMPRASLinha(YM,pYKEY,idItem,1))
		set $piece(objLinha,Y,16) = 1 			;Encerrado
		set $piece(objLinha,Y,17) = pMotivo 	;Motivo do Encerramento
		set $piece(objLinha,Y,18) = $horolog	;Encerrado Em
		set $piece(objLinha,Y,19) = YBED		;Encerrado Por
		
		set strStatus = $$$Save("VARSESCOMPRASLinha",chave,objLinha,1)
	
	$$$End
	
	if $$$ISOK(strStatus) {					

		set objCompra = $get(^VARSESCOMPRAS(YM,pYKEY,1))

		set isCompraEncerrada = $$checkCompraEncerrada(pYKEY)		
		if (isCompraEncerrada = $$$YES)  {			
			;set $piece(objCompra,Y,7) = 12 ;PositionHere -> 12 - Processo Encerrado
			set $piece(objCompra,Y,7) = 13
			set strStatus = $$$Save("VARSESCOMPRAS",pYKEY,objCompra,1)			
		
		} else {
		
			set isCompraFinalizada = $$checkCadastroParecer^VARSESCOMPRASParecer(pYKEY,objCompra)
			if (isCompraFinalizada = $$$YES) {
				set $piece(objCompra,Y,7) = 11 ;PositionHere -> 11 - Processo de Compra Finalizado
				set strStatus = $$$Save("VARSESCOMPRAS",pYKEY,objCompra,1)
												
			} else {				
				set isParticipantesDefinidos = $$GetExisteParticipante^VARSESCOMPRAS(pYKEY)
				if ( (isParticipantesDefinidos = $$$YES) && ($piece(objCompra,Y,7) = 5) ) { ;PositionHere -> 5 - Aguardando Selec. Participantes
					set $piece(objCompra,Y,7) = 7 ;PositionHere -> 7 - Aguardando Cadastrar Propostas
					set strStatus = $$$Save("VARSESCOMPRAS",pYKEY,objCompra,1)				
				}				
			}
		}	
	}
	
	quit strStatus	
	
checkCompraEncerrada(pYKEY)
	if pYKEY = "" quit ""
	
	new isCompraEncerrada, objLinha, idItem
	set isCompraEncerrada = $$$NO
	
	$$$Order3(^VARSESCOMPRASLinha,YM,pYKEY,idItem)
		
		set objLinha = $get(^VARSESCOMPRASLinha(YM,pYKEY,idItem,1))
		
		if $piece(objLinha,Y,16) = 1 {  ;Encerrado
			set isCompraEncerrada = $$$YES
		} else {
			set isCompraEncerrada = $$$NO
			quit	
		}
			
	$$$End
	
	quit isCompraEncerrada
 
 
GetMotivoDescricao(pMotivo)
	if pMotivo = "" quit ""
	quit $piece($get(^INPARA(YM,"VARSESCOMPRASENCERRAR",SPRACHE,pMotivo,1)),Y,1)
	
]]></Routine>
</Export>