<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VAROrdemUnitarizacaoEtapa" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VAROrdemUnitarizacaoEtapa
    #include COMSYS
    #include VARConst
    #include INConst
    #define EmAndamento 0
    #define Concluido 1
    
    quit
    
OnBeforeDelete(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Verifica se o usuário pode deletar uma linha (etapa) da ordem de unitarização.
    ;   Etapas que já estão com status = 1-Concluído não devem poder mais ser excluídos.
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------
    new idOrdemFrac, idEtapa, objEtapa, idUltimaEtapaDATEN
    
    set idOrdemFrac = $$$KEY1(pYKEY)
    set idEtapa     = $$$KEY2(pYKEY)
    
    set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,idOrdemFrac,idEtapa,1))   
    
    if ($$$VAROrdemUnitarizacaoEtapaSituacao(objEtapa) = $$$Concluido) {    
        $$$Alert("Esta etapa já foi concluída e não pode ser excluída.")
        set Q = $$$QDontDelete
        quit
    }
    
    ;Caso a entrega ainda esteja editável, precisa ser a última do GRID.
    set idUltimaEtapaDATEN = $$GetIDUltimaEtapa(YUSER)
    
    if (idEtapa < idUltimaEtapaDATEN) {
         $$$Alert("É preciso deletar as etapas posteriores na ordem antes de deletar esta.")
         set Q = $$$QDontDelete
         quit   
    } 
    
    quit
    
AtualizaStatusLinhas(pidOrdemFrac,pstrLinhasAConcluir,pidMOV="")
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Atualiza o status das linhas (etapas) da ordem de unitarização que foram
    ;   selecionadas pelo usuário.
    ;
    ; Chamado por:
    ;   Método ConcluirEtapaTxn^VAROrdemUnitarizacao.mac
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------    
    quit:( (pidOrdemFrac = "") || (pstrLinhasAConcluir = "") ) ""
    
    new strStatus, cnt, idEtapa, objEtapa, chave
    set strStatus = $$$OK
    
    for cnt = 1:1:$length(pstrLinhasAConcluir) {        
        set idEtapa = $piece(pstrLinhasAConcluir,";",cnt)
        quit:(idEtapa = "")
        
        set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))
        
        set $$$VAROrdemUnitarizacaoEtapaSituacao(objEtapa) = 1 ;Situação 1 = Concluído
        
        ;Caso tenha havido quantidade movimentada nesta linha, grava qual foi a movimentação
        if ($$$VAROrdemUnitarizacaoEtapaQuantSelEtiq(objEtapa) > 0) {
            set $$$VAROrdemUnitarizacaoEtapaCodMovimentacao(objEtapa) = pidMOV
        }
        
        set chave = pidOrdemFrac_$$$COMMA_idEtapa
        
        set strStatus = $$$Save("VAROrdemUnitarizacaoEtapa",chave,objEtapa,$$$YES)      
    }
    
    quit strStatus

GetQuantidadeTotalTodasEtapas(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Retorna a quantidade total em todas as etapas criadas pelo usuário, independentemente
    ;   do seu status.
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------
    quit:(pYKEY = "") 0

    new idEtapa, objEtapa, qtyEtapa, qtyPerda, qtyTotalEtapa, qtyTotalTodasEtapas

    set qtyTotalTodasEtapas = 0

    $$$Order3(^VAROrdemUnitarizacaoEtapa,YM,pYKEY,idEtapa)
    
        set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,pYKEY,idEtapa,1))
        set qtyEtapa = $$$VAROrdemUnitarizacaoEtapaQuantSelEtiq(objEtapa)
        set qtyPerda = $$$VAROrdemUnitarizacaoEtapaQuantPerda(objEtapa)
                
        set qtyTotalEtapa = qtyEtapa + qtyPerda     
        set qtyTotalTodasEtapas = qtyTotalTodasEtapas + qtyTotalEtapa

    
    $$$End
    
    quit qtyTotalTodasEtapas

GetLinhasAConcluir(pidOrdemFrac)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Retorna as linhas que o usuário marcou (x)-Concluir e que ainda não foram
    ;   previamente concluídas.
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------
    quit:(pidOrdemFrac = "") ""
    
    new listaItensAConcluir, idEtapa, objEtapa, situacaoEtapa, blnConcluir
    
    set listaItensAConcluir = ""
    
    $$$Order3(^VAROrdemUnitarizacaoEtapa,YM,pidOrdemFrac,idEtapa)
        
        set objEtapa      = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))
        set situacaoEtapa = $$$VAROrdemUnitarizacaoEtapaSituacao(objEtapa)
        set blnConcluir   = $$$VAROrdemUnitarizacaoEtapaConcluir(objEtapa)
        
        ;Ignora etapas previamente concluídas
        continue:(situacaoEtapa = $$$Concluido)
        
        if (blnConcluir = $$$YES) {
            
            if (listaItensAConcluir = "") {
                set listaItensAConcluir = idEtapa
            } else {
                set listaItensAConcluir = listaItensAConcluir_";"_idEtapa
            }
        }

    $$$End
    
    quit listaItensAConcluir

GetQuantidadeAConcluir(pidOrdemFrac,pstrLinhasAConcluir)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Retorna o somatório da quantidade das etapas que o usuário selecionou para Concluir.
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------

    quit:( (pidOrdemFrac = "") || (pstrLinhasAConcluir = "") ) ""
    
    new fltQuantTotal, cnt, idEtapa, objEtapa
    
    set fltQuantTotal = 0
    
    for cnt = 1:1:$length(pstrLinhasAConcluir) {
        
        set idEtapa = $piece(pstrLinhasAConcluir,";",cnt)
        quit:(idEtapa = "")
        
        set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))
        
        set fltQuantTotal = fltQuantTotal + $$$VAROrdemUnitarizacaoEtapaQuantSelEtiq(objEtapa)      
    }
    
    quit fltQuantTotal
    
GetQuantidadePerda(pidOrdemFrac,pstrLinhasAConcluir)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Retorna o somatório da quantidade de perda das etapas que o usuário selecionou 
    ;   para Concluir.
    ;
    ; History:
    ;   08-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------
    quit:( (pidOrdemFrac = "") || (pstrLinhasAConcluir = "") ) ""
    
    new fltQuantTotalPerda, cnt, idEtapa, objEtapa
    
    set fltQuantTotalPerda = 0
    
    for cnt = 1:1:$length(pstrLinhasAConcluir) {
        
        set idEtapa = $piece(pstrLinhasAConcluir,";",cnt)
        quit:(idEtapa = "")
        
        set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))
        
        set fltQuantTotalPerda = fltQuantTotalPerda + $$$VAROrdemUnitarizacaoEtapaQuantPerda(objEtapa)      
    }
    
    quit fltQuantTotalPerda 
    
GetListaEtapasFromLinhas(pidOrdemFrac,pstrLinhasAConcluir)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Retorna as etapas que o usuário selecionou para Concluir com base nas linhas
    ;   que foram marcadas como (x)-Concluir.
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------    
    quit:( (pidOrdemFrac = "") || (pstrLinhasAConcluir = "") ) ""
    
    new listaEtapas, cnt, idEtapa, objEtapa, numEtapa   
    set listaEtapas = ""
    
    for cnt = 1:1:$length(pstrLinhasAConcluir) {
        
        set idEtapa = $piece(pstrLinhasAConcluir,";",cnt)
        quit:(idEtapa = "")
        
        set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))
        
        set numEtapa = $$$VAROrdemUnitarizacaoEtapaEtapa(objEtapa)
        
        if listaEtapas = "" {
            set listaEtapas = numEtapa
        } else {
            set listaEtapas = listaEtapas_", "_numEtapa
        }
    }
    
    quit listaEtapas
    
    
GetListaMovimentacoes(pidOrdemFrac)
    ;-------------------------------------------------------------------------------
    ; Descrição:
    ;   Retorna a lista de movimentações de estoque que foram geradas em uma determinada
    ;   ordem de unitarização.
    ;
    ; History:
    ;   03-Jan-2013     Gustavo     Criado
    ;-------------------------------------------------------------------------------
    quit:(pidOrdemFrac = "") ""
    
    new idEtapa, objEtapa, situacaoEtapa, strListaMovimentacoes, continue
    
    set strListaMovimentacoes = ""
    
    $$$Order3(^VAROrdemUnitarizacaoEtapa,YM,pidOrdemFrac,idEtapa)
        
        set objEtapa      = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))
        set situacaoEtapa = $$$VAROrdemUnitarizacaoEtapaSituacao(objEtapa)
        
        ;Ignora etapas não concluídas
        continue:(situacaoEtapa '= $$$Concluido)
        
        set idMOV = $$$VAROrdemUnitarizacaoEtapaCodMovimentacao(objEtapa)
        continue:(idMOV = "")
        
        if (strListaMovimentacoes = "") {
            set strListaMovimentacoes = idMOV
        } elseif '$find(strListaMovimentacoes,idMOV) {
            set strListaMovimentacoes = strListaMovimentacoes_";"_idMOV
        }       

    $$$End
    
    quit strListaMovimentacoes


GetNextEtapa(pYKEY)
    new numUltimaEtapa, numNextEtapa, date, gridLine
    
    set numUltimaEtapa = 1
    set numNextEtapa   = 1
    set date = $piece($horolog,",",1)
    
    set gridLine = ""
    for {
        set gridLine = $order(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEROW",gridLine),-1)
        quit:(gridLine = "")

        ;Desconsidera linhas que foram deletadas
        continue:($get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","KILLEDRECORD",gridLine)) = 1)
     
        set idLinha = $get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEROW",gridLine,1))
        set numUltimaEtapa = $get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEVALUE","Y"_idLinha_"_1",1))
        
        set numNextEtapa = $increment(numUltimaEtapa)              
        quit
    }
    
    quit numNextEtapa
    
    
GetIDUltimaEtapa(YUSER)
    new idUltimaEtapa, date, gridLine
    
    set idUltimaEtapa = ""
    set date = $piece($horolog,",",1)
    
    set gridLine = ""
    for {
        set gridLine = $order(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEROW",gridLine),-1)
        quit:(gridLine = "")

        ;Desconsidera linhas que foram deletadas
        continue:($get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","KILLEDRECORD",gridLine)) = 1)
     
        set idUltimaEtapa = $get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEROW",gridLine,1))
             
        quit
    }
    
    quit idUltimaEtapa
]]></Routine>
</Export>