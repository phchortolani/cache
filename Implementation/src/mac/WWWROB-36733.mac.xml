<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWROB" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWROB ;WWWROB;DT;SUCHEN URL'S DURCHSUCHEN LINKS12.10.1999 ;seek scour 
	;------------------------------------------------------------------
	; Description of Function :
	;		SUCHEN URL'S DURCHSUCHEN LINKS12.10.1999 ;SEARCHES URL'S SCANNING
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 01-Jun-2005		RobertW		SR12056: Attempt at Performance Increase
	; DT	seek scour 
	;------------------------------------------------------------------
	SET %("VAR","YBACK")=""
	SET %("VAR","YFORM")=YFORM
	SET %("VAR","YAUSWAHL")=URL
	DO ^WWWFORM
	QUIT
	
ROBOT ;EINSPRUNG EINTRAG
	;------------------------------------------------------------------
	; History :
	; 30-Mar-2006 SC SR13942: Use JobWrapper^COMUtilJob
	;------------------------------------------------------------------
	;SET YURLS=$$^WWWLOW(YAUSWAHL)
	SET YURLS=$zconvert(YAUSWAHL,"L")
	SET MESS="??"
	IF YURLS'="" DO
	. IF $DATA(^WWWROB(0,YURLS)) SET MESS=$$^WWWTEXT(361)  ;EINGETRAGEN ;regd. 
	. IF +$GET(^WWWROB(0,YURLS,1))'=0 SET MESS=$$^WWWTEXT(362)   ;DURCHSUCHT
	. IF '$DATA(^WWWROB(0,YURLS)) DO
	. . SET MESS=$$^WWWTEXT(360)
	. . SET ^WWWROB(0,YURLS,1)=""   ;NEUER EINTRAG
	. . SET ^WWWROBs(0,1," ",YURLS)=""
	. . IF YQUERY'="%KEY" QUIT   ;MSM
	;. . do JobWrapper^COMUtilJob("START^WWWROB",YUSER,2)  ;START SUCHE ;take-off search ;SR13942
	. . JOB START^WWWROB::2  ;START SUCHE ;take-off search ;SR13942
	. . QUIT
	. QUIT
	WRITE "<HR>",YCR
	WRITE "<FONT SIZE=3>",YCR
	WRITE "<B>",YCR
	WRITE "<CENTER>",YCR
	WRITE "url: http://"
	WRITE YURLS," ",MESS  ;DRUCKEN SUCHTEXT ;print 
	WRITE "</CENTER>",YCR
	WRITE "</B>",YCR
	WRITE "</FONT>",YCR
	WRITE "<HR>",YCR
	QUIT
	
START ;SUCHEN URL ;seek URL 
	;------------------------------------------------------------------
	; History:
	; 30-Mar-2006 	SC		SR13942: No longer set environment within routine. 
	; 						Maintained by calling function.	
	;------------------------------------------------------------------
	LOCK +^WWWROB(0):0 QUIT:'$TEST  ;SCHON GESTARTET ;ALREADY STARTED
	DO ^WWWVAR ;SR13942 reversed.
	FOR  QUIT:$ORDER(^WWWROBs(0,1," ",""))=""  SET URL="" FOR  SET URL=$ORDER(^WWWROBs(0,1," ",URL)) QUIT:URL=""  DO
	. SET ^WWWROB(0,URL,1)=+$HOROLOG_Y_$PIECE($HOROLOG,",",2)
	. SET ^WWWROBs(0,1,+$HOROLOG,URL)=""
	. KILL ^WWWROBs(0,1," ",URL)
	. SET LINES=$$GET^%wldhttp(URL,40)
	. QUIT:+LINES=0
	. MERGE ^WWWROBT(0,URL)=%html
	. SET LINE="" FOR  SET LINE=$ORDER(%html(LINE)) QUIT:LINE=""  DO
	. . ;SET TEXT=$$^WWWLOW(%html(LINE))
	. . SET TEXT=$zconvert(%html(LINE),"L")
	. . ;SET TEXT1=$$^WWWLOW($GET(%html(LINE+1)))
	. . SET TEXT1=$zconvert($GET(%html(LINE+1)),"L")
	. . SET AH="a href="
	. . IF $FIND(TEXT_TEXT1,AH) DO
	. . . FOR YI=2:1 QUIT:$PIECE(TEXT_TEXT1,AH,YI)=""  DO
	. . . . SET URLN=$TRANSLATE($PIECE($PIECE($PIECE(TEXT_TEXT1,AH,YI),">",1)," ",1),"""")
	. . . . QUIT:URLN=""
	. . . . QUIT:$FIND(URLN,"@")
	. . . . QUIT:$FIND(URLN,">")
	. . . . QUIT:$FIND(URLN,"<")
	. . . . QUIT:$FIND(URLN,"?")
	. . . . QUIT:$FIND(URLN,"&")
	. . . . QUIT:$FIND(URLN,$CHAR(9))
	. . . . QUIT:'$FIND(URLN,".")
	. . . . IF $EXTRACT(URLN,1,7)="http://" SET URLN=$EXTRACT(URLN,8,99)
	. . . . IF $EXTRACT(URLN,1,2)="//" SET URLN=$EXTRACT(URLN,2,99)
	. . . . IF $EXTRACT(URLN)="/" QUIT
	. . . . QUIT:$LENGTH(URLN)<8
	. . . . QUIT:$EXTRACT(URLN)'="w"
	. . . . IF '$DATA(^WWWROB(0,URLN)) DO
	. . . . . SET ^WWWROB(0,URLN,1)=""  ;NEUER EINTRAG
	. . . . . SET ^WWWROBs(0,1," ",URLN)=""
	. . . . . QUIT
	. . . . QUIT
	. . . QUIT
	. . QUIT
	. QUIT
	LOCK
	HALT
]]></Routine>
</Export>