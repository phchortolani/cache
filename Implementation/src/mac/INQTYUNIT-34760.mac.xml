<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INQTYUNIT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INQTYUNIT(ITEM,ORDER,LINE)
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		MENGENEINHEITEN JE 100 / 1000 USW
	;       Quantity Units 100, 1000, etc.
	; 
	; If the product has a unit which indicates a multiple or if the
	; price for the product is based on a multiple of the unit (e.g. 100)
	; and the system is set to calculate using those multiples,
	; return the multiplier.  Whether this is used appropriately is another matter.
	; 
	;   *********************************************************************
	;   *  Form Help for INART D138 is currently;                           *
	;   *                                                                   *
	;   *  Here you can enter the price unit for sales (1/10/100 or 1000).  *        
	;   *  If you select a price unit this will be shown as information on  *
	;   *  line item creation.                                              *
	;   *********************************************************************
	; 
	; Usage :
	;	set UNIT=$$^INQTYUNIT(ITEM)
	;	set UNIT=$$^INQTYUNIT(,ORDER,LINE)
	;
	; Inputs : 
	;	ITEM  = ITEMNUMBER
	;	ORDER = ORDERNUMBER
	;	LINE  = LINE ITEM
	;
	; ByRef : None
	;
	; Returns :
	;   Conversion factor, default=1
	;
	; History :
	; 27-Jul-2009	GRF		SR16602: doco; boolean macros
	; 13-Jun-2006	GRF		Doco
	; 02-Sep-2005	JW		SR12966: INAUFP is not shared 
	; 14-Jul-2005	JW		INART is not shared! INVORG data may not exist!!
	; 26-Apr-2005	GRF		Documentation
	; 18.02.2005	FIS		EINHEIT, NICHT ARTIKELART (Use D40 "Unit of Measure", not D26 "Item source"
	; 06.01.2005	TYBD	Created
	;-------------------------------------------------------------------------------
	; ********************************************************
	; Controls :
	;   INVORG
	;     D204 : Calculate Quantity Unit /1xxx (Boolean)
	;            Hidden on form? Therefore never uses unit name-based multipliers?
	;     D205 : Calculate Price QTY /1xxx     (Boolean)
	;            Overrides unit name-based multiplier if actually set.
	;   INART
	;     D40  : $$$INARTUnitofMeasure()
	;     D138 : Price Unit (currently can be 1,10,100, or 1000 if not left blank)
	; ********************************************************
	NEW RETURN,ITEM1,UNIT
	
	SET RETURN = 1
	
	;+++++++++++++++++++++++++++++++++++++++
	;	ITEM1		objINART
	;+++++++++++++++++++++++++++++++++++++++
	
	IF $GET(ITEM)="" IF $GET(ORDER)'="" IF $GET(LINE)'="" DO
	. SET ITEM = $PIECE($GET(^INAUFP(YM,ORDER,LINE,1)),Y,4)  ;immer aus artikel, sonst chaos ! ;fis;02.03.05
	
	IF $GET(ITEM)'="" DO
	. SET ITEM1 = $GET(^INART(YM,ITEM,1))
	. QUIT:ITEM1=""
	. QUIT:$GET(^INVORG(YM,YM,1))=""               ; FIXME : not needed - just use $get - same result <GRF>
	. ;
	. SET UNIT=""
	. IF $PIECE(^INVORG(YM,YM,1),Y,204)=$$$YES IF $PIECE(ITEM1,Y,40)'=""  SET UNIT   = $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(ITEM1,Y,40),1)),Y,1)
	. IF $FIND(UNIT,"/") IF +$PIECE(UNIT,"/",2)'=0                        SET RETURN = $PIECE(UNIT,"/",2)    ;WENN Z.B. MENGENEINHEIT= /100 
	. IF $PIECE(^INVORG(YM,YM,1),Y,205)=$$$YES IF +$PIECE(ITEM1,Y,138)'=0 SET RETURN = $PIECE(ITEM1,Y,138)   ;WENN Z.B. PEISEINHEIT JE 100  
	
	// moved ineffective block below
	
	IF +RETURN=0 SET RETURN = 1
	
	QUIT RETURN
	
]]></Routine>
</Export>