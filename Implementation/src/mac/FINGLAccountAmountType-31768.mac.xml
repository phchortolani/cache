<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLAccountAmountType" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLAccountAmountType
	
#include COMSYS
#include FINConst
	
OnBeforeSave(pYKEY,pYFELD)
	;--------------------------------------------------------------------------
	; Routine to do checks prior to saving
	; 
	; History
	; 08-Jul-2003	shobby	Creation (TR005557)
	;--------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$CheckDefault(pYKEY,pYFELD)
	
	if strStatus'=$$$OK {
		set Q=1
		do ReturnError^COMUtils((strStatus))
	}
	quit
	
	
CheckDefault(pYKEY,pYFELD)
	;--------------------------------------------------------------------------
	; Routine to check that there is only one default account amount type.
	; 
	; History
	; 13-Dec-2004	MS		SR11214 Check for idDefault being null - was causing subscript error
	; 08-Jul-2003	shobby	Creation (TR005557)
	;--------------------------------------------------------------------------
	new strStatus,idDefault,blnDefault,objAcctAmountType
	
	set strStatus = $$$OK
	if pYFELD'="" {
		set blnDefault = $$$FINGLAccountAmountTypeDefault1(pYFELD)
		if blnDefault {
			set idDefault = $$GetDefault^FINGLAccountAmountType()
			if idDefault="" set idDefault = " "
			set objAcctAmountType = $get(^FINGLAccountAmountType(0,idDefault,1))
			set blnDefault = $$$FINGLAccountAmountTypeDefault1(objAcctAmountType)
			if blnDefault {
				if (idDefault'=pYKEY) && (idDefault'="") {
					set strStatus = $listbuild("Fin00190",idDefault)
				} ; "There can not be more than one default Accrual Account. Current Default ´%1´."
			}
		}
	}
	quit strStatus
	
	
GetDefault()
	;--------------------------------------------------------------------------
	; Routine to get the Default Account Amount Type.
	; 
	; History
	; 08-Jul-2003	shobby	Creation (TR005557)
	;--------------------------------------------------------------------------
	new idDefault
	
	set idDefault = $order(^FINGLAccountAmountTypes(0,1,1,""))
	if idDefault="" {
		set idDefault = $order(^FINGLAccountAmountType(0,""))
	}
	quit idDefault


OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Checks whether this item can be deleted.
	; 
	; History
	; 09-Mar-2004	shobby	Creation
	;-------------------------------------------------------------------------------
	quit $$CheckLinks^FINSYS(pYKEY,$listbuild("FINGLAccountTotal","FINGLDistribution","FINGLJournal","FINGLTran"),"FINGLAccountAmountType")


GetDescription(pidType)
    ;-------------------------------------------------------------------------------
    ; Get the text Description of the Account Amount Type
    ;
    ; Returns:
    ;
    ; History:
    ; 12-May-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	new objAcctAmountType,strDescription
	
	set objAcctAmountType = $get(^FINGLAccountAmountType(0,pidType,1))
	set strDescription    = $$$FINGLAccountAmountTypeDescription(objAcctAmountType)
	quit strDescription
	
]]></Routine>
</Export>