<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescriptionIssue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescriptionIssue
	
#include COMConst
#include MEDConst
#include INConst
#include COMView
	
OnBeforeButtonLine()   ; FIXME : Remove from here and form MEDPrescriptionIssue if not required <GRF>
	;-------------------------------------------------------------------------------
	; Description of Function :
	;	Load in Prescriptions that are valid to be issued
	;
	; Called By :
	;	Form - MEDPrescriptionIssue
	;		
	; History :
	; 29-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	quit
	
	
OnAfterButtonLine()   ; FIXME : Remove from here and form MEDPrescriptionIssue if not required <GRF>
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By :
	;		
	; History :
	; 29-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	quit
	
	
OnAfterDataFields(pstrCompany,pstrForm)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; History :
	; 29-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	do LoadGrid(pstrCompany,pstrForm)
	quit
	
	
LoadGrid(pstrCompany=0,pstrForm,pblnLocked=$$$NO)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 13-Sep-2010	PPP		SR16642: Updated the Criteria
	; 29-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
	
	kill ^CacheTempLocked(YUCI,YUSER)
	do ClearOld^WWWMultiLock(YUSER)
	
	$$$ClearExternalFilter("MEDPrescriptionIssue") 
	
	//YFORM is used as a global within COMGridEdit31
	set YFORM = "MEDPresAsIssue"
	
	// SR16642
	do AddExternalFilter^COMViewFilter("MEDPrescriptionIssue","P1","",$$$EnumCOMVIEWCOMPARATORStartsWith,$$$YES)
	do AddExternalFilter^COMViewFilter("MEDPrescriptionIssue","D"_$$$FldMEDPrescriptionSupplyingLocn,"",$$$EnumCOMVIEWCOMPARATORStartsWith,$$$YES)
	
 	// SR16642 only show open Prescriptions, not closed ones
 	do AddExternalFilter^COMViewFilter("MEDPrescriptionIssue","D"_$$$FldMEDPrescriptionStatus,"1",$$$EnumCOMVIEWCOMPARATORGreaterThan,$$$NO)
 	do AddExternalFilter^COMViewFilter("MEDPrescriptionIssue","D"_$$$FldMEDPrescriptionStatus,"4",$$$EnumCOMVIEWCOMPARATORLessThan,$$$NO)
	do AddExternalFilter^COMViewFilter("MEDPrescriptionIssue","D14",$horolog+1,$$$EnumCOMVIEWCOMPARATORLessThan,$$$YES)
	
	set YFELD	= ""
	set YKEY	= ""		// No header
	;do SetYFORM^COMGridEdit31Head("MEDPrescription")
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)          = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)       = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)            = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)             = $$$NO
	set $$$COMGridEditParameterContainer(YAUSWAHL)           = "MEDPrescriptionIssue"
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$YES
	set $$$COMGridEditParameterCOMViewGrid(YAUSWAHL)         = $$$YES
	
	do Start^COMGridEdit31(YFORM,YKEY)
	set YFORM = "MEDPrescriptionIssue"                 ; COMViewFilter uses YFORM byref
	
	do LoadCOMViewGrid^COMViewFilter("MEDPrescriptionIssue","MEDPresAsIssue",1)
	
	quit $$$YES
	
	
GetActiveIssueNumber(pidPres)
	;-------------------------------------------------------------------------------
	; Description :
	; Gets the active issue for this Prescription
	;			
	; Called By :
	;		
	; Inputs : pidPres	Requisition Number
	;
	; ByRef : YLOCATION
	;
	; Returns : Open Issue number for line
	;
	; History :
	; 29-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new enumType,idIssue,idPres,idxReqNo,idxType,objIssue,objPres
	
	set idIssue = ""
	
	if pidPres="" {
		set idPres = $$GetKey^COMGridEdit31G()
	}
	if pidPres'="" {
		set objPres = $get(^MEDPrescription(0,pidPres,1))
	
		set idxReqNo = $$$Index(pidPres)
		
		set idIssue = $order(^INIssues(0,5,5,idxReqNo,""),-1)
		if idIssue'="" {
			set objIssue = $get(^INIssue(0,idIssue,1))
			if $$$INIssueStatus(objIssue) > 3 {
				set idIssue = "" ; no active issue and can create a repeat.
			}
		}
	}
	quit idIssue
	
	
FulfilPrescription(pidPRES)
	;-------------------------------------------------------------------------------
	; create an Issue or a prescription and redirect to the correct Form
	;
	;	(Button)Create Issue (FORMSPEC) MEDPresAsIssue
	; 
	; ByRef:
	; 	YFORM
	; 	YBACK
	; 
	; History:
	; 05-Nov-2010	shobby	SR17607: YBACK key was created with extra quotes.
	; 02-Jul-2009	DWR		SR16642: Created modified version INreqIssueAsReq
	;-------------------------------------------------------------------------------
	new idIssue,idTFR,objPress,strStatus
 	
 	set strStatus = $$$OK
	
	if pidPRES '= "" {
		set idIssue = $$GetActiveIssueNumber(pidPRES)
		if idIssue '="" {
			;SR17607 do RedirectForm^COMUtils("MEDPrescriptionDispense",idIssue,"MEDPrescription,"""_pidPRES_"")
			if (+$$$WWWClientParamCoreChangesSESAU($get(^WWWClientParam(YM,YM,1)))) {
				do RedirectForm^COMUtils("VARDispensacaoJudicial",idIssue,"VARPrescricaoJudicial,"_pidPRES_"") 
			} else {
				do RedirectForm^COMUtils("MEDPrescriptionDispense",idIssue,"MEDPrescription,"_pidPRES_"")  ;SR17607 
			}  		
		} else {
			do CreateIssue(pidPRES)
		}
	}
	quit
	
	
CreateIssue(pYKEY)
	;-------------------------------------------------------------------------------
	;	Creates an INIssue from the selected MEDPrescription
	;	
	; Called By :
	;	For every line in a MEDPrescription, if it doesn't have a corresponding IssueLine, 
	;	create a new header and assign the line to it
	;		
	; Inputs : 
	;	pYKEY	-	MEDPrescription ID
	;	
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 02-Feb-2012	SCR		SR17979: Add Issue from Ward option
	; 04-Feb-2011	GRF		SR17579: Rename CalculateBaseQuantity
	; 02-Jul-2009	DWR		SR16642: Created -  modified version INreqIssueAsReq
	;-------------------------------------------------------------------------------
	new fltQtyReq,fltUnitQty
	new idBaseUnit,idIssue,idItem,idLine,idLocn,idPres,idPresLine,idUnit
	new objIssue,objIssueLine,objPres,objPresLine,objReq,objReqLine,sc,strStatus
	new idIssueLoc,intIssueType
	
	set strStatus = $$$OK
		
	//	since $$GetKey^COMGridEdit31G() is based on current view and click,
	// it can get invalidated when using mutiple windows
	set idPres = $$GetKey^COMGridEdit31G()
	if (pYKEY '= "") {
		set idPres = pYKEY
	}
	if (idPres = "") {
		$$$StartScript()
		write "alert("_$$$Text("MED00362")_");"       ; "Unable to determine Prescription Number"
		$$$EndScript()
		if (+$$$WWWClientParamCoreChangesSESAU($get(^WWWClientParam(YM,YM,1)))) {
			do RedirectForm^COMUtils("VARPrescricaoJudicial")
		} else {	
			do RedirectForm^COMUtils("MEDPrescriptionIssue")
		}
		quit                                                    ; *** EARLY EXIT ***
	}
	
	set objPres = $get(^MEDPrescription(0,idPres,1))
	
	if $$$ISOK(strStatus) {
			
	;+++++++++++++++++++++++++++++++++++++++
		tstart
	;+++++++++++++++++++++++++++++++++++++++
		
		if objPres '="" {
			
			;------------------------------- Issue 
			
			set idIssue = $$$NextKey("INIssue")
			
			
			
			set objIssue=""
			
			; SR17979 vvvv
			set intIssueType = 5
			set idIssueLoc=$$$MEDPrescriptionIssuingLocation(objPres)
			set $$$INIssueToLocn(objIssue)       = $$$MEDPrescriptionDispensingLocn(objPres)  ; Dispense Location
			if idIssueLoc'="" {
				set $$$INIssueToLocn(objIssue) = $$$MEDPrescriptionIssuingLocation(objPres)
				set intIssueType = 1
			} 
			; SR17979 
			
			set $$$INIssueStatus(objIssue)       = 1  
			set $$$INIssueFromLocn(objIssue)     = $$$MEDPrescriptionSupplyingLocn(objPres)
			set $$$INIssueDemandType(objIssue)   = 3 ; Sale
			set $$$INIssueReference(objIssue)    = idPres
			set $$$INIssueCreatedOn(objIssue)    = +$horolog
			set $$$INIssueCreatedBy(objIssue)    = $get(YBED)
			set $$$INIssueCustomerID(objIssue)   = $$$MEDPrescriptionPatientID(objPres)
			set $$$INIssueCustomerName(objIssue) = $$$MEDPrescriptionPatientSurname(objPres)_", "_$$$MEDPrescriptionPatientOtherNames(objPres)
			set $$$INIssueType(objIssue)         = intIssueType ; SR17979 Prescription to Issue to Ward
			;set $$$INIssueType(objIssue)         = 5 ; Prescription
			set $$$INIssueReferrerID(objIssue)   = $$$MEDPrescriptionProviderNumber(objPres)
			set $$$INIssueReferrerName(objIssue) = $$$MEDPrescriptionDoctorName(objPres)
			set $$$INIssueDocketNo(objIssue)     = idPres
			set $$$INIssueSubType1(objIssue)     = $$$MEDPrescriptionPrescriptionType(objPres)
			
			if $$$INIssueFromLocn(objIssue) = "" {
				if (+$$$WWWClientParamCoreChangesSESAU($get(^WWWClientParam(YM,YM,1)))) {
					
					set idLocalFarmacia = $$getFarmaciaAcoesJudiciais^VARParametroCliente(0)
					
					if (idLocalFarmacia '= "") {
						set $$$INIssueFromLocn(objIssue) = idLocalFarmacia
					} else {
						set $$$INIssueFromLocn(objIssue) = $get(YLOCATION)
					}	
				} else {
					set $$$INIssueFromLocn(objIssue) = $get(YLOCATION)
				}
			}     
			;set $$$INIssueToLocn(objIssue)       = $$$MEDPrescriptionDispensingLocn(objPres)  ; SR17979 Dispense Location
			
			set strStatus = $$$Save("INIssue",idIssue,objIssue,$$$YES)
			
			;------------------------------- Issue Lines
			
			if $$$ISOK(strStatus) {
				set idLine = 0		//Issue Line Counter
				set idPresLine = ""
				for {
					set idPresLine = $order(^MEDPrescriptionLine(0,idPres,idPresLine))
					quit:idPresLine=""
					
					set objPresLine = $get(^MEDPrescriptionLine(0,idPres,idPresLine,1))
					if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
						continue:($$$MEDPrescriptionLineFREE4(objPresLine)=5) ;Suspended
					}
					set idItem      = $$$MEDPrescriptionLineItem(objPresLine)
					
					if $$$MEDPrescriptionPrescriptionType(objPres) = 2 {                      ; Custom manufacture
						set fltUnitQty     = $$$MEDPrescriptionLineDoseQty(objPresLine)
					
					} else {
						;if $$$MEDPrescriptionLineDispenseQuantity(objPresLine) '="" {
						if +$$$MEDPrescriptionLineDispenseQuantity(objPresLine) > 0 {
							set fltUnitQty = $$$MEDPrescriptionLineDispenseQuantity(objPresLine)
						} else {
							set fltUnitQty = $$$MEDPrescriptionLineDoseTotalQuantity(objPresLine)
						}
					}
										
					if $$$MEDPrescriptionLineDispenseUOM(objPresLine)'="" {
						set idUnit = $$$MEDPrescriptionLineDispenseUOM(objPresLine)
					} else {
						set idUnit = $$$MEDPrescriptionLineDoseUOM(objPresLine)
					}
					
					set idBaseUnit = $$$INARTUnitofMeasure($get(^INART(0,idItem,1)))
					set fltQtyReq  = $$CalcInventoryQty^INUOMConversion(idItem,idUnit,fltUnitQty)  ; SR17579
					
					//Get the Current Location if not specified
					set idLocn = $$$MEDPrescriptionSupplyingLocn(objPres)
					if idLocn="" set idLocn = YLOCATION
					
					set strStatus = $$CreateIssueLine(idIssue,idLocn,idItem,idBaseUnit,fltQtyReq,$increment(idLine),$select((intIssueType = 1):$$$INIssueToLocn(objIssue),1:"")) ; SR17979
					;set strStatus = $$CreateIssueLine(idIssue,idLocn,idItem,idBaseUnit,fltQtyReq,$increment(idLine))
					quit:$$$ISERR(strStatus)
				}
			}
		}
		
	;+++++++++++++++++++++++++++++++++++++++
		if ($$$ISOK(strStatus)) {
			tcommit:($tlevel > 0)
		} else {
			trollback
		}
	;+++++++++++++++++++++++++++++++++++++++
	}
	
	if $$$ISOK(strStatus) {
		if idIssue'="" {
			set $$$MEDPrescriptionSupplyingLocn(objPres) = $get(YLOCATION)
			set strStatus = $$$Save("MEDPrescription",idPres,objPres,$$$YES)
			
			//If a Scanning Form then clear out IssueQty
			if $$IsScanReq^WWW120Scan("MEDPresDispLine") {
				set strStatus = $$Transaction^COMTransaction("ClearIssueQty^INReqIssueAsReq("""_idIssue_""")",1)
			}
			if (+$$$WWWClientParamCoreChangesSESAU($get(^WWWClientParam(YM,YM,1)))) {
				do RedirectForm^COMUtils("VARDispensacaoJudicial",idIssue)
			} else {
				do RedirectForm^COMUtils("MEDPrescriptionDispense",idIssue)
			}
		}
	
	} else {
		if (+$$$WWWClientParamCoreChangesSESAU($get(^WWWClientParam(YM,YM,1)))) {
			do RedirectForm^COMUtils("VARPrescicaoJudicial",pYKEY)
		} else {
			do RedirectForm^COMUtils("MEDPrescription",pYKEY)
		}
	}
	quit
	
	
CreateIssueLine(pidINIssue,pidLocn,pidItem,pidUnit,pfltQtyReq,pidLine,pidToLocn)
	;-------------------------------------------------------------------------------
	; Called by the Firm process
	; This process generates the INIssueLines for the Production Order
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 09-Feb-2012	SCR		SR17979: Set To storage for Transfer to Ward Option
	; 28-Oct-2010	GRF		SR166422.1: Remove early quit without return value
	; 22-Oct-2010	PPP		SR16642: Created
	; 						Pick Lines with Bundles & Storages defined (System Generated)
	;-------------------------------------------------------------------------------
	new blnNotFirst,fltOutstanding,fltQtyIssued,idLn
	new loopStock,lstAvailableIssues,objBundle,objINIssueLine,objStorage,strStatus
	
	set strStatus      = $$$OK
 	set blnNotFirst    = $$$NO
 	set fltOutstanding = pfltQtyReq
 	
 	if (+$$$WWWClientParamCoreChangesSESAU($get(^WWWClientParam(YM,YM,1)))) {
		set strStatus 	   	   = $$CreateIssuePatient^VARDispensacaoJudicial(pidINIssue,pidLocn,pidItem,pidUnit,pfltQtyReq,pidLine,.idLn,.blnNotFirst,.fltOutstanding)
		set lstAvailableIssues = ##class(alDRP.iDRP).SourceIssue(pidItem,pidLocn,fltOutstanding)
	} else {
		set lstAvailableIssues = ##class(alDRP.iDRP).SourceIssue(pidItem,pidLocn,pfltQtyReq)
	}
 	
	if ((lstAvailableIssues.Count() > 0) && (fltOutstanding > 0) && $$$ISOK(strStatus)) {
		for loopStock=1:1:lstAvailableIssues.Count() {
			set objStorage   = lstAvailableIssues.GetAt(loopStock).Storage
			set objBundle    = lstAvailableIssues.GetAt(loopStock).Bundle
			set fltQtyIssued = lstAvailableIssues.GetAt(loopStock).QtyToIssue
			
			set objINIssueLine = ""
			
			set $$$INIssueLineStatus(objINIssueLine)      = 1				
			set $$$INIssueLineQtyRequired(objINIssueLine) = pfltQtyReq
			set $$$INIssueLineQtyIssued(objINIssueLine)   = fltQtyIssued
			set $$$INIssueLineItem(objINIssueLine)        = pidItem
			set $$$INIssueLineUnit(objINIssueLine)        = pidUnit
			; SR17979 vvvv
			if pidToLocn'="" {
				set $$$INIssueLineToStorage(objINIssueLine)   = $$GetStorage^INIssueAuto(pidItem,pidToLocn)
			}
			; SR17979 ^^^^
			set $$$INIssueLineFromStorage(objINIssueLine) = objStorage.Code
			set $$$INIssueLineBundle(objINIssueLine)      = objBundle.%Id()
			set $$$INIssueLineReference(objINIssueLine)   = pidINIssue
			
			if blnNotFirst = $$$YES {
				set idLn    = $increment(idLn)
				set pidLine = (pidLine\1)_"."_idLn
			} else {
				set blnNotFirst = $$$YES
			}
			
			set strStatus = $$$Save("INIssueLine",pidINIssue_","_pidLine,objINIssueLine,$$$YES)
			set fltOutstanding = fltOutstanding - fltQtyIssued
			quit:$$$ISERR(strStatus)
		}
	}
	
	//Blank Lines for remaining Qty
	;if fltOutstanding>0 {                               ; SR166422.1
	;	quit:$$$ISERR(strStatus)			; EARLY EXIT
		
	if $$$ISOK(strStatus) && (fltOutstanding>0) {
		set objINIssueLine = ""
		set $$$INIssueLineStatus(objINIssueLine)      = 1				
		set $$$INIssueLineQtyRequired(objINIssueLine) = pfltQtyReq
		set $$$INIssueLineQtyIssued(objINIssueLine)   = fltOutstanding
		set $$$INIssueLineItem(objINIssueLine)        = pidItem
		set $$$INIssueLineUnit(objINIssueLine)        = pidUnit
	//	set $$$INIssueLineToStorage(objINIssueLine)   = idToStorage
	//	set $$$INIssueLineFromStorage(objINIssueLine) = 
	//	set $$$INIssueLineBundle(objINIssueLine)      = 
		set $$$INIssueLineReference(objINIssueLine)   = pidINIssue
		
		if blnNotFirst = $$$YES {
			set idLn    = $increment(idLn)
			set pidLine = (pidLine\1)_"."_idLn
		} else {
			set blnNotFirst = $$$YES
		}
		set strStatus = $$$Save("INIssueLine",pidINIssue_","_pidLine,objINIssueLine,$$$YES)	
	}
	quit strStatus
	
	
Editable(pidIssue) 
	;-------------------------------------------------------------------------------
	; Is form editable (unprocessed) ?
	;
	; Params:	pidPres - Prescription id
	;
	; Returns:	status
	;
	; History:
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new objIssue,strStatus
	
	set strStatus = $$$OK
	
	if $data(^INIssue(0,pidIssue,1)) {
		set objIssue = $get(^INIssue(0,pidIssue,1))
		
		if $$$INIssueStatus(objIssue) > 1  {          ; "Open"
			set strStatus = $$$MakeStatus("IN00377")
		} ; "Record already processed"
	}
	quit strStatus
	
]]></Routine>
</Export>