<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARRecebimentoHistorico" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARRecebimentoHistorico
	#include COMSYS
	quit
	
	
Imprimir()
	;do Imprimir^VARRecebimentoHistorico()
	new Local, Fornecedor, DataInicial, DataFinal, Produto
	
	set Local       = $get(VORG(1))
	set Fornecedor  = $get(VORG(2))
	set DataInicial = $get(VORG(3))
	set DataFinal   = $get(VORG(4))
	set Produto     = $get(VORG(5))
	
	do RunReportHistoricoRecebimento^VARJasperRunReport(DataInicial, DataFinal, Local, Fornecedor, Produto)
	
	quit
	
OnAfterSave()
	;OnAfterSave^VARRecebimentoHistorico
	new Local
	set Local = $get(VORG(1))
 	;do GoToForm^COMUtilForm("VARRecebimentoHistorico",Local,,,,,) 	
 	do Imprimir()
	
	quit	
	
	
OnBeforeButtonLine(pYFELD)

	do LoadListaLocaisDeEstoque^VAREstoqueZerado
	
	quit
		
		
OnAfterDataFields
	;OnAfterDataFields^VARRecebimentoHistorico

	;do MontaGrid
	
	quit
	

MontaGrid
	;MontaGrid^VARRecebimentoHistorico
	
	quit:($get(VORG(1)) = "")
	
	new filterItem, sNumRec, sTipo, sItem, sLocal, sEndereco, sQuantidade, sLote, sValidade,
	    sCustoUnitario, sPrecoTotal, sCustoUnitarioSQL, totalQty, isBlockedStorage, prefixStyle,
	    posfixStyle, reasonBlocked, lstHeader, objStorage, blnTSU, descStorage, physicalStorage,
	    isBlockedPhysicalStorage, Produto

	set filterLocal = $get(VORG(1))
	set DataInicio  = $get(VORG(3))
	set DataFim     = $get(VORG(4))
	set Produto     = $get(VORG(5))
	
	if filterLocal = "" set filterLocal = YKEY
	
	;O Custo Unitário está sendo calculado usando Preço Total / Quantidade porque o core usa essa
	;forma para calcular o Custo Médio dos Produtos E estava dando diferença entre os relatórios
	;de Histórico de Recebimento e Posição do Estoque
	
	&sql(DECLARE HistoricoRec CURSOR FOR
		
		select
		  INR.ReceiptNumber,
		  INR.Type,
		  INLine.Item,
		  INR.Location,
		  INLine.StockLocation,
		  INLine.Quantity,
		  INLine.LotNumber,
		  INLine.ExpirationDate,
		  INSum.UnitCost as CustoUnitario,
		  (INSum.UnitCost*INLine.Quantity) as PrecoTotal,
		  (INSum.CostAfterDiscount/INSum.QuantityReceived) as CustoUnitarioCalculado,
		  INR.ProcessedDate
        INTO :sNumRec,:sTipo,:sItem,:sLocal,:sEndereco,:sQuantidade,:sLote,:sValidade,:sCustoUnitario,:sPrecoTotal,:sCustoUnitarioSQL,:sDataRecebimento			
		from SQLUser.INREC as INR 
		left join SQLUser.INRECLine as INLine on (INR.Company = INLine.Company 
		                                      and INR.ReceiptNumber = INLine.ReceiptNumber)
		left join SQLUser.INRECSummary as InSum on (INLine.Company       = INSum.Company
		                                        and INLine.ReceiptNumber = INSum.ReceiptNumber
		                                        and CAST(INLine.LineNumber as INT) = INSum.LineNumber)
		where INR.Status = 1
		  and INR.Type <> 5
		  and INLine.ReversedByREC is null
		  and ((:Produto IS NULL) OR (INLine.Item = :Produto))
		  and DATEDIFF('dd', INR.ProcessedDate, :DataFim) >= 0
          and DATEDIFF('dd', INR.ProcessedDate, :DataInicio) <= 0
		order by INLine.Item
		
		)
	
	&sql(open HistoricoRec)
    if (SQLCODE) {
		quit
    }
	
	//Tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Cód.","Produto","Endereço","No. Lote","Validade","Quantidade","Unidade","Preço (R$)","Valor Total (R$)")
  	
  	if $$Start^COMTable(lstHeader) {
		set count       = 0
		set totalQty    = 0
		set vTotalGeral = 0
		set isBlockedPhysicalStorage = ""
		
		for { &sql(fetch HistoricoRec)
				   quit:SQLCODE
			
			continue:(sEndereco = "X")
			
			set vTotalItem = 0
			continue:(sItem = "")
			
		 	set objItem = $get(^INART(YM,sItem,1))
		 	continue:(objItem = "")
		 	continue:(sItem = 2)
 	
 			set descUnit  = ""
 			set descGroup = ""
 			set descConta = "" 			
 			
			set descItem = $piece(objItem,Y,1)
			set idUnit   = $piece(objItem,Y,40)
			if (idUnit '= "") { 
				set descUnit   = $piece($get(^WWW101(YM,"EINHEIT",SPRACHE,idUnit,1)),Y,1)
			}
			set idGroup = $piece(objItem,Y,30)
			if (idGroup '= "") {
				set descGroup  = $piece($get(^INItemGroup(YM,idGroup,1)),Y,2)
			}
			
			set objStorage = $get(^INLP(YM,sLocal,sEndereco,1))
			set blnTSU     = $piece(objStorage,Y,21)
			
			if (blnTSU = $$$YES) {

				set physicalStorage = $piece(objStorage,Y,22)					
				if (physicalStorage '= "") {		
					set isBlockedPhysicalStorage = $$checkIfBlockedStorage^VARStock(sLocal,physicalStorage)
				}			
			
				set descStorage = physicalStorage_" ["_sEndereco_"]"					
				
			} else {				
				set descStorage = sEndereco				
			}
			
			;Petik 28/12/2009
			;Alterado para o novo campo Conta Contábil (D278)
			;set conta      = $piece(objItem,Y,276)
			set conta = $piece(objItem,Y,278)
			if (conta '= "") {
				set descConta  = $piece($get(^INPARA(YM,"VARTCICONTACONTABIL","PT",conta,1)),Y,1)
			}

			set vTotalItem  = sPrecoTotal
			set vTotalGeral = vTotalGeral + sPrecoTotal
		
			set count = $i(count)
			set totalQty = totalQty + sQuantidade
			
			if sValidade '= "" set sValidade = $zdate(sValidade,4)
			
			set isBlockedStorage = ""
			set isBlockedStorage = $$checkIfBlockedStorage^VARStock(sLocal,sEndereco)
			
			set prefixStyle = ""
			set posfixStyle = ""
			
			if ( (isBlockedStorage = $$$YES) || (isBlockedPhysicalStorage = $$$YES) ) {
				
				if (blnTSU = $$$YES) {
					set reasonBlocked = $$getMotivoEndBloqueado^VARWMSUtils(sLocal,physicalStorage)
				} else {
					set reasonBlocked = $$getMotivoEndBloqueado^VARWMSUtils(sLocal,sEndereco)
				}
				
				set prefixStyle = "<font color='gray' style='text-decoration:line-through'><span title = 'Endereço indisponível. Motivo: "_reasonBlocked_"'>"
				set posfixStyle = "</span></font>"
			}
			
			do NewLine^COMTable()
	    	do InsertCell^COMTable(sItem)
	    	do InsertCell^COMTable(descItem,,,,,,,,,1)
	    	do InsertCell^COMTable(prefixStyle_descStorage_posfixStyle)
	    	do InsertCell^COMTable(prefixStyle_sLote_posfixStyle)
	    	do InsertCell^COMTable(prefixStyle_sValidade_posfixStyle)
	    	do InsertCell^COMTable(prefixStyle_$$^WWWTR(0,12,sQuantidade)_posfixStyle,,,,"RIGHT")
	    	do InsertCell^COMTable(descUnit)
	    	do InsertCell^COMTable($$^WWWTR(0,12,sCustoUnitario,4),,,,"RIGHT")
	    	do InsertCell^COMTable($$^WWWTR(0,8,sPrecoTotal,2),,,,"RIGHT")
	    	do EndLine^COMTable()
		}
		
		do NewLine^COMTable("lightsteelblue")
		do InsertCell^COMTable("<strong>Valor Total em Estoque</strong>",,,,"RIGHT",,8)
	    do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,vTotalGeral,2)_"</strong>",,,,"RIGHT")
		do EndLine^COMTable()
		
		do Stop^COMTable()	
  	}
	
	&sql(close HistoricoRec)	
	
	quit
]]></Routine>
</Export>