<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAta" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARAta
#include COMSYS
#include COMConst
#include VARConst
#include INConst

OnBeforeButtonLine(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o formulário está somente leitura ou não.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
 	new objAta
 	set objAta = $get(^VARAta(YM,pYKEY,1))
 	
 	set ^CacheTempEvent(YUCI,"VARAtaLinha","Format") = 1
 	
	if ($$$VARAtaStatus(objAta) > 0) {
		set $piece(YVOR,Y,23) = $$$EnumReadOnly
	}
	
	quit
	
OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Realiza verificações antes de salvar a ata.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new strStatus, dteAssinatura, dtePublicacao, dteValidade
		
	set strStatus = $$$OK
	
	set dteAssinatura = $piece(pYFELD,Y,2)
	set dtePublicacao = $piece(pYFELD,Y,3)
	set dteValidade	  = $piece(pYFELD,Y,4)
		
	// Verifica as datas inseridas pelo usuário
	if ( (dtePublicacao < dteAssinatura) && (dtePublicacao '= "") ) {
		set strStatus = "A data de publicação da ata não pode ser menor que a data de homologação. Favor verificar."
	
	} elseif (dteValidade < dteAssinatura) {
		set strStatus = "A data de validade da ata não pode ser menor que a data de homologação. Favor verificar."
	}
	
	if $$$ISOK(strStatus) {
		//Verifica se a ata já foi cadastrada
		new idAta, noAta, noAtaAtual
		
		set noAtaAtual = $piece(pYFELD,Y,1)
		
		$$$Order2(^VARAta,YM,idAta)			
			
			continue:(idAta = pYKEY)
			
			set noAta = $$GetNumeroAta(idAta)			
			if (noAta = noAtaAtual) {
				set strStatus = "Já existe uma ata cadastrada com o número "_noAtaAtual_". O código da ata é o "_idAta_"."
				quit
			}
	
		$$$End
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}	
	
	quit
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
	; Carrega a tabela de ordens de fornecimento geradas a partir da ata.
    ; 
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")

	if $$GetStatusAta(pYKEY) > 0 {
		do PrintTableCompras(pYKEY)		
	}
 	 	 	 	 	 
 	quit
	
OnAfterPrimaryKey(pYKEY,pYFELD="")
	;-------------------------------------------------------------------------------
    ; Insere mensagem na tela a respeito da ata.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	if $$GetStatusAta(pYKEY) = 1 { ;1-Processado
		do ShowMensagemAta(pYKEY)
	}
 
	quit
	
ShowMensagemAta(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se a ata possui alguma prorrogação ou se está vencida, e caso afirmativo,
    ; mostra uma mensagem na tela para o usuário.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new dteToday, dteValidade, NoAditivo, objAditivo, tipoAditivo, blnAlerta, isProcessado
	
	set dteToday = $piece($horolog,",",1)
	set dteValidade = $$GetValidadeAta(pYKEY)
 
	if ((dteValidade < dteToday) && (dteValidade '= "")){
		w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:5px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
		w "	<font color=red size=2>"
		w "		Atenção: o prazo de vigência desta ata venceu em <strong>"_$zdate(dteValidade,4)_"</strong>."
		w "	</font>"
		w "</div>"
	
	} elseif (dteValidade >= dteToday) {	
		/*
		set blnAlerta = $$$NO
		set blnAlerta = $$blnAtaProrrog(pYKEY)
	
		if blnAlerta = $$$YES {
			w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:5px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
			w "	<font color=blue size=2>"
			w "		Atenção: esta ata foi prorrogada, e sua vigência foi extendida até <strong>"_$zdate(dteValidade,4)_"</strong>."
			w "	</font>"
			w "</div>"
		}
		*/
	}
	
	quit	

CanProcessar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o status da ata admite ser processado e se a ata possui itens
    ; duplicados.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
 	new objAta, blnItensRepetidos
 	set objAta = $get(^VARAta(YM,pYKEY,1)) 	

	if ($$$VARAtaStatus(objAta) > 0) {
		set YQ = $$$YQDisable("Esta ata não está em aberto.")
	
	} elseif ('$data(^VARAtaLinha(YM,pYKEY))) {
		set YQ = $$$YQDisable("Nenhum produto inserido na ata.")		
		
	} else {
		if ($$blnRepeatedItems(pYKEY) = $$$YES) {
			set YQ = $$$YQDisable("Não é possível processar pois existem produtos repetidos nesta ata.")
			
		} else {		
			set YQ = $$$YQEnable
		}
	}
	
	quit
	
CanRevisar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se a ata admite ser revisada.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
 	quit:(pYKEY = "")
 	
 	new objAta
 	set objAta = $get(^VARAta(YM,pYKEY,1)) 
	
	if ($$isAtaInUse(pYKEY) = $$$YES) {
		set YQ = $$$YQDisable("Não é possível revisar pois já foi gerada compra a partir desta ata. Consulte a tabela na parte inferior da tela para verificar as compras que foram geradas.")
	
	} elseif ($$$VARAtaStatus(objAta) = 0) {
		set YQ = $$$YQDisable("Esta ata já está em modo de edição.")
	
	} else { 
		set YQ = $$$YQEnable
	}
	
	quit	
	
Processar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Seta o status da ata para processado.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new strStatus, objAta
	set strStatus = $$$OK
	
	set objAta = $get(^VARAta(YM,pYKEY,1))			
	
	if ($$$VARAtaStatus(objAta) = 0) {
	
		set $$$VARAtaStatus(objAta) = 1 ;1-Processado
		
		set strStatus = $$$Save("VARAta",pYKEY,objAta,1)
	
		if $$$ISERR(strStatus) { 
			$$$StartScript()
			w "alert('Não foi possível processar a ata. Favor verificar.');"
			$$$EndScript()
		}	
		
		do GoToForm^COMUtilForm("VARAta",pYKEY)	
	}
		
	quit

RevisarAta(pYKEY)
	;-------------------------------------------------------------------------------
    ; Seta o status da ata para 0-Em Aberto.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new strStatus, objAta
	set strStatus = $$$OK
	
 	set objAta = $get(^VARAta(YM,pYKEY,1)) 			
	
	if ($$$VARAtaStatus(objAta) = 1) {
	
		set $$$VARAtaStatus(objAta) = 0 ;0-Em Aberto
		
		set strStatus = $$$Save("VARAta",pYKEY,objAta,1)
	
		if $$$ISERR(strStatus) { 
			$$$StartScript()
			w "alert('Não é possível revisar a ata. Favor verificar.');"
			$$$EndScript()
		}	
		
		do GoToForm^COMUtilForm("VARAta",pYKEY)	
	}
		
	quit

isAtaInUse(pidAta)
	;-------------------------------------------------------------------------------
    ; Retorna se a ata está em uso, ou seja, se foi feita alguma compra (INAUF) a 
    ; partir desta ata.
    ;
    ; History:
    ; 27-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
    quit:(pidAta = "") $$$NO	

	new blnAtaInUse
	set blnAtaInUse = $$$NO

	if $data(^INAUFPs(YM,103,$$$Index(pidAta)))  {
		set blnAtaInUse = $$$YES
	}

	quit blnAtaInUse
	
blnRepeatedItems(pYKEY)
	;-------------------------------------------------------------------------------
    ; Retorna sim ou não se existem produtos repetidos na ata.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
    quit:(pYKEY = "") $$$NO	

	new blnRepeatedItems, idAtaLinha, idProduto, idLinhaSearch, idProdutoSearch
	
	set blnRepeatedItems = $$$NO
		
	$$$Order3(^VARAtaLinha,YM,pYKEY,idAtaLinha)
		
		quit:(blnRepeatedItems = $$$YES)			
		set idProduto = $piece($get(^VARAtaLinha(YM,pYKEY,idAtaLinha,1)),Y,1)		
		
		$$$Order3(^VARAtaLinha,YM,pYKEY,idLinhaSearch)
			
			continue:(idLinhaSearch = idAtaLinha)
			
			set idProdutoSearch = $piece($get(^VARAtaLinha(YM,pYKEY,idLinhaSearch,1)),Y,1)			
			if (idProdutoSearch = idProduto) {				
				set blnRepeatedItems = $$$YES
				quit
			}
		
		$$$End	
	$$$End	
	
	quit blnRepeatedItems

PrintTableCompras(pidAta)
	;-------------------------------------------------------------------------------
    ; Exibe tabela com as compras (ordens de fornecimento) que foram geradas a partir
    ; da ata selecionada.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pidAta = "")
	
	new lstHeader, countCompra, idAtaLinha, idCompra, objCompra, idFornecedor, dteCompra,
		empenho, idCompraLinha, objCompraLinha, idProduto, qtyComprada, qtyTotalRecebida, descProduto

	//Monta cabeçalho da tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Data","Nº Compra","Nº Empenho","Fornecedor","Produto","Quant. Compra","Quant. Recebida")

	set countCompra = 0
    			    							 	
  	if $$Start^COMTable(lstHeader,"Compras geradas a partir desta ata:",,,$$$NO) {

 		//Compras
 		$$$Order4(^INAUFPs,YM,103,$$$Index(pidAta),idAtaLinha)

 			$$$Order5(^INAUFPs,YM,103,$$$Index(pidAta),idAtaLinha,idCompra)
 				
 				set objCompra    = $get(^INAUF(YM,idCompra,1))
 				set idFornecedor = $$$INAUFSupplierNumber(objCompra)
 				set dteCompra    = $$$INAUFOrderDate(objCompra)
 				set empenho      = $$$INAUFApproval1(objCompra)
 				
 				$$$Order6(^INAUFPs,YM,103,$$$Index(pidAta),idAtaLinha,idCompra,idCompraLinha)

 					set objCompraLinha = $get(^INAUFP(YM,idCompra,idCompraLinha,1))
		    		
		    		set idProduto   = $$$INAUFPItemNumber(objCompraLinha)
		    		set qtyComprada = $$$INAUFPQuantity(objCompraLinha)
		    		
					set qtyTotalRecebida = $$getQuantidadeRecebida^VARCompra(idCompra,idCompraLinha) - $$getQuantidadeEstornada^VARCompra(idCompra,idCompraLinha)		  			
		  			set countCompra = $i(countCompra)
		  			   				            				              	      	     	   		       			  		
   					do NewLine^COMTable()
   					do InsertCell^COMTable($zdate(dteCompra,4),"VARCompra",idCompra)
   					do InsertCell^COMTable(idCompra,"VARCompra",idCompra)
   					do InsertCell^COMTable(empenho,"VARCompra",idCompra)
   					do InsertCell^COMTable("("_idFornecedor_") "_$$SQLGetDescFornecedor^VARSQL(idFornecedor),"VARCompra",idCompra,,,,,,,$$$YES)
   					do InsertCell^COMTable("("_idProduto_") "_$$SQLGetDescricaoProduto^VARSQL(idProduto),"VARCompra",idCompra,,,,,,,$$$YES)
					do InsertCell^COMTable($$^WWWTR(0,12,qtyComprada),"VARCompra",idCompra,,"RIGHT")
					do InsertCell^COMTable($$^WWWTR(0,12,qtyTotalRecebida),"VARCompra",idCompra,,"RIGHT")
  		    		do EndLine^COMTable()
	 			
	 			$$$End

 			$$$End
 		$$$End
		
 		if (countCompra = 0) {
	 		do NewLine^COMTable()
	 		do InsertCell^COMTable("Nenhuma compra gerada a partir desta ata.",,,,,,7)
	 		do EndLine^COMTable()
 		}
 		
 		do Stop^COMTable()
  	}

	quit	

GetValorTotalAta(pYKEY)
	;-------------------------------------------------------------------------------
    ; Retorna o valor total da ata de registro de preços.
    ;
    ; History:
    ; 20-Sep-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "") ""
	
	new idAtaLinha, quantLinha, valorLinha, totalLinha, totalAta
	
	set totalAta = 0
	
	$$$Order3(^VARAtaLinha,YM,pYKEY,idAtaLinha)
	
		set quantLinha = $piece($get(^VARAtaLinha(YM,pYKEY,idAtaLinha,1)),Y,2)
		set valorLinha = $piece($get(^VARAtaLinha(YM,pYKEY,idAtaLinha,1)),Y,4)
	
		set totalLinha = quantLinha * valorLinha
		set totalAta   = totalAta + totalLinha	
	
	$$$End
	
	quit totalAta

GetCNPJFornecedor(pYKEY)
	quit:($$$KEY2(pYKEY) = "") ""

	new idFornecedor
	set idFornecedor = $piece($get(^VARAtaLinha(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1)),Y,5)

	quit $$getCNPJ^VARINLIEF(idFornecedor)
	
GetNumeroAta(pYKEY)
	quit:(pYKEY = "") ""
	quit $piece($get(^VARAta(YM,pYKEY,1)),Y,1)

GetValidadeAta(pYKEY)
	if (pYKEY = "") quit ""
	quit $piece($get(^VARAta(YM,pYKEY,1)),Y,4)
	
GetStatusAta(pYKEY)
	if (pYKEY = "") quit ""
	quit $piece($get(^VARAta(YM,pYKEY,1)),Y,16)	
	
OnBlurDataHomologacao(YINHALT,YFELD)
	;-------------------------------------------------------------------------------
    ; Preenche o campo "Data de Validade", como padrão +365 dias = 1 ano
    ; do OnBlurDataHomologacao^VARAta(YINHALT,YFELD)
    ;
    ; History:
    ; 09-Out-2011	Karine	Created
    ;-------------------------------------------------------------------------------	
	
	new dtValidade
	
	;; EM TESTE - COMENTADO NO CAMPO E COMITADO PARA SUBIR ALTERAÇÕES MAIS URGENTES
	
	if YINHALT '= "" {
		
		set dtValidade = YINHALT + 365
		set dtValidade = $zdate(dtValidade)
		if dtValidade '= "" {
				; set Conselho
				set %TXT(1)=%TXT(1)_"#Y"_YFORM_"D4~"_dtValidade
		}		}
	quit
	
CanViewReport(pKEY)
	new objAta
	if $$$NoKey(pKEY) {
		set YQ = $$$YQDisable("Selecione um registro.")
	}else{
		set objAta = ^VARAta(0,pKEY,1)

		if ($$$VARAtaStatus(objAta) > 0)  {
			set YQ = $$$YQEnable
		}else{
			set YQ = $$$YQDisable("A Ata tem que ser processada para poder gerar o relatório.")
		}
	}
	quit

ImprimeRelatorio(YKEY)
	
		do RunReportAta^VARJasperRunReport(YKEY)
	
		
	quit
]]></Routine>
</Export>