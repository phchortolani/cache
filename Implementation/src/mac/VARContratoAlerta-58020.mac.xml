<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContratoAlerta" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContratoAlerta
#include COMSYS
 
OnAfterSave
	new Tipo, Categoria, Contratada, Confirmado, dteInicial, dteFinal, duracao
  
	set Tipo = $get(VORG(1))
 
	//Gambi pra consertar o bug no OnBlur do campo e after submit setando um valor no campo
	if $get(VORG(2)) '= "" {
		set Categoria = $get(VORG(2))
	} else {
		set Categoria = ""
	}
 
	set Contratada = $get(VORG(3))
	set Confirmado = $get(VORG(4))
	set dteInicial = $get(VORG(5))
	set dteFinal   = $get(VORG(6))
	set duracao    = $get(VORG(7))
 	
 	if Categoria = "" set $piece(^VARTempAlertaContrato(YBED),Y,2) = ""
 	
	if ((dteInicial '= "") && (duracao '= "")) || ((dteFinal '= "") && (duracao '= "")) {
		$$$Alert("Favor selecionar a data inicial e/ou a data final, ou somente uma duração. A consulta não foi realizada.")
		
	} elseif (dteInicial < $piece($h,",",1) && (dteInicial '= "")) || (dteFinal < $piece($h,",",1) && (dteFinal '= "")) {	
		$$$Alert("Não é possível consultar uma data menor que a data atual. Essa é uma consulta de alerta de término de vigência. Para consultar contratos já vencidos, utilize a Pesquisa de Contratos.")	
 
	} elseif (Tipo = "") && (Categoria '= "") {	
		$$$Alert("Você informou a categoria mas não definiu o tipo de contrato. Favor verificar. A consulta não foi realizada.")	
 
	} else {
		set $piece(^VARTempAlertaContrato(YBED),Y,1) = Tipo
		set $piece(^VARTempAlertaContrato(YBED),Y,2) = Categoria
		set $piece(^VARTempAlertaContrato(YBED),Y,3) = Contratada
		set $piece(^VARTempAlertaContrato(YBED),Y,4) = Confirmado
		set $piece(^VARTempAlertaContrato(YBED),Y,5) = dteInicial
		set $piece(^VARTempAlertaContrato(YBED),Y,6) = dteFinal
		set $piece(^VARTempAlertaContrato(YBED),Y,7) = duracao
	}
	
	do VARLogAfterSave^VARUtil(YBED,YFORM)
	do GoToForm^COMUtilForm("VARContratoAlerta",1)
	
	quit
 
 
OnAfterDataFields
	;-------------------------------------------------------------------------------
    ; Mostra a tabela de alerta de término de vigência.
    ; 
    ; History:
    ; 03-Mai-2012	Gustavo  Incluído VARLogAfterSave
    ; 14-Aug-2009	Gustavo  Created
    ;-------------------------------------------------------------------------------
	new lstHeader, NoProcessoCompra, Tipo, Categoria, Contratada, DatadeTerminodaVigencia, count, dteHoje,
		prazo, strBotaoVerMais, hint, isNotificado, dataNotificado, CodContrato, alertHint, userNotificado

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	//Filtros	
	new filterTipo, filterCategoria, filterContratada, filterConfirmado, filterDteInicial, filterDteFinal,
		filterDuracao
		
	set filterTipo 		 = ""
	set filterCategoria  = ""
	set filterContratada = ""
	set filterConfirmado = ""
	set filterDteInicial = ""
	set filterDteFinal   = ""
	set filterDuracao    = ""
	
	kill ^VARAlertaUnico
	set ^VARAlertaUnico = 0
	
	if $data(^VARTempAlertaContrato(YBED)) > 0 {
			
		set filterTipo 		 = $piece($get(^VARTempAlertaContrato(YBED)),Y,1)
		set filterCategoria  = $piece($get(^VARTempAlertaContrato(YBED)),Y,2)
		set filterContratada = $piece($get(^VARTempAlertaContrato(YBED)),Y,3)
		set filterConfirmado = $piece($get(^VARTempAlertaContrato(YBED)),Y,4)
		set filterDteInicial = $piece($get(^VARTempAlertaContrato(YBED)),Y,5)
		set filterDteFinal   = $piece($get(^VARTempAlertaContrato(YBED)),Y,6)
		set filterDuracao    = $piece($get(^VARTempAlertaContrato(YBED)),Y,7)
	}
	
	if filterDuracao = "" set filterDuracao = 90
 	
 	set dteHoje = $piece($horolog,",",1)
 	
	&sql(DECLARE ContratosComAlerta CURSOR FOR
		 SELECT CodContrato,Tipo,Categoria,Contratada,$$GetVigenciaContrato^VARContrato(CodContrato) as DatadeTerminodaVigencia, NoContrato
		 INTO :CodContrato,:Tipo,:Categoria,:Contratada,:DatadeTerminodaVigencia,:NoContrato
		 FROM VARContrato
		 WHERE $$GetVigenciaContrato^VARContrato(CodContrato) >= :dteHoje
		   AND Celebrado = 1
		 ORDER BY DatadeTerminodaVigencia ASC
	)
 	
	&sql(open ContratosComAlerta)
    if (SQLCODE) {
		quit
    }
 	
	//Tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("","Confirmado Por","","","Seq.","No.","Tipo","Categoria","Contratada","Término de Vigência","Prazo Restante")
  	
  	set count = 0
 
  	if $$Start^COMTable(lstHeader,"Contratos com alerta de término de vigência:") {
	  	
		for { &sql(fetch ContratosComAlerta)
				   quit:SQLCODE
 			
			set prazo = (DatadeTerminodaVigencia - dteHoje) + 1 ;o 1 representa o dia de hoje
			continue:(prazo > 180)
			
			if filterTipo 	    '= "" continue:(Tipo '= filterTipo)
			if filterCategoria  '= "" continue:(Categoria '= filterCategoria)
			if filterContratada '= "" continue:(Contratada '= filterContratada)
			if filterDteInicial '= "" continue:(DatadeTerminodaVigencia < filterDteInicial)
			if filterDteFinal   '= "" continue:(DatadeTerminodaVigencia > filterDteFinal)
			if filterDuracao    '= "" continue:(prazo > filterDuracao)
			
			set count = $i(count)
			
			set hint = "Objeto: "_$$GetObjetoContrato^VARContrato(CodContrato)
			set strBotaoVerMais = "<IMG SRC="_YGIF_"open.gif TITLE='"_hint_"' border=0 align=""center"" >"
			
			set isNotificado = $$$NO
			set dataNotificado = ""
			set userNotificado = ""
			set dataNotificado = $piece($get(^VARContrato(YM,CodContrato,1)),Y,30)
			
			if (dataNotificado '= "") {
				set isNotificado = $$$YES
				set userNotificado = $extract($$SQLGetUserName^VARSQL($piece($get(^VARContrato(YM,CodContrato,1)),Y,31)),1,15)
			}
			
			if filterConfirmado '= "" continue:(isNotificado = $$$YES)
			
			set alertHint = "Atenção: resta(m) "_prazo_" dia(s) para o término deste contrato."
 
			if (prazo <= 30) {
				set strBotaoAtencao = "<IMG SRC="_YGIF_"alertRed.gif"_" TITLE='"_alertHint_"' border=0 align=""center"" >"
			}
			elseif ((prazo > 30) && (prazo <= 60)) {
				set strBotaoAtencao = "<IMG SRC="_YGIF_"alertYellow.gif"_" TITLE='"_alertHint_"' border=0 align=""center"" >"
			}
			elseif ((prazo > 60) && (prazo <= 90)) {
				set strBotaoAtencao = "<IMG SRC="_YGIF_"alertBlue.gif"_" TITLE='"_alertHint_"' border=0 align=""center"" >"
			}
			else {
				set strBotaoAtencao = ""
			}
			
			
			do NewLine^COMTable()
			do InsertCellCheckbox^COMTable(CodContrato,"SelectContrato^VARContratoAlerta",isNotificado,isNotificado)
		    do InsertCell^COMTable(userNotificado)
		    do InsertCell^COMTable(strBotaoAtencao)	
		    do InsertCell^COMTable(strBotaoVerMais,"VARContrato",CodContrato)	
	    	do InsertCell^COMTable(count,"VARContrato",CodContrato)
	    	do InsertCell^COMTable(NoContrato,"VARContrato",CodContrato)
	    	do InsertCell^COMTable($$GetDescTipoContrato^VARContrato(CodContrato),"VARContrato",CodContrato,,,,,,,1)
	    	do InsertCell^COMTable($$GetDescCategoriaContrato^VARContrato(CodContrato),"VARContrato",CodContrato,,,,,,,1)
	    	do InsertCell^COMTable(Contratada_" - "_$extract($$SQLGetDescFornecedor^VARSQL(Contratada),1,30),"VARContrato",CodContrato,,,,,,,1)
	    	do InsertCell^COMTable($zdt(DatadeTerminodaVigencia,4),"VARContrato",CodContrato)
	    	do InsertCell^COMTable(prazo_" dia(s)","VARContrato",CodContrato)
	    	do EndLine^COMTable()	
		}
		
		set strBotaoConfirma = "<IMG SRC="_YGIF_"AEPicoCloseDealOn.gif TITLE='Clique para confirmar o recebimento da notificação do término de vigência dos contratos selecionados.' border=0 align=""center"" >"
					
		do NewLine^COMTable()
		do InsertCellCallback^COMTable(strBotaoConfirma,"ConfirmaAlerta^VARContratoAlerta","")
		
		if (count = 0) {
			do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,10)
		}
		
		do EndLine^COMTable()
		
		do Stop^COMTable()
  	}
  	
	&sql(close ContratosComAlerta)
	
	quit
	
SelectContrato(pYKEY="",checked="")
	if ($piece(checked,",",1) = "true") {
		set ^VARTempAlertaContratoCheck(YM,YBED,pYKEY) = 1
	}
	elseif ($piece(checked,",",1) = "false") {
		kill ^VARTempAlertaContratoCheck(YM,YBED,pYKEY)
	}
 
	quit $$$YES
	
ConfirmaAlerta(param)
	new strStatus, CodContrato, objContrato
	
	set strStatus = $$$OK
	
	if $data(^VARTempAlertaContratoCheck(YM,YBED)) = 0 {
		set ^VARAlertaUnico = $Increment(^VARAlertaUnico)
		
		if (^VARAlertaUnico > 1){
			kill ^VARAlertaUnico
			quit
		}
		
		$$$Alert("Nenhum contrato foi selecionado.")
	
		quit
	}	
	
	$$$Order3(^VARTempAlertaContratoCheck,YM,YBED,CodContrato)
		
		set objContrato = $get(^VARContrato(YM,CodContrato,1))
		set $piece(objContrato,Y,30) = $horolog   ;Notificação Confirmada Em
		set $piece(objContrato,Y,31) = YBED		  ;Notificação Confirmada Por
		
		set strStatus = $$$Save("VARContrato",CodContrato,objContrato,1)
		kill ^VARTempAlertaContratoCheck(YM,YBED,CodContrato) ;Limpa a temp depois que salvou
	
	$$$End
 
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	}
	
	set ^VARAlertaUnico = $Increment(^VARAlertaUnico)

	$$$Alert("Notificações confirmadas com sucesso.")
	do RefreshForm^COMUtilForm()
		
	quit
	
	
EnviarEmailAlertaVigencia(pLimit)
	;-------------------------------------------------------------------------------
    ; Envia o email de alerta do término de vigência de contratos.
    ; 
    ; History:
    ; 26-Nov-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	; w $$EnviarEmailAlertaVigencia^VARContratoAlerta("90")
	
	$$$VAR
		
	//Consulta os Contratos com Alerta de Vigência para a quantidade de dias enviada no parâmetro
	new dteHoje, dteLimit, CodContrato, Tipo, Categoria, Contratada, DatadeTerminodaVigencia, NoContrato,
		Mensagem, Remetente, EmailDestino, Assunto, count, Objeto, strDestinatarios, User
	
	set strStatus = $$$OK
	set dteHoje   = $piece($horolog,",",1)
 	set dteLimit  = dteHoje + pLimit
 	set CompanyName = $$SQLGetCompanyName^VARSQL()
 	
	&sql(DECLARE AlertaContratos CURSOR FOR
		 SELECT CodContrato,Tipo,Categoria,Contratada,$$GetVigenciaContrato^VARContrato(CodContrato) as DatadeTerminodaVigencia, NoContrato
		 INTO :CodContrato,:Tipo,:Categoria,:Contratada,:DatadeTerminodaVigencia,:NoContrato
		 FROM VARContrato
		 WHERE $$GetVigenciaContrato^VARContrato(CodContrato) >= :dteHoje
		  AND $$GetVigenciaContrato^VARContrato(CodContrato) <= :dteLimit
		  AND Celebrado = 1
		  AND NotificacaoConfirmadaEm is null
		 ORDER BY $$GetVigenciaContrato^VARContrato(CodContrato) ASC
	)
 
	&sql(open AlertaContratos)
    if (SQLCODE) {
 	   &sql(close AlertaContratos)
	   quit $$$NO
    }
	
	set count = 0
	
	set Mensagem = "(Email enviado automaticamente pelo sistema)<br /><br />"
	set Mensagem = Mensagem_"<strong>Atenção, os seguintes contratos possuem vigência terminando nos próximos "_pLimit_" dias:</strong><br />"
 
	for { &sql(fetch AlertaContratos)
			   quit:SQLCODE
		
		set count = $i(count)
		set Mensagem = Mensagem_count_". "_$$GetDescTipoContrato^VARContrato(CodContrato)_" No. "_NoContrato
		
		set Categoria = $$GetDescCategoriaContrato^VARContrato(CodContrato)
		if (Categoria '= "") {
			set Mensagem = Mensagem_" - Categoria: "_Categoria
		}
		
		set Mensagem = Mensagem_" - Contratada: ("_Contratada_") "_$extract($$SQLGetDescFornecedor^VARSQL(Contratada),1,40)
		
		set Objeto = $$GetObjetoContrato^VARContrato(CodContrato)
		set Mensagem = Mensagem_" - Objeto: "_$extract(Objeto,1,100)
		if ($length(Objeto) > 100) set Mensagem = Mensagem_"..."
				
		set Mensagem = Mensagem_" - Vigência: "_$zdate(DatadeTerminodaVigencia,4)_"<br />"
	}
 	
    &sql(close AlertaContratos)
 	
	 //Caso exista algum alerta... envia o e-mail.
	 if (count > 0) {
	 	set Mensagem = Mensagem_"<br />--<br />"
	 	set Mensagem = Mensagem_"Módulo de Gestão de Contratos<br />"
	 	set Mensagem = Mensagem_CompanyName
	 	
		;Envia o email
		set Remetente 	  = "GerenciaDeContratos"
		set Assunto 	  = "Alerta de término de vigência de contratos - "_$zdate($horolog,4)
 
		set strDestinatarios = $piece($get(^VARContratoParametrosEmail(YM,YM,1)),Y,2)
 
	 	//Envia o email para os destinatários			
		for cnt = 1:1:$length(strDestinatarios,";") {
	    	set User 		 = $piece(strDestinatarios,";",cnt)
    		set EmailDestino = $piece($get(^WWW013(YM,User,1)),Y,9)
    		
			continue:(EmailDestino = "")
			continue:($$IsValidEmailAddress^VAREmail(EmailDestino) = $$$NO)
	    	
			set strStatus = $$EnviaEmailContratos^VAREmail("GerenciaDeContratos",User,Assunto,Mensagem)
		}
	 }
 
	quit strStatus
]]></Routine>
</Export>