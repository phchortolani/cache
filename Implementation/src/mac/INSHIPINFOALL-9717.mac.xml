<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSHIPINFOALL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSHIPINFOALL   ;INSHIPINFOALL;WEM;ANZEIGEN ALLER AUFTRAGSPOSITIONEN OHNE TRANSPORTNUMMER;24.03.2004  ; Compiled February 15, 2005 10:05:20
	#include COMSYS
	#include INConst
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN ALLER AUFTRAGSPOSITIONEN OHNE TRANSPORTNUMMER
	;		ANNOUNCEMENTS OF ALL ORDER ITEMS WITHOUT TRANSPORTATION NUMBER
	;
	; DO ^INSHIPINFOALL
	;
	;---------------------------------------
	; BIETET DIE MÖGLICHKEIT ALLEN AUFTRAGSPOSITIONEN, DIE NOCH KEINE TRANSPORTNUMMER
	; HABEN EINE SOLCHE ZUZUWEISEN UND DANN DIE DATEN IN DIE INAUFPA ZU SCHREIBEN.
	; 
	; ANHAND DER TRANSPORTNUMMER WIRD DER LIEFERANT ERMITTELT UND ALLE NOCH NICHT
	; GESCHLOSSENEN AUFTRÄGE DIESES LIEFERANTEN, DIE NOCH KEINE TRANSPORTNUMMER
	; HABEN, WERDEN DANN ANGEZEIGT.
	; 
	; ZUDEM WERDEN SCHON ALLE ZUGEWIESENENE AUFTRÄGE FÜR DIESE TRANSPORTNUMMER
	; EBENFALLS ANGEZEIGT. 
	;---------------------------------------
	; THE POSSIBILITY OFFERS TO ALL ORDER ITEMS, THE NOR NO TRANSPORTATION NUMBER;
	; HAVE SUCH TO ASSIGN AND THEN THE DATA INTO THE INAUFPA TO LETTERS.
	; 
	; ON THE BASIS THE TRANSPORTATION NUMBER THE SUPPLIER IS DETERMINED AND ALL
	; NOT YET CLOSED ORDERS OF THIS SUPPLIER, THE NOR NO TRANSPORTATION NUMBER
	; IS THEN INDICATED TO CREDIT.
	; 
	; BESIDES ALREADY ALL ZUGEWIESENENE (assigned?) ORDERS FOR THIS TRANSPORTATION
	; NUMBER LIKEWISE INDICATED.
	;---------------------------------------
	; 
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 17-Oct-2005	PO		SR13056: Pass across the Import Shipment id to the SORT procedure.
	;  5-Jul-2005	JW		SR12770: Sort assigned table by import number
	; WEM	24.03.2004  
	;-----------------------------------------------------------------------------*/
	NEW AUF,POS,LF,AB,ANZ,LIEF,TYP,UES,GEWG,AUF1
	
	SET VORG(1)=$GET(VORG(1))
	;VORG(1) = IMPORT NUMBER
	SET LIEF=""
	IF VORG(1)'="" SET LIEF=$PIECE($GET(^INSHIPINFO(YM,VORG(1),1)),Y,2)
	IF VORG(1)'="" SET SHOWQTY=1 ;SR10950
	
	SET YDDSATZ=0
	SET TYP=0
	SET GEWG=0
	SET UES=""
	
	KILL ^WWWSOR(YUSER)
	
	IF LIEF="" DO  QUIT			// Unassigned table
	. DO START100^WWWTAB
	. DO UEB()
	. DO LEER^WWWTAB(20,$LENGTH(UES,Y))
	. DO STOP^WWWTAB
	
	DO JAVASCRIPT
	
	SET ANZ=0
	DO SORT(VORG(1))
	
	IF ANZ>0 DO 
	. WRITE $$^WWWFELDNAME("INLIEF","P",1)_": ("_LIEF_") "_$PIECE($GET(^INLIEF(YM,LIEF,1)),Y,8)
	. DO SELECT   ; SELECT / DESELECT ALL FIELDS
	
	WRITE YCR
	
	DO START100^WWWTAB
	DO UEB()
	IF ANZ=0 DO  QUIT   ; BLANK LINES IF NO DATA AVAILABLE 
	. DO LEER^WWWTAB(20,$LENGTH(UES,Y))
	. DO STOP^WWWTAB
	. KILL ^WWWSOR(YUSER)
	
	SET ANZ=0
	DO DRUCK
	KILL ^WWWSOR(YUSER)
	QUIT
	
SORT(idImportShipment="")
    /*------------------------------------------------------------------------------
    ; DATEN AUFBEREITEN
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Oct-2005	PO		SR13056: Only include orders for the current import shipment.
    ;-----------------------------------------------------------------------------*/
	set LF=$$^WWWUMLAU(LIEF,1)
	FOR ABGE=$$^WWWUMLAU(" ",1),$$^WWWUMLAU(0,1) DO
	. SET AUF="" FOR  SET AUF=$ORDER(^INAUFPs(YM,11,ABGE,LF,AUF)) QUIT:AUF=""  DO
	. . SET AUF1=$GET(^INAUF(YM,AUF,1))
	. . QUIT:$PIECE(AUF1,Y,83)=""   ; BESTELLUNG NOCH NICHT GEDRUCKT ;sales order yet Not 
	. . QUIT:$PIECE(AUF1,Y,2)'=2    ; AUFTRAGSART NICHT LF-BESTELLUNG     ;Not 
	. . QUIT:$PIECE(AUF1,Y,12)=""   ; KEIN LF ;no 
	. . QUIT:'$DATA(^INLIEF(YM,$PIECE(AUF1,Y,12)))   ; KEIN GÜLTIGER LF ;no 
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^INAUFPs(YM,11,ABGE,LF,AUF,POS)) QUIT:POS=""  DO
	. . . IF $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,9)=1 QUIT  ;STORNO;FIS;15.02.05;27340
	. . . IF '$DATA(^INAUFPA(YM,AUF,POS)) SET ^WWWSOR(YUSER,"UNSORTED",AUF,POS)="" SET ANZ=ANZ+1 QUIT
	. . . SET AB=""
	. . . FOR  SET AB=$ORDER(^INAUFPA(YM,AUF,POS,LIEF,AB)) QUIT:AB=""  DO
	. . . . SET SATZ=$GET(^INAUFPA(YM,AUF,POS,LIEF,AB,1))
	. . . . ;IF $PIECE(SATZ,Y,61)'="" SET ^WWWSOR(YUSER,"SORTED",AUF,POS)="" SET ANZ=ANZ+1 QUIT
	. . . . if $PIECE(SATZ,Y,61)=idImportShipment SET ^WWWSOR(YUSER,"SORTED",$PIECE(SATZ,Y,61),AUF,POS)="" SET ANZ=ANZ+1 QUIT	// SR12770
	. . . . if $PIECE(SATZ,Y,61)=idImportShipment SET ^WWWSOR(YUSER,"UNSORTED",AUF,POS)="" SET ANZ=ANZ+1
	
	QUIT
 
	/* Now only need to display orders for current import shipment
	SET LF=$$^WWWUMLAU(LIEF,1)
	FOR ABGE=$$^WWWUMLAU(" ",1),$$^WWWUMLAU(0,1) DO
	. SET AUF="" FOR  SET AUF=$ORDER(^INAUFPs(YM,11,ABGE,LF,AUF)) QUIT:AUF=""  DO
	. . SET AUF1=$GET(^INAUF(YM,AUF,1))
	. . QUIT:$PIECE(AUF1,Y,83)=""   ; BESTELLUNG NOCH NICHT GEDRUCKT ;sales order yet Not 
	. . QUIT:$PIECE(AUF1,Y,2)'=2    ; AUFTRAGSART NICHT LF-BESTELLUNG     ;Not 
	. . QUIT:$PIECE(AUF1,Y,12)=""   ; KEIN LF ;no 
	. . QUIT:'$DATA(^INLIEF(YM,$PIECE(AUF1,Y,12)))   ; KEIN GÜLTIGER LF ;no 
	. . SET POS="" FOR  SET POS=$ORDER(^INAUFPs(YM,11,ABGE,LF,AUF,POS)) QUIT:POS=""  DO
	. . . IF $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,9)=1 QUIT  ;STORNO;FIS;15.02.05;27340
	. . . IF '$DATA(^INAUFPA(YM,AUF,POS)) SET ^WWWSOR(YUSER,"UNSORTED",AUF,POS)="" SET ANZ=ANZ+1 QUIT
	. . . SET AB="" FOR  SET AB=$ORDER(^INAUFPA(YM,AUF,POS,LIEF,AB)) QUIT:AB=""  DO
	. . . . SET SATZ=$GET(^INAUFPA(YM,AUF,POS,LIEF,AB,1))
	. . . . ;IF $PIECE(SATZ,Y,61)'="" SET ^WWWSOR(YUSER,"SORTED",AUF,POS)="" SET ANZ=ANZ+1 QUIT
	. . . . if $PIECE(SATZ,Y,61)'="" SET ^WWWSOR(YUSER,"SORTED",$PIECE(SATZ,Y,61),AUF,POS)="" SET ANZ=ANZ+1 QUIT	// SR12770
	. . . . SET ^WWWSOR(YUSER,"UNSORTED",AUF,POS)="" SET ANZ=ANZ+1
	QUIT
	*/
	
DRUCK   ; DRUCKEN DER TABELLE ;print the tabulation 
	new idImport
	
	; ZEIGEN OFFENE POSITIONEN ;exhibit 
	IF $DATA(^WWWSOR(YUSER,"UNSORTED")) DO
	. SET TYP=0
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^WWWSOR(YUSER,"UNSORTED",AUF)) QUIT:AUF=""  DO
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^WWWSOR(YUSER,"UNSORTED",AUF,POS)) QUIT:POS=""  DO
	. . . DO ANZ(TYP)
	. ;
	. DO STOP^WWWTAB
	
	; ZEIGEN ZUGEORDNETE POSITIONEN ;exhibit 
	IF $DATA(^WWWSOR(YUSER,"SORTED")) DO
	. SET TYP=1
	. IF $DATA(^WWWSOR(YUSER,"UNSORTED")) DO
	. . WRITE YCR,YCR
	. . WRITE "<br><br><b>"
	. . write $$^WWWTEXT(33823)
	. . WRITE YCR,YCR
	. . DO START100^WWWTAB
	. . DO UEB(TYP)
	. ;
	. SET idImport = ""
	. FOR  SET idImport=$ORDER(^WWWSOR(YUSER,"SORTED",idImport)) QUIT:idImport=""  DO  	// SR12770
	. . SET AUF=""
	. . FOR  SET AUF=$ORDER(^WWWSOR(YUSER,"SORTED",idImport,AUF)) QUIT:AUF=""  DO
	. . . SET POS=""
	. . . FOR  SET POS=$ORDER(^WWWSOR(YUSER,"SORTED",idImport,AUF,POS)) QUIT:POS=""  DO
	. . . . DO ANZ(TYP)
	. ;
	. DO STOP^WWWTAB
	
	QUIT
	
ANZ(TYP)
    /*------------------------------------------------------------------------------
	; ANZEIGEN DATEN -- ! ACHTUNG ! ZUSÄTZLICH EINSPRUNG AUS INSHIPINFOTL ;display estimation supernumerary out of 
	;
	; FOLGENDE VARIABLEN MÜSSEN DANN IN DER ANDEREN ROUTINE GENEWT WERDEN: ;should within the routine 
	; ANZ,TYP,AUF,POS,LIEF
	;
	; UND DIE DATEN MÜSSEN DANN ENTSPRECHEND AUFBEREITET WERDEN ;And who should fitting will 
	;
    ; Params:
	; TYP=0 UND TYP=1 DIESE ROUTINE ;And this routine 
	; TYP=2 EINSPRUNG AUS INSHIPINFOTL ;out of 
    ;
    ; Returns:
    ;
	; History:
	; 23-Mar-2005		RobertW		SR10969 - Show the foreign currency total if there is some.
    ;-----------------------------------------------------------------------------*/
	SET ANZ=ANZ+1
	SET TYP=+$GET(TYP)
	
	;IF TYP'=2 SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"K",ANZ)=AUF_Y_POS_Y_LIEF
	IF TYP'=2 SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YKEY"_ANZ,1)=AUF_Y_POS_Y_LIEF  ;FIS;05.01.05;27115
	
	WRITE YCR
	
	DO NL^WWWTAB
	DO NF^WWWTAB
	DO
	. NEW YKEY,YFORM,YBACK
	. DO
	. . IF TYP'=2 SET YBACK="INSHIPINFOALL," QUIT
	. . SET YBACK="INSHIPINFO,"
	. . QUIT
	. WRITE "<A HREF="_YAKTION_"EP=WWWFORM&amp;YFORM=INAUF&amp;YKEY="_AUF
	. DO ^WWWCGI
	. WRITE """"_" TITLE="""_$$^WWWTEXT(32047)_""""_">"   ; AUFTRAG ;order 
	. WRITE "<FONT SIZE=2>"
	. WRITE AUF
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	. WRITE "</A>"
	DO EF^WWWTAB
	
	DO NF^WWWTAB
	DO
	. NEW YKEY,YFORM,YBACK
	. DO
	. . IF TYP'=2 SET YBACK="INSHIPINFOALL," QUIT
	. . SET YBACK="INSHIPINFO,"
	. . QUIT
	. WRITE "<A HREF="_YAKTION_"EP=WWWFORM&amp;YFORM=INAUFP&amp;YKEY="_AUF_","_POS
	. DO ^WWWCGI
	. WRITE """"_" TITLE="""_$$^WWWTEXT(33487)_""""_">"   ; AUFTRAGSPOSITION
	. WRITE "<FONT SIZE=2>"
	. WRITE POS
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	. WRITE "</A>"
	DO EF^WWWTAB
	
	DO NF^WWWTAB
	DO
	. NEW YKEY,YFORM,YBACK,ART
	. SET ART=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,4)
	. DO
	. . IF TYP'=2 SET YBACK="INSHIPINFOALL," QUIT
	. . SET YBACK="INSHIPINFO,"
	. . QUIT
	. WRITE "<A HREF="_YAKTION_"EP=WWWFORM&amp;YFORM=INART&amp;YKEY="_ART
	. DO ^WWWCGI
	. WRITE """"_" TITLE="""_$$^WWWTEXT(32024)_""""_">"   ; ARTIKEL ;item 
	. WRITE "<FONT SIZE=2>"
	. WRITE ART
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	. WRITE "</A>"
	DO EF^WWWTAB
	
	;SR10950 - Show Order Quantity if printing from INSHIPINFOALL
	IF $GET(SHOWQTY)  DO 
	. DO NF^WWWTAB
	. DO
	. . NEW YKEY,YFORM,YBACK,ART
	. . SET ART=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,5)
	. . DO
	. . . IF TYP'=2 SET YBACK="INSHIPINFOALL," QUIT
	. . . SET YBACK="INSHIPINFO,"
	. . . QUIT
	. . WRITE "<A HREF="_YAKTION_"EP=WWWFORM&amp;YFORM=INAUFP&amp;YKEY="_AUF_","_POS
	. . DO ^WWWCGI
	. . WRITE """"_" TITLE="""_$$^WWWTEXT(34003)_""""_">"   ; Qty
	. . WRITE "<FONT SIZE=2>"
	. . WRITE ART
	. . WRITE "&nbsp;"
	. . WRITE "</FONT>"
	. . WRITE "</A>"
	. . QUIT
	. DO EF^WWWTAB
	
	DO NF^WWWTAB
	DO
	. NEW ARTBEZ
	. SET ARTBEZ=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,1)
	. WRITE "<FONT SIZE=2>"
	. WRITE $EXTRACT(ARTBEZ,1,50)
	. IF $LENGTH(ARTBEZ)>50 WRITE "..."
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	DO EF^WWWTAB
	
	DO
	. QUIT:TYP=2   ; KEINE ZUWEISUNGSSPALTE BEI EINSPRUNG AUS INSHIPINFOTL ;no next to out of 
	. DO NF^WWWTAB
	. IF TYP=0 DO  QUIT  ; WENN NOCH NICHT ZUGEORDNET INPUTFIELD ;when yet Not 
	. . WRITE "<INPUT NAME="_""""_"YSEL"_ANZ_""""
	. . WRITE " TYPE="_""""_"CHECKBOX"_""""
	. . DO ^WWWEVENTCALL("YSEL"_ANZ,0,3)
	. . WRITE ">"
	. . WRITE "</INPUT>"
	. . QUIT
	. WRITE "<INPUT NAME="_""""_"YSEL"_ANZ_""""   ; WENN SCHON ZUGEORDNET DANN DUMMY FELD WEGEN (DE-)SELECT JAVASCRIPT ;when yet field quibble 
	. WRITE " TYPE="_""""_"HIDDEN"_""""
	. WRITE ">"
	. NEW AB,TPN
	. SET AB=""
	. SET TPN=""
	. SET AB=$ORDER(^INAUFPA(YM,AUF,POS,LIEF,""))
	. IF AB'="" SET TPN=$PIECE($GET(^INAUFPA(YM,AUF,POS,LIEF,AB,1)),Y,61)
	. IF TPN'="" DO
	. . NEW YKEY,YFORM,YBACK
	. . DO
	. . . IF TYP'=2 SET YBACK="INSHIPINFOALL," QUIT
	. . . SET YBACK="INSHIPINFO,"
	. . . QUIT
	. . WRITE "<A HREF="_YAKTION_"EP=WWWFORM&amp;YFORM=INSHIPINFO&amp;YKEY="_TPN
	. . DO ^WWWCGI
	. .; WRITE """"_" TITLE="""_$$^WWWFELDNAME("INAUFPA","D",61)_""""_">"   ; TRANSPORTNUMMER
	. . WRITE """ TITLE="""_$$^WWWFELDNAME("INAUFPA","D",61)_""">"   ; Import Number  (Removed redundant concatenations)
	. . WRITE "<FONT SIZE=2>"
	. .; WRITE $$^WWWTEXT(33823)_" ("   ; SCHON ZUGEWIESEN ;yet 
	. . WRITE TPN ;_")"		// SR12770
	. . WRITE "</FONT>"
	. . WRITE "</A>"
	. . QUIT
	. WRITE "&nbsp;"
	. WRITE "</INPUT>"
	. DO EF^WWWTAB
	
	DO
	. QUIT:TYP'=2   ; MENGENFELD, MENGENEINHEIT, EAN-CODE UND GEWICHT NUR BEI EINSPRUNG AUS INSHIPINFOTL
	. DO NFR^WWWTAB
	. NEW MENGE,ME,GEW,COST,FCCOST
	. SET MENGE=+$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,5)
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWZAHL(MENGE,0,$LENGTH($PIECE(MENGE,".",2)))
	. WRITE "</FONT>"
	. DO EF^WWWTAB
	. ;
	. ;SR10950 - Show Received Qty
	. DO NFR^WWWTAB
	. SET RECEIVEDQTY=$$GetReceivedQuantity(AUF,POS)
	. WRITE "<FONT SIZE=2>"
	. WRITE RECEIVEDQTY
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	. DO EF^WWWTAB
	. ;
	. DO NF^WWWTAB
	. SET ME=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,40)
	. WRITE "<FONT SIZE=2>"
	. ;IF ME'="" IF SPRACHE'="" WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,ME,1)),Y,1)
	. IF ME'="" IF SPRACHE'="" WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,ME,1)),Y,1)    ;BEC;25866;07.06.04;DA ZENTRALE DATEI
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	. DO EF^WWWTAB
	. ;
	. DO NF^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. WRITE $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,2)
	. WRITE "&nbsp;"
	. WRITE "</FONT>"
	. DO EF^WWWTAB
	. ;
	. DO NFR^WWWTAB
	. SET GEW=+$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,43)
	. WRITE "<FONT SIZE=2>"
	. WRITE "&nbsp;"
	. IF GEW'=0 WRITE $$^WWWZAHL(GEW,0,$LENGTH($PIECE(GEW,".",2)))
	. WRITE "</FONT>"
	. SET GEWG=GEWG+GEW
	. DO EF^WWWTAB
	. ;
	. IF '$get(blnPutAway)  DO ;SR10951 - Omit Costs if PutAway Manifest
	. . DO NFR^WWWTAB
	. . SET COST=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,47)
	. . WRITE "<FONT SIZE=2>"
	. . WRITE "&nbsp;"
	. . IF (+COST'=0) WRITE COST ;$$^WWWZAHL(COST,0,$LENGTH($PIECE(GEW,".",2))) ; 23-Mar-2005	RobertW		SR10969: Make the currency correct, not rounded by the weight
	. . SET HK=$GET(HK)+COST
	. . WRITE "</FONT>"
	. . DO EF^WWWTAB
	. . ;
	. . ; 23-Mar-2005		RobertW		SR10969 - Show the foreign currency is there is some.
	. . do NFR^WWWTAB
	. . set FCCOST=$PIECE($GET(^INAUFPK(YM,AUF,POS,1)),Y,68)*MENGE
	. . write "<FONT SIZE=2>"
	. . write "&nbsp;"
	. . if $get(blnIsFC) do
	. . . if +FCCOST'=0 write $$FormatCurrency^COMTable(FCCOST,,strCurrency)
	. . . set FCHK=$get(FCHK,"")+FCCOST
	. . write "</FONT>"
	. . do EF^WWWTAB
	
	DO EL^WWWTAB
	
	QUIT
	;
	;
	
UEB(pblnAssigned=0)   ; ÜBERSCHRIFT FÜR DIE TABELLE ;superscription to who tabulation 
	if pblnAssigned {
		set strAssign = $$^WWWFELDNAME("INAUFPA","D",61)
	} else {
		set strAssign = $$^WWWTEXT(33821)
	}
	
	SET UES=$$^WWWTEXT(32047)_Y_$$^WWWTEXT(216)_Y_$$^WWWFELDNAME("INART","P",1)_Y_$$^WWWTEXT(34003)_Y_$$^WWWFELDNAME("INAUFP","D",1)_Y_strAssign
	;          AUFTRAG              POSITION                ARTIKELNUMMER               ARTIKELBEZEICHNUNG                 ZUWEISEN ;order 
	DO HEADER^WWWTAB(UES,2)
	
	QUIT
	
SAVE
	/*------------------------------------------------------------------------------
	; BUTTON AUSGEWÄHLTE AUFTRAGE WERDEN DER TRANSPORTNUMMER ZUGEWIESEN
	; BUTTON SELECTING ORDER OF THE TRANSPORTATION NUMBER ASSIGNED 
	;
	; Called by : form INSHIPINFOALL "Allocating Orders"
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-June-2005	MS		SR12516: don't assign order lines to the selected
	; 						import if already assigned to an import
	;-----------------------------------------------------------------------------*/
	new LFN,STATUS,SATZ,AB,KEYS,YOK,YI
	
	set STATUS=0
	set SATZ=""
	set SATZ(1)=""
	set SATZ(2)=""
	;Q:$G(VORG(1))=""
	
	set $piece(^INUSER(YM,YBED,1),Y,39)=$get(VORG(1))   ; SPEICHERN DER TRANSPORTNUMMER FÜR ANZEIGE NACH FORMULARREFRESH ;Save the to Show within 
	;													  retain import number for use after formula
	
	IF $GET(VORG(1))="" DO ^WWWFORM QUIT  ;FIS;15.03.05
	SET VORG(2)=$GET(VORG(2))  ;ALL LINE ITEMS FROM THIS ORDER
	
	;SET LFN=""
	;FOR  SET LFN=$ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"K",LFN)) QUIT:LFN=""  DO
	SET LFN(1)="YKEY"
	FOR  SET LFN(1)=$ORDER(%(YQUERY,LFN(1))) QUIT:LFN(1)=""  QUIT:$EXTRACT(LFN(1),1,4)'="YKEY"  DO  ;FIS;05.01.05;27115
	. SET LFN=$EXTRACT(LFN(1),5,99)
	. SET STATUS=+$GET(%(YQUERY,"YSEL"_LFN))
	. IF STATUS=0 IF VORG(2)'="" DO
	. . IF $PIECE($GET(%(YQUERY,LFN(1))),Y,1)=VORG(2) SET STATUS=1  ;ALL LINE ITEMS FROM THIS ORDER;FIS;15.03.05;27107
	. . QUIT
	. QUIT:STATUS=0
	. ;SET SATZ=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"K",LFN))
	. SET SATZ=$GET(%(YQUERY,LFN(1)))    ;FIS;05.01.05;27115
	. QUIT:SATZ=""
	. IF $PIECE(SATZ,Y,1)="" SET $PIECE(SATZ,Y,1)=" "   ; AUFTRAGSNUMMER
	. IF $PIECE(SATZ,Y,2)="" SET $PIECE(SATZ,Y,2)=" "   ; POSITION
	. IF $PIECE(SATZ,Y,3)="" SET $PIECE(SATZ,Y,3)=" "   ; LIEFERANT ;supplier 
	. SET KEYS=$PIECE(SATZ,Y,1)_","_$PIECE(SATZ,Y,2)_","_$PIECE(SATZ,Y,3)
	. ;
	. IF $DATA(^INAUFPA(YM,$PIECE(SATZ,Y,1),$PIECE(SATZ,Y,2),$PIECE(SATZ,Y,3))) DO  ;IMPORT NUMMER SPEICHERN ;importation numeral Save 
	. . SET AB=""
	. . FOR  SET AB=$ORDER(^INAUFPA(YM,$PIECE(SATZ,Y,1),$PIECE(SATZ,Y,2),$PIECE(SATZ,Y,3),AB)) QUIT:AB=""  DO
	. . . SET SATZ(1)=$GET(^INAUFPA(YM,$PIECE(SATZ,Y,1),$PIECE(SATZ,Y,2),$PIECE(SATZ,Y,3),AB,1))
	. . . ;write "Setting "_VORG(1)_": from "_$piece($get(SATZ(1)),Y,61)
	. . . ;SET $PIECE(SATZ(1),Y,61)=VORG(1)   ; TRANSPORTNUMMER
	. . . if $piece($get(SATZ(1)),Y,61)="" SET $PIECE(SATZ(1),Y,61)=VORG(1)   ; SR12516 only set the import number if no import number already for this order line
	. . . NEW YVOR,YFORM
	. . . SET YOK=$$^WWWSPEI("INAUFPA",$PIECE(SATZ,Y,1)_","_$PIECE(SATZ,Y,2)_","_$PIECE(SATZ,Y,3)_","_AB,SATZ(1),1)
	. ;
	. IF '$DATA(^INAUFPA(YM,$PIECE(SATZ,Y,1),$PIECE(SATZ,Y,2),$PIECE(SATZ,Y,3))) DO  ;AB MIT IMPORT NUMMER ANLEGEN ;Confirm. by means of importation numeral put onto 
	. . SET SATZ(1)=""
	. . SET $PIECE(SATZ(1),Y,1)=+$HOROLOG              ; ABDATUM
	. . SET $PIECE(SATZ(1),Y,23)=$PIECE($GET(^INSHIPINFO(YM,$GET(VORG(1)),1)),Y,9)   ; LIEFERTERMIN ;time of delivery 
	. . SET $PIECE(SATZ(1),Y,61)=VORG(1)         ; TRANSPORTNUMMER
	. . SET AB=VORG(1)
	. . NEW YVOR,YFORM
	. . SET YOK=$$^WWWSPEI("INAUFPA",$PIECE(SATZ,Y,1)_","_$PIECE(SATZ,Y,2)_","_$PIECE(SATZ,Y,3)_","_AB,SATZ(1),1)
	. ;
	. DO  ;NEURECHNEN IMPORT KOSTEN;FIS;09.09.04;25736
	. . NEW YKEY,YFELD,TYPE
	. . SET TYPE=""
	. . FOR  SET TYPE=$ORDER(^INSHIPINFOCOSTS(YM,VORG(1),TYPE)) QUIT:TYPE=""  DO
	. . . SET YKEY=VORG(1)_","_TYPE
	. . . SET YFELD=$GET(^INSHIPINFOCOSTS(YM,VORG(1),TYPE,1))
	. . . DO ^INSHIPINFOCOSTS
	
	if '$get(^CacheTempSkipRefresh(YUSER)) DO ^WWWFORM
	kill ^CacheTempSkipRefresh(YUSER)
	QUIT
	
SELECT   ; SELECT BUTTONS FÜR SELEKTIERUNG / DESELEKTIERUNG ALLER FELDER ;to everyone 
	WRITE "<TABLE BORDER=0 CELLSPACING=0 CELLPADDING=0 WIDTH=22>"
	DO NL^WWWTAB
	DO  ;
	. NEW YKEY,YFORM
	. WRITE "<TD WIDTH=10 class="_""""_"coolButton"_""""_">"
	. WRITE YCR,"<A onClick='return doLink(this)' HREF=""JavaScript:checkall(1,'"
	. WRITE $GET(ANZ)
	. WRITE "');"_""""_">"
	. WRITE "<img src="_YGIF_"select.gif border=0 TITLE="_""""_$$^WWWTEXT(33380)_""""_">"
	. WRITE "</A>"
	. WRITE "</TD>",YCR
	
	DO
	. NEW YKEY,YFORM
	. WRITE "<TD WIDTH=10 class="_""""_"coolButton"_""""_">"
	. WRITE YCR,"<A onClick='return doLink(this)' HREF=""JavaScript:checkall(0,'"
	. WRITE $GET(ANZ)
	. WRITE "');"_""""_">"
	. WRITE "<img src="_YGIF_"deselect.gif border=0 TITLE="_""""_$$^WWWTEXT(33381)_""""_">"
	. WRITE "</A>"
	. WRITE "</TD>",YCR
	
	DO EL^WWWTAB
	DO STOP^WWWTAB
	QUIT
	
JAVASCRIPT   ; SCHREIBEN DES SCRIPTS ZUM (DE-)SELEKTIEREN DER CHECKFELDER ;write the 
	IF $GET(YNOFUNCT)'=1 DO
	. WRITE YCR
	. WRITE YCR,"<script language="_""""_"JavaScript"_""""_">"
	. WRITE YCR,"<!--"
	. WRITE YCR
	. WRITE YCR,"function checkall(check,oklist)"
	. WRITE YCR," {"
	. WRITE YCR,"     var retval;"
	. WRITE YCR,"     if (oklist > 50) "  ;FIS;05.01.05;27115
	. DO CALL^WWWLOADINFO(5)
	. WRITE YCR,"     if (oklist > 999) alert('"_$$^WWWTEXT(34105,,1)_"');"  ;ZU VIELE  ;FIS;05.01.05;27115
	. WRITE YCR,"     if (oklist <= 999)  {"  
	. WRITE YCR,"        retval = EventValue("_""""_YUCI_""""_","_""""_YUSER_""""_","_""""_"INSHIPINFOALL"_""""_","
	. WRITE """"_"FIX"_""""_","_""""_"INSHIPINFOALLCH"_""""_","_""""_""""_","_""""_"6"_""""_","_""""_""_"~"_""_"~"_""""_"+oklist+"_""""_"~"_""""_"+check);"
	. WRITE YCR,"        for(var i = 1; i <= oklist; i++)"
	. WRITE YCR,"          {"
	. WRITE YCR,"          if (check==1) {"
	. WRITE YCR,"          eval("_""""_"document."_YHTMFORM_".YSEL"_""""_" + i + "_""""_".checked = 1"_""""_");"
	. WRITE YCR,"          }"
	. WRITE YCR,"        else {"
	. WRITE YCR,"          eval("_""""_"document."_YHTMFORM_".YSEL"_""""_" + i + "_""""_".checked = 0"_""""_");"
	. WRITE YCR,"          }"
	. WRITE YCR,"        }"
	. WRITE YCR,"     }"
	. WRITE YCR," }"
	. WRITE YCR,"//-->"
	. WRITE YCR,"</SCRIPT>"
	. WRITE YCR
	. WRITE YCR 
	
	QUIT
	
	
GetReceivedQuantity(pidOrder,pidLine)
	;-------------------------------------------------------------------------------
	; For a given order and order line, find the received quantity.
	;
	; Returns:
	;
	; History:
	; 23-Mar-2005	Steve S	Created (SR10950)
	;-------------------------------------------------------------------------------
	new idOrder,idLine,qty,idLoc,idStockLoc,dteRecDate,objReceived
	
	set idOrder=$get(pidOrder)
	set idLine=$get(pidLine)
	set qty=0
	
	if (idOrder'="")&&(idLine'="") {
		set idLoc=""
		for {
			set idLoc=$order(^INWEAUF(YM,idOrder,idLine,idLoc))
			quit:idLoc=""
			
			set idStockLoc=""
			for {
				set idStockLoc=$order(^INWEAUF(YM,idOrder,idLine,idLoc,idStockLoc))
				quit:idStockLoc=""
				
				set dteRecDate=""
				for {
					set dteRecDate=$order(^INWEAUF(YM,idOrder,idLine,idLoc,idStockLoc,dteRecDate))
					quit:dteRecDate=""
					
					set objReceived=$get(^INWEAUF(YM,idOrder,idLine,idLoc,idStockLoc,dteRecDate,1))
					set qty=qty+$piece(objReceived,Y,4)
				}
			}
		}
	}
	
	quit qty
	
OnBeforeButtonLine()
	;-------------------------------------------------------------------------------
	; Code to execute before the button line on the INSHIPINFOALL form.
	;
	; Returns:
	;
	; History:
	; 01-Dec-2006	Steve S		SR15233: Cleaned up/macro usage
	; 02-Sep-2005	JW			SR12966: COMTempList is not shared 
	; 31-Mar-2005	RobertW		SR11953: The createdby text (33736) only needs to be calculated once.
	; 24-Mar-2005	Steve S		Created (SR11953)
	;-------------------------------------------------------------------------------
	new idImport,objImport,idSupplier,idSupplierIndex,idOrder,objOrder,idxComplete
	new idPrevImport,strCreatedBy
	
	kill ^COMTempList(YM,YUSER)
	
	set idImport=$piece($get(^INUSER(YM,YBED,1)),Y,39)
	
	if (idImport'="") {
		set objImport		= $get(^INSHIPINFO(YM,idImport,1))
		set idSupplier		= $$$INSHIPINFOSupplier(objImport) //SR15233 (Macro)
		set idSupplierIndex	= $$$Index(idSupplier)
		
		set strCreatedBy=$$^WWWTEXT(33736)_" "
		
		for idxComplete=$$$ANINO,$$$NO {
			set idOrder=""
			for {
				set idOrder=$order(^INAUFPs(YM,11,idxComplete,idSupplierIndex,idOrder))
				quit:(idOrder="")
				
				set objOrder=$get(^INAUF(YM,idOrder,1))
				
				//SR15233 (Macros)
				continue:$$$INAUFOrderPrintedDate(objOrder)="" 				  // not printed
				continue:$$$INAUFOrderType(objOrder)'=2  					  // not a purchase order
				continue:$$$INAUFSupplierNumber(objOrder)="" 				  // no supplier
				continue:'$data(^INLIEF(YM,$$$INAUFSupplierNumber(objOrder))) // no supplier data
				
				if '$$AllLinesAssigned(idOrder) {
					set ^COMTempList(YM,YUSER,"Orders",idOrder,1)=strCreatedBy_$$$INAUFCreatedBy(objOrder)
				}
			}
		}
	}
	
	quit
	
AllLinesAssigned(pidOrder)
	;-------------------------------------------------------------------------------
	; Given an order number, are all of its order lines assigned to an import or received?
	;
	; Returns:
	;
	; History:
	; 31-Mar-2005	RobertW	SR11953: Just bail out as soon as we find one unassigned line.
	; 						Also the supplier doesn't need to go through the index routine
	; 						each time as it doesn't change.
	; 24-Mar-2005	Steve S	Created (SR11953)
	;-------------------------------------------------------------------------------
	new blnAllAssigned,idOrder,objOrder,idSupplier,idLine,objLine,blnComplete,idxSupplier
	
	set blnAllAssigned=$$$YES
	
	set idOrder=$get(pidOrder)
	
	if (idOrder'="") {
		set objOrder=$get(^INAUF(YM,idOrder,1))
		set idSupplier=$$$INAUFSupplierNumber(objOrder)
		
		if (idSupplier'="") {
			set idxSupplier=$$$Index(idSupplier)
			set idLine=""
			for {
				set idLine=$order(^INAUFP(YM,idOrder,idLine))
				quit:(idLine="")
				quit:(blnAllAssigned=$$$NO)
			
				set objLine=$get(^INAUFP(YM,idOrder,idLine,1))
				set blnComplete=+$$$INAUFPPosCompleted(objLine)
				
				if ('blnComplete)&&('$data(^INAUFPAs(YM,1,idxSupplier,idOrder,idLine))) {
					set blnAllAssigned=$$$NO
				}
			}	
		}	
	}
	
	quit blnAllAssigned
	
	
ShowStatus()
	;-------------------------------------------------------------------------------
	; If no orders found, show "No Data" next to the empty drop down.
	;
	; Returns:
	;
	; History:
	; 04-Jul-2005	Steve S	Use strMessage, not strStatus
	; 24-Mar-2005	Steve S	Created (SR11953)
	;-------------------------------------------------------------------------------
	new strMessage
	
	set strMessage=""
	
	if '$data(^COMTempList(YM,YUSER,"Orders")) {
		set strMessage=$$^WWWTEXT(33817) ;No orders found
	}
	
	quit strMessage
	
GoToAllocate(pidImport)
	;-------------------------------------------------------------------------------
	; Code to go to Allocate Orders from with the import number loaded in.
	;
	; Called by : Button on form INSHIPINFO "Edit Import Shipments"
	; 
	; Returns:
	;
	; History:
	; 09-May-2005	Steve S		Created SR:12091
	;-------------------------------------------------------------------------------
	do RedirectForm^COMUtilForm("INSHIPINFOALL","","INSHIPINFO,",YPARA,YSEITE)	
	set VORG(1)=$get(pidImport)
	set ^CacheTempSkipRefresh(YUSER)=1
	do SAVE
	
	quit
	
]]></Routine>
</Export>