<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPODiscount" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPODiscount
 
#include COMSYS
#include INConst
#include COMConst
#include COMSYSNum 
 
LoadDiscountGrid(pKEY)
 
	new YFORM,YKEY,YAUSWAHL,YFELD
 
	SET YFORM = "INPODiscount"			
	SET YKEY = $GET(pKEY)
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)= $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)= 200
	set $$$COMGridEditParameterGridName(YAUSWAHL) = YFORM
	set $$$COMGridEditParameterUpdateFields(YAUSWAHL) = ""
	set $$$COMGridEditParameterClearFields(YAUSWAHL) = ""
	set $$$COMGridEditParameterDefaultFields(YAUSWAHL) = ""
	set $$$COMGridEditParameterContainer(YAUSWAHL) = "INPO"
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL)=$$$YES
	set $$$COMGridEditParameterEnabled(YAUSWAHL)=$$$YES
	
	do ^COMGridEdit31
	Do ^WWWVAR
	quit
 
OnBeforeSaveDiscounts
	
	new strStatus
	set strStatus = $$$GRIDSave(YKEY)
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
		set Q=$$$QDontSave
	}
	quit	
 
OnBlurDOLine(&pYFELD)
 	new objDOLine
 	
	quit:$get(^CacheTempINPO(YUSER,"CurrentDO"))=""
	
	if (YINHALT'="") {
		set objDOLine= $get(^INPOLine(YM,$get(^CacheTempINPO(YUSER,"CurrentDO")),YINHALT,1))
		quit:objDOLine=""                          ; FIXME : Early exit - change to if objDOLine'="" for the rest of the 'if' block
		
		//set $$$INPODiscountsExtendedCost(pYFELD)= $$$INPOLineExtendedCost(objDOLine)
		set $$$INPODiscountQuantityOrdered(pYFELD)= $$$INPOLineQuantityOrdered(objDOLine)
		set $$$INPODiscountItem(pYFELD)=$$$INPOLineItem(objDOLine)
		set $$$INPODiscountLineType(pYFELD)=$$$INPOLineLineType(objDOLine)
	
	} else {
		//set pYFELD=""
		set $$$INPODiscountExtendedCost(pYFELD)= ""
		set $$$INPODiscountQuantityOrdered(pYFELD)= ""
		set $$$INPODiscountItem(pYFELD)=""
		set $$$INPODiscountLineType(pYFELD)=""
		set $$$INPODiscountCalculationLevel(pYFELD)=""
	}
	quit
	
OnDOBlur(pYINHALT,&pYFELD)
	quit:pYINHALT=""
	
	kill ^CacheTempINPO
	set ^CacheTempINPO(YUSER,"CurrentDO")=pYINHALT
	
	quit
	
	
CalcDiscountValue(pYKEY,pObjCurrDisc)
	new fltExtCost,fltValueAfterDiscount
	
	set fltValueAfterDiscount=""
	quit:($$$INPODiscountRateType(pObjCurrDisc)="")      ; FIXME : requires return value
	quit:($$$INPODiscountRate(pObjCurrDisc)="")
	
	if ($$$INPODiscountRateType(pObjCurrDisc)=1) {	
		set fltValueAfterDiscount =$$$FCDivd($$$FCMult($$$INPODiscountRate(pObjCurrDisc),$$$INPODiscountExtendedCost(pObjCurrDisc)),100)
	
	} elseif ($$$INPODiscountRateType(pObjCurrDisc)=2) {
		if ($$$INPODiscountPOLineNumber(pObjCurrDisc)="") && ($$$INPODiscountDeliveryOrderNumber(pObjCurrDisc)="") {
			set fltValueAfterDiscount =$$$INPODiscountRate(pObjCurrDisc)
		} else {
			set fltValueAfterDiscount =$$$FCMult($$$INPODiscountQuantityOrdered(pObjCurrDisc),$$$INPODiscountRate(pObjCurrDisc))
		}
	
	} elseif ($$$INPODiscountRateType(pObjCurrDisc)=0) {
		set fltValueAfterDiscount =$$$INPODiscountRate(pObjCurrDisc)
	}
	
	if (($$$INPODiscountDiscountType(pObjCurrDisc)=1)||($$$INPODiscountDiscountType(pObjCurrDisc)=4)) {
		set fltValueAfterDiscount = -fltValueAfterDiscount
	}
 
    quit fltValueAfterDiscount
	
	
calculateExtCost(pYKEY,pObjCurrDisc)
	new currTotExtCost,currPrevDiscounts,objDOLine,objDiscount
	new fltPrevLvl,idDO,idDOLine,idPO,intLineSeqNum,intPrevLineSeqNum
	new idxPOID
	
	set currTotExtCost = 0
	set objDiscount    = ""
	set fltPrevLvl     = ""
	set idPO           = $piece(pYKEY,",",1)
	set intLineSeqNum  = $piece(pYKEY,",",2)
	set idxPOID        = $$$Index(idPO)
	
	set fltPrevLvl     = $order(^INPODiscounts(YM,1,"")) 
	if (fltPrevLvl'="") set intPrevLineSeqNum = $order(^INPODiscounts(YM,1,fltPrevLvl,idxPOID,"")) 
	
	if ((intPrevLineSeqNum '="") && (intPrevLineSeqNum '= intLineSeqNum)) set objDiscount = $get(^INPODiscount(YM,idPO,intPrevLineSeqNum,1))
	
	if (objDiscount'="") {
		if ($$$INPODiscountCalculationLevel(objDiscount)=$$$INPODiscountCalculationLevel(pObjCurrDisc)) {
			if ($$$INPODiscountPOLineNumber(objDiscount)=$$$INPODiscountPOLineNumber(pObjCurrDisc))               &&
			   ($$$INPODiscountDeliveryOrderNumber(objDiscount)=$$$INPODiscountDeliveryOrderNumber(pObjCurrDisc)) {
				
				set currTotExtCost= $$$INPODiscountExtendedCost(objDiscount)			
			} else {
				set currTotExtCost=$$getExtCost(pObjCurrDisc,idPO)
			}
		
		} else {	
			set currPrevDiscounts = 0
			set fltPrevLvl=""
			for {
				set fltPrevLvl=$order(^INPODiscounts(YM,1,fltPrevLvl)) 
				quit:fltPrevLvl=""
				
				set intPrevLineSeqNum = ""
				for {
					set intPrevLineSeqNum = $order(^INPODiscounts(YM,1,fltPrevLvl,idxPOID,intPrevLineSeqNum))
					quit:intPrevLineSeqNum=""
					quit:intPrevLineSeqNum=intLineSeqNum
					set objDiscount = $get(^INPODiscount(YM,idPO,intPrevLineSeqNum,1))
					quit:$$$INPODiscountCalculationLevel(pObjCurrDisc)=$$$INPODiscountCalculationLevel(objDiscount)
					
					if ($$$INPODiscountItemCategory(objDiscount)'="") && ($$$INPODiscountItem(pObjCurrDisc)'="") {
						continue:$$checkItemCat($$$INPODiscountItemCategory(objDiscount),$$$INPODiscountItem(pObjCurrDisc))=0
					}
					
					if ($$$INPODiscountPOLineNumber(pObjCurrDisc)=$$$INPODiscountPOLineNumber(objDiscount))               &&
					   ($$$INPODiscountDeliveryOrderNumber(objDiscount)=$$$INPODiscountDeliveryOrderNumber(pObjCurrDisc))    {
						
						set currPrevDiscounts= $$$FCAdd(currPrevDiscounts,$$$INPODiscountValue1(objDiscount))
					
					} elseif ($$$INPODiscountPOLineNumber(pObjCurrDisc)="")        &&
					         ($$$INPODiscountDeliveryOrderNumber(pObjCurrDisc)="") &&
					         ($$$INPODiscountDeliveryOrderNumber(objDiscount)'="")     {	
						set currPrevDiscounts= $$$FCAdd(currPrevDiscounts,$$$INPODiscountValue1(objDiscount))
					} elseif ($$$INPODiscountDeliveryOrderNumber(objDiscount)'="")  &&
					         ($$$INPODiscountPOLineNumber(objDiscount)'="")         &&
					         ($$$INPODiscountDeliveryOrderNumber(pObjCurrDisc)'="") &&
					         ($$$INPODiscountPOLineNumber(pObjCurrDisc)="")         &&
					         ($$$INPODiscountDeliveryOrderNumber(objDiscount)=$$$INPODiscountDeliveryOrderNumber(pObjCurrDisc)) {
						
						set currPrevDiscounts= $$$FCAdd(currPrevDiscounts,$$$INPODiscountValue1(objDiscount))
					
					} elseif ($$$INPODiscountPOLineNumber(objDiscount)="") {  //&&($$$INPODiscountDeliveryOrderNumber(objDiscount)="")) 
						if ($$$INPODiscountCostDistribution(objDiscount)'="") {
							if ($$$INPODiscountDeliveryOrderNumber(pObjCurrDisc)'="") {
								if ($$$INPODiscountCostDistribution(objDiscount)=0) {
									set currPrevDiscounts=currPrevDiscounts+$$$FCMult($$$FCDivd($$$FCAdd($$getExtCost(pObjCurrDisc,idPO),currPrevDiscounts),$$$INPODiscountExtendedCost(objDiscount)),$$$INPODiscountValue1(objDiscount))
								
								} elseif ($$$INPODiscountCostDistribution(objDiscount)=3) {
									set currPrevDiscounts=currPrevDiscounts+$$$FCMult($$$FCDivd($$getQuantityOrdered(pObjCurrDisc),$$getTotQuantOrdered(idPO,$$$INPODiscountDeliveryOrderNumber(pObjCurrDisc),$$$INPODiscountItemCategory(pObjCurrDisc))),$$$INPODiscountValue1(objDiscount))
								
								} elseif ($$$INPODiscountCostDistribution(objDiscount)=4) {
									set currPrevDiscounts=currPrevDiscounts+$$$FCDivd($$$INPODiscountValue1(objDiscount),$$getTotLines(idPO,$$$INPODiscountDeliveryOrderNumber(pObjCurrDisc),$$$INPODiscountItemCategory(pObjCurrDisc)))
								}							
	 
							}
						}
					}
				}
			}
			set currTotExtCost=$$$FCAdd($$getExtCost(pObjCurrDisc,idPO),currPrevDiscounts)
		}
 
	} else{
		set currTotExtCost=$$getExtCost(pObjCurrDisc,idPO)
	}
	
	quit currTotExtCost
	
	
getExtCost(pYFELD,pIDPO)
	new currTotExtCost,idDO,idDOLine,objDOLine
	
	set currTotExtCost=0
	
	if ($$$INPODiscountPOLineNumber(pYFELD)'="") && ($$$INPODiscountDeliveryOrderNumber(pYFELD)'="") {
			set objDOLine=$get(^INPOLine(YM,$$$INPODiscountDeliveryOrderNumber(pYFELD),$$$INPODiscountPOLineNumber(pYFELD),1))
			set currTotExtCost = $$$FCAdd(currTotExtCost,$$$INPOLineExtendedCost(objDOLine))
			
	} elseif ($$$INPODiscountPOLineNumber(pYFELD)="") && ($$$INPODiscountDeliveryOrderNumber(pYFELD)'="") {
		set idDOLine=""
		for {
			set idDOLine=$order(^INPOLine(YM,$$$INPODiscountDeliveryOrderNumber(pYFELD),idDOLine))
			quit:idDOLine=""
			
			set objDOLine=$get(^INPOLine(YM,$$$INPODiscountDeliveryOrderNumber(pYFELD),idDOLine,1))
		
			set currTotExtCost = $$$FCAdd(currTotExtCost,$$$INPOLineExtendedCost(objDOLine))
		}
	} else {
		set idDO=""
		for {
			set idDO=$order(^INPOs(YM,2,$$$Index(pIDPO),idDO))
			quit:idDO=""
			
			set idDOLine=""
			for {
				set idDOLine=$order(^INPOLine(YM,idDO,idDOLine))
				quit:idDOLine=""
				
				set objDOLine=$get(^INPOLine(YM,idDO,idDOLine,1))
				set currTotExtCost = $$$FCAdd(currTotExtCost,$$$INPOLineExtendedCost(objDOLine))
			}
		}
	}
	
	quit currTotExtCost
	
	
getQuantityOrdered(pYFELD)
	new currQuantOrder,idDOLine,objDOLine
	
	set currQuantOrder=0
	
	if ($$$INPODiscountPOLineNumber(pYFELD)'="") && ($$$INPODiscountDeliveryOrderNumber(pYFELD)'="") {
			set objDOLine      = $get(^INPOLine(YM,$$$INPODiscountDeliveryOrderNumber(pYFELD),$$$INPODiscountPOLineNumber(pYFELD),1))
			set currQuantOrder = currQuantOrder+$$$INPOLineQuantityOrdered(objDOLine)
	
	} elseif ($$$INPODiscountPOLineNumber(pYFELD)="") && ($$$INPODiscountDeliveryOrderNumber(pYFELD)'="") {
		set idDOLine = ""
		for {
			set idDOLine = $order(^INPOLine(YM,$$$INPODiscountDeliveryOrderNumber(pYFELD),idDOLine))
			quit:idDOLine=""
			
			set objDOLine      = $get(^INPOLine(YM,$$$INPODiscountDeliveryOrderNumber(pYFELD),idDOLine,1))
			set currQuantOrder = currQuantOrder+$$$INPOLineQuantityOrdered(objDOLine)
		}
	}
	
	quit currQuantOrder
	
	
getTotQuantOrdered(pIDPO,pIDDO="",pItemCat="")
	new currQuantOrder,idDO,idDOLine,objDOLine,idxPOID
	
	set currQuantOrder=0
	set idxPOID=$$$Index(pIDPO)
	if (pIDDO'="") {
		set idDOLine=""
		for {
			set idDOLine=$order(^INPOLine(YM,pIDDO,idDOLine))
			quit:idDOLine=""
			set objDOLine=$get(^INPOLine(YM,pIDDO,idDOLine,1))
			if (pItemCat'="") {
				if ($$checkItemCat(pItemCat,$$$INPOLineItem(objDOLine))=1) 	set currQuantOrder = currQuantOrder+$$$INPOLineQuantityOrdered(objDOLine)
			} else {
				set currQuantOrder = currQuantOrder+$$$INPOLineQuantityOrdered(objDOLine)
			}
		}
	
	} else {
		set idDO=""
		for {
			set idDO=$order(^INPOs(YM,2,idxPOID,idDO))
			quit:idDO=""
			
			set idDOLine=""
			for {
				set idDOLine=$order(^INPOLine(YM,idDO,idDOLine))
				quit:idDOLine=""
				
				set objDOLine=$get(^INPOLine(YM,idDO,idDOLine,1))
				if (pItemCat'="") {
					if ($$checkItemCat(pItemCat,$$$INPOLineItem(objDOLine))=1) 	set currQuantOrder = currQuantOrder+$$$INPOLineQuantityOrdered(objDOLine)
				
				} else {
					set currQuantOrder = currQuantOrder+$$$INPOLineQuantityOrdered(objDOLine)
				}
			}
		}
	}
	
	quit currQuantOrder
	
	
getTotLines(pIDPO,pIDDO="",pItemCat="")
	new idDO,idDOLine,intTotLines,objDOLine,idxPOID
	
	set intTotLines=0
	if (pIDDO'="") {
		set idDOLine=""
		for {
			set idDOLine=$order(^INPOLine(YM,pIDDO,idDOLine))
			quit:idDOLine=""
			
			set objDOLine=$get(^INPOLine(YM,pIDDO,idDOLine,1))
			if (pItemCat'="") {
				if ($$checkItemCat(pItemCat,$$$INPOLineItem(objDOLine))=1) set intTotLines = intTotLines+1
			} else {
				set intTotLines = intTotLines+1
			}
		}
	} else {
		set idDO=""
		set idxPOID=$$$Index(pIDPO)
		for {
			set idDO=$order(^INPOs(YM,2,idxPOID,idDO))
			quit:idDO=""
			
			set idDOLine=""
			for {
				set idDOLine=$order(^INPOLine(YM,idDO,idDOLine))
				quit:idDOLine=""
				
				set objDOLine=$get(^INPOLine(YM,idDO,idDOLine,1))
				if (pItemCat'="") {
					if ($$checkItemCat(pItemCat,$$$INPOLineItem(objDOLine))=1) set intTotLines = intTotLines+1
				} else {
					set intTotLines = intTotLines+1
				}			
			}
		}
	}
	
	quit intTotLines
 
 
FillCostAfterDisc(pYKEY)
 
	new idDO,idDOLine,idxYKEY,objDOLine
	new intOrderQuant,strStat
	
	set intOrderQuant=0
	set idDO=""
	set idxYKEY=$$$Index(pYKEY)
	for {
		set idDO= $order(^INPOs(YM,2,idxYKEY,idDO))
		quit:idDO=""
		set idDOLine=""
		for {
			set idDOLine = $order(^INPOLine(YM,idDO,idDOLine))
			quit:idDOLine=""
			set objDOLine=$get(^INPOLine(YM,idDO,idDOLine,1))
			set $$$INPOLineCostAfterDiscount(objDOLine)=$$$INPOLineExtendedCost(objDOLine)
#;			set intOrderQuant=intOrderQuant+$$$INPOLineQuantityOrdered(objDOLine)
			set strStat=$$$Save("INPOLine",idDO_","_idDOLine,objDOLine,1)
		}
	}
	quit 
 
FillOrderValue(pYKEY,pRemainingDiscounts)
	new idDO,idDOLine,objDOLine,objPO
	new currOrderValue,strStat
	new idxYKEY
	quit:pYKEY=""
	set objPO=$get(^INPO(YM,pYKEY,1))
	quit:objPO=""
	set currOrderValue=0
	set idxYKEY=$$$Index(pYKEY)
	set idDO=""
	for {
		set idDO= $order(^INPOs(YM,2,idxYKEY,idDO))
		quit:idDO=""
		set idDOLine=""
		for {
			set idDOLine = $order(^INPOLine(YM,idDO,idDOLine))
			quit:idDOLine=""
			set objDOLine=$get(^INPOLine(YM,idDO,idDOLine,1))
			set currOrderValue=$$$FCAdd(currOrderValue,$$$INPOLineCostAfterDiscount(objDOLine))
		}
	}
	set $$$INPOOrderValue(objPO)=currOrderValue+pRemainingDiscounts
	set strStat=$$$Save("INPO",pYKEY,objPO,1)
	
	quit
	
	
checkItemCat(pItemCat,pItem)
	new tmpBln,item,idxItemCat,idxItem,idItem
	
	set tmpBln = 0
	set idItem=""
	set item=""
	set idxItemCat=$$$Index(pItemCat)
	set idxItem=$$$Index(item)
	for {
		set item=$order(^INARTs(YM,6,idxItemCat,idItem))
		quit:item=""
		if (item=pItem) {
			set tmpBln=1
			quit
		}
	}
		
	quit tmpBln
 
]]></Routine>
</Export>