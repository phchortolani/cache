<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTXL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTXL(YKEY,YMIN,EXECUTE)
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ARTIKELTEILESTRUKTUR NEU AUFBAUEN
	;
	; Inputs : 
	;	YKEY		ARTIKEL
	;	YMIN		1 = NUR DIESEN ARTIKEL / NICHT DIE HAUPTARTIKEL BEARBEITEN ;only item Not who 
	;	EXECUTE		1 = NICHT IN DEN HINTERGRUND SENDEN SONDERN AUSFÜHREN ;Not within foil transmit implement 
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 29-Sep-2006	GRF		Split line for clarity; doco; quits
	; 31-Aug-2005	GRF		Doco
	; 22.06.2000	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	QUIT:$GET(YKEY)=""
	
	SET YMIN=$GET(YMIN)
	IF $GET(EXECUTE)'=1 LOCK +^INPROPJOB(YM):0 IF '$TEST DO  QUIT  ;HINTERGRUND AKTIV ;foil ENABLED 
	. IF $GET(YKEY)="" QUIT
	. DO
	. . NEW NEXT
	. . SET NEXT=""
	. . FOR  SET NEXT=$ORDER(^INARTJOB(YM,NEXT)) QUIT:NEXT=""  DO
	. . . IF $FIND($GET(^INARTJOB(YM,NEXT,1)),"DO ^INARTXL("_""""_$GET(YKEY)_""""_","_$GET(YMIN)_",1)") KILL ^INARTJOB(YM,NEXT)
	. ;
	. SET ^INARTJOB(YM,$$^WWWNEXT("INARTJOB"),1)="DO ^INARTXL("_""""_$GET(YKEY)_""""_","_$GET(YMIN)_",1)"  ;EXECUTE
	
	IF $GET(EXECUTE)'=1 LOCK -^INPROPJOB(YM)
	
	new ART,ARTIKEL,ARTZW,ARTT,ARTTF,ONES,TWICE,UP,UPUP
	new YAPARA,YART,YARTART,YARTIKEL,YARTINF,YEBENE,YEINHEIT,YGEWICHT,YKOSTEN
	new YLOS,YMENGE,YLIEF,YTEXT,YZEIT,YZPOS
	
	;+++++++++++++++++++++++++++++++++++++++
	; YARTINF		objINART
	; YARTINF(1)	objINARTXL
	; ARTT			objINARTT
	; ART(1)		objINART
	;+++++++++++++++++++++++++++++++++++++++
	
	SET ART=$PIECE(YKEY,",",1)				QUIT:ART=""
	
	DO TXL
	SET ARTZW=$PIECE(YKEY,",",1)
	IF YMIN'=1 SET ART="" FOR YEBENEDEEP=1:1:11 SET ART=$ORDER(^INARTTs(YM,1,$$^WWWUMLAU(ARTZW,1),ART)) QUIT:ART=$$^WWWUMLAU(ARTZW,1)  QUIT:ART=""  SET UP(1,ART)="" DO TXL  ;HAUPTARTIKEL BEARBEITEN
	IF YMIN'=1 FOR UPUP=1:1:11 SET ARTZW="" FOR YEBENEDEEP=1:1:10 SET ARTZW=$ORDER(UP(UPUP,ARTZW)) QUIT:ARTZW=""  DO
	. QUIT:ARTZW=$PIECE(YKEY,",",1)
	. SET ART=""
	. FOR YEBENEDEEP=1:1:11 SET ART=$ORDER(^INARTTs(YM,1,$$^WWWUMLAU(ARTZW,1),ART)) QUIT:ART=""  QUIT:ART=$$^WWWUMLAU(ARTZW,1)  QUIT:ART=$PIECE(YKEY,",",1)  SET UP(UPUP+1,ART)="" DO TXL  ;ARTIKEL ÜBER DEN ARTIKEL ;item via item 
	
	; FIXME : Not necessary since newed <GRF>
	KILL UP
	KILL ^WWWSOR(YUSER)
	QUIT
	
TXL ;HAUPTTEIL
	NEW TWICE  ;VARIABLE FÜR DIE SUCHE ;to who search 
	
	QUIT:$DATA(ONES(ART))  ;NUR EIN MAL AUSFÜHREN ;only uni- sometimes implement 
	
	SET ONES(ART)=""
	KILL YEBENE
	KILL ^WWWSOR(YUSER)
	SET YARTIKEL=ART
	;S ^WWWSOR(YUSER,0,ART)=""
	KILL ^INARTXL(YM,YARTIKEL)   ;ARTIKELSTRUKTUR NEU AUFBAUEN ;recent construct 
	SET YEBENE   = 0
	SET YART	 = ART
	SET YMENGE   = 1
	SET YARTINF  = $GET(^INART(YM,YART,1))
	SET YTEXT	 = $PIECE(YARTINF,Y,1)
	SET YEINHEIT = $PIECE(YARTINF,Y,40)
	SET YLAENGE  = $PIECE(YARTINF,Y,15)  ;LÄNGE DES PRODUKTES  ;length
	SET YBREITE  = $PIECE(YARTINF,Y,16)  ;BREITE DES PRODUKTES ;width 
	SET YHOEHE   = $PIECE(YARTINF,Y,17)  ;HÖHE DES PRODUKTES   ;height 
	SET YAPARA   = $PIECE(YARTINF,Y,20)  ;PARAMETER
	SET YARTART  = $PIECE(YARTINF,Y,26)  ;ARTIKELART  ;FIS;27.03.03;23327;ARTIKELART IN INARTXL ÜBERNEHMEN
	SET YRUEST   = $PIECE(YARTINF,Y,70)  ;RUESTZEITEN
	SET YLOS	 = $PIECE(YARTINF,Y,72)  ;LOSGRÖSSE
	;SET YLIEF   = $PIECE(YARTINF,Y,12)  ;LIEFERANT BEI FREMDFERTIGUNG, ULM
	SET YNEUNAME = ""  ;BENENNUNG
	DO EBSAVE  ;SPEICHERN HAUPTNUMMER
	
	;+++++++++++++++++++++++++++++++++++++++
	; Drops through for recursive operation
	;+++++++++++++++++++++++++++++++++++++++
	
TXL1 ;SUCHEN TEILE IN TEILE ;seek within 
	NEW ARTT
	
	SET ART=YART  ;NEUE HAUPTNUMMER
	SET YEBENE=YEBENE+1
	KILL ^WWWSOR(YUSER,YEBENE)  ;NEUE EBENE ;plain 
	;QUIT:$DATA(TWICE(YART))  ;NUR EIN MAL AUSFÜHREN JE ARTIKEL
	SET TWICE(YART)=""
	QUIT:YEBENE>12   ;NICHT MEHR  ;Not more 
	
	SET ^WWWSOR(YUSER,YEBENE,YART)=""  ;PRUEFVARIABLE GEGEN ENDLOSSCHLEIFE ARTIKEL BESTEHT AUS GLEICHEM ARTIKEL ;versus item out of item 
	;W "<BR>",YCR,ART
	SET YEBENE(YEBENE)=""
	FOR  SET YEBENE(YEBENE)=$ORDER(^INARTT(YM,ART,YEBENE(YEBENE))) QUIT:YEBENE(YEBENE)=""  DO  QUIT:$GET(YEBENE)=""  QUIT:$GET(YEBENE(YEBENE))=""
	. SET ARTT=$GET(^INARTT(YM,ART,YEBENE(YEBENE),1))
	. SET YART=$PIECE(ARTT,Y,1)                                ;TEILE DATEN
	. IF YART=""   KILL ^INARTT(YM,ART,YEBENE(YEBENE)) QUIT    ;KEIN ARTIKEL ;no item 
	. SET ART(1)=$GET(^INART(YM,YART,1))                       ;ARTIKELDATEN DES TEILS ;partly 
	. IF ART(1)="" KILL ^INARTT(YM,ART,YEBENE(YEBENE)) QUIT    ;ARTIKEL NICHT MEHR VORHANDEN ;item Not more on hand 
	. DO TXL2
	
	QUIT
	
TXL2 ;YEBENEN ARTIKELTEILE
	SET YMENGE   = $PIECE(ARTT,Y,4)                      ;MENGE AUS EINHEIT ;unit quantity
	SET YARTINF  = $GET(^INART(YM,YART,1))               ;ARTIKEL INFO ;item 
	SET YTEXT	 = $PIECE(YARTINF,Y,1)                   ;TEXT DES ARTIKELS
	SET YEINHEIT = $PIECE($GET(^INART(YM,YART,1)),Y,40)  ;EINHEITEN
	SET YZEIT	 = $PIECE(ARTT,Y,7)                      ;DAUER ;time 
	SET YARTART  = $PIECE($GET(^INART(YM,YART,1)),Y,26)  ;ARTIKELART
	SET YKOSTEN  = $PIECE(ARTT,Y,8)                      ;PREIS DES PRODUKTES ;price
	SET YGEWICHT = $PIECE(ARTT,Y,6)   ;GEWICHT DES PRODUKTES ;wt. 
	SET YLAENGE  = $PIECE(ARTT,Y,15)  ;LÄNGE DES PRODUKTES  ;length 
	SET YBREITE  = $PIECE(ARTT,Y,16)  ;BREITE DES PRODUKTES  ;width 
	SET YHOEHE   = $PIECE(ARTT,Y,17)  ;HÖHE DES PRODUKTES   ;height
	SET YZPOS	 = $PIECE(ARTT,Y,18)  ;ZEICHNUNGSPOSITION
	SET YAPARA   = $PIECE(ARTT,Y,20)  ;TEILEPARAMETER
	;SET YRUEST  = $PIECE(ARTT,Y,70)  ;REUSTZEITEN
	SET YRUEST   = $PIECE($GET(^INART(YM,YART,1)),Y,70)  ;REUSTZEITEN;FIS;23691;REUSTZEITEN IN UNTERTEILE NEU ÜBERTRAGEN
	SET YLOS	 = $PIECE(ARTT,Y,72)  ;LOSGRÖSSE
	SET YNEUNAME = $PIECE(ARTT,Y,30)  ;BENENNUNG
	FOR YI=53,49,276:1:300,336:1:345,350:1:354 SET ARTTF(YI)=$PIECE(ARTT,Y,YI)  ;TYBD;2,10,2003;TYBD;7,10,2003;24370
	SET YAUSS=$PIECE(ARTT,Y,220)  ;AUSSCHUSS%
	DO EBSAVE  ;SPEICHERN
	IF $DATA(^INARTT(YM,YART)) DO   ;UNTERTEILE VORHANDEN ;on hand 
	. NEW ART,YI
	. SET ART=YART
	. FOR YI=1:1:YEBENE IF $DATA(^WWWSOR(YUSER,YI,ART)) SET ART="" QUIT  ;ENDLOSSCHLEIFE ARTIKEL BESTEHT AUS GLEICHEM ARTIKEL ;item out of item 
	. QUIT:ART=""   ;FALSCHES TEIL DRUCH ENDLOSSCHLEIFE ;whack 
	. DO TXL1       ;UNTERTEILE DRUCKEN ;print 
	. SET YEBENE=YEBENE-1
	
	QUIT
	
EBSAVE
	;-------------------------------------------------------------------------------
	;SPEICHERN DER YEBENEN ;Save the 
	;	YEBENE=TIEFE
	;	YEBENE(n)=ARTIKELPOSITION DER YEBENE ;the 
	;	YART=Artikelnummer der Unterposition ;who 
	;	YMENGE=Sollmenge der Unterposition ;who 
	;-------------------------------------------------------------------------------
	NEW YDATEI,YI
	
	SET YDATEI="^INARTXL("_""""_YM_""""_","_""""_YARTIKEL_""""
	FOR YI=1:1:YEBENE IF YEBENE(YI)'="" SET YDATEI=YDATEI_","_YEBENE(YI)
	SET YDATEI=YDATEI_")"
	IF +YLAENGE=0 SET YLANGE=""
	IF +YBREITE=0 SET YBREITE=""
	IF +YHOEHE=0  SET YHOEHE=""
	SET YARTINF(1)=""
	
	; 29-Sep-2006 vvv
	;DATEN AUS ARTIKEL ÜBERTRAGEN;TYBD;24,09,2003;24370
	;FOR YI=2,3,10,12,14,15,16,17,26,30,31,32,33,36,37,38,40:1:45,47,48,49,50,53,54,55,57,58,59,61,62,66,68,70:1:75,126,130,131,132,134,136,158,166,171,174,172,191,192,251,252,273:1:301,336:1:345 SET $PIECE(YARTINF(1),Y,YI)=$PIECE(YARTINF,Y,YI)
	FOR YI=2,3,10,12,14,15,16,17,26,30,31,32,33,36,37,38,40:1:45,47,48,49,50 SET $PIECE(YARTINF(1),Y,YI)=$PIECE(YARTINF,Y,YI)
	FOR YI=53,54,55,57,58,59,61,62,66,68,70:1:75,126,130,131,132,134         SET $PIECE(YARTINF(1),Y,YI)=$PIECE(YARTINF,Y,YI)
	FOR YI=136,158,166,171,174,172,191,192,251,252,273:1:301,336:1:345       SET $PIECE(YARTINF(1),Y,YI)=$PIECE(YARTINF,Y,YI)
	
	SET $PIECE(YARTINF(1),Y,220)=$PIECE(YARTINF,Y,200)  ;AUSSCHUSS VORGABE  ;default 
	SET $PIECE(YARTINF(1),Y,1)=$GET(YTEXT)
	IF YNEUNAME'="" SET $PIECE(YARTINF(1),Y,1)=YNEUNAME_" "_$PIECE(YARTINF(1),Y,1)
	SET $PIECE(YARTINF(1),Y,4) =$GET(YART)
	SET $PIECE(YARTINF(1),Y,5) =$GET(YMENGE)
	SET $PIECE(YARTINF(1),Y,26)=$GET(YARTART)
	SET $PIECE(YARTINF(1),Y,40)=$GET(YEINHEIT)
	SET $PIECE(YARTINF(1),Y,43)=$GET(YGEWICHT)
	SET $PIECE(YARTINF(1),Y,45)=$GET(YZEIT)
	SET $PIECE(YARTINF(1),Y,47)=$GET(YKOSTEN)
	SET $PIECE(YARTINF(1),Y,15)=$GET(YLAENGE)
	SET $PIECE(YARTINF(1),Y,16)=$GET(YBREITE)
	SET $PIECE(YARTINF(1),Y,17)=$GET(YHOEHE)
	SET $PIECE(YARTINF(1),Y,18)=$GET(YZPOS)
	SET $PIECE(YARTINF(1),Y,20)=$GET(YAPARA)
	SET $PIECE(YARTINF(1),Y,70)=$GET(YRUEST)
	SET $PIECE(YARTINF(1),Y,72)=$GET(YLOS)
	IF $GET(YAUSS)'="" SET $PIECE(YARTINF(1),Y,220)=$GET(YAUSS)  ;TYBD;4,10,2003
	FOR YI=53,49,276:1:300,336:1:345,350:1:354 IF $GET(ARTTF(YI))'="" SET $piece(YARTINF(1),Y,YI)=ARTTF(YI)  ;TYBD;2,10,2003;TYBD;7,10,2003;24370
	
	; SAVE ^INARTXL
	SET @(YDATEI)=YARTINF(1)
	QUIT
]]></Routine>
</Export>