<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWCALDAY" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWCALDAY(DATE,CAL,STD)	;WWWCALDAY;PRÜFEN KALENDERTAG;11.09.2003 ;sift 
#include COMSYS
 
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		PRÜFEN KALENDERTAG
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| 	11.09.2003 
	;|
	;\------------------------------------------------------------------/
	;
	;$$^WWWCALDAY(+$H,1)
	;DATE=DATUM
	;CAL=KALENDER-VORGABE
	;STD=DEFAULT KALENDER BETRIEB (WENN CAL="")
	;
	;RETURN:
	;0 = ARBEITSTAG
	;1 = WOCHENENDE ;weekend 
	;2 = FEIERTAG ;holiday
	NEW RETVAL,TAG,MONAT,ARBTAGE
	
	SET RETVAL=""
	SET DATE=$GET(DATE)
	IF +DATE=0 SET DATE=+$HOROLOG
	SET CAL=$GET(CAL)  ;KALENDERVORGABE
 
	IF CAL="" && (+$GET(STD)=1) {
		SET CAL=$PIECE($GET(^WWW0121(0,YM,YLOCATION,1)),Y,54)  ;STANDARD KALENDERVORGABE BETRIEB ;location 
	}
 
	SET ARBTAGE=$PIECE($GET(^INVORG(YM,YM,1)),Y,17)  ;ARBEITSTAGE AUS VORGABE WWS/PPS MANAGER ;out of default WWS / PPS 
	IF +ARBTAGE=0 SET ARBTAGE=5  ;DFLT. @-NET MANAGER
	SET RETVAL=0
	
	;DO
	IF $DATA(^TERMIN9(0,SPRACHE,DATE)) {
		SET RETVAL=2  ;FEIERTAG STANDARD ;holiday 
	} else {
		IF CAL="" {
			IF $$^WWWDAY(DATE)>ARBTAGE SET RETVAL=1  ;WOCHENENDE STANDARD ;weekend 
		} else {
			IF $DATA(^WWWCAL1(0,CAL,DATE,1)) {
				SET RETVAL=2  ;FEIERTAG LT. FEIERTAGSKALENDER ;holiday 
			} else {
				SET TAG=$EXTRACT(100+$$^WWWDAYTAG(DATE),2,3)
				SET MONAT=$EXTRACT(100+$$^WWWMONTH(DATE),2,3)
				
				IF $DATA(^WWWCAL1s(0,1,1,$$$Index(TAG_"/"_MONAT),CAL)) {
					SET RETVAL=2  ;FEIERTAG ;holiday 
				} else {
					IF $PIECE($GET(^WWWKALENDER(0,CAL,1)),Y,2)="" {
						IF $$^WWWDAY(DATE)>ARBTAGE SET RETVAL=1  ;WOCHENDENDE
					} else {
						IF '$FIND(";"_$TRANSLATE($PIECE($GET(^WWWKALENDER(0,CAL,1)),Y,2),",",";")_";",";"_$$^WWWDAY(DATE)_";") SET RETVAL=1 ;WOCHENDENDE
					}
				}
			}
		}
	}
	
	QUIT RETVAL
]]></Routine>
</Export>