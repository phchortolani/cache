<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW100" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW100
	
#include COMSYS
#include WWWConst
#include COMConst
 ;SR17807
	
OnBeforeDataAccess()
	;-------------------------------------------------------------------------------
	; Check standard buttons in WWW120 and show changes in WWW120D	
	; 
	; ByRefs:
	;
	; History:
	; 21-Oct-2009	shobby	SRBR016969: Don't show the 17-Grid layout button if this
	; 							is not a grid form.
	; 07-Apr-2008	shobby	SRBR014446: corrected previous change on form
	; 							customising. (WWW120D)
	; 25-Feb-2007	shobby	SRBR014446: Only display 'Export To Excel' button on
	; 							'Edit Grid' forms.
	; 11-Dez-2007	GM		SRBR014601: Created
	;-------------------------------------------------------------------------------
 	new objWWW120,strStatus
	
 	set strStatus = $$$OK
  	if $$$KEY1(YKEY)="BUTTON" {
  		if ($get(YFORM)="WWW120") || ($get(YFORM)="WWW120D") {
   			if $get(YKEYCONTAINER)'="" {
		    	set objWWW120 = $get(^WWW120(0,$$$KEY1(YKEYCONTAINER),1))
		    	if ($$$WWW120FormType(objWWW120)'=12)         &&
		    	   (($$$KEY3(YKEY)=16) || ($$$KEY3(YKEY)=17))    {
			    	; 16-Export To Excel
			    	; 17-Grid Layout
			    	; do this test for both form types (07-Apr-2008)
			    	
			    	set strStatus='$$$OK
		    	
		    	} elseif $get(YFORM)="WWW120D"  { 
		    		set strStatus = ((";"_$$$WWW120DoNOTDisplayStandardButto(objWWW120)_";")'[(";"_$$$KEY3(YKEY)_";"))
				}
			}
		}
 	}
 	quit strStatus
	
	
GetFormat(pstrValue,pstrDefaultValue="",pSPRACHE="")
	;-------------------------------------------------------------------------------
	; Check if "FELDFORMAT" parameter exists in INPARA, WWW101 and WWW100 classes.
	;
	; Params: pstrValue			: The value we're after (assumed to be non-null)
	; 		  pstrDefaultValue	: The default value
	;        
	; Note: Required when patching SR17724 to 1.70-1.72
	;
	; ByRefs:
	;
	; Returns: strFormat
	;			e.g. for date     MM/DD/AAAA, TT.MM.YYYY, DD-MM-YYYY, YYYYMMDD, etc.
	;			     for numbers  NN,NNN.NN   or NN.NNN,NN
	;
	; History:
	; 27-May-2014	shobby	CORE-385: Loosen dependency on INPARA
	; 20-Jul-2011	shobby	SR17807: Moved from INPARA
	; 27-Aug-2007	shobby	SR13729: Only default pSPRACHE to SPRACHE if it is not
	; 							passed in as a parameter.
	; 23-Aug-2007	GM		SR13729: Included "pSPRACHE" parameter
	; 20-Aug-2007	GM		SR13729: Created
	;-------------------------------------------------------------------------------
	new objINPARA,objWWW100,objWWW101,strFormat
	
	set strFormat = ""
	if pSPRACHE="" set pSPRACHE = $get(SPRACHE)
	
	if (pSPRACHE'="") {
		if $get(YM)'="" {
			if ##class(%Library.Routine).Exists("INPARA") {
				new SPRACHE
				set SPRACHE=pSPRACHE
				set strFormat=$$GetParamDesc^INPARA("FELDFORMAT",pstrValue) ;CORE-385.
			}
		;	set objINPARA = $get(^INPARA(YM,"FELDFORMAT",pSPRACHE,pstrValue,1))
		;	set strFormat = $$$INPARAText(objINPARA)
		}
		if strFormat="" {
			set objWWW101 = $get(^WWW101(0,"FELDFORMAT",pSPRACHE,pstrValue,1))
			set strFormat = $$$WWW101Text(objWWW101)
		}
		if strFormat="" {
			set objWWW100 = $get(^WWW100(0,"FELDFORMAT",pSPRACHE,pstrValue,1)) 
			set strFormat = $$$WWW100Text(objWWW100) 
		}	
	}
	
	if strFormat="" {
		set strFormat = pstrDefaultValue
	}
	quit strFormat
	
]]></Routine>
</Export>