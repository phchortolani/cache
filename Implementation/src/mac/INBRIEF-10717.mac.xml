<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INBRIEF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INBRIEF(YDATEI) ;INBRIEF;DT;DRUCKEN SERIENBRIEFE;13.04.2000  ; Compiled February 9, 2005 10:54:41
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		DRUCKEN SERIENBRIEFE
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| DT	13.04.2000  
	;|
	;\------------------------------------------------------------------/
	;
	NEW YPERSON,LFN,LFN1,PERSONEN,LANDSPRACHE
	IF $GET(VORG(1))=""  DO ^WWWINFO($$^WWWTEXT(32310)) QUIT  ;KEINE AUSWAHL GETROFFEN   ;AN WEN ;no Selection upon who 
	IF $GET(VORG(2))=""  DO ^WWWINFO($$^WWWTEXT(32310)) QUIT  ;KEINE AUSWAHL GETROFFEN   ;WELCHEN BRIEF ;no Selection letter 
	IF $GET(YDATEI)="" SET YDATEI="INLIEF"
	SET BRIEF=$GET(^INBRIEF(YM,VORG(2),1))  ;FORMBRIEF VORHANDEN?
	QUIT:BRIEF=""
	DO ^WWWBACK
	DO ^WWWSTART()
	WRITE "<BODY bgcolor="_YWHITE
	WRITE " topmargin=1 leftmargin=1"
	WRITE ">"
	SET FONT=$P(BRIEF,Y,11)
	IF FONT'="" SET FONT=$P($G(^WWW100(0,"SCHRIFTART",SPRACHE,FONT,1)),Y,1)
	IF FONT="" SET FONT="courier"
	WRITE "<FONT SIZE=2 FACE="_""""_FONT_""""_">"
	DO BELEG
	WRITE "</FONT>"
	SET YNOFOOT=1   ;==> KEINE FUSSNOTE
	DO ^WWWSTOP
	QUIT
	
BELEG ;DRUCKEN BELEGE ;print 
	SET ADR(1)="^"_YDATEI_"(YM,ADR,1)"  ;DATEIVORGABE, IST VARIABEL, WEIL ROUTINE AUS VERSCHIEDENEN FORMULAREN AUFGERUFEN WERDEN KANN
	SET VORG(1)=$TRANSLATE(VORG(1),";",",")
	S LANDSPRACHE=SPRACHE
	FOR YI=1:1 SET ADR=$PIECE(VORG(1),",",YI) QUIT:ADR=""  DO   ;LAUFENDE ANSCHRIFT
	. KILL PERSONEN
	. SET ADR1=$GET(@ADR(1))
	. ;DURCHLAUF DURCH INPARTN UND PRÜFEN OB LFD.NR. STANDARDKONTAKT IST ;trans- And sift whether 
	. SET LANDSPRACHE=$P(ADR1,Y,69)  
	. IF LANDSPRACHE="" SET LANDSPRACHE=SPRACHE
	. SET LFN=""  
	. FOR  SET LFN=$ORDER(^INPARTN(YM,ADR,LFN)) QUIT:LFN=""  DO
	. . SET LFN1=$GET(^INPARTN(YM,ADR,LFN,1))
	. . IF $PIECE(LFN1,Y,26)=1 SET PERSONEN(LFN)=LFN1   ;JEDEN STANDARDKONTAKT IN "PERSON" ZWISCHENSPEICHERN
	. . QUIT
	. IF '$DATA(PERSONEN) SET PERSONEN(0)=""   ;WENN KEIN STANDARDKONTAKT VORHANDEN ;when no on hand 
	. SET LFN="" FOR  SET LFN=$ORDER(PERSONEN(LFN)) QUIT:LFN=""  DO  ;AUSLESEN DER STANDARDKONTAKTE ;pick out the 
	. . SET LFN1=$GET(PERSONEN(LFN))
	. . SET YPERSON=""
	. . DO ANSCHR(ADR1)
	. . DO BETREFF($PIECE(BRIEF,Y,2))
	. . DO ANREDE($PIECE(ADR1,Y,19))
	. . DO BRIEF(VORG(2))    ;($PIECE(BRIEF,Y,5))
	. . DO UNTER(YBED)
	. . DO FF^WWWW()
	. . QUIT
	. QUIT
	QUIT
	
ANSCHR(ADRESSE)  ;ANSCHRIFT
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<FONT FACE=ARIAL>"
	WRITE "<FONT SIZE=2>"
	SET ANSCH=""
	DO  ;ZUSAMMENSTELLEN ANSCHRIFT
	. NEW PLZ,ORT,PF
	. SET PF=0  ;S PF=+$G(VORG(3))   ;KEINE POSTFÄCHER ;no 
	. SET NUM=0
	. IF $G(LANDSPRACHE)="DE" IF $PIECE(ADRESSE,Y,11)'="" IF '$FIND($PIECE(ADRESSE,Y,11),"Post") SET $PIECE(ADRESSE,Y,11)="Postfach "_$PIECE(ADRESSE,Y,11)
	. IF $PIECE(ADRESSE,Y,4)'="" SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,4)
	. IF $PIECE(ADRESSE,Y,6)'="" SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,6)
	. IF $PIECE(ADRESSE,Y,7)'="" SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,7)
	. ;
	. ;SETZEN VON ANREDE, VORNAME UND NACHNAME
	. S YPERSON=""
	. IF $G(LFN1)'="" DO
	. . SET NUM=NUM+1  ;
	. . I $G(LANDSPRACHE)'="" IF $PIECE(LFN1,Y,1)'="" SET $PIECE(ANSCH,Y,NUM)=$PIECE($GET(^INPARA(YM,"ANREDE",LANDSPRACHE,$PIECE(LFN1,Y,1),1)),Y,1)_" "   ;ANREDE ;salutation 
	. . IF $PIECE(LFN1,Y,3)'="" SET $PIECE(ANSCH,Y,NUM)=$PIECE(ANSCH,Y,NUM)_$PIECE(LFN1,Y,3)_" "  ;ANREDE + VORNAME, FALLS KEINE ANREDE VORHANDEN
	. . IF $PIECE(LFN1,Y,2)'="" SET $PIECE(ANSCH,Y,NUM)=$PIECE(ANSCH,Y,NUM)_$PIECE(LFN1,Y,2)   ;ANREDE + VORNAME + NACHNAME ;salutation given name family name 
	. . SET YPERSON=$PIECE(LFN1,Y,31)
	. . IF YPERSON="" SET YPERSON=1    ;BRIEFANREDE HOLEN FÜR SPÄTER, ODER 1 ZUM ABBRUCH
	. . QUIT
	. ;
	. SET PLZ=$PIECE(ADRESSE,Y,12)
	. IF PF=1 DO   ;OHNE POSTFACH ;without 
	. . IF $PIECE(ADRESSE,Y,10)'="" SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,10)
	. . IF $PIECE(ADRESSE,Y,12)'="" SET PLZ=$PIECE(ADRESSE,Y,12)
	. . QUIT
	. IF PF=0 DO   ;MIT POSTFACH ;by means of 
	. . IF $PIECE(ADRESSE,Y,11)'="" SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,11)
	. . IF $PIECE(ADRESSE,Y,11)="" SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,10)
	. . IF $PIECE(ADRESSE,Y,13)'="" SET PLZ=$PIECE(ADRESSE,Y,13)
	. . QUIT
	. ;
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,50)'=1 DO
	. . NEW YSPRACHE
	. . SET YSPRACHE=$G(LANDSPRACHE)
	. . ;IF YSPRACHE="DE" IF $PIECE(ADRESSE,Y,14)'="" SET $PIECE(ADRESSE,Y,14)=$PIECE($GET(^WWW102(0,"BUNDESLAND",COUNTRY,$PIECE(ADRESSE,Y,14),1)),Y,1)
	. . SET NUM=NUM+2
	. . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,95)=1 DO  ;23531;ULM;30.04.2003
	. . . SET $PIECE(ANSCH,Y,NUM)=PLZ_" "_$PIECE(ADRESSE,Y,16)_" "_$PIECE(ADRESSE,Y,14)  ;STATE CODE
	. . . QUIT
	. . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,95)'=1 SET $PIECE(ANSCH,Y,NUM)=PLZ_" "_$PIECE(ADRESSE,Y,16)
	. . QUIT
	. ;
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,50)=1 DO
	. . NEW YSPRACHE
	. . SET YSPRACHE=$G(LANDSPRACHE)
	. . ;IF YSPRACHE="DE" IF $PIECE(ADRESSE,Y,14)'="" SET $PIECE(ADRESSE,Y,14)=$PIECE($GET(^WWW102(0,"BUNDESLAND",COUNTRY,$PIECE(ADRESSE,Y,14),1)),Y,1)
	. . SET NUM=NUM+2
	. . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,95)=1 DO
	. . . SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,16)_", "_$PIECE(ADRESSE,Y,14)_" "_PLZ   ;STATE CODE;23531;ULM;29.04.2003
	. . . QUIT
	. . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,95)'=1 SET $PIECE(ANSCH,Y,NUM)=$PIECE(ADRESSE,Y,16)_" "_PLZ
	. . QUIT
	. I $G(LANDSPRACHE)'="" IF $PIECE(ADRESSE,Y,17)'="" IF $PIECE(ADRESSE,Y,17)'=$GET(YCOUNTRY) SET NUM=NUM+1 SET $PIECE(ANSCH,Y,NUM)=$PIECE($GET(^WWW100(0,"LAND",LANDSPRACHE,$PIECE(ADRESSE,Y,17),1)),Y,1)
	. QUIT
	DO  ;DRUCKEN ANSCHRIFT ;print 
	. NEW YI
	. FOR YI=1:1:9 DO
	. . WRITE $EXTRACT($PIECE(ANSCH,Y,YI),1,40)
	. . WRITE "<BR>"
	. . QUIT
	. QUIT
	QUIT
	
BETREFF(BETREFF) ;BETREFF DES BRIEFES
	SET BETREFF=$GET(BETREFF)
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<TABLE CELLSPACING=0 BORDER=0 WIDTH=90%>"
	WRITE "<TR>"
	WRITE "<TD ALIGN=RIGHT WIDTH=100%>"
	WRITE "<FONT SIZE=2>"
	SET YDATE=+$HOROLOG
	WRITE $$^WWWDATE(YDATE)   ;DATUM ;Date  ;Date Date 
	WRITE "</TD>"
	WRITE "</TR>"
	WRITE "<TR>"
	WRITE "<TD ALIGN=LEFT>"
	WRITE "<FONT SIZE=2>"
	WRITE "<B>"
	WRITE YCR,BETREFF   ;BETREFF
	WRITE "</TD>"
	WRITE "<TD ALIGN=LEFT WIDTH=15%>"
	WRITE "<FONT SIZE=2>"
	WRITE "</B>"
	WRITE "</TD>"
	WRITE "</TR>"
	WRITE YCR
	WRITE "</TABLE>"
	QUIT
	
ANREDE(ANREDE)   ;BRIEFTEXT
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE "<BR>"
	IF $TRANSLATE($G(YPERSON)," ,")'="" IF YPERSON'=1 SET ANREDE=YPERSON  ;YPERSON WIRD IN ANSCHRIFT GESETZT, WENN STANDARDKONTAKT GESETZT IST
	IF ANREDE="" WRITE $$^WWWTEXT(33035)
	WRITE $GET(ANREDE) 
	WRITE "<BR>"
	WRITE "<BR>"
	QUIT
	
BRIEF(BRIEFNUMM)   ;BRIEFNUMMER
	NEW YI,TEXT
	WRITE YCR
	WRITE "<TABLE CELLSPACING=0 BORDER=0 WIDTH=100%>"
	WRITE "<TR>"
	WRITE "<TD ALIGN=LEFT WIDTH=100%>"
	WRITE "<FONT SIZE=2>"
	;FOR YI=1:1 QUIT:$PIECE(BRIEFTEXT,"|",YI,900)=""  DO
	;.SET TEXT=$PIECE(BRIEFTEXT,"|",YI)
	;.WRITE TEXT
	;.WRITE "<BR>"
	;.QUIT
	;WRITE BRIEFTEXT
	DO HTML(BRIEFNUMM)
	WRITE YCR
	WRITE "</TD>"
	;WRITE "</TR>"
	WRITE "</TR>"
	WRITE YCR
	WRITE "</TABLE>"
	QUIT
	
BRIEF1(BRIEFTEXT)   ;BRIEFTEXT
	NEW YI,TEXT
	WRITE YCR
	WRITE "<TABLE CELLSPACING=0 BORDER=0 WIDTH=100%>"
	WRITE "<TR>"
	WRITE "<TD ALIGN=LEFT WIDTH=100%>"
	WRITE "<FONT SIZE=2>"
	FOR YI=1:1 QUIT:$PIECE(BRIEFTEXT,"|",YI,900)=""  DO
	. SET TEXT=$PIECE(BRIEFTEXT,"|",YI)
	. WRITE TEXT
	. WRITE "<BR>"
	. QUIT
	WRITE YCR
	WRITE "</TD>"
	;WRITE "</TR>"
	WRITE "</TR>"
	WRITE YCR
	WRITE "</TABLE>"
	QUIT
	
UNTER(YBED)   ;UNTERSCHRIFT ;signature 
	QUIT:YBED=""
	WRITE "<BR>"
	WRITE "<BR>"
	WRITE YCR
	IF $PIECE($GET(^WWW013(0,YBED,1)),Y,58)'="" WRITE $PIECE($GET(^WWW013(0,YBED,1)),Y,58)_" "
	IF '$FIND($PIECE($GET(^WWW013(0,YBED,1)),Y,1),",") DO
	. WRITE $PIECE($GET(^WWW013(0,YBED,1)),Y,1)
	. QUIT
	IF $FIND($PIECE($GET(^WWW013(0,YBED,1)),Y,1),",") DO
	. WRITE $PIECE($PIECE($GET(^WWW013(0,YBED,1)),Y,1),",",2,99)
	. WRITE " "
	. WRITE $PIECE($PIECE($GET(^WWW013(0,YBED,1)),Y,1),",",1)
	. QUIT
	QUIT
	
HTML(X) ;KAS;DRUCKEN EINER HTML-SEITE;11APR2001; ;print unit 
	QUIT:X=""
	QUIT:$GET(^INBRIEF(YM,X,1))=""  ;X IST TEXTINHALT AUS BRIEF; MÜSSTE HIER BRIEFNUMMER SEIN, SONST "" ;ULM,19.08.2002
	NEW A,IA,IB,IE,IT,L,MC,P
	IF $EXTRACT($PIECE(^(1),Y,2),1,2)="@@" SET A="" XECUTE $TRANSLATE($PIECE(^(1),Y,5),"|") WRITE A QUIT
	IF $EXTRACT($PIECE(^(1),Y,2))="@" WRITE @$TRANSLATE($PIECE(^(1),Y,5),"|") QUIT
	SET X=$PIECE(^(1),Y,5)
	SET P=0 FOR  SET P=$FIND(X,"|",P) QUIT:'P  SET $EXTRACT(X,P-1)="<BR>"_YCR
	;  kommentar-analyse
	SET P=0 FOR  SET IA=$FIND(X,"<!--",P),IB=$FIND(X,"-->",IA) QUIT:'IA  QUIT:'IB  DO  SET P=IB ;"<!--" GEFUNDEN
	. SET IT=$EXTRACT(X,IA,IB-4)
	. FOR  QUIT:$EXTRACT(IT)'=" "  SET $EXTRACT(IT)=""
	. FOR  QUIT:$EXTRACT(IT,$LENGTH(IT))'=" "  SET $EXTRACT(IT,$LENGTH(IT))=""
	. QUIT:$EXTRACT(IT,1,3)'="IN("  QUIT:$EXTRACT(IT,$LENGTH(IT))'=")"  ;INHALT = "IN(...)"?
	. SET L=IT,$EXTRACT(L,1,$FIND(L,"(")-1)="",$EXTRACT(L,$LENGTH(L))=""  ;name = SYSTEMVARIABLE ODER FUNKTION ODER UP? ;Or Or 
	. IF $EXTRACT(L,1,2)="@@" NEW A SET A="" XECUTE $EXTRACT(L,3,$LENGTH(L)) SET $EXTRACT(X,IA-4,IB-1)=A,IB=IA-4 QUIT  ;UNTERPROGRAMM AUSFUEHREN ;Program 
	. IF $DATA(@IT)#11 SET $EXTRACT(X,IA-4,IB-1)=@IT,IB=IA-4 QUIT  ;IN(name) VORHANDEN --> KOMMENTAR ERSETZEN ;on hand commentary take the place of 
	. SET @("L="_L),$EXTRACT(X,IA-4,IB-1)=L,IB=IA-4 QUIT  ;JA --> KOMMENTAR ERSETZEN ;yes commentary take the place of 
	. QUIT
	SET P=0 FOR  DO  QUIT:'IA
	. NEW IN
	. ; $macro - analyse
	. SET IA=$FIND(X,"$",P) IF IA DO  QUIT
	. . SET A="" 
	. . FOR L=IA:1:IA+63 QUIT:$LENGTH(X)<L  IF $DATA(^INBRIEF(YM,$EXTRACT(X,IA,L),1)) SET A=$EXTRACT(X,IA,L),IA=IA-1 QUIT
	. . WRITE $EXTRACT(X,P,IA-1) 
	. . SET P=IA
	. . IF A'="" DO HTML(A) SET P=P+$LENGTH(A)+1
	. . QUIT
	. ; <macro>...</macro> - analyse
	. SET IA=$FIND(X,"<",P),IB=$FIND(X,">",IA)
	. IF ('IA)!('IB) WRITE $EXTRACT(X,P,$LENGTH(X)) QUIT
	. SET IT=$EXTRACT(X,IA,IB-2) SET:$EXTRACT(IT,$LENGTH(IT))="/" $EXTRACT(IT,$LENGTH(IT))=""
	. SET MC=$PIECE(IT," ",1) DO HTPA(IT) ;MACRO NAME=1ST PART
	. IF MC="" WRITE $EXTRACT(X,P,IB) SET P=IB+1 QUIT  ; <> DURCHREICHEN
	. IF $LENGTH(MC)>64 WRITE $EXTRACT(X,P,IB) SET P=IB+1 QUIT  ; <NAME...> AUSDRUCK ZU LANG: DURCHREICHEN
	. IF $GET(^INBRIEF(YM,MC,1))="" WRITE $EXTRACT(X,P,IB) SET P=IB+1 QUIT  ; <NAME> NICHT ANGELEGT: DURCHREICHEN ;Not 
	. SET IE=0 SET:$EXTRACT(X,IB-2,IB-1)="/>" IE=IB SET:'IE IE=$FIND(X,"</"_MC_">",IB)
	. IF 'IE WRITE $EXTRACT(X,P,IB) SET P=IB+1 QUIT  ; </NAME> NICHT GEFUNDEN: <NAME> DURCHREICHEN ;Not 
	. WRITE $EXTRACT(X,P,IA-2) DO HTIN($EXTRACT(X,IB,IE-$LENGTH(IT)-4)),HTML(MC) SET P=IE
	. QUIT
	QUIT
	
HTIN(X) ;AUFTEILUNG EINES STRINGS IN SUBSTRINGS UND VARIABLEN ;within And 
	NEW I,IA,IB,IC,P
	SET IN=X
	FOR I=1:1:$LENGTH(X,",") SET IN(I)=$PIECE(X,",",I)
	SET P=0 FOR  SET IA=$FIND(X,"&",P) QUIT:'IA  SET IB=$FIND(X,"=",IA) QUIT:'IB  SET IC=$FIND(X,"&",IB) SET:'IC IC=$LENGTH(X)+2 SET:IB>(IA+2) IN($EXTRACT(X,IA,IB-2))=$$HTHX($EXTRACT(X,IB,IC-2)),P=IC-1
	SET IA="" FOR  SET IA=$ORDER(IN(IA)) QUIT:IA=""  SET IN(IA)=$$HTHX(IN(IA))
	QUIT
	
HTPA(X) ;PARAMETER ERMITTELN ;parameter find 
	NEW IA,IE
	SET IE=$FIND(X," ") ;MACRO NAME ABTRENNEN
	FOR  QUIT:X=""  DO
	. SET $EXTRACT(X,1,IE-1)="" 
	. FOR  QUIT:$EXTRACT(X)'=" "  SET $EXTRACT(X)=""
	. SET IA=$FIND(X,"=") IF 'IA SET X="" QUIT
	. QUIT:IA<3
	. IF $EXTRACT(X,IA)="""" SET IE=$FIND(X,"""",IA+1) SET:'IE IE=$LENGTH(X)+1 SET IN($EXTRACT(X,1,IA-2))=$EXTRACT(X,IA+1,IE-2) QUIT
	. IF $EXTRACT(X,IA)="'" SET IE=$FIND(X,"'",IA+1) SET:'IE IE=$LENGTH(X)+1 SET IN($EXTRACT(X,1,IA-2))=$EXTRACT(X,IA+1,IE-2) QUIT
	. SET IE=$FIND(X," ",IA+1) SET:'IE IE=$LENGTH(X)+1 SET IN($EXTRACT(X,1,IA-2))=$EXTRACT(X,IA,IE-1)
	. QUIT
	SET IA="" FOR  SET IA=$ORDER(IN(IA)) QUIT:IA=""  SET IN(IA)=$$HTHX(IN(IA))
	QUIT
	
HTHX(X) ;UMWANDLUNG %nn NACH $C(0Xnn) ;within 
	NEW A,AH,AL,I
	SET I=0 FOR  SET I=$FIND(X,"%",I) QUIT:'I  DO  SET:A'<0 $EXTRACT(X,I-1,I+1)=$CHAR(A)
	. SET A=-1
	. QUIT:$EXTRACT(X,I)=""  SET AH=$FIND("0123456789abcdef",$TRANSLATE($EXTRACT(X,I),"ABCDEF","abcdef"))-2 QUIT:AH<0
	. QUIT:$EXTRACT(X,I+1)=""  SET AL=$FIND("0123456789abcdef",$TRANSLATE($EXTRACT(X,I+1),"ABCDEF","abcdef"))-2 QUIT:AL<0
	. SET A=AH*16+AL
	. QUIT
	QUIT X
]]></Routine>
</Export>