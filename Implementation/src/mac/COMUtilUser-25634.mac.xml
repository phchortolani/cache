<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilUser" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilUser
    ;-------------------------------------------------------------------------------
    ; Common Utilities for Error processing
    ; 
    ; See also : 
    ; 	COMUtilDate		Date/Time Utilities
    ; 	COMUtilStr		String Manipulation
    ; 	COMUtilGlo		Global Manipulation
    ; 	COMUtilForm		@netManager Form Utilities
    ; 	COMUtilClass	@netManager Class Utilities
    ;-------------------------------------------------------------------------------
#include COMConst
#include WWWConst
#include COMSYS
	
GetDataBaseAdministrator()
    ;-------------------------------------------------------------------------------
	; A list of database administrators
	; Used by COMUtilError to determine who is to receive error messages
	; 
	; History:
	; 04-Apr-2007	GRF		SR15492: Don't include terminated employees
	; 20-Mar-2007	GRF		SR12293: Created; moved subroutine from COMUtilError
    ;-------------------------------------------------------------------------------
	new dteTerminated,idUser,idUserAccess,lstUsers
	
	set lstUsers     = ""
	set idUserAccess = ""
	for {
		set idUserAccess=$order(^WWW013s(0,5,idUserAccess))
		quit:idUserAccess=""
		quit:idUserAccess>2  ;2 is database administrators
		
		set idUser=""
		for {
			set idUser=$order(^WWW013s(0,5,idUserAccess,idUser))
			quit:idUser=""
			
			set dteTerminated = $$$WWW013TerminationOn($get(^WWW013(0,idUser,1)))                               ; SR15492
			if (dteTerminated="") || (dteTerminated>$horolog) {                   ;current or not yet finished  ; SR15492 
				if '$listfind(lstUsers,idUser) { // SR12293: Stop me being done twice if I am both a SysAdmin and a DB Admin
					set lstUsers=lstUsers_$listbuild(idUser)
				}
			}
		}
	}
	quit lstUsers
	
]]></Routine>
</Export>