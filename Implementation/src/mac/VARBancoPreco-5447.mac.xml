<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARBancoPreco" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARBancoPreco
#include COMSYS
#include INConst
#include COMConst
#include VARConst

OnBeforeFormConstruction(pYKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Quando o usuário vem de um link externo a essa tela, caso tenha vindo com a 
    ;	chave primária no formato External,idItem ele já carrega o resultado na tela
    ; 	sem que o usuário tenha que clicar no 'Ok'.
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")

	if ($$$KEY1(pYKEY) = "External") {
		do VARLogAfterSave^VARUtil(YBED,YFORM)
		set $piece(^VARTempBancoPreco(YM,YBED,1),Y,1) = $$$KEY2(pYKEY)
	}

	quit

OnAfterSave
	new idProduto
	set idProduto = $get(VORG(1))
	
	set $piece(^VARTempBancoPreco(YM,YBED,1),Y,1) = idProduto	

	do VARLogAfterSave^VARUtil(YBED,YFORM)
	do GoToForm^COMUtilForm("VARBancoPreco",idProduto)
	
	quit
	

OnAfterDataFields
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo OnAfterDataFields do form VARBancoPreco.
    ; 
    ; Histórico:
    ;	03-Mai-2012  Gustavo	Incluído VARLogAfterSave
    ;	29-Set-2011  Gustavo	Atualizado - inserida a parte de ata
    ; 	27-Jul-2010  			Criado
    ;-------------------------------------------------------------------------------
	new flterItem
	set flterItem = $piece($get(^VARTempBancoPreco(YM,YBED,1)),Y,1)
	
	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)

	do HistoricoPrecoCompra(flterItem)			;Cabeçalho + Compras
	do PrintHistoricoPrecoAta(flterItem)		;Ata de Registro de Preços
	do PrintHistoricoCotacao(flterItem)			;Estimativa de Preços
	do PrintCatalogoPrecos(flterItem)			;Resumo
	
	quit
	
HistoricoPrecoCompra(pidItem)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARBancoPreco.mac.
    ; 
    ; Histórico:
    ; 	27-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	if (pidItem = "") {
		w "<br />"	
		w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:6px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
		w "	<font size=2>"
		w "		<strong>Selecione um produto para consultar no banco de preços</strong>."
		w "	</font>"
		w "</div>"
		w "<br />"
		quit
	}

	do WidgetDadosDoProduto^VARConsultaGerencialProduto(pidItem)
	do PrintHistoricoPrecoCompra(pidItem)

	quit

PrintHistoricoPrecoAta(pidItem)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARBancoPreco.mac.
    ; 
    ; Histórico:
    ; 	29-Set-2010  Gustavo	Criado
    ;-------------------------------------------------------------------------------
	quit:(pidItem = "") 	
 	
 	new meuSQL, lstHeader, count, idAta, objAta, dteAta, idAtaLinha, objAtaLinha, idProduto, idUnidade, 
 		fltPrice, idFornecedor, idMarca, fltSaldoItem, dteValidade, situacaoValidade

	set meuSQL = "SELECT AtaLinha.CodAta,"
	set meuSQL = meuSQL_" AtaLinha.LinhadaAta,"
	set meuSQL = meuSQL_" AtaLinha.Produto,"
	set meuSQL = meuSQL_" AtaLinha.Unidade,"
	set meuSQL = meuSQL_" AtaLinha.ValorUnitario,"
	set meuSQL = meuSQL_" AtaLinha.Fornecedor,"
	set meuSQL = meuSQL_" Ata.DataHomologacao"
	set meuSQL = meuSQL_" FROM VARAta Ata"
	set meuSQL = meuSQL_" LEFT JOIN VARAtaLinha AtaLinha on (Ata.CodAta = AtaLinha.CodAta)"
	set meuSQL = meuSQL_" WHERE UPPER(AtaLinha.Produto) = "_pidItem
	set meuSQL = meuSQL_" ORDER BY Ata.DataHomologacao DESC"
	
	do formulaData.Prepare(meuSQL)  ;Prepare Query
	do formulaData.Execute()  		;Execute Query

	set lstHeader = ""
 	set lstHeader = lstHeader_$listbuild("#","Ata","Item","Fornecedor","Fabricante","Data da Ata","Situação","Saldo","Preço Unitário") 

	write "<div style=margin-left:6px;line-height:21px;>"

	if $$Start^COMTable(lstHeader,"Histórico de Atas de Registro de Preços",,,$$$NO) {		
		
		set count = 0
	
		while (formulaData.Next()) {
							
 			set count = $i(count)
 			
 			set idAta		 = formulaData.GetData(1)
 			set idAtaLinha 	 = formulaData.GetData(2)
 			set idProduto	 = formulaData.GetData(3)
 			set idUnidade	 = formulaData.GetData(4)
 			set fltPrice 	 = formulaData.GetData(5)
 			set idFornecedor = formulaData.GetData(6)
 			set dteAta		 = formulaData.GetData(7)
 			set idMarca   	 = "" ;Ainda não implementado
 			
 			set fltSaldoItem = $$SQLGetSaldoItemQuant^VARAtaLinha(idAta,idAtaLinha)
 			set dteValidade  = $$GetValidadeAta^VARAta(idAta) //Substituir posteriormente por método que retorna vigência
 			
 			if (dteValidade < $horolog) {
	 			set situacaoValidade = "Vencida"
 			} else {
	 			set situacaoValidade = "Vigente"
 			} 			
 			
			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idAta,"VARAta",idAta,,,,,,$$$YES)
			do InsertCell^COMTable(idAtaLinha,"VARAta",idAta,,,,,,$$$YES)
			do InsertCell^COMTable($$SQLGetDescFornecedor^VARSQL(idFornecedor),"VARAta",idAta,,,,,,$$$YES)							
			do InsertCell^COMTable(idMarca)				
			do InsertCell^COMTable($zdate(dteAta,4),,,,"CENTER")
			do InsertCell^COMTable(situacaoValidade)
			do InsertCell^COMTable($$^WWWTR(0,12,fltSaldoItem)_" "_$$SQLGetSiglaUnit^VARSQL(idUnidade),,,,"RIGHT")
			do InsertCurrencyCell^COMTable(fltPrice)
			do EndLine^COMTable() 		
		}
		
		if (count = 0)  {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhuma ata encontrada para este produto.",,,,,,9)
			do EndLine^COMTable()
		}
 	
 		do Stop^COMTable()
 		do formulaData.Close()
	}
	
	write "</div>"	
	
	quit
	
PrintHistoricoPrecoCompra(pidItem)
 	quit:(pidItem = "") 	
 	
 	new lstHeader, fltQty, fltPrice, fltTotal, fabricante, supplier, dteOrder, idCompra, empenho
 
	set formulaData = ##class(%ResultSet).%New()  ;Create Result Set Object
 
	set meuSQL = "SELECT *"
	set meuSQL = meuSQL_" FROM Report.VARHistoricoPrecoCompra"
	set meuSQL = meuSQL_" WHERE Produto = "_pidItem
	set meuSQL = meuSQL_" ORDER BY ordenamento DESC"
	
	do formulaData.Prepare(meuSQL)  ;Prepare Query
	do formulaData.Execute()  ;Execute Query
 
	set lstHeader = ""
  	;set lstHeader = lstHeader_$listbuild("#","Compra","Empenho","Fornecedor","Fabricante","Quantidade","Preço Unitário","Valor Total","Data de Emissão")
 	set lstHeader = lstHeader_$listbuild("#","Compra","Empenho","Fornecedor","Fabricante","Data de Emissão","Preço Unitário")

	write "<div style=margin-left:6px;line-height:21px;>"
		
	if $$Start^COMTable(lstHeader,"Histórico de Preços por Compra",,,$$$NO) {		
		
		set count = 0
		
		while (formulaData.Next()) {
			
			set count = $i(count)
			
			;set fltQty     = formulaData.GetData(1)
			set fltPrice   = formulaData.GetData(2)
			;set item       = formulaData.GetData(3)
			;set fltTotal   = formulaData.GetData(4)
			set fabricante = formulaData.GetData(5)
			set supplier   = formulaData.GetData(6)
			set dteOrder   = formulaData.GetData(7)
			set idCompra   = formulaData.GetData(8)
			set empenho	   = formulaData.GetData(9)

			;Caso o preço esteja vazio, não interessa
			continue:(fltPrice = "")
			
			//Removendo a hora
			set dteOrder = $piece(dteOrder," ",1)

			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idCompra,"VARCompra",idCompra,,,,,,$$$YES)
			do InsertCell^COMTable(empenho,"VARCompra",idCompra,,,,,,$$$YES)
			do InsertCell^COMTable(supplier,"VARCompra",idCompra,,,,,,$$$YES)
			do InsertCell^COMTable($$GetBrandName^VARSQL(fabricante))
			;do InsertCell^COMTable($$^WWWTR(0,12,fltQty),"VARCompra",idCompra,,"RIGHT")
			;do InsertCurrencyCell^COMTable(fltTotal,,"VARCompra",idCompra)
			do InsertCell^COMTable(dteOrder,,,,"CENTER",,,,)
			do InsertCurrencyCell^COMTable(fltPrice)			
			do EndLine^COMTable()
		}
		
		if (count = 0)  {
				do NewLine^COMTable()
				do InsertCell^COMTable("Nenhum registro de compra encontrado para este produto.",,,,,,9)
				do EndLine^COMTable()
		}
		
		do Stop^COMTable()
		do formulaData.Close()
	}
	
	write "</div>"
		
	quit
	
PrintCatalogoPrecos(pidItem)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARBancoPreco.mac.
    ; 
    ; Histórico:
    ; 	27-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidItem = "")
	
	new lstHeader, idLinha, objBancoPrecoLinha, idFornecedor, fltPreco, dtePreco, count

	write "<div style=margin-left:6px;line-height:21px;>"
	
	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("#","Fornecedor","Data do Preço","Preço Unitário")

	if $$Start^COMTable(lstHeader,"Catálogo de Preços por Fornecedor",,,$$$NO) {		
		
		set count = 0
		
		$$$Order3(^VARBancoPrecoLinha,YM,pidItem,idLinha)
			
			set count = $i(count)						
			set objBancoPrecoLinha = $get(^VARBancoPrecoLinha(YM,pidItem,idLinha,1))
			
			set idFornecedor = $piece(objBancoPrecoLinha,Y,1)
			set fltPreco     = $piece(objBancoPrecoLinha,Y,2)
			set dtePreco	 = $piece(objBancoPrecoLinha,Y,3)
			
			;Caso o preço esteja vazio, não interessa
			continue:(fltPreco = "")

			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idFornecedor_" - "_$$SQLGetDescFornecedor^VARSQL(idFornecedor),"VARBancoPrecoCatalogo",pidItem,,,,,,)
			do InsertCell^COMTable($zdate(dtePreco,4),,,,"CENTER")
			do InsertCurrencyCell^COMTable(fltPreco)
			do EndLine^COMTable()
	
		$$$End
		
		if (count = 0) {
			do NewLine^COMTable()	
			do InsertCell^COMTable("Nenhum preço registrado em catálogo para este fornecedor.","VARBancoPrecoCatalogo",pidItem,,,,4)
			do EndLine^COMTable()
		}
	
		do Stop^COMTable()	
	}
	
	write "</div>"
	write "<br />"
	
	quit

PrintHistoricoCotacao(pidItem)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARBancoPreco.mac.
    ; 
    ; Histórico:
    ; 	11-Ago-2010		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidItem = "")
	
	new lstHeader, idEstimativa, idEstimativaLinha, idPropostaLinha, objPropostaLinha, 
		idFornecedor, fltPreco, dtePreco, count, key
	
	write "<br />"
	write "<div style=margin-left:6px;line-height:21px;>"

	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("#","Fornecedor","No. Estimativa","Data da Estimativa","Preço Unitário")

	if $$Start^COMTable(lstHeader,"Histórico de Propostas em Estimativa de Preços",,,$$$NO) {		
		
		set count = 0
		
		$$$Order4(^VARBancoPrecoEstimativaLinhas,YM,1,pidItem,idEstimativa)
			$$$Order5(^VARBancoPrecoEstimativaLinhas,YM,1,pidItem,idEstimativa,idEstimativaLinha)
				$$$Order4(^VARBancoPrecoEstimativaProp,YM,idEstimativa,idEstimativaLinha,idPropostaLinha)
			
			set count = $i(count)
			set objPropostaLinha = $get(^VARBancoPrecoEstimativaProp(YM,idEstimativa,idEstimativaLinha,idPropostaLinha,1))
			
			set idFornecedor = $piece(objPropostaLinha,Y,1)
			set fltPreco     = $piece(objPropostaLinha,Y,3)
			set dtePreco	 = $piece(objPropostaLinha,Y,6)
			
			;Caso o preço esteja vazio, não interessa
			continue:(fltPreco = "")
			
			set key = idEstimativa_$$$COMMA_idEstimativaLinha
			
			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idFornecedor_" - "_$$SQLGetDescFornecedor^VARSQL(idFornecedor),"VARBancoPrecoEstimativaPropCad",key,,,,,,$$$YES)
			do InsertCell^COMTable(idEstimativa,"VARBancoPrecoEstimativaPropCad",key,,,,,,$$$YES)
			do InsertCell^COMTable($zdate(dtePreco,4),,,,"CENTER")
			do InsertCurrencyCell^COMTable(fltPreco)
			do EndLine^COMTable()
			
				$$$End
			$$$End
		$$$End
		
		if (count = 0) {
			do NewLine^COMTable()	
			do InsertCell^COMTable("Nenhum preço registrado em propostas de estimativas de preços.",,,,,,5)
			do EndLine^COMTable()
		}
	
		do Stop^COMTable()	
	}
	
	write "</div>"
	write "<br />"
	
	quit
	
Imprimir(pYKEY)
	if (pYKEY = "") set pYKEY = $piece($get(^VARTempBancoPreco(YM,YBED,1)),Y,1)
	do RunReportHistoricoPrecos^VARJasperRunReport(pYKEY)
	quit
]]></Routine>
</Export>