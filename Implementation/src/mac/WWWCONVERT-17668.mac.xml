<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWCONVERT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWCONVERT(YTRANS,YSHOW,YPARAMETER)
	
#include COMSYS
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		STARTEN DATENUMFORMUNG
	;	DO ^WWWCONVERT(CONVERT,SHOW,YPARAMETER)  ;LFD UMFORMUNG, 1=SHOW RESULT
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 01/08/2003	DT
	;-------------------------------------------------------------------------------
	DO ^WWWVAR
	
	NEW I,YDATEI,YDATEI1,YA,YKEY,YFELD,MAXKEY,YLFN,YI,%FELD1,YSATZ,YEXEC
	
	; NOTE : YPARA will persist since not an arg and not in NEW command; not used directly here - perhaps in called routines
	SET YPARA = $GET(YPARAMETER)
	QUIT:$GET(YTRANS)=""
	SET YSATZ = $GET(^WWWCONVERT(0,YTRANS,1))
	QUIT:YSATZ=""
	SET YDATEI  = $PIECE(YSATZ,Y,1)
	SET YDATEI1 = $PIECE(YSATZ,Y,2)
	
	NEW I,YEXEC,YYYM
	
	SET YEXEC = $PIECE(YSATZ,Y,4)
	IF YEXEC'="" XECUTE YEXEC                     ; *** EXECUTE ***  (Execute At Conversion)
	QUIT:YDATEI=""
	QUIT:YDATEI1=""
	
	SET MAXKEY = +$ORDER(^WWW002(0,YDATEI,""),-1)
	IF MAXKEY=0 SET MAXKEY=1
	IF $PIECE(YSATZ,Y,3)=1 DO   ;DELETE TARGET BEFORE CONVERT
	. SET YA="^"_YDATEI1      KILL @YA
	. SET YA="^"_YDATEI1_"s"  KILL @YA
	. SET YA="^"_YDATEI1_"b"  KILL @YA
	. SET YA="^"_YDATEI1_"bi" KILL @YA
	. SET YA="^"_YDATEI1_"t"  KILL @YA
	
	SET YDATA=$GET(^WWW001(0,YDATEI,1))
	SET YA="^"_YDATEI_"("_$$^WWWYM(YDATEI,1)
	IF $PIECE(YDATA,Y,12)'="" IF $PIECE(YDATA,Y,13)'="" DO  ;UCI UND VOL ;UCI And 
	. SET YA="^["_""""_$PIECE(YDATA,Y,12)_""""_","_""""_$PIECE(YDATA,Y,13)_""""_"]"_$PIECE(YA,"^",2,999)
	
	SET YA(1)=YA
	SET YA=YA_"-1)"
	FOR  DO NEXT QUIT:YA=""  DO SATZ
	IF $PIECE(YSATZ,Y,6)=1 DO  ;DELETE SOURCE AFTER CONVERT ;rump 
	. SET YA="^"_YDATEI      KILL @YA
	. SET YA="^"_YDATEI_"s"  KILL @YA
	. SET YA="^"_YDATEI_"b"  KILL @YA
	. SET YA="^"_YDATEI_"bi" KILL @YA
	. SET YA="^"_YDATEI_"t"  KILL @YA
	
	IF $PIECE(YSATZ,Y,9)'="" DO ^WWWCONVERT($PIECE(YSATZ,Y,9))  ;NEXT CONVERT AFTER CONVERT 
	QUIT
	
	
NEXT ;NACHSTER DATENSATZ ;data record 
	SET YA=$QUERY(@YA)
	IF '$FIND($TRANSLATE(YA,""""),$TRANSLATE(YA(1),"""")) SET YA=""
	QUIT
	
	
SATZ ;EINZELSAETZE
	SET %FELD1 = ""
	IF +$PIECE(YDATA,Y,8)'=0 SET %FELD1 = $$^WWWSETL(YA)
	IF +$PIECE(YDATA,Y,8)=0  SET %FELD1 = $GET(@YA)
	SET YFELD = %FELD1  ;ZUR PRÃœFUNG
	SET Q  = 0
	SET YQ = 0
	;F $PIECE(YSATZ,Y,5)=""  SET $PIECE(YSATZ,Y,5) = "+"   ;AUTO ID
	IF $PIECE(YSATZ,Y,5)=""  SET YKEY  = $PIECE(YA,",",2)
	IF $PIECE(YSATZ,Y,5)'="" SET YKEY  = $PIECE(%FELD1,Y,$PIECE(YSATZ,Y,5))
	IF $PIECE(YSATZ,Y,5)="+" SET YKEY  = $$^WWWNEXT(YDATEI1)
	IF $PIECE(YSATZ,Y,7)'="" SET YEXEC = $PIECE(YSATZ,Y,7) XECUTE YEXEC   ;EXECUTE FOR ID;NO SAVE= YQ OR Q=1 ;EXECUTE ID 
	QUIT:YQ=1  ;NO SAVE
	QUIT:Q=1   ;NO SAVE
	
	IF YKEY="" SET YKEY = $$^WWWNEXT(YDATEI1)
	QUIT:YKEY=""
	
	SET YFELD = ""
	SET YLFN  = ""
	FOR  SET YLFN = $ORDER(^WWWCONVERT1(0,YTRANS,YLFN)) QUIT:YLFN=""  DO
	. NEW YSATZ
	. SET YSATZ   = $GET(^WWWCONVERT1(0,YTRANS,YLFN,1))
	. SET YINHALT = $PIECE(YSATZ,Y,8)                                             ;FIXED
	. IF $PIECE(YSATZ,Y,1)'="" SET YINHALT = $PIECE(%FELD1,Y,$PIECE(YSATZ,Y,1))
	. SET YEXEC = $PIECE(YSATZ,Y,3)
	. IF YEXEC'="" XECUTE YEXEC                                                     ; *** EXECUTE ***  (Execute At Conversion)
	. IF $PIECE(YSATZ,Y,4)'="" SET YINHALT = $TRANSLATE(YINHALT,$PIECE(YSATZ,Y,4))  ;AUSFILTERN
	. IF $PIECE(YSATZ,Y,5)'="" SET YINHALT = $$^WWWTRANSLATE(YINHALT,$PIECE(YSATZ,Y,5),$PIECE(YSATZ,Y,6))  ;UMSETZTEN
	. ;
	. IF YINHALT'="" IF $DATA(^WWWCONVERT2(0,YTRANS,YLFN,YINHALT,1)) DO             ; Conversion Table Arrays
	. . SET YINHALT = $PIECE($GET(^WWWCONVERT2(0,YTRANS,YLFN,YINHALT,1)),Y,1)
	. ;
	. IF $PIECE(YSATZ,Y,7)'="" SET YINHALT = $PIECE(YSATZ,Y,7)_YINHALT              ; Additional Text 
	. ;
	. QUIT:$PIECE(YSATZ,Y,2)=""                         ; no target field
	. SET $PIECE(YFELD,Y,$PIECE(YSATZ,Y,2)) = YINHALT
	
	DO  ;SAVE
	. NEW YFORM,YVOR
	. SET YOK = $$^WWWSPEI(YDATEI1,YKEY,YFELD,$$$YES) 
	
	IF $GET(YSHOW)=1 DO
	. WRITE !!,"<-"_YA,"=",%FELD1,"  <br>"
	. WRITE !!,"->"_"^"_YDATEI1_"(0,"_YKEY_",1)"_"="_YFELD
	
	QUIT
	
]]></Routine>
</Export>