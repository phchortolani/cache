<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPDISCOUNT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPDISCOUNT
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ORDER LINE ITEM DISCOUNT
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 11-Jul-2007	GRF		SR15558: Doco; quits
	; 02-Aug-2005	JW		SR21988: Added call to AddPercentage
	; 22.06.2004	FIS		25327
	;-------------------------------------------------------------------------------
	NEW YYFELD
	
	SET YKEY=$GET(YKEY)
	IF $PIECE(YKEY,",",1)="" QUIT
	IF $PIECE(YKEY,",",2)="" QUIT
	SET YFELD=$GET(YFELD)    QUIT:YFELD=""
	SET YYFELD=YFELD
	DO
	. NEW YFELD,AMOUNT,PERCENT,REASON
	. SET YFELD=$GET(^INAUFP(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),1))
	. QUIT:YFELD=""
	. ;
	. SET AMOUNT=""
	. SET PERCENT=""
	. SET REASON=""
	. SET SEQ=""
	. FOR  SET SEQ=$ORDER(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),SEQ)) QUIT:SEQ=""  DO
	. . SET DISCOUNT=$GET(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),SEQ,1))
	. . ;IF $PIECE(DISCOUNT,Y,3)'="" SET PERCENT=PERCENT+$PIECE(DISCOUNT,Y,3) QUIT  ;PERCENT DISCOUNT
	. . IF $PIECE(DISCOUNT,Y,3)'="" SET PERCENT = $$AddPercentage(PERCENT,$PIECE(DISCOUNT,Y,3)) QUIT          ;PERCENT DISCOUNT
	. . ; FIXME : <GRF> is it reasonable to skip BOTH the following lines if % Discount is present? (QUIT on line above)
	. . IF $PIECE(DISCOUNT,Y,4)'="" SET AMOUNT  = AMOUNT+$PIECE(DISCOUNT,Y,4)                                 ;AMOUNT MARKUP
	. . IF $PIECE(DISCOUNT,Y,2)'="" SET REASON  = $PIECE($GET(^INREASONCODE(YM,$PIECE(DISCOUNT,Y,2),1)),Y,2)  ;REASON CODE
	. ;
	. SET $PIECE(YFELD,Y,121)=""
	. SET $PIECE(YFELD,Y,128)=""
	. SET $PIECE(YFELD,Y,122)=PERCENT
	. SET $PIECE(YFELD,Y,211)=AMOUNT*-1
	. SET $PIECE(YFELD,Y,210)=REASON
	. DO EH^INBRUTTONETTO    ; Updates YFELD ByRef
	. DO
	. . NEW YFORM,YVOR,YOK
	. . SET YOK=$$^WWWSPEI("INAUFP",$PIECE(YKEY,",",1)_","_$PIECE(YKEY,",",2),YFELD,1)
	
	QUIT
 
AddPercentage(pfltAmt1,pfltAmt2)
    ;-------------------------------------------------------------------------------
    ; The addition of 2 percentages is not just their sum
    ;
    ; Returns:Float
    ;
    ; History:
    ;  2-Aug-2005	JW			SR12988: Copied from INAUFDISCOUNT
    ;-------------------------------------------------------------------------------
	quit pfltAmt1 + pfltAmt2 - (pfltAmt1*pfltAmt2/100)
	
	
ONOFF	;SWITCH ON OR OFF
	NEW PERCENT,AMOUNT,SEQ,DISCOUNT
	
	SET YKEY=$GET(YKEY)
	
	IF $PIECE(YKEY,",",1)'="" IF $PIECE(YKEY,",",2)'="" IF $DATA(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2))) DO
	. ;
	. IF YLFN=121 SET YINHALT=""  ;CUSTOMER DISCOUNT
	. IF YLFN=128 SET YINHALT=""  ;ADDITIONAL DISCOUNT
	. IF YLFN=122 DO              ;LINE ITEM DISCOUNT PERCENT
	. . NEW PERCENT
	. . SET PERCENT=""
	. . SET SEQ=""
	. . FOR  SET SEQ=$ORDER(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),SEQ)) QUIT:SEQ=""  DO
	. . . SET DISCOUNT=$GET(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),SEQ,1))
	. . . IF $PIECE(DISCOUNT,Y,3)'="" SET PERCENT=PERCENT+$PIECE(DISCOUNT,Y,3)  ;PERCENT DISCOUNT
	. . ;
	. . SET YINHALT=PERCENT
	. ;
	. IF YLFN=211 DO   ;SPECIAL MARKUP
	. . NEW AMOUNT
	. . SET AMOUNT=""
	. . SET SEQ=""
	. . FOR  SET SEQ=$ORDER(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),SEQ)) QUIT:SEQ=""  DO
	. . . SET DISCOUNT=$GET(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2),SEQ,1))
	. . . IF $PIECE(DISCOUNT,Y,3)'="" QUIT  ;PERCENT DISCOUNT PREFERRED
	. . . IF $PIECE(DISCOUNT,Y,4)'="" SET AMOUNT=AMOUNT+$PIECE(DISCOUNT,Y,4)  ;AMOUNT MARKUP
	. . ;
	. . SET YINHALT=$$^WWWZAHL(AMOUNT*-1,0,2)  ;LINE ITEM DISCOUNT PERCENT
	. ;
	. ;IF $FIND(",125,129,210,",","_YLFN_",") SET YHID=1 QUIT  ;HIDDEN
	. SET YHID=2  ;READONLY
	
	QUIT
	
LINK	;LINK TO SUBFORM IF EXIST
	IF $GET(YKEY)'="" IF $DATA(^INAUFPDISCOUNT(YM,$PIECE(YKEY,",",1),$PIECE(YKEY,",",2)))     ;QUIT
	;SET YQ=1,$PIECE(YA,Y,9)=""  ;DISABLE AND HIDE BUTTON
	QUIT
	
]]></Routine>
</Export>