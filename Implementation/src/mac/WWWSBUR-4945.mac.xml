<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSBUR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSBUR
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		RUECKSPRUNG BUTTON
	;		Return/re-entry button
	;		(Also updates ^WWWZWS - not an @nM class)
	;
	; ByRef :
	;	YNUMMER		fieldNo  or  fieldNo/Type where Type=1,2,3 (2 is default)
	;	            1 : updates FFKEY; 2 : updates FFFELD, 3: updates FMFELD
	;	YQUERY
	;	YFORM
	;	YBUTTON		
	;	SPRACHE
	;	
	; Returns :
	; 	YKEY		byRef as null
	;	YUSER		byRef
	;	YOPEN		byRef
	;	FFFELD		byRef
	;	FFKEY		byRef
	;	FMKEY		byRef
	;	FFFORM		byRef
	;
	;
	; History :
	; 09-Mar-2007	GRF		SR12505: Doco; expand commands; quits; braces
	; 29.09.1998	DT
	;-------------------------------------------------------------------------------
	NEW ART,SUCH,YSUCH1,YKEY2,YKEY3,YKEY4,YKEY5
	
	QUIT:'$DATA(YNUMMER)
	QUIT:YNUMMER=""
	
	;SET YOPEN="OLD"  ;TYBD;28,11,2003;
	SET ART=$PIECE(YNUMMER,"/",2)
	IF ART="" SET ART="2"
	SET YNUMMER=$PIECE(YNUMMER,"/",1)
	IF +YNUMMER=0 QUIT
	
	SET YKEY=$TRANSLATE($GET(%(YQUERY,"YKEY")),"|"," ")
	SET YUSER=$GET(%(YQUERY,"YUSER"))
	
	SET SUCH=""
	IF YBUTTON'="" SET SUCH=$PIECE($GET(^WWW124(0,YFORM,SPRACHE,YBUTTON,1)),Y,53)      ; Search Function OnClick
	IF SUCH=""     SET SUCH=$ORDER(^WWW123(0,YFORM,""))
	
	SET YSUCH1=""
	IF SUCH'="" SET YSUCH1=$PIECE($GET(^WWW123(0,YFORM,SUCH,1)),Y,13) ;WELCHEN KEY ÜBERNEHMEN ;Transfer Which Key 
	
	if +YSUCH1=0 {
		SET YSUCH1=1
		SET YKEY2=$PIECE(YKEY,",",2)                                      ;ZWEITER SCHLÜSSEL  ; second key 
		SET YKEY3=$PIECE(YKEY,",",3)                                      ;DRITTER SCHLÜSSEL  ; third key 
		SET YKEY4=$PIECE(YKEY,",",4)                                      ;VIERTER SCHLÜSSEL  ; fourth key
		SET YKEY5=$PIECE(YKEY,",",5)                                      ;FÜNFTER SCHLÜSSEL  ; fifth key 
		SET YKEY6=$PIECE(YKEY,",",6)                                      ;SECHSTER SCHLÜSSEL ; sixth key
	} else {
		SET YKEY2="",YKEY3="",YKEY4="",YKEY5="",YKEY6=""
	}
	
	SET YKEY=$PIECE(YKEY,",",YSUCH1)
	QUIT:YKEY=""       ;TYBD;28.11.2003
	
	SET YOPEN="OLD"    ;TYBD;28.11.2003
	SET FFFORM=YFORM   ;TYBD;28.11.2003
	
	;  FIXME : <GRF> Appears to be one parameter too few in ^WWWZWS list (note no class for this global)
	;                See routines WWWVAR and WWWFORM2 for six parameter usage.
	;                This *COULD* simply be two different uses of the Global much like ^WWWDATEN.
	
	if ART=3 {
		SET $PIECE(FMFELD,Y,YNUMMER)=YKEY
		SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FMFELD",1),Y,YNUMMER)=YKEY
	
	} elseif ART=2 {
		SET $PIECE(FFFELD,Y,YNUMMER)=YKEY
	;	SET $PIECE(YFELD,Y,YNUMMER)=YKEY
	;	SET $PIECE(^WWWDATEN(YM,+$H,YUSER,YFORM,"D",1),Y,YNUMMER)=YKEY
	;	SET $PIECE(^WWWUSE(0,YUSER,YFORM,"D",1),Y,YNUMMER)=YKEY
		SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFFELD",1),Y,YNUMMER)=YKEY
		IF YKEY2'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFFELD",1),Y,YNUMMER+1)=YKEY2 SET $PIECE(FFFELD,Y,YNUMMER+1)=YKEY2  ;2.KEY NEXT FELD ;field 
		IF YKEY3'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFFELD",1),Y,YNUMMER+2)=YKEY3 SET $PIECE(FFFELD,Y,YNUMMER+2)=YKEY3  ;3.KEY NEXT FELD ;field 
		IF YKEY4'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFFELD",1),Y,YNUMMER+3)=YKEY4 SET $PIECE(FFFELD,Y,YNUMMER+3)=YKEY4  ;4.KEY NEXT FELD ;field 
		IF YKEY5'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFFELD",1),Y,YNUMMER+4)=YKEY5 SET $PIECE(FFFELD,Y,YNUMMER+4)=YKEY5  ;5.KEY NEXT FELD ;field 
		IF YKEY6'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFFELD",1),Y,YNUMMER+5)=YKEY6 SET $PIECE(FFFELD,Y,YNUMMER+5)=YKEY6  ;6.KEY NEXT FELD ;field 
	
	} elseif ART=1 {
		SET $PIECE(FFKEY,Y,YNUMMER)=YKEY
		SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFKEY",1),",",YNUMMER)=YKEY
		IF YKEY2'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFKEY",1),",",YNUMMER+1)=YKEY2 SET $PIECE(FFKEY,",",YNUMMER+1)=YKEY2  ;2.KEY NEXT FELD ;field 
		IF YKEY3'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFKEY",1),",",YNUMMER+2)=YKEY3 SET $PIECE(FFKEY,",",YNUMMER+2)=YKEY3  ;3.KEY NEXT FELD ;field 
		IF YKEY4'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFKEY",1),",",YNUMMER+3)=YKEY4 SET $PIECE(FFKEY,",",YNUMMER+3)=YKEY4  ;4.KEY NEXT FELD ;field 
		IF YKEY5'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFKEY",1),",",YNUMMER+4)=YKEY5 SET $PIECE(FFKEY,",",YNUMMER+4)=YKEY5  ;5.KEY NEXT FELD ;field 
		IF YKEY6'="" SET $PIECE(^WWWZWS(0,+$HOROLOG,YUSER,"FFKEY",1),",",YNUMMER+5)=YKEY6 SET $PIECE(FFKEY,",",YNUMMER+5)=YKEY6  ;6.KEY NEXT FELD ;field 
	}
	
	SET YKEY=""
	QUIT
]]></Routine>
</Export>