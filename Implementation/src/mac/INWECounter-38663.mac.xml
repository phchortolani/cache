<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWECounter" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWECounter
	/*------------------------------------------------------------------------------
	;
	; This is used as there are many ways to create an INWE, and we want to keep
	; the date from INWEAUF/INWEFREI etc consistent with that in INWE.
	; 
	;-----------------------------------------------------------------------------*/
 
Increment(pidItem,pidLocn,pidStore,pdteRecvd)
	;-------------------------------------------------------------------------------
	; Increment the INWE Counter (comparable to ^WWW128 called by ^WWWNEXT)
	;
	; Params:	pidItem,pidLocn,pidStore - INWE keys: item, location, storage
	; 			pdte 		- (optional) the date to increment. Defaults to today
	;
	; Returns:	incremented date    (e.g. 61234.00005)
	;
	; History:
	; 11-Jun-2009	GRF		SR16508: explicit company 0
	; 10-Mar-2009	GRF		variables; doco
	; 04-May-2006	JW		SR14617: Added $data check - keep looping.
	; 22-Mar-2006	JW		SR14422: Created
	;-------------------------------------------------------------------------------
	new dteNew,intCounter
	
	if $get(pdteRecvd)="" {
		set pdteRecvd = +$horolog
	} else {
		set pdteRecvd = pdteRecvd\1		// integer date
	}
	set dteNew = pdteRecvd
	do {
		set intCounter = $increment(^INWECounter(0,pidItem,pidLocn,pidStore,pdteRecvd,1))
		
		if intCounter '= 1 {		// Don't increment if first entry
			set dteNew = pdteRecvd + (intCounter*$$GetIncrementFactor^WWWDATE1())
		}
	} while ($data(^INWE(0,pidItem,pidLocn,pidStore,dteNew,1)))
	
	quit dteNew
	
 
 
]]></Routine>
</Export>