<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INFIBCHARMATRIX" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INFIBCHARMATRIX	;INFIBBUCHMATRIX;FIS;FINANCE CHARACTERISTIC MATRIX;23.12.2004  ; Compiled January 28, 2005 09:45:55
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FINANCE CHARACTERISTIC MATRIX
	;		Produce warning messages if Required/Optional Characteristic
	;		values are not present
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 21-Jun-2005	GRF		SR12629 : remove YKEY from subroutine calls until reqd.
	; 10-Jun-2005	GRF		SR12629 : Don't make unnecessary ^WWWUMLAU calls;
	; 						major rewrite to correct NotUsed<Required<Optional &
	; 						missed areas + wrong piece numbers for posting
	; 						characteristics; Add YKEY and YFORM to argument list.
	; 27-Jan-2005	GRF		CHECK2 recoded
	; FIS	23.12.2004  
	;-------------------------------------------------------------------------------
#define cvNotUsed	0
#define cvRequired	1
#define cvOptional	2
	QUIT
	
FINMATRIX(YKEY="",YFELD="")
	DO FINMATRIX2($get(YFORM),YKEY,YFELD)
	QUIT
	
FINMATRIX2(YFORM="",YKEY="",YFELD="") ;GRF;27-Jan-2005
	new arrMsg
 
	quit:YFORM=""
	quit:YKEY=""
	quit:YFELD=""
	
	; "Execute After Saving Data Records" in Forms
	; do FINMATRIX^INFIBCHARMATRIX($g(YKEY),$g(YFELD))
	;	INFIBART            INFIBITEMGROUP
	;	INFIBKUNDE          INFIBCUSTGROUP
	;	INFIBLIEF           INFIBSUPGROUP
	;	INFIBLP             INLP
	;	WWW0121             WWW0121C				WWW0124
	; 
	; FIXME : not all relevant forms call this routine   (previous list do)
	;	INLIEF              INKUNDE                 INAUF
	;	
	
	if YFORM="INART"                {  do Item(YFELD,.arrMsg)
	} elseif YFORM="INFIBART"       {  do Item(YFELD,.arrMsg)
	} elseif YFORM="INFIBITEMGROUP" {  do ItemGroup(YFELD,.arrMsg)
	} elseif YFORM="INLP"           {  do StockLocn(YFELD,.arrMsg)
	} elseif YFORM="INFIBLP"        {  do StockLocn(YFELD,.arrMsg)
	} elseif YFORM="WWW0124"        {  do Department(YFELD,.arrMsg)
	} elseif YFORM="WWW0121"        {  do SiteLocn(YFELD,.arrMsg)
	} elseif YFORM="WWW0121C"       {  do SiteLocn(YFELD,.arrMsg)
	} elseif YFORM="INLIEF"         {  do Supplier(YFELD,.arrMsg)      ; D ^INFIBKRE
	} elseif YFORM="INFIBLIEF"      {  do Supplier(YFELD,.arrMsg)
	} elseif YFORM="INFIBSUPGROUP"  {  do SupplierGroup(YFELD,.arrMsg)
	} elseif YFORM="INKUNDE"        {  do Customer(YFELD,.arrMsg)      ; D ^INKUNDE,^INFIBDEB
	} elseif YFORM="INFIBKUNDE"     {  do Customer(YFELD,.arrMsg)
	} elseif YFORM="INCUSTGROUP"    {  do CustomerGroup(YFELD,.arrMsg)
	} elseif YFORM="INAUF"          {  do Order(YFELD,.arrMsg)         ; #D ^INAUF1 DO ^INDOC($G(YFORM),$G(YKEY))
	} else {
		quit                        ; from subroutine
	}
	
	; Alert Message : Field xxxxx = Required  or  Optional
	if $DATA(arrMsg($$$cvRequired)) {
		do MSG^WWW013MSG(YBED,arrMsg($$$cvRequired)_" = "_$$^WWWFELDNAME("INFIBCHARMATRIX","D",$$$cvRequired))
	} elseif $DATA(arrMsg($$$cvOptional)) {
		do MSG^WWW013MSG(YBED,arrMsg($$$cvOptional)_" = "_$$^WWWFELDNAME("INFIBCHARMATRIX","D",$$$cvOptional))
	}
	
	quit
	
Item(objItem="",arrMsg)
	;-------------------------------------------------------------------------------
	; Stock Item
	; 
	; TODO : Work up the Item Group chain
	; 
	; ByRef :
	;   arrMsg
	;   
	; History:
	;  1-Dec-2006	JW		BR014285: Item group change
	;-------------------------------------------------------------------------------
	new ItemGroup,ItemPosn,MESSG1,MESSG2,MESSG3,Msg,PostChar,YI,YI2
	
	set ItemGroup = $$$INARTItemGroup(objItem)
	if ItemGroup="" set ItemGroup = $$$INVORGDefaultItemGroup($get(^INVORG(YM,YM,1)))
	//set Delim = $$Delim()		BR014285
	
	for YI=3,2,1 {
		set ItemPosn  = $piece("382,383,384",",",YI)
		set PostChar  = $piece(objItem,Y,ItemPosn)
		if PostChar="" {
			//set PostChar = $$CheckItemGroup(ItemGroup,YI,Delim)	BR014285
			set PostChar = $$CheckItemGroup(ItemGroup,YI)
		}
		if PostChar="" {
			set MESSG1=$$CHECK2("IC"_YI  ,$$$NO)
			set MESSG2=$$CHECK2("C-IC"_YI,$$$NO)
			set MESSG3=$$CHECK2("P-IC"_YI,$$$NO)
			
			if (MESSG1=$$$cvRequired) || (MESSG2=$$$cvRequired) || (MESSG3=$$$cvRequired) {
				set Msg = $$$cvRequired
			} elseif (MESSG1=$$$cvOptional) || (MESSG2=$$$cvOptional) || (MESSG3=$$$cvOptional) {
				set Msg = $$$cvOptional
			} else {
				set Msg = $$$cvNotUsed
			}
			if Msg'=$$$cvNotUsed set arrMsg(Msg)=$$^WWWFELDNAME("INART","D",ItemPosn)
		}
	}
	quit
	
	//CheckItemGroup(pItemGroup="",pPostCharNo="",pDelim=".")	BR014285
CheckItemGroup(pItemGroup="",pPostCharNo="")
	;-------------------------------------------------------------------------------
	; Continue searching up Item Group Chain until we have a value for the
	; specified Posting Characteristic or reach top of chain.
	;
	; e.g. Item 123 => Item Group A-AA-5 => Group A-AA => Group A [stops]
	; 
	; History :
	;  1-Dec-2006	JW		BR014285: Item group change
	; 10-Jun-2005	GRF		Created
	;-------------------------------------------------------------------------------
	new objItemGroup,PostChar
	
	quit:pPostCharNo=""
	
	set PostChar=""
	for {
		quit:pItemGroup=""
		
		set objItemGroup=$get(^INFIBITEMGROUP(YM,pItemGroup,1))
		set PostChar = $piece(objItemGroup,Y,pPostCharNo)
		quit:PostChar'=""
		
		set pItemGroup = $$$INItemGroupParentGroup($get(^INItemGroup(YM,pItemGroup,1)))
		/*	BR014285
		if pItemGroup[pDelim {
			set pItemGroup = $piece(pItemGroup,pDelim,1,$length(pItemGroup,pDelim)-1)
		} else {
			set pItemGroup = ""
		} */
	}
	
	quit PostChar
	
ItemGroup(objItemGroup,arrMsg)
	;-------------------------------------------------------------------------------
	; Item Group
	; 
	; TODO : Work up the Item Group chain
	; 
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new GroupPosn,MESSG1,MESSG2,MESSG3,Msg,YI
	
	for YI=3,2,1 {
		set GroupPosn = $piece("1,2,3",",",YI)
		if $piece(objItemGroup,Y,GroupPosn)="" {
			set MESSG1=$$CHECK2("IC"_YI)
			set MESSG2=$$CHECK2("C-IC"_YI)
			set MESSG3=$$CHECK2("P-IC"_YI)
			
			if (MESSG1=$$$cvRequired) || (MESSG2=$$$cvRequired) || (MESSG3=$$$cvRequired) {
				set Msg = $$$cvRequired
			} elseif (MESSG1=$$$cvOptional) || (MESSG2=$$$cvOptional) || (MESSG3=$$$cvOptional) {
				set Msg = $$$cvOptional
			} else {
				set Msg = $$$cvNotUsed
			}
			if Msg'=0 set arrMsg(Msg)=$$^WWWFELDNAME("INFIBITEMGROUP","D",GroupPosn)
		}
	}
	
	quit
	
StockLocn(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	;
	; TODO : Work up the Location chain
	;
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,YI,YI2
	
	for YI=2,1 {
		set YI2=$piece("16,17",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("LC"_YI,$$$NO)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("INLP","D",YI2)
		}
	}
	quit
	
Department(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	;
	; TODO : Work up the Location chain
	;
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,YI,YI2
	
	for YI=2,1 {
		set YI2=$piece("5,6",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("LC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("WWW0124","D",YI2)
		}
	}
	quit
	
SiteLocn(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,YI,YI2
	
	for YI=2,1 {
		set YI2=$piece("70,71",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("LC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("WWW0121","D",YI2)
		}
	}
	quit
	
Supplier(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	;
	; TODO : Supplier Group
	; 
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,YI,YI2
	
	for YI=3,2,1 {
		set YI2=$piece("235,236,237",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("SC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("INLIEF","D",YI2)
		}
	}
	quit
	
SupplierGroup(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,Msg,YI,YI2
	
	for YI=3,2,1 {
		set YI2=$piece("1,2,3",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("CC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("INSUPGROUP","D",YI2)
		}
	}
	
	quit
	
Customer(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	;
	; TODO : Customer Group
	;
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,YI,YI2
	
	for YI=3,2,1 {
		set YI2=$piece("235,236,237",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("CC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("INKUNDE","D",YI2)
		}
	}
	quit
	
CustomerGroup(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,Msg,YI,YI2
	
	for YI=3,2,1 {
		set YI2=$piece("1,2,3",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("CC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("INCUSTGROUP","D",YI2)
		}
	}
	
	quit
	
	
Order(YFELD,arrMsg)
	;-------------------------------------------------------------------------------
	; ByRef :
	;   arrMsg
	;-------------------------------------------------------------------------------
	new MESSG1,Msg,YI,YI2
	
	for YI=2,1 {
		set YI2=$piece("330,331",",",YI)
		if $piece(YFELD,Y,YI2)="" {
			set MESSG1=$$CHECK2("OC"_YI)
			if MESSG1'=0 set arrMsg(MESSG1)=$$^WWWFELDNAME("INAUF","D",YI2)
		}
	}
	
	quit
	
	
CHECK2(CHARTYPE="",REQUIRED=$$$YES)  ;CHECK VALIDATION ;GRF;27-Jan-2005
	;-------------------------------------------------------------------------------
	; REQUIRED : 0 Never set RESPONSE to 1
	;            1   May set RESPONSE to 1
	; returns 0 : Not used
	;         1 : Required
	;         2 : Optional     ;Required overrides Optional
	;-------------------------------------------------------------------------------
	new EFFDATE,RESPONSE
	
	quit:CHARTYPE=""
	
	set RESPONSE = $$$cvNotUsed
	
	if $data(^INFIBCHARMATRIXs(YM,$$$cvOptional,CHARTYPE)) {
		if $order(^INFIBCHARMATRIXs(YM,$$$cvOptional,CHARTYPE,$HOROLOG+1),-1)'="" set RESPONSE = $$$cvOptional
	}
	
	if REQUIRED {
		if $data(^INFIBCHARMATRIXs(YM,$$$cvRequired,CHARTYPE)) {
			if $order(^INFIBCHARMATRIXs(YM,$$$cvRequired,CHARTYPE,$HOROLOG+1),-1)'="" set RESPONSE = $$$cvRequired
		}
	}
	quit RESPONSE
	
	/*
Delim()
	;-------------------------------------------------------------------------------
	; 
	; History :
	; 10-Jun-2005	GRF		Copied from INFIBBUCHCODE
	;-------------------------------------------------------------------------------
	new Class,Company,Delim,idStructure,idx,objStructCode,objStructure
	
	set Delim = "."
	set Company = $$^WWWUMLAU(YM,1)
	set Class   = "INPARA"
	
	set idStructure = ""
	set idx = ""
	for {
		set idx=$order(^WWWSTRUKTURs(0,1,Company,Class,YM,idx))
		quit:idx=""
		quit:idStructure'=""
		
		set objStructure = $get(^WWWSTRUKTUR(0,YM,idx,1))
		if $translate($piece(objStructure,Y,2),$char(34))="WARENGRUPPE,SPRACHE" {
			set idStructure   = +$piece(objStructure,Y,3)
			set objStructCode = $get(^WWW100(0,"STRUKTUR",SPRACHE,idStructure,1))
			set Delim         = $extract($piece(objStructCode,Y,1),3)            ; XX.XX.XX => "."
		}
	}
	quit Delim	
	*/
]]></Routine>
</Export>