<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWINFO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
 
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWINFO("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
WWWINFO(YA,YHEAD,LINK,NOBUTTON,HTML)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN INFO
	;
	;	SAMPLE FOR LINK
	;
	;	SET YLINK=""
	;	DO  ;LINK
	;	. NEW YKEY,YI
	;	. SET YI=YAKTION_"EP=WWWFORM&YFORM=INAUF&YKEY="_ORDER
	;	. DO VAR^WWWCGI
	;	. SET YLINK=YI
	;	DO ^WWWINFO($$^WWWTEXT(32188)_" ("_ORDER_")",0,YLINK)  ;ORDER CREATED WITH LINK
	;
	; Inputs : 
	;	YA			=text
	;	YHEAD		=1 don't display header
	;	LINK		=link (text has link) 
	;	NOBUTTON	=1 no back button / 2 back button has link
	;	HTML		=1 do not change < and >
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 03-Feb-2010	GRF		SR17160: simplify - blnLong; closing anchor without opening when NOBUTTON'=2
	; 02-Apr-2007	RPW		Refactored and added variables for pieces. 
	; 19-Dec-2006	JW		BR014262: Rewrite. Use GetType fn.
	; 08-Dec-2006	PO		SR15276: Changed name of EventBroker to JSLibraries
	; 22-Sep-2006	JW		SR15072: Added alert if in hyperevent (moved from ReturnError^COMUtilError)
	; 02-Jun-2006	JW		SR14697: Always use eventbrokeren1.js
	; 21-Jul-2005	GRF		SR13024: Doco
	; 01-Jun-2005	RobertW	SR12056: Attempt at Performance Increase
	; 04-APR-2005	TYBD	IDs FOR BUTTON ADDED FOR TESTDIRECTOR
	; 29.07.1997	DT		created
	;-------------------------------------------------------------------------------
	new blnCenter,blnForce,blnFrame,blnLong
	
	$$$LogR("",$get(YA)_"<"_$get(YHEAD)_">"_$$$InHyperEvent)
	
	SET $ZTRAP=""
	QUIT:$GET(YA)=""
	
	; TODO : WWWINFO checks for $$$InHyperEvent so don't need to check again in ReturnError^COMUtilERROR.
	;        $$$Alert includes $$DecodeError call so entry in WWWINFO will convert list to
	;        string and then check that string doesn't need further conversion.
	;        Should have clear definition of what form parameter should be in and process
	;        accordingly - possibly separate entry points.                <GRF>
	;        
	
	// SR15072 Note: This is used for OnBeforeSave - assume that this is the only time
	// 		 		 we will come in here during a hyperevent.
	if $$$InHyperEvent {
		$$$Alert(YA)
		quit
	}
	
	SET NOBUTTON = $GET(NOBUTTON)
	SET HTML     = $GET(HTML)
		
	;LOOP SICHERUNG WEGEN ERROR IN ERROR ;TYBD;19.05.2003 ;quibble within 
	; If duplicated error processing ***HALT***
	; Note : This will stop any subsequent code being processed - usually as a
	;        result of an bug where error processing is being handled both at a
	;        lower and a higher level.
	IF ($GET(YUSER)'="") && (+$GET(YTRAKT)'=0) {
		IF YTRAKT>10000                                           DO CLOSE^WWWSTART HALT  ; too many "processes" actioned by user today
		IF $GET(^WWWZWS(0,+$HOROLOG,YUSER,"X","YTRAKT",1))=YTRAKT DO CLOSE^WWWSTART HALT  ; current "process" already has had an error reported 
		SET ^WWWZWS(0,+$HOROLOG,YUSER,"X","YTRAKT",1)=YTRAKT
	}
	
	;---------------------------------------
	
	if +$GET(YHEAD)=0 {
		SET KOPF = $$^WWWTEXT(6)_"WWWINFO"	; "Information"
		DO ^WWWSTART(KOPF)
		
		if $$GetType^WWWMENU() = 7 {        ; Popup     //BR014262
			do ^WWWFORM8
		} else {
			do JSLibraries^WWWFORM8()	// SR14697 // SR15276
		}
		DO ^WWWBODY(0) ;FORMAT BODY
		IF '$DATA(Y) DO ^WWWVORG				// Is this line necessary ??
		SET YVOR = $GET(^WWW012(0,YM,1))
		DO ^WWWUP(0)                        ;ANKER FESTLEGEN 
		IF $GET(YUSER)'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,10) = ""
		DO ^WWWKOPF(KOPF)                   ;KOPFZEILE
	}	
	
	;--------------------------------------- TABLE 1
	
	WRITE "<TABLE CELLSPACING=0 BORDER=0>"
	WRITE "<TR>"
	WRITE "<TD"
	
	set blnForce  = +$$$WWW012ForcePicturesAsButtons(YVOR)
	set blnCenter =  $$$WWW012CenterFormContents(YVOR)
	set blnFramed =  $$$WWW012FormsFramed(YVOR)
	
	IF blnForce WRITE " class=""coolButton"""  ; MOUSEEFFECT
	WRITE ">"
	
	SET YQ = 0
	
	;WENN AUS FORMAULAR ;when out of 
	if (NOBUTTON'=1) && (NOBUTTON'=2)            &&
	   (+$GET(YHEAD)=0) && ($get(YFORM)'="")     &&
	   ($DATA(YKEY))                             &&
	   ((YKEY'="") || ($DATA(^WWW122(0,YFORM))))   {
		
		if blnForce {
			WRITE "<A HREF="""
		} else {
			WRITE "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(99))_""" onClick=""window.location='"
		} ; "Back"
		WRITE YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YOPEN="_$select($get(YKEY)'="":"OLD",1:"NEW") // SR14427
		DO ^WWWCGI
		IF 'blnForce WRITE "'"
		WRITE """>"
		IF blnForce WRITE "<IMG SRC="""_YGIF_"hback.gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(99)_""" border=0 id=""BUTTON_BACK""></A>"
		SET YQ=1
	}
	
	;WENN AUS FORMAULAR ;when out of 
	if (NOBUTTON=2) && ($get(LINK)'="") {
		if blnForce {
			write "<A HREF="""
		} else {
			write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(99))_""" onClick=""window.location='"
		} ; "Back"
		
		write LINK
		if 'blnForce write "'"
		write """>"
		IF blnForce WRITE "<IMG SRC="""_YGIF_"hback.gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(99)_""" border=0 id=""BUTTON_BACK""></A>"
	}
 
	;WENN KEIN FORMULAR ;when no form 
	IF (NOBUTTON'=1) && (+$GET(YHEAD)=0) && (YQ=0) && ($get(YTARGET)="") {
		;I +$P(YVOR,Y,24)'=1!(YUSER="")!(YBED="UNKNOWN") W "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWTEXT(99)_""" onClick=""window.history.back()"">" Q
		IF YUSER'="" && (YBED'="UNKNOWN") && (blnForce) WRITE "<A HREF="""_YAKTION_"EP=WWWMENU" DO ^WWWCGI WRITE """>" WRITE "<IMG SRC="""_YGIF_"end.gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(10)_""" border=0 id=""BUTTON_BACK""></A>"
	} ; "Cancel"
	
	//WRITE YCR
	WRITE "</TD></TR></TABLE>"
	
	;--------------------------------------- TABLE 2
	
	if blnCenter write "<CENTER>"
	IF blnFramed {
		DO ^WWWFRAME(0)
		WRITE "<TR><TD>"
	}
	
	WRITE "<TABLE CELLSPACING=0 BORDER=0>"
	WRITE "<TR><TD>"
	IF 'blnForce WRITE "<BR>"   ;WRITE "<HR>"
	WRITE "</TD></TR>"
	
	;-----
	
	WRITE "<TR><TD>"
	
	IF '$FIND(YA,"|") {
		set blnLong = ($length(YA)>100)                         ; SR17160
		write $select(blnLong:"<H5>",1:"<H3>")
	;	IF $LENGTH(YA)'>100 WRITE "<H3>"
	;	IF $LENGTH(YA)>100  WRITE "<H5>"
		IF $GET(YHEAD)=1                     WRITE "<FONT FACE=ARIAL>"
		IF ($GET(LINK)'="") && (NOBUTTON'=2) WRITE "<A HREF="""_LINK_""">"
		WRITE $$^WWWUML(YA,HTML) 
	;	IF $GET(LINK)'=""                    WRITE "</A>"       ; SR17160
		IF ($GET(LINK)'="") && (NOBUTTON'=2) WRITE "</A>"
		IF $GET(YHEAD)=1                     WRITE "</FONT>"
		write $select(blnLong:"</H5>",1:"</H3>")
	;	IF $LENGTH(YA)'>100 WRITE "</H3>"                       ; SR17160
	;	IF $LENGTH(YA)>100 WRITE "</H5>"
	
	} else {
		FOR YI=1:1 {
			SET YI(1)=$PIECE(YA,"|",YI)
			QUIT:YI(1)=""
			
			set blnLong = ($length(YI(1))>100)                  ; SR17160
			write $select(blnLong:"<H5>",1:"<H3>")
		;	IF $LENGTH(YI(1))'>100 WRITE "<H3>"
		;	IF $LENGTH(YI(1))>100 WRITE "<H5>"
			IF ($GET(LINK)'="") && (NOBUTTON'=2) WRITE "<A HREF="""_LINK_""">"
			WRITE $$^WWWUML(YI(1),HTML)
		;	IF $GET(LINK)'=""                    WRITE "</A>"   ; SR17160
			IF ($GET(LINK)'="") && (NOBUTTON'=2) WRITE "</A>"
			write $select(blnLong:"</H5>",1:"</H3>")
		;	IF $LENGTH(YI(1))'>100 WRITE "</H3>"                ; SR17160
		;	IF $LENGTH(YI(1))>100 WRITE "</H5>"
			IF $PIECE(YA,Y,YI+1)'="" {
				WRITE "</TD></TR>"
				WRITE "<TR><TD>"
			}
		}
	}
		
	WRITE "</TD></TR>"
	
	;-----
	
	WRITE "<TR><TD>"
	IF 'blnForce WRITE "<BR>"  ;WRITE "<HR>"
	WRITE "</TD</TR>"
	WRITE "</TABLE>"
	
	WRITE "</FONT>"
	
	IF blnFramed {
		WRITE "</TD></TR>"
		DO ^WWWFRAME(1)
	}
	
	IF blnCenter WRITE "</CENTER>"
	
	;---------------------------------------
	
	IF +$GET(YHEAD)=0 {
		DO ^WWWUP(1)  ;ANKER NACH OBEN ;armature within upstairs 
		DO ^WWWSTOP   ;ENDE ;termination 
	}
	
	quit
	
]]></Routine>
</Export>