<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPInvLineGet" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPInvLineGet
	
#include COMSYS
#include FINConst
#include INConst
	
GetOverride(fltVariance)
    ;-------------------------------------------------------------------------------
    ; Returns:
    ;
    ; History:
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ;-------------------------------------------------------------------------------
	quit ($$$FINGeneralSetupOverrideVariancePosting($get(^FINGeneralSetup(YM,YM,1))))||($$$FCZero(fltVariance))
	
	
GetOrderDetails(pYFELD,pidOrder,pidLine)
    ;-------------------------------------------------------------------------------
    ; Grab the order data from the FINAPInvLine and split into order# and line#.
    ; 
    ; Returns: order# and line# as paramaters
    ;
    ; History:
    ; 27-Jul-2009	GRF		Prefixes
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 10-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new strOrder
	
	set strOrder = $$$FINAPInvLineOrderNumber($get(pYFELD))
	set pidOrder = $$$KEY1(strOrder)
	set pidLine  = $$$KEY2(strOrder)
	
	quit
 
GetReceivedQuantityDisplay(YKEY,YFELD)
 	;-------------------------------------------------------------------------------
    ; History:
	; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
	;-------------------------------------------------------------------------------
	quit "Shobby"
 
GetAdditionalCharges(pYKEY="",pYFELD="")
    ;-------------------------------------------------------------------------------
	; Loop through all the charges coming up with a total.
	; This will also need to include FC capable code
    ;
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ;-------------------------------------------------------------------------------
	quit $$GetAdditionalCharges^FINAPInvCharge2($get(pYKEY),$get(pYFELD))
 
GetAdditionalChargesVP(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Get the variance posting for additional charges
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Jul-2006	GRF		Removed unnecessary set
    ; 28-Oct-2005	PO		SR13706: Only set strVarPost to "" if fltAdditional is 0
    ; 17-Oct-2005	PO		SR13706: Only default where to post variance if selected.
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 07-Apr-2005	PO		SR11349 Use FC macros
    ; 19-Jan-2005	RobertW	Created (SR10061)
    ;-------------------------------------------------------------------------------
	new fltAdditional,objGenSetup,strVarPost
	
	set strVarPost    = $$$FINAPInvLineVarianceOTHDPost(pYFELD)
	set fltAdditional = $$GetAdditionalCharges^FINAPInvCharge2(pYKEY,pYFELD)
	
	if ($$GetOverride(fltAdditional) || (strVarPost="")) {
		set objGenSetup = $get(^FINGeneralSetup(YM,YM,1))
	
		if $$$FCLTZero(fltAdditional) && ($$$FINGeneralSetupOtherAmountLT0(objGenSetup)'="") {
			set strVarPost = $$$FINGeneralSetupOtherAmountLT0(objGenSetup)
		
		} elseif $$$FCGTZero(fltAdditional)&&($$$FINGeneralSetupOtherAmountGT0(objGenSetup)'="") {
			set strVarPost = $$$FINGeneralSetupOtherAmountGT0(objGenSetup)
		
		} elseif $$$FCZero(fltAdditional) {
			set strVarPost = ""
		}
	}
	
	quit strVarPost
	
	
GetRECQ(pYFELD)
	;-------------------------------------------------------------------------------
	; History:
	; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
	; 07-Apr-2005	RPW		SR11836: Modified OpenQuantity to UnmatchedQuantity,
	; 							removed duplicate line
	;-------------------------------------------------------------------------------
	quit $$$FINAPInvLineUnmatchedQuantity(pYFELD)-$$$FINAPInvLineOrderQuantity(pYFELD)
	
	
GetINVQ(pYFELD)
    ;-------------------------------------------------------------------------------
    ; Returns:
    ;
    ; History:
    ; 23-Nov-2005	PO		SR13895: Use invoice exchange rate.
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
	; 07-Apr-2005	RobertW	SR11836: Modified OpenQuantity to UnmatchedQuantity,
	; 							removed duplicate line
    ; 15-Mar-2005	PO		Created
    ;-------------------------------------------------------------------------------
	new curAmount
	
	;quit $$$FCMult($$$FINAPInvLineOrderEach(pYFELD),$$$FCSub($$$FINAPInvLineInvoiceQuantity(pYFELD),$$$FINAPInvLineUnmatchedQuantity(pYFELD)))
	set curAmount = $$$FCMult($$$FINAPInvLineOrderEach(pYFELD),($$$FINAPInvLineInvoiceQuantity(pYFELD)-$$$FINAPInvLineUnmatchedQuantity(pYFELD)))
	quit $$$FCUpdate(curAmount,$$$FCRate($$$FINAPInvLineInvoiceEach(pYFELD)),1) ; SR13895
	
	
GetVARFC(pYFELD)
    ;-------------------------------------------------------------------------------
    ; Calculate Foreign Currency variation
    ;
    ; Returns: Foreign Currency variance from Receive Date to Invoice Date
    ;
    ; History:
    ; 23-Nov-2005	PO		SR13895: Base FC variance on invoice quantity and
    ; 							unmatched if closed.
    ; 24-Oct-2005	PO		SR13702: Do not include change in quantity or UP in FC
    ; 							variance calculation
    ; 15-Jul-2005	RPW		SR12981: Newed variables
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 20-May-2005	PO/PK	SR11256: Modified to take $ Variance into account
	; 18-Apr-2005	PO		SR11349: Sourced values from right places
    ; 08-Apr-2005	RPW		SR11836: Change FCMake to FCJoin
    ; 15-Mar-2005	PO		SR11349: Created
    ;-------------------------------------------------------------------------------
	new fltRecConvRate,fltInvConvRate,curRecUP,curOrderEach,curAmount
	
	set curOrderEach   =  $$$FINAPInvLineOrderEach(pYFELD)
	set curRecUP       = +$$$FCAmount(curOrderEach)
	set fltRecConvRate =  $$$FCRate(curOrderEach)
	set fltInvConvRate =  $$$FCRate($$$FINAPInvLineInvoiceEach(pYFELD))
	
	if $$$FINAPInvLineClosed(pYFELD) { ; SR13895
		set curAmount = $justify((curRecUP*(fltInvConvRate-fltRecConvRate))*$$$FINAPInvLineUnmatchedQuantity(pYFELD),0,2) ; $$$FINAPInvLineOrderQuantity(pYFELD),0,2)  ; SR13895
	} else {
		set curAmount = $justify((curRecUP*(fltInvConvRate-fltRecConvRate))*$$$FINAPInvLineInvoiceQuantity(pYFELD),0,2) ; $$$FINAPInvLineOrderQuantity(pYFELD),0,2)  ; SR13895
	}
	
	quit curAmount
	
	/*
	new fltCurConvRate,fltInvConvRate,curInvEach,curInvUnitPrice,curInvTotal,curInvTotalPrice
	
	if $$$ALWAYS {
		set curInvEach      =  $$$FINAPInvLineInvoiceEach(pYFELD)
		set curInvUnitPrice = +$$$FCAmount(curInvEach)
		set fltCurConvRate  =  $$$FCRate(curInvEach)
		set fltInvConvRate  =  $$$FCRate($$$FINAPInvLineOrderEach(pYFELD))
		
		quit (((curInvUnitPrice*fltCurConvRate)-(curInvUnitPrice*fltInvConvRate))*$$$FINAPInvLineInvoiceQuantity(pYFELD))
	
	} else {
		;  7-Jul-2005	JW			SR12811: Use totals
		set curInvTotal      =  $$$FINAPInvLineInvoiceTotal(pYFELD)
		set curInvTotalPrice = +$$$FCAmount(curInvTotal)
		set fltInvConvRate   =  $$$FCRate($$$FINAPInvLineOrderEach(pYFELD))
		
		quit ((curInvTotal-$justify(curInvTotalPrice*fltInvConvRate,0,2)))
	}
	*/
	
	
GetVARFCPost(pYFELD)
	;-------------------------------------------------------------------------------
	; Get the variance posting for Foreign Currency Amount.
	;
	; Returns:
	;
	; History:
	; 28-Oct-2005	PO		SR13706: Only set strVarPost to "" if curInvFC is 0
	; 17-Oct-2005	PO		SR13706: Only default where to post variance if selected.
	; 24-Jun-2005	PO		SR12705: Created
	;-------------------------------------------------------------------------------
	new curInvFC,strVarPost,objGenSetup
 
	set strVarPost = $$$FINAPInvLineVarianceFCDPost(pYFELD)
	set curInvFC   = $$GetVARFC(pYFELD)
 
	if ($$GetOverride(curInvFC) || (strVarPost="")) {
		set objGenSetup = $get(^FINGeneralSetup(YM,YM,1))
 
		if $$$FCLTZero(curInvFC)&&($$$FINGeneralSetupFCVarianceLT0(objGenSetup)'="") {
			set strVarPost = $$$FINGeneralSetupFCVarianceLT0(objGenSetup)
		
		} elseif $$$FCGTZero(curInvFC) && ($$$FINGeneralSetupFCVarianceGT0(objGenSetup)'="") {
			set strVarPost = $$$FINGeneralSetupFCVarianceGT0(objGenSetup)
		
		} elseif $$$FCZero(curInvFC) {
			set strVarPost = ""
		}
	}
 
	quit strVarPost
	
	
GetINVQVP(pYFELD)
	;-------------------------------------------------------------------------------
    ; Get the variance posting for Inventory Quantity.
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Oct-2005	PO		SR13706: Only set strVarPost to "" if curInvFC is 0
    ; 17-Oct-2005	PO		SR13706: Only default where to post variance if selected.
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 07-Apr-2005	PO		SR11349 Use FC macros
    ; 19-Jan-2005	RPW		Created (SR10061)
    ;-------------------------------------------------------------------------------
	new fltInvQ,strVarPost,objGenSetup,blnOverride
	
	set strVarPost=$$$FINAPInvLineVarianceINVQPost(pYFELD)
	set fltInvQ=$$GetINVQ(pYFELD)
	
	if ($$GetOverride(fltInvQ)||(strVarPost="")) {
		
		set objGenSetup=$get(^FINGeneralSetup(YM,YM,1))
		
		if $$$FCLTZero(fltInvQ)&&($$$FINGeneralSetupInventoryQuantityLT0(objGenSetup)'="") { ;SR13706
			set strVarPost=$$$FINGeneralSetupInventoryQuantityLT0(objGenSetup)
		} elseif $$$FCGTZero(fltInvQ)&&($$$FINGeneralSetupInventoryQuantityGT0(objGenSetup)'="") {
			set strVarPost=$$$FINGeneralSetupInventoryQuantityGT0(objGenSetup)
		} elseif $$$FCZero(fltInvQ) {
			set strVarPost=""
		}
	}
	
	;if strVarPost="" set strVarPost="&nbsp;"
	
	quit strVarPost
 
GetINVPrice(pYFELD)
    ;-------------------------------------------------------------------------------
    ; Returns the difference in price between the invoice total and ordered total
    ;
    ; Returns:Currency
    ;
    ; History:
    ; 23-Nov-2005	PO		SR13895: Use invoice exchange rate.
    ; 21-Oct-2005	PO		Round the calculated order total.
    ; 18-Aug-2005	RPW		SR11983: Fixed nested $$$FC issue.
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 02-Jun-2005	shobby	Code Check
    ; 09-May-2005	PO		SR12256 If no FC amount clear base amount, (if base
    ; 							amount that is a FC variance, not applicable here)
    ; 22-Mar-2005	Paul K	Modified to use invoice line total field rather than
    ; 							line each field.
    ; 16-Mar-2005	PO		SR11349 FC support
    ;-------------------------------------------------------------------------------
	new curAmount,curInvTtl,curOrdEach,fltInvConvRate,fltQty
	
	set curInvTtl=$$$FINAPInvLineInvoiceTotal(pYFELD)
	set curOrdEach=$$$FINAPInvLineOrderEach(pYFELD)
	;set fltOrdEachConvRate=$$$FCRate(curOrdEach)  ; SR13895
	set fltInvConvRate=$$$FCRate(curInvTtl) ; SR13895
	set fltQty    = $$$FINAPInvLineInvoiceQuantity(pYFELD)
	set curAmount = $$$FCMult(curOrdEach,fltQty)
	set curAmount = $$$FCRound(curAmount)
	set curAmount = $$$FCSub(curInvTtl,curAmount)
 
	if $$$FCZero(curAmount) {
		set curAmount=$$$FCUpdate(curAmount,"",1)
	} else {
		set curAmount=$$$FCUpdate(curAmount,fltInvConvRate,1) ; SR13895
	}
	quit curAmount   ;$$$FCSign(curAmount)
 
GetINVPriceVP(pYFELD)
	;-------------------------------------------------------------------------------
    ; Get the variance posting for Inventory Dollar Amount.
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Oct-2005	PO		SR13706: Only set strVarPost to "" if curInvFC is 0
    ; 17-Oct-2005	PO		SR13706: Only default where to post variance if selected.
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 07-Apr-2005	PO		SR11349 Use FC macros
    ; 19-Jan-2005	RobertW	Created (SR10061)
    ;-------------------------------------------------------------------------------
	new curInvPrice,strVarPost,objGenSetup
	
	set strVarPost=$$$FINAPInvLineVarianceINVDPost(pYFELD)
	set curInvPrice=$$GetINVPrice(pYFELD)
	
	if ($$GetOverride(curInvPrice)||(strVarPost="")) {
	
		set objGenSetup=$get(^FINGeneralSetup(YM,YM,1))
		
		if $$$FCLTZero(curInvPrice)&&($$$FINGeneralSetupInventoryAmountLT0(objGenSetup)='"") { ;SR13706
			set strVarPost=$$$FINGeneralSetupInventoryAmountLT0(objGenSetup)
		} elseif $$$FCGTZero(curInvPrice)&&($$$FINGeneralSetupInventoryAmountGT0(objGenSetup)'="") {
			set strVarPost=$$$FINGeneralSetupInventoryAmountGT0(objGenSetup)
		} elseif $$$FCZero(curInvPrice) {
			set strVarPost=""
		}
	}
	;if strVarPost="" set strVarPost="&nbsp;"
	
	quit strVarPost
	
GetTotalVariance(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Get the variance posting for the total variance
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Oct-2005	PO		SR13706: Only set strVarPost to "" if curInvFC is 0
    ; 17-Oct-2005	PO		SR13706: Only default where to post variance if selected.
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 14-Mar-2005	PO		SR11349 Use FC macros
    ; 19-Jan-2005	RobertW	Created (SR10061)
    ;-------------------------------------------------------------------------------
	new fltInvQ,fltInvPrice,fltAdditional,strVarPost,fltTotal
	new objGenSetup
	
	set strVarPost=$$$FINAPInvLineVariancePosting(pYFELD)
	
	set fltInvQ       = $$GetINVQ(pYFELD)
	set fltInvPrice   = $$GetINVPrice(pYFELD)
	set fltAdditional = $$GetAdditionalCharges^FINAPInvCharge2(pYKEY,pYFELD) ; Needs to be implemented so amount returned is in either FC or base
	
	if ($$$FCur(fltInvQ)'="") && ($$$FCur(fltInvPrice)'="") {
		set fltTotal = $$$FCAdd(fltInvQ,fltInvPrice)
	;	set fltTotal = $$$FCAdd($$$FCAdd(fltInvQ,fltInvPrice),fltAdditional)
	
	} else {
		set fltTotal = fltInvQ+fltInvPrice+fltAdditional
	}
	
	if ($$GetOverride(fltTotal) || (strVarPost="")) {
		set objGenSetup = $get(^FINGeneralSetup(YM,YM,1))
 		
		if $$$FCLTZero(fltTotal) && ($$$FINGeneralSetupTotalVarianceLT0(objGenSetup)'="") { ;SR13706
			set strVarPost = $$$FINGeneralSetupTotalVarianceLT0(objGenSetup)
		
		} elseif $$$FCGTZero(fltTotal)&&($$$FINGeneralSetupTotalVarianceGT0(objGenSetup)'="") {
			set strVarPost = $$$FINGeneralSetupTotalVarianceGT0(objGenSetup)
		
		} elseif $$$FCZero(fltTotal) {
			set strVarPost = ""
		}
	}
	
	;if strVarPost="" set strVarPost="&nbsp;"
	quit strVarPost
	
GetCalcTotal(pYFELD)
    ;-------------------------------------------------------------------------------
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ;-------------------------------------------------------------------------------
	;quit ($$$FINAPInvLineInvoiceQuantity(pYFELD)*$$$FINAPInvLineInvoiceEach(pYFELD))
	quit $$$FINAPInvLineInvoiceTotalIncludingTax(pYFELD)
 
GetQuantityUnit(pYFELD)
    ;-------------------------------------------------------------------------------
    ; Get the quantity unit for the receive line.
    ; Get either the order unit, or the item inventory unit.
    ;
    ; Returns: quantity unit code
    ;
    ; History:
    ; 02-Jun-2005	shobby	Moved from FINAPInvLine.  CodeCheck.
    ; 22-Feb-2005	JW		Changed null item condition
    ; 07-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
	new item,objItem,unit
	
	set unit = $$$FINAPInvLineOrderUnit(pYFELD) ; order unit
	
	if unit="" {
		set item = $$$FINAPInvLineItem(pYFELD)
		;quit:item=""
		if item'="" {
			set objItem = $get(^INART(YM,item,1))
			set unit = $$$INARTUnitofMeasure(objItem) ; inventory unit
		}
	}
	
	quit unit
	
]]></Routine>
</Export>