<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHCResultShow" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHCResultShow ; Health Check Result Show
	
#include COMConst
	
AfterDataFields()
    ;-------------------------------------------------------------------------------
    ; Show Grid After Data Fields
    ; Called from COMHCResultShow
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Apr-2004	LW	  Changed to use multiple forms - allowing back functionality
    ; 12-Feb-2004   SCR   Created
    ;-------------------------------------------------------------------------------
	new Para,Type
	
	set Para=$get(YPARA)
	set Type=$piece(Para,":",1)
	set Para=$piece(Para,":",2,999)
	
	if YFORM="COMHCResultShow"{             do ShowDates()
	} elseif YFORM="COMHCResultShowTFDS" {  do ShowTypesForDateSite(Para)
	} elseif YFORM="COMHCResultShowSFD" {   do ShowSitesForDate(Para)
	} elseif YFORM="COMHCResultShowR" {     do ShowResults(Para) } 
	quit
	
ShowDates()
    ;-------------------------------------------------------------------------------
    ; Show Health Checks count for all dates
    ; 
    ; History:
    ; 01-Dec-2005	RPW		SR13916: Speed improvement, no requirement for indirection
    ; 21-Apr-2004	LW	  	Changed to use multiple forms - allowing back functionality
    ; 11-Feb-2004   SCR		Created
    ;-------------------------------------------------------------------------------
 	new Header,Date,Rec,Entry,Row,Error,Temp,Status,Form,Key,Para,idJob
 	
 	set idJob=$job
 	
 	kill ^CacheTempHC(idJob)
	set Entry=""
	for {
		set Entry=$order(^COMHCResult(YM,Entry))
		quit:Entry=""
		
		set Rec=$get(^COMHCResult(YM,Entry,1))
		set Date=+$$$COMHCResultDate2(Rec)
		set Status=$$$COMHCResultStatus(Rec)
		set Row=$get(^CacheTempHC(idJob,Date))
		set $piece(Row,Y,1)=Date
		set $piece(Row,Y,2)=$piece(Row,Y,2)+1		; Count
		set Error=Status=0
		set $piece(Row,Y,3)=$piece(Row,Y,3)+Error	; Error Count	
		set ^CacheTempHC(idJob,Date)=Row
	}
	
 	
 	set Header=""
 	set Header=Header_$listbuild($$$StrCOMHCResultDate2)
 	set Header=Header_$listbuild($listbuild("Com00090"))  ; Count
 	set Header=Header_$listbuild($listbuild("Com00091"))  ; Errors
 	if $$Start^COMTable(Header,$listbuild("Com00092")  ) { ; Health Checks
 		set Date=""
  		for {
   			set Date=$order(^CacheTempHC(idJob,Date),-1)
   			quit:Date=""
   			
   			set Rec=$get(^CacheTempHC(idJob,Date))
   			set Form="COMHCResultShowSFD"
   			set Para="SitesForDate:"_Date
   			set Key="Date"
   			do NewLine^COMTable($select($piece(Rec,Y,3):"lightcoral",1:""))
   			do InsertCellDate^COMTable($piece(Rec,Y,1),Form,Key,Para)
   			do InsertCell^COMTable($piece(Rec,Y,2),Form,Key,Para)
   			do InsertCell^COMTable($piece(Rec,Y,3),Form,Key,Para)
   			do EndLine^COMTable() 
  		}
  		do Stop^COMTable()
 	}
	kill ^CacheTempHC(idJob)
 	quit
 
ShowTypesForDateSite(Spec="")
    ;-------------------------------------------------------------------------------
    ; Show Health Checks count for all Types for a date
    ; 
    ; History:
    ; 01-Dec-2005	RPW		SR13916: Speed improvement, no requirement for indirection
    ; 21-Apr-2004	LW	  Changed to use multiple forms - allowing back functionality
    ; 12-Feb-2004   SCR			Created
    ;-------------------------------------------------------------------------------
 	new Header,Type,Rec,Entry,Row,Error,Temp,Status,Form,Key,Para,Date,Site,idJob
 	
 	set idJob=$job
 	set Date=$piece(Spec,":",1)
 	set Site=$piece(Spec,":",3)
 	kill ^CacheTempHC(idJob)
 	
	set Entry=""
	for {
		set Entry=$order(^COMHCResults(YM,3,+Date,Entry))
		quit:Entry=""
		
		set Rec=$get(^COMHCResult(YM,Entry,1))
		if Site=$$$COMHCResultSite1(Rec) {
			set Type=$$$COMHCResultType1(Rec)
			if Type="" set Type="?"
			set Status=$$$COMHCResultStatus(Rec)
			set Row=$get(^CacheTempHC(idJob,"List","Type"))
			set $piece(Row,Y,1)=Type
			set $piece(Row,Y,2)=$piece(Row,Y,2)+1		; Count
			set Error=Status=0
			set $piece(Row,Y,3)=$piece(Row,Y,3)+Error	; Error Count	
			set ^CacheTempHC(idJob,"List",Type)=Row
		}
	}
	
	set Type=""
	for {
		set Type=$order(^CacheTempHC(idJob,"List",Type))
		quit:Type=""
		
		set Row=$get(^CacheTempHC(idJob,"List",Type))
		set Error=+$piece(Row,Y,3)
		set ^CacheTempHC(idJob,"Error",Error,Type)=Row
	}
 	
 	set Header=""
 	set Header=Header_$listbuild($$$StrCOMHCResultType1)
 	set Header=Header_$listbuild($listbuild("Com00090"))  ; Count
 	set Header=Header_$listbuild($listbuild("Com00091"))  ; Errors
 	if $$Start^COMTable(Header,$listbuild("Com00095",$$^WWWDATE(Date),Site)  ) { ; Health Checks
 		set Error=""
 		for {
	 		set Error=$order(^CacheTempHC(idJob,"Error",Error),-1)
	 		quit:Error=""
	 		
	 		set Type=""
	  		for {
	   			set Type=$order(^CacheTempHC(idJob,"Error",Error,Type))
	   			quit:Type=""
	   			
	   			set Rec=$get(^CacheTempHC(idJob,"Error",Error,Type))
	   			set Form="COMHCResultShowR"
	   			set Para="Results:"_Date_":"_Type_":"_Site
	   			set $piece(Para,":",8)="MainSite,"_Site
	   			set Key="Type"
	   			do NewLine^COMTable($select($piece(Rec,Y,3):"lightcoral",1:""))
	   			do InsertCell^COMTable($piece(Rec,Y,1),Form,Key,Para)
	   			do InsertCell^COMTable($piece(Rec,Y,2),Form,Key,Para)
	   			do InsertCell^COMTable($piece(Rec,Y,3),Form,Key,Para)
	   			do EndLine^COMTable() 
	  		}
 		}
  		do Stop^COMTable()
 	}
	kill ^COMTempHC(idJob)
	quit
	
	
ShowSitesForDate(Spec="")
    ;-------------------------------------------------------------------------------
    ; Show Health Checks count for all Types for a date
    ; 
    ; History:
    ; 01-Dec-2005	RPW		SR13916: Speed improvement, no requirement for indirection
    ; 21-Apr-2004	LW	  Changed to use multiple forms - allowing back functionality
    ; 12-Feb-2004   SCR			Created
    ;-------------------------------------------------------------------------------
 	new Header,Site,Rec,Entry,Row,Error,Temp,Status,Form,Key,Para,Count,Date,idJob
 	
 	
 	set idJob = $job
 	set Date  = $piece(Spec,":",1)
 	kill ^CacheTempHC(idJob)
 	
	set Entry=""
	set Count=0
	for {
		set Entry=$order(^COMHCResults(YM,3,+Date,Entry))
		quit:Entry=""
		
		set Rec=$get(^COMHCResult(YM,Entry,1))
		set Site=$$$COMHCResultSite1(Rec)
		if Site="" set Site="?"
		set Status=$$$COMHCResultStatus(Rec)
		set Row=$get(^CacheTempHC(idJob,"List",Site))
		if Row="" set Count=Count+1
		set $piece(Row,Y,1)=Site
		set $piece(Row,Y,2)=$piece(Row,Y,2)+1		; Count
		set Error=Status=0
		set $piece(Row,Y,3)=$piece(Row,Y,3)+Error	; Error Count	
		set ^CacheTempHC(idJob,"List",Site)=Row
	}
	
	set Site=""
	for {
		set Site=$order(^CacheTempHC(idJob,"List",Site))
		quit:Site=""
		
		set Row=$get(^CacheTempHC(idJob,"List",Site))
		set Error=+$piece(Row,Y,3)
		set ^CacheTempHC(idJob,"Error",Error,Site)=Row
	}
 	
 	set Header=""
 	set Header=Header_$listbuild($$$StrCOMHCResultSite1)
 	set Header=Header_$listbuild($listbuild("Com00090"))  ; Count
 	set Header=Header_$listbuild($listbuild("Com00091"))  ; Errors
 	
 	if $$Start^COMTable(Header,$listbuild("Com00093",$$^WWWDATE(Date))  ) { ; Health Checks
 		set Error=""
 		for {
	 		set Error=$order(^CacheTempHC(idJob,"Error",Error),-1)
	 		quit:Error=""
	 		
	 		set Site=""
	  		for {
	   			set Site=$order(^CacheTempHC(idJob,"Error",Error,Site))
	   			quit:Site=""
	   			
	   			set Rec=$get(^CacheTempHC(idJob,"Error",Error,Site))
	   			set Form="COMHCResultShowTFDS"
	   			set Para="TypesForDateSite:"_Date_"::"_Site
	   			set Key="Site"
	   			do NewLine^COMTable($select($piece(Rec,Y,3):"lightcoral",1:""))
	   			do InsertCell^COMTable($piece(Rec,Y,1),Form,Key,Para)
	   			do InsertCell^COMTable($piece(Rec,Y,2),Form,Key,Para)
	   			do InsertCell^COMTable($piece(Rec,Y,3),Form,Key,Para)
	   			do EndLine^COMTable() 
	  		}
 		}
  		do Stop^COMTable()
 	}
	kill ^CacheTempHC(idJob)
	
 
 	quit
 
ShowResults(Spec="")
    ;-------------------------------------------------------------------------------
    ; Show Health Check Results 
    ; 
    ; History:
    ; 01-Dec-2005	RPW		SR13916: Speed improvement, no requirement for indirection
    ; 03-Jun-2005	shobby	Changed the Date sort to use the complete field. SR10782
    ; 20-Jun-2004	shobby	Reverse sort order for Dates.
    ; 21-Apr-2004	LW	  	Changed to use multiple forms - allowing back functionality
    ; 12-Feb-2004   SCR		Created
    ;-------------------------------------------------------------------------------
 	new Header,Rec,Entry,Row,Error,Temp,Status,Form,Key
 	new Date,Type,Location,Start,End,Order,Site
 	new Ok,Sort,ThisSite,MainSite,Para,StatusDesc
 	new idJob
 	
 	set idJob=$job
 	
 	set Date=		$piece(Spec,":",1)
 	set Type=		$piece(Spec,":",2)
 	set Site=		$piece(Spec,":",3)
 	set Location=	$piece(Spec,":",4)
 	set Start=		$piece(Spec,":",5)
 	set End=		$piece(Spec,":",6)
 	set Order=		$piece(Spec,":",7)
  	
 	kill ^CacheTempHC(idJob)
	set Entry=""
	for {
		set Entry=$order(^COMHCResult(YM,Entry))
		quit:Entry=""
		
		set Rec=$get(^COMHCResult(YM,Entry,1))
		set Ok=1
		set ThisSite=$$$COMHCResultSite1(Rec)
		if Date'=$$$COMHCResultDate2(Rec) 				Set Ok=0
		if Type'=$$$COMHCResultType1(Rec) 				Set Ok=0
		if (Site'="")&&(Site'=ThisSite) 				Set Ok=0
		if Ok=1 {
			set Sort=""
			;if Order="Date" set Sort=$$$COMHCResultDate2(Rec)
			if Order="Date" set Sort=$$$COMHCResultComplete(Rec)
			if Order="Type" set Sort=$$$COMHCResultType1(Rec)
			if $piece(Order,",",1)="MainSite" {
				set MainSite=$piece(Order,",",2)
				if MainSite=ThisSite&0 {
					set Sort="A"
				} elseif ($$$COMHCResultSource(Rec)) {
					set Sort="B"
				} elseif ($$$COMHCResultStatus(Rec)=0) {
					set Sort="C"_ThisSite
				} else {
					set Sort="D"_ThisSite
				}
			}
			if Sort="" set Sort=" "
			set ^CacheTempHC(idJob,Sort,Entry)=""
		}
	}
 	
 	set Header=""
 	set Header=Header_$listbuild($$$StrCOMHCResultEntry) 		; Entry
 	set Header=Header_$listbuild($$$StrCOMHCResultDate2) 		; Date 
 	set Header=Header_$listbuild($$$StrCOMHCResultSite1) 		; Site
 	set Header=Header_$listbuild($$$StrCOMHCResultType1) 		; Type
 	set Header=Header_$listbuild($$$StrCOMHCResultMethod1) 	    ; Method
 	set Header=Header_$listbuild($$$StrCOMHCResultComplete) 	; Complete
 	set Header=Header_$listbuild($$$StrCOMHCResultTotal)		; Total
 	set Header=Header_$listbuild($$$StrCOMHCResultStatus)		; Status
 	
 	if $$Start^COMTable(Header,$listbuild("Com00093",$$^WWWDATE(Date))  ) { ; Health Checks
 		set Sort=""
  		for {
	  		set Sort=$order(^CacheTempHC(idJob,Sort))
	  		quit:Sort=""
	  		
	  		set Entry=""
	  		for {
		  		set Entry=$order(^CacheTempHC(idJob,Sort,Entry),$select(Order="Date":1,1:-1))
		  		quit:Entry=""
		  		
	   			set Rec=$get(^COMHCResult(YM,Entry,1))
   				set Form="COMHCResult"
   				set Para="Results:"_Spec
   				set Key=Entry
   				set Status=$$$COMHCResultStatus(Rec)
   				set StatusDesc=$$GetEnumDescription^COMUtils("COMHCRESULTSTATUS",Status)
   				do NewLine^COMTable($select(Status=0:"lightcoral",1:""))
   				do InsertCell^COMTable(Entry,Form,Key,Para,"Right")
   				do InsertCellDate^COMTable($$$COMHCResultDate2(Rec),Form,Key,Para)
   				do InsertCell^COMTable($$$COMHCResultSite1(Rec),Form,Key,Para)
   				do InsertCell^COMTable($$$COMHCResultType1(Rec),Form,Key,Para)
   				do InsertCell^COMTable($$$COMHCResultMethod1(Rec),Form,Key,Para)
   				do InsertCellDate^COMTable($$$COMHCResultComplete(Rec),Form,Key,Para)
   				do InsertCell^COMTable($$$COMHCResultTotal(Rec),Form,Key,Para,"Right")
   				do InsertCell^COMTable(StatusDesc,Form,Key,Para)
   				do EndLine^COMTable() 
	  		}
  		}
  		do Stop^COMTable()
 	}
	kill ^CacheTempHC(idJob)
	
 	quit
 	
]]></Routine>
</Export>