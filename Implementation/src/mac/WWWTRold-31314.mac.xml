<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWTRold" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWTRold(YIO,YTYP,YINHALT,YNK)
	
#include WWWConst
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FORMAT
	;  S YINHALT=$$^WWWTR(0,8,"1000")       ;ANZEIGE   = 1.000,00 AUS 1000
	;  S YINHALT=$$^WWWTR(1,8,"1.000,00")   ;SPEICHERN = 1000     AUS 1.000,00
	;  S YINHALT=$$^WWWTR(0,1,$H)           ;ANZEIGE TT.MM.JJJJ
	;  S YINHALT=$$^WWWTR(1,1,"01.01.2000") ;SPEICHERT $H FORMAT
	;
	; Inputs : 
	;  YIO				Direction of transformation
	;  					= 0 FORMAT NACH AUSSEN      ; Internal to Literal  format	- Display
	;  					= 1 FORMAT ZUM SPEICHERN    ; Literal  to Internal format 	- Store
	;  
	;  YTYP				FORMATVORLAGE AUS WWW100(0,"FELDTYP",SPRACHE,YTYP,1)
	;     					(FORMAT NACH WWW101(0,"FELDFORMAT",SPRACHE,YTYP,1) or WWW100
	;  YINHALT			ZU FORMATIERENDER INHALT ;within purport 
	;  YNK				Number of Decimal Places (applied to 12-Floating and 18-Exchange Rate only)
	;
	; ByRef :
	;	YDECIMAL		Decimal point as comma or period
	;	YDECIMALLEN		Number of decimal places (applied to 8-Currency only)
	;
	; Returns :
	;
	;
	; History :
	; 13-Jun-2007	GRF		SR15525: replace macro name
	; 11-Apr-2007	GRF		SRBR014310: Doco; braces
	; 11-Dec-2005	RPW		SR14144: The system was not allowing a zero exchange rate, we do now.
	; 22-Dec-2005	JW		SR13195: Edited memo and checkbox types for input.
    ; 28-Oct-2005	JW		SR13074: Exchange Rate type. Invert depending on system setup.
	; 27-Oct-2005	SS/JW	SR13745: Added YNK (Dec. Places) parameter
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 15.Jan.1998	DT
	;-------------------------------------------------------------------------------
	new YFORMAT,YPR
	
	SET YA1=""
	QUIT:'$DATA(YINHALT) YA1
	SET YA1=YINHALT
	IF YA1="" QUIT YA1
	
	if '$data(YTYP) set YTYP=6       ; "Text' by default
	if $get(YDECIMAL)="" {
		if SPRACHE="DE" {
			set YDECIMAL=","           ; FIXME: Needs to change - this is rubbish
		} else {
			set YDECIMAL="."
		}
	}
	
	IF '$FIND(".,",YDECIMAL) SET YDECIMAL=","
	IF +$GET(YIO)=1 DO IN     DO CRYPT1
	IF +$GET(YIO)=0 DO CRYPT0 DO OUT
	QUIT YINHALT
	
CRYPT1 ;ENCRYPT
	;IF $EXTRACT(YINHALT)'=$CHAR(0) SET YINHALT=$CHAR(0)_$REVERSE(YINHALT) ;table-mat 
	QUIT
	
CRYPT0 ;DECRYPT
	;IF $EXTRACT(YINHALT)=$CHAR(0) SET YINHALT=$REVERSE($EXTRACT(YINHALT,2,32000))
	QUIT
	
	;---------------------------------------
	; YTYP : Input Type     
	;---------------------------------------
	;  0 - Hidden 
	;  1 - Date 
	;  2 - Yes/No 
	;  3 - Memo 
	;  4 - Integer 
	;  5 - Password 
	;  6 - Text 
	;  7 - Time 
	;  8 - Currency            <
	;  9 - Counter 
	; 10 - File Name 
	; 11 - Draw 
	; 12 - Floating            <
	; 13 - IP-format 
	; 14 - Timestamp 
	; 15 - Collection 
	; 16 - Embedded 
	; 17 - Date CCYYMMDD 
	; 18 - Exchange Rate       <
	; 19 - Sequence Key
	;---------------------------------------
	
IN
	;-------------------------------------------------------------------------------
	; Input -> Storage
	;
	; Params:
	;
	; ByRefs:
	; 	YINHALT
	;
	; Returns:
	;
	; History:
	; 26-Mar-2009	GRF		SR16452: Added type 19 - Sequence Key
	; 20-Aug-2007	GM		SR13729: Change "FELDFORMAT" parameter to $$GetFormat^INPARA()
	; 04-Jun-2007	GRF		SRBR014528: $get required around ^WWWWAE reference
	; 11-Apr-2007	GRF		SRBR014310: Naked References
	; 05-Apr-2007	RPW		Fixed IPAddress checks.
	; 27-Nov-2006	JW		SR15101: Convert invalid currency entry to 0
	;-------------------------------------------------------------------------------
	SET YINHALT=$TRANSLATE(YINHALT,Y,"-")
	
	;IF YTYP=3 QUIT     ;MEMO		;SR13195
	IF YTYP=3 do  QUIT   ;MEMO
	. set YINHALT = $translate(YINHALT,$char(13,10),"|")
	
	if YTYP=19 set YINHALT = +YINHALT quit              ; SR16452
	
	;IF $GET(YFORM)'="" IF $EXTRACT($GET(YFORM),1,3)'="WWW" IF $EXTRACT($GET(YFORM),1,3)'="APM" SET YINHALT=$TRANSLATE(YINHALT,""""_"'<>","´´()")
	IF $GET(YFORM)'="" IF $EXTRACT($GET(YFORM),1,3)'="WWW" IF $EXTRACT($GET(YFORM),1,3)'="APM" DO
	. ; D128		$$$WWW120DoNotConvertQuotes()
	. IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,128)=$$$YES QUIT  ;ANFÜHRUNGSZEICHEN NICHT UMSETZTEN ;FIS;19.02.03 ;quotation marks Not 
	. SET YINHALT=$TRANSLATE(YINHALT,""""_"'","´´")    ; double and single quotes changed to $char(180)
	. QUIT:'$FIND(YINHALT,"<")
	. QUIT:'$FIND(YINHALT,">")
	. DO ^WWWUML(YINHALT,2)  ;NUR <> ;only where HTML tag present
	
	IF YTYP=6 QUIT                               ;TEXT - AS IS
	
	IF YTYP=2 DO  QUIT                           ; Boolean
	. SET:$FIND(YINHALT,1) YINHALT=1
	. IF YINHALT="CHECKED"   SET YINHALT=1
	. IF YINHALT="UNCHECKED" SET YINHALT=""
	. ;IF YINHALT="" SET YINHALT=0 
	. ;IF YINHALT=-1 SET YINHALT=1 ;table-mat 
	. IF YINHALT'=1 SET YINHALT=""
	. if YINHALT=0  set YINHALT=""	;SR13195
	
	IF YTYP=1 DO
	. IF YINHALT="."           SET YINHALT=+$HOROLOG QUIT
	. IF $EXTRACT(YINHALT)="-" SET YINHALT=+$HOROLOG-$EXTRACT(YINHALT,2,9)
	. IF $EXTRACT(YINHALT)="+" SET YINHALT=+$HOROLOG+$EXTRACT(YINHALT,2,9)
	. ;w !,"A",YINHALT        ; DO NOT RESTORE WITHOUT ADEQUATE CONTROL OVER WHO SEES THIS OPERATION
	. SET YINHALT=$$^WWWDATE1(YINHALT)  ;datum
	. ;w !,"B",YINHALT
	
	IF YTYP=7 DO
	. IF YINHALT="." SET YINHALT=$PIECE($HOROLOG,",",2) QUIT
	. SET YINHALT=$$^WWWTIME1(YINHALT) QUIT  ;uhrzeit
	
	IF YTYP=14 DO  QUIT  ;timestamp
	. IF YINHALT="." SET YINHALT=$HOROLOG QUIT  ;ZEITSTEMPEL
	. IF YINHALT=""  SET YINHALT=$HOROLOG QUIT  ;ZEITSTEMPEL
	. IF $PIECE(YINHALT," ",2)'="" SET YINHALT=$$^WWWDATE1($PIECE(YINHALT," ",1))_","_$$^WWWTIME1($PIECE(YINHALT," ",2,4)) QUIT   ;ZEITSTEMP
	. IF $PIECE(YINHALT," ",2)=""  SET YINHALT=$$^WWWDATE1(YINHALT) QUIT   ;ZEITSTEMP
	
	IF (YTYP=8)||(YTYP=12)||(YTYP=18) IF YINHALT'="" DO  QUIT    ;währung und betrag ;and 	;SR13074
	. NEW YINHALT1
	. IF $GET(YDECIMALLEN)="" SET YDECIMALLEN=2  ;TYBD;26328;15,10,2004;DECIMALKOMMASTELLEN
	. ;FORMAT "12.34@USD22.35@.562"
	. ;        ^^^^^ ^^^^^^^^^^^^^
	. SET YINHALT1 = $PIECE(YINHALT,"@",2,99)
	. SET YINHALT  = $PIECE(YINHALT,"@",1)
	. DO
	. . SET YINHALT=$TRANSLATE(YINHALT," ;:_#`'") ;SR13745
	. . ;SET YINHALT=$TRANSLATE(YINHALT,"/ ;:_#`'",",") ;SR13745  ;ACHTUNG / IN , TAUSCHEN, WENN KEY
	. . //SR13729
	. . ;SET TRENN=".,"
	. . ;IF SPRACHE'="" IF $DATA(^WWW100(0,"FELDFORMAT",SPRACHE,8,1)) SET TRENN=$TRANSLATE($PIECE(^WWW100(0,"FELDFORMAT",SPRACHE,8,1),Y,1),"nNxX")   ; SRBR014310 Naked Reference
	. . ;IF SPRACHE'="" IF $DATA(^WWW101(0,"FELDFORMAT",SPRACHE,8,1)) SET TRENN=$TRANSLATE($PIECE(^WWW101(0,"FELDFORMAT",SPRACHE,8,1),Y,1),"nNxX")   ; SRBR014310 Naked Reference
	. . ;IF SPRACHE="DE"  IF $LENGTH(TRENN)'=2 SET TRENN=".,"
	. . ;IF SPRACHE'="DE" IF $LENGTH(TRENN)'=2 SET TRENN=",."
	. . SET TRENN=$$GetFormat^INPARA(8,".,")  ;SR13729
	. . SET TRENN=$TRANSLATE(TRENN,"nNxX")  ;SR13729
	. . //
	. . IF YDECIMAL="," DO
	. . . SET YINHALT  = $TRANSLATE(YINHALT,$EXTRACT(TRENN,2)_$EXTRACT(TRENN,1),".")   ;KOMMA IN PUNKT UND PUNKT RAUS ;comma within point And point 
	. . . SET YINHALT1 = $TRANSLATE(YINHALT1,$EXTRACT(TRENN,2)_$EXTRACT(TRENN,1),".")  ;KOMMA IN PUNKT UND PUNKT RAUS ;comma within point And point 
	. . ;
	. . IF YDECIMAL'="," DO
	. . . SET YINHALT=$TRANSLATE(YINHALT,",")
	. . ;
	. . ;
	. . IF $EXTRACT(YINHALT)="=" DO
	. . . NEW SUMM1,SUMM2,SUMM3
	. . . SET YINHALT=$EXTRACT(YINHALT,2,99)
	. . . SET SUMM1=0
	. . . IF $FIND(YINHALT,"-") SET SUMM1=+$PIECE(YINHALT,"-",1)-$PIECE(YINHALT,"-",2) SET YINHALT=SUMM1 QUIT
	. . . IF $FIND(YINHALT,"+") SET SUMM1=+$PIECE(YINHALT,"+",1)+$PIECE(YINHALT,"+",2) SET YINHALT=SUMM1 QUIT
	. . . IF $FIND(YINHALT,"*") SET SUMM1=+$PIECE(YINHALT,"*",1)*$PIECE(YINHALT,"*",2) SET YINHALT=SUMM1 QUIT
	. . . IF $FIND(YINHALT,"/") IF +$PIECE(YINHALT,"/",2)'=0 SET SUMM1=(+$PIECE(YINHALT,"/",1))/(+$PIECE(YINHALT,"/",2)) SET YINHALT=SUMM1 QUIT
	. . . SET YINHALT=+YINHALT
	. . ;
	. . IF YTYP=8 IF YWHR'="" IF +YINHALT=0 DO  QUIT   ;SUCHEN FREMDWÄHRUNG ;seek 
	. . . NEW YWHR1,YWHR2,YWHR3,I
	. . . ;SET YWHR1=$$^WWWUPER($TRANSLATE(YINHALT," 1234567890,."))  ;ABWEICHENDES WÄHRUNGSKENNZEICHEN
	. . . ;SET YWHR1=$$^WWWUPER($TRANSLATE(YINHALT," -1234567890,."))  ;ABWEICHENDES WÄHRUNGSKENNZEICHEN;FIS;MINUSBETRÄGE;27024;21.12.04
	. . . SET YWHR1=$zconvert($TRANSLATE(YINHALT," -1234567890,."),"U")  ;ABWEICHENDES WÄHRUNGSKENNZEICHEN;FIS;MINUSBETRÄGE;27024;21.12.04
	. . . IF $ASCII(YWHR1)=128 SET YWHR1="EUR"
	. . . ;
	. . . ;QUIT:YWHR1=""
	. . . if (YWHR1="") || '$data(^WWWWAE(0,YWHR1)) set YINHALT=0 quit		; SR15101
	. . . ;
	. . . IF YWHR1="DM" SET YWHR1="DEM"     ; FIXME : Obsolete
	. . . IF YWHR1="EU" SET YWHR1="EUR"
	. . . SET YWHR3=$TRANSLATE(YINHALT,"abcdefghijklmnopqrstuvwxyz$ABCDEFGHIJKLMNOPQRSTUVWXYZ"_$CHAR(128))  ;BETRAG ;Sum 
	. . . ;QUIT:+YWHR3=0
	. . . if +YWHR3=0 set YINHALT=0 quit		; SR15101
	. . . ;
	. . . SET YWHR2=$PIECE($GET(^WWWWAE(0,YWHR1,1)),Y,5)  ;UMRECHNUNGSFAKTOR FREMDWÄHRUNG
	. . . do GetConversionRate(YFORM,YKEY,.YWHR2)
	. . . IF YINHALT1'="" IF '$FIND(YINHALT1,"@") SET YWHR2=YINHALT1                ;VORGABE UMRECHNUNGSFAKTOR FREMDWÄHRUNG;FIS;25727;21.05.04
	. . . IF YINHALT1'="" IF $FIND(YINHALT1,"@")  SET YWHR2=$PIECE(YINHALT1,"@",2)  ;VORGABE UMRECHNUNGSFAKTOR FREMDWÄHRUNG;FIS;25727;21.05.04
	. . . SET YINHALT1=""
	. . . IF YWHR1'="" IF +YWHR3'=0 SET YINHALT1=YWHR1_YWHR3_"@"_YWHR2  ;FORMAT: USD10,00@0,8 ;FIS;25727;21.05.04
	. . . ;IF +YWHR2'=0 SET YINHALT=$JUSTIFY(YWHR3*YWHR2,0,YDECIMALLEN) QUIT           ;RECHNEN ;TYBD;26328;15,10,2004;DECIMALKOMMASTELLEN
	. . . if $isvalidnum(YWHR2) SET YINHALT=$JUSTIFY(YWHR3*YWHR2,0,YDECIMALLEN) QUIT   ;RECHNEN ;TYBD;26328;15,10,2004;DECIMALKOMMASTELLEN ; SR14144 Allow any valid exchange rate to calculate the amount
	. . . SET YINHALT=YWHR3   ;OHNE RECHNEN;FIS;24971;27.02.04
	. . ;
	. . ; FIXME : <GRF> YWHR might still be "" if +YINHALT'=0 in previous test
	. . ;IF YTYP=8 IF $DATA(^WWWWAE(0,YWHR,1)) IF +$PIECE(^(1),Y,5)'=0 SET YINHALT=YINHALT*$PIECE(^(1),Y,5)  ;UMRECHNEN WÄHRUNGEN   ; SRBR014310 Naked Ref
	. . IF YTYP=8 IF +$PIECE($get(^WWWWAE(0,YWHR,1)),Y,5)'=0 SET YINHALT=YINHALT*$PIECE(^WWWWAE(0,YWHR,1),Y,5)  ;UMRECHNEN WÄHRUNGEN ;  SRBR014528
	. . ;
	. . IF (YTYP=12)||(YTYP=18) DO
	. . . IF +YINHALT=0 IF YINHALT'=0   SET YINHALT=+$TRANSLATE(YINHALT,"abcdefghijklmnopqrstuvwxyz$ABCDEFGHIJKLMNOPQRSTUVWXYZ"_$CHAR(128))  ;NUR BETRAG;FIS;25.05.04;25727
	. . . IF $EXTRACT(YINHALT,1,2)="0." SET YINHALT=+YINHALT  ;TYBD;15,12,2004;WEGEN SORTKEY INHALTSSTOFFE 0,6 = .6
	. . . set:$get(YNK)'="" YINHALT = +$JUSTIFY(YINHALT,0,YNK)    ; SR15384 - decimal places
	. . ;
	. . IF (YTYP=18) && (+YINHALT'=0) DO  		;SR13074
	. . . if $$$WWWSystemSetupExchangeRatesAsFraction($get(^WWWSystemSetup(0,0,1))) do      	; SR15525
	. . . . set YINHALT = 1 / YINHALT		; Invert it.
	. ;
	. ; (relocated from post-do command for clarity  SRBR014310)
	. ;IF $FIND(YINHALT1,"@") IF YINHALT1'="" SET YINHALT=YINHALT_"@"_YINHALT1
	. IF YTYP=8 IF YINHALT1'="" SET YINHALT=YINHALT_"@"_YINHALT1  ;FIS;25727;21.05.04
	
	
	; FIXME : <GRF> Integers should involve truncation.
	;         NOTE : This translation is not appropriate for entering "1,234" in English
	;         format since it will change the number to "1.234" and then to "1".
	;         In European format we will have "1.234,56" converted to "1.234.56"
	;         and then to "1.234" and finally "1".
	;         
	; WARNING : Some data fields have type INTEGER when they should be FLOATING or something else.
	;         
	;  e.g.	IF (YTYP=4) && (YINHALT'="") SET YINHALT=$TRANSLATE(YINHALT,",",".")\1 QUIT   ;ZAHLEN
	
	IF YTYP=4 IF YINHALT'="" SET YINHALT=+$TRANSLATE(YINHALT,",",".") QUIT   ;ZAHLEN
	
	
	;IF YTYP=9 IF YINHALT'="" IF YINHALT'="+" SET YINHALT=$PIECE(+$TRANSLATE(YINHALT,",","."),".",1) QUIT  ;ZÄHLERFELD
	
	
	IF YTYP=13 DO  ;IP FORMAT
	. NEW IP,I
	. IF YINHALT="" QUIT  ;KEINE IP ;no IP 
	. ;SET IP="..."
	. SET YINHALT=$$^WWWIP1($TRANSLATE(YINHALT,"-, :\/#'+*;","..........."))
	. ;FOR I=1:1:4 SET IP(1)=+$PIECE(YINHALT,".",I) DO
	. ;. IF IP(1)<0 SET IP(1)=0
	. ;. IF IP(1)>255 SET IP(1)=255
	. ;. SET $PIECE(IP,".",I)=$EXTRACT(1000+IP(1),2,4)
	. ;
	. ;SET YINHALT=IP
	
	
	IF YTYP=17 DO  ;DATUMSFORMAT  DDMMJJJJ IN JJJJJMMDD ;within 
	. SET YINHALT=$TRANSLATE(YINHALT,".-/,")
	. IF $LENGTH(YINHALT)=2 SET YINHALT=YINHALT_$$^WWWMONTH()_$$^WWWYEAR()
	. IF $LENGTH(YINHALT)=4 SET YINHALT=YINHALT_$$^WWWYEAR()
	. IF $LENGTH(YINHALT)=6 SET YINHALT=$EXTRACT(YINHALT,1,4)_$EXTRACT($$^WWWYEAR(),1,2)_$EXTRACT(YINHALT,5,6)
	. SET YINHALT=$EXTRACT(YINHALT,5,8)_$EXTRACT(YINHALT,3,4)_$EXTRACT(YINHALT,1,2)
	
	QUIT
	
OUT
	;-------------------------------------------------------------------------------
	;	Convert internal format to literal format.
	;	OUTPUT VOM DISK ZUM USER
	; 
	; History:
	; 26-Mar-2009	GRF		SR16452: Added type 19 - Sequence Key
	; 12-Jul-2007	shobby	SRBR014603: Replace the | character for $$$CRLF in memo fields.
	; 29-Jun-2007	shobby	SRBR014567: Make the new behaviour dependent on a SysSetup global
	; 28-Jun-2007	shobby	SRBR014567: Change to handle 'European' style numbers.  ie 1.000,23
	; 04-Jun-2007	GRF		SRBR014528: $get required around ^WWWWAE reference
	; 15-May-2007	GRF		SRBR014469: Correction to Floating Point/Exchange Rate types
	; 						(12/18) to strip trailing zeros after the decimal place.
	;-------------------------------------------------------------------------------
	
	/* vvvvv Disabled Block Start
	;IF YTYP=15 DO  QUIT   ;MULTI FELDER
	. IF YINHALT'="" IF '$FIND(YINHALT,";") IF $LISTGET(YINHALT,1)'=""  DO
	. . NEW YINHALT1,YI
	. . SET YINHALT1=YINHALT
	. . SET YINHALT=""
	. . FOR YI=1:1 QUIT:$LISTGET(YINHALT1,YI)=""  SET YINHALT=YINHALT_$LISTGET(YINHALT1,YI)_";"  QUIT:$LENGTH(YINHALT)>30000
	; ^^^^^ Disabled Block End */
	
	IF YTYP=6 QUIT   ;TEXT
	IF YTYP=3 set YINHALT=$$Replace^COMUtilStr(YINHALT,"|",$$$CRLF) QUIT   ;MEMO  ;BR014603
	if YTYP=19 set YINHALT = $select(YINHALT[".":$justify(YINHALT,0,2),1:YINHALT) quit     ; SR16452
	
	IF YTYP=2 set:YINHALT=-1 YINHALT=1 IF YINHALT="" SET YINHALT=0                     QUIT
	IF YTYP=1                                 SET YINHALT=$$^WWWDATE(YINHALT)          QUIT
	;IF YTYP=9 IF +YINHALT'=0 IF YINHALT'="+" SET YINHALT=+$PIECE(YINHALT,".",1)       QUIT
	
	; FIXME : <GRF> Integers should involve truncation.
	;         NOTE : Literal form will not have thousands separator
	;         - may not be a problem but see notes under IN.
	;  e.g.	IF (YTYP=4) && (+YINHALT'=0) SET YINHALT=$TRANSLATE(YINHALT,",",".")\1 QUIT   ;ZAHLEN
	
	IF YTYP=4 IF YINHALT'="" IF +YINHALT'=0   SET YINHALT=+$TRANSLATE(YINHALT,",",".") QUIT
	
	IF YTYP=7 IF YINHALT'=""                  SET YINHALT=$$^WWWTIME(YINHALT)          QUIT
	
	;   vvv SRBR014310 replace (Naked Ref)
	;IF YTYP=8 IF YINHALT'="" IF $FIND(YINHALT,"@") SET YINHALT=$PIECE(YINHALT,"@",1)  ;FIS;21.05.04;25727;WÄHRUNGSUMRECHNUNG MIT VORGABE
	;IF YTYP=8 IF $DATA(^WWWWAE(0,YWHR,1)) IF +$PIECE(^WWWWAE(0,YWHR,1),Y,5)'=0 SET YINHALT=YINHALT/$PIECE(^(1),Y,5)  ;UMRECHNEN WÄHRUNGEN
	;IF YTYP=8 IF YINHALT'="" IF +YINHALT'=0!(YINHALT="0") DO
	;. IF $GET(YDECIMALLEN)="" SET YDECIMALLEN=2  ;TYBD;26328;15,10,2004;DECIMALKOMMASTELLEN
	;. SET YINHALT=$$^WWWZAHL(YINHALT,0,YDECIMALLEN) QUIT  ;TYBD;WENN 0 DANN 0,00 AUSGEBEN;;TYBD;26328;15,10,2004;DECIMALKOMMASTELLEN
	
	IF YTYP=8 {
		IF YINHALT'="" IF $FIND(YINHALT,"@") SET YINHALT=$PIECE(YINHALT,"@",1)  ;FIS;21.05.04;25727;WÄHRUNGSUMRECHNUNG MIT VORGABE
	; FIXME : <GRF> YWHR might be null
		IF +$PIECE($get(^WWWWAE(0,YWHR,1)),Y,5)'=0 SET YINHALT=YINHALT/$PIECE(^WWWWAE(0,YWHR,1),Y,5)  ;UMRECHNEN WÄHRUNGEN    ; Naked Reference ; SRBR014528
	; FIXME : <GRF> Don't need extra test (see WWWTRnew)
	; e.g.	IF (+YINHALT'=0) || (YINHALT="0") {
		IF (YINHALT'="") && ((+YINHALT'=0) || (YINHALT="0")) {
			IF $GET(YDECIMALLEN)="" SET YDECIMALLEN=2   
			SET YINHALT=$$^WWWZAHL(YINHALT,0,YDECIMALLEN)
		}
	}
	
	IF (YTYP=18) && (+YINHALT'=0) {  		;SR13074
		if $$$WWWSystemSetupExchangeRatesAsFraction($get(^WWWSystemSetup(0,0,1))) {    	; SR15525
			set YINHALT = +$justify(1/YINHALT,0,10)		; Invert it and justify.
		}
	}
	
	if $get(YNK)="" set YNK = $LENGTH($PIECE(YINHALT,".",2)) ;SR13745
	
	;IF (YTYP=12)||(YTYP=18) IF YINHALT'="" IF YINHALT'=0 SET YINHALT=$$^WWWZAHL(YINHALT,0,$LENGTH($PIECE(YINHALT,".",2))) DO  QUIT
	
	; NOTE : WWWZAHL will add appropriate separators at thousands and decimal
	;        points ("comma and dot" or "space and comma")
	;        Cannot simply apply + to YINHALT before or after WWWZAHL call.
	;        This code will only strip trailing spaces in a "comma and dot"
	;        Must also strip trailing comma for European format - without stripping 1.000 to 1 (for a thousand)
	
	; BR014567 vvvv new version (see also SR15525 for more complete revision)
	IF (YTYP=12)||(YTYP=18) IF YINHALT'="" IF YINHALT'=0 SET YINHALT=$$^WWWZAHL(YINHALT,0,YNK) DO  QUIT
	. NEW YI
	. if $FIND(YINHALT,YDECIMAL) do                               ; don't strip from "1,000" [1.000 European format]
	. . for YI=1:1 quit:$extract(YINHALT,$length(YINHALT))'=0  set YINHALT=$extract(YINHALT,1,$length(YINHALT)-1)  ; as it should have been
	. . if $extract(YINHALT,$length(YINHALT))=YDECIMAL         set YINHALT=$extract(YINHALT,1,$length(YINHALT)-1)  ; and convert "2." to "2"
	
	IF YTYP=14 {   ;ZEITSTEMPEL
		IF $PIECE(YINHALT,",",2)'="" {
			SET YINHALT=$$^WWWDATE(YINHALT)_" "_$$^WWWTIME(+$PIECE(YINHALT,",",2))
		} else {
			SET YINHALT=$$^WWWDATE(YINHALT)
		}
	}
	
	IF YTYP=17 {  ;DATUMSFORMAT  JJJJJMMDD IN  DDMMJJJJ
		SET YINHALT=$EXTRACT(YINHALT,7,8)_"."_$EXTRACT(YINHALT,5,6)_"."_$EXTRACT(YINHALT,1,4)
	}
	
	QUIT
	
GetConversionRate(pstrYFORM,pstrYKEY,pstrRate)
	;-------------------------------------------------------------------------------
	; Attempt to execute the code and log an error on failure.
	;
	; Returns:
	;
	; History:
	; 01-Jun-2007	GRF		SR15525: was using YFORM rather than pstrYFORM
	; 15-Feb-2005	RobertW	Do not log NOLINE errors at all.
	; 03-Feb-2005	RobertW	Modified to show the error on the screen
	; 						except for NOLINE or NOROUTINE errors
	; 28-Jan-2005	RobertW	Created (SR10061)
	;-------------------------------------------------------------------------------
	new strCode,strRate
	
	set strCode="set strRate=$$GetFCRate^"_pstrYFORM_"()"
	set $ztrap="ExecuteCodeError^WWWTR"
	xecute strCode
	if strRate'="" set pstrRate=strRate
	
	quit
	
	
ExecuteCodeError  ; Internal Tag
	new strUser,strError
	set $ztrap=""
	
	set strError=$zerror
	
	if ($get(^DebugExecuteCode,0)'=0)&&($find(strError,"<NOLINE>")=0) {
		set strUser=$get(YBED,"UNKNOWN")
		set ^ExecuteCodeError(strUser,$i(^ExecuteCodeError(strUser)),$zdt($h,3),"Code")=strCode
		set ^ExecuteCodeError(strUser,$i(^ExecuteCodeError(strUser)),$zdt($h,3),"Error")=strError
	}
		
	quit
	
	
Cur(Amount)
	;-------------------------------------------------------------------------------
	; Return Currency Code
	;
	; Returns:
	;
	; History:
	; 11-Apr-2005   PO		SR11349 Copied from ^COMSYSFC
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Currency
	
	
ConvRate(Amount)
	;-------------------------------------------------------------------------------
	; Return Conversion Rate
	;
	; Returns:
	;
	; History:
	; 25-May-2007	GRF		SR15525: missing "@" in $length
	; 16-Jun-2005	PO		SR: Ensure Amount @ preceds FC amount
	; 30-May-2005   PO		SR12050: Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	; Convert "USD20@.8" to "@USD20@.8" so aligned correctly for Split
	
	;if $length(Amount)'=3 {    ; SR15525
	if $length(Amount,"@")'=3 {
		if Amount?1.A.E {
			set Amount="@"_Amount
		}
	}
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Rate
	
	
FCBase(Amount)
	;-------------------------------------------------------------------------------
	; Return Base Amount
	;
	; Returns:
	;
	; History:
	; 01-Jun-2005   PO		SR12050: Created
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	
	quit Base
	
	
FCAmount(Amount)
	;-------------------------------------------------------------------------------
	; Return Foreign Amount
	;
	; Returns:
	;
	; History:
	; 25-May-2007	GRF		SR15525: missing "@" in $length
	; 16-Jun-2005	PO		SR: Ensure Amount @ preceds FC amount
	; 11-Apr-2005   PO		SR11349 Copied from ^COMSYSFC
	;-------------------------------------------------------------------------------
	new Base,Foreign,Currency,Rate
	
	; Convert "USD20@.8" to "@USD20@.8" so aligned correctly for Split
	
	;if $length(Amount)'=3 {    ; SR15525
	if $length(Amount,"@")'=3 {
		if Amount?1.A.E {
			set Amount="@"_Amount
		}
	}
	
	do Split(Amount,.Base,.Foreign,.Currency,.Rate)
	quit Foreign
	
	
Split(Amount,Base,Foreign,Currency,Rate)	
	;-------------------------------------------------------------------------------
	; Split FC Amount to Base Amount, Foreign Amount, Currency Code, Rate
	; 
	; By Ref : Base,Foreign,Currency,Rate
	; Returns:
	;
	; History:
	; 11-Apr-2005   PO		SR11349 Copied from ^COMSYSFC
	;-------------------------------------------------------------------------------
	new FC
	
	set Base=$piece(Amount,"@",1)
	set FC  =$piece(Amount,"@",2)
	set Rate=$piece(Amount,"@",3)
	
	set Foreign =$translate(FC,"abcdefghijklmnopqrstuvwxyz$ABCDEFGHIJKLMNOPQRSTUVWXYZ")
	set Currency=$translate(FC," -1234567890,.")
	
	quit
	
ConvertLocaleNumber(pstrValue,YDECIMAL)
	;-------------------------------------------------------------------------------
	; Strip thousands delimiters and convert decimal place
	; in literal to internal numeric format change
	; 
	; 	Comma-Dot (English)  : Strip commas                            1,234.56 => 1234.56
	; 	Dot-Comma (European) : Strip dots and convert commas to dots   1.234,56 => 1234.56
	; 	      becomes
	; 	None-Dot  (Internal)
	; 
	; set fltValue = $$ConvertLocaleNumber^WWWTR(pstrValue,YDECIMAL)
	; 
	; History:
	; 23-May-2007	GRF		SR15525: created
	;-------------------------------------------------------------------------------
	new numValue
	
	;---------------------------------------
	; If necessary, calling routine should perform the following first.
	;  
	;    set:$get(YDECIMAL)="" YDECIMAL = $extract($$GetNumberDelimiters^COMUtilLocale(SPRACHE),2)
	;---------------------------------------
	
	if YDECIMAL=","  {
		set numValue = $translate(pstrValue,",.",".")      ; European
	} else {
		set numValue = $translate(pstrValue,",")           ; English
	}
	quit numValue
	
]]></Routine>
</Export>