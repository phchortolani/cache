<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPROSTEMP01" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPROSTEMP01(YKEY,KILL) ;INPROSTEMP01;FIS;ÜBERTRAGEN VON ÄNDERUNGEN IN INPROSTEMP0 NACH WWW013Z;08.10.01
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		ÜBERTRAGEN VON ÄNDERUNGEN IN INPROSTEMP0 NACH WWW013Z
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	08.10.01
	;|
	;\------------------------------------------------------------------/
	;
	NEW STEMP,LFN,BETR,YFELDOLD,GKALT,KEY,SATZ,SATZOLD
	SET KILL=$GET(KILL)
	SET STEMP=$PIECE(YKEY,",",1)
	QUIT:STEMP=""
	SET LFN=$PIECE(YKEY,",",2)
	QUIT:LFN=""
	SET YFELD=$GET(YFELD)
	IF YFELD="" SET YFELD=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,"INPROSTEMP0","D",1))
	QUIT:YFELD=""
	SET GKALT=""
	SET YFELDOLD=$GET(^INPROSTEMP0(YM,STEMP,LFN,1))
	IF $PIECE(YFELDOLD,Y,14)'="" SET GKALT=4  ;GESTARTET
	IF $PIECE(YFELDOLD,Y,16)'="" SET GKALT=5  ;GESTOPPT
	SET KEY=""
	SET SATZ=""
	SET SATZOLD=""
	SET BETR=YLOCATION
	IF GKALT=4 DO
	. IF $PIECE(YFELDOLD,Y,14)'="" IF $PIECE(YFELDOLD,Y,19)'="" IF $PIECE(YFELDOLD,Y,15)'="" DO
	. . IF $DATA(^WWW013Z(YM,BETR,$PIECE(YFELDOLD,Y,14),$PIECE(YFELDOLD,Y,19),$PIECE(YFELDOLD,Y,15),GKALT,1)) DO
	. . . SET SATZ=^(1)
	. . . SET SATZOLD=SATZ
	. . . DO ^WWWSKILL("WWW013Z",BETR_","_$PIECE(YFELDOLD,Y,14)_","_$PIECE(YFELDOLD,Y,19)_","_$PIECE(YFELDOLD,Y,15)_","_GKALT)
	. . . QUIT
	. . QUIT
	. QUIT
	IF GKALT=5 DO
	. IF $PIECE(YFELDOLD,Y,16)'="" IF $PIECE(YFELDOLD,Y,19)'="" IF $PIECE(YFELDOLD,Y,17)'="" DO
	. . IF $DATA(^WWW013Z(YM,BETR,$PIECE(YFELDOLD,Y,16),$PIECE(YFELDOLD,Y,19),$PIECE(YFELDOLD,Y,17),GKALT,1)) DO
	. . . SET SATZ=^(1)
	. . . SET SATZOLD=SATZ
	. . . DO ^WWWSKILL("WWW013Z",BETR_","_$PIECE(YFELDOLD,Y,16)_","_$PIECE(YFELDOLD,Y,19)_","_$PIECE(YFELDOLD,Y,17)_","_GKALT)
	. . . QUIT
	. . QUIT
	. QUIT
	IF KILL=1 DO PROT QUIT  ;LÖSCHEN OHNE NEUAUFBAU ;Delete without 
	IF $PIECE(YFELD,Y,19)="" SET $PIECE(YFELD,Y,19)=YBED  ;MITARBEITER
	SET $PIECE(SATZ,Y,5)=STEMP  ;STEMPELNUMMER
	SET $PIECE(SATZ,Y,10)=$PIECE(YFELD,Y,10)  ;UNTER-NUMMER
	;START ;take-off 
	IF $PIECE(YFELD,Y,14)'="" DO
	. SET KEY=BETR_","_+$PIECE(YFELD,Y,14)_","_$PIECE(YFELD,Y,19)_","_+$PIECE(YFELD,Y,15)_","_"4"
	. SET $PIECE(SATZ,Y,1)=$PIECE(YFELD,Y,14)
	. SET $PIECE(SATZ,Y,2)=$PIECE(YFELD,Y,15)
	. QUIT
	;STOP
	IF $PIECE(YFELD,Y,16)'="" DO
	. SET KEY=BETR_","_+$PIECE(YFELD,Y,16)_","_$PIECE(YFELD,Y,19)_","_+$PIECE(YFELD,Y,17)_","_"5"
	. SET $PIECE(SATZ,Y,1)=$PIECE(YFELD,Y,16)
	. SET $PIECE(SATZ,Y,2)=$PIECE(YFELD,Y,17)
	. QUIT
	;SPEICHERUNG
	DO 
	. NEW YFORM,YVOR
	. SET OK=$$^WWWSPEI("WWW013Z",KEY,SATZ,1)
	. DO PROT
	. QUIT
	QUIT
	
PROT   ;PROTOKOLLDATEI
	NEW YUHR,YDATEI
	SET YDATEI="WWW013Z"
	IF $PIECE($GET(^WWW120(0,YDATEI,1)),Y,29)'=1 QUIT  ;KEIN PROTOKOLL ;no protocol 
	SET YUHR=$PIECE($HOROLOG,",",2)
	IF KEY="" DO
	. IF GKALT=4 SET KEY=BETR_","_$PIECE(YFELDOLD,Y,14)_","_$PIECE(YFELDOLD,Y,19)_","_$PIECE(YFELDOLD,Y,15)_","_GKALT
	. IF GKALT=5 SET KEY=BETR_","_$PIECE(YFELDOLD,Y,16)_","_$PIECE(YFELDOLD,Y,19)_","_$PIECE(YFELDOLD,Y,17)_","_GKALT
	. QUIT
	SET ^WWWPRO(YM,YDATEI,+$HOROLOG,YUHR,YBED,KEY,0,1)=SATZOLD  ;ALT ;stale 
	IF KILL'=1 SET ^WWWPRO(YM,YDATEI,+$HOROLOG,YUHR,YBED,KEY,1,1)=SATZ  ;NEU ;recent 
	QUIT
]]></Routine>
</Export>