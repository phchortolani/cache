<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFCalc" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFCalc
 
#include COMSYS
#include INConst
 
 
OrderTotal(pidOrder,lineAmounts) 
    ;-------------------------------------------------------------------------------
    ; Calculate Order Total (Before tax, after line discounts)
    ;
    ; Params:	pidOrder - order id
    ; 			lineAmounts - byRef: array of line amounts: lineAmounts(idLine) = lineAmount
    ;
    ; Returns: order total
    ;
    ; History:
    ; 02-Aug-2005	JW		SR12988: Created
    ;-------------------------------------------------------------------------------
	new orderTotal,curAmount,idLine
	
	set orderTotal = 0
	
	set idLine = ""
	for {
		set idLine = $order(^INAUFP(YM,pidOrder,idLine))
		quit:idLine=""
		
		set curAmount = $$$INAUFPNetSalesPrice($get(^INAUFP(YM,pidOrder,idLine,1)))
		
		if curAmount'=0 {
			set lineAmounts(idLine) = curAmount
			set orderTotal = orderTotal + curAmount
		}
	}
	
	quit orderTotal
	
	
	
OrderDiscountPortion(pidOrder="",plstOnlyInclLines="",pfltDiscounts,pfltSurcharges,parrNettAmounts)
    ;-------------------------------------------------------------------------------
    ; Calculate the portion of the order discounts (or surcharges) relating to the lines
    ; passed in.
    ; 
    ; Params: 	pidOrder			- order id
    ; 			plstOnlyInclLines	- Lines to include in results. If null, include all
    ; 			pcurDiscounts		- byRef - discount amount
    ; 			pcurSurcharges		- byRef - surcharge amount
    ; 			parrNettAmounts		- byRef - the nett values
    ; 
    ; Returns: discount-surcharge total (tax ex)
    ; 
    ; History:
    ; 13-Sep-2005	JW			SR13434: Rewrote - moved from INAUFHTML
    ; 30-Aug-2005	JW			SR13311: Return tax ex amount
    ; 18-Aug-2005	JW			SR12950: Add tax amount
    ; 14-Jul-2005	PO/JW/GRF	SR12949: Changed handling of booleans as part of peer review
    ; 12-Jul-2005	JW			SR12949: Added '+' to booleans
    ; 20-Jun-2005	JW			Created SR12707
    ;-------------------------------------------------------------------------------
	new idDiscount,objOrderDiscount,idDiscountType,fltDiscount,objDiscount
	new idx,idLine,arrResults,fltOrderTotal,arrAmounts
	
	kill parrNettAmounts
	set fltOrderTotal = $$OrderTotal(pidOrder,.arrAmounts)
	merge parrNettAmounts = arrAmounts
	
	set pfltDiscounts = 0
	set pfltSurcharges = 0
	
	if $get(pidOrder)'="" {
		
		set idDiscount=""
		for {
			set idDiscount=$order(^INAUFDISCOUNT(YM,pidOrder,idDiscount))
			quit:idDiscount=""
			
			set objOrderDiscount=$get(^INAUFDISCOUNT(YM,pidOrder,idDiscount,1))
			
			set idDiscountType=$$$INAUFDISCOUNTDiscountType(objOrderDiscount)
			if idDiscountType'="" {
				set objDiscount=$get(^INDISCOUNT(YM,idDiscountType,1))
				
				do VirtualRoundApportion^COMUtilNum($$$INAUFDISCOUNTDiscountAmount(objOrderDiscount),.arrAmounts,.arrResults,fltOrderTotal)
				set fltDiscount = 0
				for idx=1:1:$listlength(plstOnlyInclLines) {
					set idLine=$list(plstOnlyInclLines,idx)
					set fltDiscount = fltDiscount + $get(arrResults(idLine))
					set parrNettAmounts(idLine) = parrNettAmounts(idLine) + $get(arrResults(idLine))
				}
				
				if +$$$INDISCOUNTSurcharge(objDiscount) {
					set pfltSurcharges = pfltSurcharges + fltDiscount
				} else {
					set pfltDiscounts = pfltDiscounts + fltDiscount
				}
			}
		}
	}
	quit (pfltDiscounts-pfltSurcharges)
	
]]></Routine>
</Export>