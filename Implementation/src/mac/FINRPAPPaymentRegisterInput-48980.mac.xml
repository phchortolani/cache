<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPAPPaymentRegisterInput" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPAPPaymentRegisterInput
	#include COMSYS
	#include FINConst
	
	;++++++++++++++++++++++++++++++++++++++++++++++++
	;
	; Routines used for the parameter input form
	; 
	;++++++++++++++++++++++++++++++++++++++++++++++++
 		
OnBeforeRunning(pidReportID)
	;-------------------------------------------------------------------------------
	; Routine to run before running the report
	;
	; Params: pidReportID		: The report id for the reprint
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus = $$$OK
 
	if (pidReportID="")&&('$data(^CacheTempAccounts(YUSER))) {
		set strStatus=$listbuild("Fin00392")  ;No Bank Account
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
 
	quit
	
	
StartAccountSearch()
	;-------------------------------------------------------------------------------
	; Start the bank account selection COMView window
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new strYMFELD,idRun
	
	set strYMFELD = $$GetFormData^COMUtilForm(YFORM,"M")
	set idRun = $piece(strYMFELD,Y,4)
	
	if (idRun'="") {
		$$$Alert("Fin01081")  ;Account search not possible - reprint has been specified.
	} else {
		do Initialise^COMViewCustom("FINGLBank","AccountSelected^FINRPAPPaymentRegisterInput") // MUST NOT CALL WITHOUT WRAPPER
		do Start^COMViewCustom() // MUST NOT CALL WITHOUT WRAPPER
	}
	
	quit $$$OK
	
	
AccountSelected(pobjResultSet)
	;-------------------------------------------------------------------------------
	; Routine to run when an account rowset is selected
	;
	; Params:	pobjResultSet		: The account row sets
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------	
	new idAccount,arrData
	
	while pobjResultSet.Next() {
		set idAccount=$$GetID^COMViewCustom(pobjResultSet)
		if (idAccount'="") set ^CacheTempAccounts(YUSER,idAccount)=""	
	}
	
	do UpdateSelected(.arrData)
	write "document.WWW2.AccountsSelected.value="""_arrData("AccountsSelected")_""";",!
	write "document.WWW2.AccountsSelectedCounter.value="_arrData("AccountsSelectedCounter")_";",!
 
	quit
	
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Routine to run after data fields of the parameter input form
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new arrData
	
	if '$data(YMFELD) kill ^CacheTempAccounts(YUSER)
	
	quit:$get(YSEITE)'=1 // only for page one
	kill arrData
	
	do UpdateSelected(.arrData)
			
	write "<B>"_$$$Text("Fin01078")_"</B>",!   ;Bank Accounts Selected
	write "",!
	write "<table style='font-size:14px'>",!
	write "<tr>",!
	write "<td><input readonly style='background-color:lightgrey' size=10 name='AccountsSelectedCounter' value='"_arrData("AccountsSelectedCounter")_"'>",!
	write "<div id='StatusMessage' style='display:inline;'></div>",!
	write "</td></tr>",!
	write "<tr>",!
	write "<td><textarea readonly style='background-color:lightgrey' cols=95 rows=25 name='AccountsSelected'></textarea></td>",!
	write "</tr>",!
	write "</table>",!
		
	$$$StartScript()
	write "document.WWW2.AccountsSelected.value="""_arrData("AccountsSelected")_""";",!
	$$$EndScript()
		
	quit
	
	
OnBlurReportID(pYINHALT,pYOLDV)
	;-------------------------------------------------------------------------------
	; Code to run on blur of the report id.
	;
	; Params: pYINHALT		: New value
	; 		  pYOLDV		: Old value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 17-Oct-2006	Steve S		SR15124: Use Payment Date/Site Loc
	; 10-Oct-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new idRun,idOldRun,blnReadOnly,objFINAPPaymentRegister
	new dtePay,idLoc
 
	set idRun 		= $get(pYINHALT)
	set idOldRun 	= $get(pYOLDV)
 
	if (idRun'=idOldRun) { // change made
		if (idRun'="")&&($data(^FINAPPaymentRegister(YM,idRun))) {
			//SR15124
			set objFINAPPaymentRegister = $get(^FINAPPaymentRegister(YM,idRun,1))
			set dtePay = $$$FINAPPaymentRegisterPaymentDate(objFINAPPaymentRegister)
			set idLoc  = $$$FINAPPaymentRegisterSiteLocation(objFINAPPaymentRegister)
			do PopulateDataField^COMUtils(YFORM,5,dtePay,"M")
			do PopulateDataField^COMUtils(YFORM,7,idLoc,"M")
			do ToggleReadOnly^COMUtilForm(5,$$$YES,YFORM,,"M")
		} elseif (idRun="") {
			do ToggleReadOnly^COMUtilForm(5,$$$NO,YFORM,,"M")	
		}
	}
 
	quit
	
	
UpdateSelected(parrData)
	/*------------------------------------------------------------------------------
	; Update current data selected accounts
	;
	; Params:
	;
	; ByRefs: parrData		: The array with the account data
	;
	; Returns:
	;
	; History:
	; 27-Sep-2006	Steve S		SR15012: Created
	;-----------------------------------------------------------------------------*/
	new idAccount,intCount,strAccounts,strName,blnLimit,strText
	new strDetail
	
	set intCount=0
	set idAccount=""
	
	set strAccounts=""
	set blnLimit=$$$NO
	
	for {
		set idAccount=$order(^CacheTempAccounts(YUSER,idAccount))
		quit:(idAccount="")
		
		set intCount=intCount+1
		
		if (intCount<=500) {
			set strDetail=$justify(idAccount,30)
			set strName=$$$FINGLBankBankAccountName($get(^FINGLBank(YM,idAccount,1)))
			if strName="" set strName="---"
			set strDetail=strDetail_": "_strName_"\n"
			
			if ($length(strAccounts)+$length(strDetail))<32000 {
				set strAccounts=strAccounts_strDetail
			}
		} else {
			if 'blnLimit {
				set blnLimit=$$$YES
				set strText=$zconvert($$$Text("Fin00967"),"U")  ;Only displaying first 500 entries
				set strAccounts=$justify(strText,5)_"\n"_strAccounts
			}	
		}
	}
	
	set parrData("AccountsSelected")=strAccounts
	set parrData("AccountsSelectedCounter")=intCount
	
	quit
	
	
OnBlurSiteLocation(pYINHALT,pYOLDV)
	;-------------------------------------------------------------------------------
	; Code to run on blur of the site location field
	;
	; Params: pYINHALT		: New value
	; 		  pYOLDV		: Old value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 17-Oct-2006	Steve S		SR15124: Created
	;-------------------------------------------------------------------------------
	new idLoc,idOldLoc,strYMFELD,idRun,objRun,idRunLoc
	
	set idLoc 		= $get(pYINHALT)
	set idOldLoc 	= $get(pYOLDV)
	
	if (idLoc'=idOldLoc) {
		set strYMFELD = $$GetFormData^COMUtilForm(YFORM,"M")
		set idRun = $piece(strYMFELD,Y,4)
		if (idRun'="")&&($data(^FINAPPaymentRegister(YM,idRun))) {
			set objRun = $get(^FINAPPaymentRegister(YM,idRun,1))
			set idRunLoc = $$$FINAPPaymentRegisterSiteLocation(objRun)
			if (idRunLoc'="")&&(idLoc'=idRunLoc) { // can't change an existing filter
				$$$Alert($listbuild("Fin01082",idRun))  ;Can not modify site location filter for Report Number %1.
				do PopulateDataField^COMUtils(YFORM,7,idRunLoc,"M")
			}
		}
	}
 
	quit
]]></Routine>
</Export>