<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFANZAHL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFANZAHL(YAUFTRAG,RECHNUNG,RUECK)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZAHLUNGSSUMME RECHNEN
	;
	; Inputs : 
	;	YAUFTRAG = AUFTRAG
	;	RECHNUNG = RECHNUNGSNUMMER
	;
	;	RUECK = RÜCKBUCHUNG VORNEHMEN
	;		 ANZAHLBETR = SUMME ANZAHLUNG NETTO
	;		 ANZAHLG()  = BRUTTO ANZAHLUNG JE STEUERSATZ
	;		 ANZAHLGN() = NETTO ANZAHLUNG JE STEUERSATZ 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 29-Dec-2006	GRF		SR15333: Naked References; quits
	; 14.07.2001	FAN 
	;-------------------------------------------------------------------------------
	NEW ANZAHLBETR,YFELDPZ,DATUM
	
	SET ANZAHLBETR=0
	QUIT:YBELEG=12 ANZAHLBETR  ;NICHT BEI ANZAHLUNGSRECHNUNG SELBST ;Not next to self 
	SET YAUFTRAG=$GET(YAUFTRAG)         IF YAUFTRAG="" QUIT ANZAHLBETR
	SET RECHNUNG=$GET(RECHNUNG)         IF RECHNUNG="" QUIT ANZAHLBETR
	IF '$DATA(^INAUFPZ(YM,YAUFTRAG)) QUIT ANZAHLBETR
	IF YCOPY=1 IF $DATA(YANZAHLTEMP) IF $GET(YANZAHLTEMP)'=0 QUIT YANZAHLTEMP  ;FIS;16.04.04;25558;WIEDERHOLUNGSDRUCK (SPRACHE) KEINE NEUBERECHNUNG
	
	SET DATUM=""
	FOR  SET DATUM=$ORDER(^INAUFPZ(YM,YAUFTRAG,DATUM)) QUIT:DATUM=""  DO
	. SET YFELDPZ=$GET(^INAUFPZ(YM,YAUFTRAG,DATUM,1))
	. QUIT:$PIECE(YFELDPZ,Y,11)=""  ;ANZAHLUNGSBETRAG NOCH NICHT BERECHNET ;yet Not 
	. IF $PIECE(YFELDPZ,Y,12)="" DO  QUIT  ;NOCH NICHT MIT END-RECHNUNG VERRECHNET ;yet Not by means of 
	. . SET ANZAHLBETR=ANZAHLBETR+$PIECE(YFELDPZ,Y,11)
	. . IF YCOPY'=1 IF YBELEG=7!(YBELEG=17) DO
	. . . SET $PIECE(^INAUFPZ(YM,YAUFTRAG,DATUM,1),Y,12)=RECHNUNG  ;SPEICHERN, MIT WELCHER RECHNUNG DIE ANZAHLUNG VERRECHNET WURDE
	. . . IF +$GET(RUECK)=1 DO RUECKBUCH  ;RÜCKBUCHEN DER TEILZAHLUNG;FIS;21.02.05;27033
	. ;
	. ;IF $PIECE(YFELDPZ,Y,12)=$EXTRACT(RECHNUNG,1,$LENGTH($PIECE(YFELDPZ,Y,12))) DO  ;RECHNUNGSKOPIE
	. IF YCOPY=1 DO
	. . SET ANZAHLBETR=ANZAHLBETR+$PIECE(YFELDPZ,Y,11)
	
	IF $DATA(YANZAHLTEMP) SET YANZAHLTEMP=ANZAHLBETR
	QUIT ANZAHLBETR
	
RUECKBUCH		;RÜCKBUCHEN DER TEILZAHLUNG;FIS;21.02.05;27033
	NEW STEUER,STEUERKZ,YDEBITOR,ERLOESE,A,KONTEN,WAGR,MWPOX
	
	IF $GET(YAUFTRAG1)="" SET YAUFTRAG1=$GET(^INAUF(YM,YAUFTRAG,1))
	SET YBETRIEB=$GET(YBETRIEB)   ;BETRIEBSNUMMER
	IF YBETRIEB="" NEW YBETRIEB SET YBETRIEB=$PIECE(YAUFTRAG1,Y,6)  ;BETRIEB DES VERKAUFS ;location
	IF YBETRIEB="" SET YBETRIEB=YLOCATION
	SET YDEBITOR=$PIECE($GET(^INKUNDE(YM,YKUNDE,1)),Y,48)
	IF YDEBITOR="" SET YDEBITOR=YKUNDE
	
	SET STEUER=1  ;INLAND
	SET STEUERKZ=+$PIECE(YADRES,Y,54)  ;0,INLAND,EG,3.LAND  -> FELD 54 WIRD OBEN NEU GESETZT !!!
	IF $PIECE(YADRES,Y,17)'="" IF $PIECE(YADRES,Y,17)'=$GET(YCOUNTRY) SET STEUER=0  ;KEIN DEUTSCHLAND ;no Germany 
	IF $PIECE(YADRES,Y,50)'="" IF '$FIND($PIECE(YADRES,Y,50),$GET(YCOUNTRY)) SET STEUER=0  ;STEUERID AUSLAND ;foreign country 
	IF $PIECE(YADRES,Y,54)'="" IF $PIECE(YADRES,Y,54)'=1 SET STEUER=0  ;KEINE STEUER  -> FELD 54 WIRD OBEN NEU GESETZT !!! ;no tax field upstairs recent staid 
	IF STEUERKZ=2 IF $PIECE(YADRES,Y,50)="" SET STEUER=1  ;EG OHNE UST-ID = STEUER BERECHNEN ! (PRIVATPERSON)  -> FELD 50 WIRD OBEN NEU GESETZT !!! ;without tax calculate field upstairs recent staid 
	
	DO
	. SET KONTEN=""
	. SET KONTKST=""
	. SET KONTCOGS=""
	. SET MWPOX=$ORDER(MWPO(""))  ;UST-SATZ
	. IF $PIECE(YFELDPZ,Y,21)'="" SET MWPOX=$PIECE(YFELDPZ,Y,21)
	. IF MWPOX="" SET MWPOX=1
	. SET BETRAG=$PIECE(YFELDPZ,Y,11)
	. SET ANZAHLGN(MWPOX)=$GET(ANZAHLGN(MWPOX))+BETRAG  ;SPEICHERN NETTO-TEILZAHLUNGSBETRAG JE MWST-SATZ;FIS;21.02.05;27033
	. IF STEUER'=0 DO
	. . NEW MWST,MWSTP
	. . SET MWST=+$PIECE($GET(^WWW101(0,"MWST",SPRACHE,+MWPOX,1)),Y,1)  ;FIS;22.03.04;;25372;MWST '= STEUER !
	. . SET MWSTP=$JUSTIFY(BETRAG/100*MWST,0,2)
	. . SET BETRAG=BETRAG+MWSTP
	. ;
	. SET ANZAHLG(MWPOX)=$GET(ANZAHLG(MWPOX))+BETRAG  ;SPEICHERN BRUTTO-TEILZAHLUNGSBETRAG JE MWST-SATZ;FIS;21.02.05;27033
	. ;
	. SET WAGR=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv SR15333  Naked Ref
	.;IF WAGR'="" SET KONTEN=$GET(^INKSTL(YM,YBETRIEB,WAGR,STEUERKZ,3,1))  ;AUS PARAMETER ;out of parameter 
	.;IF WAGR'="" IF $GET(^INKSTLUST(YM,YBETRIEB,WAGR,STEUERKZ,3,+MWPOX,1))'="" SET KONTEN=^ (1)  ;KONTEN JE STEUERSATZ;FIS;17.03.04;25372
	. ;
	. if WAGR'="" do
	. . set KONTEN = $get(^INKSTLUST(YM,YBETRIEB,WAGR,STEUERKZ,3,+MWPOX,1))
	. . if KONTEN="" set KONTEN = $get(^INKSTL(YM,YBETRIEB,WAGR,STEUERKZ,3,1))
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ SR15333  Naked Ref
	. ;
	. IF KONTEN="" IF $GET(SORTI)'="" SET KONTEN=$GET(^INKSTL1(YM,YBETRIEB,SORTI,STEUERKZ,3,1))  ;AUS VORGABE SORTIMENT ;out of default 
	. IF KONTEN="" DO
	. . ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv SR15333  Naked Ref
	. .;SET KONTEN=$GET(^INKSTL(YM,YBETRIEB,0,STEUERKZ,3,1))  ;AUS VORGABE SONSTIGES ;out of default 
	. .;IF $GET(^INKSTLUST(YM,YBETRIEB,0,STEUERKZ,3,+MWPOX,1))'="" SET KONTEN=^ (1)  ;KONTEN JE STEUERSATZ;FIS;17.03.04;25372
	. . ;
	. . set KONTEN = $get(^INKSTLUST(YM,YBETRIEB,0,STEUERKZ,3,+MWPOX,1))
	. . if KONTEN="" set KONTEN = $get(^INKSTL(YM,YBETRIEB,0,STEUERKZ,3,1))
	. . ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ SR15333  Naked Ref
	. ;
	. IF KONTEN'="" DO
	. . SET KONTKST  = $PIECE(KONTEN,Y,1)  ;AUS PARAMETER ;out of parameter 
	. . SET KONTCOGS = $PIECE(KONTEN,Y,4)  ;FIS;25830;28.06.04;VORGABE PRODUKTKOSTENKONTO
	. ;
	. IF KONTKST=""  SET KONTKST=$PIECE($GET(^INFIBPAR(0,YM,YBETRIEB,1)),Y,14)  ;VORGABE KOSTENSTELLE ;default cost centre 
	. IF KONTCOGS="" SET KONTCOGS=$PIECE($GET(^INFIBPAR(0,YM,YBETRIEB,1)),Y,34)
	. ;
	. SET ERLOESE=$PIECE(YFELDPZ,Y,20)  ;VORGABE ERLOES AUS TEILZAHLUNG;FIS;22349;11.11.03
	. IF ERLOESE="" SET ERLOESE=$PIECE($GET(^INFIBPAR(0,YM,YBETRIEB,1)),Y,27)  ;VORGABE ERLOES ;default 
	. IF ERLOESE="" SET ERLOESE=$PIECE(KONTEN,Y,3)
	. IF ERLOESE="" SET ERLOESE="ERLOESE"
	. ;
	. ;SET A=Y_+$HOROLOG_Y_(BETRAG*-1)_Y_YDEBITOR_Y_ERLOESE_Y_STEUER_Y_RECHNUNG_Y_Y  ;RÜCKBUCHUNG
	. SET A=Y_+$HOROLOG_Y_BETRAG_Y_ERLOESE_Y_YDEBITOR_Y_MWPOX_Y_RECHNUNG_Y_Y  ;RÜCKBUCHUNG
	. ;
	. SET $PIECE(A,Y,8)=$$^WWWTEXT(34145,,1)_" "_$PIECE(YFELDPZ,Y,7)  ;TEILZAHLUNGSRÜCKBUCHUNG
	. SET $PIECE(A,Y,12)=KONTKST  ;KOSTENSTELLE ;cost centre 
	. SET $PIECE(A,Y,35)=KONTCOGS  ;FIS;25830;28.06.04;VORGABE PRODUKTKOSTENKONTO
	. ;
	. IF +$PIECE(A,Y,3)=0 QUIT:$PIECE($GET(^INFIBPAR(0,YM,YBETRIEB,1)),Y,39)'=1  ;BUCHEN AUCH WENN 0
	. SET KEY=$$^INFIBBUCH(A,YBETRIEB,"","")   ;BUCHEN IN FIBU GESAMTBUCH="" = ALLES
	
	SET $PIECE(^INAUFPZ(YM,YAUFTRAG,DATUM,1),Y,28)=+$HOROLOG   ;RÜCKBUCHUNG ERFOLGT
	QUIT
]]></Routine>
</Export>