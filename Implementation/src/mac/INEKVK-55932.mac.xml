<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INEKVK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INEKVK ;INEKVK;DT;ERRECHNEN VK AUS EK;20.01.2000
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERRECHNEN VK AUS EK
	;
	;	UNTERPROGRAMM AUFRUF AUS EVENTBROKER UND ANDEREN PROGRAMMEN ;Program out of And 
	;	SONDERPROGRAMM : YINHALT=EINGABE IM FELD ;field 
	;	                 YVAR=ZUSATZVARIABLE
	;	ERGEBNIS:        %TXT(1)
	;	
	; Inputs : 
	;
	;
	; ByRef :
	;	YVAR
	;	YINHALT
	;
	; Returns :
	;	%TXT(1) set to cost, space, or null
	;
	; History :
	; 05-Feb-2008	GRF		Doco
	; 20.01.2000	DT		Created
	;-------------------------------------------------------------------------------
	NEW ART,LIEF,BIS,EK,AUFSCHLAG,VK,RUNDUNG
	
	SET %TXT(1)=$GET(%TXT(1))
	QUIT:$GET(YVAR)=""  ;KEIN ARTIKEL ;no item 
	
	SET ART=YVAR
	IF YINHALT="" SET %TXT(1)=" " QUIT  ;KEIN AUFSCHLAG=""
	SET AUFSCHLAG=$$^WWWTR(1,12,YINHALT)
	SET %TXT(1)=""
	SET EK=$$^INARTKOST(ART)+$PIECE($GET(^INART(YM,ART,1)),Y,71)  ;FINDEN ARTIKEL-EK + RÜSTKOSTEN ;find 
	IF +EK=0 SET %TXT(1)=" " QUIT  ;KEIN EK ;no Planned Cost
	
	SET VK=EK*((100+AUFSCHLAG)/100)
	
	;---------------------------------------
	;	D54		$$$INVORGRoundUpOfPrice()
	;	D202	$$$INVORGSaleCost()
	;---------------------------------------
	
	IF $PIECE($GET(^INVORG(YM,YM,1)),Y,202)=1 SET VK=EK  ;VK=EK;TYBD;2,1,2005
	SET RUNDUNG=+$PIECE($GET(^INVORG(YM,YM,1)),Y,54)
	IF RUNDUNG'=0 DO
	. SET VK=$JUSTIFY(VK,0,0)
	. IF RUNDUNG=2 IF VK>10   SET VK = $JUSTIFY(VK/10,0,0)*10     QUIT
	. IF RUNDUNG=4 IF VK>100  SET VK = $JUSTIFY(VK/100,0,0)*100   QUIT
	. IF RUNDUNG=5 IF VK>1000 SET VK = $JUSTIFY(VK/1000,0,0)*1000 QUIT
	
	SET %TXT(1)=$$^WWWZAHL(VK,0,2)
	;IF +VK=0 SET %TXT(1)=" "     FAN AUSSCHALTEN AM 15.01.2002
	QUIT
	
FORMAT(NR)
	;-------------------------------------------------------------------------------
	;	FAN FORMULAR INART, INARTL, INARTV, VERKAUFSPREIS  88,98,108,118
	;
	; Inputs : 
	;	NR		
	;
	; ByRef :
	;	YVAR
	; 
	; History :
	; 05-Feb-2008	GRF		Doco
	;-------------------------------------------------------------------------------
	QUIT:NR=""
	
	NEW VK,DIF
	SET %TXT(1)=""
	QUIT:$GET(YVAR)=""  ;KEIN ARTIKEL ;no item 
	
	DO ^INEKVK
	SET VK=%TXT(1)
	SET %TXT(1)="#Y"_YFORM_"D"_(NR-2)_"~"_$$^WWWZAHL(YINHALT,0,2)
	IF $DATA(YFELD) SET DIF=+$$^WWWTR(1,8,VK)-$PIECE(YFELD,Y,NR) IF DIF'<-.5 IF DIF'>.5 QUIT   ;KEINE ÄNDERUNG WENN DIFF WENIGER ALS .5 ;no alteration when minus when 
	SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D"_NR_"~"_VK 
	QUIT
]]></Routine>
</Export>