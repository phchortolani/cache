<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLAccountBalance" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLAccountBalance
	
#include COMSYS
#include FINConst
 
OnAfterDataFields()
	do ShowBalances(YPARA)
	quit
 

ShowBalances(pYPARA)
    ;-------------------------------------------------------------------------------
    ; Show Balances for Child Accounts.
    ;
    ; Returns:
    ;
    ; History:
    ; 12-May-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	new idAccountParent,idAccountType,idAccount,strAccountTypeDesc
	new objFINGLAccountTotal,fltAmount,objAcctAmountType,blnCurrencyType
	new fltTotal,strColour,strHeading
	
	quit:$get(pYPARA)=""
	
	set idAccountParent = $$$KEY1(pYPARA)
	set idAccountType   = $$$KEY2(pYPARA)
	set fltTotal        = 0
	
	if idAccountType'="" {
		set objAcctAmountType  = $get(^FINGLAccountAmountType(0,idAccountType,1))
		set strAccountTypeDesc = $$$FINGLAccountAmountTypeDescription(objAcctAmountType)
		set blnCurrencyType    = $$$FINGLAccountAmountTypeCurrencyType(objAcctAmountType)
		
		set strHeading = idAccountType_ " - "_strAccountTypeDesc
		do Start^COMTable($listbuild($$$Text($listbuild("Fin00317")),$$$Text($listbuild("Fin00232"))),strHeading)  ; "Account, Total"
		set idAccount = ""
		for {
			set idAccount = $order(^FINGLAccounts(0,50,idAccountParent,idAccount))   ; FIXME : $$$Index?
			quit:idAccount=""
			
			set objFINGLAccountTotal = $get(^FINGLAccountTotal(0,idAccount,idAccountType,1))
			set fltAmount = +$$$FINGLAccountTotalAmount(objFINGLAccountTotal)
			set fltTotal  = fltTotal+fltAmount
			do NewLine^COMTable()
			do InsertCell^COMTable(idAccount) ; Type
			if blnCurrencyType {
				do InsertCurrencyCell^COMTable(fltAmount)
			} else {
				do InsertCell^COMTable(fltAmount,,,,"Right")
			}
			do EL^WWWTAB
		}
		; Blank Line
		do NewLine^COMTable()
		do InsertCell^COMTable("")
		do InsertCell^COMTable("")
		do EL^WWWTAB
	
		set objFINGLAccountTotal = $get(^FINGLAccountTotal(0,idAccountParent,idAccountType,1))
		set fltAmount = +$$$FINGLAccountTotalAmount(objFINGLAccountTotal)
		if +fltAmount'=+fltTotal {
			set strColour = YRED
		} else {
			set strColour = ""
		}
		
		; Child Totals
		do NewLine^COMTable(strColour)
		do InsertCell^COMTable($$$Text($listbuild("Fin00232"))) ; Total
		if blnCurrencyType {
			do InsertCurrencyCell^COMTable(fltTotal)
		} else {
			do InsertCell^COMTable(fltTotal,,,,"Right")
		}
		do EL^WWWTAB
	
		; Parent Total
		do NewLine^COMTable(strColour)
		do InsertCell^COMTable($$$Text($listbuild("Fin00232"))_" "_idAccountParent) ; Total
		if blnCurrencyType {
			do InsertCurrencyCell^COMTable(fltTotal)
		} else {
			do InsertCell^COMTable(fltAmount,,,,"Right")
		}
		do EL^WWWTAB
	
		do Stop^COMTable()
	}
	quit
	
]]></Routine>
</Export>