<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFBERECHT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFBERECHT(AUF1)
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PRÜFEN AUFTRAGBERECHTIGUNG
	;		Check Order authorisation
	;
	;	do VOR^INAUFBERECHT       ;create variables ACCAL,ACCRE,ACCKD... VORBEREITEN FÜR BERECHTUNGEN
	;	$$^INAUFBERECHT(AUF1)     ;return boolean Authorising order access =1 BERECHTIGUNG             
	;
	; Called By :
	; 	INAUFBANZBIT, INAUFNEU1, INAUFNEU2, INAUFPTSN, INAUFSPERR, INAUFSUCH1
	; 	INAUFTEILELIST, INDRPRUN5, INNACHKALK, INPROPLANDR, INSTAUMPJ, INSTAUMPJA
	; 	INTIMEAUFP, INUEBER, INVKWKSUCH, INWEAUFVORAP, INWEAUFVORAPM7, INWF50
	; 	
	; 
	; Inputs : 
	;	AUF1		objOrder
	;
	; ByRef : (Creates with call to VOR if not present)
	;	ACCKD
	;	ACCAL
	;	ACCVK
	;	ACCEI
	;	ACCLE
	;	ACCRE
	;	
	; Returns :
	;	ACC		Authorised $$$YES/$$$NO
	;
	; History :
	; 13-Nov-2007	GRF		SR15598 : Doco; Boolean macros; braces
	; 20.11.2003	FAN		Created
	;-------------------------------------------------------------------------------
	SET ACC  = $$$NO
	SET AUF1 = $GET(AUF1)
	QUIT:AUF1="" ACC
	
	;---------------------------------------
	; If for some strange reason we pass the
	; order key rather than the order record,
	;     get the order record
	;---------------------------------------
	if '$FIND(AUF1,Y) if $DATA(^INAUF(YM,AUF1,1)) SET AUF1=$GET(^INAUF(YM,AUF1,1))  ;15.12.03;FIS;WENN NUR KEY
	
	; SR15598 vvv
	if '$DATA(ACCKD) || '$DATA(ACCAL) || '$DATA(ACCVK) || '$DATA(ACCEI) || '$DATA(ACCLE) || '$DATA(ACCRE) {
		NEW ACCVK,ACCAL,ACCKD,ACCEI,ACCLE,ACCRE
		DO VOR  ;FIS;15.12.03;GGF. NEU SETZEN ;recent typeset
	}
	
	set strOrderType        = $$$INAUFOrderType(AUF1)                  ; D2
	set idResponsible       = $$$INAUFResponsible(AUF1)                ; D14
	set blnComplaintOrSpare = +$$$INAUFComplaintSparePartOrder(AUF1)   ; D169
	if strOrderType="" set strOrderType = 0
	
	if ACCAL = $$$YES {
		if ACCKD = $$$YES {                           set ACC = $$$YES ;ALLE
		} else {
			if idResponsible=YBED                     set ACC = $$$YES ;BEDIENER IST ZUSTÄNDIGER MITARBEITER
			if blnComplaintOrSpare = $$$NO {
				if (strOrderType=0) && (ACCKD=$$$YES) set ACC = $$$YES ;KUNDE AUFTRAG ;customer order 
				if (strOrderType=1) && (ACCEI=$$$YES) set ACC = $$$YES ;EIGENE AUFTRAG ;order 
				if (strOrderType=2) && (ACCLE=$$$YES) set ACC = $$$YES ;LIEFERRANTEN-BESTELLUNGEN
			} else {
				IF ACCRE=$$$YES                       set ACC = $$$YES ;REKLAMATIONEN
			}
		}
		
	} else {
		if idResponsible=YBED                         set ACC = $$$YES ;BEDIENER IST ZUSTÄNDIGER MITARBEITER
		if blnComplaintOrSpare = $$$NO {
			if (strOrderType=0) && (ACCKD=$$$YES)     set ACC = $$$YES ;KUNDE AUFTRAG ;customer order 
			if (strOrderType=1) && (ACCEI=$$$YES)     set ACC = $$$YES ;EIGENE AUFTRAG ;order 
			if (strOrderType=2) && (ACCLE=$$$YES)     set ACC = $$$YES ;LIEFERRANTEN-BESTELLUNGEN
		} else {
			IF ACCRE=$$$YES                           set ACC = $$$YES ;REKLAMATIONEN
		}
	}	
	/*
	IF '$DATA(ACCKD) || '$DATA(ACCAL) || '$DATA(ACCVK) || '$DATA(ACCEI) || '$DATA(ACCLE) || '$DATA(ACCRE) NEW ACCVK,ACCAL,ACCKD,ACCEI,ACCLE,ACCRE DO VOR  ;FIS;15.12.03;GGF. NEU SETZEN ;recent typeset 
	
	IF ACCAL=1 IF ACCKD=1 SET ACC = $$$YES                     ;ALLE
	IF ACCAL=1 IF ACCKD'=1 DO
	. IF $PIECE(AUF1,Y,14)=YBED SET ACC = $$$YES    ;BEDIENER IST ZUSTÄNDIGER MITARBEITER
	. IF +$PIECE(AUF1,Y,169)'=0 IF ACCRE=1 SET ACC = $$$YES ;REKLAMATIONEN
	. IF +$PIECE(AUF1,Y,169)=0 DO
	. . IF $PIECE(AUF1,Y,2)="" SET $PIECE(AUF1,Y,2)=0
	. . IF $PIECE(AUF1,Y,2)=0 IF ACCKD=1 SET ACC = $$$YES    ;KUNDE AUFTRAG ;customer order 
	. . IF $PIECE(AUF1,Y,2)=1 IF ACCEI=1 SET ACC = $$$YES    ;EIGENE AUFTRAG ;order 
	. . IF $PIECE(AUF1,Y,2)=2 IF ACCLE=1 SET ACC = $$$YES    ;LIEFERRANTEN-BESTELLUNGEN
	
	IF ACCAL'=1 DO
	. IF $PIECE(AUF1,Y,14)=YBED SET ACC = $$$YES    ;BEDIENER IST ZUSTÄNDIGER MITARBEITER
	. IF +$PIECE(AUF1,Y,169)'=0 IF ACCRE=1 SET ACC = $$$YES ;REKLAMATIONEN
	. IF +$PIECE(AUF1,Y,169)=0 DO
	. . IF $PIECE(AUF1,Y,2)="" SET $PIECE(AUF1,Y,2)=0
	. . IF $PIECE(AUF1,Y,2)=0 IF ACCKD=1 SET ACC=1    ;KUNDE AUFTRAG ;customer order 
	. . IF $PIECE(AUF1,Y,2)=1 IF ACCEI=1 SET ACC=1    ;EIGENE AUFTRAG ;order 
	. . IF $PIECE(AUF1,Y,2)=2 IF ACCLE=1 SET ACC=1    ;LIEFERRANTEN-BESTELLUNGEN
	*/  ; SR15586 ^^^
	
	QUIT ACC
	
VOR
	;-------------------------------------------------------------------------------
	;		VORBEREITEN		BERECHTIGUNGEN
	;		Check Access Approval
	;		
	; Called By :
	; 	INAUFBERECHT
	; 	INAUFBANZBIT, INAUFNEU1, INAUFNEU2, INAUFPTSN, INAUFSPERR, INAUFSUCH1
	; 	INAUFTEILELIST, INSTAUMPJ, INSTAUMPJA, INTIMEAUFP, INUEBER, INVKWKSUCH, INWF50
	; 	
	; Returns : (byRef) boolean flags
	; 	ACCVK	Order Processing With Sales Prices
	; 	ACCAL	New Orders
	; 	ACCKD	Customer Orders / Order Processing With Customer Info.
	; 	ACCEI	Manufacturing Orders
	; 	ACCLE	Suppliers Orders
	; 	ACCRE	Replacements
	; 
	; History :
	; 15.12.03	FIS		NUR WENN GENERELLE BERECHTIGUNG FÜR KUNDENAUFTRÄGE (Override
	; 					Cust Order unless already $$$NO)
	;-------------------------------------------------------------------------------
	
	;---------------------------------------
	;	D22		$$$INVORGOrderProcessingWithSalesP()  [SalesPrices]
	;	D35		$$$INVORGOrderProcessingWithCustom()  [Customer Information]
	;	
	;	D23		$$$WWW124Module1()
	;---------------------------------------
	SET ACCVK = $$^WWWACCESS("",$PIECE($GET(^INVORG(YM,YM,1)),Y,22))                        ;BERECHTIGUNGEN VON VK-PREISE
	SET ACCAL = $$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INAUFBANZ",SPRACHE,3,1)),Y,23))      ;BERECHTIGUNGEN VON ALLE AUFTRAG ;order 
	
	SET ACCKD = $$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INAUFBANZ",SPRACHE,4,1)),Y,23))      ;BERECHTIGUNGEN VON KUNDE AUFTRAG ;customer order 
	IF ACCKD=$$$YES SET ACCKD = $$^WWWACCESS("",$PIECE($GET(^INVORG(YM,YM,1)),Y,35))        ;BERECHTIGUNGEN VON KUNDENDATEN
	
	SET ACCEI = $$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INAUFBANZ",SPRACHE,5,1)),Y,23))      ;BERECHTIGUNGEN VON EIGEN AUFTRAG ;personal order 
	SET ACCLE = $$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INAUFBANZ",SPRACHE,6,1)),Y,23))      ;BERECHTIGUNGEN VON LIEF AUFTRAG ;order 
	SET ACCRE = $$^WWWACCESS("",$PIECE($GET(^WWW124(0,"INAUFBANZ",SPRACHE,7,1)),Y,23))      ;BERECHTIGUNGEN VON RE AUFTRAG ;order 
	quit
	
]]></Routine>
</Export>