<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INStocktakeAdd" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[

INStocktakeAdd
	
#include INConst
	
Search()
	;-------------------------------------------------------------------------------
	; Allows the user to search for items to add to a stocktake.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-May-2011	shobby	SR17559: Created
	;-------------------------------------------------------------------------------
	do Initialise^COMViewCustom("INART","Selected^INStocktakeAdd",$$$NO) ;Close popup after load all lines	
	do Start^COMViewCustom()
	quit $$$OK
	
	
Selected(pobjResultSet)
	;-------------------------------------------------------------------------------
	; Adds the selected lines to the grid, replacing final blank lines
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 21-Jun-2011	shobby	SR17559: Created
	;-------------------------------------------------------------------------------
	new arrGrid,blnUpdate,idINART,idINWEINVPLine,idLast
	new objINWEINVPLine,YFELD,YKEY,YFORM
	
	set YFORM = "INWEINVPLine"
	set YKEY  = $$$GRIDYKEYContainer
	set YFELD = $$GetYFELDContainer^COMGridEdit31G()
	
	do GetGridContents^COMGridEdit31G(.arrGrid,YFORM)
	
	set idLast = ""
	set idINWEINVPLine = ""
	for {
		set idINWEINVPLine = $order(arrGrid(YKEY,idINWEINVPLine),-1)
		quit:idINWEINVPLine=""
		quit:$$$INWEINVPLineItem(arrGrid(YKEY,idINWEINVPLine))'=""
		
		set idLast = idINWEINVPLine
	}
	
	set blnUpdate = $$$NO
	
	while pobjResultSet.Next() {
		set idINART = $$GetID^COMViewCustom(pobjResultSet)
		
		set objINWEINVPLine = ""
		set $$$INWEINVPLineItem(objINWEINVPLine) = idINART
		if idLast="" {
			if blnUpdate {
				do SetGridContents^COMGridEdit31G(.arrGrid,1,YFORM)
				set blnUpdate = $$$NO
			}
			do AddLine^COMGridEdit31Add(objINWEINVPLine,,,,,,,$$$YES)
		
		} else {
			set arrGrid(YKEY,idLast) = objINWEINVPLine
			set blnUpdate = $$$YES
			set idLast    = $order(arrGrid(YKEY,idLast))
		}
	}
	if blnUpdate do SetGridContents^COMGridEdit31G(.arrGrid,1,YFORM)
	quit
	
]]></Routine>
</Export>