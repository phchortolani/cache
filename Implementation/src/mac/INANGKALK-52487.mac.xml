<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INANGKALK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INANGKALK(YKEY)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PREISE NEU HOLEN AUS ARTIKELSTAMM
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 05-Sep-2006	GRF		Naked References
	; 30-Aug-2006	GRF		SR12027: simplify -negative as +; YCR; quits
	; 24-Jun-2005	GRF		Clarify
	; 09.04.2001	FAN
	;-------------------------------------------------------------------------------
	NEW POS,ART,LIEF,YFELDALT,YFELDNEU,KEY,ANZM,ABBRUCH
	
	SET YDDSATZ=0
	SET ANZM=0
	
	;ZURÜCK BUTTEN ;retro- 
	SET $PIECE(YVOR,Y,24)=1
	DO ^WWWBACK ;ZURUECK
	
	;DRUCKEN TABELLE ;print tabulation 
	;ÜBERSCHRIFT BEARBEITEN ;superscription 
	SET UES(1)=$$^WWWTEXT(31406)_Y_$$^WWWTEXT(32024)_Y_$$^WWWTEXT(32326)_"&nbsp;"_$$^WWWTEXT(32467)_"&nbsp;/&nbsp;"_$$^WWWTEXT(32468)_Y_$$^WWWTEXT(32327)_"&nbsp;"_$$^WWWTEXT(32467)_"&nbsp;/&nbsp;"_$$^WWWTEXT(32468)_Y_$$^WWWTEXT(32328)_"&nbsp;"_$$^WWWTEXT(32467)_"&nbsp;/&nbsp;"_$$^WWWTEXT(32468)_Y_$$^WWWTEXT(32329)_"&nbsp;"_$$^WWWTEXT(32467)_"&nbsp;/&nbsp;"_$$^WWWTEXT(32468)
	;                POS                  ARTIKEL            HERSTELLKOSTEN    -         ALT                             NEU                EINZEL VK---                   ALT                                 NEU                GESAMT VK---                   ALT                        NEU          NOTTO GESAMT VK---                       ALT                             NEU
	
	DO UPUEB  ;DRUCKEN ÜBERSCHRIFT ;print superscription 
	
	SET YKEY=$GET(YKEY)
	IF YKEY'="" SET POS="" FOR  SET POS=$ORDER(^INANGP(YM,YKEY,POS)) QUIT:POS=""  DO
	. IF $PIECE($GET(^INANGP(YM,YKEY,POS,1)),Y,199)=1 SET ABBRUCH(POS)="" QUIT   ;KEINE AUTOMATISCHE PREISRECHNUNG ;no 
	. SET ART=$PIECE($GET(^INANGP(YM,YKEY,POS,1)),Y,4)               ;ARTIKEL NUMMER ;item numeral 
	. IF ART=""  QUIT  ;DO ^WWWINFO($$^WWWTEXT(46)) QUIT             ;keine Daten vorhanden
	. ; 
	. ; FIXME : call $$^INQTYUNIT(ART) here once and use variable below <GRF>
	. ; 
	. SET LIEF=$PIECE($GET(^INANGP(YM,YKEY,POS,1)),Y,12)             ;LIEF NUMMER ;numeral 
	. IF LIEF="" SET LIEF=$ORDER(^INARTK(YM,ART,LIEF))
	. SET YFELD   =$GET(^INART(YM,ART,1))              ;ARTIKEL STAMMDATEN ;item master information 
	. SET YFELDALT=$GET(^INANGP(YM,YKEY,POS,1))        ;ANGEBOT ALT DATEN  ;quote old date 
	. ;
	. ;--------ANGEBOTSPOSITION  LIEFERANTENKONDITION-----EK
	. IF LIEF'="" IF $DATA(^INARTK(YM,ART,LIEF,1)) DO 
	. . SET ^INANGPK(YM,YKEY,POS,1)=$GET(^INARTK(YM,ART,LIEF,1))    ;ARTIKEL LIEFERANTENKONDITION ;item 
	. . SET $PIECE(^INANGPK(YM,YKEY,POS,1),Y,12)=$PIECE(^INANGPK(YM,YKEY,POS,1),Y,12)*$PIECE(YFELDALT,Y,5)/$$^INQTYUNIT(ART)  ;BASIS-EK*MENGE ; 05-Sep-2006
	. . SET $PIECE(^INANGPK(YM,YKEY,POS,1),Y,13)=$PIECE(^INANGPK(YM,YKEY,POS,1),Y,13)*$PIECE(YFELDALT,Y,5)/$$^INQTYUNIT(ART)  ;NETTO-EK*MENGE ; 05-Sep-2006
	. ;
	. ;--------ANGEBOTSPOSITION-----VK
	. SET $PIECE(YFELD,Y,88)=$$^INSALESPRICE(ART,,$PIECE($GET(^INANG(YM,YKEY,1)),Y,6),$PIECE(YFELD,Y,90),$PIECE($GET(^INANG(YM,YKEY,1)),Y,1))  ;FIS;03.06.03;23477;PREISE NEU AUS INSALESPRICE
	. SET $PIECE(^INANGP(YM,YKEY,POS,1),Y,118)=$PIECE(YFELD,Y,88)        ;EINZEL-VK
	. SET $PIECE(^INANGP(YM,YKEY,POS,1),Y,119)=$JUSTIFY($PIECE(YFELD,Y,88)*$PIECE(YFELDALT,Y,5)/$$^INQTYUNIT(ART),0,2)   ;EINZEL-VK*MENGE=GESAMT-VK
	. SET $PIECE(^INANGP(YM,YKEY,POS,1),Y,47) =$$^INANGKOST(YKEY,POS)        ;HERSTELLKOSTEN
	. SET NETTO=$PIECE(^INANGP(YM,YKEY,POS,1),Y,119)                         ;ERRECHNEN NETTO PREIS ;price 
	. IF +$PIECE(YFELDALT,Y,121)'=0 SET NETTO=NETTO-(NETTO/100*$PIECE(YFELDALT,Y,121))
	. IF +$PIECE(YFELDALT,Y,122)'=0 SET NETTO=NETTO-(NETTO/100*$PIECE(YFELDALT,Y,122))
	. IF +$PIECE(YFELDALT,Y,128)'=0 SET NETTO=NETTO-(NETTO/100*$PIECE(YFELDALT,Y,128))
	. IF +$PIECE(YFELDALT,Y,212)'=0 SET NETTO=NETTO-(NETTO/100*$PIECE(YFELDALT,Y,212))       ;TYBD;19.2.2005;27377;GEM
	. IF +$PIECE(YFELDALT,Y,214)'=0 SET NETTO=NETTO+(NETTO/100*$PIECE(YFELDALT,Y,214))       ;TYBD;19.2.2005;27377;GEM ; 30-Aug-2006
	. SET $PIECE(^INANGP(YM,YKEY,POS,1),Y,123)=$JUSTIFY(NETTO,0,2)
	. ; ERREICHTER AUFSCHLAG ;overcharge 
	. IF +$PIECE(YFELD,Y,47)'=0 SET $PIECE(^INANGP(YM,YKEY,POS,1),Y,116)=$JUSTIFY(NETTO/($PIECE(^INANGP(YM,YKEY,POS,1),Y,47)+($PIECE(^INANGP(YM,YKEY,POS,1),Y,71)*$$^INPROLOS(ART,$PIECE(YFELDALT,Y,5))))-1*100,0,2)  
	. SET $PIECE(^INANGP(YM,YKEY,POS,1),Y,79)=+$HOROLOG       ;ANGEBOT DATUM     ;quote Date 
	. SET YFELDNEU=$GET(^INANGP(YM,YKEY,POS,1))               ;ANGEBOT NEU DATEN ;quote recent 
	. SET KEY=YKEY_","_POS 
	. ;
	. DO
	. . NEW YKEY
	. . ;ANFANGEN TABELLE. ;commence 
	. . SET ANZM=ANZM+1
	. . WRITE YCR
	. . DO NL^WWWTAB
	. . DO NF^WWWTAB
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INANGP&YKEY="_KEY ;LINK SETZEN---------ANGEBOTPOSITION
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""  ;ATIKELBEARBEITEN: ARTIKEL BEZEICHNUNG ;item notation 
	. . WRITE ">"
	. . WRITE POS
	. . WRITE "&nbsp;"
	. . DO ZWL^WWWTAB
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INANGP&YKEY="_KEY  ;LINK SETZEN---------ART
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_":&nbsp;"_$PIECE($GET(^INART(YM,ART,1)),Y,1)_""""  ;DATENSATZ AUSWÄHLEN
	. . WRITE ">"
	. . WRITE ART
	. . WRITE "&nbsp;"
	. . DO ZWR^WWWTAB
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INANGP&YKEY="_KEY  ;LINK SETZEN---------HERSTELLENKOST ALT/NEU
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""  ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. . WRITE ">"
	. . WRITE $$^WWWZAHL($PIECE(YFELDALT,Y,47),0,2,YWHR)_"&nbsp;/&nbsp;"_$$^WWWZAHL($PIECE(YFELDNEU,Y,47),0,2,YWHR)
	. . WRITE "&nbsp;"
	. . DO ZWR^WWWTAB
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INANGP&YKEY="_KEY  ;LINK SETZEN---------EINZEL VK ALT/NEU
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""  ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. . WRITE ">"
	. . WRITE $$^WWWZAHL($PIECE(YFELDALT,Y,118),0,2,YWHR)_"&nbsp;/&nbsp;"_$$^WWWZAHL($PIECE(YFELDNEU,Y,118),0,2,YWHR)
	. . WRITE "&nbsp;"
	. . DO ZWR^WWWTAB
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INANGP&YKEY="_KEY  ;LINK SETZEN---------GESAMT VK ALT/NEU
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""  ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. . WRITE ">"
	. . WRITE $$^WWWZAHL($PIECE(YFELDALT,Y,119),0,2,YWHR)_"&nbsp;/&nbsp;"_$$^WWWZAHL($PIECE(YFELDNEU,Y,119),0,2,YWHR)
	. . WRITE "&nbsp;"
	. . DO ZWR^WWWTAB
	. . WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INANGP&YKEY="_KEY  ;LINK SETZEN---------NETTO VK ALT/NEU
	. . DO ^WWWCGI
	. . WRITE """"
	. . WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""  ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. . WRITE ">"
	. . WRITE $$^WWWZAHL($PIECE(YFELDALT,Y,123),0,2,YWHR)_"&nbsp;/&nbsp;"_$$^WWWZAHL($PIECE(YFELDNEU,Y,123),0,2,YWHR)
	. . WRITE "&nbsp;"
	. . DO EF^WWWTAB
	. . DO EL^WWWTAB
	
	;DO ^WWWINFO($$^WWWTEXT(30007))
	IF ANZM=0 DO  ;BITTE PRÜFEN ;please sift 
	. NEW YI
	. DO NL^WWWTAB
	. FOR YI=1:1:6 DO
	. . DO NF^WWWTAB
	. . WRITE "&nbsp;"
	. . DO EF^WWWTAB
	. DO EL^WWWTAB
	
	DO STOP^WWWTAB  ;ENDE TABELLE ;termination tabulation 
	
	IF ANZM=0!($DATA(ABBRUCH)) DO
	. WRITE "<H3>"
	. IF $DATA(ABBRUCH) DO
	. . NEW XPOS
	. . WRITE $$^WWWTEXT(216)_" "  ;POSITION
	. . SET XPOS=""
	. . FOR  SET XPOS=$ORDER(ABBRUCH(XPOS)) QUIT:XPOS=""  WRITE XPOS  WRITE:$ORDER(ABBRUCH(XPOS))'="" " + "   ;ABGEBRUCHENE POSITION
	. ;
	. WRITE " "_$$^WWWTEXT(32292)_"</H3>"   ;BITTE PRÜFEN ;please sift 
	. WRITE "<BR>"
	
	IF ANZM'=0 WRITE "<H3>"_$$^WWWTEXT(30007)_"</H3>"   ;üBERTAGUNG ABGESCHLOSSEN!
	QUIT
	
UPUEB ;ÜBERSCHRIFT ;superscription 
	
	WRITE "<CENTER>"
	DO START00^WWWTAB
	DO NL^WWWTAB
	DO NHWO^WWWTAB
	WRITE "<FONT SIZE=3>"
	WRITE "<B>"
	WRITE "<NOBR>"
	WRITE "Preis neu übertragen"
	WRITE "</NOBR>"
	WRITE "</B>"
	DO EH^WWWTAB
	DO EL^WWWTAB
	DO STOP^WWWTAB
	WRITE "<BR>"
	WRITE "</CENTER>"
	
	DO START100^WWWTAB  ;STARTEN DER ÜBERSCHRIFT ;launching the superscription 
	WRITE "<THEAD>" 
	DO NL^WWWTAB
	FOR I=1:1 QUIT:$PIECE(UES(1),Y,I,99)=""  DO NHW^WWWTAB WRITE $PIECE(UES(1),Y,I),"&nbsp;" DO EH^WWWTAB
	DO EL^WWWTAB
	WRITE "</THEAD>"
	
	QUIT
	
]]></Routine>
</Export>