<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM4" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORM4
	
#include COMSYS
	
#define LogR(%1,%2) 	;
#define LogR2(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORM4("_%2_") : "_$zh $$$JournalOn
#;define LogR2(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORM4("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Display Grid Fields         ;ANZEIGEN GRIDFELDER
	; 
	; NOTE: Manual fields are not displayed properly
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 27-Jul-2007	shobby	SRBR014586: #include COMSYS for $$$Index
	; 08-Jun-2007	GRF		SR15542: Naked Reference; Expand commands; Doco; quits;
	;							missed </FONT>
	; 21.08.1997	DT
	;-------------------------------------------------------------------------------
	new YKEY1,YLFN,YA,YI,YDATEI1,YMAXKEY,YMAL,YSEI,YZEILE,YSPL,SCHLUESSEL,YFARBCODE,YMAXNUM    ;,YSORT,YORIENT
	
	$$$LogR("",YFORM)
	
	write YCR,YCR,"<!-- ************************* GRID DATENFELDER (WWWFORM4)************************** -->",YCR,YCR
	
	set YSORT   = +$get(%(YQUERY,"YSORT"))
	set YORIENT = +$get(%(YQUERY,"YORIENT"))
	set YFOART1 = 3
	
	if YDATEI="" set YDATEI=$piece($get(^WWW120(0,YFORM,1)),Y,11)
	quit:YDATEI=""
	
	set YMAXKEY=+$order(^WWW002(0,YDATEI,""),-1)
	if YMAXKEY=0 set YMAXKEY=1
	set YGRIKEY=YMAXKEY
	
	; Fill table with blank lines when no entries (?) & quit         D144	$$$WWW102DoNotShowEmptyLinesInList() 
	;---------------------------------------
	if YGRIKEY'=1 if YKEY="" do  quit
	. quit:$piece($get(^WWW012(0,0,1)),Y,144)=$$$YES
	. set YDDSATZ=0
	. for YI=1:1:20 do ANZEIGE1
	 
	set YDATEI1 = YDATEI
	;ZUSAMMENSTELLEN DES FESTEN KEYS (IMMER EINEN WENIGER)
	set YKEY1 = ""
	set YA    = "^"_YDATEI_"("_$$^WWWYM(YDATEI,0)
	set YDATA = $get(^WWW001(0,YDATEI,1))
	if $piece(YDATA,Y,12)'="" if $piece(YDATA,Y,13)'="" do  ;UCI and VOL
	. quit:$find(YA,"^[")
	. set YA = "^["_""""_$piece(YDATA,Y,12)_""""_","_""""_$piece(YDATA,Y,13)_""""_"]"_$piece(YA,"^",2,999)
	
	set YA(3)=YA
	if YKEY'="" for YI=1:1:YMAXKEY quit:$piece(YKEY,",",YI)=""  do
	. quit:YI=YGRIKEY
	. set YA(99)=$translate($piece(YKEY,",",YI),"""")  ;FEHLER IN DATUMSBEARBEITUNG
	. if $piece($get(^WWW002(0,YDATEI,YI,1)),Y,3)=1 set YA(99)=$$^WWWDATE1(YA(99))
	. set YA=YA_","_""""_YA(99)_""""
	. set YKEY1=YKEY1_YA(99)
	. if YI'=(YGRIKEY-1) if $piece(YKEY,",",YI+1)'="" set YKEY1=YKEY1_","
	
	;VORBEREITEN SUCHFUNKTION ;prepare 
	;YDATEI = DATEINAME ;file name 
	;YFKEY  = FESTE PRIMÄRSCHLÜSSEL
	;YAUSW  = VARIABLE PRIMÄRSCHLÜSSEL (AB KEY) ODER SORT AB ;Or Confirm. 
	;YRICHT = 0 UP ; 1 DOWN
	;YSORT  = SORTIERSCHLÜSSEL
	;YANZ   = ANZAHL DER GESUCHTEN WERTE ;Number the 
	;YKOMP  = 1 = KOMPLETTER KEY; 2 LETZTER KEY FEHLT(GRID-SUCHE)
	;YFIND  = SATZ IN FORM YFELD, WENN $F DER SAETZE->DANN STOP
	;YFFKY  = SATZ IN FORM YKEY, WENN $F DER SAETZE->DANN STOP
	
	set YMAXNUM = 2000
	if YFORM="WWW009" set YMAXNUM=""  ; no limit for language text
	
	do ^WWWSOR(YDATEI,YKEY1,"",YORIENT,YSORT,YMAXNUM,1)
	set YDATA = $get(^WWW001(0,YDATEI,1))
	
	;EINZELWERTE ANZEIGEN  ;display 
	new YKEY,YDDSATZ
	
	set YDDSATZ = 0
	set YA(2)   = ""
	for  set YA(2) = $order(^WWWSOR(YUSER,"KEY",YA(2))) quit:YA(2)=""  do
	. set YKEY=""
	. for  set YKEY = $order(^WWWSOR(YUSER,"KEY",YA(2),YKEY)) quit:YKEY=""  do
	. . ;ZUSAMMENBAUEN DES SCHLÜSSELS
	. . set YFELD=""
	. . if YDATEI'="" do
	. . . set MAXYKEY = +$order(^WWW002(0,YDATEI,""),-1)
	. . . if MAXYKEY=0 set MAXYKEY=1
	. . . set ERSTFELD = $order(^WWW003(0,YDATEI,""))
	. . . set FELDKEY  = ""
	. . . if ERSTFELD'="" set FELDKEY = $piece(^WWW003(0,YDATEI,ERSTFELD,1),Y,11) ;FUER MIT ODER OHNE ,1) ZUM SCHLUESSEL
	. . . if MAXYKEY'=0 do
	. . . . set Q = 0
	. . . . set SCHLUESSEL = "^"_YDATEI_"("_$$^WWWYM(YDATEI,1)
	. . . . set YDATA      = $get(^WWW001(0,YDATEI,1))
	. . . . if $piece(YDATA,Y,12)'="" if $piece(YDATA,Y,13)'="" do  ;UCI and VOL 
	. . . . . quit:$find(SCHLUESSEL,"^[")
	. . . . . set SCHLUESSEL = "^["_""""_$piece(YDATA,Y,12)_""""_","_""""_$piece(YDATA,Y,13)_""""_"]"_$piece(SCHLUESSEL,"^",2,999)
	. . . . ;
	. . . . for I=1:1:MAXYKEY set XYKEY=$translate($piece(YKEY,",",I),"""") set SCHLUESSEL=SCHLUESSEL_""""_XYKEY_"""" set:I<MAXYKEY SCHLUESSEL=SCHLUESSEL_"," if XYKEY="" set Q=1
	. . . . if $piece(YDATA,Y,8)=4  set SCHLUESSEL = SCHLUESSEL_$select(FELDKEY'="":","_FELDKEY,1:"")_")"
	. . . . if $piece(YDATA,Y,8)'=4 set SCHLUESSEL = SCHLUESSEL_",1)"
	. . . . set YKEY1 = $translate($piece($piece(SCHLUESSEL,",",2,99),",1)",1),"""")
	. . . . if $piece(YDATA,Y,8)=4 set YKEY1 = $translate($piece($piece(SCHLUESSEL,"(",2),")",1),"""")
	. . . . quit:YKEY1=""
	. . . . quit:$find(YKEY1,")")
	. . . . if Q=0 set YFELD = $$^WWWSETL(SCHLUESSEL)
	. . ;
	. . do ANZEIGE  ;DATEN ANZEIGEN ;display 
	
	; Fill table with blank lines if insufficient records	D144	$$$WWW012DoNotShowEmptyLinesInList()
	;---------------------------------------
	if YDDSATZ<20 do
	. quit:$piece($get(^WWW012(0,0,1)),Y,144)=$$$YES
	. new YI
	. set YI = YDDSATZ
	. for YI=YI:1:20 do ANZEIGE1
	
	kill ^WWWSOR(YUSER,"KEY")
	
	quit
	
	
ANZEIGE ;DER GRIDFELDER ;the 
	;-------------------------------------------------------------------------------
	; History:
	; 10-Nov-2010	GRF		SR17243: Clear temp global before and after running; if
	;							an OnBeforeFormat tag exists on the form's routine
	;							execute the code and act on the changes.
	; 26-Feb-2010	GRF		SR17119.1: use strBGColour & strSetFont to improve
	; 							performance
	; 13-Jan-2010	shobby	SR17119: Just enough work to get Manual Fields working
	; 							on grid forms (but not for all data types).  At this
	; 							time there are only two such fields on INARTPACK
	; 27-Jul-2007	shobby	SRBR014615:	only call $$^WWWUMLAU once (idForm)
	; 19-Jul-2007	shobby	SRBR014615: Subroutined NoAccess call to make sure all
	; 							customisations are considered
    ; 28-Oct-2005	JW		SR13074: 	Exchange Rate type
	;-------------------------------------------------------------------------------
	new idForm,strBGColour,strBGOverride,strSetFont
	
	$$$LogR2("ANZEIGE",$get(YKEY)_"<"_$get(YKEY1))
	
	kill ^CacheTempWWWFORM4(YUSER,"OnBeforeFormat")    ; SR17243
	
	set YDDSATZ = YDDSATZ+1
	set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)  ; SR17119.1 vvv
	
	if $piece(YVOR,Y,7)="" {
		set strSetFont = "<FONT>"
	} else {
		set strSetFont = "<FONT SIZE="""_$piece(YVOR,Y,7)_""">"
	}                                                                ; SR17119.1 ^^^
	
	write YCR,"<TR>"
	
	; Display Last Key as first column
	; (WWWFORM3 4a.)
	;---------------------------------------
	set YLFN = YMAXKEY
	do
	. write "<TD VALIGN=TOP NOWRAP"
	. set YTYP = $piece($get(^WWW002(0,YDATEI,YLFN,1)),Y,3)
	. if (YTYP=4) || (YTYP=8) || (YTYP=12) || (YTYP=18) write " ALIGN=RIGHT"
	. ;
	. do OnBeforeFormat(YFORM,YKEY,YFELD,"P",YLFN,.strBGOverride)    ; SR17243 vvv
	. write $select($get(strBGOverride)="":strBGColour,1:" BGCOLOR="""_strBGOverride_"""")
	. write ">"_strSetFont
	. ;write strBGColour_">"_strSetFont      ; SR17119.1             ; SR17243 ^^^
	. ;
	. if ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) && ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumCreateOnly) do
	. . new YKEY
	. . write "<A"
	. .;write " TITLE="_""""_$$^WWWTEXT(374)_""""          ; "Select Data Record"  ; SR17243
	. . write " TITLE="_""""_$select($get(YTOOLTIP)="":$$^WWWTEXT(374),1:YTOOLTIP)_""""   ; "Select Data Record"
	. . write " HREF="_""""_YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YSORT="_$get(YSORT)_"&amp;YORIENT="_$get(YORIENT)
	. . set YKEY = $translate(YKEY1,"""")
	. . do ^WWWCGI
	. . write """"_">"
	. ;
	. set YINHALT = $piece(YKEY1,",",YLFN)
	. ;
	. set YBBN    = YLFN
	. set YINHALT = $$^WWWFORMAT(YDATEI,"P",YLFN,YINHALT)
	. ;
	. write $$^WWWUML(YINHALT)
	. if YINHALT=" " write "[space]"
	. if $get(YINHALTK)'="" write " ("_$$^WWWUML(YINHALTK)_")"
	. if ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) && ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumCreateOnly) write "</A>"
	. ;
	. write "</FONT></TD>",YCR      ; SR15542
	
	;---------------------------------------
	; Process form fields in display sequence order (don't include hidden fields)
	; (WWWFORM3 4b.)
	; 
	; INDEX 3 : Form / Tab / Row / Column : idForm / idFldNo
	;---------------------------------------
	set idForm = $$$Index(YFORM)
	set YSEI = ""
	for  set YSEI = $order(^WWW122s(0,3,idForm,YSEI)) quit:YSEI=""  do
	. set YZEILE = ""
	. for  set YZEILE = $order(^WWW122s(0,3,idForm,YSEI,YZEILE)) quit:YZEILE=""  do
	. . set YSPL = ""
	. . for  set YSPL = $order(^WWW122s(0,3,idForm,YSEI,YZEILE,YSPL)) quit:YSPL=""  do
	. . . set YBBN = ""
	. . . for  set YBBN = $order(^WWW122s(0,3,idForm,YSEI,YZEILE,YSPL,YFORM,YBBN)) quit:YBBN=""  do
	. . . . quit:$$NoAccess^WWWFORMCOMMON(YFORM,YDATEI,YBBN,.YTXT,.YLFN,"",$get(YOPTION),$get(YOPTION1),$get(YPARA1))
	. . . . ;
	. . . . ; TODO (shobby) YTYP should be obtained from YTXT to take in to account form settings.
	. . . . ;
	. . . . $$$LogRx(YFORM_"-"_YBBN_":3:"_$$$WWW122DefaultVariableInput(YTXT)_"<"_YLFN_"<")
	. . . . ;
	. . . . if YLFN="" do
	. . . . . set YTYP    = $$$WWW122InputType(YTXT)                                                     ;SR17119
	. . . . . set YINHALT = $$CalculateString^WWWFORMD($$$WWW122DefaultVariableInput(YTXT),YFORM,YKEY1)  ;SR17119
	. . . . . set YINHALT = $$DisplayData^COMGridEdit31F1(YINHALT,YFORM,YBBN)                            ;SR17119
	. . . . if YLFN'="" do
	. . . . . set YINHALT = $piece(YFELD,Y,YLFN)
	. . . . . set YTYP    = $piece($get(^WWW003(0,YDATEI,YLFN,1)),Y,3)
	. . . . ;
	. . . . $$$LogRx(YFORM_"-"_YBBN_":4:"_YINHALT_"<"_YTYP_"<")
	. . . . ;
	. . . . quit:YTYP=0                             ; Hidden field
	. . . . ;
	. . . . if YTYP=2 if YINHALT="" set YINHALT=0                    ;IMMER JA/NEIN ANZEIGEN
	. . . . set YINHALT   = $$^WWWFORMAT(YDATEI,"D",YLFN,YINHALT,3)  ;Return colour as well for WWW101/INPARA
	. . . . ;
	. . . . $$$LogRx(YFORM_"-"_YBBN_":5:"_YINHALT_"<"_YTYP_"<"_YLFN_"<")
	. . . . ;
	. . . . set YFARBCODE = $piece(YINHALT,Y,2)
	. . . . set YINHALT   = $piece(YINHALT,Y,1)
	. . . . if YINHALT="" set YINHALT = "&nbsp;"
	. . . . ;
	. . . . write "<TD NOWRAP"
	. . . . if (YTYP=4) || (YTYP=8) || (YTYP=12) || (YTYP=18) write " ALIGN=RIGHT"
	. . . . ;
	. . . . do OnBeforeFormat(YFORM,YKEY,YFELD,"D",YLFN,.strBGOverride)                  ; SR17243
	. . . . ;
	. . . . do
	. . . . . if $get(strBGOverride)'="" write " BGCOLOR="""_strBGOverride_"""" quit     ; SR17243
	. . . . . if YFARBCODE'="" write " BGCOLOR="_""""_$piece($get(^WWW100(0,"FARBE",SPRACHE,YFARBCODE,1)),Y,1)_"""" quit
	. . . . . write strBGColour        ; SR17119.1
	. . . . write ">"_strSetFont       ; SR17119.1
	. . . . ;
	. . . . if ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) && ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumCreateOnly) do
	. . . . . new YKEY
	. . . . . write "<A"
	. . . . .;write " TITLE="_""""_$$^WWWTEXT(374)_""""   ; "Select Data Record"  ; SR17243
	. . . . . write " TITLE="_""""_$select($get(YTOOLTIP)="":$$^WWWTEXT(374),1:YTOOLTIP)_""""   ; "Select Data Record"
	. . . . . write " HREF="_""""_YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YSORT="_$get(YSORT)_"&amp;YORIENT="_$get(YORIENT)
	. . . . . set YKEY = $translate(YKEY1,"""")
	. . . . . do ^WWWCGI
	. . . . . write """"_">"
	. . . . ;
	. . . . write $$^WWWUML(YINHALT)
	. . . . if YINHALT'="" if $find(YINHALT,".GIF") || ($find(YINHALT,".gif")) || ($find(YINHALT,".JPG")) || ($find(YINHALT,".jpg")) do THUMP
	. . . . if ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) && ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumCreateOnly) write "</A>"
	. . . . ;
	. . . . write "</FONT></TD>",YCR      ; SR15542
	
	write "</TR>",YCR
	
	kill ^CacheTempWWWFORM4(YUSER,"OnBeforeFormat")    ; SR17243
	quit
	
	
ANZEIGE1 ;LEERFELDER
    ;-------------------------------------------------------------------------------
    ; History:
	; 26-Feb-2010	GRF		SR17119.1: use strBGColour & strSetFont to improve
	; 							performance
	; 11-Feb-2009	GRF		SR16347: Manual Form Field won't have Class link - 
	; 							subscript error on YLFN
    ; 27-Jul-2007	shobby	SRBR014615:	Removed YHIDDSE as a parameter to NoAccess
	; 27-Jul-2007	shobby	SRBR014586: Include YOPTION, YOPTION1, YPARA1 in the 'access' test.
	; 19-Jul-2007	shobby	SRBR014615: Subroutined NoAccess call to make sure all
	; 						customisations are considered
    ; 28-Oct-2005	JW		SR13074: Exchange Rate type
    ; 9-Sep-2005	JW		Only call WWWUMLAU once (idxForm)
    ;-------------------------------------------------------------------------------
	new idxForm,strBGColour,strSetFont
	
	set YDDSATZ = YDDSATZ+1
	set strBGColour = " BGCOLOR="_$select(YDDSATZ#2:YWHITE,1:YGRAY)  ;SR17119.1 vvv
	
	if $piece(YVOR,Y,7)="" {
		set strSetFont = "<FONT>"
	} else {
		set strSetFont = "<FONT SIZE="""_$piece(YVOR,Y,7)_""">"
	}                                                                ;SR17119.1 ^^^
	
	write YCR,"<TR>"
	
	set YLFN=YMAXKEY do
	. write "<TD VALIGN=TOP NOWRAP"
	. set YTYP=$piece($get(^WWW002(0,YDATEI,YLFN,1)),Y,3)
	. if (YTYP=4) || (YTYP=8) || (YTYP=12) || (YTYP=18) write " ALIGN=RIGHT"
	. write strBGColour_">"_strSetFont       ; SR17119.1
	. write "&nbsp;"
	. write "</FONT></TD>",YCR      ; SR15542
	
	set idxForm = $$^WWWUMLAU(YFORM,1)
	set YSEI=""
	for  set YSEI=$order(^WWW122s(0,3,idxForm,YSEI)) quit:YSEI=""  do
	. set YZEILE=""
	. for  set YZEILE=$order(^WWW122s(0,3,idxForm,YSEI,YZEILE)) quit:YZEILE=""  do
	. . set YSPL=""
	. . for  set YSPL=$order(^WWW122s(0,3,idxForm,YSEI,YZEILE,YSPL)) quit:YSPL=""  do
	. . . set YBBN=""
	. . . for  set YBBN=$order(^WWW122s(0,3,idxForm,YSEI,YZEILE,YSPL,YFORM,YBBN)) quit:YBBN=""  do
	. . . . quit:$$NoAccess^WWWFORMCOMMON(YFORM,YDATEI,YBBN,.YTXT,.YLFN,"",$get(YOPTION),$get(YOPTION1),$get(YPARA1))
	. . . . ;
	. . . . write "<TD NOWRAP"
	. . . . ;SET YTYP=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,3)
	. . . . if YLFN=""  set YTYP = $piece($get(^WWW122(0,YFORM,YBBN,1)),Y,5)     ; use form setting for manual form fields
	. . . . if YLFN'="" set YTYP = $piece($get(^WWW003(0,YDATEI,YLFN,1)),Y,3)    ; use class setting
	. . . . if YTYP=""  set YTYP = 6
	. . . . quit:YTYP=0               ;UNSICHTBAR  ;HIDDENFELDER GRID
	. . . . if YTYP=4||(YTYP=8)||(YTYP=12)||(YTYP=18) write " ALIGN=RIGHT"
	. . . . write strBGColour_">"_strSetFont       ; SR17119.1
	. . . . write "&nbsp;"
	. . . . write "</FONT></TD>",YCR      ; SR15542
	
	write "</TR>",YCR
	quit
	
THUMP ;BILDER ANZEIGEN UND ANDER OBJEKTE ; display images
	new YWIDTH1,YHEIHT1,YBORDER,YI
	
	quit:YINHALT=""
	quit:$find(YINHALT,"|")
	quit:$find(YINHALT," ")
	
	set YWIDTH1=100
	set YHEIHT1=100
	set YBORDER=1
	for YI=53:1:56 set YO(YI)=$piece(YVOR,Y,YI)
	for YI=53:1:56 if '$data(YO(YI)) set YO(YI)=""
	if YO(53)=1 quit  ;KEIN OBJEKT ;no thing 
	if +YO(54)'=0 set YWIDTH1=YO(54)
	if +YO(55)'=0 set YHEIHT1=YO(55)
	if +YO(56)'=0 set YBORDER=0
	if $find(YINHALT,".GIF") || $find(YINHALT,".gif") || $find(YINHALT,".jpg") || $find(YINHALT,".JPG") do  quit
	. if '$find(YINHALT,"/") set YINHALT=YGIF_YINHALT
	. write "&nbsp;"
	. write YCR,"<IMG SRC="_""""_$translate(YINHALT,"|")_""""_" WIDTH="_YWIDTH1_" HEIGHT="_YHEIHT1_" BORDER="_YBORDER_" ALIGN=RIGHT VALIGN=BASELINE TITLE="_""""_$piece(YINHALT,".",1)_""""_">"
	
	if $find(YINHALT,".AVI") || $find(YINHALT,".avi") do  quit
	. if '$find(YINHALT,"/") set YINHALT=YGIF_YINHALT
	. write "&nbsp;"
	. write "<CENTER><IMG DYNSRC="_""""_YINHALT_""""_" START=mouseover"_"></CENTER>"
	
	if $length(YINHALT,".")=2 if $extract(YINHALT,$length(YINHALT)-3)="." do  quit
	. if '$find(YINHALT,"/") set YINHALT=YGIF_YINHALT
	. write "<EMBED SRC="_""""_YINHALT_""""_">"
	
	quit
	
	
OnBeforeFormat(YFORM,YKEY,YFELD,YART,YLFN,&pstrBGOverride)
	;-------------------------------------------------------------------------------
	; Duplicate Form Type 12 : Edit Grid "OnBeforeFormat" processing for
	; Form Type 3 : Grid forms - only when there is an "OnBeforeFormat" tag in the
	; routine matching the form name
	; 
	; Will use ^CacheTempWWWFORM4(YUSER,"OnBeforeFormat") as temporary storage
	; where all records need to be reviewed first.
	; 
	; Params: YFORM, YKEY, YFELD
	;         - isolated to preserve values in calling routine
	;
	; ByRefs:
	;
	; Returns: Nothing
	;
	; History:
	; 10-Nov-2010	GRF		SR17243: created
	;-------------------------------------------------------------------------------
	$$$LogR2("OnBeforeFormat",YKEY_"<"_YFELD_"<"_YART_"<"_YLFN)
	
	do CallEvent^COMGridEdit31Events("do OnBeforeFormat^"_YFORM_"(YKEY,YFELD,YART,YLFN,.pstrBGOverride)","Format")   ; *** EXECUTE ***
	
	$$$LogRx("OBF1:"_$get(pstrBGOverride)_"<"_$get(YTOOLTIP))
	
	quit
	
]]></Routine>
</Export>