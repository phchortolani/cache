<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINCharValue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINCharValue
	
#include COMSYS
#include INConst
	
	;-------------------------------------------------------------------------------
	; Functions to return the Posting Characteristics associated with Masterfile and
	; Order records
	; 
	; 
	; History:
	; 10-Mar-2005	GRF		SR10478 : Created for 3 way match
	;-------------------------------------------------------------------------------
	
	; TOTO: Several (if not all) directly reference pieces have macros now defined, would be worth using throughout code.
	
GetItemCV(pidItem="",&parrChars)
	;-------------------------------------------------------------------------------
	; Return Posting Characteristics IC1, IC2 and LC3 for an Item,
	; supplying from the associated Group if not specified at the item level
	; 
	; ByRef : parrChars
	; 
	; History :
	; 01-Dec-2006	JW		BR014285: Item group change
	; 09-Sep-2005	GRF		SR13468 : Recursive action copied from INFIBBUCHCODE
	;-------------------------------------------------------------------------------
	new blnFinished,idGroup,objGroup,objItem,objSys,strValues
	
	quit:pidItem=""
	
	set objItem   = $get(^INART(0,pidItem,1))
	set strValues = $piece(objItem,Y,382,384)
	set objSys    = $get(^INVORG(0,0,1))
	set idGroup   = $$$INARTItemGroup(objItem)
	if idGroup="" set idGroup = $$$INVORGDefaultItemGroup(objSys)
	
	; Continue searching up Item Category Chain until we have a value for each Char Type
	; or reach top of chain
	; e.g. Item 123 => Item Group A-AA-5 => Group A-AA => Group A [stops]
	;      If all three characteristic types have values earlier, don't check further.
 
	for {
		set blnFinished = (($piece(strValues,Y,1)'="") &&
		                   ($piece(strValues,Y,2)'="") &&
		                   ($piece(strValues,Y,3)'="")    )
		quit:blnFinished
		quit:idGroup=""
		
		set objGroup = $get(^INFIBITEMGROUP(0,idGroup,1))
		if $piece(strValues,Y,1)="" set $piece(strValues,Y,1) = $piece(objGroup,Y,1)
		if $piece(strValues,Y,2)="" set $piece(strValues,Y,2) = $piece(objGroup,Y,2)
		if $piece(strValues,Y,3)="" set $piece(strValues,Y,3) = $piece(objGroup,Y,3)
		
		set idGroup = $$$INItemGroupParentGroup($get(^INItemGroup(0,idGroup,1)))
	}
	
	set parrChars("IC1") = $piece(strValues,Y,1)
	set parrChars("IC2") = $piece(strValues,Y,2)
	set parrChars("IC3") = $piece(strValues,Y,3)
	quit
	
	
GetLocnCV(pidLocn="",pidStorage="",&parrChars)
	;-------------------------------------------------------------------------------
	; Return Posting Characteristics LC1 and LC2 for a Storage (or Location),
	; supplying from the associated Department or Location if not specified at the
	; Stock Location level
	; 
	; ByRef : parrChars
	; 
	; History:
	; 09-Dec-2009	GRF		SR16667: Comment obsolete code; Storage not StockLocn
	;-------------------------------------------------------------------------------
	new objLocn,objStore,strValues
	
	quit:pidLocn=""
	
	set strValues = ""
	
	if pidStorage'="" {
		set objStore  = $get(^INLP(0,pidLocn,pidStorage,1))
		set strValues = $piece(objStore,Y,16,17)
	;	set Dept      = $piece(objStore,Y,14)              ; DEPRECATED
	
	;	if Dept'="" {                                     ; DEPRECATED
	;		set objDept = $get(^WWW0124(0,Dept,1))
	;		if $piece(strValues,Y,1)="" set $piece(strValues,Y,1) = $piece(objDept,Y,5)
	;		if $piece(strValues,Y,2)="" set $piece(strValues,Y,2) = $piece(objDept,Y,6)
	;	}
	}
	
	set objLocn = $get(^WWW0121(0,0,pidLocn,1))
	if $piece(strValues,Y,1)="" set $piece(strValues,Y,1) = $piece(objLocn,Y,70)
	if $piece(strValues,Y,2)="" set $piece(strValues,Y,2) = $piece(objLocn,Y,71)
	
	set parrChars("LC1") = $piece(strValues,Y,1)
	set parrChars("LC2") = $piece(strValues,Y,2)
	quit
	
	
GetSupCV(pidSup="",&parrChars)
	;-------------------------------------------------------------------------------
	; Return Posting Characteristics SC1, SC2 and SC3 for a Supplier
	; 
	; ByRef : parrChars
	; 
	; History :
	; 31-Mar-2005	GRF		SR10478 : Add Supplier & Customer Group definitions
	;-------------------------------------------------------------------------------
	new idSupGroup,objSup,objSupGroup
	
	quit:pidSup=""
	
	set objSup = $get(^INLIEF(0,pidSup,1))
	set parrChars("SC1") = $piece(objSup,Y,235)
	set parrChars("SC2") = $piece(objSup,Y,236)
	set parrChars("SC3") = $piece(objSup,Y,237)
	
	set idSupGroup = $piece(objSup,Y,31)
	if idSupGroup'="" {
		set objSupGroup = $get(^INFIBSUPGROUP(0,idSupGroup,1))
		if parrChars("SC1")="" set parrChars("SC1") = $piece(objSupGroup,Y,1)
		if parrChars("SC2")="" set parrChars("SC2") = $piece(objSupGroup,Y,2)
		if parrChars("SC3")="" set parrChars("SC3") = $piece(objSupGroup,Y,3)
	}
	quit
	
	
GetCustCV(pidCust="",&parrChars)
	;-------------------------------------------------------------------------------
	; Return Posting Characteristics CC1, CC2 and CC3 for a Customer
	; 
	; ByRef : parrChars
	; 
	; History :
	; 31-Mar-2005	GRF		SR10478 : Add Supplier & Customer Group definitions
	;-------------------------------------------------------------------------------
	new idCustGroup,objCust,objCustGroup
	
	quit:pidCust=""
	
	set objCust = $get(^INKUNDE(0,pidCust,1))
	set parrChars("CC1") = $piece(objCust,Y,235)
	set parrChars("CC2") = $piece(objCust,Y,236)
	set parrChars("CC3") = $piece(objCust,Y,237)
	
	set idCustGroup = $piece(objCust,Y,31)
	if idCustGroup'="" {
		set objCustGroup = $get(^INFIBCUSTGROUP(0,idCustGroup,1))
		if parrChars("CC1")="" set parrChars("CC1") = $piece(objCustGroup,Y,1)
		if parrChars("CC2")="" set parrChars("CC2") = $piece(objCustGroup,Y,2)
		if parrChars("CC3")="" set parrChars("CC3") = $piece(objCustGroup,Y,3)
	}
	quit
	
	
GetOrderCV(pidOrder="",&parrChars)
	;-------------------------------------------------------------------------------
	; Return Posting Characteristics OC1 and OC2 for an Order
	; 
	; ByRef : parrChars
	;-------------------------------------------------------------------------------
	new objOrder
		
	quit:pidOrder=""
	
	set objOrder = $get(^INAUF(0,pidOrder,1))
	set parrChars("OC1") = $piece(objOrder,Y,330)
	set parrChars("OC2") = $piece(objOrder,Y,331)
	
	quit
	
]]></Routine>
</Export>