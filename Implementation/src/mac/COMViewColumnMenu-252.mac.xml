<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewColumnMenu" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewColumnMenu
    ;-------------------------------------------------------------------------------
	;	Column menu supportCode
    ;-------------------------------------------------------------------------------
#include COMSYS
#include WWWConst
	
Show(pidKey="",pidField="",pintFlag=0,pidCell="")
    ;-------------------------------------------------------------------------------
    ; Populates the Column menu popup.
    ;
    ; History:
    ; 27-Jul-2011	shobby	SR17788: ColumnName only has one parameter.
    ; 18-Feb-2011	shobby	SR17663: Clipboard data difference in Firefox.
    ; 01-Dec-2009	PPP		SR17025:Added Green Ball Icon if Column already customised
    ; 16-Nov-2009	PPP		SR17025:Add new menu option - Customize Column Name
	; 11-Dec-2006	shobby	SRBR014286:Corrected GIF names.
    ; 29-Aug-2006	JW		SR14763: Group by does not work - removed
    ; 20-Feb-2006	PO		SR14250: Source security settings from form related to
    ; 							data and not COMViewSearch
    ; 31-Jan-2006	PO		SR14250: Show menu options if user has priviledge to
    ; 							perform action.
    ; 11-Feb-2005	PO		SR10965 Include support for cell context menus.
    ; 17-Jan-2005	PaulK	Created
    ;-------------------------------------------------------------------------------
	new blnShowMenuOptions,idClass,idField,idForm,idNestField,imgCustom,strDesc
	
	set idForm  = $get(^CacheTempView(YUSER,"Form"))
	set idClass = $get(^CacheTempView(YUSER,"Class"))
	set idField = pidField
	
	do StartMenu()
	
	if pintFlag=0 {
		set blnShowMenuOptions = $$HasViewAccess^COMView(YBED,idForm,YM)
		
		if idField'="" {
			do AddRow("SortAsc.gif",$$$Text("Com00154"),"SortColumn("""","""_idField_""",1)")    ;Sort Ascending
			do AddRow("SortDsc.gif",$$$Text("Com00155"),"SortColumn("""","""_idField_""",-1)")   ;Sort Descending
			
			; Non @NM classes
			if '$data(^WWW001(0,idClass)) {
				set idNestField = $piece(pidField,"->",$length(pidField,"->"))
				set strDesc 	= $$GetCustomisedText^WWWClassTranslation($$GetClassForNestedField^COMViewFilterColumn(idClass, idField),idNestField)
				set imgCustom = "ball_grey.gif"
				if strDesc'="" {
					set imgCustom = "ball_green.gif"
				}
	;SR17788	do AddRow(imgCustom,$$$Text("Com00329"),"ColumnName("""","""_idField_""")")   ; "Customize Field Name"
				do AddRow(imgCustom,$$$Text("Com00329"),"ColumnName("""_idField_""")")        ; "Customize Field Name"   ;SR17788
			}
			
			do AddSeparator()
			
			if blnShowMenuOptions do AddRow("xxclose.gif",$$$Text("Com00156"),"ColumnClick("""_idField_""")")  		 ;Remove This Column
		}
		if blnShowMenuOptions do AddRow("sort.gif",$$$Text("Com00157"),"ChooseShow()")    ; Field Chooser
		
	} else {
	; FIXME : <GRF> GetRelation^COMViewUtils is a function. returns an object??DWR
		do GetRelation^COMViewUtils(.idClass,.idField)
		if $extract(idField)="D" {
			if $zboolean(pintFlag,2,1) do AddRow("clear.gif",$$$Text("Com00164"),"CallBack(""ShowCompleteData^COMViewCellData""(event.pageX ? event.pageX : event.screenX),(event.pageY ? event.pageY : event.screenY),"""_$zconvert(pidKey,"o","JS")_""","""_$zconvert(pidField,"o","JS")_""")")  ;Show complete data
		}
		if $$$WWW120ModificationsProtocol($get(^WWW120(0,idClass,1))) {
			if $zboolean(pintFlag,1,1)&&($extract(idField)="D") do AddRow("time.gif",$$$Text("Com00163"),"CallBack(""ShowCompleteHistory^COMViewCellData"",(event.pageX ? event.pageX : event.screenX),(event.pageY ? event.pageY : event.screenY),"""_$zconvert(pidKey,"o","JS")_""","""_$zconvert(pidField,"o","JS")_""")")  ;Show history
		}
		if $get(YUSERAGENT)="MSIE" {
			if pidCell'="" do AddRow("copy.gif",$$$Text("Com00232"),"window.clipboardData.setData(""Text"",parent."_pidCell_".innerHTML);",1)
		} else {
			if pidCell'="" do AddRow("copy.gif",$$$Text("Com00232"),"window.clipboardData.setData(""Text"",document.getElementById("""_pidCell_""").innerHTML); ",0)  ;SR17663 1->0
		}   ; "Copy text to clipboard"
	}
	do EndMenu()
	quit
	
	
StartMenu()
    ;-------------------------------------------------------------------------------
    ; Initialises a custom context menu
    ;
    ; History:
    ; 05-Jul-2010	GRF		SR17???: Missing semicolon; split creation for clarity
    ; 09-May-2005	PaulK	Suppress Context menu for table (this table is a context menu)
    ; 21-Jan-2005	PaulK	Created
    ;-------------------------------------------------------------------------------
	new strHTML
	
	kill ^CacheTempMenu(YUSER)
	;set ^CacheTempMenu(YUSER)="<TABLE style='border-width:2px;border-style:outset;border-color:slategray;position:relative;width:100%;height:100%table-layout:fixed;' border=0 cellpadding=0 cellspacing=0 oncontextmenu='event.returnValue=false; return false;'>"
	set strHTML = "<TABLE style='border-width:2px;border-style:outset;border-color:slategray;"
	set strHTML = strHTML_"position:relative;width:100%;height:100%;table-layout:fixed;' "
	set strHTML = strHTML_"border=0 cellpadding=0 cellspacing=0 oncontextmenu='event.returnValue=false; return false;'"
	set strHTML = strHTML_">"
	set ^CacheTempMenu(YUSER) = strHTML
	
	quit
	
	
EndMenu()
	;-------------------------------------------------------------------------------
	; Shows a custom context menu.
	;
	; History:
	; 21-Jan-2005	PaulK	Created
	;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML = $get(^CacheTempMenu(YUSER))_"</TABLE>"
	if $get(^CacheTempMenu(YUSER,"Concat")) {
		write "Columns.document.body.innerHTML+="""_$zcvt(strHTML,"o","JS")_""";"
	
	} else {
		write "Columns.document.body.innerHTML="""_$zcvt(strHTML,"o","JS")_""";"
	}
	quit
	
	
AddRow(pstrImage,pstrDescription,pstrCallBack="",pblnNotParent=0,pblnEnabled=1)
    ;-------------------------------------------------------------------------------
    ; Adds a row to a custom context menu.
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Aug-2010	shobby	SR17445.1: Set width of image column
    ; 11-Aug-2010	shobby	SR17445: onclick doesn't fire in firefox. Using onmouseup
    ; 							Simplification of event.srcElement code.
    ; 01-Sep-2008	shobby	SRBR014976: Set the FontFace as per company settings.
    ; 24-Feb-2005	shobby	pblnEnabled to show the menu option but nothing will happen.
    ; 11-Feb-2005	PO		SR10965 Onclick if no command hide popup.
    ; 21-Jan-2005	PaulK	Created
    ;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML="<TR "
	if pblnEnabled {
		set strHTML=strHTML_"onmouseover='"
	;SR17445 set strHTML=strHTML_" var row= event.target ? event.target.parentNode : event.srcElement.parentNode;"  ;SR17253
		set strHTML=strHTML_" var row=event.srcElement.parentNode;"             ;SR17445
		set strHTML=strHTML_" if (row.tagName==""TD"") {row=row.parentNode;}"	;SR17253
		set strHTML=strHTML_" row.parentNode.parentNode.style.backgroundcolor=""lightgrey"";"
		set strHTML=strHTML_" row.cells[0].style.borderColor=""darkblue"";"
		set strHTML=strHTML_" row.cells[0].style.backgroundColor=""lightsteelblue"";"
		set strHTML=strHTML_" row.cells[0].style.filter="""";"
		set strHTML=strHTML_" row.cells[1].style.borderColor=""darkblue"";"
		set strHTML=strHTML_" row.cells[1].style.backgroundColor=""lightsteelblue"";'"
		set strHTML=strHTML_" onmouseout='"
	;SR17445 set strHTML=strHTML_" var row=event.target ? event.target.parentNode : event.srcElement.parentNode;"	;SR17253
		set strHTML=strHTML_" var row=event.srcElement.parentNode;"             ;SR17445
		set strHTML=strHTML_" if (row.tagName==""TD"") {row=row.parentNode;}"	;SR17253
		set strHTML=strHTML_" row.cells[0].style.borderColor=""lightgrey"";"
		set strHTML=strHTML_" row.cells[0].style.backgroundColor=""lightgrey"";"
		set strHTML=strHTML_" row.cells[0].style.filter=""progid:DXImageTransform.Microsoft.Alpha(opacity=60)"";"
		set strHTML=strHTML_" row.cells[1].style.borderColor=""whitesmoke"";"
		set strHTML=strHTML_" row.cells[1].style.backgroundColor=""whitesmoke"";'"
		
		if pstrCallBack="##do nothing##" {
			
		} elseif pstrCallBack'="" {
			if pblnNotParent {
				set strHTML=strHTML_" onclick='"_pstrCallBack_"; if (parent.Columns) {parent.Columns.hide();} else {Columns.hide();}'"  ;SR17253
			} else {
	;SR17445	set strHTML=strHTML_" onclick='if (parent.Columns) {parent."_pstrCallBack_"; parent.Columns.hide();} else {"_pstrCallBack_"; Columns.hide();}'"  ;SR17253
				set strHTML=strHTML_" onmouseup='if (parent.Columns) {parent."_pstrCallBack_"; parent.Columns.hide();} else {"_pstrCallBack_"; Columns.hide();}'"  ;SR17253
			}
		} else {
			set strHTML=strHTML_" onclick='if (parent.Columns) {parent.Columns.hide();} else {Columns.hide();}'" ; SR10965  ;SR17253 ;SR17445
		}
	}
	set strHTML=strHTML_">"
	set strHTML=strHTML_"<TD style='width:25px; filter:progid:DXImageTransform.Microsoft.Alpha(opacity=60);border-style:solid;background-color:lightgrey;border-color:lightgrey;border-bottom-width:1px;border-left-width:1px;border-top-width:1px;border-right-width:0px;'>" ;SR17445.1
	if (pstrImage'="") {
		set strHTML=strHTML_"<IMG height=""20px"" width=""20px"" align='absmiddle' SRC='"_YGIF_pstrImage_"'>"
	} else {
		set strHTML=strHTML_"&nbsp;"
	}
	set strHTML=strHTML_"</TD>"
	set strHTML=strHTML_"<TD style='"
	if 'pblnEnabled set strHTML=strHTML_"color:gray;"
	set strHTML=strHTML_"border-style:solid;background-color:whitesmoke;border-color:whitesmoke;border-bottom-width:1px;border-right-width:1px;border-top-width:1px;border-left-width:0px;font-size:12px;font-family:"_$$FontFace^WWW012()_";'>" ;BR014976
	set strHTML=strHTML_$zcvt(pstrDescription,"o","JS")
	set strHTML=strHTML_"</TD>"
	set strHTML=strHTML_"</TR>"
	set ^CacheTempMenu(YUSER)=$get(^CacheTempMenu(YUSER))_strHTML
	if $length($get(^CacheTempMenu(YUSER)))>30000 {
		if '$get(^CacheTempMenu(YUSER,"Concat")) write "Columns.document.body.innerHTML='';"
		set ^CacheTempMenu(YUSER,"Concat")=1
		write "Columns.document.body.innerHTML+="""_$zcvt($get(^CacheTempMenu(YUSER)),"o","JS")_""";"
		set ^CacheTempMenu(YUSER)=""
	}
	quit
	
	
AddSeparator()
    ;-------------------------------------------------------------------------------
    ; Adds a separator to the custom context menu.
    ;
    ; History:
    ; 12-Apr-2005	PaulK	Commented
    ;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML=""
	set strHTML=strHTML_"<TR style='height:5px;'>"
	set strHTML=strHTML_"<TD style='filter:progid:DXImageTransform.Microsoft.Alpha(opacity=60);height:5px;background-color:lightgrey;border-color:lightgrey;border-bottom-width:1px;border-left-width:1px;border-bottom-top:1px;'>"
	set strHTML=strHTML_"&nbsp;</TD>"
	set strHTML=strHTML_"<TD style='height:5px;background-color:whitesmoke;border-color:whitesmoke;border-bottom-width:1px;border-right-width:1px;border-bottom-top:1px;'>"
	set strHTML=strHTML_"<HR style='width:95%;left:5%;'>"
	set strHTML=strHTML_"</TD>"
	set strHTML=strHTML_"</TR>"
	set ^CacheTempMenu(YUSER)=$get(^CacheTempMenu(YUSER))_strHTML
	quit
	
	
DefaultContext()
	;-------------------------------------------------------------------------------
	; Change context menu to only print
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 14-Aug-2006	RPW		SR13836: Use Start/End Script macros and removed YCR.
	; 11-Aug-2006	JW		SR13836: Created
	;-------------------------------------------------------------------------------
	$$$StartScript()
	write "var Columns=window.createPopup(); function DisplayContext() {"
	do StartMenu^COMViewColumnMenu()
	do AddRow^COMViewColumnMenu("xxprint.gif",$$$Text("Com00170"),"window.print()")	; "Print"
	do EndMenu^COMViewColumnMenu()
	//write "	Columns.show(event.screenX,event.screenY,200,26); }"  ;SR17253
	write "	Columns.show((event.pageX ? event.pageX : event.screenX),(event.pageY ? event.pageY : event.screenY),200,26); }"
	$$$EndScript()
	
	quit
	
	;---------------------------------------
	; CreateWindowLocation
	;---------------------------------------
	
OPEN
	new strImg
	
	if YFORM="INWEINVEHeader" {
		set strImg = "reset"
		set YSTATUS = $$^WWWTEXT(32810)		; "Refresh"
	} else {
		set strImg  = "open"
		SET YSTATUS = $$^WWWTEXT(101)		; "Open"
	}
	do AddRow^COMViewColumnMenu(strImg_".gif",YSTATUS,"CallBack(""SaveNow^WWWFORMF"",""1"");")
	quit
	
	
RREV
	;DO Direction^WWWFORMF("rrev","FIRST","PAGEL","&lt;&lt;",301)
	new strScript
	
	set strScript="CallBack('"
	set strScript=strScript="document.WWW.style.cursor = ""wait""; SaveAction(1); "
	set strScript=strScript="window.location="""
	set strScript=strScript_$$DirectionURL2^WWWFORMF("FIRST")
	set strScript=strScript_"""')"
	do AddRow^COMViewColumnMenu("rrev.gif",$$$Text(301),strScript)
	quit
	
]]></Routine>
</Export>