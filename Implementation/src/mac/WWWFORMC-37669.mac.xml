<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMC
#include WWWFORM
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORMC("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Button with forms     BUTTON BEI forMULAREN
	;
	; Inputs : 
	;	YFORM			idForm
	;	YVOR			objWWW120
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 14-Jun-2012	shobby	SR18033: Javascript buttons may be enable or disabled dynamically.
	; 08-Jul-2009	shobby	SR16455.2:  Start a new line even if the button marked
	; 							for this is not displayed.
	; 24-Jan-2008	shobby	SRBR014526: Call to standard Coolbar function.
	; 27-Jun-2007	Karine	SRBR014535: Changes to improve the code 
	; 25-Jun-2007	shobby	SRBR014490: Pass in the button number (YLFN) to $$Disabled
	; 22-Jun-2007	Karine	SRBR014535: Show in the button title the form header that the 
	;                       button points to. 
	; 07-Jun-2007	RPW		SRBR014417: Allow List Forms to use the Download button
	; 						as well. INMAIL was missing it's button
	; 07-May-2007	GRF		SR15511: Doco; replace if and if not tests with if/else
	; 03-Jan-2007	JW		SR15240: Revision to OpenTD macro for Disabled buttons
	; 25-Oct-2006	JW		BR014279: Grid check for selected record - append to
	; 							confirmation text.
	; 24-Aug-2006	GRF		Doco
	; 13-Jan-2006	JW		SR13195: Rewrote to use braces/macros/functions.
	; 14-Sep-2005	RPW		SR13438: Change the $$$Alert($$^WWWTEXT into $$$Alert($lb
	; 08-Sep-2005	shobby	SR13438: Use $$$Alert macro when creating the alert message,
	; 							this handles things like quotes in the alert message.
	; 28-Jul-2005	RPW		SR11983: Check for YOPTION="" when piece 50 of the button
	; 							def is not ""
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 29.09.1998  	DT
	;-------------------------------------------------------------------------------
	new YI,YA,I,YLFN,SPRACHE,YEXEC,YKEYXX,YBPOS,YPRINT,Q,YQ,YCHECK
	new oldYBACK,blnButtons,blnNextLine,idxForm,xferVar,strPic,strTitle
	
	$$$LogR("",YFORM)
	
	set blnNextLine = $$$NO
	
	;;VERTEILERBUTTON
	;if YBEDBER=1 write YCR,YCR,"<!-- ************************* SPECIAL BUTTON (WWWFORMC) ************************* -->",YCR,YCR
	
	;+++++++++++++++++++++++++++++++++++++++
	;	YA			objWWW124		Buttons
	;	YVOR		objWWW120		Form
	;+++++++++++++++++++++++++++++++++++++++
	
	set blnButtons = $$$WWW120PicturesAsButtons(YVOR)
		
	if YFOART=5 if '$data(^WWW122(0,YFORM)) set YWIDTH="",YHEIGHT=""
	if $$$WWW120StandardSubmit(YVOR)="" if 'blnButtons write "<TR>" ;nicht bilder als button UND KEIN STANDARDSUBMIT ;not when And no 
	
	set YI=0
	set SPRACHE  = $$^WWWLANGU(YBED)
	set LANGUAGE = SPRACHE
	set oldYBACK = YBACK
	
	; SEARCH BUTTONS 
	set idxForm = $$^WWWUMLAU(YFORM,1)
	set YBPOS   = ""
	for {
		set YBPOS = $order(^WWW124s(0,2,idxForm,SPRACHE,YBPOS))
		quit:YBPOS=""
		
		set YLFN = ""		;alle angelegten Button ;All Button 
		for {
			set YLFN = $order(^WWW124s(0,2,idxForm,SPRACHE,YBPOS,YFORM,SPRACHE,YLFN))
			quit:YLFN=""
			
			set YA = $get(^WWW124(0,YFORM,SPRACHE,YLFN,1))
			if ($$$WWW124FunctionJavaScriptOnClick(YA)'="")&&($$$WWW124Dynamic(YA)) {					;SR18033
				write $$Button^WWWFORMC2012(YA,oldYBACK,.YI,YPARA,YMANDANT,YOPTION,YFORM,SPRACHE,YLFN)  ;SR18033
			} else {
				set Q     = ""
				set YQ    = ""
				set YBACK = oldYBACK
				if $extract($$$WWW124TargetFrameName(YA))="@" set YBACK  = ""   ;TYBD;OHNE YBACK ;without 
			
				if $$$WWW124NextLine(YA) set blnNextLine = $$$WWW124NextLine(YA)
			
				continue:$$DontShow()
				continue:$$^WWWACCESS($$$WWW124Authorization1(YA),$$$WWW124Module1(YA))'=$$$YES  ;KEIN ZUGANG ;no 
				continue:(($$$WWW124ExecuteOnClick(YA)="")            &&
				          ($$$WWW124NewFormOnClick(YA)="")            &&
				          ($$$WWW124LinkWithexeOrHttp(YA)="")         &&
				          ($$$WWW124DownloadFileOnClick(YA)="")       &&
				          ($$$WWW124FunctionJavaScriptOnClick(YA)="") &&
				          ($$$WWW124ExecuteForManualLink(YA)="")      &&
				          ($$$WWW124SearchFunctionOnClick(YA)="")     &&
				          ($$$WWW124ChangeoversWithOnClick(YA)="")    &&
				          ($$$WWW124QueryOnClick(YA)="")              &&
				          ($$$WWW124HyperEventOnClick(YA)="")            )   // No actions
			
				set strTitle = $$GetButtonDescription^WWW124(YFORM,SPRACHE,YLFN)

				set YPRINT = ""
				if $$$WWW124OutputWithWindowprint(YA)=$$$YES set YPRINT = $$$YES  ;ZIEL DRUCKEN ;target print 
			
				if $$$WWW124QueryOnClick(YA)'="" set $$$WWW124ExecuteOnClick(YA)="QUERY,"_$$$WWW124QueryOnClick(YA)  ;QUERY AUFRUFEN
			
				set xferVar = $$$WWW124TransferVariableIdentifie(YA)
				if xferVar'="" {   ;ZUSATZVARIABLE
					if $data(@xferVar) {
						set @xferVar = $get(%(YQUERY,xferVar))
					}
				}
			
				set YI = YI+1
			
				if blnNextLine && blnButtons {
					set blnNextLine = $$$NO
					write YCR,"<TD>"
					if $$MenuType^WWW013()'=14 write "<IMG SRC="""_YGIF_"delimiter.gif"">"
					write "</TD>"
					write "</TR></TABLE>"
					write $$Coolbar^WWWFORMCOMMON("")
					write "<TR>"                      ;NEUE ZEILE
					if $$MenuType^WWW013()'=14{
						write "<TD WIDTH=3><IMG SRC="""_YGIF_"delimiter1.gif""></TD>"
					}
				}
			
				set strPic  = $$$WWW124PictureFileOnButton(YA)
				set newForm = $$$WWW124NewFormOnClick(YA)
			
				;KEIN BUTTON? ;no picture
				if blnButtons && (strPic="") {
					set strPic=$zconvert($extract($$^WWWUMLAU($$$WWW124ButtonDescription(YA),3)),"L")
					if '$find("abcdefghijklmnopqrstuvwxyzöäüß@?1234567890",strPic) set strPic = "x"
					set strPic = strPic_".gif"  ;KEIN BUTTON ABER BUTTON ;no yet 
				}
			
				set YTARGETF = $$$WWW124TargetFrameName(YA)
				if $extract(YTARGETF)="@" set YTARGETF = @$extract(YTARGETF,2,99)  ;ZIELTARGET AUS PARAMETER ;out of parameter 
			
				set YCONF1 = ""  ; Start JS
				set YCONF2 = ""  ; End JS
			
				continue:$$Disabled^WWWFORMB(newForm,blnButtons,strPic,$$$YES,.YCONF1,.YCONF2,YLFN)
			
				// Add confirmation text
			
				if ($$$WWW124AcknowledgementTextBefore(YA)'="") {
					$$$Append(YCONF1," if (confirm('"_$translate($$$WWW124AcknowledgementTextBefore(YA),"'""","´´")_"')) {")
					$$$Prepend(YCONF2,"} ")
				}
			
				if $$$WWW124AutomaticSUBMIT(YA) if newForm'="" if $$$WWW124ExecuteOnClick(YA)="" set $$$WWW124ExecuteOnClick(YA)="SUBMIT"   ;AUTOSUBMIT
			
				;-------------------------------
			
				$$$OpenTD($$$NO)
			
				;HREF DURCH MANUELLEN EXECUTE
				if $$$WWW124ExecuteForManualLink(YA)'="" {
					do MANU^WWWFORMB
				
				} elseif $$$WWW124SearchFunctionOnClick(YA)'="" {
					do Search(strTitle)
				
				} elseif (newForm="") && ($zconvert($$$WWW124ExecuteOnClick(YA),"U")="SUBMIT") {      ; *** EXECUTE ***
					do EXEC^WWWFORMB
				
				} elseif $$$UPPER($$$WWW124ExecuteOnClick(YA))="HELP" {                               ; *** EXECUTE ***
					do Help(strTitle)
				
				} elseif $$$WWW124ChangeoversWithOnClick(YA)'="" {
					do SEITE^WWWFORMB
				
				} elseif $$$WWW124FunctionJavaScriptOnClick(YA)'="" {
					do Java(strTitle)
				
				} elseif $$$WWW124HyperEventOnClick(YA)'="" {
					do HYPER^WWWFORMB
				
				} elseif $$$WWW124LinkWithexeOrHttp(YA)'="" {
					do VERKN^WWWFORMB
				
				} elseif ($$$WWW124DownloadFileOnClick(YA)'="") && (($get(YKEY)'="") || (YFOART=2)) {
					do Download(strTitle)
				
				} elseif (newForm="") && ($$$WWW124ExecuteOnClick(YA)'="") {
					do Exec(YBACK,strTitle)
				
				} elseif ($$$WWW124ExecuteOnClick(YA)="" || ($$$UPPER($$$WWW124ExecuteOnClick(YA))="SUBMIT")) && (newForm'="") {
					do Exec2(YKEY,YBACK,strTitle)
				}
				$$$CloseTD
			
				;-------------------------------
			}
		}
	}
 	
	if (($$^WWWACCESS(1)=$$$YES) || ($$^WWWACCESS(2)=$$$YES))     &&
	   ($get(YSCREENM)="")                                        &&
	   ($$$WWW013CompilerTranslator($get(^WWW013(0,YBED,1)))'="")    {
		
		set YSCREENM = 2  ;ENABLE TOOLBAR for SYSADMIN AND CUSTOMIZING TOOLBAR for DATABASE ADMIN;FIS;03.05.05;SR12200
	}
	if (+$get(YSCREENM)'=0) && ((YFOART=1) || (YFOART=4) || (YFOART=3)) {
		new YPRINT
		do ^WWWSCRB  ;SCREEN DFLT BUTTON
	}
	
	quit
	 
DontShow() 
    ;-------------------------------------------------------------------------------
    ; Works out whether a button should not be displayed
    ; *** Some parts deprecated by single company 0 ***
    ;
    ; Called By: ^WWWFORMC
    ;
    ; ByRefs:
    ;	YA
    ;	YMANDANT	
    ;	YOPTION		
    ;
    ; Returns: boolean
    ;
    ; History:
    ; 20-Dec-2005	JW		SR13195: Created
    ;-------------------------------------------------------------------------------
	new blnDontShow,strOnlyShow
	
	set blnDontShow = $$$NO
	
	if $$$WWW124ButtonBelongsToADataItem(YA) {
		set blnDontShow = $$$YES
	
	} elseif ($$$WWW124DisplayOnlyWhenMenuParam(YA)'="") && '$find(","_YPARA_",",","_$$$WWW124DisplayOnlyWhenMenuParam(YA)_",") {
		set blnDontShow = $$$YES     ;NICHT ANZEIGE WEIL PARAMETER FALSCH ;Not Show since parameter wrong 
	
	} elseif ($$$WWW124ButtonOnlyForCompany(YA)'="") && '$find(","_$$$WWW124ButtonOnlyForCompany(YA)_",",","_YMANDANT_",") {
		set blnDontShow = $$$YES
		
	} elseif ($$$WWW124ButtonNotforCompany(YA)'="") && $find(","_$$$WWW124ButtonNotforCompany(YA)_",",","_YMANDANT_",") {
		set blnDontShow = $$$YES
		
	} else {
		set strOnlyShow = $$$WWW124ShowOnlyWhenYOPTION(YA)
		if (strOnlyShow'="") && ((YOPTION="") || '$find(","_strOnlyShow_",",","_YOPTION_",")) {
			set blnDontShow = $$$YES
		}
	}
	quit blnDontShow
	
 
Download(pstrTitle)
	new strDownload,strField,strFile,strDirectory,objCompany,strChar
	
	if blnButtons  {
		write "<A class=link"
	} else {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""
	}
	
	$$$OpenOnClick
	$$$StartWindow
	
	set objCompany=$get(^WWW012(0,YM,1))
	
	;---------------------------------------
	;	May contain   [Path] VORG@ FieldNo @/ [File]
	;	Constructs    [Path] [SubPath] / [File]         where SubPath comes from INVORG DfieldNo
	;	
	;	Likely FieldNo values match the FTP directories : 96,97,98,181,182,183,184,187
	;	Possibly also Standard directories - less likely?
	;	(No additional analysis has been performed to build this list <GRF>)
	;---------------------------------------
	set strDownload=$$$WWW124DownloadFileOnClick(YA)
	
	if $find(strDownload,"VORG@") {                       ;BEC;26614;22.11.04
		set strField = $piece($piece(strDownload,"VORG@",2),"@",1)
		set strDownload=$piece(strDownload,"VORG@"_strField_"@/",1)_$piece($get(^INVORG(YM,YM,1)),Y,strField)_"/"_$piece(strDownload,"VORG@"_strField_"@/",2)    ;BEC;26614;22.11.04
	}
	
	if $find(strDownload,"YM/")   set strDownload=$piece(strDownload,"YM/",1)_YM_"/"_$piece(strDownload,"YM/",2)
	if $find(strDownload,"YUCI/") set strDownload=$piece(strDownload,"YUCI/",1)_YUCI_"/"_$piece(strDownload,"YUCI/",2)
	set strDownload=$zconvert(strDownload,"L")
	set strDownload=$translate(strDownload,"/","\")
	
	if $find(strDownload,"\auto\") {                      ;ANLEGEN UNTERVERZEICHNIS AUTOMATISCH ;put onto automatic 
		set strFile=$translate($$$WWW012PhysicalFTPDirectory(objCompany),"/","\")
		
		if $extract(strFile,$length(strFile))="\" set strFile=$extract(strFile,1,$length(strFile)-1)
		if $extract(strDownload)'="\" set strDownload="\"_strDownload
		
		set strDirectory=strFile_$piece(strDownload,"auto",1)_$translate(YKEY,", """)
		
		if '##class(%Library.File).DirectoryExists(strDirectory) {
			do ##class(%Library.File).CreateDirectoryChain(strDirectory)
		}
		set strDownload = $piece(strDownload,"auto",1)_$translate(YKEY,", """)_$piece(strDownload,"auto",2,99)
	}
	
	set strDownload = $translate(strDownload,"\","/")   ;LESEN AUTO strFile 
 
	if $find(strDownload,"auto") {
		set strDownload = $piece(strDownload,"auto",1)_$translate(YKEY,", """)_$piece(strDownload,"auto",2,99)
	}
	
	set strFile = $translate($$$WWW012VirtualFTPDirectory(objCompany),"\","/")
	
	for {
		set strChar = $extract(strFile,$length(strFile))
		quit:strChar'="/"
		
		set strFile = $extract(strFile,1,$length(strFile)-1)
	}
 
	if $extract(strDownload)="/" set strDownload = $extract(strDownload,2,9999)  ; HERAUSNEHMEN
	write "ftp://"_strFile_"/"_strDownload
	
	$$$EndWindow
	$$$CloseOnClick
	
	write ">"
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
		write "</A>"
	}
	
	quit
 
 
Exec(YBACK,pstrTitle) ; FIXME : Pass blnButtons
	;-------------------------------------------------------------------------------
	; History:
	; 29-Sep-2009	shobby	SR16913: Some rework to prevent multiple button clicking.
	; 02-May-2006	Steve S	SR14592: Transaction buttons can no longer be doubleclicked
	;-------------------------------------------------------------------------------
	new YEXE,strAction
	
	write YCR
	if blnButtons {
		write "<A class=link "
	} else {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""
	}
	$$$OpenOnClick
	set strAction = ""
	if $$$WWW124BlankDuringProcessing(YA) set strAction = strAction_" NewPage(document.WWW); "
	if $$$WWW124AutomaticSUBMIT(YA)	      set strAction = strAction_" BEARB('',1); retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','','9'); "
	
	
	set strAction = strAction_$select(YTARGETF="":" window.location='",1:" subWindow('")
	
	if $$$WWW124SetVariableYPARA(YA)'="" new YPARA set YPARA=$$$WWW124SetVariableYPARA(YA)   ; new parameter
	set strAction = strAction_YAKTION
	if $$$WWW124DoNotPrepareHTML(YA)  {
		set strAction = strAction_"EP=WWWMANU1&amp;YFORM="_YFORM
	
	} else {
		set strAction = strAction_"EP=WWWMANU&amp;YFORM="_YFORM
	}
	set YBACK = YBACK_YFORM_","
	if $extract($$$WWW124TargetFrameName(YA))="@" set YBACK=""   ; without YBACK
	if $extract($$$WWW124ExecuteOnClick(YA))="""" {              ; EXECUTE 
		set YEXE = $$$WWW124ExecuteOnClick(YA)
		set YEXE = "s $piece(YA,Y,6)="_YEXE
		xecute YEXE
	}
	if '$$$WWW124DoNotPrepareHTML(YA) set strAction=strAction_"&YEXEC="_$translate($$$WWW124ExecuteOnClick(YA),"# ""","*|`")											;16913
	if $$$WWW124DoNotPrepareHTML(YA)  set strAction=strAction_"&YEXEC="_$translate($$$WWW124ExecuteOnClick(YA),"# ""%","*|`")		// Is it correct to remove '%' ?	;16913
	
	if $$$WWW124TransferVariableIdentifie(YA)'="" set strAction=strAction_"&"_$$$WWW124TransferVariableIdentifie(YA)_"="_@($$$WWW124TransferVariableIdentifie(YA))
	set strAction=strAction_$$WWWCGI2^WWWCGI()
	if YTARGETF'="" if YTARGETF'=YTARGET set strAction=strAction_"&YLOCKBACK="_YTRAKT  ;AKTUELLE LOCKDATEI BEI SCHLIESSEN ZURÜCKHOLEN
	
	write $$DoOnClick^WWWBUTTON(strAction)
	write $select(YTARGETF="":"' ",1:"','"_YTARGETF_"') ")

	$$$CloseOnClick
	
	write ">"
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
		write "</A>"
	}
	if $$$WWW124DMSFileForTargetHttp(YA)'="" do ABLAGE
 
	quit
	
 
Exec2(YKEY,YBACK,pstrTitle) ; FIXME : Pass blnButtons
	;-------------------------------------------------------------------------------
	; History:
	; 02-May-2006	SteveS	SR14592: Transaction buttons can no longer be double-clicked
	;-------------------------------------------------------------------------------
	new MAPPING,YNOKEY,YNEXKEY,YI,MAP,idClass,YSCHLUES
	
	write YCR
	if blnButtons {
		write "<A class=link"
	} else { 
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""""
	}
	
	$$$OpenOnClick
	if $$$WWW124BlankDuringProcessing(YA) write " NewPage(document.WWW); "
	if ($zconvert($$$WWW124ExecuteOnClick(YA),"U")="SUBMIT") || $$$WWW124AutomaticSUBMIT(YA) {
		write "BEARB('',1); retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','','9');"
	}
	$$$StartWindow
		
	if $$$WWW124SetVariableYPARA(YA)'="" new YPARA set YPARA=$$$WWW124SetVariableYPARA(YA)  ; new parameter
	
	write YAKTION_""
	set YKEYXX  = YKEY
	set MAPPING = $translate($$$WWW124DataItemMapToNewFormNnnn(YA),",",";")   ;FELDMAPPING XXX=FELD
	if MAPPING'="" {
		set YNOKEY = YFORM  ;BEI RÜCKSPRUNG KEINE ÜBERGABE DES KEY´S ;next to no 
		
		for YI=1:1 {
			set MAP = $piece(MAPPING,";",YI)
			quit:MAP=""
			
			if ($piece(MAP,"=",1)=0) || ($piece(MAP,"=",1)=999) {
				set $piece(YKEY,",",+$piece(MAP,"=",2))  = ""
				set $piece(YFKEY,",",+$piece(MAP,"=",2)) = ""   ;KEINE VORGABE ;no default 
				
			} elseif $extract($piece(MAP,"=",1))="""" {
				set $piece(YKEY,",",+$piece(MAP,"=",2)) = $translate($piece(MAP,"=",1),"""")
				
			} elseif $extract($piece(MAP,"=",1))="P" {
				set $piece(YKEY,",",+$piece(MAP,"=",2)) = $piece(YKEYXX,",",+$extract($piece(MAP,"=",1),2,99))  ;AUS PRIMÄRSCHLÜSSEL
				
			} else {
				set $piece(YKEY,",",+$piece(MAP,"=",2)) = $piece(YFELD,Y,+$piece(MAP,"=",1))  ;AUS DATENFELD ;out of data item 
			}
		}
	}
	if newForm'="" {  ;PRUEFEN FPRMULAR AUF DATENINHALT ;upon 
		set idClass = $piece($get(^WWW120(0,newForm,1)),Y,11)
		
		if idClass'="" {   ;KEINE DATEI ;no data file 
			set YSCHLUES="^"_idClass_"("_$$^WWWYM(idClass,1)
			for YI=1:1 {
				quit:$piece(YKEY,",",YI)=""
				
				set YSCHLUES=YSCHLUES_""""_$piece(YKEY,",",YI)_""""
				if $piece(YKEY,",",YI+1)'="" set YSCHLUES=YSCHLUES_","
			}
			if $extract(YSCHLUES,$length(YSCHLUES))="," set YSCHLUES=$extract(YSCHLUES,1,$length(YSCHLUES)-1)
			if $length(YSCHLUES)'<($length(idClass)+1+$length($$^WWWYM(idClass))) {
				set YSCHLUES=YSCHLUES_")"
				if $DATA(@(YSCHLUES)) { 
					set pstrTitle = "* "_pstrTitle
					if $$$WWW124DisplayFirstDataRecord(YA) {    ;ERSTEN DATENSATZ AUSWÄHLEN ;data record pick out 
						set YNEXKEY = $piece($piece($QUERY(@YSCHLUES),"(",2),")",1)
						set YNEXKEY = $piece(YNEXKEY,",",2,$length(YNEXKEY,",")-1)
						if '$find("-"_$translate(YNEXKEY,""""),"-"_$translate(YKEYXX,"""")) set YNEXKEY = ""
						if YNEXKEY'="" set YKEY = YNEXKEY
					}
				}
			}
		}
	}
	
	write "EP=WWWFORM&amp;YFORM="_newForm
	set YBACK=YBACK_YFORM_","
	
	if $extract($$$WWW124TargetFrameName(YA))="@" new YBACK set YBACK=""    ;OHNE YBACK ;without 
	if $$$WWW124TransferVariableIdentifie(YA)'="" write "&"_$$$WWW124TransferVariableIdentifie(YA)_"="_$get(@($$$WWW124TransferVariableIdentifie(YA)))
	if YTARGETF'="" if YTARGETF'=YTARGET          write "&YLOCKBACK="_YTRAKT  ;AKTUELLE LOCKDATEI BEI SCHLIESSEN ZURÜCKHOLEN
	do ^WWWCGI
	
	$$$EndWindow
	$$$CloseOnClick
	
	write ">"
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
		write "</A>"
	}
	if $$$WWW124DMSFileForTargetHttp(YA)'="" do ABLAGE
	
	quit
	
	
Search(pstrTitle) ; FIXME : Pass blnButtons
	if blnButtons {
		write "<A class=link "
	} else {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""
	}
	
	write " onClick="""
	if $$$WWW124SetVariableYPARA(YA)'="" write "document.WWW.YPARA.value='"_$$$WWW124SetVariableYPARA(YA)_"'; "  ;PARAMETER NEU
	write " ShowSearch();"
	write """>"
	
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
		write "</A>"
	}
	quit
	
				
Help(pstrTitle) ; FIXME : Pass blnButtons
	;-------------------------------------------------------------------------------
	; Show a help button
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 21-Dec-2006	JW		SR14235: Added reference to child window
	;-------------------------------------------------------------------------------
	if blnButtons {
		write "<A class=link "
	} else {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML(pstrText)_""" "
	}
	write " onClick=""subWindow('"_YAKTION_"EP=WWWHELP&amp;YFORM="_YFORM_$$WWWCGI2^WWWCGI($$$YES)_"');"">"
	
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
		write "</A>"
	}
 	quit
 
 
Java(pstrTitle="") ; FIXME : Pass blnButtons
	write $$Java^WWWFORMC2012(pstrTitle)			;SR18033
	;SR18033 ;-------------------------------------------------------------------------------
	;SR18033 ;-------------------------------------------------------------------------------
	;SR18033 if blnButtons {
	;SR18033 	write "<A onClick='return doLink(this)' HREF=""JavaScript:"_YCONF1
	;SR18033 } else {
	;SR18033 	write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""" onClick="""_YCONF1
	;SR18033 }
	;SR18033 if $$$WWW124AutomaticSUBMIT(YA)=1    write "retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','','9');"
	;SR18033 if $$$WWW124SetVariableYPARA(YA)'="" write "document.WWW.YPARA.value='"_$$$WWW124SetVariableYPARA(YA)_"'; "  ;PARAMETER NEU
	;SR18033 write $$$WWW124FunctionJavaScriptOnClick(YA)
	;SR18033 if '$find($$$WWW124FunctionJavaScriptOnClick(YA),";") write ";"
	;SR18033 write YCONF2
	;SR18033 write """"
	;SR18033 if YTARGETF'="" write " TARGET="""_YTARGETF_""""
	;SR18033 write ">"
	;SR18033 if blnButtons {
	;SR18033 	do StopButton^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
	;SR18033 	write "</A>"
	;SR18033 }
	quit
	
 	//++++++++++++++++++++++++++++++++++++++
 	// JW - Don't believe the following are used
 	//++++++++++++++++++++++++++++++++++++++
	
ABLAGE ;SPEICHERN URL IN ZWISCHENDATEI ;Save URL within 
	quit
	
ABLAGE1  
	quit:$length($piece(YVOR,Y,15))<3
	quit:$translate(YKEY,",*+-. """)=""
	
	new YI,YURL,I
	
	set YURL = $piece($get(^WWW012(0,YM,1)),Y,44)
	set YI   = YAKTION
	
	if +$$$WWW124DoNotPrepareHTML(YA)=$$$NO if $$$WWW124ExecuteOnClick(YA)'="" do
	. set YI=YI_"EP=WWWMANU&amp;YFORM="_YFORM
	
	if +$$$WWW124DoNotPrepareHTML(YA)=$$$YES if $$$WWW124ExecuteOnClick(YA)'="" do
	. set YI=YI_"EP=WWWMANU1&amp;YFORM="_YFORM
	
	if $$$WWW124ExecuteOnClick(YA)="" if newForm'="" do
	. set YI=YI_"EP=WWWFORM&amp;YFORM="_newForm
	
	set YBACKX=YBACK
	
	set YBACK=YBACK_YFORM_","
	if $extract($$$WWW124TargetFrameName(YA))="@" new YBACK set YBACK=""   ;TYBD;OHNE YBACK ;without 
	set YI=YI_"&YEXEC="_$translate($$$WWW124ExecuteOnClick(YA),"# ","*|")
	if $$$WWW124TransferVariableIdentifie(YA)'="" set YI=YI_"&"_$$$WWW124TransferVariableIdentifie(YA)_"="_@($$$WWW124TransferVariableIdentifie(YA))
	do VAR^WWWCGI
	set YBACK=YBACKX
	set YI(1)=$piece(YA,Y,16)
	if $extract(YI(1))="/" set YI(1)=$extract(YI(1),2,99)
	if $extract(YI(1),$length(YI(1)))'="/" set YI(1)=YI(1)_"/"
	set YI(1)=YI(1)_$translate(YKEY,",""","/")
	if $extract(YI(1))="/" set YI(1)=$extract(YI(1),2,999)
	if $extract(YI(1),$length(YI(1)))'="/" set YI(1)=YI(1)_"/"
	set YI(3)=YI(1)
	set YI(1)=""
	for I=1:1 quit:$piece(YI(3),"/",I)=""  set YI(1)=$piece(YI(3),"/",I) do
	. if $extract(YI(1))="@" do
	. . if +$extract(YI(1),2,99)'=0 set YI(1)=$piece(YFELD,Y,+$extract(YI(1),2,99)) quit
	. . set YI(1)=$get(@($extract($piece(YI(3),"/",I),2,99)))
	. ;
	. if YI(1)="" set YI(1)="*"
	. set $piece(YI(3),"/",I)=YI(1)
	
	;DATEI SPEICHERN ;data file Save 
	new II
	
	quit:$$$WWW124DMSFileForTargetHttp(YA)=""
	
	set YI(1)=YI(3)
	set YI(2)="^"_$$$WWW124DMSFileForTargetHttp(YA)_"("_$$^WWWYM($$$WWW124DMSFileForTargetHttp(YA))
	for II=1:1 quit:$piece(YI(1),"/",II)=""  set YI(2)=YI(2)_","""_$piece(YI(1),"/",II)_""""
	set YI(2)=YI(2)_",1)"
	set @YI(2)=YI_Y_+$HOROLOG
	
	quit
	
]]></Routine>
</Export>