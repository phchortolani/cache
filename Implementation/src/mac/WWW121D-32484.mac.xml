<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW121D" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include WWWConst
	
	; 12-Feb-2010	shobby	SR17139 Class/Form changed (Do Not remove comment)
	; 22-Oct-2009	shobby	SR16969 Class/Form changed (Do Not remove comment)
	
WWW121D(YKEY)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ÜBERTRAGEN RELATIONEN AUF ALLE
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 05-Jul-2011	GRF		-: Quits; Naked Reference
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	;			 	DT		ÜEBERTRAGEN RELATIONEN AUF ALLE
	;-------------------------------------------------------------------------------
	;DO ^WWWINFO($$^WWWTEXT(32018))  ; "Check Started"
	do ^WWWINFO("All Relations In Other Forms Has Been Changed!")  ;GESTARTET
	new YFORM,YDATEI,YNEWSET
	
	set YFORM   = $$$KEY1($get(YKEY))   quit:YFORM=""
	set YLFN    = $$$KEY2($get(YKEY))   quit:YLFN=""
	set YMANX   = $$$KEY3($get(YKEY))   quit:YMANX=""
	
	set YNEWSET = $piece($get(^WWW121D(0,YFORM,YLFN,YMANX,1)),Y,6)  quit:YNEWSET=""   ; $$$WWW121DFieldDescription  NEUE FELDBEZEICHNUNG
	set YDATEI  = $piece($get(^WWW120(0,YFORM,1)),Y,11)             quit:YDATEI=""    ; $$$WWW120ClassUsedInForm
	
	quit:$order(^WWW002(0,YDATEI,""),-1)'=1  ; only continue if class has a single key
	
	set YOLDSET = $piece(^WWW002(0,YDATEI,YLFN,1),Y,2)               ;$$$WWW002TextInForms 
	if $piece($get(^WWW0021(0,YDATEI,YLFN,SPRACHE,1)),Y,1)'="" {
		set YOLDSET = $piece(^WWW0021(0,YDATEI,YLFN,SPRACHE,1),Y,1)  ;$$$WWW0021PropertyDescription   SPRACHABHÄNGIG
	}
	if YNEWSET'="" if YOLDSET'="" do SETHELP(YFORM,"P",YLFN,SPRACHE,YMANX,YOLDSET,YNEWSET)
	job START^WWW121D(YDATEI,YNEWSET,YOLDSET,YMANX,SPRACHE)::1    ; launch background job
	quit
	
	
START(YDATEI,YNEWSET,YOLDSET,YMANX,YSPRACHE) ; launch background job
	do ^WWWVAR
	if $get(YSPRACHE)'="" set SPRACHE = $get(YSPRACHE)
	
	set YM = YMANX
	if $get(YMANX)="" set YM = 0
	
	set $ztrap="^WWWERROR"
	do LOW^%PRIO
	set YFORM = ""
	for  set YFORM = $order(^WWW122(0,YFORM)) quit:YFORM=""  do
	. set YDATEI1 = $piece($get(^WWW120(0,YFORM,1)),Y,11)
	. set YLFN=""
	. for  set YLFN = $order(^WWW122(0,YFORM,YLFN)) quit:YLFN=""  do
	. . set YFELD = $get(^WWW122(0,YFORM,YLFN,1))
	. . if $piece(YFELD,Y,32)=YDATEI do SET quit
	. . if YDATEI1'="" if +$piece(YFELD,Y,1)'=0 do
	. . . if $piece($get(^WWW003(0,YDATEI1,+$piece(YFELD,Y,1),1)),Y,8)=YDATEI do SET
	
	quit
	
	
SET ;SET WWW122D ;table-mat 
	do
	. new YVOR,YFELD,YDATEI,YKEY
	. set YKEY  = YFORM_","_YLFN_","_YM
	. set YFELD = ""
	. set $piece(YFELD,Y,6) = YNEWSET
	. set YOK = $$^WWWSPEI("WWW122D",YKEY,YFELD,$$$YES)
	
	if YNEWSET'="" if YOLDSET'="" do SETHELP(YFORM,"D",YLFN,SPRACHE,YMANX,YOLDSET,YNEWSET)
	quit
	
	
SETHELP(YFORM,TYPE,YLFN,LANG,YMANX,YOLDSET,YNEWSET) ;SETZEN HELPFILE ;typeset 
	if $get(TYPE)'="P" if $get(TYPE)'="D" quit
	
	new CUSTOMIZE,HELP
	
	set HELP = $piece($get(^WWW127(0,YFORM,TYPE,YLFN,SPRACHE,1)),Y,1)
	
	if TYPE="P" do  quit
	. set CUSTOMIZE = $get(^WWW121D(0,YFORM,YLFN,YMANX,1))  quit:CUSTOMIZE=""   ;KEIN CUSTOMIZEING ;no 
	. set HELP(1) = $piece(CUSTOMIZE,Y,5)                 if HELP(1)'=""  quit  ;SCHON MANUELLE HILFE ;yet succour 
	. set HELP(2) = $$TRANSLATE(HELP,YOLDSET,YNEWSET)     if HELP=HELP(2) quit  ;KEINE ÄNDERUNG ;no alteration 
	. set $piece(^WWW121D(0,YFORM,YLFN,YMANX,1),Y,5) = HELP(2)      ; Custom Help Text
	. set $piece(^WWW121D(0,YFORM,YLFN,YMANX,1),Y,7) = $$$YES       ; Hide Alphalinc Help Text 
	
	if TYPE="D" do  quit
	. set CUSTOMIZE = $get(^WWW122D(0,YFORM,YLFN,YMANX,1))  quit:CUSTOMIZE=""   ;KEIN CUSTOMIZEING ;no 
	. set HELP(1) = $piece(CUSTOMIZE,Y,5)                 if HELP(1)'=""  quit  ;SCHON MANUELLE HILFE ;yet succour 
	. set HELP(2) = $$TRANSLATE(HELP,YOLDSET,YNEWSET)     if HELP=HELP(2) quit  ;KEINE ÄNDERUNG ;no alteration 
	. set $piece(^WWW122D(0,YFORM,YLFN,YMANX,1),Y,5) = HELP(2)      ; Custom Help Text
	. set $piece(^WWW122D(0,YFORM,YLFN,YMANX,1),Y,7) = $$$YES       ; Hide Alphalinc Help Text 
	
	quit
	
	
TRANSLATE(TEXT,VON,NACH)  ;UMSETZTEN
	;-------------------------------------------------------------------------------
	; Find all instances of one string in a text and replace it with another.
	; e.g. if Class has form description of "Edit Item Master Data" might want to
	;      customise as "Edit Stock Definition" instead.  This change would replace
	;      the strings in the help text.
	;-------------------------------------------------------------------------------
	new TEXT1,VON1
	
	set TEXT1(1) = $get(TEXT)
	set TEXT1(2) = $zconvert($get(TEXT),"U")
	set VON1     = $zconvert($get(VON),"U")
	set TEXT1 = TEXT
	for  quit:'$find(TEXT1(2),VON1)  do
	. new YCUT
	. set YCUT     = $find(TEXT1(2),VON1)
	. set TEXT1    = $extract(TEXT1,1,YCUT-1-$length(VON1))_$char(255)_$extract(TEXT1,YCUT,$length(TEXT1))           ;EINFÜGEN PLATZHALTER ;interpolate 
	. set TEXT1(2) = $extract(TEXT1(2),1,YCUT-1-$length(VON1))_$char(255)_$extract(TEXT1(2),YCUT,$length(TEXT1(2)))  ;EINFÜGEN PLATZHALTER ;interpolate 
	
	for  quit:'$find(TEXT1,$char(255))  do
	. new YCUT
	. set YCUT  = $find(TEXT1,$char(255))
	. set TEXT1 = $extract(TEXT1,1,YCUT-2)_NACH_$extract(TEXT1,YCUT,$length(TEXT1))
	
	quit TEXT1
	
	
CustomHelpText(pobjWWW121D)
	;-------------------------------------------------------------------------------
	; If Help Text is a WWW009 reference, return that Text string rather than
	; the Text ID.
	; 
	; 25-May-2011	shobby	SR16925.2: HelpText->CustomHelpText
	;-------------------------------------------------------------------------------
	quit $$PreProcessText^WWWFORMCOMMON(pobjWWW121D,$$$FldWWW121DCustomHelpText)
	
	
CustomTrainingText(pobjWWW121D)
	;-------------------------------------------------------------------------------
	; If Training Text is a WWW009 reference, return that Text string rather than
	; the Text ID.
	; 
	; 25-May-2011	shobby	SR16925.2: IndividualTrainingText->CustomTrainingText
	;-------------------------------------------------------------------------------
	quit $$PreProcessText^WWWFORMCOMMON(pobjWWW121D,$$$FldWWW121DCustomTrainingText)
	
]]></Routine>
</Export>