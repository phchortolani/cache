<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INVALIDLOCATION" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INVALIDLOCATION(ITEM,LOCATION1,DATE,PREISKZ)
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		gives back the upper Location
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 07-Jul-2006	GRF		Doco; quits
	; 02.04.2004	BEC		Created
	;-------------------------------------------------------------------------------
	NEW BETRIEB,TRENN,LOCATION2,SLOCATION,PREISID,YI,YII,YDATE,PREISDATE,YFELD,HLOCATION,LOCATION
	NEW LOCATION,objINVORG        ;BEC;7.07.04;26061
	
	SET ITEM      = $GET(ITEM)
	SET LOCATION1 = $GET(LOCATION1)
	SET DATE      = $GET(DATE)
	SET PREISKZ   = $GET(PREISKZ)
	
	SET PREISDATE = ""
	IF DATE="" SET DATE=+$HOROLOG
	SET BETRIEB   = ""
	SET PREISID   = ""
	
	;quit $$NEW()		SR14568: See new code below
	
	SET SLOCATION = $PIECE($GET(^INVORG(YM,YM,1)),Y,171)         ;STANDARD lOCATION FOR CALU. OF
	SET HLOCATION = $PIECE($GET(^INVORG(YM,YM,1)),Y,172)         ;CALC. HIRACI LOCATION
	
	IF ITEM'="" DO
	. FOR LOCATION=LOCATION1,SLOCATION QUIT:BETRIEB'=""  DO
	. . QUIT:LOCATION=""
	. . FOR TRENN=".","-" DO  QUIT:BETRIEB'=""     ;DFLT.  :--> AB HIER NEU: GGF. VOR PROGRAMMSTAND AUSSCHALTEN ;Confirm. here is pre- eliminate 
	. . . DO:HLOCATION'=1  FOR YI=($LENGTH(LOCATION,TRENN)+1):-1:1 DO  QUIT:BETRIEB'="" 
	. . . . SET LOCATION1=$PIECE(LOCATION,TRENN,1,$GET(YI))      ;BEC;25.05.04;$G(YI) HINZUGEFÜGT
	. . . . FOR LOCATION2=LOCATION1,LOCATION1_TRENN DO  QUIT:BETRIEB'="" 
	. . . . . QUIT:LOCATION2=""
	. . . . . SET YFELD=$GET(^INARTPREISBE(YM,ITEM,LOCATION2,1))          ;BETRIEBSABHÄNIGER PREIS ;price 
	. . . . . QUIT:YFELD=""
	. . . . . QUIT:+$PIECE(YFELD,Y,66)'=0                                ;WENN ARTIKELSPERRE VORHANDEN; ;when on hand 
	. . . . . DO:PREISKZ'=""  IF PREISKZ="" FOR  SET PREISKZ=$ORDER(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ)) QUIT:PREISKZ=""  DO  QUIT:BETRIEB'=""
	. . . . . . IF $DATA(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ)) DO 
	. . . . . . . SET YDATE=DATE+1 FOR YII=1:1:20 SET YDATE=$ORDER(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ,YDATE),-1) QUIT:YDATE=""  QUIT:BETRIEB'=""  DO
	. . . . . . . . SET YFELD=$GET(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ,YDATE,1))
	. . . . . . . . QUIT:YFELD=""
	. . . . . . . . IF $PIECE(YFELD,Y,10)'="" IF $PIECE(YFELD,Y,10)=1    QUIT    ;WENN GESPERRT ;when DISABLED 
	. . . . . . . . IF $PIECE(YFELD,Y,5)'=""  IF $PIECE(YFELD,Y,5)<YDATE QUIT    ;BIS DATUM ;until Date 
	. . . . . . . . IF YDATE'="" IF YDATE>DATE                           QUIT    ;AB DATUM ;Confirm. Date     JW - This line is redundant
	. . . . . . . . IF $PIECE(YFELD,Y,2)=""    IF $PIECE(YFELD,Y,1)=""   QUIT    ;BERECHNETER PREIS  ;price 
	. . . . . . . . SET BETRIEB=LOCATION2
	. . . . . . . . SET PREISID=PREISKZ
	. . . . . . . . SET PREISDATE=YDATE
	
	IF BETRIEB="" SET BETRIEB=$PIECE($GET(^INVORG(YM,YM,1)),Y,171)    ;BEC;08.06.04;25866;$G eingefügt
	IF BETRIEB="" SET BETRIEB=YLOCATION
	QUIT BETRIEB_Y_PREISID_Y_PREISDATE
	
	
NEW()
	set objINVORG = $get(^INVORG(YM,YM,1))
	set SLOCATION=$$$INVORGLocationForStandardCalcul(objINVORG)         ;STANDARD lOCATION FOR CALU. OF
	set HLOCATION=$$$INVORGCalulationwithLocationHie(objINVORG)         ;CALC. HIRACI LOCATION
	
	if ITEM'="" {
		for LOCATION=LOCATION1,SLOCATION {
			continue:LOCATION=""
			
			for TRENN=".","-" {
				if 'HLOCATION {
					do GetPrice()
				}					// FIXME JW - I think this should be an else...
				for YI=($length(LOCATION,TRENN)+1):-1:1 {
					do GetPrice()
					quit:BETRIEB'=""
				}
				quit:BETRIEB'=""     ;DFLT.  :--> AB HIER NEU: GGF. VOR PROGRAMMSTAND AUSSCHALTEN ;Confirm. here is pre- eliminate 
			}
			quit:BETRIEB'=""
		}
	}
	
	if BETRIEB="" set BETRIEB=SLOCATION    ;BEC;08.06.04;25866;$G eingefügt
	if BETRIEB="" set BETRIEB=YLOCATION
	
	quit BETRIEB_Y_PREISID_Y_PREISDATE
 
GetPrice() private
 	
	set LOCATION1=$piece(LOCATION,TRENN,1,$get(YI))      ;BEC;25.05.04;$G(YI) HINZUGEFÜGT
	
	for LOCATION2=LOCATION1,LOCATION1_TRENN {
		continue:LOCATION2=""
		set YFELD=$get(^INARTPREISBE(YM,ITEM,LOCATION2,1))          ;BETRIEBSABHÄNIGER PREIS ;price 
		continue:YFELD=""
		continue:+$$$INARTPREISBEItemDisabled(YFELD)'=0                                ;WENN ARTIKELSPERRE VORHANDEN; ;when on hand 
		
		if PREISKZ'="" {
			do Sub2
		} else {
			FOR {
				set PREISKZ=$order(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ))
				quit:PREISKZ=""
				do Sub2
				quit:BETRIEB'=""
			}
		}
		quit:BETRIEB'="" 
	}
	quit
 
 
Sub2 private
	
	if $data(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ)) {
		set YDATE=DATE+1
		for YII=1:1:20 {                                                          // JW - Why hardcode to look at 20 prices?
			set YDATE=$order(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ,YDATE),-1)
			quit:YDATE=""
			
			set YFELD=$get(^INARTPREISBE1(YM,ITEM,LOCATION2,PREISKZ,YDATE,1))
			continue:YFELD=""
			continue:$$$INARTPREISBE1PriceIsDisabled(YFELD)                                         ;WENN GESPERRT ;when DISABLED 
			continue:($$$INARTPREISBE1ValidTo(YFELD)'="")&&($$$INARTPREISBE1ValidTo(YFELD)<YDATE)   ;BIS DATUM ;until Date 
			continue:($$$INARTPREISBE1Entered(YFELD)="")&&($$$INARTPREISBE1Base(YFELD)="")          ;BERECHNETER PREIS  ;price 
			
			set BETRIEB   = LOCATION2
			set PREISID   = PREISKZ
			set PREISDATE = YDATE
			quit					// Found Price
		}
	}
 
	quit
]]></Routine>
</Export>