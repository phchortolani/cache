<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INKUNDEARTSPERR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INKUNDEARTSPERR(KUN,ART,LIEF)
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		SPERREN ARTIKEL FÜR KUNDEN
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;| FIS	15.02.2005  
	;\------------------------------------------------------------------/
	IF '$D(YM) DO ^WWWVAR
	DO
	. LOCK +^INPROPJOB(YM):0 IF '$TEST  QUIT  ;PRÜFEN OB JOB LÄUFT
	. LOCK -^INPROPJOB(YM)
	. DO ^INPROPJOB  ;JOB STARTEN
	
	SET ^INARTJOB(YM,$$^WWWNEXT("INARTJOB"),1)="DO SPERR^INKUNDEARTSPERR("_""""_$GET(KUN)_""""_","_""""_$GET(ART)_""""_","_""""_$GET(LIEF)_""""_")"  ;EXECUTE
	QUIT
	
	
SPERR(KUN,ART,LIEF)
	NEW ART1,SPERR
	
	SET KUN  = $GET(KUN)    ;CUSTOMER
	SET LIEF = $GET(LIEF)   ;SUPPLIER
	SET ART  = $GET(ART)    ;ITEM
	
	IF ART'="" DO SPERR1 QUIT  ;EIN ARTIKEL, EIN ODER ALLE KUNDEN
	IF LIEF'="" DO  ;ALLE ARTIKEL EINES LIEFEERANTEN, EIN ODER ALLE KUNDEN
	. SET LIEF(1)=$$^WWWUMLAU(LIEF,1)
	. SET ART="" FOR  SET ART=$ORDER(^INARTKs(YM,1,LIEF(1),ART)) QUIT:ART=""  DO
	. . DO SPERR1
	
	IF LIEF="" SET ART="" FOR  SET ART=$ORDER(^INART(YM,ART)) QUIT:ART=""  DO SPERR1  ;ALLE ARTIKEL, 1.LIEFERANT AUS INARTK,  EIN ODER ALLE KUNDEN
	QUIT
	
	
SPERR1
	IF ART'="" DO
	. SET ART1=$GET(^INART(YM,ART,1))
	. DO:KUN'=""  IF KUN="" FOR  SET KUN=$ORDER(^INKUNDE(YM,KUN)) QUIT:KUN=""  DO
	. . SET SPERR=0
	. . DO
	. . . SET KUN1=$GET(^INKUNDE(YM,KUN,1))
	. . . IF $PIECE(KUN1,Y,257)=1 IF $PIECE(ART1,Y,66)'=0 SET SPERR=1 QUIT  ;ALLE NICHT FREIEN ARTIKEL SPERREN
	. . . IF $PIECE(KUN1,Y,256)'="" IF $PIECE(ART1,Y,66)'="" IF $FIND(";"_$TRANSLATE($PIECE(KUN1,Y,256),",",";")_";",";"_$PIECE(ART1,Y,66)_";") SET SPERR=1 QUIT  ;GESPERRTER ARTIKEL-FLAG
	. . . IF $PIECE(KUN1,Y,258)=1 DO  ;ARTIKEL OHNE LF-RAHMENVERTRAG SPERREN
	. . . . NEW RV,RV1,LIEF1
	. . . . SET LIEF1=$GET(LIEF)
	. . . . IF LIEF1="" SET LIEF1=$ORDER(^INARTK(YM,ART,""))
	. . . . IF LIEF1="" SET SPERR=1 QUIT  ;KEIN LIEFERANT
	. . . . SET RV=$PIECE($GET(^INKUNDELIEF(YM,KUN,LIEF1,1)),Y,34)  ;LIEFERANTEN-RAHMENVERTRAGSNUMMER
	. . . . IF RV="" SET SPERR=1 QUIT  ;KEIN RAHMENVERTRAG
	. . . . SET RV1=$GET(^INRAHMEN(YM,LIEF1,RV,1))
	. . . . IF RV1="" IF '$DATA(^INRAHMENGROUP(YM,LIEF1,RV)) SET SPERR=1 QUIT  ;KEINE DATEN
	. . . . IF $PIECE(RV1,Y,8)'="" IF $PIECE(RV1,Y,8)>$HOROLOG SET SPERR=1 QUIT  ;NOCH NICHT GÜLTIG
	. . . . IF $PIECE(RV1,Y,9)'="" IF $PIECE(RV1,Y,9)<$HOROLOG SET SPERR=1 QUIT  ;NICHT MEHR GÜLTIG
	. . ;
	. . IF SPERR=1 IF '$DATA(^INKUNDEARTSPERR(YM,KUN,ART)) DO  ;ARTIKEL SPERREN
	. . . NEW YFORM,YVOR,YOK
	. . . SET YOK=$$^WWWSPEI("INKUNDEARTSPERR",KUN_","_ART,"",1)  ;WWWSPEI WEGEN BITMAP
	. . ;
	. . IF SPERR=0 IF $DATA(^INKUNDEARTSPERR(YM,KUN,ART)) DO  ;ARTIKEL FREIGEBEN
	. . . DO ^WWWSKILL("INKUNDEARTSPERR",KUN_","_ART)  ;WWWSKILL WEGEN BITMAP
	
	QUIT
	
]]></Routine>
</Export>