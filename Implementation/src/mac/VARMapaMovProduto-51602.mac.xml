<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARMapaMovProduto" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARMapaMovProduto
	#include INConst
	#include WWWConst
	#include VARConst
	
	#define MesInicial $$$VARParametroClienteMesSaldoInicialReceb($get(^VARParametroCliente(0,0,1)))
	#define AnoInicial $$$VARParametroClienteAnoSaldoInicialReceb($get(^VARParametroCliente(0,0,1)))

	quit
	
OnAfterSave
	new Local, Produto, DataInicial, DataFinal
	
	kill ^VARTempMapaMovProduto(YM,YBED)
	
	set DataInicial	= $get(VORG(1))
	set DataFinal	= $get(VORG(2))
	set Produto 	= $get(VORG(3))
	set Local 		= $get(VORG(4))
	
	set $piece(^VARTempMapaMovProduto(YM,YBED,1),Y,1) = DataInicial
	set $piece(^VARTempMapaMovProduto(YM,YBED,1),Y,2) = DataFinal
	set $piece(^VARTempMapaMovProduto(YM,YBED,1),Y,3) = Produto
	set $piece(^VARTempMapaMovProduto(YM,YBED,1),Y,4) = Local
	
	set MesInicial = $$SQLGetMes^VARSQL(DataInicial)
	set AnoInicial = $$SQLGetAno^VARSQL(DataInicial)
	
	set MesFinal = $$SQLGetMes^VARSQL(DataFinal)
	set AnoFinal = $$SQLGetAno^VARSQL(DataFinal)
	
	if ((DataInicial = "")||(DataFinal = "")||(Produto = "")){
		$$$Alert("Preencha todos os campos obrigatórios.")
		do GoToForm^COMUtilForm("VARMapaMovProduto","",,,,,)
		quit
	} elseif ($$CheckFechamentoContabil^VAREntradaNotaFiscal(MesInicial,AnoInicial) = $$$NO){
		$$$Alert("A data inicial não está contida nos meses/anos que já foi gerado o fechamento contábil.")
		do GoToForm^COMUtilForm("VARMapaMovProduto","",,,,,)
		quit
	} elseif ($$CheckFechamentoContabil^VAREntradaNotaFiscal(MesFinal,AnoFinal) = $$$NO){
		$$$Alert("A data final não está contida nos meses/anos que já foi gerado o fechamento contábil.")
		do GoToForm^COMUtilForm("VARMapaMovProduto","",,,,,)
		quit
	} elseif (DataInicial > DataFinal){
		$$$Alert("A data inicial não pode ser maior que a data final.")
		do GoToForm^COMUtilForm("VARMapaMovProduto","",,,,,)
		quit
	}
	
	do RunReportMapaMovProduto^VARJasperRunReportContinuacao(DataInicial,DataFinal,Produto,Local)

	quit
	
GetQuantInicialProduto(pDataInicial,pLocal,pProduto)
	new ValorTotal, DataFinalAnterior
	
	set ValorTotal 			= 0
	set DataFinalAnterior  	= ""
	
	quit:((pDataInicial="")||(pLocal="")||(pProduto="")) ValorTotal
		
	set DataFinalAnterior = $zdateh($SYSTEM.SQL.DATEADD("dd",-1,pDataInicial),3)
	
	&sql(
		SELECT	SUM(QtyMoved)
		INTO	:ValorTotal
		FROM	alSOH.dStockHistory
		WHERE	Item = :pProduto
		AND		Location = :pLocal
		AND		DateConfirmed <= :DataFinalAnterior
		AND 	(NonStock IS NULL or NonStock = 0)
	)
	
	set:(ValorTotal="") ValorTotal = 0
	
	quit ValorTotal
	
GetPeriodoInicialFechamento(pData)
	new periodoInicial,blnPeriodoInicial
	
	set periodoInicial 		= $$SQLGetUltimoDiaMes^VARSQL($$$MesInicial,$$$AnoInicial)
	set blnPeriodoInicial 	= $$$NO
	
	if ( pData <= periodoInicial ){
		set blnPeriodoInicial = $$$YES
	}
	
	quit blnPeriodoInicial
	
GetPatientReq(pMov,pDoc)
	quit:((pMov'="REQUISICAO")||(pDoc="")) ""
	
	$$$VAR
	
	set objIssue = $get(^INIssue(YM,pDoc,1))
	set patientID = $$$INIssueCustomerName(objIssue)
	
	quit:(patientID="") ""
	
	quit $$SQLGetPatientName^VARSQL(patientID)
]]></Routine>
</Export>