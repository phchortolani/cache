<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMTable
	;-------------------------------------------------------------------------------
	; NOTE : Some variables (e.g. gblnUsePaging) are not newed.  They retain their
	;        existence during processing and are killed when processing is complete.
	; 
	; History:
	; 03-Dec-2009	GRF		Boolean Macros; coding standards
	; 04-Nov-2004	GRF		Add doco
	; 13-May-2004	GRF		include COMSYS rather than SYSDisc
	;-------------------------------------------------------------------------------
	
#include COMSYS
	
DisplayGrid(pobjData,pblnIncludeNewLineLink=1,pstrOrder="ASC")
	;-------------------------------------------------------------------------------
	; Builds a grid out of the passed in data
	;
	; History:
	; 12-Mar-2003	shobby	If already posted don't display the 'New' line.
	; 10-Mar-2003	shobby	Allow grid sort order to be controlled.
	; 07-Mar-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new blnAllowLinks,blnNewLine,blnOrder,idx,lstHeadings,strHighlightIdx
	
	set pstrOrder = $zconvert(pstrOrder,"U")
	do Start(@pobjData@("!!Headings"),@pobjData@("!!Title"))
	set blnAllowLinks   = '$get(@pobjData@("!!Posted"))
	set blnOrder        = $select(pstrOrder="ASC":1,pstrOrder="DESC":-1,1:1)
	set strHighlightIdx = $get(@pobjData@("!!Highlight"))
	
	set idx = ""          ; FIXME : Brace format; idx is prefix not variable name
	for  set idx = $order(@pobjData@("!!Data",idx),blnOrder) quit:idx=""  do
	. set blnNewLine = $listget(@pobjData@("!!Data",idx,"Date"),1)
	. if (blnNewLine'="[New]") || (pblnIncludeNewLineLink&blnAllowLinks) do
	. . do NewLine($select(strHighlightIdx=idx:"lightgreen",1:""))
	. . do PackageLine(pobjData,idx,@pobjData@("!!Headings"),blnAllowLinks)
	. . do EndLine()
		
	do Stop()
	quit
 
 
PackageLine(pobjData,pidx,plstHeadings,pblnAllowLinks=$$$YES)
	;-------------------------------------------------------------------------------
	; Pull apart each line and convert to cells in a grid
	;
	; History:
	; 13-May-2004	GRF		Clarify If Test
	; 12-Mar-2003	shobby	Don't display a line based on a parameter pblnAllowLinks.
	; 07-Mar-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new blnClearIfZero,i,intColumns,objCell,strAlignment,strData,strLinkForm
	new strLinkKey,strLinkParameters,strType
	
	set intColumns = $listlength(plstHeadings)          ; FIXME : Brace format
	for i=1:1:intColumns do
	. set objCell=$get(@pobjData@("!!Data",pidx,$listget(plstHeadings,i)))
	. if $$$ALWAYS do  ;if objCell'="" do
	. . set strData=$listget(objCell,1)
	. . set strType=$listget(objCell,2)
	. . set strLinkForm=""
	. . set strLinkKey  =""
	. . set strLinkParameters=""
	. . if pblnAllowLinks do
	. . . set strLinkForm      =$listget(objCell,3)
	. . . set strLinkKey       =$listget(objCell,4)
	. . . set strLinkParameters=$listget(objCell,5)
	. . set strAlignment  =$listget(objCell,6)
	. . set blnClearIfZero=$listget(objCell,7)
	. . if strType="Text"     do InsertCell(strData,strLinkForm,strLinkKey,strLinkParameters,strAlignment)
	. . if strType="Date"     do InsertCellDate(strData,strLinkForm,strLinkKey,strLinkParameters)
	. . if strType="Boolean"  do InsertCellBoolean(strData,strLinkForm,strLinkKey,strLinkParameters)
	. . if strType="Currency" do InsertCurrencyCell(strData,blnClearIfZero,strLinkForm,strLinkKey,strLinkParameters)
	. . if strType=""         do InsertCell("")
	quit
	
FormatCurrency(pstrAmount,pblnHideZeros=$$$NO,pstrCurrencyOverride="",pidLang="")
	;-------------------------------------------------------------------------------
	; Wrapper for WWWZAHL. Adds currency symbol and formats an amount to
	; how the system is currently set up.
	; 
	; History:
	; 23-Mar-2005	PO		SR11349 FC support
	; 23-Mar-2005	RPW		SR10961: Allow the users to pass in a different currency
	; 							so that if we are not using the default currency this
	; 							still works.
	; 24-Feb-2005	SteveS	Commented - cleaned up If test. 
	;-------------------------------------------------------------------------------
	new strResult,strCurrency,strCVRate,strJustify
	
	set strResult=""
 
	;if pstrCurrencyOverride="" {
	;	if $case($$$CurrencyToUse,"Foreign":1,"Either":1,:0) {
	;		set pstrCurrencyOverride=$$$FCur(pstrAmount)
	;		set pstrAmount=$$$FCAmount(pstrAmount)
	;	}
	;}
 
	if $get(pstrAmount)'="" {
		if ('pblnHideZeros && (+pstrAmount=0)) set pstrAmount = 0.00001
		if ( pblnHideZeros && (+pstrAmount=0)) set pstrAmount = ""
		set strCurrency = $select(pstrCurrencyOverride="":YWHR,$$$YES:pstrCurrencyOverride)
		set strCVRate   = ""
		if pstrCurrencyOverride'="" set strCVRate = 1
		set strJustify = strCVRate
		if pidLang'=""    set SPRACHE = pidLang
		if pstrAmount'="" set strResult = $$^WWWZAHL(pstrAmount,0,2,strCurrency,"",strCVRate)
	}
	
	quit strResult
	
	
InsertCell(pstrText, pstrForm="", pidTran="",pstrParam="",pstrAlignment="",
		plngPage="",plngSpan="",pintSize=2,pstrWindow="",pblnWrap=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Inserts a cell into a grid
    ; 
    ; Params :
    ;    1 pstrText			Cell - Contents (Link - Hyperlink)
    ;    2 pstrForm			Link - YFORM (Form name)
    ;    3 pidTran			Link - YKEY
    ;    4 pstrParam		Link - YPARA & YAUSWAHL
    ;    5 pstrAlignment	Cell - ALIGN					[LEFT, RIGHT, CENTER]
	;    6 plngPage			Link - YSEITE (Page/Tab Number)
	;    7 plngSpan			Cell - COLSPAN
	;    8 pintSize			Cell - font-size				[1-7]
	;    9 pstrWindow		Link - new window
	;   10 pblnWrap			Cell - NOWRAP
	;  
    ; History:
    ; 04-Jan-2012	shobby	SR17973: Removed &nbsp
    ; 20-Dec-2006	JW		SR14235: Changed param from pstrExtra to pstrWindow.
    ; 28-Feb-2006	JW		SR13697: Added spaces
    ; 20-Jan-2006	GRF		Doco
    ; 21-Sep-2005	JW		SR13309: If text is all spaces convert.
    ; 13-Sep-2005	RPW		SR13306: Increase the column count for non paging as well.
    ; 12-Apr-2005	SS		SR10923: Added pintSize
    ; 01-Mar-2004   PK		Added Column Span Param
    ;-------------------------------------------------------------------------------
	if $get(pstrAlignment)=""      set pstrAlignment = "Left"
	if $$IsList^COMUtils(pstrText) set pstrText      = $$DecodeError^COMUtilError(pstrText)
	
	if $translate(pstrText,$$$SPACE)="" set pstrText = "&nbsp;"
	
	if gblnUsePaging {
		set @gStorage@(glngRow,glngColumn) = pstrText_Y_pstrForm_Y_pidTran_Y_pstrParam_Y_pstrAlignment_Y_plngPage_Y_plngSpan
		set glngColumn = glngColumn+1
	
	} else {
		do NewField(pstrAlignment,gColour,plngSpan,pintSize,pblnWrap)
		;write "&nbsp;"
		if pidTran'="" {
			do InsertLink(pstrForm,pidTran,pstrText,$get(pstrParam),$get(plngPage),,pstrWindow)
		} else {
			write pstrText
		}
		;write "&nbsp;"
		set glngColumn = glngColumn+1
		do EF^WWWTAB
	}
	quit
	
 
NewField(pstrAlignment,pstrColour,plngSpan="",pintSize=2,pblnWrap=$$$NO)
	;-------------------------------------------------------------------------------
	; 06-Jul-2011	shobby	SR18056: Padding changes from Brasil.
	; 21-May-2010	shobby	SR17315: IE and FF manage absolute-size fonts differently
	; 13-Sep-2005	RPW		SR13306: Parse the font size into a style format. 
	; 							This is used as the font information here is overwritten by innerText.
	; 							Added an id to the cell or (Row * 100) + Col
	; 12-Apr-2005	SteveS	SR10923: Use pintSize (was hard-coded to 2)
	;-------------------------------------------------------------------------------
	new strSize
	
	;set strSize=$case(pintSize,1:"xx-small",2:"x-small",3:"small",4:"medium",5:"large",6:"x-large",7:"xx-large",:"small")
	set strSize=$case(pintSize,1:"7.5pt",2:"10pt",3:"12pt",4:"13.5pt",5:"18pt",6:"24pt",7:"36pt",:"12pt")
 	write YCR,"<TD"_$select('pblnWrap:" NOWRAP",1:"")_" VALIGN=TOP STYLE='font-size:"_strSize_"; padding:1px 2px;' "
 	
 	if plngSpan'="" {
	 	write "COLSPAN="_plngSpan_" "
 	}
	write "id=td"_((glngRow*100)+glngColumn)_" " ; SR13306
 	write "ALIGN="_pstrAlignment_" BGCOLOR="_pstrColour_">"
 
 	quit
 
InsertCellPercentage(pstrValue,pstrForm="",pidTran="",pstrParam="",plngPage="")
	;-------------------------------------------------------------------------------
	; History:
	; 19-Feb-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	set pstrValue=$$Round^COMUtils((pstrValue*100))_" %"
 
	do InsertCell(pstrValue,pstrForm,pidTran,pstrParam,"Right",plngPage)
	
	quit
	
	
InsertCurrencyCell(pstrValue,pblnClearIfZero=$$$NO,pstrForm="",pidTran="",pstrParam="",plngPage="",plngSpan="",pstrSize=2)
	;-------------------------------------------------------------------------------
	; History:
	; 23-Mar-2006	PO		SR14407: Added plngSpan & pstrSize
	; 19-Feb-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	if (+pstrValue=0) && pblnClearIfZero {
	  	set pstrValue = "" 
	} else {
		set pstrValue = $$FormatCurrency(pstrValue)
	}
	
	do InsertCell(pstrValue,pstrForm,pidTran,pstrParam,"Right",plngPage,plngSpan,pstrSize)
	
	quit
 
 
InsertCellDate(pstrValue="",pstrForm="",pidTran="",pstrParam="",plngPage="")
	;-------------------------------------------------------------------------------
	; History:
	; 19-Feb-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new strDateTime
	
	set strDateTime = $$^WWWDATE(pstrValue)
	if $piece(pstrValue,",",2)'="" set strDateTime = strDateTime_" "_$$^WWWTIME(pstrValue)
	
	do InsertCell(strDateTime,pstrForm,pidTran,pstrParam,"Left",plngPage)
	
	quit
	
	
InsertCellTime(pstrValue="",pstrForm="",pidTran="",pstrParam="",plngPage="")
	;-------------------------------------------------------------------------------
	; History:
	; 31-Jan-2006 	JW		SR14215: Included in patch
	; 07-Dec-2005	SteveS	Creation
	;-------------------------------------------------------------------------------
	new strTime
	
	set strTime = $$^WWWTIME(pstrValue)
 
	do InsertCell(strTime,pstrForm,pidTran,pstrParam,"Left",plngPage)
	quit
	
 
InsertCellBoolean(pblnValue,pstrForm="",pidTran="",pstrParam="",plngPage="",pblnSuppressNo=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Inserts a boolean cell
    ;
    ; History:
    ; 14-Jan-2004	Paul K	Added pblnSuppressNo
	; 19-Feb-2003	shobby	Creation
    ;-------------------------------------------------------------------------------
	new strValue
	
	set pblnValue = +pblnValue
	set strValue  = $get(^WWW100(0,"JA/NEIN",SPRACHE,pblnValue,1))
	if pblnSuppressNo && 'pblnValue set strValue = ""
	do InsertCell(strValue,pstrForm,pidTran,pstrParam,"Left",plngPage)
	quit
	
	
InsertCellCheckbox(pstrText,pstrRoutine,pblnIsChecked,pblnReadOnly)
    ;-------------------------------------------------------------------------------
    ; Add a checkbox to the COMTable
    ;
    ; Params:
    ; pstrText     : The text which can be anything, ie a key
    ; pstrRoutine  : The routine to call back, it gets passed two values, the text from the
    ; 			     previous parameter and the checked state of the checkbox
    ; pblnIsChecked: If this radio button is selected
    ; pblnReadOnly : If this checkbox is readonly
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Sep-2005	RPW		SR13304: Created
    ;-------------------------------------------------------------------------------
	do InsertCell("<input type=checkbox "_$select(pblnReadOnly:"DISABLED",1:"")_" "_$select(pblnIsChecked:"CHECKED",1:"")_" id=cb"_((glngRow*100)+glngColumn)_" onclick='EventValue("""_YUCI_""","""_YUSER_""","""_YFORM_""",""FIX"","""_pstrRoutine_""","""_pstrText_""",""6"",this.checked+"",""+this.id);'>",,,,"Center")
	quit
	
	
InsertCellRadioButton(pstrText,pstrRoutine,pblnIsChecked,pblnReadOnly,pstrGroup="") 
    ;-------------------------------------------------------------------------------
    ; Add a radio button to the COMTable
    ;
    ; Params:
    ; pstrText     : The text which can be anything, ie a key
    ; pstrRoutine  : The routine to call back, it gets passed two values, the text from the
    ; 			     previous parameter and the checked state of the checkbox
    ; pblnIsChecked: If this radio button is selected
    ; pblnReadOnly : If this radio button is readonly
    ; pstrGroup    : The name of the group of radio buttons, to which this belongs
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Sep-2005	RPW		SR13304: Created
    ;-------------------------------------------------------------------------------
  	if pstrGroup'="" set pstrGroup = "name="_pstrGroup
 	do InsertCell("<input type=radio "_$select(pblnReadOnly:"DISABLED",1:"")_" "_$select(pblnIsChecked:"CHECKED",1:"")_" "_pstrGroup_" id=rb"_((glngRow*100)+glngColumn)_" onclick='EventValue("""_YUCI_""","""_YUSER_""","""_YFORM_""",""FIX"","""_pstrRoutine_""","""_pstrText_""",""6"",this.checked);'>",,,,"Center")
	quit
	
	
InsertCellCallback(pstrText,pstrRoutine,pstrParams="")	// NOT IN USE
	;-------------------------------------------------------------------------------
	; Write a hyperlink which performs a cach√© callback (instead of
	; going to another form)
	;
	; Params:	pstrText		: The text for the link
	; 			pstrRoutine		: The routine to run (X^Y)
	; 			pstrParams		: The parameters to use (p1,p2,...,pn)
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 21-Feb-2006	SteveS	SR14110: Created
	;-------------------------------------------------------------------------------
	new intNumParams,loop,strHTML
	
	set intNumParams = $length(pstrParams,",")
	set strHTML="<a onclick='doLink(this);' href='javascript:CallBack("""_pstrRoutine_""","
	
	for loop=1:1:intNumParams {
		set strHTML = strHTML_""""_$piece(pstrParams,",",loop)_""""
		if (intNumParams'=1) && (loop'=intNumParams) set strHTML = strHTML_","
	}
	
	set strHTML = strHTML_");'>"_pstrText_"</a>"
	do InsertCell(strHTML)
	quit
	
	
InsertCellJS(pstrText,pstrJS="")
	;-------------------------------------------------------------------------------
	; Write a hyperlink which performs JS (instead of going to another form)
	;
	; Params:	pstrText		: The text for the link
	; 			pstrJS			: The JS to run
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 01-Mar-2006	JW		SR14110: Created (replaced routine above)
	;-------------------------------------------------------------------------------
	do InsertCell("<a href=""#"" onClick="""_pstrJS_""">"_pstrText_"</a>")
 
	quit
	
	
 
InsertLink(pstrForm,pidTran,pstrText,pstrParam="",plngPage="",pblnQuitWithString=$$$NO,pstrWindow="")
	;-------------------------------------------------------------------------------
	;
	; Params:	pstrWindow - if not null, open in this window 
	;
	; Returns:
	;
	; History:
    ; 20-Dec-2006	JW		SR14235: Added reference to child window
    ; 11-Nov-2005	JW		SR11904: Child user for popups 
    ;-------------------------------------------------------------------------------
	new ipio,io,Line,strBack,strText,strURL
	
	set strText = ""
	set strBack = $get(YBACK)
	if pidTran="""+""" set pidTran = ""
	set pstrParam = $zconvert(pstrParam,"o","HTML")
	; only add YFORM to the stack if not last element
	if (($piece(strBack,",",$length(strBack,",")-1)'=YFORM)&&(YFORM'=pstrForm)) {
		set strBack = strBack_YFORM_","
	}
	
	if pblnQuitWithString {
		set ipio = $io
		set io   = "temp.txt"
		open io:"wns":0
		use io
	}
	
	new YFORM,YKEY,YBACK,YPARA
	
	set strURL = YAKTION_"EP=WWWFORM&amp;YFORM="_pstrForm_"&amp;YKEY="_pidTran_"&amp;YBACK="_strBack
	if $get(pstrParam)'="" {
		$$$Append(strURL,"&amp;YPARA="_pstrParam)
		$$$Append(strURL,"&amp;YAUSWAHL="_pstrParam)
	}
	if $get(plngPage)'="" {
		new YSEITE
		$$$Append(strURL,"&amp;YSEITE="_plngPage)
	}

	$$$Append(strURL,$$WWWCGI2^WWWCGI(pstrWindow'=""))
	
	if pstrWindow'="" {
		write "<a href='' onclick=""subWindow('"_strURL_"','"_pstrWindow_"'); return false;"">"
	} else {
		write "<a href='"_strURL_"'>"
	}
	
	write pstrText
	write "</A>"
 	
	if pblnQuitWithString {
		close io
		open io:"rs":0
		do $ZUTIL(68,40,1)
		use io
		for {
			quit:$zeof=-1
			read Line
			set strText=strText_Line
		}
		use ipio
		close io
		do $ZUTIL(140,5,io) ; Remove File
	}
	quit strText
	
	
InitialiseGrid(pblnRefreshEveryTime)
	;-------------------------------------------------------------------------------
	;
	;-------------------------------------------------------------------------------
	set gStorage = "^mtemp(""ManualQuery"","_$job_")"
	if ($get(@gStorage@("Key"))'=(YKEY_Y_YFORM_Y_$get(YSEITE)))!(pblnRefreshEveryTime) kill @gStorage
	set @gStorage@("Key")=YKEY_Y_YFORM_Y_$get(YSEITE)
	set glngColumn = 1
	set glngRow    = 0
	quit
	
	
Start(pstrColumns,pstrHeaderText="",pblnUsePaging=$$$NO,pblnRefreshEveryTime=$$$YES,
	  pblnFullWidth=$$$YES,pblnFormFeed=$$$YES,pintSize=2,pintCaptionSize)
	;-------------------------------------------------------------------------------
	; Start building a table
	;   pstrColumns				List of column headings
	;   pstrHeaderText			Table Title line
	;   pblnUsePaging			Instead of just displaying cells as they come in,
	;   						store the cell to be displayed at end
	;   pblnRefreshEveryTime
	;   pblnFormFeed			Print every COMTable on a new page
	;   pintSize				Size of header text (was hard-coded to 2 previously)
	;   pintCaptionSize			Size of table caption text
	; 
	; History:
	; 30-Jan-2007	JW		SR15358 : Rewrote in brace.
	; 23-Mar-2006	PO		SR14407 : Added pintCaptionSize - separate caption size from column heading size
    ; 28-Feb-2006	JW		SR13697 : Added spaces
	; 11-Apr-2005	SteveS	SR12093 : Added pblnFormFeed and pintSize
	; 16-Nov-2004	GRF		SR10478 : Add pblnFullWidth
	;-------------------------------------------------------------------------------
	new blnContinue,intTotalCols,loop,strColumnHeader,strHeading
 
	set gblnUsePaging = pblnUsePaging
	do InitialiseGrid(pblnRefreshEveryTime)
	
	set intTotalCols = $listlength(pstrColumns)
	set strHeading   = ""
	
 	for loop=1:1:intTotalCols {
		set:strHeading'="" strHeading = strHeading_Y
		set strColumnHeader = $listget(pstrColumns,loop)
		if $$IsList^COMUtils(strColumnHeader) set strColumnHeader=$$DecodeError^COMUtilError(strColumnHeader)
		set strHeading = strHeading_"&nbsp;"_strColumnHeader_"&nbsp;"
 	}
 	
	; the header may be a string constant in a list build
	if $get(pstrHeaderText)'="" {
		if $$IsList^COMUtils(pstrHeaderText) {
			set pstrHeaderText = $$DecodeError^COMUtilError(pstrHeaderText)
		}
	}
 
	set blnContinue = $$$YES
	if gblnUsePaging {
		if $data(@gStorage@("Header")) {
			set blnContinue = $$$NO
			; pump out what is currently in the buffer without re-getting data
			; (assuming that the developer uses this start as a conditional)
			do Stop()
		} else {
			set @gStorage@("Header")     = strHeading
			set @gStorage@("HeaderText") = pstrHeaderText
		}
	
	} else {
		if $data(pintCaptionSize)#2 { ; SR14407
			do Header(strHeading,pstrHeaderText,pblnFullWidth,pblnFormFeed,pintSize,pintCaptionSize)
		} else {
			do Header(strHeading,pstrHeaderText,pblnFullWidth,pblnFormFeed,pintSize)
		}
	}
	quit blnContinue
	
	
Header(pstrHeading,pstrHeaderText,pblnFullWidth=$$$YES,pblnFormFeed=$$$YES,pintSize=2,pintCaptionSize)
	;-------------------------------------------------------------------------------
	; History:
	; 23-Mar-2006	PO		SR14407: Added pintCaptionSize
	; 11-Apr-2005	SteveS	SR12093 : Add pblnFormFeed and pintSize
	; 16-Nov-2004	GRF		SR10478 : Add pblnFullWidth; missing quit
	; 02-Jul-2003	SCR		Unnewed YDDSATZ
	;-------------------------------------------------------------------------------
	new ANZM
	
	set ANZM    = 0
	set YDDSATZ = 1  ; This should not be newed *********************
	
	if pblnFormFeed do FF^WWWW()   ;FORMFEED
	if $data(pintCaptionSize)#2 { ; SR14407
		if pstrHeaderText'="" write YCR,"<FONT SIZE="_pintCaptionSize_"><b>"_pstrHeaderText_"</b>"
	} else {
		if pstrHeaderText'="" write YCR,"<FONT SIZE="_pintSize_"><b>"_pstrHeaderText_"</b>"
	}
	
	if pblnFullWidth {
		do START100^WWWTAB
	} else {
		do START1^WWWTAB
	}
	
	do HEADER^WWWTAB(pstrHeading,pintSize) ;Use pintSize
	
	quit
	
	
Footer(pstrColumns)
	;-------------------------------------------------------------------------------
	; 
	;-------------------------------------------------------------------------------
	new intCount,loop,strHeading
	
	set intCount   = $listlength(pstrColumns)
	set strHeading = ""
	do NewLine(YDARKGRAY)
	for loop=1:1:intCount {
		set strHeading = $listget(pstrColumns,loop)
		do InsertCell(strHeading)
	}
 
	do EL^WWWTAB
	quit
	
	
Stop()
	;-------------------------------------------------------------------------------
	; History:
	;-------------------------------------------------------------------------------
	new lngColumn,lngRow,objData
	
	set lngRow = ""
	if gblnUsePaging {
		do Header(@gStorage@("Header"),@gStorage@("HeaderText"))
		; variable used in insert cell to divert the data to a global
		; rather than the screen. reset to display data.
		set gblnUsePaging = $$$NO
		for {
			set lngRow = $order(@gStorage@(lngRow))
			quit:lngRow=""
			
			do NewLine(@gStorage@(lngRow))
			set lngColumn = ""
			for {
				set lngColumn = $order(@gStorage@(lngRow,lngColumn))
				quit:lngColumn=""
				
				set objData = @gStorage@(lngRow,lngColumn)  ;objData Construction: pstrText_Y_pstrForm_Y_pidTran_Y_pstrParam_Y_plngPage
				do InsertCell($piece(objData,Y,1),$piece(objData,Y,2),$piece(objData,Y,3),$piece(objData,Y,4),$piece(objData,Y,5),$piece(objData,Y,6),$piece(objData,Y,7))
			}
			do EndLine()
		}
	}
	do STOP^WWWTAB
	kill gColour
	kill gStorage
	kill gblnUsePaging
	kill glngColumn
	kill glngRow
	quit
	
	
NewLine(pstrColour)
	;-------------------------------------------------------------------------------
	; Lines are normally generated with alternating "white" or "gray" backgrounds
	; but an alternate colour can be specified, normally to indicate selection,
	; active/inactive status, etc.
	; 
	; YBBSATZ : row number(?) used to determine odd or even.
	; 
	; 13-Sep-2005	RPW		SR13306: Store the row and column changes for non paging as well.
	;-------------------------------------------------------------------------------
	if gblnUsePaging {
		set glngRow    = glngRow+1
		set glngColumn = 1
		set @gStorage@(glngRow) = $get(pstrColour)
	
	} else {
		set glngRow    = glngRow+1
		set glngColumn = 1
		set gColour    = YWHITE
		if $get(pstrColour)'="" {
			set gColour = pstrColour
		} else {
			if $get(YDDSATZ)'="" {
 				if $get(YDDSATZ)#2=1 set gColour = YWHITE
 				if $get(YDDSATZ)#2=0 set gColour = YGRAY
			}
		}
		do NL^WWWTAB
	}
	quit
	
	
EndLine()
	;-------------------------------------------------------------------------------
	; 
	;-------------------------------------------------------------------------------
	if 'gblnUsePaging do EL^WWWTAB
	quit
	
	
QueryTemplate
	;-------------------------------------------------------------------------------
	; not used in code exactly, just a template of a query
	; History :
	; 21-Dec-2004	GRF		Add ^COMTable to make useful template for external code.
	;-------------------------------------------------------------------------------
	new lstHeader
	
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild()
	
	if $$Start^COMTable(lstHeader,"header") {
		for {
			do NewLine^COMTable("colour")
			do InsertCell^COMTable("","Form","Key","Param")
			do InsertCellDate^COMTable("","Form","Key","Param")
			do InsertCellPercentage^COMTable("","Form","Key","Param")
			do InsertCurrencyCell^COMTable("",$$$NO,"Form","Key","Param")
			do InsertCellBoolean^COMTable("","Form","Key","Param")
			do EndLine^COMTable()
		}
		do Stop^COMTable()
	}
	quit
	
	
JSBooleanToBoolean(pstrBoolean,pblnUseBlankForZero=$$$YES)
	quit $select(pstrBoolean="true":1,1:$select(pblnUseBlankForZero:" ",1:0))
	
	
BlankRows(pintColumns,pintRows=5)
    ;-------------------------------------------------------------------------------
    ; Draw blank rows
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 01-Nov-2005	RPW		SR-: Created
    ;-------------------------------------------------------------------------------
	new loopCols,loopRows
	  
	for loopRows=1:1:pintRows {
		do NewLine()
		for loopCols=1:1:pintColumns {
			do InsertCell("")
		}
		do EndLine()
	}
	quit
	
]]></Routine>
</Export>