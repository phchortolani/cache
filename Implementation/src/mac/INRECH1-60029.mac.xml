<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRECH1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRECH1
	
#include INConst
#include COMSYS
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		DRUCKEN EINZEL RECHNUNG
	;		Print single invoice
	; 
	; Inputs : 
	;
	; ByRef :
	;	YKEY		idOrder, idOrdLine
	;	YCOPY		boolean		Copy of Invoice
	;	YNOCOPY		boolean		Are we allowed to print copies?
	;	
	; Returns :
	;	(ByRef) YBELEG = 7  "Invoice"
	;
	;
	; History :
	; 06-Dec-2006	GRF		SR15272: Moved code to subroutine; doco
	; 18-Sep-2006	JW		SR15048: Changed error msg. Cleaned up.
	; 10-Mar-2006	SC		SR14349: After Invoice Printing, check if Order + Lines completed.
	; 23-Jan-2006	GRF		SR14105: Doco
	; 19-Aug-2005	GRF		SR13526: Doco
	; 29-Aug-2005	Steve S	SR13107: Cater for single-line printing
	; 14-Aug-2000	DT?		Created
	;-------------------------------------------------------------------------------
	new idItem,KUNDE,POS1,strStatus,VORKASSE,YQ2
	
	if $piece($get(YKEY),",",1)="" do ^WWWFORM quit     ;KEIN KEY ENTHALTEN ;no KEY included
	
	kill ^CacheTempSingleLine(YUSER)
	if $piece($get(YKEY),",",2)'="" {
		set ^CacheTempSingleLine(YUSER,$piece($get(YKEY),",",1),$piece($get(YKEY),",",2))=""	
	}
	;PRUEFEN WH
	set YAUFTRAG = $$$KEY1(YKEY)
	
	;+++++++++++++++++++++++++++++++++++++++
	;	POS1		objINAUFP		Order Line
	;+++++++++++++++++++++++++++++++++++++++
	;	VORKASSE		blnCashInAdvance
	;	YBELEG			7 = "Invoice"
	;	YAUFTRAG(POS)	
	;+++++++++++++++++++++++++++++++++++++++
	
	set strStatus = $$$OK
	
	if '$$$INVORGNoSourcingCheckOnInvoiceP($get(^INVORG(0,0,1))) {
		; Check each order line 
		;   If Source = "Not Specified" and not a "Non-Stock" item, flag as a warehouse problem
		if $$$INAUFPartDeliveryPossible($get(^INAUF(0,YAUFTRAG,1)))'=$$$YES {
			set POS = ""
			for {
				set POS = $order(^INAUFP(0,YAUFTRAG,POS))
				quit:POS=""

				set POS1 = $get(^INAUFP(0,YAUFTRAG,POS,1))
				if +$$$INAUFPSource(POS1)=0 {
					set idItem = $$$INAUFPItemNumber(POS1)
					if (idItem'="") && '$$$INARTItemWithoutInventoryMaint($get(^INART(0,idItem,1))) {
						set strStatus = $listbuild("IN00543",POS)  ;Order line %1 needs to be sourced before invoice can be printed.
					}
				}
				quit:$$$ISERR(strStatus)
		}
			
			if $$$ISERR(strStatus) {
				; "No Sourcing Check On Invoice Print" turned OFF, and a line is not sourced
				write $$$Text(strStatus)
			}
		}
	}
	quit:$$$ISERR(strStatus)                                      ; *** EARLY EXIT ***
	
	set VORKASSE = $$CashInAdvance(YAUFTRAG)
	
	set POS = $piece($get(YKEY),",",2)
	if POS'="" set YAUFTRAG(POS)=""  ;FIS;23466;RECHNUNG AUCH WENN GESPERRT ;tab too when DISABLED 
	
	set YBELEG=7  ;RECHNUNG ;Invoice
	
	if $get(YCOPY)="" set YCOPY=0  ;KEINE WIEDERHOLUNG ;no repetition 
	if +YCOPY=0 if $order(YAUFTRAG(""))="" do  ;PRÜFEN AUF GESAMTKOPIE;FIS;16.09.03 ;check upon 
	. new YPOS,YQ
	. set YQ=0
	. set YPOS=""
	. for  set YPOS=$order(^INAUFP(0,YAUFTRAG,YPOS)) quit:YPOS=""  do  quit:YQ=1
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,9)   = $$$YES quit       ;STORNO              ; Cancellation
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,205) = $$$YES quit       ;SPERR               ;         
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,97)  = "" set YQ=1       ;NOCH NICHT GEDRUCKT ; Bill Printed Date - not yet set
	. ;
	. if YQ=0 set YCOPY=1  ;WIEDERHOLUNGSDRUCK, DA ALLE BEREITS GEDRUCKT
	
	;---------------------------------------
	
	set YQ2=0
	if +YCOPY=0 if $order(YAUFTRAG(""))="" do  ;KEINE POSITIONEN ANGEGEBEN=ALLE ;no line given so do all lines
	. new YPOS
	. set YQ2=1
	. set YPOS=""
	. for  set YPOS=$order(^INAUFP(0,YAUFTRAG,YPOS)) quit:YPOS=""  do  quit:YQ2=0
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,9)   = $$$YES quit  ;STORNO 
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,205) = $$$YES quit  ;SPERR
	. . if VORKASSE'=1 if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,5)'<0 if $piece($get(^INVORG(0,0,1)),Y,150)=1 if +$piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,90)'=$$$YES quit   ;NUR AUSLIEFERUNGSFÄHIGE DRUCKEN;TYBD;27,07,2003;UL (TONY)
	. . if VORKASSE'=1 if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,5)'<0 if $piece($get(^INVORG(0,0,1)),Y,150)=2 if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,92)=""       quit   ;NUR GELIEFERTE DRUCKEN;FIS;28.06.04;25791
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,97)'="" quit  ;BEREITS GEDRUCKT ;yet 
	. . set YQ2=0  ;MERKER, DASS ZUMINDEST 1 POSITION GEDRUCKT WIRD;FIS;24056;10.09.03
	. ;
	. ;IF YQ=0 SET YCOPY=1  ;WIEDERHOLUNGSDRUCK, DA SONST KEIN DRUCK
	
	if YQ2=1 write $$^WWWTEXT(33583) quit   ;ABBRUCH, KEINE ZU BERECHNENDEN POSITIONEN    "No Line Items To Proceed!"
	
	;---------------------------------------
	
	set YQ2=0
	if +YCOPY=0 if $order(YAUFTRAG(""))'="" do  ; POSITIONEN ANGEGEBEN   ; line given
	. new YPOS,YQ
	. set YQ=0
	. set YQ2=1
	. set YPOS=""
	. for  set YPOS=$order(YAUFTRAG(YPOS)) quit:YPOS=""  do  quit:YQ=1 
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,9)   = $$$YES quit  ;STORNO 
	. . if VORKASSE'=1 if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,5)'<0 if $piece($get(^INVORG(0,0,1)),Y,150)=1 if +$piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,90)'=$$$YES quit   ;POSITIONEN NICHT PRÜFEN !! ;Line not checked 
	. . if VORKASSE'=1 if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,5)'<0 if $piece($get(^INVORG(0,0,1)),Y,150)=2 if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,92)=""       quit   ;NUR GELIEFERTE DRUCKEN;FIS;28.06.04;25791
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,97)'="" quit  ;BEREITS GEDRUCKT ;yet 
	. . if $piece($get(^INAUFP(0,YAUFTRAG,YPOS,1)),Y,101)=""  set YQ=1  ;NOCH NICHT GEDRUCKT ;yet Not 
	. . set YQ2=0  ;MERKER, DASS ZUMINDEST 1 POSITION GEDRUCKT WIRD;FIS;24056;10.09.03
	. ;
	. if YQ=0 set YCOPY=1  ;WIEDERHOLUNGSDRUCK, DA SONST KEIN DRUCK
	
	if YQ2=1 write $$^WWWTEXT(33583) quit   ;ABBRUCH, KEINE ZU BERECHNENDEN POSITIONEN    "No Line Items To Proceed!"
	
	;---------------------------------------
	
	if $data(XYAUFTRAG) if $order(YAUFTRAG(""))'="" do  ;Rechnung mit Lieferung schicken;FIS;25693;01.07.04
	. new XPOS
	. set XPOS=""
	. for  set XPOS=$order(YAUFTRAG(XPOS)) quit:XPOS=""  do
	. . if '$data(XYAUFTRAG(XPOS))   kill YAUFTRAG(XPOS) quit  ;NUR POSITIONEN AUS LIEFERSCHEIN DRUCKEN ;only out of packing slip print 
	. . if $get(XYAUFTRAG(XPOS))="X" kill YAUFTRAG(XPOS)  ;NICHT DRUCKEN ;Not print 
	
	if $data(XYAUFTRAG) if $order(YAUFTRAG(""))="" do
	. new XPOS
	. set XPOS=""
	. for  set XPOS=$order(XYAUFTRAG(XPOS)) quit:XPOS=""  do
	. . if $get(XYAUFTRAG(XPOS))="X" kill XYAUFTRAG(XPOS)  ;NICHT DRUCKEN ;Not print 
	. ;
	. merge YAUFTRAG=XYAUFTRAG  ;NUR POSITIONEN AUS LIEFERSCHEIN DRUCKEN ;only out of packing slip print 
	
	if $get(YNOCOPY)=$$$YES quit:YCOPY=1
	
	do ^INDRUCK     ;DRUCK ;printing (also => General Ledger through INDRUCK71/INDRUCK72 calls to FIBU^INFIBBUCH)
	do ^INDRUCKZHD  ;DRUCK KOPIE ZU ANSPRECHENPATNER      ;FAN;13.03.2003;23059
	do CheckForComplete^INTAGES1(YAUFTRAG) //SR14349
	
	quit
	
	
CashInAdvance(pidOrder)
	;-------------------------------------------------------------------------------
	;	Check if needs cash in advance
	;	PRÜFEN VORKASSE;25791;13.04.07
	;
	; History :
	; 06-Dec-2006	GRF		SR15272: Created as separate function
	;-------------------------------------------------------------------------------
	new blnCashRequired,idCustomer,objCustomer
	
	set blnCashRequired = $$$NO
	set idCustomer = $piece($get(^INAUF(0,pidOrder,1)),Y,1)
	
	if idCustomer'="" {
		set objCustomer = $get(^INKUNDE(0,idCustomer,1))
		if $$$INKUNDEMethodOfPayment(objCustomer)=5 {          ; D102	5 : "Cash In Advance"
			set blnCashRequired = $$$YES
		} else {
			set idCustomer = $$$INKUNDEBillTo(objCustomer)     ; D38
			if idCustomer'="" {
				if $$$INKUNDEMethodOfPayment($get(^INKUNDE(0,idCustomer,1)))=5 set blnCashRequired = $$$YES
			}
		}
	}
	quit blnCashRequired
	
	
GESAMT ; EINSPRUNG GESAMT ;total
    ;-------------------------------------------------------------------------------
    ; Calls the routine to print an invoice with just one line to print
    ;
    ; Returns:
    ;
    ; History:
    ; 26-Aug-2005	SteveS	Commented
    ;-------------------------------------------------------------------------------
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1) = $piece($get(YKEY),",",1)  ;BEARBEITET
	do INRECH1
	quit
	
	
WIEDERHOLUNG    ;  EINSPRUNG MIT WIEDERHOLUNG (OHNE BUCHEN) ;by means of repetition 
	set YCOPY=1
	set ^WWW126(0,"INAUF",YBED,1,1) = $piece($get(YKEY),",",1)   ;BEARBEITET
	do INRECH1
	quit        
	
	
SHIP ;SEND ALWAYS TOGETHER WITH DELIVERY NOTE;FIS;25693;01.07.04
	new XYAUFTRAG
	
	set YCOPY=0
	if $get(YAUFTRAG)="" quit
	if $piece($get(YKEY),",",1)="" new YKEY set YKEY=YAUFTRAG  ;KEIN KEY ENTHALTEN ;no KEY include 
	set ^WWW126(0,"INAUF",YBED,1,1) = $piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=0  ;MAIL
	merge XYAUFTRAG=YAUFTRAG
	do INRECH1
	quit          
	
	
MAIL ;SEND VIA MAIL  / EINSPRUNG VON DRUCKFORMULAR
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1) = $piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=1  ;MAIL
	do INRECH1
	quit       
	
	
FAX ;SEND VIA FAX  / EINSPRUNG VON DRUCKFORMULAR
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=2  ;FAX
	do INRECH1
	quit
	
XML ;SEND VIA XML  / EINSPRUNG VON DRUCKFORMULAR
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=3  ;MAIL
	do INRECH1
	quit      
	
	
EDI ;SEND VIA EDI  / EINSPRUNG VON DRUCKFORMULAR
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=4  ;EDI
	do INRECH1
	quit        
	
	
FTP ;SEND VIA FTP  / EINSPRUNG VON DRUCKFORMULAR
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=5  ;FTP
	do INRECH1
	quit     
	
	
EDIP ;SEND VIA EDIPRO  / EINSPRUNG VON DRUCKFORMULAR
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=6  ;EDIP
	do INRECH1
	quit          
	
	
PDF ;SEND VIA PDF  / EINSPRUNG VON DRUCKFORMULAR     ;BEC;28.04.03;ERGÄNTZT PDF
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=7  ;PDF
	do INRECH1
	quit
	
	
STDIF ;SEND VIA STD. INTERFACE  / EINSPRUNG VON DRUCKFORMULAR    ;FIS;23.02.04;25177
	set YCOPY=0
	set ^WWW126(0,"INAUF",YBED,1,1)=$piece($get(YKEY),",",1)   ;BEARBEITET
	set YSEND=9  ;STANDARDSCHNITTSTELLE
	do INRECH1
	quit
	
	
Printable(pidKey="")
    ;-------------------------------------------------------------------------------
    ; Determine whether invoice is printable
    ;
    ; Params: Key for order ie. INAUF
    ;
    ; ByRefs: None
    ;
    ; Returns: strStatus - 0 if no error, otherwise 1_[Error message]
    ;
    ; History:
    ; 29-Oct-2008	GRF		SR15924: clarify sql command
    ; 01-Oct-2008	HQN		SR15924: INWEAUF no longer populated during sourcing,
    ; 							look for dPlannedMovement Type:Sale
    ; 03-Apr-2006	RPW		SR14502: If the sum of the sourcings are not equal to
    ; 							the quantity on the line, error out.
    ; 19-Jan-2006	PO		SR14109: Handling for same invoice used on two orders
    ; 08-Jan-2006	PO		SR14152: Created
    ;-------------------------------------------------------------------------------
	new fltQtySourced,idKey,idLine,intMovementType,intQty
	new objINAUF,objINAUFP,objINWEAUF,SQLCODE,strQuery,strStart,strStatus
	
	set strStatus = $$$NO
 
	if $get(pidKey)'="" {
		if pidKey'=+pidKey {
			set idKey = $$$DBLQUOTE_pidKey_$$$DBLQUOTE
		} else {
			set idKey = pidKey
		}
 
		set objINAUF = $get(^INAUF(0,pidKey,1))
 
		if $$$OrdersInvoiceEndsWithDot(objINAUF) && ($$$INAUFOrderType(objINAUF) = 0) {
			set strStatus = $$$YES
		
		} else {
			if $$$INAUFOrderType(objINAUF) '= 0 {
				set strStatus = $listbuild("IN00388") ;  Invoicing can only be performed for customer orders.
			} else {
				set strStatus = $listbuild("IN00389") ;  There are no order lines ready to be invoiced.
				set idLine = ""
				for {
					set idLine = $order(^INAUFP(0,pidKey,idLine))
					quit:idLine=""
 
					set objINAUFP = $get(^INAUFP(0,pidKey,idLine,1))
 
					if ($$$INAUFPSource(objINAUFP)'="") && ($$$INAUFPInvoiceNumber(objINAUFP)="") {
						
						// SR14502: There could be many "sourcings". Add them and see if they equal the lines quantity
						
						if $$$INAUFComplaintSparePartOrder(objINAUF)|| $$$INARTItemWithoutInventoryMaint($get(^INART(0,$$$INAUFPItemNumber(objINAUFP),1))) { // SR14609?
							set intQty = 0
						} else {						
							set intQty = $$$INAUFPQuantity(objINAUFP)
							set intMovementType = $listbuild(0)
							&sql(SELECT SUM(QtyRequired) INTO :fltQtySourced FROM alDRP.dPlannedMovement
							       WHERE Reference=:pidKey  AND
							             LineNumber=:idLine AND
							             MovementType_MovementTypeId=:intMovementType
							)
		; FIXME: REPLACE WITH A CALL THROUGHT iSAL
							
							set intQty = intQty - fltQtySourced
						}
					}
						
					if $get(intQty)'=0 {
						set strStatus = $listbuild("IN00405")  ; "There are lines which are not completely sourced."
		// We must quit immediately at this point, only takes 1 to stop the print.
						quit
					}
					 else {					
						set strStatus = $$$OK
					}
				}
			}
		}
	}
	
	if $$$ISOK(strStatus) {
		set YQ = $$$YQEnable
	} else {
		set YQ = 2 ;$$$YQDisable($$$Text(strStatus)) ; SR14207 RPW/PO Later make sure $$$YQHidden is defined
	}
	
	quit
	
	
Reprintable(pidKey="")
	;-------------------------------------------------------------------------------
    ; Determine whether invoice is reprintable ie. has the order been invoiced.
    ;
    ; Params: pidKey - Order number
    ;
    ; ByRefs: None
    ;
    ; Returns: Nothing
    ;
    ; History:
    ; 20-Jan-2006	PO		SR14109: Handling for same invoice used on two orders
    ; 18-Jan-2006	PO		SR14207: Created
    ;-------------------------------------------------------------------------------
	new strStatus,objINAUF
	
	set strStatus = $$$NO
	
	if $get(pidKey)'="" {
		set objINAUF = $get(^INAUF(0,pidKey,1))
		
		set strStatus = $$$OK	
		if $$$OrdersInvoiceEndsWithDot(objINAUF) && ($$$INAUFOrderType(objINAUF)=0) {
			set strStatus = $listbuild("IN00392")  ; "This orders invoice can not be re-printed, contact your systems administrator."
		} elseif $$$INAUFInvoiceNumber(objINAUF) = "" {
			set strStatus = $listbuild("IN00391")  ; "The Invoice has not been printed."
		}
	}
	
	if $$$ISOK(strStatus) {
		set YQ = $$$YQEnable
	} else {
		set YQ = 2 ; $$$YQDisable($$$Text(strStatus)) ; SR14207 RPW/PO Later make sure $$$YQHidden is defined
	}
	quit
	
]]></Routine>
</Export>