<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDDispenseIssue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDDispenseIssue
	
#include COMSYS 
#include COMConst
#include MEDConst
#include INConst
#include COMView

	
OnAfterDataFields(pstrForm)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; History :
	; 05-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	do LoadGrid(pstrForm)
	quit
	
	
LoadGrid(pstrForm,pblnLocked=$$$NO)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 05-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
	
	kill ^CacheTempLocked(YUCI,YUSER)
	do ClearOld^WWWMultiLock(YUSER)
	
	$$$ClearExternalFilter("MEDDispenseIssue") 
	
	//YFORM is used as a global within COMGridEdit31
	set YFORM = "MEDDispAsIssue"
	
	// SR16642
	do AddExternalFilter^COMViewFilter("MEDDispenseIssue","P1","",$$$EnumCOMVIEWCOMPARATORStartsWith,$$$YES)
	do AddExternalFilter^COMViewFilter("MEDDispenseIssue","D"_$$$FldMEDPrescriptionSupplyingLocn,"",$$$EnumCOMVIEWCOMPARATORStartsWith,$$$YES)
	
 	// SR16642 only show open Prescriptions, not closed ones
 	do AddExternalFilter^COMViewFilter("MEDDispenseIssue","D"_$$$FldMEDPrescriptionStatus,"1",$$$EnumCOMVIEWCOMPARATORGreaterThan,$$$NO)
 	do AddExternalFilter^COMViewFilter("MEDDispenseIssue","D"_$$$FldMEDPrescriptionStatus,"4",$$$EnumCOMVIEWCOMPARATORLessThan,$$$NO)
	do AddExternalFilter^COMViewFilter("MEDDispenseIssue","D14",$horolog+1,$$$EnumCOMVIEWCOMPARATORLessThan,$$$YES)
	
	set YFELD	= ""
	set YKEY	= ""		// No header
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)          = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)       = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)            = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)             = $$$NO
	set $$$COMGridEditParameterContainer(YAUSWAHL)           = "MEDDispenseIssue"
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$YES
	set $$$COMGridEditParameterCOMViewGrid(YAUSWAHL)         = $$$YES
	
	do Start^COMGridEdit31(YFORM,YKEY)
	set YFORM = "MEDDispenseIssue"                 ; COMViewFilter uses YFORM byref
	
	do LoadCOMViewGrid^COMViewFilter("MEDDispenseIssue","MEDDispAsIssue",1)
	
	quit $$$YES
	
	
GetActiveDispense(pidPres,pidCurrent="",pblnTimeCheck=$$$NO,pblnInatives=$$$NO)
	;-------------------------------------------------------------------------------
	; Description :
	; Gets the active issue for this Prescription
	;			
	; Called By :
	;		
	; Inputs : pidPres	Requisition Number
	;		 : pidCurrent Current Dispense
	;
	; ByRef : YLOCATION
	;
	; Returns : Open Issue number for line
	;
	; History :
	; 06-Nov-2014	SCR		HEVA-1623: Exclude Rejected Lines if Time Check = $$$YES
	; 22-Jan-2013	SCR		HEVA-783: Exclude current Dispense
	; 05-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new dtEnd,dtStart,idDispense,idxPres,objCurrent,objDispense
	
	set idDispense = ""
	
	if pidPres="" {
		set pidPres = $$GetKey^COMGridEdit31G()
	}
	
	if pidPres'="" {
		//set objPres = $get(^MEDPrescription(0,pidPres,1))	
		set idxPres = $$$Index(pidPres)
		
		; HEVA-783 vvvv
		set idDispense = ""
		for {
			set idDispense = $order(^MEDDispenses(0,1,idxPres,idDispense),-1)
			quit:idDispense=""
			continue:idDispense=pidCurrent
			set objDispense = $get(^MEDDispense(0,idDispense,1))
			if ('pblnInatives) {
				if $$$MEDDispenseStatus(objDispense) > 2 {
					set idDispense = "" ; no active Dispense and can create a repeat.
				}
			}
			if (idDispense'="") && (pblnTimeCheck=$$$YES) && (pidCurrent'="") {
				set objCurrent	= $get(^MEDDispense(0,pidCurrent,1))
				set dtStart		= $$$MEDDispenseShiftStart(objCurrent)
				set dtEnd		= $$$MEDDispenseShiftEnd(objCurrent)
				if $$RecjectOK(idDispense,dtStart,dtEnd) {
					continue
				}
			}
			quit
		}
		; HEVA-783 ^^^^
		;set idDispense = $order(^MEDDispenses(0,1,$$$Index(idxPres),""),-1)
		;if idDispense'="" {
		;	set objDispense = $get(^MEDDispense(0,idDispense,1))
		;	if $$$MEDDispenseStatus(objDispense) > 2 {
		;		set idDispense = "" ; no active Dispense and can create a repeat.
		;	}
		;}
	}

	quit idDispense
	
RecjectOK(pidDispense,pdtStart,pdtEnd)
	;-------------------------------------------------------------------------------
	; Description :
	; Check if all the Dispense Lines are rejected and the Times are not the same
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef : 
	;
	; Returns : Yes if all Lines are Rejected and Time are not the same
	;
	; History :
	; 06-Nov-2014	SCR		HEVA-1623: Created
	;-------------------------------------------------------------------------------
	new blnOK,blnRej,idLine,objDispense,objLine

	if (pdtStart="") || (pdtEnd="") {
		quit $$$NO  ; Short cut exit , only applies when there are input times
	}
	set blnOK		= $$$YES
	set objDispense	= $get(^MEDDispense(0,pidDispense,1))
	if (pdtStart = $$$MEDDispenseShiftStart(objDispense) ) && (pdtEnd = $$$MEDDispenseShiftEnd(objDispense)) {
		set blnOK	= $$$NO
	}
	if blnOK=$$$YES {
		set idLine	= ""
	  	set blnRej	= $$$YES 
	  	for {
		  	set idLine	= $order(^MEDDispenseLine(YM,pidDispense,idLine))
		  	quit:idLine=""
		  	set objLine	= $get(^MEDDispenseLine(YM,pidDispense,idLine,1))
		  	if $$$MEDDispenseLineValidatedStatus(objLine)'="refuse.gif" {
		  		set blnRej	= $$$NO
	  		}
	  	}
	  	if blnRej	= $$$NO {
	  		set blnOK	= $$$NO
  		}
	}
	quit blnOK

PickPrinted(pidPres)
	;-------------------------------------------------------------------------------
	; Description :
	; Gets the Date/Time of the Pick List if printed
	;			
	; Called By :
	;		
	; Inputs : pidPres	Requisition Number
	;
	; ByRef : 
	;
	; Returns : %TimeStatmp
	;
	; History :
	; 05-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new idDispense
	new dtePick
	
	/*
	set idDispense = $$GetActiveDispense(pidPres)
	set dtePick = ""
	if idDispense'="" {
		set objDispense = $get(^MEDDispense(0,idDispense,1))
		set dtePick = $$$MEDDispensePickListPrinted(objDispense)
		if dtePick'="" {
			set dtePick = $$^WWWDATE(+dtePick)_" "_$$^WWWTIME($p(dtePick,",",2))
		}
	}
	*/
	
	quit 
	
FulfillDispense(pYKEY)
	;-------------------------------------------------------------------------------
	; create an Dispense for a prescription and redirect to the correct Form
	;
	;	(Button) Create Dispense (FORMSPEC) MEDDispAsIssue
	; 
	; ByRef:
	; 	YFORM
	; 	YBACK
	; 
	; History:
	; 05-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new idDispense,strStatus
 	new idPres
 	
 	set strStatus = $$$OK
 	
	if ($get(pYKEY) '= "") {
		set idPres = pYKEY
	} else {
		set idPres = $$GetKey^COMGridEdit31G()
	}
	
	if idPres '= "" {
		set idDispense = $$GetActiveDispense(idPres)
		if idDispense '="" {
			do RedirectForm^COMUtils("MEDDispense",idDispense,"MEDDispenseIssue,"_idPres)
			//do GoToForm^COMUtilForm("MEDDispense",idDispense)
		
		} else {
			do CreateDispense(idPres)
		}
	}
	quit
	
	
CreateDispense(pYKEY)
	;-------------------------------------------------------------------------------
	;	Creates an INIssue from the selected MEDPrescription
	;	
	; Called By :
	;	For every line in a MEDPrescription, if it doesn't have a corresponding IssueLine, 
	;	create a new header and assign the line to it
	;		
	; Inputs : 
	;	pYKEY	-	MEDPrescription ID
	;	
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 05-Jun-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new idDispense,idPres,objPres,strStatus
	
	set strStatus = $$$OK
		
	//	since $$GetKey^COMGridEdit31G() is based on current view and click,
	// it can get invalidated when using mutiple windows
	
	set idPres = $$GetKey^COMGridEdit31G()
	
	if (pYKEY '= "") {
		set idPres = pYKEY
	}
	
	if (idPres = "") {
		$$$StartScript()
		write "alert("_$$$Text("MED00362")_");"       ; "Unable to determine Prescription Number"
		$$$EndScript()
		do RedirectForm^COMUtils("MEDPrescriptionIssue")
		quit                                                    ; *** EARLY EXIT ***
	}
	
	set objPres = $get(^MEDPrescription(0,idPres,1))
	
	if $$$ISOK(strStatus) {
			
	;+++++++++++++++++++++++++++++++++++++++
		tstart
	;+++++++++++++++++++++++++++++++++++++++
		
		set strStatus = $$CreateDispense^MEDDispense(idPres, .idDispense)
		
	;+++++++++++++++++++++++++++++++++++++++
		if ($$$ISOK(strStatus)) {
			tcommit:($tlevel > 0)
		} else {
			trollback
		}
	;+++++++++++++++++++++++++++++++++++++++
	}
	
	if $$$ISOK(strStatus) {
		if idDispense '= "" {
			set $$$MEDPrescriptionSupplyingLocn(objPres) = $get(YLOCATION)
			set strStatus = $$$Save("MEDPrescription",idPres,objPres,$$$YES)
			
			do RedirectForm^COMUtils("MEDDispense",idDispense,"MEDDispenseIssue,"_idPres)
			//do GoToForm^COMUtilForm("MEDDispense",idDispense)
		}
	
	} else {
		do RedirectForm^COMUtils("MEDDispenseIssue",pYKEY)
	}

	quit
	
]]></Routine>
</Export>