<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWTABLE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWTABLE(YLINEMAX,YSORTWAY,YSORTBY,YLINEMIN,YHIGHTSROLL,pstrYFILTER)
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		CREATE TABLE OUT OF FILE
	;
	; Inputs : 
	;	YLINEMAX	= NUMBER OF LINES TO DISPLAY
	;	YSORTWAY	= SORT FORWARD/BACKWARD
	;	YSORTBY		= SORT BY FIELD
	;	YLINEMIN	= 
	;	YHIGHTSROLL	= AFTER NUMBER OF PIXEL START SROLLINT, I "" NO SROLLING, if -1 NO IFRAME
	;	pstrYFILTER = Filter set (data to be filtered out - checks against DATAFILTER)
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 07-Jan-2009	FIS		SR16236: pstrYFILTER for filter methid added
	; 06-Jan-2009	FIS		SR16236: allow different font size
	; 							reload function added
	; 04-Dec-2008	FIS		SR16205: create table without iframe
	; 12-Jun-2007	GRF		SR15543: Naked Reference; braces; doco
	; 18-Aug-2006	RPW		SR14807: Removed Do {} while(0) loop!
	; 27-Jan-2006	GRF		SR14203: Doco
	; 19.07.2004	FIS		26038: created
	;-------------------------------------------------------------------------------
	
	;-------------------------------------------------------------------------------
	// DEFINITION DER ÜBERSCHRIFT
	;
	// ^WWWSOR(YUSER,"TITLE")= Headline
	// ^WWWSOR(YUSER,"HEAD")= HEAD1 ~ HEAD2 ~ HEAD3 ~ ...
	// ^WWWSOR(YUSER,"HEADSIZE")= 100 ~ 100 ~ 150 ~ ...  (SIZE IN PIXEL)
	// ^WWWSOR(YUSER,"HEADFIX")= 500  (MAXIMUM HEAD SIZE IN PIXEL)
	;
	// VORGABE DATEI ODER DATENSÄTZE
	// ^WWWSOR(YUSER,"KEY",YLFN)= ^TEMP(0,KEY1,KEY2,1)
	// ^WWWSOR(YUSER,"DATA",YLFN)= KEY1 ~ KEY2 ~ TEXT1 ~ TEXT2 ~ TEXT3 ~ ...
	;
	// FILTER
	// ^WWWSOR(YUSER,"DATAFILTER",YLFN)= VALUE that should get filtered against YFILTER
	;
	// LAYOUT ANGABEN
	// ^WWWSOR(YUSER,"LEFT")=YI1,YI5   ;FELDER LINKSBÜNDIG ;left justified 
	// ^WWWSOR(YUSER,"RIGHT")=YI2,YI4  ;FELDER RECHTSBÜNDIG ;right justified 
	// ^WWWSOR(YUSER,"FONTSIZE")=x     ;use fontsize x (ie. 2)
	// ^WWWSOR(YUSER,"TYPE",YI)=FELDTYP  ;FELDER, MIT FELDFORMATIERUNG (Z.B. 8=WÄHRUNG)
	// ^WWWSOR(YUSER,"STYLE",YI)=Style-Sheet Angaben (z.B. "background-color:red;")
	// ^WWWSOR(YUSER,"NOSORT")=YI1,YI5   ;SPALTEN OHNE SORTIERUNG;COLUMS WITHOUT SORT LINK;(1,2)
	// ^WWWSOR(YUSER,"NOMASK",YI)=1     ;do not replace " " with "&nbsp;"
	;
	// DEFINITION LINK IN FORMULAR
	// ^WWWSOR(YUSER,"LINK","YFORM")=YFORM
	// ^WWWSOR(YUSER,"LINK","YKEY")=YI        (YI=SPALTE)
	// ^WWWSOR(YUSER,"LINK","TITLE")=Tooltip
	;
	// ODER: DEFINITION DER LINKS JE SPALTE
	// ^WWWSOR(YUSER,"LINK",YI,"EP")="WWWFORM" (WWWMANU)
	// ^WWWSOR(YUSER,"LINK",YI,"YFORM")=YFORM
	// ^WWWSOR(YUSER,"LINK",YI,"YEXEC")=""     (D|^ROUTINE)
	// ^WWWSOR(YUSER,"LINK",YI,"YKEY")=YI2 (,YI3,...)    (YI=ANZEIGESPALTE, YI2=SPALTE MIT KEY)
	// ^WWWSOR(YUSER,"LINK",YI,"TITLE")=Tooltip
	// ^WWWSOR(YUSER,"LINK",YI,"TARGET")=FRAME2
	// ^WWWSOR(YUSER,"LINK",YI,"MODAL")=1                  ;OPEN MODAL WINDOW 
	// ^WWWSOR(YUSER,"LINK",YI,"MODAL","HEIGHT")=650       ;HIGHTS
	// ^WWWSOR(YUSER,"LINK",YI,"MODAL","WIDTH")=400        ;WIDTH
	;
	//^WWWSOR(YUSER,"LINK",YI,YLFN,"YFORM")=YFORM      ;ALTERNATIVES FORMULAR PRO SPALTE,ZEILE 
	//^WWWSOR(YUSER,"LINK",YI,YLFN,"TITLE")=Tooltip    ;ALTERNATIVER TOOLTIP PRO SPALTE,ZEILE 
	//  ^WWWSOR(YUSER,"LINKUNDERLINE")=1                    ;LINKS ARE UNDERLINED
	//  U.S.W.
	;
	// ODER: JAVASCRIPT IM LINK
	// ^WWWSOR(YUSER,"LINK","JAVASCRIPT")=JAVASCRIPT FÜR ALLE
	// ^WWWSOR(YUSER,"LINK","JAVASCRIPT",YLFN)=JAVASCRIPT JE ZEILE
	// ^WWWSOR(YUSER,"LINK",YI,"JAVASCRIPT")=JAVASCRIPT JE SPALTE
	;
	;
	// ^WWWSOR(YUSER,"EVENTTYPE",YI)=3             ;CHECKBOX at the moment only checkbox
	// ^WWWSOR(YUSER,"EVENTKEYREADONLY",YI)=YII     ;DATAFIELD FOR DISABLED CHECK (DATAFIELD has to be 1 for disabled)
	;-------------------------------------------------------------------------------
	
	NEW YKEY,YFELD,YLFN,YI,YCOL,YFONT,YLEFT,YRIGHT,YTOTAL,YHEIGHT,YWIDTH,YHEADSIZE,YTABLEID,YCONFIGWIDTH
	
	SET YLINEMAX=$GET(YLINEMAX)
	SET YLINEMIN=$GET(YLINEMIN)
	IF +YLINEMAX=0 SET YLINEMAX=$PIECE($GET(^WWW012(0,YM,1)),Y,135)  ;COMPANY DFLT.
	IF +YLINEMAX=0 SET YLINEMAX=100
	;SET YSTARTAT=$GET(YSTARTAT) ;table-mat 
	SET YHIGHTSROLL=$GET(YHIGHTSROLL)
	IF YHIGHTSROLL="" SET YHIGHTSROLL=300
	set pstrYFILTER = $get(pstrYFILTER)
	
	SET YSTARTAT=1
	SET YSORTWAY=$GET(YSORTWAY)
	IF YSORTWAY'=1 IF YSORTWAY'=-1 SET YSORTWAY=1
	SET YSORTBY=$GET(YSORTBY)
	SET YFONT=2
	if (+$get(^WWWSOR(YUSER,"FONTSIZE"))'=0) set YFONT=+$get(^WWWSOR(YUSER,"FONTSIZE"))  ;SR16236
	
	SET YTABLEID=$GET(^WWWSOR(YUSER,"TABLEID"))
	IF YTABLEID="" SET YTABLEID=YFORM
	IF YTABLEID="" SET YTABLEID="WWWTABLE"
	SET YTABLECONFIG=""
	SET YHEADSIZE=$GET(^WWWSOR(YUSER,"HEADSIZE"))
	IF YHEADSIZE'="" IF $EXTRACT($REVERSE(YHEADSIZE))'=Y SET YHEADSIZE=YHEADSIZE_Y
	SET YWIDTH=""
	IF YHEADSIZE'="" FOR YI=1:1 DO  SET YWIDTH=YWIDTH+$PIECE(YHEADSIZE,Y,YI) QUIT:$PIECE(YHEADSIZE,Y,YI,99)=""
	SET YHEADFIX=$GET(^WWWSOR(YUSER,"HEADFIX"))
	IF YHEADFIX'="" IF YWIDTH>YHEADFIX SET YHEADFIX=YWIDTH
	
	IF $DATA(^WWWTABLECONFIG(YM,YTABLEID,YBED,1)) SET YTABLECONFIG=^WWWTABLECONFIG(YM,YTABLEID,YBED,1)    ; SR15543 Naked Ref
	IF $PIECE(YTABLECONFIG,Y,1)'="" IF $EXTRACT($REVERSE($PIECE(YTABLECONFIG,Y,1)))'=";" SET $PIECE(YTABLECONFIG,Y,1)=$PIECE(YTABLECONFIG,Y,1)_";"
	
	; FIXME <GRF> : The DO in the following FOR command does not have any
	;               associated code and appears should be removed
	IF YHEADFIX'="" {  //HEADFIX NIEMALS ÜBERSCHREITEN;FIS;24.02.05
		SET YCONFIGWIDTH=""
		FOR YI=1:1 DO  SET YCONFIGWIDTH=YCONFIGWIDTH+$PIECE($PIECE(YTABLECONFIG,Y,1),";",YI) QUIT:$PIECE($PIECE(YTABLECONFIG,Y,1),";",YI,99)=""
		IF YCONFIGWIDTH>YHEADFIX {
			SET $PIECE(^WWWTABLECONFIG(YM,YTABLEID,YBED,1),Y,1)=$TRANSLATE(YHEADSIZE,Y,";")
			SET $PIECE(YTABLECONFIG,Y,1)=$TRANSLATE(YHEADSIZE,Y,";")
		}
	}
	
	IF $PIECE(YTABLECONFIG,Y,1)'="",$PIECE(YTABLECONFIG,Y,1)'=YHEADSIZE {
		IF $LENGTH($PIECE(YTABLECONFIG,Y,1),";")'=$LENGTH(YHEADSIZE,Y) {
			SET $PIECE(^WWWTABLECONFIG(YM,YTABLEID,YBED,1),Y,1)=$TRANSLATE(YHEADSIZE,Y,";")
			SET $PIECE(YTABLECONFIG,Y,1)=$TRANSLATE(YHEADSIZE,Y,";")
		}
		SET YHEADSIZE=$TRANSLATE($PIECE(YTABLECONFIG,Y,1),";",Y)
		SET ^WWWSOR(YUSER,"HEADSIZE")=YHEADSIZE
	}
	
	;SET YWIDTH=""
	;IF YHEADSIZE'="" FOR YI=1:1 DO  SET YWIDTH=YWIDTH+$PIECE(YHEADSIZE,Y,YI) QUIT:$PIECE(YHEADSIZE,Y,YI,99)=""
	;IF YHEADFIX'="" IF YWIDTH>YHEADFIX SET YHEADFIX=YWIDTH
	IF YWIDTH'="" SET YWIDTH=YWIDTH+20
	
	IF $GET(YFORM)="" SET YFORM="WWWTABLE"
	
	IF $DATA(^WWWSOR(YUSER,"TITLE")) {
		WRITE "<font size="_YFONT_"><b>"
		WRITE "&nbsp;"
		WRITE $GET(^WWWSOR(YUSER,"TITLE"))
		WRITE "</b></font>"
	}
	SET YTOTAL=0
	KILL ^WWWSOR(YUSER_"TEMP")
	MERGE ^WWWSOR(YUSER_"TEMP")=^WWWSOR(YUSER)
	DO SORT
	IF YTOTAL>YLINEMAX DO PAGE
	IF YTOTAL<YLINEMAX   SET YLINEMAX=YTOTAL
	IF YLINEMIN>YLINEMAX SET YLINEMAX=YLINEMIN
	IF '$DATA(^WWWTABLECONFIG(YM,YTABLEID,YBED)) {
		SET $PIECE(YTABLECONFIG,Y,1)=$TRANSLATE(YHEADSIZE,Y,";")
		SET ^WWWTABLECONFIG(YM,YTABLEID,YBED,1)=YTABLECONFIG  ;SPEICHERN EINSTELLUNGEN
	}
	KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"TABLEDATA")
	MERGE ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"TABLEDATA")=^WWWSOR(YUSER_"TEMP")  ;MERKEN FÜR RELOAD ;to 
	
	//============== SUCHANZEIGE =============
	if YHIGHTSROLL = -1 {  ;SR16205
		DO ^WWWTABLE1(YLINEMAX,YSTARTAT,YSORTWAY,YSORTBY,YTOTAL,,,YLINEMIN,YHIGHTSROLL,pstrYFILTER)
	
	} else {
		SET YHEIGHT=(((YLINEMAX+2)*20)+4)
		;WRITE YCR,"<DIV ID=""YTABLEDISPLAY"" style="""
		WRITE YCR,"<DIV ID=""YTABLE"_YTABLEID_""" style="""
		; SR15543 vvv
		;IF YHEIGHT'>YHIGHTSROLL!(YHEADSIZE="")  WRITE " height:"_(YHEIGHT+50)_"px;"
		;IF YHEIGHT>YHIGHTSROLL IF YHEADSIZE'="" WRITE " height:"_(YHIGHTSROLL+50)_"px;"   ;SET YWIDTH=YWIDTH+20
		if (YHEIGHT>YHIGHTSROLL) && (YHEADSIZE'="") {
			write " height:"_(YHIGHTSROLL+50)_"px;"
		} else {
			write " height:"_(YHEIGHT+50)_"px;"
		}
		
		WRITE """>"
		
		;WRITE YCR,"<IFRAME name=""YTABLEDISPLAY"""
		WRITE YCR,"<IFRAME name=""YTABLE"_YTABLEID_""" id="""_YTABLEID_""""   ;BEC;01.06.05
		WRITE " src=""javascript:window.location='"_YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1("_YLINEMAX_","_YSTARTAT_","_YSORTWAY_","_YSORTBY_","_YTOTAL_",,,"_YLINEMIN_","_YHIGHTSROLL_",\'"_pstrYFILTER_"\')&amp;YFORM="_YFORM
		DO ^WWWCGI
		WRITE "'"""
		WRITE YCR," BORDER=0 FRAMEBORDER=0 FRAMESPACING=0 "
		IF YWIDTH="" {                                        ; SR15543
			write " WIDTH=100%"
		} else {
			write " WIDTH="_YWIDTH_"px"
		}
		WRITE " SCROLLING=AUTO"
		; SR15543 vvv
		;IF YHEIGHT'>YHIGHTSROLL!(YHEADSIZE="")  WRITE " height="_(YHEIGHT+50)  ;HEIGHT=100%
		;IF YHEIGHT>YHIGHTSROLL IF YHEADSIZE'="" WRITE " height="_(YHIGHTSROLL+50)
		if (YHEIGHT>YHIGHTSROLL) && (YHEADSIZE'="") {
			write " height="_(YHIGHTSROLL+50)
		} else {
			write " height="_(YHEIGHT+50)
		}
		WRITE ">"
		WRITE "</IFRAME></div>",YCR
		
		
		// vvv Function for re-load with new filter ;SR16236
		write YCR,"<script language=""JavaScript"">",YCR
		write "function reloadTable"_YTABLEID_"(pFilter) {",YCR
		write "   if (pFilter == undefined) pFilter = """_pstrYFILTER_""";",YCR
		write "   document.getElementById('"_YTABLEID_"').src='"_YAKTION_"EP=WWWMANU&YEXEC=DO|^WWWTABLE1("_YLINEMAX_","_YSTARTAT_","_YSORTWAY_","_YSORTBY_","_YTOTAL_",,,"_YLINEMIN_","_YHIGHTSROLL_",\'' + pFilter + '\')&YFORM="_YFORM_$$Replace^COMUtilStr($$VAR1^WWWCGI(1),"&amp;","&")_"'",YCR
		write "}",YCR
		write "</script>",YCR,YCR
		// ^^^^
	}
	
	QUIT
	
PAGE	//ADD PAGE FUNCTION
	NEW YCOLOR,YI,YELEMENTS
	
	SET YELEMENTS=0
	SET YCOLOR=$PIECE($GET(^WWW012(0,YM,1)),Y,5)  ;HINTERGRUNDFARBE AUS MANDANT ;out of Company 
	IF YCOLOR'="" SET YCOLOR=$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,YCOLOR,1)),Y,1)
	IF $PIECE($GET(^WWW012(0,YM,1)),Y,77)'=""  SET YCOLOR=$PIECE($GET(^WWW012(0,YM,1)),Y,77)   ;BEC;HINTERGRUNDFARBE INNENFARBE
	IF YCOLOR'="" SET YCOLOR=$PIECE($GET(^WWW100(0,"FARBE",SPRACHE,YCOLOR,1)),Y,1)
	IF YCOLOR=""  SET YCOLOR="silver"  ;AUS MANDANT ;out of Company 
	
	;---------------------------------------
	
	WRITE YCR,"<table border=0 cellspacing=0 cellpadding=0"
	WRITE " bgcolor="""_YCOLOR_""""
	WRITE ">"
	WRITE YCR,"<tr>"
	
	//FOR YI="<",1,2,3,4,5,6,7,8,9,10,">" {
	FOR YI=1:1:101 {
		SET YSTARTAT(YI)=YSTARTAT
		IF +YI'=0 SET YSTARTAT(YI)=(((YI-1)*YLINEMAX)+1)
		IF YI="<" SET YSTARTAT(YI)=0
		IF YI=">" SET YSTARTAT(YI)=((10*YLINEMAX)+1)
		IF YSTARTAT(YI)>YTOTAL QUIT
		IF (YI=35) || (YI=69) WRITE "</TR><TR>"    ;BEC;02.06.05
		WRITE "<td NOWRAP align=middle"
		WRITE " style="""
		WRITE " padding:1px;"
		WRITE " width:15px;"
		WRITE " height:15px;"
		WRITE """"
		WRITE ">"
		WRITE YCR,"<FONT SIZE=2>"
		
		DO {
			WRITE YCR,"<input type=""hidden"""
			;IF YI="<" WRITE " name=""YKZM"""
			;IF YI=">" WRITE " name=""YKZP"""
			;IF +YI'=0 WRITE " name=""YKZ"_YI_""""
			IF YI="<" WRITE " name=""YKZM"_YUSER_""""     ;BEC;01.06.05;MEHERE TABLE AUF EINER SEITE
			IF YI=">" WRITE " name=""YKZP"_YUSER_""""     ;BEC;01.06.05;MEHERE TABLE AUF EINER SEITE
			IF +YI'=0 WRITE " name=""YKZ"_YUSER_YI_""""   ;BEC;01.06.05;MEHERE TABLE AUF EINER SEITE
			WRITE " value="""
			IF +YI'=0 WRITE YLINEMAX_","_YSTARTAT(YI)_","_YSORTWAY_","_YSORTBY_","_YTOTAL_",1,,"_YLINEMIN
			IF YI="<" WRITE YLINEMAX_","_YSTARTAT(YI)_","_YSORTWAY_","_YSORTBY_","_YTOTAL_",1,0,"_YLINEMIN
			IF YI=">" WRITE YLINEMAX_","_(YSTARTAT(YI)-YLINEMAX)_","_YSORTWAY_","_YSORTBY_","_YTOTAL_",1,1,"_YLINEMIN
			WRITE """"
			WRITE ">"
		} WHILE(0)               ; always executes once only
		
		IF +YI'=0 SET YELEMENTS=YELEMENTS+1
		WRITE YCR,"<input type=""text"""
	;	IF YI="<" WRITE " id=""YKM"""
	;	IF YI=">" WRITE " id=""YKP"""
	;	IF +YI'=0 WRITE " id=""YK"_YI_""""
		IF YI="<" WRITE " id=""YKM"_YUSER_""""     ;BEC;01.06.05;MEHERE TABLE AUF EINER SEITE
		IF YI=">" WRITE " id=""YKP"_YUSER_""""     ;BEC;01.06.05;MEHERE TABLE AUF EINER SEITE
		IF +YI'=0 WRITE " id=""YK"_YUSER_YI_""""   ;BEC;01.06.05;MEHERE TABLE AUF EINER SEITE
		WRITE " value="""_YI_""""
		WRITE " readonly=""readonly"""
		WRITE " style="""
		WRITE " border:none;"
		WRITE " background-color:"_YCOLOR_";"
		IF YSTARTAT(YI)>YTOTAL!(YSTARTAT(YI)=0) WRITE " color:gray;"
		WRITE " width:23px;"
		WRITE " height:17px;"
		WRITE " cursor:pointer;"
		WRITE " padding:0;"
		WRITE " text-align:center;"
		IF YI=1 WRITE " font-weight:bold;"
		WRITE """"
		IF YSTARTAT(YI)'>YTOTAL,YSTARTAT(YI)'=0 {
		;	WRITE YCR," onClick="" { window.YTABLEDISPLAY.location='"
			WRITE YCR," onClick="" { window.YTABLE"_YTABLEID_".location='"
		;	IF YI="<" WRITE YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1(' + document.getElementById('YKZM').value + ')&amp;YFORM="_YFORM
		;	IF YI=">" WRITE YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1(' + document.getElementById('YKZP').value + ')&amp;YFORM="_YFORM
		;	IF +YI'=0 WRITE YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1(' + document.getElementById('YKZ"_YI_"').value + ')&amp;YFORM="_YFORM
			IF YI="<" WRITE YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1(' + document.getElementById('YKZM"_YUSER_"').value + ')&amp;YFORM="_YFORM
			IF YI=">" WRITE YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1(' + document.getElementById('YKZP"_YUSER_"').value + ')&amp;YFORM="_YFORM
			IF +YI'=0 WRITE YAKTION_"EP=WWWMANU&amp;YEXEC=DO|^WWWTABLE1(' + document.getElementById('YKZ"_YUSER_YI_"').value + ')&amp;YFORM="_YFORM     ;BEC;01.06.05;MEHRER TABLES AUF EINEM FORM
			DO ^WWWCGI
			WRITE "';"
			IF +YI'=0 WRITE " setStartAt"_YUSER_"("_YI_");"
			WRITE "}"""
		}
		WRITE ">"
		WRITE YCR,"</td>"
		IF YI=101 IF YTOTAL>YI WRITE "<TD>..</TD>"    ;MORE DATE AVI.
	
	}
	
	WRITE YCR,"<tr>"
	WRITE YCR,"</TABLE>",YCR
		
	;---------------------------------------
	
	$$$StartScript()
	;WRITE YCR,YCR,"<SCRIPT LANGUAGE=JavaScript>"
	;WRITE YCR,"<!--"
	;WRITE YCR,"function setStartAt(startat)"
	WRITE YCR,"function setStartAt"_YUSER_"(startat)"     ;BEC;01.06.05;MORE TABLES
	WRITE YCR,"{"
	WRITE YCR,"  var i;"
	WRITE YCR,"  for (i=1;i<="_YELEMENTS_";i++)"
	WRITE YCR,"  {"
	;WRITE YCR,"  eval(""if (document.getElementById('YK""+i+""').value == ""+startat+"") { document.getElementById('YK""+i+""').style.fontWeight='bold'; } else {document.getElementById('YK""+i+""').style.fontWeight='normal'; }"");"
	WRITE YCR,"  eval(""if (document.getElementById('YK"_YUSER_"""+i+""').value == ""+startat+"") { document.getElementById('YK"_YUSER_"""+i+""').style.fontWeight='bold'; } else {document.getElementById('YK"_YUSER_"""+i+""').style.fontWeight='normal'; }"");"    ;BEC;01.06.05;MEHER tABLS AUF EINER SEITE
	WRITE YCR,"  }"
	WRITE YCR,"}"
	;WRITE YCR,"//-->"
	;WRITE YCR,"</SCRIPT>"
	$$$EndScript()
	
	;---------------------------------------
	
	QUIT
	
SORT	//SORT DATA
	NEW YFELD,YSORT
	
	KILL ^WWWSOR(YUSER_"TEMP","DATAtemp")
	
	IF $DATA(^WWWSOR(YUSER_"TEMP","DATA")) {
		SET YLFN=""
		FOR {
			SET YLFN=$ORDER(^WWWSOR(YUSER_"TEMP","DATA",YLFN))
			QUIT:YLFN=""
			
			SET YFELD=$GET(^WWWSOR(YUSER_"TEMP","DATA",YLFN))
			
			SET YTOTAL=YTOTAL+1
			IF YTOTAL'<YSTARTAT,YTOTAL<(YSTARTAT+YLINEMAX) {
				SET YSORT=YLFN
				IF +YSORTBY'=0 SET YSORT=$PIECE(YFELD,Y,YSORTBY)
				IF YSORT="" SET YSORT=" "
				IF $EXTRACT(YLFN)=$CHAR(255) SET:YSORTWAY=1 YSORT=YLFN SET:YSORTWAY=-1 YSORT=0  ;LEERZEILEN
				SET ^WWWSOR(YUSER_"TEMP","DATAtemp",YSORT,YLFN)=YFELD
			}
		}
	} ELSEIF $DATA(^WWWSOR(YUSER_"TEMP","KEY")) {
		SET YLFN=""
		FOR {
			SET YLFN=$ORDER(^WWWSOR(YUSER_"TEMP","KEY",YLFN))
			QUIT:YLFN=""
			
			SET YDATEI=$GET(^WWWSOR(YUSER_"TEMP","KEY",YLFN))
			SET YFELD=""
			IF $DATA(@YDATEI) SET YFELD=@YDATEI
			
			SET YTOTAL=YTOTAL+1
			IF YTOTAL'<YSTARTAT,YTOTAL<(YSTARTAT+YLINEMAX) {
				SET YSORT=YLFN
				IF +YSORTBY'=0 SET YSORT=$PIECE(YFELD,Y,YSORTBY)
				IF YSORT="" SET YSORT=" "
				IF $EXTRACT(YLFN)=$CHAR(255) SET:YSORTWAY=1 YSORT=YLFN SET:YSORTWAY=-1 YSORT=0  ;LEERZEILEN
				SET ^WWWSOR(YUSER_"TEMP","DATAtemp",YSORT,YLFN)=YFELD
			}
		}
	}
	QUIT
	
]]></Routine>
</Export>