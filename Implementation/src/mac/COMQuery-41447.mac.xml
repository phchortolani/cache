<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMQuery" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMQuery ; Query 
	
#include COMSYS
	
	;-------------------------------------------------------------------------------
	; Description
	; 
    ; 1.  Uses the "Loop" entry to build a list in @Storage;
    ;     counts the number of lines found.								[GetData]
    ; 2.  Uses count to determine whether mutiple pages are required
    ;     - if so provide Page Group.									[Pages]
    ; 3.  Display column headings
    ; 4.  Loop through @Storage lines - Extract as id()
    ; 5.     Retrieve basic data as variable 'id'.
    ; 6.     Prepare to Highlight as required.
    ; 7.     Retrieve secondary data for "Ref" entries as function
    ;        or actual data location.
    ; 8.     Create a new line on the table
    ; 9.     Build and display each Column								[Col]
    ;        Entries *may* link to other forms
    ;        
    ; NOTE : If a Page Link is used to display additional lines, the
    ;        current form is replaced by a fresh copy with the browser
    ;        variable %("%KEY","QueryPage") set to PageNo_":"_QueryName
    ;        The Query array is cleared and rebuilt so is unsuitable
    ;        to preserve values from page to page.
    ;        
    ;        The routine called to process the form needs to manage
    ;        this independently of THIS routine.
    ;        
    ;        Do not use ^mtemp("Query",$job,Query("Name")) as this is
    ;        the top level of @Storage which is used for the line count.
    ;        Similarly ^mtemp("Query",$job,Query("Name"),0) contains a
    ;        key string to identify a changed query.  [ @Storage@(0) ]
    ;        
    ;        The suggested solution is to put them in
    ;        @Storage@("Save",SaveType) as this will be outside the
    ;        numerical range used by Line in the subroutine Query.
    ; 
	; History
	; 08-Oct-2004	GRF		SR10464 : Documentation added
	;-------------------------------------------------------------------------------
	
Query(&Query,&VORG)
	;-------------------------------------------------------------------------------
	; Main Query Entry Point
	;   Display a Table
	;   Handles multiple pages
	; 
	; ByRef : Query  - Array of Query Parameters
	; Query("Name")  - Query Name
	; Query("Class") - Default Class
	; Query("Form")  - Form for Link back (Current Form)
	; Query("Loop")  - Call back code for building the list of rows
	; Query("Link")  - Default Link, Y delimited
	; 				 1 - Form Name to link to (@Code for execute)
	; 				 2 - Field Type (P or D)
	; 				 3 - Field Number
	; Query("Get")   - Get Record execute Code, sets id
	;                  If it is necessary to modify some data item for display
	;                  purposes (e.g. if a text entry contains a date string),
	;                  pass id into a routine to update the value, returning the
	;                  new id
	;                   - returning the whole id - 
	; Query("Col",Num) - Column Data, Y delimited
	; 				 1 - Field Type (P or D)
	; 				 2 - Field Number
	; 				 3 - Alignment Left or Right (L, C or R)
	; 				 4 - Reference Flag (R, L or Null) [reference or link]
	; 				 5 - Reference Class
	; 				 6 - Reference Data - links with Query("Ref",Ref)
	; 				 7 - Default Data if it is extracted as NULL.
	; 				 8 - Column Heading
	;				 9 - No Data Format (1 = Dont format the data)
	; Query("Col",Num,"Link") - Column Link, overides default, same format
	; Query("Col",Num,"DoInstead") - Code to handle writing out cell content
	; Query("Ref",Ref)  - Execute code to set Reference data
	; Query("PageSize") - Number of records to show on a page. (Default 100)
	; Query("Title") - Heading to be displayed above the grid
	; 
	; ByRef : VORG - Parameters
	; 
	; History
	; 20-Mar-2012	SCR		SR17993: Added 'No Data format' and center option
	; 12-Oct-2005	JW		SR13671: Added "Title"
	; 08-Oct-2004	GRF		SR10464 : 'Pages' was displaying too many page numbers
	; 						because End was returning other than a count of the lines.
	; 						This could generate an UNDEFINED error when trying to
	; 						execute the "Get" command.
	; 29-Apr-2004	shobby	If the Reference is to a method call then don't use a
	; 						get around the returned value. (TR007245)
	; 20-May-2003	SCR		New Change
	;-------------------------------------------------------------------------------
	new ANZM,Class,Col,ColSpec,DataString,End,Line,PageSize,QueryName,Ref,Start
	new Storage,YDDSATZ,blnHighlight,count,id,strHeading
 	
	// TODO: Investigate safety of having $job in the subscript for ^mtemp
 
	set ANZM       = 0
	set YDDSATZ    = 1
	set strHeading = ""
	set Col        = ""
	set PageSize   = $get(Query("PageSize"))
	if 'PageSize set PageSize = 100
	
	; Get Data
	
	set QueryName = $get(Query("Name"))
	if (QueryName="") set QueryName = " "
	set Storage   = "^mtemp(""Query"","_$job_","""_QueryName_""")"
	Do GetData(.Query,Storage)
	
	if $get(Query("Title"))'="" {		//SR13671
		write YCR,"<br><FONT SIZE=2><B>&nbsp;",Query("Title"),"</B></FONT>"
	}
	
	do ^WWWFRAME(0)
 
	set Start = 1
	set End   = $piece($get(@Storage),Y,1)
 
	if End>PageSize do Pages(End,PageSize,.Query,.Start,.End)
	
	for  {
		set Col = $order(Query("Col",Col))
		quit:Col=""
		
		set ColSpec=Query("Col",Col)
		set Class=""
		if $piece(ColSpec,Y,8)'="" {
			set $piece(strHeading,Y,Col) = $piece(ColSpec,Y,8)
		} else {
			if $piece(ColSpec,Y,4)="R" set Class = $piece(ColSpec,Y,5)  ; Referenced Data Class
			if Class=""                set Class = $get(Query("Class"))
			set $piece(strHeading,Y,Col) = $$^WWWFELDNAME(Class,$extract($piece(ColSpec,Y,1)),$piece(ColSpec,Y,2))
		}
	}
	do FF^WWWW()  ;FORMFEED
	do START100^WWWTAB
	do HEADER^WWWTAB(strHeading)
	
	set count=""
	for Line=Start:1:End {
		kill id
		merge id = ^mtemp("Query",$job,QueryName,Line)
		if '$data(id) continue                          ; SR10464
		// TODO : Consider appropriate error message rather than skipping <GRF>
		
		xecute Query("Get")  ; Set id
		
		; based on id, determine whether to highlight line.
		set blnHighlight = 0
		if $data(Query("Highlight")) xecute Query("Highlight")  
		
		set DataString = id
		set Ref = ""
		for  {
			set Ref = $order(Query("Ref",Ref))
			quit:Ref=""
			
			if $extract(Query("Ref",Ref),1,2)="$$" {
				xecute "Set Ref(Ref)="_Query("Ref",Ref)    			; 29-Apr-2004
			} else {
				xecute "Set Ref(Ref)=$get("_Query("Ref",Ref)_")"
			}
		}
		
		set Col=""
		do NL^WWWTAB
		for {
			set Col=$order(Query("Col",Col))
			quit:Col=""
			
			do Col(.Query,Col,.id,.Ref,blnHighlight)
		}		
		do EL^WWWTAB
	}
	do ^WWWFRAME(1)
	
	quit
	
	
Col(&Query,Col,&id,&Ref,pblnHighlight)
	;-------------------------------------------------------------------------------
	;
	; History
	;
	; 20-Mar-2012	SCR		SR17993: Added 'No Data Format' and center options
	; 16-Oct-2006	PO		SR15122: DoInstead code now returns string
	; 05-May-2006	PO		SR14427: Allow link drawn out to be done by foreign code
	; 							(Phase 2)
	; 13-Sep-2005	RPW		SR13306: Added the ability to handle checkboxes with a
	; 							call back. If we are a checkbox, dont write out the
	; 							hyperlink
	; 29-Apr-2004	shobby	TR007245: Included a default value when there is no data
	;-------------------------------------------------------------------------------
	new Class,ColSpec,Data,Form,Format,Link,LinkData,NextId,strDoInstead
	new RefData,RefType,Source,strDefault,tmpGray,tmpWhite,CheckBox,idKey,idRecord
	new blnNoFormat
	
	set ColSpec = $get(Query("Col",Col))
	set Link    = $get(Query("Col",Col,"Link"))
	if Link="" set Link = $get(Query("Link"))
	
	set CheckBox     = $get(Query("Col",Col,"CheckBox")) ; SR13306
	set strDoInstead = $get(Query("Col",Col,"DoInstead")) ; SR14427 (Phase 2)
	
	set Source     = $piece(ColSpec,Y,1,2)
	set Format     = $piece(ColSpec,Y,3)
	set RefType    = $piece(ColSpec,Y,4)
	set RefData    = $piece(ColSpec,Y,6)
	set strDefault = $piece(ColSpec,Y,7)
	set Form       = $piece(ColSpec,Y,5)
	set blnNoFormat= $piece(ColSpec,Y,9) ;SR17993
	set Class=""
	if RefType="R" set Class = $piece(ColSpec,Y,5)  ; Referenced Data Class
	if Class=""    set Class = $get(Query("Class"))
	
	if RefData'="" {
		set Data = $$Data(Ref(RefData),Source,Class,,blnNoFormat) 	;SR17993
	} else {
		set Data = $$Data(.id,Source,Class,,blnNoFormat)			;SR17993
	}
	if Data="" set Data = strDefault
	
	set LinkData = ""
	if RefType="L" { ; Link Ref Type
		set LinkData = Data
		set Data     = "Link"	
	}
	set:'blnNoFormat Data = $zconvert(Data,"O","HTML")  ; Convert '>' to &gt etc. ; SR17993
	;set Data = $zconvert(Data,"O","HTML")  ; Convert '>' to &gt etc.
	if Data="" set Data = "&nbsp;"
	
	set tmpGray =YGRAY
 	set tmpWhite=YWHITE
 	if pblnHighlight {
	 	set YWHITE="lightgreen"
	 	set YGRAY ="lightgreen"
 		}
	; SR17993 vvvv
	if Format="L" {
		do NFL^WWWTAB
	} elseif Format="C" {
		do NFZ^WWWTAB
	} else {
		do NFR^WWWTAB
	}
	; SR17993 ^^^^
	;if Format="L"  do NFL^WWWTAB
	;if Format'="L" do NFR^WWWTAB
	
	write "&nbsp;"
	write "<FONT SIZE=2>"
	;write "id="_
 	
	set YGRAY  = tmpGray
 	set YWHITE = tmpWhite
	
	if strDoInstead="" { // SR14427 (Phase 2)
		if Link'="" {
			set LinkData = ""
			set Form     = $piece(Link,Y,1)
			set NextId   = $$Data(.id,$piece(Link,Y,2,3))
			if CheckBox="" { ; SR13306
				do Link(Form,NextId)
			}
		}
	
		if LinkData'="" {
			set Form   = $piece(LinkData,",",1)
			set NextId = $piece(LinkData,",",2,9)
			set Link   = LinkData
			do Link(Form,NextId)
		}
	
		 ; SR13306 Handle the checkbox instead of the YES/NO hyperlink.	
		if '$get(Query("Locked"),0) && (CheckBox'="") {
			set Data     = $select(Data="Yes":1,1:0) ; Handle Old Data.
			set idKey    = $piece(NextId,Y,1)
			set idRecord = $piece(NextId,Y,2)
			write "<input type=checkbox "_$select(Data=1:"CHECKED",1:"")_" onclick='EventValue("""_YUCI_""","""_YUSER_""","""_YFORM_""",""FIX"","""_CheckBox_""","""_idKey_""",""6"","""_idRecord_"""+this.checked);'>"		
		} else {
			write Data
		}
		if Link'="" {
			write "</A>"
		}
	} else {
		xecute "write $$"_strDoInstead_"("""_Col_""","""_Data_""")" // SR15122
	}
	write "&nbsp;"
	do EF^WWWTAB
	quit
	
	
Data(&Record,Field,Class,Blank,pblnNoFormat=0)
	;-------------------------------------------------------------------------------
	; Get Data from Source
	;   Record - Data
	;   Field - Type & Number
	;   Class - Used for formating, No Class = No Formatting
	;   Blank - Return Blank if Data is Null, Used for Referenced data
	; 
	; History
	; 20-Mar-2012	SCR		SR17993: No Format Option
	; 20-Aug-2004	shobby	SR10265: Pass in the 'Return' parameter to WWWFORMAT to
	; 							specify the relation.
	;-------------------------------------------------------------------------------
	new Cnt,Data,End,Index,Return,Start,Type

	
	set Type   = $extract($piece(Field,Y,1),1)
	set Return = $extract($piece(Field,Y,1),2)
	
	set (Start,End) = $piece(Field,Y,2)
	if +Start'=Start {               ;  Range Check
		set Start = +Start
		set End   = $piece(End,"-",2)
	}
	set Data=""
	if Type="P" {
		set Index = 0
		for Cnt = Start:1:End {
			set Index = Index+1
			set $piece(Data,",",Index) = $get(Record(Cnt))
		}
	}
	
	if Type="D"            set Data = $piece(Record,Y,Start,End)
	if ($get(Class)'="")&&('pblnNoFormat)     set Data = $$^WWWFORMAT(Class,Type,Start,Data,Return) ;SR17993
	;if $get(Class)'=""     set Data = $$^WWWFORMAT(Class,Type,Start,Data,Return)
	if $get(Blank),Data="" set Data = " "

	quit Data
	
	
Link(Form,Key,Options)
	;-------------------------------------------------------------------------------
	; Provide hyperlinks from displayed data to other forms if required.
	; 
	; History
	; 02-Jan-2014	SCR		SESDF655: Added Execute only option
	; 08-May-2006	PO		SR14427 (Phase 2): Allow for table to be displayed on a
	; 							form when no record is loaded.
	; 24-Apr-2006	HJRB	SRBR014014: YOPEN=2 to keep DATEN between pages
	; 12-Oct-2005	JW		SR13671: YPARA shouldn't have extra comma
	; 01-Aug-2005	PO		SR13009 Also only add form name to YBACK if Form and
	; 							YFORM not same; if Form and YFORM are same and YBACK
	; 							ends with YFORM then remove it.
	; 21-Jul-2005	PO		SR13009 Do not continually append to YBACK
	; 13-May-2004	sh/paul	TR007715 : Only set YPARA if it isn't blank,
	;							Sometimes can end up with YPARA being set in this
	;							link twice.
	; 30-Apr-2004	shobby	TR007578: Append YFORM to existing YBACK rather than
	; 							overwriting
	; 							<TODO - shobby, need to find the ID somewhere>
	; 29-Apr-2004	shobby	TR007245: Put 2nd part of Key into YPARA
	;-------------------------------------------------------------------------------
	new strYBACK
	
	if Form["@ExecuteOnly@" {
		xecute $piece(Form,"@",3,999)
		quit
	}
	set strYBACK = YBACK
 	write "<A HREF="_""""_YAKTION_"EP=WWWFORM"
 	if $extract(Form)="@" xecute $extract(Form,2,999)
	if (Form'=YFORM) && ($reverse($piece($reverse(YBACK),",",2))'=YFORM) {
		set strYBACK = strYBACK_YFORM_","
	
	} elseif (Form=YFORM) && ($reverse($piece($reverse(YBACK),",",2))=YFORM) {
		set strYBACK = $piece(strYBACK,",",1,$length(strYBACK,",")-2)_","
	}
	set YBACK = strYBACK
	write "&amp;YFORM="_Form_"&amp;YKEY="_$piece(Key,Y,1)_"&amp;YBACK="_YBACK ;_YFORM_","
 	
	if $piece(Key,Y,2)'="" {
		write "&amp;YPARA="_$piece(Key,Y,2)
	}
	if YFKEY_YKEY '= "" write "&amp;YFKEY="_YFKEY_YKEY_","
	
	new YFORM,YKEY,YBACK,YSEITE,YPARA ; Assumes YFORM,YKEY,YBACK,YSEITE,YPARA in WWWCGI so they are newed
	do ^WWWCGI
	write """"_">"
	quit
	
	
Pages(Count,PageSize,&Query,&Start,&End)
	;-------------------------------------------------------------------------------
	; If the number of entries is greater than the nominated page size
	; display an extra group to allow movement through the pages.
	; e.g.
	;    Items per page  [ 100 ]  1  2  3  4  5
	;                                -  -  -  -     <= Hyperlinks when on page 1
	;                                
	; ByRef : Query,Start,End
	; 
	; TODO : Consider adding << and >> (and possibly < and >) buttons since
	;		 normally only 10 pages each side of currently displayed page -
	;		 no fast shift if a lot of data exists <GRF>
	;		 
	;  
    ; History:
    ; 16-May-2006	Steve S	SR14579: Show "Items Per Page" as greyed-out (readonly)
    ; 12-Oct-2005	JW		SR13671: Added YPARA
	;-------------------------------------------------------------------------------
	new Current,Max,Page
	
	set Current = 0
	if ($zconvert($get(Query("Name")),"O","HTML")=$piece($get(%("%KEY","QueryPage")),":",2)) {
		set Current = +$get(%("%KEY","QueryPage"))
	}
	if 'Current set Current = 1
	set Max   = Count-1   \ PageSize + 1
	set Start = Current-1 * PageSize + 1
	set End   = Current   * PageSize
	if (End>Count) set End = Count
	
	//FIXME: Using a FIN language text in a COM module ???
	
	write "<TR SIZE=2>",!,"<TD ALIGN=LEFT VALIGN=TOP>"
	write YCR,"<FONT SIZE=2><B>   "_$$^WWWTEXT("Fin00068")_": "_"</B></FONT>"      ;  Items per page  
	write "</A>"
	write YCR,"<INPUT NAME="_"YANZAHL SIZE=5 MAXLENGTH=5 TYPE=float VALUE="_""""_PageSize_""""
	write YCR," TABINDEX=ART style="_""""_"padding-top:0 ;padding-bottom:0"_""""_" style="_""""_"text-align:right; background-color:lightgrey"_"""" //SR14579
	;write " onChange='submit();'"
	;write "  onFocus='select();'"
	write "  readOnly"
	write "  unselectable"
	write ">"
	
	for Page=$select(Current-10<1:1,1:Current-10):1:$select(Current+10>Max:Max,1:Current+10) {
		write "&nbsp;"
		if Page'=Current {
			;do Link("@s Form=$$PageLink($get(Options),"""_$get(Query("Name"))_""")",YKEY,Page)
			do Link("@s Form=$$PageLink($get(Options),"""_$get(Query("Name"))_""")",YKEY_Y_YPARA,Page)	//SR13671
			write "<U>",Page,"</U></A>"
			
		} else {
			write Page
		}
 	}
	write "</TR>"
	quit
	
	
PageLink(Page,PageName)
	;-------------------------------------------------------------------------------
	; Generated URL for re-calling form with an offset to the required page of data
	; 
	; History:
	; 24-Apr-2006	JW		SRBR014014: YOPEN=2 to keep DATEN between pages
	;-------------------------------------------------------------------------------
	new Form
	
	set Form = YFORM
	if $get(YSEITE) write "&amp;YSEITE="_YSEITE
	kill YPARA1
	
	write "&amp;YOPEN=2"
	write "&amp;QueryPage="_Page_":"_$zconvert($get(PageName),"O","HTML")
	quit Form
	
	
GetData(&Query,&Storage)
	;-------------------------------------------------------------------------------
	; Use the "Loop" command to build the lines to be displayed
	; Retain and recheck key so only extract data once for a query session.
	; Set the number of lines in @Storage
	; 
    ; If there are multiple pages, the form will be called again whenever
    ; a page link is used to view other information.  In that case the Browser
    ; will pass in a value through %("%KEY","QueryPage") - this will not exist on
    ; the initial entry.
    ; 
    ; ("QueryPage" is set in PageLink() when there are multiple pages.)
	; 
	; ByRef : Query   - Array of Query parameters
    ;         Storage - Data storage location (^mtemp)
	; 
	; History:
	; 08-Oct-2004	GRF		SR10464 : Replace ! with ||
	;-------------------------------------------------------------------------------
	new Count,key
	
	set key=YKEY_Y_YFORM_Y_$GET(YSEITE)_Y_$get(Query("Name"))
	
	; If the key is different or we are coming into the form from other than a Page Link
	if ((key'=$get(@Storage@(0))) || '$data(%("%KEY","QueryPage"))) {
		kill @Storage
		set Count=""
		xecute "set Count=$$"_Query("Loop")_"(.Query,.Storage,.VORG)"
		set @Storage = Count_Y_$horolog
		kill %("%KEY","QueryPage")
	}
	set @Storage@(0) = key
	quit
	
]]></Routine>
</Export>