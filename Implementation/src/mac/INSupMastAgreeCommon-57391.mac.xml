<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSupMastAgreeCommon" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSupMastAgreeCommon
	
#include COMSYS
#include INConst
#include COMConst
#include WWWConst
	
	;++++++++++++++++++++++++++++++++++++++++++++++++++
	;   Routines common to supplier agreement forms
	;++++++++++++++++++++++++++++++++++++++++++++++++++
	
OnAfterDataFields(pYKEY,pidForm="INSupMastAgreeItem")
	;-------------------------------------------------------------------------------
	; Routine to run after data fields
	;
	; Params:	pYKEY	: The YKEY value
	; 			pidForm	: The form we're in
	;
	; Returns:
	;
	; History:
	; 29-Jun-2007	RPW		SR15553: Allow the grid to be locked ignoring YSEITE
	; 28-Dec-2006	Steve S	SR15341: Created (Copied from REQUISITIONVAR)
	;-------------------------------------------------------------------------------
	new idForm,YAUSWAHL,blnEnabled
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = pidForm
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = YFORM
	
	set blnEnabled = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	
	if blnEnabled {
		set $$$COMGridEditParameterEnabled(YAUSWAHL) = (YSEITE=1)
	} else {
		set $$$COMGridEditParameterEnabled(YAUSWAHL) = $$$NO
	}
	
	$$$GRIDStart(pidForm,pYKEY)
	quit
	
	
OnBeforeSave(pYKEY)
	;-------------------------------------------------------------------------------
	; On before save code -- save the grid.
	;
	; Params:	pYKEY		: The YKEY value
	;
	; Returns:
	;
	; History:
	; 04-Jan-2007	SS		SR15342: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		$$$Error(strStatus)	
	}
	quit
	
	
ValidateGridLine(pobjLine,pintCurField,pintQtyFld,pintOpenDateFld,pintCloseDateFld)
	;-------------------------------------------------------------------------------
	; Common code for validating agreement lines.
	; Checks for non-negative qty, and open date <= close date.
	; If any problems are found, the appropriate cell is highlighted
	; and given an error tooltip.
	;
	; Params:	pobjLine			: The grid line record
	; 			pintCurField		: The current focus field
	; 			pintQtyFld			: The qty field number (piece)
	; 			pintOpenDateFld		: The open date field number (piece)
	; 			pintCloseDateFld	: The close date field number (piece)
	;
	; Returns:
	;
	; History:
	; 18-Jan-2010	shobby	SR15753: Cleanup redundant code.
	; 04-Jan-2007	SS		SR15342: Created
	;-------------------------------------------------------------------------------
	new fltQty,dteOpen,dteClose
	
	set fltQty   = $piece(pobjLine,Y,pintQtyFld)
	set dteOpen  = $piece(pobjLine,Y,pintOpenDateFld)
	set dteClose = $piece(pobjLine,Y,pintCloseDateFld)
	
	if (pintCurField=pintQtyFld) {
		if (fltQty'="") && (fltQty<=0) {
			set YWARNING = $$$YES
			set YTOOLTIP = $$$Text("IN00632")  ; "Quantity must be greater than 0."
		}
	
	} elseif (pintCurField = pintOpenDateFld) {
		if (dteOpen'="") && (dteClose'="") && (dteOpen>dteClose) {
			set YWARNING = $$$YES
			set YTOOLTIP = $$$Text("IN00614") ; "Open Date can not be after Close Date."
		}
	
	} elseif (pintCurField = pintCloseDateFld) {
		if (dteClose'="") && (dteOpen'="") && (dteClose<dteOpen) {
			set YWARNING = $$$YES
			set YTOOLTIP = $$$Text("IN00614") ; "Open Date can not be after Close Date."
		}
	}
	quit
	
]]></Routine>
</Export>