<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWKOPF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
formNameWWWKOPF(YKOPF)
	
#include COMSYS
#include WWWConst
	
#define jsMarker(%1)
#;define jsMarker(%1) 	write YCR,YCR,"<!-- ************************* ",%1," (WWWKOPF)************************* -->",YCR,YCR
	;-------------------------------------------------------------------------------
	; 	Displays System Heading, including [logoff] button
	;		HTML Heading          HTMLKOPF
	;
	; Inputs : 
	;	YKOPF	Current page title
	;
	; ByRef :
	;	^WWWVAR variables
	;	
	; Returns :
	;
	;
	; History :
    ; 15-Jul-2014	shobby	HEVA-1565: MenuType should also consider WWW012
	; 21-Oct-2013	shobby	SESPE-581: Always show header if using a MegaMenu....
	; 20-Jun-2013	shobby	CORE-127: Missing TR and TD could cause MegaMenu to appear above the buttons in firefox.
	; 07-Jun-2013	shobby	CORE-123: Removed extra border on MegaMenu
	; 06-Jun-2013	shobby	CORE-116.2: MegaMenu created in WWWBODY for COMViewSearch forms.
	; 05-Jun-2013	shobby	CORE-116: Create MegaMenu at this point.
	; 30-Apr-2012	shobby	SR17998: More detailed determination as to whether to show
	;						form header.
	; 11-Dec-2009	GRF		SR16871: class macros, cleanup, doco
	; 15-Oct-2009	shobby	SR16925: Only translate header if a language code exists.
	; 06-Jan-2009	FIS		don't use gradient filter with system colors
	; 15-Dec-2008	HQN		Added additional notes regarding functionality
	; 25-Jul-2007	RPW		SRadhoc: Rewrote in { syntax 
	; 07-May-2007	GRF		SR15511: Doco; quits; expand commands
	; 13-Dec-2006	JW		BR014262: Rewrite. Use GetType fn.
	; 18-Sep-2006	JW		Cleaned up.
	; 31-May-2005	RPW		Added the job to the tooltip.
	; 28.04.1998	DT		GEAENDERT=57916,57692
	;-------------------------------------------------------------------------------
	new YA,YQ,YVOR1,objWWW120
	
	if ($$MenuType^WWW013() '= 13)&&($$MenuType^WWW013() '= 14) quit:$get(YKOPF)="" ;HEVA-1565 
	;HEVA-1565 if $$$WWW013MenuType($get(^WWW013(0,YBED,1))) '= 13 quit:$get(YKOPF)=""
	
	; FIXME : Precludes text codes with alpha prefixes but if that is allowed we
	;         might have a problem with hard-coded heading strings.
	if +YKOPF'=0 if $data(^WWW009(0,SPRACHE,YKOPF)) if $$^WWWTEXT(YKOPF)'=YKOPF set YKOPF = $$^WWWTEXT(YKOPF)  ; Convert text code to heading text
	set YVOR1 = $get(^WWW012(0,0,1))  ; General Company Parameters
	
	;---------------------------------------
	set YQ = $$$YQEnable
	if $$DoNotDisplayFormHeader^WWW120(YFORM) set YQ=1 ;SR17998 
	;SR17998 if $get(YFORM)'="" {
	;SR17998 	set objWWW120 = $get(^WWW120(0,YFORM,1))
	;SR17998 	if $$$WWW120DoNOTDisplayFormHeader(objWWW120) {
	;SR17998 		set YQ = 1
	;SR17998 	} elseif $$$WWW120InheritCompanyProperties(objWWW120) {
	;SR17998 		if $$$WWW012DoNotDisplayHeader(YVOR1) set YQ = 1
	;SR17998 	}
	;SR17998 }
	
	if ($$MenuType^WWW013() '= 13) quit:YQ=1 ;HEVA-1565 
	;HEVA-1565 if $$$WWW013MenuType($get(^WWW013(0,YBED,1))) '= 13 quit:YQ=1
	;---------------------------------------
	
	
	if $get(YUSER)'="" set $piece(^WWWUSER(0,YUSER,1),Y,10)=$$$YES  ; Display Form heading ?
	
	$$$jsMarker("Form Title")
	
	write "<NOBR id='NOBR'>" ;SR1799
	;HEVA-1565if $$$WWW013MenuType($get(^WWW013(0,YBED,1))) = 13 {										;CORE-123
	if $$MenuType^WWW013() = 13||($$MenuType^WWW013() = 14) {										;CORE-123;HEVA-1565
		write "<table id=""FRAME_Header"" width=""100%"" border=""0"" cellspacing=""0"" cellpadding=""0"">"          ;CORE-123
 	} else {																					;CORE-123
  		do ^WWWFRAME(0,,,"FRAME_Header") ; open table ;CORE-81									;CORE-123
 	}																							;CORE-123
	
	if ($$GetType^WWWMENU()=7) {       ; POPUP
		do V3
	} else {
		;HEVA-1565 if ($$$WWW013MenuType($get(^WWW013(0,YBED,1)))=13)&&(YFORM'="")&&($$$WWW120FormType($get(^WWW120(0,YFORM,1)))'=12) {
		if ($$MenuType^WWW013()=13)&&(YFORM'="")&&($$$WWW120FormType($get(^WWW120(0,YFORM,1)))'=12) { ;HEVA-1565 
			if (YFORM'="COMViewSearch")||($$$WWW120FormType($get(^WWW120(0,YFORM,1)))=5) {
				write "<TR><TD>" 	;CORE-127
				write $$GetMegaMenu^WWWMegaMenu(YKOPF) //SR17998 ;CORE-116 ;CORE-116.2
				write "</TD></TR>" 	;CORE-127
			}
		}elseif ($$MenuType^WWW013()=14)&&(YFORM'="")&&($$$WWW120FormType($get(^WWW120(0,YFORM,1)))'=12) { ;HEVA-1565 
			if YFORM="WWWBLANK"{
				if ($$$WWW120FormType($get(^WWW120(0,YFORM,1)))=5) {
					write "<TR><TD>" 	;CORE-127
					write $$GetMegaMenu^WWWMenuSideBar(YKOPF) //SR17998 ;CORE-116 ;CORE-116.2
					write "</TD></TR>" 	;CORE-127
				}
			}
		} else {
			do V2
		}
	}
	do ^WWWFRAME(1)                    ; close table
	
	quit
	
	
V1
    ;-------------------------------------------------------------------------------
    ; VERSION CLASSIC 
    ; NOTE: Not currently in use
    ; 
    ; History:
	; 09-Aug-2007	shobby	BR014237: Call to CreateLoginLink so that pressing the X
	; 							button will go back to the login screen.
    ; 09-Dec-2005	JW		SR13195: Removed confirm msg
    ;-------------------------------------------------------------------------------
	write YCR,"<TR bgcolor=0>",YCR
	
	write "<TD bgcolor=midnightblue align=left nowrap>"
	write "<font color=white size=3>"
	write "<b>",YCR
	
	do ^WWWUP($$$NO)
	write YCR
	if '$find(YKOPF,".gif") write "&nbsp;"_$$^WWWUML($translate(YKOPF,"_"," "))     ; überschrift 
	write " ["_$get(YUCI)_"/"_$get(YM)_"]"
	write " </B>"
	write "</td>",YCR
	
	write "<TD bgcolor=silver align=right width=20>"
	do CreateLoginLink()
	write "</TD>",YCR
	
	write "</TR>",YCR
	quit
	
	
CreateLoginLink()
	;-------------------------------------------------------------------------------
	; Draw the hyperlink nicely.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 15-Feb-2010	shobby	SR17176: Try and Catch to stop error when cancelling 'Discard
	; 							Changes' message box.
	; 16-Oct-2009	shobby	SR16948: Can't use $job as a subscript to preserve URL
	; 							in CacheTempURL
	; 09-Aug-2007	shobby	SRBR014237:	Renamed from Blank and changed to call out
	; 							to the login screen.
	; 25-Jul-2007	RPW		SRadhoc: Created
	;-------------------------------------------------------------------------------
	new strURL
	
	set strURL = $get(^CacheTempURL(YUCI,YUSER))
	if strURL="" set strURL = $$getLoginPage^WWWLogin()
 	write "<A onClick="""
 	write "try {"												;SR17176
	;write " top.document.location.href='"_strURL_"'; "
	write " window.top.location.href='"_strURL_"'; "
	write "} catch (e) { }"
	write """>"													;SR17176
	write "<IMG SRC="""_YGIF_"exit.gif"" ALIGN=ABSBOTTOM TITLE="_$$^WWWTEXT(33980)_" border=0>"   ; "Logout"
	write "</A>"
	quit
	
	
	;-------------------------------------------------------------------------------
	; Form Header  (0/1/Locn/USER)                     | AlphaLinc 1.23.45 | (o) [X]    V2^WWWKOPF
	;-------------------------------------------------------------------------------
	;  [Button Line]
	;---------------------------------------
	;
	;
	;  [Form]
	;
	;
	;---------------------------------------
	;  [Page End]
	;-------------------------------------------------------------------------------
	
	; <TR>
	;   <TD NOWRAP BGCOLOR=xxx style=" border-right:none;" ALIGN=LEFT>
	;        [Start Anchor] [Form Header] [Session Information]
	;   </TD>
	;   <TD> [Version No.]    </TD>
	; [ <TD> [Flexible Menu]  </TD> ]
	;   <TD> [Login Link  X button]     </TD>
	; </TR>


HeaderText(YFORM)
	;-------------------------------------------------------------------------------
	;
	; History:
	; 26-Dec-2013	shobby	CORE-291.2: Don't UMLAU the text.
	; 17-Dec-2013	shobby	CORE-291: Created
	;-------------------------------------------------------------------------------
	new strHeader
	
	set strHeader = $$FormHeaderOrImageFile^WWW120(YFORM)
	if $get(YMENUNAME)'="" {
		set strHeader=$TRANSLATE(YMENUNAME,"_"," ") ;CORE-291.2
	} elseif $extract(strHeader)="@" {
		xecute "set strHeader="_$extract(strHeader,2,$length(strHeader))
	} else {
		IF '$FIND(YKOPF,".gif") {
			set strHeader=" "_$TRANSLATE(YKOPF,"_"," ")
		}
	}
	quit strHeader

V2
    ;-------------------------------------------------------------------------------
    ; VERSION IN EINER FARBE ;with single colour 
    ;
    ; History:
    ; 15-Jul-2014	shobby	HEVA-1565: MenuType should also consider WWW012
    ; 18-Dec-2013	shobby	CORE-291: Subroutined HeaderText and removed WWWCoreChanges
    ; 25-Feb-2011	GRF		SR17661: Pass ColorCodeForHeaderRight to Warning Light
	; 08-Feb-2011	shobby	SR17657: Dashboard indicator on RHS 
	; 22-Sep-2010	GRF		SR17515: "FARBE" property is not language based; missing
	; 							</font>
    ; 21-May-2010	shobby	SR17316: Missing >
    ; 22-Apr-2010	shobby	SR17253: Cross Browser Support
	; 01-Sep-2009	PPP		SRAdhoc: Add the version #,and the ability to add some
	; 							Text (e.g. BETA)
	; 16-Dec-2008	HQN		SR16240: Detect menu type 10, show links 
    ; 12-Dec-2008	HQN		SR16240: Moved System Information Tooltip building to
    ; 							$$GetSystemInfoTooltip for code reuse.
    ; 16-Sep-2008	shobby	BR014983: Allow calling out to external routines.
    ; 							Called from form or customisation.
    ; 16-Sep-2007	shobby	BR014983: Put the location name back in (and tidied up
    ; 							some 'new line' issues in the tooltip)
    ; 24-Oct-2007	GRF		Separation of IP and Form
	; 09-Aug-2007	shobby	BR014237: Call to CreateLoginLink so that pressing the X
	; 							button will go back to the login screen.
    ; 25-Jul-2007	RPW		SRadhoc: Rewrote in { syntax 
    ; 09-Dec-2005	JW		SR13195: Removed confirm msg
    ; 25.04.2005	FIS		SR12200: $$$WWW0121ColorTableHeader
    ; 11.12.2003	FIS		colour on the right
    ;-------------------------------------------------------------------------------
	new COLOR,COLORR,strHeader
	
	;	D101		$$$WWW012ColorCodeForHeaderLeft()
	;	D147		$$$WWW012ColorCodeForHeaderRight()
	;	
	;	D112		$$$WWW120InDevelopmentBy	=> RED
	;	
	;	D75			$$$WWW0121ColorTableHeader
	
	set COLOR=""
	if $piece(YVOR1,Y,101)'="" set COLOR = $piece($get(^WWW100(0,"FARBE","EN",$piece(YVOR1,Y,101),1)),Y,1)
	if COLOR="" set COLOR = "DIMGRAY"
	
	set COLORR = $piece(YVOR1,Y,147)
	if COLORR'="" set COLORR = $piece($get(^WWW100(0,"FARBE","EN",COLORR,1)),Y,1)
	if COLORR=""  set COLORR = COLOR
	
	if ($get(YFORM)'="") && ($piece($get(^WWW120(0,YFORM,1)),Y,112)'="") {
		set COLOR  = "RED"
		set COLORR = "RED"
	}
	if ($get(YLOCATION)'="") && (+$piece($get(^WWW0121(0,0,YLOCATION,1)),Y,75)'=0) {
		set COLOR  = $piece($get(^WWW100(0,"FARBE","EN",$piece($get(^WWW0121(0,0,YLOCATION,1)),Y,75),1)),Y,1)
		set COLORR = COLOR
	}
	
	write YCR,"<TR>",YCR
	
	;---------------------------------------
	; Form Header
	;---------------------------------------
	write "<TD NOWRAP"
	write " BGCOLOR="_COLOR  ;für netscape & co.
	write " style="""
	if $piece(YVOR1,Y,101)'>256 write $$ImageTransformGradient^WWWFORMCrossBrowserSupportVisual(COLOR,COLORR) ;SR17253
	;SR17253 IF $PIECE(YVOR1,Y,101)'>256 WRITE "filter:progid:DXImageTransform.Microsoft.Gradient(GradientType=1, StartColorStr='"_COLOR_"', EndColorStr='"_COLORR_"');"  ;FIS;11.12.03  ;SR16236 system colors
	write " border-right:none;"
	write """"                  ; end style
	write " ALIGN=LEFT><FONT SIZE=3 COLOR="_YWHITE_">",YCR
	
	do ^WWWUP($$$NO)
	
	write "<B>",YCR
	write $$HeaderText(YFORM)
	write "</B>"
	
	;---------------------------------------
	; Session Information (Company/Locn ID/Locn Name/User ID)
	;---------------------------------------
	write " <FONT SIZE=1>"
	if $get(YFORM)=""  {
		write "<A HREF=""#"""
	
	} else {   ; Quick Location Change
		write "<A HREF="""_YAKTION_"EP=WWWFORM&amp;YFORM=WWW0131B&amp;YBACK="_YFORM_","_$$VAR1^WWWCGI(1)_""""
	}
	write " STYLE=""color:"_YWHITE_""""
	write " TITLE="""_$$GetSystemInfoTooltip() ; SR16240:
	write """"
	write ">",YCR
	
	write " ("_YM
	if $get(YLOCATION)'="" {
		write "/"_YLOCATION
		write "/"_$piece($get(^WWW0121(0,0,YLOCATION,1)),Y,1)
	}
	write "/"_$get(YBED)
	write ")"
	write "</A>"
	
	do Calculator()
	
	;---------------------------------------
	; "In Development:"
	;---------------------------------------
	if COLOR="RED" write $$^WWWTEXT(32978)_" "_$piece($get(^WWW120(0,YFORM,1)),Y,112)   ; FIXME : What if user has set colour to RED?
	write "</FONT>"
	write "</TD>",YCR
	
	;---------------------------------------
	; Version No
	;---------------------------------------
	if '(+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
		WRITE "<TD bgcolor="_COLORR_" align=center width=160>"  ;SR17316
		WRITE "<b>"
		WRITE "<font color=white size=1>"
		WRITE "AlphaLinc "_$$GetVersion()
		WRITE " </B>"
		WRITE "</TD>"
	}
	
	; 10 = "Flexible Menu"
	;HEVA-1565 set objWWW013 = $get(^WWW013(0,YBED,1))
	;HEVA-1565 if ($$$WWW013MenuType(objWWW013) = 10) { 
	if ($$MenuType^WWW013() = 10) { ;HEVA-1565
		write "<td style=""background-color:"_COLORR_";text-align:right;width:20px;"">"_$$GetButtonStyle()_$$GetOverviewButton()_"</td>"
		write "<td style=""background-color:"_COLORR_";text-align:right;width:20px"">"_$$GetMenuButton()_"</td>"
	}
	if '(+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
		do WarningIndicator^WWWDASHBOARD(COLORR) ; SR17657, SR17661
	}

	write "<TD bgcolor="_COLORR
	write " style=""border-left:none;"""
	write " align=right width=20>"
	
	do CreateLoginLink()
	write "</TD>",YCR
	write "</TR>",YCR
	quit
	
	
GetVersion()
	;-------------------------------------------------------------------------------
	; Show the Version #
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 02-Sep-2009	PPP		SRAdhoc: If Build with a Odd Minor Release is loaded,
	;							BETA is added to the Release (only if it is not DEV)
	; 01-Sep-2009	PPP		SRAdhoc: Created
	;-------------------------------------------------------------------------------
	new strVer
	
	set strVer = $piece($get(^WWWVERSION(0,"AlphaLinc",1)),Y,1)
	
	if $get(^Development)'=1 {
		set intMinor = $piece(strVer,".",2)
		if intMinor#2 {
			set strVer = strVer _" BETA"
		}
	}
	quit strVer
	
	
Calculator()
	;-------------------------------------------------------------------------------
	; Show the calculator
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 25-Jul-2007	RPW		SRadhoc: Created
	;-------------------------------------------------------------------------------
	new OPT,URL
	
	set URL = YAKTION_"EP=WWWFORM&amp;YFORM=WWWCALC&amp;YUSER="_YUSER_"&amp;YBED="_YBED_"&amp;YUCI="_$get(YUCI)_"&amp;YM="_YM
	set OPT = "HEIGHT=260,WIDTH=220,SCROLLBARS=NO,RESIZEABLE=NO"
	
	write "<A TITLE=Calculator HREF=""#"" onclick="""
	write "javascript:var calculator=window.open('"_URL_"','calculator','"_OPT_"');"">&nbsp;&nbsp;&nbsp;&nbsp;"
	write "</A>"
	
	quit
	
	
V3
	;-------------------------------------------------------------------------------
	;   VERSION WITH POPUP MENU
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
    ; 22-Apr-2010	shobby	SR17253: Cross Browser Support
	; 09-Aug-2007	shobby	BR014237: Call to CreateLoginLink so that pressing the X
	; 							button will go back to the login screen.
	; 25-Jul-2007	RPW		SRadhoc: rewrite in { syntax
	;-------------------------------------------------------------------------------
	new COLOR,COLORR
	
	set COLOR=""
	if $piece(YVOR1,Y,101)'="" set COLOR=$piece($get(^WWW100(0,"FARBE","EN",$piece(YVOR1,Y,101),1)),Y,1)
	if COLOR="" set COLOR="DIMGRAY"
	set COLORR=$piece(YVOR1,Y,147)  ;FARBE RECHTS;FIS;11.12.03 ;tincture on the right 
	if COLORR'="" set COLORR=$piece($get(^WWW100(0,"FARBE","EN",COLORR,1)),Y,1)
	if COLORR=""  set COLORR=COLOR
	if ($get(YLOCATION)'="") && (+$piece($get(^WWW0121(0,0,YLOCATION,1)),Y,75)'=0) {
		set COLOR  = $piece($get(^WWW100(0,"FARBE","EN",$piece($get(^WWW0121(0,0,YLOCATION,1)),Y,75),1)),Y,1)
		set COLORR = COLOR
	}
	
	write YCR,"<TR bgcolor=0>",YCR
	
	write "<TD"
	write " BGCOLOR="_COLOR  ;für netscape & co. ;to 
	;SR17253 IF $PIECE(YVOR1,Y,101)'>256 WRITE " style=""filter:progid:DXImageTransform.Microsoft.Gradient(GradientType=1, StartColorStr='"_COLOR_"', EndColorStr='"_COLORR_"');"""
	if $piece(YVOR1,Y,101)'>256 write " style="""_$$ImageTransformGradient^WWWFORMCrossBrowserSupportVisual(COLOR,COLORR)_";"""
	write " align=left nowrap>"
	write YCR
	write "<font color=white size=3>"
	write "<b>"
	write YCR
	do ^WWWUP($$$NO)
	write YCR
	if '$find(YKOPF,".gif") write "&nbsp;"_$$^WWWUML($translate(YKOPF,"_"," "))
	write YCR
	write " </B>"
	write "</TD>",YCR
	
	write "<TD bgcolor="_COLORR_" align=right width=20>"
	do ^WWWMENU8
	write "</TD>",YCR
	
	write "<TD bgcolor="_COLORR_" align=right width=20>"
	do CreateLoginLink()
	write "</TD>",YCR
	
	write "</TR>",YCR
	
	quit
	
	
GetSystemInfoTooltip()
	;-------------------------------------------------------------------------------
	; Returns a string containing correctly formatted string to be inserted into an
	; <A TITLE="">  construct
	;---------------------------------------
	;   System Information
	;   Company 0 (ALPHALINC)    [deprecated information - reference removed]
	;   Location 1 (Locn1)
	;   Cost Centre CC (CostCentre)
	;   User USER (User Name)
	;   Language EN (English)
	;   IP 000.000.000.000
	;   Form INFormName
	;   Job 1234
	;---------------------------------------
	;
	; ByRefs:
	; 	YM			Company ID
	; 	YLOCATION	Location ID
	; 	YCR			Newline Character
	; 	SPRACHE		Language ID
	; 	YBED		User ID
	; 	YIPADDR		IP Address of current user
	; 	YFORM		FORM ID
	; 
	; History:
	; 28-May-2014	shobby	CORE-385: Loosen ties to INCostCentre
	; 11-Dec-2009	GRF		SR16871: Add Cost Centre; skip deprecated YM; macros
	; 12-Dec-2008	HQN		SR16240: Rewrote to return instead of direct write, 
	; 							reusable in outside routines
	;-------------------------------------------------------------------------------
	new idCostCentre,strTooltip
	
	set strTooltip = $$^WWWTEXT(30038,,1)                                      ; "System Information"
	
	set strTooltip = strTooltip_YCR_$$^WWWTEXT(388,,1)_" "_YLOCATION           ; "Location"
	if YLOCATION'="" {
		set strTooltip = strTooltip_" ("_$extract($$$WWW0121LocationName($get(^WWW0121(0,0,YLOCATION,1))),1,80)_")"
		if ##class(%Library.Routine).Exists("INCostCentre") { ;CORE-385
			set strTooltip=strTooltip_$$GetToolTip^INCostCentre(YLOCATION)
		}
	}
	
	set strTooltip = strTooltip_YCR_$$^WWWTEXT(232,,1)                         ; "Username"
	set strTooltip = strTooltip_" "_YBED
	if YBED'="" set strTooltip = strTooltip_" ("_$extract($$$WWW013Name($get(^WWW013(0,YBED,1))),1,80)_")"
	set strTooltip = strTooltip_YCR
	
	set strTooltip = strTooltip_$$^WWWTEXT(300,,1)                             ; "Language"
	set strTooltip = strTooltip_" "_SPRACHE
	if SPRACHE'="" set strTooltip = strTooltip_" ("_$extract($$$WWW100Text($get(^WWW100(0,"SPRACHE",SPRACHE,SPRACHE,1))),1,80)_")"
	
	set strTooltip = strTooltip_YCR 
	
	set strTooltip = strTooltip_$$^WWWTEXT(33776,,1)                           ; "IP"
	set strTooltip = strTooltip_" "_$get(YIPADDR)_"&nbsp;&nbsp;"
	
	if $get(YFORM)'="" {
		set strTooltip = strTooltip_YCR_$$^WWWTEXT(93,,1)_" "_YFORM _" "_$$GetGridForm()            ; "Form"
	}
	
	set strTooltip = strTooltip_" "_YCR_$$^WWWTEXT(34165,,1)_" "_$job          ; "Job"
	set strTooltip = strTooltip_" "_YCR_$$^WWWTEXT(34362,,1)_" "_$piece($zutil(54,13,$ZU(54,0)),",",1)_" "	;Server Machine
	quit strTooltip
	
GetGridForm()
	;-------------------------------------------------------------------------------
	; Returns the Edit Grid Form if there is one
	; 
	; History:
	; 30-Dec-2013	SCR		SESDF655: Created
	;-------------------------------------------------------------------------------
	new idGridForm,idParentUser
	set idParentUser 	= $$$GetParentUser(YUSER)
	set idGridForm		= ""
	if idParentUser'="" && ($data(^CacheTemp(idParentUser,"Grid","Container"))#10) && ($get(^CacheTemp(idParentUser,"Grid","Container"))=$get(YFORM)) { 
		set idGridForm	= $get(^CacheTemp(idParentUser,"Grid","Name"))
	}
	quit idGridForm

	
GetButtonStyle()
	;-------------------------------------------------------------------------------
	; Returns a string containing styles relevant to GetMenuButton and 
	; GetOverviewButton
	; 
	; History:
	; 16-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML = "<style>"_$char(10)
	set strHTML = strHTML_" A.menuButton { border:0px;height:27px; } A.menuButton IMG { border:0px; }"
	set strHTML = strHTML_" A.Start { width:84px;background-image:url("_YGIF_"StartOff.gif);background-repeat:no-repeat;background-position:center; }"
	set strHTML = strHTML_" A.Start:hover { background-image:url("_YGIF_"StartOn.gif); }"
	set strHTML = strHTML_" A.Menu {width:84px;background-image:url("_YGIF_"MenuOff.gif);background-repeat:no-repeat;background-position:center;}"
	set strHTML = strHTML_" A.Menu:hover {background-image:url("_YGIF_"MenuOn.gif)}"_$char(10)_"</style>"
	quit strHTML 
	
	
GetMenuButton()
	;-------------------------------------------------------------------------------
	; Returns the HTML code for the Menu Button
	;
	; History:
	; 21-May-2010	shobby	SR17316: Firefox doesn't display buttons without display:block
	; 16-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML = "<a style=""display:block"" class=""menuButton Menu"" onclick=""parent.showMenu(1);return false;"""
	set strHTML = strHTML_" href="""_YAKTION_"EP=WWWMENU"_$$WWWCGI2^WWWCGI($$$NO)_""" title=""Menu"" target=""FRAME2"_YUSER_"""></a>"
	quit strHTML
	
	
GetOverviewButton()
	;-------------------------------------------------------------------------------
	; Returns the HTML code for the Overview Button, used for Menu Type (10)
	; Flexible Menu
	; 
	; History:
	; 21-May-2010	shobby	SR17316: Firefox doesn't display buttons without display:block
	; 16-Dec-2008	HQN		SR16240: Created
	;-------------------------------------------------------------------------------
	new strHTML
	
	set strHTML = "<a style=""display:block"" class=""menuButton Start"""
	set strHTML = strHTML_" href="""_YAKTION_"EP=WWWFORM&YFORM=WWWMenuOverview"_$$WWWCGI2^WWWCGI($$$NO)_""" title=""Menu Overview""></a>"
	quit strHTML
	
]]></Routine>
</Export>