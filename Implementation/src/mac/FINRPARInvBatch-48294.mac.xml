<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPARInvBatch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPARInvBatch
	#include COMSYS
	#include FINConst
	#include WWWConst
 
 
OnBeforeRunning(pblnPrintAll,pstrGroups)
    ;-------------------------------------------------------------------------------
    ; Routine to run before trying to execute the invoice batch report
    ;
    ; Returns:
    ;
    ; History:
    ; 10-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus=$$$OK
 
	if (+$get(pblnPrintAll)=0)&&($get(pstrGroups)="") {
		set strStatus=$lb("Fin00936")  ;Must specify a printing option.
	}
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
 
	quit strStatus
	
	
InvoiceBatch(plstParams="")
    /*-------------------------------------------------------------------------------
    ; Prepare for an invoice batch run.
    ;
    ; Params: plstParams:	The parameter list from the batch
    ;
    ; ByRefs:
    ; 
    ; Returns:
    ;
    ; History:
    ; 11-Sep-2006	JW			SR14771: Cleaned up. Added CanPrint, multiple groups
    ; 06-Dec-2005	Steve S		SR12974: Created
    ;-------------------------------------------------------------------------------*/
	new blnPrintAll,strGroups,idInvoice,objFINARInv,idCustomer,objFINARCustomer
	new idGroup,idLine,intLoop,strCustGroups
 	
	set blnPrintAll=+$listget(plstParams,4)
	set strGroups=$listget(plstParams,5)		// Comma separated
	
	set idInvoice=""
	for {
		set idInvoice=$order(^FINARInvs(YM,5," ",idInvoice))
		quit:(idInvoice="")
		
		if $$CanPrint^FINARInvPrint(idInvoice) {	//SR14771
			set objFINARInv=$get(^FINARInv(YM,idInvoice,1))
			set idCustomer=$$$FINARInvCustomer(objFINARInv)
			
			//if (idCustomer'="") {
			set objFINARCustomer=$get(^FINARCustomer(YM,idCustomer,1))
			//if $$$FINARCustomerPrintInvoices(objFINARCustomer) {
			if strGroups="" {
				if blnPrintAll do AddInvoice(idInvoice,$increment(idLine),plstParams)
			} else {
				/*
				set idGroup=$$$FINARCustomerCustomerGroup(objFINARCustomer)
				if $find(","_strGroups_",",","_idGroup_",") {
				*/
				
				if $$MultiFind^COMUtilStr(strGroups,$$$FINARCustomerCustomerGroup(objFINARCustomer),",") {		//SR14771
					do AddInvoice(idInvoice,$increment(idLine),plstParams)
				}
			}
		}
	}
 
	quit
	
	
AddInvoice(pidInvoice,pidLine,plstParams) private
    /*-------------------------------------------------------------------------------
    ; Add an invoice to the batch
    ;
    ; Params: pidInvoice:	The invoice id
    ; 		  pidLine:		The batch line
    ; 		  plstParams:	List of parameters to use
    ;
    ; ByRefs:
    ; 
    ; Returns:
    ;
    ; History:
    ; 06-Dec-2005	Steve S		SR12974: Created
    ;-------------------------------------------------------------------------------*/
	new strDesc,idCustomer
	
	set strDesc=$$$StrFINARInvInvoiceNumber_": "_pidInvoice
	
	set idCustomer=$$$FINARInvCustomer($get(^FINARInv(YM,pidInvoice,1)))
	set strDesc=strDesc_" >>> "_$$$Text("Fin00557")_": "_idCustomer ;Customer
	
	set ^CacheTempBatch($job,pidLine)=strDesc
	
	set ^CacheTempBatch($job,pidLine,1)=pidInvoice
	set ^CacheTempBatch($job,pidLine,2)=$listget(plstParams,3)
	set ^CacheTempBatch($job,pidLine,3)=$listget(plstParams,1)
	set ^CacheTempBatch($job,pidLine,4)=$listget(plstParams,2)
	set ^CacheTempBatch($job,pidLine,5)=$listget(plstParams,6)
 
	quit
	
	;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
	;
	; SR12974:
	; Batch printing has made the routine below obsolete
	; 
	;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
	
	/*
CanPrint(pidCompany,pidInvoice,pblnPrintAll,pstrGroups="") //NOT IN USE
    ;--------------------------------------------------------------------------------
    ; Determines whether an invoice is in the batch print
    ;
    ; Returns:
    ;
    ; History:
    ; 11-Nov-2005	Steve S		SR12482: Created
    ;---------------------------------------------------------------------------------
	new blnPrint,objInvoice,idCust,objCustomer,idGroup
	
	set blnPrint=$$$NO
	
	set pblnPrintAll=+$get(pblnPrintAll)
	if $ascii(pstrGroups)=0 set pstrGroups=""
	
	if pblnPrintAll && (pstrGroups'="") {
		set pblnPrintAll=$$$NO	
	}
	
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="") {
		set objInvoice=$get(^FINARInv(pidCompany,pidInvoice,1))
		if $$$FINARInvPrintedDate(objInvoice)="" {
			set idCust=$$$FINARInvCustomer(objInvoice)
			if (idCust'="") {
				set objCustomer=$get(^FINARCustomer(pidCompany,idCust,1))
				if $$$FINARCustomerPrintInvoices(objCustomer) {
					if (pstrGroups="") {
						if pblnPrintAll set blnPrint=$$$YES	
					} else {
						set idGroup=$$$FINARCustomerCustomerGroup(objCustomer)
						if ($find(","_pstrGroups_",",","_idGroup_",")>0) {
							set blnPrint=$$$YES	
						}
					}
				}
			}
		}	
	}
 
	quit blnPrint
	*/
]]></Routine>
</Export>