<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFMATRIX" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFMATRIX(YARTIKEL,YDATEI,YAUFTRAG,YBESTBETR)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ARTIKEL AUSWAHL TABELLE NACH MATRIX
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 28-Aug-2006	GRF		SR12027: Naked Ref; YCR
	; 31-May-2006	GRF		Doco
	; 03.06.2003	FIS		23477;PREIS NEU AUS INSALESPRICE
	; 02.07.2002	DT		Created
	;-------------------------------------------------------------------------------
	NEW ARTX,ARTY,POSX,POSY,ANZST,YOK
	
	SET YOK=""
	QUIT:$GET(YARTIKEL)="" YOK
	
	SET YAUFTRAG  = $GET(YAUFTRAG)
	SET YBESTBETR = $GET(YBESTBETR)
	
	;SUCHEN WEITEEBENEN ;seek 
	FOR ARTX=YARTIKEL_"-",YARTIKEL_"." FOR  SET ARTX=$ORDER(^INART(YM,ARTX)) QUIT:ARTX=""  QUIT:$EXTRACT(ARTX,1,$LENGTH(YARTIKEL))'=YARTIKEL  DO 
	. SET ARTY=ARTX
	. SET ARTY=$TRANSLATE(ARTY,".","-")
	. SET ANZST=$LENGTH(ARTY,"-")
	. IF ANZST<4 SET YARTIKEL(YARTIKEL)=""                    ;NORMALER FRAME
	. IF ANZST>3 SET YARTIKEL($PIECE(ARTY,"-",1,ANZST-2))=""  ;MULTI FRAME
	
	;AUFRUF TABLES
	IF $ORDER(YARTIKEL(""))="" DO START  ;NUR 2 EBENEN ;only 
	SET YARTIKEL=""
	FOR  SET YARTIKEL=$ORDER(YARTIKEL(YARTIKEL)) QUIT:YARTIKEL=""  DO START  ;MULTI-EBENEN
	QUIT YOK
	
START ;STARTEN FRAME ;launching 
	KILL ^WWWSOR(YUSER,"X")
	KILL ^WWWSOR(YUSER,"Y")
	DO SORT
	QUIT:'$DATA(^WWWSOR(YUSER,"X"))
	DO ANZEIGE
	KILL ^WWWSOR(YUSER,"X")
	KILL ^WWWSOR(YUSER,"Y")
	QUIT
	
SORT ;ZUSAMMENBAU DER ARTIKEL ;the item 
	FOR ARTX=YARTIKEL_"-",YARTIKEL_"." FOR  SET ARTX=$ORDER(^INART(YM,ARTX)) QUIT:ARTX=""  QUIT:$EXTRACT(ARTX,1,$LENGTH(YARTIKEL))'=YARTIKEL  DO 
	. SET ARTY=$PIECE(ARTX,YARTIKEL,2)
	. SET ARTY=$TRANSLATE(ARTY,".","-")
	. IF $EXTRACT(ARTY)'="-" QUIT                      ;KEIN TEIL ;no sub-pieces 
	. SET ARTY=$EXTRACT(ARTY,2,99)
	. QUIT:$LENGTH(ARTY,"-")>2
	. QUIT:$DATA(^WWWSOR(YUSER,"ARTIKEL",ARTX))
	. SET ^WWWSOR(YUSER,"ARTIKEL",ARTX)=""
	. IF $PIECE(ARTY,"-",1)'=""                           SET ^WWWSOR(YUSER,"X",$PIECE(ARTY,"-",1))=ARTX                     ;HEADER
	. IF $PIECE(ARTY,"-",1)'="" IF $PIECE(ARTY,"-",2)'="" SET ^WWWSOR(YUSER,"Y",$PIECE(ARTY,"-",2),$PIECE(ARTY,"-",1))=ARTX  ;DATENFELD ;data item 
	. IF $PIECE(ARTY,"-",1)'="" IF $PIECE(ARTY,"-",2)=""  SET ^WWWSOR(YUSER,"Y"," ",$PIECE(ARTY,"-",1))=ARTX                 ;DATENFELD ;data item 
	
	QUIT
	
ANZEIGE ;DES TABLES
	SET YOK=1                   ;TABELLE ANGEZEIGT ;tabulation 
	WRITE "<BR>"
	IF $DATA(^INART(YM,YARTIKEL,1)) DO
	. WRITE "<B>"
	. WRITE $PIECE(^INART(YM,YARTIKEL,1),Y,1)
	. WRITE "&nbsp;"
	. WRITE $EXTRACT($TRANSLATE($PIECE(^INART(YM,YARTIKEL,1),Y,10),"|"," "),1,60)
	. IF $LENGTH($PIECE(^INART(YM,YARTIKEL,1),Y,10))>60 WRITE "..."
	. WRITE "</B>"
	
	DO ^WWWFRAME(2)   ;TABLE AN  ;upon 
	DO ANZX
	DO ANZY
	DO ^WWWFRAME(1)   ;TABLE AUS ;out of 
	WRITE "<BR>"
	QUIT
	
ANZX ;HEADER
	WRITE "<TR>"
	DO
	. WRITE "<TH VALIGN=TOP NOWRAP VALIGN=TOP ALIGN=MIDDLE"
	. WRITE " BGCOLOR="_YDARKGRAY
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. WRITE YARTIKEL  ;HAUPTARTIKELNUMMER
	. WRITE " "
	. WRITE "</TH>"
	
	SET POSX=""
	FOR  SET POSX=$ORDER(^WWWSOR(YUSER,"X",POSX)) QUIT:POSX=""  DO
	. WRITE "<TH VALIGN=TOP NOWRAP ALIGN=MIDDLE "
	. WRITE " BGCOLOR="_YDARKGRAY
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. WRITE POSX
	. WRITE " "
	. WRITE "</TH>"
	
	WRITE YCR,"</TR>"
	QUIT
	
ANZY ;ANZEIGE DATENFELDER ;Show 
	
	;+++++++++++++++++++++++++++++++++++++++
	;	ART			objWWWSOR
	;	ART1		objINART
	;+++++++++++++++++++++++++++++++++++++++
	
	SET POSY=""
	FOR  SET POSY=$ORDER(^WWWSOR(YUSER,"Y",POSY)) QUIT:POSY=""  DO
	. WRITE YCR
	. WRITE "<TR>"
	. SET YDDSATZ=$GET(YDDSATZ)+1
	. WRITE "<TD NOWRAP VALIGN=TOP ALIGN=MIDDLE"
	. IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. WRITE ">"
	. WRITE "<FONT SIZE=2>"
	. WRITE "<B>"
	. WRITE POSY                              ;ARTIKEL ;item 
	. WRITE "&nbsp;"
	. WRITE "</B>"
	. WRITE "</TD>"
	. ;
	. SET POSX=""
	. FOR  SET POSX=$ORDER(^WWWSOR(YUSER,"X",POSX)) QUIT:POSX=""  DO
	. . NEW ART,ART1
	. . SET ART=$GET(^WWWSOR(YUSER,"Y",POSY,POSX))
	. . IF ART="" DO  QUIT   ;KEINEN ARTIKEL ;item 
	. . . WRITE "<TD"
	. . . IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . . WRITE ">"
	. . . WRITE "&nbsp;"
	. . . write "</TD>"
	. . ;
	. . SET ART1=$GET(^INART(YM,ART,1))
	. . WRITE "<TD NOWRAP VALIGN=MIDDLE"
	. . IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . WRITE ">"
	. . WRITE "<FONT SIZE=2>"
	. . ;
	. . ;MENGENAUSWAHL
	. . WRITE ART
	. . WRITE "&nbsp;"
	. . DO
	. . . NEW YKEY,YBACK,MENGE
	. . . WRITE YCR
	. . . WRITE "<A HREF="""_YAKTION_"EP=WWWMANU1&YFORM=INDPRRUN&YEXEC=D|MANU^INDRPRUN&YKEY="_ART
	. . . IF YBESTBETR'="" WRITE "&YLOCATION="_YBESTBETR
	. . . DO ^WWWCGI
	. . . WRITE """"
	. . . WRITE " TITLE="_""""_"Projeted Available Balance"_""""
	. . . WRITE " TARGET=TEILEFRAME2"
	. . . WRITE ">"
	. . . WRITE YCR
	. . . SET MENGE=$$^INARTMENGE(ART,,YBESTBETR)
	. . . ;IF +MENGE'=0 
	. . . WRITE "("_MENGE_")"
	. . . WRITE "</A>"
	. . . WRITE YCR
	. . ;
	. . WRITE "<BR>"
	. . IF $PIECE(ART1,Y,10)'="" DO
	. . . WRITE "<A HREF=# TITLE="_""""_$TRANSLATE($PIECE(ART1,Y,10),"|",$CHAR(13))_""""_">"
	. . ;
	. . WRITE $EXTRACT($PIECE(ART1,Y,1),1,30)
	. . IF $PIECE(ART1,Y,10)'="" DO
	. . . WRITE "</A>"
	. . ;
	. . WRITE "<BR>"
	. . ;WRITE $$^WWWTEXT(31407)_" "_"<BR>"      ;MENGE
	. . WRITE "<INPUT NAME="_"YMNG"_ART_" SIZE=10 MAXLENGTH=20 TYPE=float VALUE="_""""_""""
	. . WRITE " TABINDEX=ART style="_""""_"padding-top:0 ;padding-bottom:0"_""""_" style="_""""_"text-align:right"_""""
	. . WRITE "  onFocus='select();'"
	. . IF $GET(YHTMFORM)="WWW2" WRITE " onBlur='retval = EventValue("_""""_YUCI_""""_","_""""_YUSER_""""_","_""""_YFORM_""""_","_""""_"FIX"_$GET(YKEY)_""""_","_""""_""""_",this.value,"_""""_"0"_""""_","_""""_"YMNG"_ART_""""_");'"
	. . WRITE ">"
	. . ;
	. . ;ARTIKELGÜLTIGKEIT PRÜFEN ;check 
	. . SET GUELTIG=1
	. . SET GUEDAT=""
	. . IF $GET(YDATEI)="INAUF" IF $GET(YAUFTRAG)'="" SET GUEDAT=$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,19)  ;LIEFERTERMIN ;time of delivery 
	. . IF $GET(YDATEI)="INANG" IF $GET(YAUFTRAG)'="" SET GUEDAT=$PIECE($GET(^INANG(YM,YAUFTRAG,1)),Y,19)  ;LIEFERTERMIN ;time of delivery 
	. . IF GUEDAT="" SET GUEDAT=+$HOROLOG
	. . IF $PIECE(ART1,Y,201)'="" IF $PIECE(ART1,Y,201)>GUEDAT SET GUELTIG=0  ;GÜLTIGKEITSDATUM NOCH NICHT ERREICHT ;yet Not 
	. . IF $PIECE(ART1,Y,202)'="" IF $PIECE(ART1,Y,202)<GUEDAT SET GUELTIG=0  ;GÜLTIGKEITSDATUM ÜBERSCHRITTEN
	. . ;
	. . ;ARTIKELSPERRE
	. . IF GUELTIG=1 DO
	. . . ;IF $PIECE(ART1,Y,66)=1 WRITE "<FONT COLOR=RED>"     ;ARTIKEL ;item 
	. . . ;IF $PIECE(ART1,Y,66)=2 WRITE "<FONT COLOR=ORANGE>"  ;ACHTUNG ;estimation 
	. . . ;IF $PIECE(ART1,Y,66)=3 WRITE "<FONT COLOR=GREEN>"   ;...
	. . ;
	. . IF GUELTIG=0 WRITE "<FONT COLOR=RED>"
	. . WRITE "&nbsp;"
	. . ;MENGENEINHEIT
	. . IF +$PIECE(ART1,Y,46)=0 SET $PIECE(ART1,Y,46)=+$PIECE(ART1,Y,40)  ;VERKAUFSEINHEIT=MENGENEINHEIT
	. . IF +$PIECE(ART1,Y,46)=0 SET $PIECE(ART1,Y,46)=1                                                     ;STUECK
	. . IF '$DATA(^WWW101(0,"EINHEIT",SPRACHE,+$PIECE(ART1,Y,46),1)) SET $PIECE(ART1,Y,46)=1                ;STUECK
	. . IF +$PIECE(ART1,Y,46)'=0 WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,+$PIECE(ART1,Y,46),1)),Y,1)  ;EINHEIT ;unit
	. . ;
	. . //IF +$PIECE(ART1,Y,66)'=0 DO  ;AUSLAUF ODER GESPERRTER ARTIKEL ;runout Or item 
	. . . //WRITE "<BR>"
	. . . ;W "&nbsp;"
	. . . //WRITE $PIECE($GET(^WWW101(0,"SPERRE",SPRACHE,$PIECE(ART1,Y,66),1)),Y,1)
	. . ;
	. . IF GUELTIG=0 DO  ;ARTIKELGÜLTIGKEIT PRÜFEN ! ;sift 
	. . . WRITE "<BR>"
	. . . ;W "&nbsp;"
	. . . WRITE $$^WWWTEXT(32541)
	. . ;
	. . ;MAXIMALE BESTELLMENGE PRÜFEN BEI LF-BESTELLUNG ;ordering quantity check next to 
	. . IF $PIECE($GET(YFELD),Y,2)=2 DO
	. . . WRITE YCR
	. . . NEW MAXM
	. . . SET MAXM=0
	. . . QUIT:$PIECE($GET(YFELD),Y,12)=""
	. . . SET MAXM=$PIECE($GET(^INARTK(YM,ART,$PIECE(YFELD,Y,12),1)),Y,70)     ; 28-Aug-2006 simplify & naked ref
	. . . QUIT:+MAXM=0
	. . . ;WRITE "<BR>"
	. . . WRITE "&nbsp;"
	. . . WRITE "<FONT COLOR="_YRED_">"
	. . . WRITE $$^WWWFELDNAME("INARTK","D",70)     ;MAXIMALE BESTELLMENGE ;ordering quantity
	. . . WRITE ":&nbsp;"
	. . . WRITE MAXM
	. . . WRITE "</FONT>"
	. . WRITE "&nbsp;"
	. . ;
	. . ;PREIS ;price
	. . WRITE "<BR>"
	. . IF $PIECE($GET(YFELD),Y,2)'=0 DO              ;PREIS BEI BESTELLUNG = EK ;price next to sales order Planned Cost 
	. . . SET $PIECE(ART1,Y,88)=$$^INARTKOST(ART,1)   ;EINKAUFSPREIS
	. . ;
	. . IF $PIECE($GET(YFELD),Y,2)=0 DO               ;FIS;03.06.03;23477;PREIS NEU AUS INSALESPRICE
	. . . SET $PIECE(ART1,Y,88)=$$^INSALESPRICE(ART,,$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,6),$PIECE(ART1,Y,90),$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,1))   ;EINKAUFSPREIS
	. . ;
	. . WRITE $$^WWWZAHL($PIECE(ART1,Y,88),10,2,YWHR)   ;ERSTER PREIS ;first price 
	. . WRITE "&nbsp;"
	. . WRITE "</TD>"
	. ;
	. WRITE "</TR>",YCR
	
	QUIT
	
]]></Routine>
</Export>