<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtiljavascript" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtiljavascript
	
#include COMSYS
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMUtiljavascript("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Write out an HTML script tag only if it is required.
	;
	; Params:
	; pstrAttributes - 
	; pstrScriptType - 
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 13-Dec-2006	JW&SH	SR15311: Switched condition.  '$$$InHyperEvent is incorrect.
	; 20-Jun-2006	RPW		SRBR014013: Check for %session existing
	; 23-May-2006	PO		SR14665: Created
	;-------------------------------------------------------------------------------
StartScript(pstrAttributes="",pstrScriptType="type='text/javascript'")
	new idUser
	
	$$$LogR("StartScript","")
	
	if $$$NotInHyperEvent {
		set idUser = %session.SessionId
		if ($increment(^CacheTempScriptTag(idUser,$job)) = 1) { ; (idUser = "") || 
	$$$LogRx("script:"_pstrAttributes)
			write "<script "_$select(pstrAttributes'="":pstrAttributes_" ",1:"")_pstrScriptType_"><!--"_$$$CRLF
		}
	}
	quit
	
	
	;-------------------------------------------------------------------------------
	; Write out an HTML end of script tag only if it is required.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 13-Dec-2006	JW&SH	SR15311: Switched condition.  '$$$InHyperEvent is incorrect.
	; 20-Jun-2006	RPW		SRBR014013: Check for %session existing
	; 23-May-2006	PO		SR14665: Created
	;-------------------------------------------------------------------------------
EndScript()
	new idUser
	
	$$$LogR("EndScript","")
	
	if $$$NotInHyperEvent {
		set idUser = %session.SessionId
		if ($increment(^CacheTempScriptTag(idUser,$job),-1) = 0) { ; (idUser = "") || 
			write $$$CRLF_"//--></script>"
			kill ^CacheTempScriptTag(idUser,$job)
		}
	}
	quit
	
	
Alert(str)
	;-------------------------------------------------------------------------------
	; Utility that displays an Alert msg on the front end.
	;
	; History:
	; 04-May-2004   Paul K	Commented
	;-------------------------------------------------------------------------------
	set str = ##class(%CSP.Page).QuoteJS(str)
	&js<
		alert("<%=str%>");
	>
	quit
	
]]></Routine>
</Export>