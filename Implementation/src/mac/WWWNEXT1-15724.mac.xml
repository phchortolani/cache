<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWNEXT1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWNEXT1(YDATEI,YFKEY,YI,YZAHL)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		NAECHSTE LAUFENDE NUMMER
	;		Next Serial Number
	;		
	;	e.g. set NUM=$$^WWWNEXT1("INKUNDEP",KUNDE,2,1)
	;
	; Inputs : 
	;	YDATEI	= DATEI
	;	YFKEY	= KEY 
	;	YI		= LFD KEY
	;	YZAHL	= ZAHL UM DIE HOCHGEZAEHLT WIRD ;Incrementing factor - default 1.
	;	 		     some strange rules if has trailing zeros - to work out
	; ByRef :
	;
	;
	; Returns :
	;	Next value
	;
	; History :
	; 08-Mar-2006	GRF		Doco
	; 17-Dec-2003	TYBD	Revise Code
	; 17-Mar-1999	DT		(C) BY DITMAR TYBUSSEK
	;-----------------------------------------------------------------------------
	NEW YNKEY,YSKEY,YII,YMAXKEY,A,B,C,D,LGTH

	QUIT:'$DATA(YFKEY) ""
	QUIT:'$DATA(YDATEI) ""
	QUIT:'$DATA(YI) ""
	QUIT:YFKEY="" ""
	QUIT:YDATEI="" ""
	
	;---------------------------------------
	
	SET YZAHL = +$GET(YZAHL)
	IF YZAHL=0 SET YZAHL = 1
	SET YMAXKEY = $ORDER(^WWW002(0,YDATEI,""),-1)
	QUIT:YMAXKEY="" ""
	
	;---------------------------------------
	;FIXME : This performs *NO* locking at all <GRF>
	
	SET YNKEY="",YNKEY(1)=""
	SET YSKEY=$GET(YKEY)
	FOR YII=1:1:(YI-1) DO  IF ($PIECE(YFKEY,",",YII+1)'="") || ($GET(YKEY(YII+1))'="") SET YNKEY=YNKEY_","
	. IF $PIECE(YFKEY,",",YII)'="" SET YNKEY = YNKEY_""""_$PIECE(YFKEY,",",YII)_"""" QUIT
	. IF $GET(YKEY(YII))'=""       SET YNKEY = YNKEY_""""_$GET(YKEY(YII))_""""       QUIT
	
	QUIT:YNKEY="" ""
	
	;---------------------------------------
	
	SET YNKEY(1) = "^"_YDATEI_"("_$$^WWWYM(YDATEI,1)_YNKEY
	IF $EXTRACT(YNKEY(1),$LENGTH(YNKEY(1)))'="," SET YNKEY(1) = YNKEY(1)_","
	SET YNKEY(1) = YNKEY(1)_""""_$CHAR(255)_""""_")"
	SET A        = $ORDER(@YNKEY(1),-1)
	
	;ALTER CODE ; Old Code
	;SET A=A+YZAHL  ;TYBD;17,12,2003
	;QUIT A         ;TYBD
	
	;NEUER CODE ; New Code
	SET (B,C,D)=""
	FOR YI=1:1 QUIT:$E(A,YI)=""  DO
	. IF C="" IF '$FIND("1234567890",$EXTRACT(A,YI)) SET B = B_$EXTRACT(A,YI) QUIT  ;LETTERS
	. IF D="" IF $EXTRACT(A,YI)=0                    SET C = C_$EXTRACT(A,YI) QUIT  ;ZEROS
	. SET D = D_$EXTRACT(A,YI)  ;NUMBER
	
	SET A    = D
	SET LGTH = $LENGTH(C_A)
	SET A    = A+YZAHL  ;NÃ„CHSTE NUMMER(N) ;next 
	
	;NULLEN
	IF $EXTRACT(YZAHL,$LENGTH(YZAHL))="0" DO
	. QUIT:$EXTRACT(A,$LENGTH(A))=0
	. FOR  SET A=A+1 QUIT:$EXTRACT(A,$LENGTH(A))=0
	
	IF $EXTRACT(YZAHL,$LENGTH(YZAHL)-1,$LENGTH(YZAHL))="00" DO
	. QUIT:$EXTRACT(A,$LENGTH(A)-1,$LENGTH(A))="00"
	. FOR  SET A=A+1 QUIT:$EXTRACT(A,$LENGTH(A)-1,$LENGTH(A))="00"
	
	IF C'="" SET A = $REVERSE($EXTRACT($REVERSE(C_A),1,LGTH))
	SET A = B_A                ;ALPHANUMERIC
	
	QUIT A
]]></Routine>
</Export>