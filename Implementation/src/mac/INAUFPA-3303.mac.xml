<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPA" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPA
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		ALLE ABÂ´S GLEICH
	;		All order confirmations equal
	;		
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 01-Feb-2006	GRF		SR14203: Doco
	; 05.Feb.2001	DT		Created
	;-----------------------------------------------------------------------------*/
	DO KOPIE
	IF $PIECE(YFELD,Y,61)'="" DO  ;NEURECHNEN IMPORT KOSTEN;FIS;09.09.04;25736
	. NEW IMPORT,TYPE
	. SET IMPORT=$PIECE(YFELD,Y,61)
	. NEW YKEY,YFELD
	. SET TYPE=""
	. FOR  SET TYPE=$ORDER(^INSHIPINFOCOSTS(YM,IMPORT,TYPE)) QUIT:TYPE=""  DO
	. . SET YKEY=IMPORT_","_TYPE
	. . SET YFELD=$GET(^INSHIPINFOCOSTS(YM,IMPORT,TYPE,1))
	. . DO ^INSHIPINFOCOSTS
	
	QUIT
	
KOPIE ;
	NEW AUF,POS,SATZ,POSX,LIE,AB,SATZ1,INDEX,GESAMT
	
	QUIT:+$GET(VORG(1))=0       ;KEINE KOPIE ;no copy 
	QUIT:$GET(YKEY)=""
	SET AUF=$PIECE(YKEY,",",1)          QUIT:AUF=""
	SET POS=$PIECE(YKEY,",",2)          QUIT:POS=""
	SET LIE=$PIECE(YKEY,",",3)          QUIT:LIE=""
	SET AB =$PIECE(YKEY,",",4)          QUIT:AB=""
	
	set GESAMT=""
	SET POSX=""
	FOR  SET POSX=$ORDER(^INAUFP(YM,AUF,POSX)) QUIT:POSX=""  DO
	. SET SATZ=$GET(^INAUFP(YM,AUF,POSX,1))
	. QUIT:$PIECE(SATZ,Y,12)'=LIE             ;LIEFERANT NICHT GLEICH ;supplier Not without delay 
	. SET SATZ=$GET(^INAUFPK(YM,AUF,POSX,1))  ;AUS AUFTRAG ;out of order 
	. SET GESAMT=GESAMT+$$^INNETTO(SATZ,1)
	
	SET PRUEF=$TRANSLATE(GESAMT-VORG(1),"-")
	IF PRUEF>1 QUIT  ;MEHR ALS 1 EINHEIT UNTERSCHIED ;more when unity difference 
	
	SET POSX=""
	FOR  SET POSX=$ORDER(^INAUFP(YM,AUF,POSX)) QUIT:POSX=""  IF POSX'=POS DO
	. SET SATZ=$GET(^INAUFP(YM,AUF,POSX,1))
	. QUIT:$PIECE(SATZ,Y,12)'=LIE             ;LIEFERANT NICHT GLEICH ;supplier Not without delay 
	. QUIT:$DATA(^INAUFPA(YM,AUF,POSX,LIE))   ;SCHON AB VORHANDEN ;yet Confirm. on hand 
	. SET SATZ=$GET(^INAUFPK(YM,AUF,POSX,1))  ;AUS AUFTRAG LIEFERANTENINFO ;out of order 
	. quit:SATZ=""
	. SET SATZ1=YFELD
	. for INDEX=2:1:13,15,16,22,26,47,51,52,53 SET $PIECE(SATZ1,Y,INDEX)=$PIECE(SATZ,Y,INDEX)  ;AUS INAUFPK ;out of 
	. SET $PIECE(SATZ1,Y,13)=$$^INNETTO(SATZ1)
	. SET $PIECE(SATZ1,Y,22)=$$^INNETTO(SATZ1,1)
	. SET YOK=$$^WWWSPEI("INAUFPA",AUF_","_POSX_","_LIE_","_AB,SATZ1)  ;SPEICHERN UND SORTKEY ;Save And 
	
	QUIT
]]></Routine>
</Export>