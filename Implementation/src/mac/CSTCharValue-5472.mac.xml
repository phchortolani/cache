<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="CSTCharValue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
CSTCharValue
	;-------------------------------------------------------------------------------
	; Routines associated with form CSTCharValue
	; 
	; 
	; History:
	; 25-Jan-2005	GRF		SR10478 : Created
	;-------------------------------------------------------------------------------
#include CST
 
BeforeButton()
	;-------------------------------------------------------------------------------
	; Build Amended Application Parameter List
	;  - exclude parent and component item characteristics
	; 
	; History
	; 09-May-2005	GRF		SR10478 : Dont't allow RC to be entered
	; 25-Jan-2005	GRF		SR10478 : Created
	;-------------------------------------------------------------------------------
	new key
	
	merge ^COMTempList(YM,YUSER,"CSTCHARVALUES") = ^WWW101(0,"CSTCHARVALUES",SPRACHE)
	set key = ""
	for {
		set key = $order(^COMTempList(YM,YUSER,"CSTCHARVALUES",key))
		quit:key=""
		
		kill:$extract(key,2)="-" ^COMTempList(YM,YUSER,"CSTCHARVALUES",key)
		kill:$extract(key,1,2)="RC" ^COMTempList(YM,YUSER,"CSTCHARVALUES",key)
	}
	quit
	
BeforeSave(pKey="")	
	;-------------------------------------------------------------------------------
	; Warn if Char Value already in use
	; 
	; Returns : Q - should storage occur? - if uniqueness enforced
	; 
	; History
	; 20-Mar-2007	GRF		SR15360: Replace alert popup with std error processing,
	; 05-Jan-2007	GRF		SR15360: replace code with Alert.  Use language text.
	; 05-May-2005	GRF		SR10478: Created
	;-------------------------------------------------------------------------------
	new blnDuplicate,CharType,CharVal,delimDup,KeyType,strDuplicate //,strMsg
	
	set Q = $$$QSave
	
	set KeyType = $piece(pKey,$$$COMMA,1)
	set CharVal = $piece(pKey,$$$COMMA,2)
	quit:KeyType=""
	quit:CharVal=""
	
	set strDuplicate = ""
	set delimDup     = ""
	set blnDuplicate = $$$NO
	set CharType=""
	for {
		set CharType=$order(^CSTCharValues(YM,1,CharVal,CharType))
		quit:CharType=""
		
		if CharType'=KeyType {
			set strDuplicate = strDuplicate_delimDup_CharType
			set blnDuplicate = $$$YES
			set delimDup     = ";"
		}
		
	}
	
	if blnDuplicate {
		
	;	write $$$DBLQUOTE_CharVal_$$$DBLQUOTE_" is already used with "              ; SR15360 05-Jan-2007
	;	write $select(strDuplicate[";":"Types",1:"Type")_" :"_strDuplicate
		
	;	set strMsg = $$$Text($listbuild("CSTERR00213",CharVal,strDuplicate))        ; SR15360 20-Mar-2007
	;	$$$Alert(strMsg)
		
		; "%1 is already used with characteristic type %2"
		do ReturnError^COMUtilError($listbuild("CSTERR00213",CharVal,strDuplicate))
		set Q = $$$QDontSave
	}
	
	quit
	
]]></Routine>
</Export>