<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMForm" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMForm
	;-------------------------------------------------------------------------------
	; getting / setting form fields.
	; To be used for client installs
	;-------------------------------------------------------------------------------
#include COMSYS
#include COMConst
#include WWWConst
	
FormField(field,blSet=$$$NO,value="",blWriteNull=$$$NO,formModule="",pstrValue="")
	;-------------------------------------------------------------------------------
    ; Printing and setting form fields of a specified module. Use it to set defaults.
    ; 
    ; Params:	field		- data field to get/set
    ; 			blSet		- boolean: set fields?
    ; 			value		- value to set to
    ; 			blWriteNull	- boolean: write form id to screen if value is = string?
    ; 			pstrValue	- value to compare against
    ; 			formModule	- module that forms are from (default: all forms)
    ;
    ; Returns:strStatus (a status flag)
    ;
    ; History:
	; 03-Jul-2014	shobby	CORE-385.20: Break link to SAL module
    ; 04-Nov-2004	shobby	Allow the value to check against to be passed in.
    ;  3-Nov-2004	JW		Added do ^WWVAR - as it may be used from terminal session.
    ; 21-Oct-2004	JW		SR10596: Created
    ;-------------------------------------------------------------------------------
	new idForm,modLen,objForm,strStatus
	
	if ($get(Y)="")||($get(YM)="") do ^WWWVAR
	
	set strStatus = $$$OK
	set modLen = $length(formModule)
	set idForm=formModule
	for {
		set idForm=$order(^WWW120(0,idForm))
		quit:idForm=""
		quit:$extract(idForm,1,modLen)'=formModule	; only specified module
		
		set objForm=$get(^WWW120(0,idForm,1))
		
		if blWriteNull && ($piece(objForm,Y,field)=pstrValue) {
			write idForm,!	; write to screen if null
		}
		
		if blSet {	; set?
			set $piece(objForm,Y,field)=value
			;CORE-385.20 set strStatus=$$$SaveGlobal("WWW120",idForm,objForm)
			set strStatus=$$Save^COMUtils("WWW120",idForm,objForm,$$$YES) ;CORE-385.20 
		}
		
		quit:strStatus'=$$$OK
	}
	
	quit strStatus
	
ModProtocolAllOff(lastOff=$$$NO,proOff=$$$NO)
	;-------------------------------------------------------------------------------
	; Turn off the Modifications Protocol (Logging) piece for every form.
	; Also kills globals ^WWWLAST and ^WWWPRO depending on params.
	;
	; Params: booleans - whether 2 globals will be kept alive
	; 
	; Returns: success/fail
	;
	; History:
	; 
	; 21 Oct 2004	JW		Moved from AllOf^SALModProtocol
	; 26 Aug 2004	JW		SR10305: Created
	;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus = $$FormField($$$FldWWW120ModificationsProtocol,$$$YES,"",$$$NO,"")
	
	if lastOff {
		kill ^WWWLAST
		kill ^WWWLASTs
	}
	if proOff {
		kill ^WWWPRO
		kill ^WWWPROs
	}
	
	quit strStatus
	
ModProtocolMastersOn(pstrMasterList="INKUNDE,INLIEF,INART")
	;-------------------------------------------------------------------------------
	; Turn on the Modifications Protocol (Logging) piece for the masterfile forms.
	;
	; Params: List of forms to have piece set. Default: suppliers/customers/items
	; 
	; Returns: success/fail
	;
	; History:
	; 03-Jul-2014	shobby	CORE-385.20: Break link to SAL module
	; 21 Oct 2004	JW		Moved from MastersOn^SALModProtocol
	; 26 Aug 2004	JW		SR10305: Created
	;-------------------------------------------------------------------------------
	new idForm,loop,objForm,strStatus
	
	set strStatus=$$$OK
	for loop=1:1:$length(pstrMasterList,",") {
		set idForm=$piece(pstrMasterList,",",loop)
		set objForm=$get(^WWW120(0,idForm,1))
		set $$$WWW120ModificationsProtocol(objForm)=$$$YES
		;CORE-385.20 set strStatus=$$$SaveGlobal("WWW120",idForm,objForm)
		set strStatus=$$Save^COMUtils("WWW120",idForm,objForm) ;CORE-385.20 
		
		quit:strStatus'=$$$OK
	}
	
	quit strStatus
	
	
InheritCompanyLayout(pModule="",blSet=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Check the Inherit Company Layout Field on all forms of a specified module.
    ; If it is not set, write the form name to the screen. Can also set it.
    ;
    ; Params:	pModule	- module to check
    ; 			blSet	- whether to set the field to yes
    ;
    ; Returns: status
    ;
    ; History:
    ; 20-Jun-2005	GRF		SR11908 : use $$$OK; add null default argument
    ; 31-Mar-2005	Paul K	Ensure return value. (SR#11908)
    ; 21-Oct-2004	JW		Created
    ;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$OK
	if pModule'="" {
		set strStatus = $$FormField($$$FldWWW120InheritCompanyProperties,blSet,blSet,$$$YES,pModule)
	}
	quit strStatus
	
	
FastSave(pModule="",blSet=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Check the 'Fast Save' Field on all forms of a specified module.
    ; If it is not set, write the form name to the screen. Can also set it.
    ;
    ; Params:	pModule	- module to check
    ; 			blSet	- whether to set the field to yes
    ;
    ; Returns: status
    ;
    ; History:
    ; 20-Jun-2005	GRF		SR11908 : use $$$OK; add null default argument
    ; 31-Mar-2005	Paul K	Ensure return value. (SR#11908)
    ; 04-Nov-2004	shobby	Copied from Joe's routines.
    ; 21-Oct-2004	JW		Created
    ;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$OK
	if pModule'="" {
		set strStatus=$$FormField($$$FldWWW120FastSave,blSet,blSet,$$$YES,pModule,1)
	}
	quit strStatus
	
	
ConstantFieldValidation(pModule="",blSet=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Check the 'Constant Field Validating' Field on all forms of a specified module.
    ; If it is not set, write the form name to the screen. Can also set it.
    ;
    ; Params:	pModule	- module to check
    ; 			blSet	- whether to set the field to yes
    ;
    ; Returns: status
    ;
    ; History:
    ; 20-Jun-2005	GRF		SR11908 : use $$$OK; add null default argument
    ; 31-Mar-2005	Paul K	Ensure return value. (SR#11908)
    ; 04-Nov-2004	shobby	Copied from Joe's routines.
    ; 21-Oct-2004	JW		Created
    ;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$OK
	if pModule'="" {
		set strStatus=$$FormField($$$FldWWW120ConstantFieldValidating,blSet,blSet,$$$YES,pModule,"")
	}
	quit strStatus
]]></Routine>
</Export>