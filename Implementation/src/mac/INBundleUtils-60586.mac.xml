<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INBundleUtils" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INBundleUtils
	#include INConst

Format(pidKey,pstrValue,pintCol="",&pintExpiredCol="")
	;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 15-Apr-2013	shobby		HEVA-154: Include Owner.
	; 06-Nov-2012	SCR			SR18159: Change to INBrand
	; 02-Oct-2012	SCR			SR17993: Copied from 1.74 with Brand and Owner excluded
	; 07-Feb-2012	shobby		SR17922: Owner
	; 18-Jan-2012	shobby		SR17922: Created
	;-------------------------------------------------------------------------------
	new strValue,strText
	
	set strText="" ;HEVA-154
	if pidKey = "Use By Date" {
		set strText=$$^WWWDATE(pstrValue,$get(SPRACHE))
		//SRAdhoc
		if pstrValue<$horolog {
			set pintExpiredCol = pintCol
		}
	} elseif pidKey="Brand" {
		if pstrValue'="" {
			set strText=$$$INBrandDescription($get(^INBrand(YM,pstrValue,1))) ; SR18159
			;set strText=$$$INBrandDescription($get(^INBRAND(YM,pstrValue,1)))
		}
		if strText="" set strText=pstrValue
	} elseif pidKey="Program" {
		if pstrValue'="" {
			set strText=$$$INPROJECTProjectDescription($get(^INPROJECT(YM,pstrValue,1)))
		}
		if strText="" set strText=pstrValue
	} elseif pidKey="Owner" {
		if pstrValue'="" {
			set strText=$$$INOWNERDescription($get(^INOWNER(YM,pstrValue,1)))
		}
		if strText="" set strText=pstrValue
	} else {
		set strText=pstrValue				
	}
	quit strText


BundleAttributes2(pobjBundleStock,pidLoc="") ;HEVA-154.1
	;-------------------------------------------------------------------------------
	; Get Bundle Attributes
	;
	; History:
	; 11-Sep-2013	SCR		CORE-266: Added Lot Control by Location
	;-------------------------------------------------------------------------------
	new idKey,objAttr,strAttrValues,blnOnlyTrackPrograms

	set blnOnlyTrackPrograms=$$OnlyTrackPrograms(pidLoc)
	set strAttrValues=""
	set idKey = "" 
	for { 
		set objAttr = pobjBundleStock.Bundle.Attributes.GetNext(.idKey) 
		quit:(idKey  = "")
		if $$AttributeOK(objAttr.AttributeName,blnOnlyTrackPrograms) { 	
			continue:(objAttr.AttributeName="ReceiptNo")
			set strAttrValues = strAttrValues_$$Format^INBundleUtils(objAttr.AttributeName,objAttr.AttributeValue)_Y   ;HEVA-154
		}
	}
	quit strAttrValues

BundleAttributes(pobjBundleStock,&pintCol,&pintExpiredCol="",pidLoc="",pobjBundle="") ;HEVA-154.1
	;-------------------------------------------------------------------------------
	; Get Bundle Attributes
	;
	; History:
	; 11-Sep-2013	SCR		CORE-266: Added Lot Control by Location
	; 15-Sep-2013	SCR		CORE-93: Optional pobjBundle , overrides pobjBundleStock
	;-------------------------------------------------------------------------------
	new sc,strAttrValues,arrAttrib,idKey,blnOnlyTrackPrograms
	
	set blnOnlyTrackPrograms=$$OnlyTrackPrograms(pidLoc)
	
	set strAttrValues=""
	if $isObject(pobjBundle) {
		set sc = ##class(alSOH.iBundle).GetAttributesForBundle(pobjBundle.%Id(),.arrAttrib)
	} else {
		set sc = ##class(alSOH.iBundle).GetAttributesForBundle(pobjBundleStock.Bundle.%Id(),.arrAttrib)
	}
	set idKey = "" 
	for { 
		set idKey = $order(arrAttrib(idKey)) 
		quit:idKey=""
		if $$AttributeOK(idKey,blnOnlyTrackPrograms) { 	
			set pintCol = $increment(pintCol)
			set strAttrValues = strAttrValues_$$Format^INBundleUtils(idKey,arrAttrib(idKey))_Y   ;SR17922
		}
	}
	quit strAttrValues
	
DynaHeadings(pobjItemPolicy,pidLoc="",&plstHeaders,&plstColTypes)
	;-------------------------------------------------------------------------------
	; Get Dyna Headings for a Item Policy (With No Lot Location control) 
	;
	; History:
	; 11-Sep-2013	SCR		CORE-266: Created
	;-------------------------------------------------------------------------------
	new idAttrKey,objAttr,strAttrName,blnOnlyTrackPrograms,strAttribute
	
	set idAttrKey = ""
	
	set blnOnlyTrackPrograms=$$OnlyTrackPrograms(pidLoc)
	
	for {
		set objAttr = pobjItemPolicy.Attributes.GetNext(.idAttrKey)
		quit:(idAttrKey = "")
		
		set strAttribute = objAttr.Attribute.Name
		
		continue:(objAttr.Attribute.Name="ReceiptNo")
		
		if $$AttributeOK(strAttribute,blnOnlyTrackPrograms) {
			set strAttrName =  ##class(alINV.uiPolicyAttrTrans).GetText(objAttr.Attribute.Name,SPRACHE) //SR15698
			set plstHeaders  = plstHeaders_$listbuild(strAttrName)
			set plstColTypes = plstColTypes_$listbuild($listbuild("left","link"))
		}
	}
	quit
	
OnlyTrackPrograms(pidLoc="")
	;-------------------------------------------------------------------------------
	; Get the 'OnlyTrackPrograms' flag from the Location
	;
	; History:
	; 11-Sep-2013	SCR		CORE-266: Created
	;-------------------------------------------------------------------------------
	new blnOnlyTrackPrograms,objLoc
	
	set blnOnlyTrackPrograms = $$$NO
	if pidLoc'="" {
		set objLoc=$get(^WWW0121(0,0,pidLoc,1))
		if $$$WWW0121OnlyTrackPrograms(objLoc) {
			set blnOnlyTrackPrograms = $$$YES
		}
	}
	quit blnOnlyTrackPrograms

AttributeOK(pstrAttribute,pblnOnlyTrackPrograms)
	;-------------------------------------------------------------------------------
	; Check if the Attribute is ok to display
	;
	; History:
	; 11-Sep-2013	SCR		CORE-266: Created
	;-------------------------------------------------------------------------------
	quit ('pblnOnlyTrackPrograms)||(pblnOnlyTrackPrograms && (pstrAttribute="Program"))

]]></Routine>
</Export>