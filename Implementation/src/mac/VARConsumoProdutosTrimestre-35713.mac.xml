<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARConsumoProdutosTrimestre" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[VARConsumoProdutosTrimestre
#include COMSYS

OnAfterSave
	
	kill ^VARTempConsumoProdutosTrimestreFiltro(YM,YBED)
	
	set $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,1) = $get(VORG(1))
	set $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,2) = $get(VORG(2))
	set $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,3) = $get(VORG(3))
	set $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,4) = $get(VORG(4))
	set $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,5) = $get(VORG(5))
	
	do VARLogAfterSave^VARUtil(YBED,YFORM)
 	do GoToForm^COMUtilForm("VARConsumoProdutosTrimestre",1,,,,,)	
		
	quit
	
OnAfterDataFields

	new objTemp,vMes,vAno,vLocal,vCC,vGrupo

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)

	set vMes 	= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,1)
	set vAno 	= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,2)
	set vLocal  = $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,3)
	set vCC 	= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,4)
	set vGrupo  = $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,5)
	
	do PrintTabela(vMes,vAno,vLocal,vCC,vGrupo)
	
	quit
	
PrintTabela(vMes,vAno,vLocal,vCC,vGrupo)
	
	new meuSQL,produto,mes,ano,local,centrodeCusto,grupo,descCC,descGrupo,unidadeMedida,quantidade,totalGeral,total,descProduto
	
	set anoPassado = ""
	
	if vMes = 2 {
		set vMesRetrasado = 12
		set anoPassado = vAno - 1
		set vMesPassado = vMes - 1
	} elseif vMes = 1 {
		set vMesPassado = 12
		set vMesRetrasado = 11
		set anoPassado = vAno - 1
	}else {
		set vMesPassado 	= vMes - 1
		set vMesRetrasado 	= vMes - 2
	}
	set meuSQL = "SELECT Linha.Produto, Linha.Mes, Linha.Ano, Linha.LocalEstoque, "
	set meuSQL = meuSQL_" Linha.CentrodeCusto, Inart.ItemGroup, "
	set meuSQL = meuSQL_" $$SQLGetCentroCustoDesc^VARSQL(Linha.CentrodeCusto) "
	set meuSQL = meuSQL_" FROM VARCMMLinha as Linha INNER JOIN INART as Inart on Linha.Produto = Inart.ItemNumber "
	set meuSQL = meuSQL_" WHERE (Linha.Mes = '"_vMes_"' OR Linha.Mes = '"_vMesPassado_"' OR Linha.Mes = '"_vMesRetrasado_"')  "
	set meuSQL = meuSQL_" AND (Linha.Ano = '"_vAno_"' "
	if anoPassado '= "" {
		set meuSQL = meuSQL_" OR Linha.Ano = '"_anoPassado_"' )"
	}else {
		set meuSQL = meuSQL_") "
	}
	if vLocal '= "" {
		set meuSQL = meuSQL_" AND Linha.LocalEstoque = '"_vLocal_"' "	
	}
	
	if vCC '= "" {
		set meuSQL = meuSQL_" AND Linha.CentrodeCusto = '"_vCC_"' "	
	}
	
	if vGrupo '= "" {
		set meuSQL = meuSQL_" AND $$SQLGetGrupoPai^VARSQL(Inart.ItemGroup) =  '"_vGrupo_"' "
	}
	
	set meuSQL = meuSQL_" GROUP BY Linha.Produto "
	set meuSQL = meuSQL_" ORDER BY Linha.CentrodeCusto,Inart.ItemGroup,Linha.Produto  "
	
	set rs = ##class(%ResultSet).%New()
	do rs.Prepare(meuSQL)
	do rs.Execute()
	
	set contador = 0
	set grupoAnterior = ""
	set centrodeCustoAnterior = 1
	set totalGeral = 0
	set totalCentroCusto = 0
	set totalGrupo = 0
	set total = 0
	set anoSuporte = ""
	set qtyTotalGrupo = 0
	
	new lstHeader
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Produto","U.A.","Consumo "_$$AnoMesFormato(vAno,,,vMesRetrasado,anoPassado),"Consumo "_$$AnoMesFormato(vAno,,vMesPassado,,anoPassado),"Consumo "_$$AnoMesFormato(vAno,vMes,,,anoPassado),"Valor Total")

	while (rs.Next()) {
		       
		set produto			= rs.GetData(1)
		set mes		 		= rs.GetData(2)
		set ano		 		= rs.GetData(3)
		set local 	 		= rs.GetData(4)
		set centrodeCusto   = rs.GetData(5)
		set grupo 			= rs.GetData(6)
		set descCC 			= rs.GetData(7)
		
		set descProduto	 		= $$SQLGetDescricaoProduto^VARSQL(produto)
		set descGrupo   		= $$SQLGetDescGrupoItem^VARSQL(grupo)
		set grupoPai			= $$SQLGetGrupoPai^VARSQL(grupo)
		set descGrupoPai		= $$SQLGetDescGrupoItem^VARSQL($$SQLGetGrupoPai^VARSQL(grupo))
		set unidadeMedida   	= $$SQLGetUnitSigla2^VARSQL(produto)
		
		if (vLocal = "") {
			
			set qtyAtual 			= $$GetCMMYearMonth(produto,"",ano,vMes)	
			
			if (anoPassado '= ""){
				if (vMes = 1){
					set qtyUmMesAtras   	= $$GetCMMYearMonth(produto,"",anoPassado,vMesPassado)
					set qtyDoisMesesAtras	= $$GetCMMYearMonth(produto,"",anoPassado,vMesRetrasado)
				} elseif (vMes = 2){
					set qtyUmMesAtras   	= $$GetCMMYearMonth(produto,"",vAno,vMesPassado)
					set qtyDoisMesesAtras	= $$GetCMMYearMonth(produto,"",anoPassado,vMesRetrasado)
				}
			} else {
				set qtyUmMesAtras   	= $$GetCMMYearMonth(produto,"",vAno,vMesPassado)
				set qtyDoisMesesAtras	= $$GetCMMYearMonth(produto,"",vAno,vMesRetrasado)

			}
			
		} else {
			
			set qtyAtual 			= $$GetCMMYearMonth(produto,local,ano,vMes)		
			if (anoPassado '= ""){
				if (vMes = 1){
					set qtyUmMesAtras   	= $$GetCMMYearMonth(produto,local,anoPassado,vMesPassado)
					set qtyDoisMesesAtras	= $$GetCMMYearMonth(produto,local,anoPassado,vMesRetrasado)
				} elseif (vMes = 2){
					set qtyUmMesAtras   	= $$GetCMMYearMonth(produto,local,vAno,vMesPassado)
					set qtyDoisMesesAtras	= $$GetCMMYearMonth(produto,local,anoPassado,vMesRetrasado)
				}
			} else {
				set qtyUmMesAtras   	= $$GetCMMYearMonth(produto,local,vAno,vMesPassado)
				set qtyDoisMesesAtras	= $$GetCMMYearMonth(produto,local,vAno,vMesRetrasado)

			}
		}
			
		set valorAtual = qtyAtual * $$GetCost^INItemCost(produto)
		set valorMesPassado = qtyUmMesAtras * $$GetCost^INItemCost(produto)
		set valorMesRetrasado = qtyDoisMesesAtras * $$GetCost^INItemCost(produto)
		
		set total =  valorAtual + valorMesPassado + valorMesRetrasado
		set totalGeral = totalGeral + total

		if ( (grupoAnterior = grupoPai) &&(centrodeCustoAnterior '= centrodeCusto) || (grupoAnterior '= grupoPai) && (grupoAnterior '= "") ) {
			
			do NewLine^COMTable("lightsteelblue")	
			do InsertCell^COMTable("<strong>Total do Grupo de Estocagem</strong>",,,,"RIGHT",,5)
			do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,totalGrupo,2)_"</strong>",,,,"RIGHT")
			do EndLine^COMTable()
			
			set totalGrupo = 0		
		}
		
		if ( (centrodeCustoAnterior '= centrodeCusto) && (centrodeCustoAnterior '= 1) ) {
		
		do NewLine^COMTable("lightsteelblue")	
		do InsertCell^COMTable("<strong>Total do Centro de Custo</strong>",,,,"RIGHT",,5)
		do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,totalCentroCusto,2)_"</strong>",,,,"RIGHT")
		do EndLine^COMTable()
		
		set totalCentroCusto = 0
		
		do Stop^COMTable()
		
		w "<br />"
		w "<hr />"
		w "<br />"
		 
		w !,"<strong>Centro de Custo: "_centrodeCusto_" - "_descCC_"</strong>"
		do Start^COMTable(lstHeader,"Grupo de Estocagem "_grupoPai_" - "_descGrupoPai,,,)
					
		}
		
		if contador = 0 {
			w "<strong>Centro de Custo: "_centrodeCusto_" - "_descCC_"</strong>"
			do Start^COMTable(lstHeader,"Grupo de Estocagem "_grupoPai_" - "_descGrupoPai,,,)
		}
		do NewLine^COMTable()
		do InsertCell^COMTable(produto_" - "_descProduto,,,,"LEFT",,,,,)
		do InsertCell^COMTable(unidadeMedida,,,,"CENTER")
		do InsertCell^COMTable("R$ "_$$^WWWTR(0,8,valorMesRetrasado,2),,,,"RIGHT")
		do InsertCell^COMTable("R$ "_$$^WWWTR(0,8,valorMesPassado,2),,,,"RIGHT")
		do InsertCell^COMTable("R$ "_$$^WWWTR(0,8,valorAtual,2),,,,"RIGHT")
		do InsertCell^COMTable("R$ "_$$^WWWTR(0,8,total,2),,,,"RIGHT")
		do EndLine^COMTable()
		
		set totalGrupo = totalGrupo + total
		set totalCentroCusto = totalCentroCusto + total	
		
		;Armazena em uma classe Temporária para ser utilizada no relatório PDF
		set anoMesFormatoMesRetrasado = $$AnoMesFormato(vAno,,,vMesRetrasado,anoPassado)
		set anoMesFormatoMesPassado   = $$AnoMesFormato(vAno,,vMesPassado,,anoPassado)
		set anoMesFormatoAtual		  = $$AnoMesFormato(vAno,vMes,,,anoPassado)
		
		set stKey 		= YBED_","_produto
		set objReport   = ""_Y_unidadeMedida_Y_anoMesFormatoMesRetrasado_Y_anoMesFormatoMesPassado_Y_anoMesFormatoAtual_Y_total_Y_vMes_Y_ano_Y_local_Y_centrodeCusto_Y_grupo_Y_valorMesRetrasado_Y_valorMesPassado_Y_valorAtual
		set status 	    = $$$Save("VARTempConsumoProdutosTrimestre",stKey,objReport,1)
		; end armazenamento
		
		set grupoAnterior = grupoPai
		set centrodeCustoAnterior = centrodeCusto
		set contador = $increment(contador)

	}
	if (contador = 0) {
		do Start^COMTable(lstHeader)
		do NewLine^COMTable()
		do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,9)
		do EndLine^COMTable()	
		quit
	}		

	do NewLine^COMTable("lightsteelblue")	
	do InsertCell^COMTable("<strong>Total do Grupo de Estocagem</strong>",,,,"RIGHT",,5)
	do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,totalGrupo,2)_"</strong>",,,,"RIGHT")
	do EndLine^COMTable()
	
	do NewLine^COMTable("lightsteelblue")	
	do InsertCell^COMTable("<strong>Total do Centro de Custo</strong>",,,,"RIGHT",,5)
	do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,totalCentroCusto,2)_"</strong>",,,,"RIGHT")
	do EndLine^COMTable()

	do NewLine^COMTable("lightsteelblue")
	do InsertCell^COMTable("<strong>Total Geral:</strong>",,,,"RIGHT",,5)
	do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,totalGeral,2)_"</strong>",,,,"RIGHT")	
	do EndLine^COMTable()
	
	do Stop^COMTable()	

	do rs.Close()
	
	quit
	
AnoMesFormato(ano,mes="",mesPassado="",mesRetrasado="",anoPassado)

		if (mes '= ""){
			quit mes_"/"_ano
		}elseif (mesPassado '= ""){
			if (mesPassado = 12){
				quit mesPassado_"/"_anoPassado
			}else{
				quit mesPassado_"/"_ano
			}
		}elseif (mesRetrasado '= ""){
			if ((mesRetrasado = 11) || (mesRetrasado = 12)){
				quit mesRetrasado_"/"_anoPassado
			}else{
				quit mesRetrasado_"/"_ano
			}
		}	
		quit
		
Imprimir

	set mes 			= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,1)
	set ano 			= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,2)
	set local			= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,3)
	set centrodeCusto	= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,4)
	set grupo			= $piece(^VARTempConsumoProdutosTrimestreFiltro(YM,YBED,1),Y,5)
	
	do RunReportConsumoProdutosTrimestre^VARJasperRunReportContinuacao(mes,ano,local,centrodeCusto,grupo)


GetCMMYearMonth(pidItem,pidLocal="",currentyear,currentmonth)
	$$$VAR
	if pidItem="" q 0
	set qtyCMMLocation = 0
	if pidLocal'="" {

		if $order(^VARCMMLinha(YM,pidItem,currentyear,currentmonth,pidLocal,""))'="" {
				
			$$$Order6(^VARCMMLinha,YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction)
					
				set qty = 0
				set qty = $piece($get(^VARCMMLinha(YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction,1)),Y,1)
				set qtyCMMLocation = qtyCMMLocation + qty
	
			$$$End
		}
		
	}else {
	
		$$$Order5(^VARCMMLinha,YM,pidItem,currentyear,currentmonth,pidLocal)
				
			$$$Order6(^VARCMMLinha,YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction)
					
				set qty = 0
				set qty = $piece($get(^VARCMMLinha(YM,pidItem,currentyear,currentmonth,pidLocal,idTransaction,1)),Y,1)
				set qtyCMMLocation = qtyCMMLocation + qty
				
	
			$$$End
		$$$End
	}
	q qtyCMMLocation]]></Routine>
</Export>