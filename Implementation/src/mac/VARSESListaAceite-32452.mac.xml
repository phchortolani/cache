<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESListaAceite" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESListaAceite
#include COMSYS
#include VARConst
#include INConst
Run()
	new strStatus,dado,link
	if YKEY="" {
		KILL ^COMTempListaAceite(YM,YUSER)
		set dado=""
		set dado=$order(^INTFR(YM,dado))
		while dado'="" {
			if YLOCATION=$piece($get(^INTFR(YM,dado,1)),Y,4) {
				do CanCreateReceipt^INTRNRec(dado) 
				if YQ=0 {
					SET ^COMTempListaAceite(YM,YUSER,dado,1)=dado_Y_$piece($get(^INTFR(YM,dado,1)),Y,1)_Y_$piece($get(^INTFR(YM,dado,1)),Y,26)_Y_$piece($get(^INTFR(YM,dado,1)),Y,3)_Y_$piece($get(^INTFR(YM,dado,1)),Y,4)_Y_$piece($get(^INTFR(YM,dado,1)),Y,22)				}
			}
			set dado=$order(^INTFR(YM,dado))		
		}
		set link= YAKTION_"EP=WWWSEAR&YFORM=COMTempListaAceite&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YLOCATION="_YLOCATION
		$$$StartScript()
		write "window.location = '"_link_"'"
		$$$EndScript()
	}
	else {
		new idRec
		set strStatus = $$Transaction^COMTransaction("Transaction^VARSESListaAceite("""_$piece(YKEY,YKOMMA,2)_""")",$$$YES)
		set idRec=strStatus
		if $$$ISOK(strStatus) {
			set link= YAKTION_"EP=WWWFORM&YFORM=INRECTFR&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YLOCATION="_YLOCATION_"&YKEY="_idRec
			$$$StartScript()
			write "window.location = '"_link_"'"
			$$$EndScript()
		}
 	}
	quit
	
Transaction(pID)
	// pID is the Transfer Order (INTFR) number.
	new strStatus,idRec,objRecLine,transferLine,objINREC,dado,valor
 	if $$OpenReceipt^INTFRInterfaceRec(pID,.idRec) {	//SR15239
	}
	elseif $$$ISOK($$Transaction^COMTransaction("CreateReceiptTxn^INTFRInterface("""_pID_""")",$$$YES)) {
		set idRec=$get(^CacheTempTransferReceipt(YUSER))
	}
	kill ^CacheTempTransferReceipt(YUSER)
	if idRec="" quit "0~"_$$$Text("MED01000")  ;Não foi possivel criar o aceite ;use text macro;15-Sep-2008
	set objINREC=$get(^INREC(YM,idRec,1))
	set $piece(objINREC,Y,21)=$piece($get(^INTFR(YM,pID,1)),Y,3)
	set $piece(objINREC,Y,22)=$piece($get(^INTFR(YM,pID,1)),Y,26)
	set $piece(objINREC,Y,69)=$piece($get(^INTFR(YM,pID,1)),Y,22) //Campo Grupo, GustavoFiuza@22-Dez-07
	set strStatus = $$$Save("INREC",idRec,objINREC,$$$YES)
	if $$$ISOK(strStatus) {
		set dado=""
		set dado=$order(^INRECLine(YM,idRec,dado))
		while dado'="" {
			// "dado" is the Line Number of the Receipt.
			
			// Andre, 15/10/2007: Correção, pois estava sendo obtida a qtd. 
			// solicitada de uma linha da INTFRLine que tivesse
			// o mesmo número que a linha do recibo - INRECLine. Mas,
			// isto nem sempre é verdade, e é preciso obter o
			// número da linha da INTFRLine relacionada a partir
			// do campo CacheLine da INRECLine.
			
			set objRecLine = $get(^INRECLine(YM,idRec,dado,1))
			set transferLine = $piece($$$INRECLineCacheLink(objRecLine),YKOMMA,2)
			
			// Next line is wrong because Receipt line number is not necessarily equals to the Transfer line number:
			//set valor=$piece($get(^INTFRLine(YM,pID,dado,1)),Y,17) // Retrieves requested qty from Transfer Order.
			
			set valor=$piece($get(^INTFRLine(YM,pID,transferLine,1)),Y,17) // Retrieves requested qty from Transfer Order.
			set $piece(^INRECLine(YM,idRec,dado,1),Y,30)=valor // Sets the requested qty field (Qtd. Solicitada).
			
			// Andre - fim.
			
			set dado=$order(^INRECLine(YM,idRec,dado))
		}
		set strStatus=idRec
		quit strStatus
	} else {
		quit strStatus
	}
]]></Routine>
</Export>