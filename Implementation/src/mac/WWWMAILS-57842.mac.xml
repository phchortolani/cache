<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMAILS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMAILS(HOST,SENDER,RECEIP,SUBJECT,TXT,DISPLAYFROM,DISPLAYTO,USER,PASS)
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SENDEN VON MAILS
	;		SEND E-MAIL
	;
	; HOW TO USE:
	;	S TXT(1)="HELLO"
	;	S TXT(2)="#attach c:\dir\file.txt"
	;	WRITE $$^WWWMAILS("XXX.XXX.XXX.XXX","support@mycompany.com","info@mycompany.com","Testmail",.TXT,"my Company","It's me","myaccount","mypass")
	;
	; Inputs : 
	;	HOST			Host                    e.g. "mail.service.com"         (SMTPDOMAIN)
	;	SENDER			Sender                  e.g. "support@service.com"	(FROM)
	;	RECEIP			Recipient               e.g. "user.name@customer.com"	(TO)
	;	SUBJECT			E-mail Subject Line     e.g. "Server Alert"
	;	TXT				Message String (Single Line - if TEXT doesn't exist)
	;	DISPLAYFROM		Displayed Sender        e.g. "Support Centre"
	;	DISPLAYTO		Displayed Recipient     e.g. "John Smith"
	;	USER			User Name
	;	PASS			Password
	;
	;
	; ByRef :
	;	The presence of array TEXT(n) will over-ride the string TXT.  If .TXT is specified
	;	the calling routine should new TEXT to protect the message.
	;	The lines in the array are copied to ^WWWSOR and passed to $$BIGSEND^WWWSMTP2
	;	as another "ByRef" where they are treated as text or as instructions to attach a file.
	;
	; Returns :
	;
	;
	;	FIXME : inconsistent use of TEXT/TXT <GRF>
	;
	; History :
	; 22-Mar-2007	GRF		SR15046: remove CACHE() arguments again since other arguments
	; 						are still passed implicitly.
	; 20-Mar-2007	GRF		SR15046: Explicitly pass HOST and ^WWWSOR argument to CACHE.
	; 18-Jan-2007	GRF		SR15046: doco; quits
	; 23.11.1999	DT
	;-------------------------------------------------------------------------------
	new charset,CTEncoding,LFN,location,REQUEST
	
	IF '$DATA(TEXT) SET TEXT=$GET(TXT)
	
	; FOR MAILSERVER WITH AUTHENTIFICATION 
	SET DISPLAYFROM = $GET(DISPLAYFROM)            ;BEC;25806;23.07.04  ;INFORMATION NAME
	SET DISPLAYTO   = $GET(DISPLAYTO)              ;BEC;25806;23.07.04  ;INFORMATION NAME
	SET USER        = $GET(USER)                   ;BEC;25806;23.07.04
	SET PASS        = $GET(PASS)                   ;BEC;25806;23.07.04 
	
	; OLD:SERVICE:25806
	; Message must be put in the global ^wldSMTP(LineNo)=Line_Of_Text
	; Attachment list is in the global ^wldAttach(LineNo)=path/filename
	;SET ^wldSMTPx=1
	;LOCK +^wldSMTPx:40
	;KILL ^wldAttach
	;KILL ^wldSMTP
	;MERGE ^wldSMTP=TEXT
	
	SET REQUEST=$GET(YUSER)
	IF REQUEST=""  SET REQUEST=$RANDOM(1000000000)
	SET REQUEST="MAILOUT"_REQUEST
	
	LOCK +^WWWSOR(REQUEST):40			// SR15046 FIXME (4) 
	KILL ^WWWSOR(REQUEST)
	MERGE ^WWWSOR(REQUEST)=TEXT     ;BEC;25806;23.07.04
	
	;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv NOT USED
	SET charset    = "iso-8859-1"
	SET CTEncoding = "7bit"
	SET location   = "base64"
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
	
	;SET LFN(1)=0 ;table-mat 
	;SET LFN=""
	;FOR  SET LFN=$ORDER(TEXT(LFN)) QUIT:LFN=""  DO
	;. IF $FIND($GET(TEXT(LFN)),"#attach") SET LFN(1)=LFN(1)+1 SET ^wldAttach(LFN(1))=$PIECE(TEXT(LFN),"#attach ",2)
	
	DO CACHE                    ; SR15046 22-Mar-2007
	;LOCK -^wldSMTPx
	LOCK -^WWWSOR(REQUEST)      ;BEC;25806;23.07.04
	KILL ^WWWSOR(REQUEST)       ;BEC;25806;23.07.04
	
	QUIT ERROR
	
CACHE ;INTERSYSTEMS
	;-------------------------------------------------------------------------------
	; History:
	; 22-Mar-2007	GRF		SR15046: remove CACHE() arguments again since other arguments
	; 						are still passed implicitly; remove HTML arg to BIGSEND call.
	; 20-Mar-2007	GRF		SR15046: Timezone didn't handle other than full hour GMT
	; 						offsets and didn't calculate -ve times (e.g. US) properly;
	; 						explicitly pass HOST and ^WWWSOR argument and specify use
	; 						HTML format; use WWW012 macros; don't proceed if HOST
	; 						isn't defined (calling routine to determine default if not
	; 						defined in WWW012)
	;-------------------------------------------------------------------------------
	;---------------------------------------
	;	D40		$$$WWW012TCPIPEMailServer(objCompany)
	;	D42		$$$WWW012PortNumberSMTP(objCompany)
	;	D43		$$$WWW012EMailDomainName(objCompany)
	;	D71		$$$WWW012GMTOffsethour(objCompany)
	;---------------------------------------
	new GMT,GMTOFF,objCompany,SMTPPORT
	
	set objCompany = $GET(^WWW012(0,YM,1))
	IF HOST="" SET HOST=$$$WWW012TCPIPEMailServer(objCompany)   ; SR15046
	if HOST="" quit                                             ; SR15046
	SET SMTPPORT =  $$$WWW012PortNumberSMTP(objCompany)
	
	; Timezone
	;---------------------------------------                    ; SR15046
	;IF GMTOFF<0 SET GMT="-"_$EXTRACT(100+GMTOFF,2,3)_"00"
	;IF GMTOFF>0 SET GMT="+"_$EXTRACT(100+GMTOFF,2,3)_"00"
	set GMT = $$Offset(+$$$WWW012GMTOffsethour(objCompany))
	;---------------------------------------
		
	SET DOMAIN = $$$WWW012EMailDomainName(objCompany)                          ; SR15046
	IF $PIECE(SENDER,"@",2)="" SET SENDER=$PIECE(SENDER,"@",1)_"@"_DOMAIN
	IF $PIECE(RECEIP,"@",2)="" SET RECEIP=$PIECE(RECEIP,"@",1)_"@"_DOMAIN
	
	SET ERROR=0
	;SET ERROR=+$$BIGSEND^WWWSMTP2(HOST,SENDER,RECEIP,SUBJECT,GMT,SMTPPORT,DISPLAYFROM,DISPLAYTO,USER,PASS)   ;BEC;25806;23.07.04  ; SR15046
	SET ERROR = +$$BIGSEND^WWWSMTP2(HOST,SENDER,RECEIP,SUBJECT,GMT,SMTPPORT,DISPLAYFROM,DISPLAYTO,USER,PASS,REQUEST)
	
	IF ERROR'=0 SET ERROR=0 QUIT  ;OK
	SET ERROR=1  ;FEHLER ;shortcoming 
	QUIT
	
	;*******************************************************************************
	;   WWWMAILS(HOST,B,C,D,E,F,G,H,I)
	;PackageMail(pidMessage,D,C,G,B,F,H,I)
	
	;-------------------------------------------------------------------------------
	; Revised Usage :
	;    set idMessage = $$Allocate^WWWMAILS()
	;    for intLineNo=1:1 {                    ; repeat as necessary
	;    	set ^WWWSOR(idMessage,intLineNo) = strText
	;    }
	;    set strStatus = $$PackageMail^WWWMAILS(idMessage,...)
	;    do Deallocate^WWWMAILS(idMessage)
	;-------------------------------------------------------------------------------
 
	
PackageMail(pidMessage,pstrSubject="",pstrRecipient,pstrDisplayTo="",pstrSender="",pstrDisplayFrom="",pidUser="",pstrPassword="")
	;-------------------------------------------------------------------------------
	; History:
	;  21-Mar-2007	GRF		SR15482: created from main and CACHE subroutines as new
	;  						entry point.
	;-------------------------------------------------------------------------------
	new objCompany,strDomain,strHost,strOffset,strStatus,strSMTPPort
	
	set strStatus  = $$$OK
	set objCompany = $get(^WWW012(0,YM,1))
	
	if $get(pidMessage)=""                               set strStatus='$$$OK     ; TODO : error messages
	if $$$ISOK(strStatus) && '$data(^WWWSOR(pidMessage)) set strStatus='$$$OK
	if $$$ISOK(strStatus) && ($get(pstrRecipient)="")    set strStatus='$$$OK
	if $$$ISOK(strStatus) && (pstrSender="") {
		set pstrSender = $$$WWW012AutoEmailUsername(objCompany)
	}
	if $$$ISOK(strStatus) {
		set strSMTPPort = $$$WWW012PortNumberSMTP(objCompany)
		set strHost     = $$$WWW012TCPIPEMailServer(objCompany)
		if strHost="" set strStatus=$$$NO
	}
	if $$$ISOK(strStatus) {
		set strOffset = $$Offset(+$$$WWW012GMTOffsethour(objCompany))
		set strDomain = $$$WWW012EMailDomainName(objCompany)
		if $piece(pstrSender,"@",2)=""    set pstrSender    = $piece(pstrSender,"@",1)_"@"_strDomain
		if $piece(pstrRecipient,"@",2)="" set pstrRecipient = $piece(pstrRecipient,"@",1)_"@"_strDomain
		
		set strStatus = $$SMTPSend^WWWSMTP2(pidMessage,  strHost,         pstrSender,    pstrRecipient, pstrSubject,  strOffset,
		                                    strSMTPPort, pstrDisplayFrom, pstrDisplayTo, pidUser,       pstrPassword,  $$$YES)
	}
	quit strStatus
	
Allocate()
	;-------------------------------------------------------------------------------
	; Single access point to allocate next e-mail message storage array.
	; 
	; History:
	;  21-Mar-2007	GRF		SR15482: created; find next available message number
	;-------------------------------------------------------------------------------
	new idMessage,intMessage
	
	for intMessage=0:1 {
		set idMessage="MAILOUT"_intMessage
		lock +^WWWSOR(idMessage):0
		quit:$test
	}
	kill ^WWWSOR(idMessage)
	quit idMessage
	
Deallocate(pidMessage)
	;-------------------------------------------------------------------------------
	; Single access point to deallocate e-mail message storage array once message sent
	; 
	; History:
	;  21-Mar-2007	GRF		SR15482: created; release lock
	;-------------------------------------------------------------------------------
	
	if $get(pidMessage)'="" {	
		kill ^WWWSOR(pidMessage)
		lock -^WWWSOR(pidMessage)#"I"
	}
	
	quit
	
Offset(pfltOffset)
	;-------------------------------------------------------------------------------
	; History:
	;  20-Mar-2007	GRF		SR15046: Created. Isolate Timezone calculation.
	;-------------------------------------------------------------------------------
	new strMin,strOffset
	
	set strOffset = "+"
	if pfltOffset<0 {
		set strOffset  = "-"
		set pfltOffset = -pfltOffset
	}
	set strMin     = pfltOffset#1*60
	set pfltOffset = pfltOffset\1
	if strMin=0     set strMin="00"
	if strMin#15'=0 set strMin="00"              ; only allow 0, 15, 30, 45 min
	
	set strOffset=strOffset_$extract(100+pfltOffset,2,3)_strMin
 
	quit strOffset
	
	;*******************************************************************************
	
	
]]></Routine>
</Export>