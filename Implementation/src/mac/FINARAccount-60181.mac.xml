<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARAccount" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARAccount
	; 08-Jul-2003	SCR			Bug Fixed from Code Checking
	#Include FIN
	#Include FINConst

CheckPeriod(pidAccount="",pdteInvoiceDate="")
    ;-------------------------------------------------------------------------------
    ; Check the InvoiceDate (if passed in) to make sure that it is able to be 
    ; posted for this control account.
    ;
    ; Returns:Status
    ;
    ; History:
    ; 26-May-2005	Paul K		Created SR:12472
    ;-------------------------------------------------------------------------------
    new strStatus,objAccount
    
	set strStatus=$$$OK
	if (pidAccount'="")&&(pdteInvoiceDate'="") {
		set objAccount=$get(^FINARAccount(YM,pidAccount,1))
		if $$$FINARAccountPeriodStartDate(objAccount)>pdteInvoiceDate {
			set strStatus=$$$Text($listbuild("Fin00774",pidAccount,$$^WWWDATE($$$FINARAccountPeriodStartDate(objAccount))))  ;Can not create invoices for Control Account %1 with an invoice date less than %2.
		}
	}
	quit strStatus
GetDiscount(pidControlAccount)
	; Return the Discount code for the Control Account
	; 21-May-2003	SCR			Added $get
	new objControlAccount
	set objControlAccount=$get(^FINARAccount(YM,pidControlAccount,1))
	quit $$$FINARAccountDefaultDiscountTerm(objControlAccount)
	
UpdatePeriodEndDate(Key)
	;---------------------------------------------------------------------------
	; Update the Period End Date
	; 
	; History
	; 07-Jul-2003		shobby		Force save to update End Date (TR005539)
	;---------------------------------------------------------------------------
	;
	new Rec,Start,Monthly,Days,End,strStatus
	
	if Key'="" {
		set Rec=$get(^FINARAccount(YM,Key,1))
		if Rec'="" {
			set Start=$$$FINARAccountPeriodStartDate(Rec)
			set Monthly=$$$FINARAccountMonthlyPeriod(Rec)
			set Days=$$$FINARAccountDaysinPeriod(Rec)
			set End=$$UpdatePeriodEndDate^FINSYSPer(Start,Monthly,Days)
			set $piece(YFELD,Y,6)=End
			set $piece(Rec,Y,6)=End
			set strStatus=$$Save^FINSYS("FINARAccount",Key,Rec)
		}
	}

	quit

GetPeriodEndDate(Key)
	new dteEnd,objFINARAccount,Start,Monthly,Days
	;
	set dteEnd=""
	if Key'="" {
		set objFINARAccount=$get(^FINARAccount(YM,Key,1))
		if objFINARAccount'="" {
			set Start=$$$FINARAccountPeriodStartDate(objFINARAccount)
			set Monthly=$$$FINARAccountMonthlyPeriod(objFINARAccount)
			set Days=$$$FINARAccountDaysinPeriod(objFINARAccount)
			set dteEnd=$$UpdatePeriodEndDate^FINSYSPer(Start,Monthly,Days)
		}
	}
	quit dteEnd

PeriodClose(Key)
	;------------------------------------------------------------------------------
	;
	; History
	; 07-Jul-2003		shobby		Close period and start a new one. (TR005539).
	; 
	;------------------------------------------------------------------------------
	;
	new objFINARAccount,strStatus,dteStart
	;
	set strStatus=$$$OK
	if Key'="" {
		set objFINARAccount=$get(^FINARAccount(YM,Key,1))
		if objFINARAccount'="" {
			set dteStart=$$$FINARAccountPeriodEndDate(objFINARAccount)+1
			if dteStart>($h+1) {
				set strStatus=$lb("Fin00188") ;Can't close an account with 'Period End Date' later than tomorrow.
			} else {
				set $$$FINARAccountPeriodStartDate(objFINARAccount)=$$$FINARAccountPeriodEndDate(objFINARAccount)+1
				set strStatus=$$Save^FINSYS("FINARAccount",Key,objFINARAccount)
				set $$$FINARAccountPeriodEndDate(objFINARAccount)=$$GetPeriodEndDate(Key)
				set strStatus=$$Save^FINSYS("FINARAccount",Key,objFINARAccount)
			}
		}
	}
	if strStatus=$$$OK {
		do ^WWWFORM
	} else {
		set Q=1
		do ReturnError^COMUtils(strStatus)
	}
	quit



OnBeforeDelete(pYKEY)
	;--------------------------------------------------------------------------
	; Test whether this thing can be deleted
	; 
	; History
	; 09-Mar-2004	shobby		Use standard Check Links method.
	; 08-Mar-2004	shobby		Creation
	; 
	;--------------------------------------------------------------------------
	;
	quit $$CheckLinks^FINSYS(pYKEY,$lb("FINARCustomer"),"FINARAccount")

]]></Routine>
</Export>