<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INERECH1D13" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INERECH1D13
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		D13		Net Purchase Price
	;
	; ByRef :
	;	YKEY
	;	YFELD		INERECH1 (?)
	;   YINHALT		(Returned)
	;   
	;
	; History :
	; 10-Jan-2007	GRF		SR15357: Boolean Macro; doco
	; 14.07.2004	FIS		26103: ANZEIGEN EINZEL-EK
	;-------------------------------------------------------------------------------
	NEW AUF,POS,PREIS,RECHEE,RECHEP,MENGE1,MENGEG,FACTOR
	
	SET AUF=$PIECE($GET(YKEY),",",1)                      QUIT:AUF=""
	SET POS=$PIECE($GET(YKEY),",",2)                      QUIT:POS=""
	SET YFELD=$GET(YFELD)                                 QUIT:YFELD=""
	SET MENGE=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,5)     QUIT:+MENGE=0
	
	;---------------------------------------
	;	D13		$$$INERECH1NetPurchasePrice()
	;	D72		$$$INERECH1TransferNetPurchasePriceI()
	;	D73		$$$INERECH1UnitPriceTransferedAt()
	;	D80		$$$INERECH1ServiceCostsInvoice()
	;---------------------------------------
	
	SET RECHEP=0
	DO EINZELEK^INERECH1              ; sets RECHEP
	
	; Plus Service Costs Invoice  ;ZZGL. SERVICEKOSTENRECHNUNGEN;FIS;09.09.04;25736
	IF $PIECE(YFELD,Y,81)=$$$YES DO
	. NEW SERVCOST,LIEF,RECH,RECH1
	. SET SERVCOST=0
	. SET LIEF=""
	. FOR  SET LIEF=$ORDER(^INERECH1(YM,AUF,POS,LIEF)) QUIT:LIEF=""  DO
	. . SET RECH=""
	. . FOR  SET RECH=$ORDER(^INERECH1(YM,AUF,POS,LIEF,RECH)) QUIT:RECH=""  DO
	. . . SET RECH1=$GET(^INERECH1(YM,AUF,POS,LIEF,RECH,1))
	. . . IF $PIECE(RECH1,Y,80)'="" SET SERVCOST=SERVCOST+$PIECE(RECH1,Y,13)
	. ;
	. IF SERVCOST'=0 SET RECHEP=$JUSTIFY((RECHEP+(SERVCOST/MENGE)),0,2)
	
	IF ($PIECE(YFELD,Y,72)=$$$YES) || ($PIECE(YFELD,Y,73)="") SET YINHALT=$$^WWWZAHL(RECHEP,0,2)
	QUIT
]]></Routine>
</Export>