<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWLISTKOPF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWLISTKOPF      ;WWWLISTKOPF;FAN-FIS;;17.01.2001
#include WWWConst
	;-------------------------------------------------------------------------------\
	; Wrapper for older calls to routine as tag
	;-------------------------------------------------------------------------------
	do Print()
	
	quit
	
Print(pstrHideList="")
	;-------------------------------------------------------------------------------
	; Display the stocktake criteria at the beginning of various stocktake reports.
	;	
	;	ERSTELLEN VON DECKBLATT FÜR STATISTIKEN
	;	
	;	ABFRAGE DES AKTUELLEN FORMULARS (YFORM) NACH DATENSÄTZEN (AUSWAHLFELDERN),
	;	UM DIE EINGABEN DES BENUTZERS AUF EIN STATISTIK-DECKBLATT ZU DRUCKEN.
	;	FORMATUMWANDLUNG ERFOLGT IN ^WWWTR
	;
	; History:
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 03-Oct-2008	GRF		SR15810: Argument name; Naked References
	; 04-May-2006	JW		SR14508: Added parameter, don't show empty filters, fixed WWWTR
	; 17-Jan-2001	FAN-FIS	Created
	;-------------------------------------------------------------------------------
	
	; FIXME : Naked References
	
	new LFN,YVAR,YI,YINHALT,YNAME,YQ,YFELD,YTYP,YVARIN,relType
	
	quit:$get(YFORM)=""
	quit:'$data(^WWW122(0,YFORM))
	
	do ^WWWFRAME(0)  ;ANFANG ;outset 
	write YCR,"<TR>"
	write YCR,"<TD ALIGN=CENTER VALIGN=TOP BGCOLOR="_YGRAY_">"
	;DRUCKEN ÜBERSCHRIFTEN ;print 
	;WRITE "<CENTER>"
	do START1000^WWWTAB
	do NL^WWWTAB
	do NHWO^WWWTAB
	write "<FONT SIZE=3>"
	write "<B>"
	write "<NOBR>"
	write $get(YKOPF)
	; ÜBERSCHRIFT1: STATISTIK-NAME
	write "</NOBR>"
	write "</B>"
	do EH^WWWTAB
	do EL^WWWTAB
	do STOP^WWWTAB
	write "<BR><BR>"
	
	;DO START00^WWWTAB
	;DO NL^WWWTAB
	;DO NF^WWWTAB
	;DO ZW^WWWTAB
	;WRITE "<FONT SIZE=3>"
	;WRITE "<B>"
	;WRITE $$^WWWTEXT(47)_"<FONT SIZE=1>"_"&nbsp;"_"&nbsp;"_"&nbsp;"_"["_YBED_", "_$$^WWWDATE($HOROLOG)_", "_$$^WWWTIME($HOROLOG)_"]"_"</FONT>"
	; ÜBERSCHRIFT2:           AUSWAHL_    [ BEDIENER,  DATUM,   UHRZEIT]
	;WRITE "</B>"
	;WRITE "<FONT SIZE=3>"
	;WRITE "<BR><BR>"
	;DO STOP^WWWTAB
	;DO FS^WWWTAB($$^WWWDATE($H))
	
	write "<TABLE BORDER=0 WIDTH=60%><TR><TD>"
	do FS^WWWTAB()
	;SCHREIBEN TABELLENANFANG ;write 
	write YCR
	;WRITE "<CENTER>"
	write "<TABLE BORDER=0 WIDTH=100% CELLSPACING=5 BGCOLOR="_YGRAY_">"
	set LFN=""    ;SUCHEN NACH DATENFELDERN DES AKTUELLEN FORMULARS IN ^WWW122
	for  set LFN=$order(^WWW122(0,YFORM,LFN)) quit:LFN=""  do
	. quit:$find(";"_pstrHideList_";",";"_LFN_";")	//SR14508
	. ;
	. set YNAME=""
	. set YFELD=$get(^WWW122(0,YFORM,LFN,1))  ;DATENFELD ;data item 
	. quit:$piece(YFELD,Y,2)=15     ;FAN;07.11.2003;24520; UNSICHTBARFELD ZEIGT NICHT.
	. quit:$piece(YFELD,Y,5)=0      ;FAN;07.11.2003;24520; UNSICHTBARFELD ZEIGT NICHT.
	. ;
	. set YNAME=$piece(YFELD,Y,12)  ;FELDNAME
	. if $data(^WWW1221(0,YFORM,LFN,SPRACHE,1)) set YNAME=$piece(^WWW1221(0,YFORM,LFN,SPRACHE,1),Y,1)  ;within Language ; Naked Ref SR15810
	. set YVAR=$piece(YFELD,Y,21)  ;INHALT IN VARIABLE ( VORG() ) ;purport within 
	. set YTYP=$piece(YFELD,Y,5)  ;FELDTYP
	. set YVARIN=$select(YVAR="":"",1:$translate(@YVAR,";",","))  ;INHALT DER VORG-VARIABLE
	. ;
	. quit:YVARIN=""	//SR14508
	. ;
	. do NL^WWWTAB
	. do ZWR^WWWTAB
	. write "<FONT SIZE=2>"
	. write YNAME_" :"_"&nbsp"  ;SCHREIBEN FELDNAME
	. do ZWL^WWWTAB
	. ;
	. for YI=1:1 set YINHALT=$piece(YVARIN,",",YI) quit:$piece(YVARIN,",",YI,999)=""  do  ;SCHLEIFE FÜR MULTI-FELDER ;to 
	. . set YQ=0
	. . if YINHALT'="" if $length(YINHALT)<100 do   ;UMWANDLUNG PARAMETER-NUMMER IN PARAMETER-NAME AUS FORMULAR ;within out of form 
	. . . if $data(^WWW1251(0,YFORM,LFN,YINHALT,1)) do             ; SR15810 Naked Refs vvv
	. . . . set YINHALT=$piece(^WWW1251(0,YFORM,LFN,YINHALT,1),Y,1)
	. . . . if $piece(^WWW1251(0,YFORM,LFN,YINHALT,1),Y,2)'="" do   ;TYBD;UMSTELLUNG DER ERFASSUNGEN;25,8,2004;26307;
	. . . . . set YTYP=$piece(^WWW1251(0,YFORM,LFN,YINHALT,1),Y,2)
	. . . . . set YINHALT=$$GetLiteral^WWWTR(YTYP,YINHALT)
	. . . . set YQ=1
	. . ;
	. . if YQ=0 do  ;UMWANDLUNG PARAMETER-NUMMER IN PARAMETER-NAME AUS RELATIONSDATEI ;within out of 
	. . . new YDAT,YKE,YFE,YSAT,YSPR,YDATA
	. . . set YDAT=$piece(YFELD,Y,32)   ;RELATIONS-DATEI
	. . . quit:YDAT=""
	. . . set YKE=$piece(YFELD,Y,33)    ;RELATION-PRIMÄRSCHLÜSSEL
	. . . if YKE'="" if $extract(YKE)'="""" if '$find(YKE,",")  quit:'$data(@(YKE))  quit:$get(@(YKE))=""
	. . . ;WENN YKE NICHT "" UND NICHT ANFANGEN " UND NICHT ","ERHALTEN, DANN LAUFT WEITER
	. . . ;WENN INHALT VON YKE KEINE WEITER KNOTENPUNKT GEBT UND WENN ES "" IST, DANN QUIT
	. . . set YFE=+$piece(YFELD,Y,34)   ;RELATIONS DATENFELD NR. ;data item 
	. . . if +YFE=0 set YFE=1           ;WENN KEINE AUSGEWÄHLT DANN IST 1 ZB.HIER TEXT  ;when no select Text 
	. . . set YSPR=$piece($get(^WWW001(0,YDAT,1)),Y,7)  ;SPRECHENDATEI FÜR RELATION ;to 
	. . . if YSPR'="" set YDAT=YSPR,YFE=1  ;WENN SPRACHDATEI GIBT DANN SETZEIN ;when 
	. . . set YSAT="^"_YDAT_"("_$$^WWWYM(YDAT,1)  ;YSAT=^RELATIONS-DATEI(YM         ????
	. . . set YDATA=$get(^WWW001(0,YDAT,1))   ;ÜBER OBJEKT KLASSE GANZ DATENFELD ;via thing groovy wholly data item 
	. . . if YKE'="" if $extract(YKE)'="," set YSAT=YSAT_YKE_","    
	. . . ;WENN RELATION-PRIMÄSCHLÜSSEL NICHT "",DANN YSAT=^RELATIONS-DATEI(YM,PRIMÄSCHLÜSSEL,    ?????
	. . . set YSAT=YSAT_""""_$translate(YINHALT,"""")_""""                            ;????
	. . . if YSPR'="" set YSAT=YSAT_",SPRACHE"      ;YSAT=^RELATIONS-DATEI(YM,PRIMÄSCHLÜSSEL,SPRACHE
	. . . if $piece(YDATA,Y,8)'=4 set YSAT=YSAT_",1)"  
	. . . ;ALTERNATIVE DATENSPEICHERUNG PRÜFUNG      YSAT=^RELATIONS-DATEI(YM,PRIMÄSCHLÜSSEL,SPRACHE,1) 
	. . . if $piece(YDATA,Y,8)=4 set YSAT=YSAT_")"  ;YSAT=^RELATIONS-DATEI(YM,PRIMÄSCHLÜSSEL,SPRACHE,) 
	. . . set YSAT(1)=$piece($$^WWWSETL(YSAT),Y,YFE)   ;YSAT(1)=$P(
	. . . if $extract(YKE)="""" if YSAT(1)'="" set YINHALT=$extract($translate(YSAT(1),"|"," "),1,30) set YQ=1 quit
	. . . ;
	. . . ;IF YSAT(1)'="" SET YINHALT="("_YINHALT_") "_$EXTRACT($TRANSLATE(YSAT(1),"|"," "),1,30) SET YQ=1
	. . . if YSAT(1)'="" do
	. . . . set relType = $$$WWW002InputType(^WWW002(0,YDAT,$length(YKE,","),1))
	. . . . set YINHALT="("_$$GetLiteral^WWWTR(relType,YINHALT)_") "_$extract($translate(YSAT(1),"|"," "),1,30)
	. . . . set YQ=1
	. . ;
	. . if YQ=0 set YINHALT=$$GetLiteral^WWWTR(YTYP,YINHALT)
	. . if YTYP=2 if (YINHALT=1)||(YINHALT=0) set YINHALT=$piece($get(^WWW100(0,"JA/NEIN",SPRACHE,YINHALT,1)),Y,1)
	. . ;
	. . write "<FONT SIZE=2>"
	. . write "&nbsp;"_YINHALT  ;SCHREIBEN FELDINHALT (WENN PARAMETER DANN PARAMETERNAME)
	. . write "<BR>"
	
	do EF^WWWTAB
	do EL^WWWTAB
	do STOP^WWWTAB
	;WRITE "</CENTER>"
	do FSE^WWWTAB
	write YCR,"</TD></TR></TABLE>"
	;WRITE "</CENTER>"
	write YCR,"<BR>"
	write YCR,"</TD></TR>"
	write YCR,"</TABLE>"
	do ^WWWFRAME(1)
	if $get(NOLINE)'=1 do FF^WWWW()  ;KOPF NUR AUF NEUER SEITE DRUCKEN, WENN NOLINE NICHT 1
	quit
]]></Routine>
</Export>