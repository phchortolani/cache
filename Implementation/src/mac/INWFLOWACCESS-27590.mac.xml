<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWFLOWACCESS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWFLOWACCESS(AUF) ;INWFLOWACCESS;FIS;ERMITTELN ZUSTÄNDIGER MITARBEITER LAUT WORKFLOW;27.06.2002
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		ERMITTELN ZUSTÄNDIGER MITARBEITER LAUT WORKFLOW
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	27.06.2002
	;|
	;\------------------------------------------------------------------/
	;
	;STATUS:
	;10=AUFTRÄGE OHNE WH ;without 
	;11=AUFTRÄGE OHNE BESTELLDRUCK ;without 
	;12=AUFTRÄGE OHNE AUFTRAGSBESTÄTIGUNG ;without order confirmation 
	;13=AUFTRÄGE OHNE WE ;without In 
	;14=AUFTRÄGE OHNE AUSLIEFERDATUM ;without 
	;15=AUFTRÄGE OHNE LIEFERSCHEIN ;without packing slip 
	;16=AUFTRÄGE OHNE AUSGANGSRECHNUNG ;without 
	;17=AUFTRÄGE OHNE EINGANGSRECHNUNG ;without 
	;18=ERLEDIGTE AUFTRÄGE
	;19=UNTERKALKULIERTE AUFTRÄGE
	;20=GESPERRTE AUFTRÄGE
	;21=NOCH NICHT GESTARTETE FERTIGUNGEN ;Not 
	;22=NOCH NICHT BEENDETE FERTIGUNGEN ;Not 
	NEW STATUS,BENUTZER,AUF1,POS,POS1,OK,SATZ,MINDAUFSCHLAG,SELBSTKOST,UNTER,KAA,GESAMT,WORKFLOW,YI
	SET OK=0
	SET STATUS=""
	SET AUF=$GET(AUF)
	SET MINDAUFSCHLAG=+$PIECE($GET(^INVORG(YM,YM,1)),Y,11)  ;MINDESAUFSCHLAG DER ERREICHT WERDEN SOLL ;the will 
	SET GESAMT=0  ;GESAMTBETRAG
	SET SELBSTKOST=0  ;EK BETRAG ;Planned Cost Sum 
	SET WORKFLOW=$GET(^INWFLOW(YM,YBED,1))  ;ZUSÄTZLICHE ANZEIGEN ;display 
	IF WORKFLOW="" QUIT OK
	IF AUF'="" DO
	. SET AUF1=$GET(^INAUF(YM,AUF,1))
	. SET POS="" FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . QUIT:POS1=""
	. . SET GESAMT=GESAMT+$PIECE(POS1,Y,123)  ;GESAMTPREIS DER POSITION mit RABATT ohne SKONTO ;the by means of deduction Without 
	. . SET SELBSTKOST=SELBSTKOST+$PIECE(POS1,Y,47)+$PIECE(POS1,Y,71)  ;EKPREIS SELBSTKOSTEN ;total production costs 
	. . IF $PIECE(POS1,Y,30)'="" IF $PIECE(WORKFLOW,Y,1)'="" QUIT:'$FIND(";"_$TRANSLATE($PIECE(WORKFLOW,Y,1),",",";")_";",";"_$TRANSLATE($PIECE(POS1,Y,30),",",";")_";")  ;NICHT DIE RICHTIGE WG
	. . IF $PIECE(AUF1,Y,2)'="" IF $PIECE(WORKFLOW,Y,2)'="" QUIT:'$FIND(";"_$TRANSLATE($PIECE(WORKFLOW,Y,2),",",";")_";",";"_$TRANSLATE($PIECE(AUF1,Y,2),",",";")_";")  ;NICHT DIE RICHTIGE AUFTRAGSART
	. . IF $PIECE(AUF1,Y,6)'="" IF $PIECE(WORKFLOW,Y,3)'="" QUIT:'$FIND(";"_$TRANSLATE($PIECE(WORKFLOW,Y,3),",",";")_";",";"_$TRANSLATE($PIECE(AUF1,Y,6),",",";")_";")  ;NICHT DER RICHTIGE BETRIEB ;TYB;02.07.2002
	. . ;
	. . IF $PIECE(POS1,Y,60)=1 SET STATUS=STATUS_",18," QUIT                     ;ABGESCHLOSSEN
	. . IF $PIECE(POS1,Y,205)=1 SET STATUS=STATUS_",20," QUIT                    ;NOCH NICHT FREIGEGEBEN ;yet Not 
	. . IF $PIECE($GET(^INAUF(YM,AUF,1)),Y,38)=1 SET STATUS=STATUS_",20," QUIT   ;NOCH NICHT FREIGEGEBEN ;yet Not 
	. . IF $PIECE(POS1,Y,7)="" SET STATUS=STATUS_",10," QUIT                     ;KEINE WH FESTGELEGT ;no 
	. . IF $PIECE(AUF1,Y,2)=2!(+$PIECE(POS1,Y,7)=1) DO  QUIT:STATUS'=""    ;BESTELLUNGEN
	. . . IF $PIECE(POS1,Y,83)="" SET STATUS=STATUS_",11," QUIT                   ;BESTELLUNG NOCH NICHT GEDRUCKT ;sales order yet Not 
	. . . IF '$DATA(^INERECH1(YM,AUF,POS)) SET STATUS=STATUS_",17,"               ;KEINE EINGANGSRECHNUNG ERHALTEN ;no 
	. . . IF $PIECE(POS1,Y,12)'="" IF $PIECE($GET(^INLIEF(YM,$PIECE(POS1,Y,12),1)),Y,34)=1 IF '$DATA(^INAUFPA(YM,AUF,POS)) SET STATUS=STATUS_",12," QUIT           ;AB NOCH NICHT ERFASST ;Confirm. yet Not 
	. . . IF '$DATA(^INWEAUF(YM,AUF,POS)) SET STATUS=STATUS_",13," QUIT           ;WE NOCH NICHT ERFASST ;In yet Not 
	. . . QUIT
	. . IF $PIECE(AUF1,Y,2)=1!(+$PIECE(POS1,Y,7)=3) DO  QUIT:STATUS'=""    ;FERTIGUNGEN
	. . . IF $PIECE(POS1,Y,165)="" SET STATUS=STATUS_",21," QUIT                  ;FERTIGUNG NICHT GESTARTET ;Not 
	. . . IF $PIECE(POS1,Y,90)'=1 SET STATUS=STATUS_",22," QUIT                   ;NICHT BEENDETE FERTIGUNGEN ;Not 
	. . . QUIT
	. . QUIT:$PIECE(AUF1,Y,2)=1
	. . IF $PIECE(POS1,Y,76)="" SET STATUS=STATUS_",14," QUIT                    ;AUSLIEFERUNGSDATUM NICHT GEPLANT ;Not 
	. . ;IF $PIECE(POS1,Y,87)="" SET STATUS=STATUS_",NN," QUIT                    ;BEREITSTELLUNGSSCHEIN NICHT GEDRUCKT
	. . IF $PIECE(POS1,Y,92)="" SET STATUS=STATUS_",15," QUIT                    ;KEIN LIEFERSCHEIN GEDRUCKT ;no packing slip 
	. . IF $PIECE(POS1,Y,97)="" SET STATUS=STATUS_",16," QUIT                    ;KEINE AUSGANGSRECHNUNG GEDRUCKT ;no 
	. . QUIT
	. ;
	. IF $PIECE(AUF1,Y,2)=0 DO
	. . SET UNTER=1
	. . IF +MINDAUFSCHLAG=0 SET UNTER=0  ;KEINE AUSWERTUNG ;no 
	. . IF +GESAMT'=0 IF +SELBSTKOST'=0 DO
	. . . SET KAA=$JUSTIFY((GESAMT/SELBSTKOST)-1*100,0,2)
	. . . IF KAA>MINDAUFSCHLAG SET UNTER=0  ;GENUG KALULIERT ;sufficiently 
	. . . QUIT
	. . IF UNTER=1 SET STATUS=STATUS_",19,"                                      ;UNTERKALKULIERTE AUFTRÄGE
	. . QUIT
	. QUIT
	IF STATUS'="" IF WORKFLOW'="" DO
	. FOR YI=1:1  QUIT:$PIECE(STATUS,",",YI,999)=""  IF $PIECE(STATUS,",",YI)'="" IF $PIECE(WORKFLOW,Y,$PIECE(STATUS,",",YI))=1 SET OK=1
	. QUIT
	QUIT OK
]]></Routine>
</Export>