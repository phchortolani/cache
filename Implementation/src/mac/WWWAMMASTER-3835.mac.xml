<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWAMMASTER" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWAMMASTER ;WWWAMMASTER;FIS;YAMAPPLIC MANAGER MASTER CONTROL;18.11.2002
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		YAMAPPLIC MANAGER MASTER CONTROL
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;| 30-May-2005		RobertW		SR12056: Attempt at Performance Increase
	;| FIS	18.11.2002
	;|
	;\------------------------------------------------------------------/
	;
	;
	;WEBLINK: HTTP://IP/..?EP=WWWAMMASTER&YAMAPPLIC=APPLICATION&SPRACHE=LANGUAGE&YM=COMPANY
	;CSP:     HTTP://IP/..?YAMAPPLIC=APPLICATION&SPRACHE=LANGUAGE&YM=COMPANY
	;
	;NEW (%request,%session,%KEY,%CGIEVAR,%ZCS,YHYPER,YHYPEREVENT,YUCI,YM,SPRACHE,YAMUSER,YAMPAGE,YAMPARA,YAMCUSTOMER,YXMLHTTPREQ)
	NEW (%request,%session,%KEY,%CGIEVAR,%ZCS,YHYPER,YHYPEREVENT,YHYPERHEADER,YHYPERFOOTER,YUCI,YM,SPRACHE,YAMUSER,YAMPAGE,YAMPARA,YAMCUSTOMER,YXMLHTTPREQ)
	;
	SET $ZTRAP="^WWWAMERROR"
	SET YAMCANCEL=""
	DO ^WWWAMVAR
	IF YAMCANCEL'="" DO ^WWWAMERRORMSG(YAMCANCEL) QUIT
	;
	;EXECUTE ROUTINE
	IF $PIECE($GET(^WWWAMDEFPAGE(0,YAMPAGE,1)),Y,11)'="" DO  QUIT
	. XECUTE $PIECE($GET(^WWWAMDEFPAGE(0,YAMPAGE,1)),Y,11)
	. QUIT
	;
	;GET AND PROCESS HTML-CODE
	;=========================
	;
	SET YAMHTML=""
	SET YAMVHTML=$PIECE($GET(^WWWAMDEFPAGE(0,YAMPAGE,1)),Y,2)  ;HTML-FORM
	IF YAMVHTML'="" SET YAMHTML=$PIECE($GET(^WWWAMHTML(YM,YAMVHTML,1)),Y,2)  ;GET HTML-CODE
	;
	;READ HTML OUT OF FILE
	IF YAMVHTML="" DO
	. SET YAMFOLD=$PIECE($GET(^WWWAMDEFPAGE(0,YAMPAGE,1)),Y,3)  ;FOLDER
	. IF YAMFOLD="" SET YAMFOLD=$PIECE($GET(^WWWAMDEFSYS(0,YAMAPPLIC,1)),Y,13)  ;DFLT
	. SET YAMFILE=YAMFOLD_$PIECE($GET(^WWWAMDEFPAGE(0,YAMPAGE,1)),Y,4)  ;FILENAME
	. IF YAMFILE'="" DO
	. . NEW LINE,HTML
	. . IF $FIND($ZVERSION,"Linux") SET YAMFILE=$TRANSLATE(YAMFILE,"\","/")  ;LINUX
	. . SET YAMDEV=""
	. . IF '$DATA(YAMFILE) QUIT
	. . OPEN YAMFILE:(/READ):1
	. . USE YAMFILE
	. . FOR LINE=1:1 READ HTML#30000:0 QUIT:$ZEOF=-1  QUIT:'$TEST  DO  QUIT:$FIND(HTML,"</HTML>")  QUIT:$FIND(HTML,"</html>")
	. . . SET HTML(LINE)=HTML
	. . . QUIT
	. . CLOSE YAMFILE
	. . DO OPEN^WWWSTART
	. . SET LINE="" FOR  SET LINE=$ORDER(HTML(LINE)) QUIT:LINE=""  SET YAMHTML=YAMHTML_HTML(LINE)_YCR
	. . QUIT
	. QUIT
	;
	DO  ;PREPARE HTML-CODE
	. NEW YAMHTML1,I,YAMLINE
	. SET YAMHTML1=YAMHTML
	. SET YAMHTML=""
	. FOR I=1:1:1000 SET YAMLINE=$PIECE(YAMHTML1,"|",I) DO  QUIT:$FIND(YAMLINE,"</HTML>")  QUIT:$FIND(YAMLINE,"</html>")
	. . SET YAMHTML=YAMHTML_YAMLINE_$CHAR(13,10)
	. . QUIT
	. QUIT
	;
	SET YAMANZ=0
	FOR YI=1:1:1200 SET YAMLINE=$PIECE(YAMHTML,$CHAR(13,10),YI) DO  QUIT:$FIND(YAMLINE,"</HTML>")  QUIT:$FIND(YAMLINE,"</html>")  ;READ HTML-CODE
	. ;QUIT:$TRANSLATE(YAMLINE," "_$CHAR(13)_$CHAR(10))=""
	. ;
	. ;SEARCH IN HTML-CODE FOR DEFINED TAGS: ;within 
	. ;-------------------------------------
	. ;example: original  = <td><language id="001">12345</language></td>
	. ;         processed = <td>This is the text no. 12345</td>
	. SET YAMTAGN="" FOR  SET YAMTAGN=$ORDER(^WWWAMDEFTAG(0,YAMTAGN)) QUIT:YAMTAGN=""  DO
	. . ;FOR YAMTAG=YAMTAGN,$$^WWWUPER(YAMTAGN),$$^WWWLOW(YAMTAGN) IF $FIND(YAMLINE,"<"_YAMTAG) IF $EXTRACT($PIECE(YAMLINE,"<"_YAMTAG,2))=">"!($EXTRACT($PIECE(YAMLINE,"<"_YAMTAG,2))=" ")!($EXTRACT($PIECE(YAMLINE,"<"_YAMTAG,2),1,2)="/>") DO  QUIT
	. . FOR YAMTAG=YAMTAGN,$zconvert(YAMTAGN,"U"),$zconvert(YAMTAGN,"L") IF $FIND(YAMLINE,"<"_YAMTAG) IF $EXTRACT($PIECE(YAMLINE,"<"_YAMTAG,2))=">"!($EXTRACT($PIECE(YAMLINE,"<"_YAMTAG,2))=" ")!($EXTRACT($PIECE(YAMLINE,"<"_YAMTAG,2),1,2)="/>") DO  QUIT
	. . . SET YAMTAGX=YAMTAG
	. . . SET YAMID=""
	. . . IF $FIND(YAMLINE,"<"_YAMTAG_"/>") SET YAMLINE=$PIECE(YAMLINE,"<"_YAMTAG_"/>",1)_"<"_YAMTAG_">"_"</"_YAMTAG_">"_$PIECE(YAMLINE,"<"_YAMTAG_"/>",2)
	. . . DO
	. . . . NEW ID,AZ
	. . . . FOR ID="id","ID" FOR AZ="'","""" DO
	. . . . . IF $FIND(YAMLINE,"<"_YAMTAG_" "_ID_"="_AZ) DO
	. . . . . . SET YAMID=$PIECE($PIECE(YAMLINE,"<"_YAMTAG_" "_ID_"="_AZ,2),AZ,1)  ;ID OF TAG ;ID TAG 
	. . . . . . SET YAMTAGX=YAMTAG_" "_ID_"="_AZ_YAMID_AZ  ;TAG WITH ID ;TAG ID 
	. . . . . . QUIT
	. . . . . QUIT
	. . . . QUIT
	. . . SET YAMCONTENT=$PIECE($PIECE(YAMLINE,"<"_YAMTAGX_">",2),"</"_YAMTAG_">",1)  ;CONTENT OF TAG
	. . . SET YAMLINE=$PIECE(YAMLINE,"<"_YAMTAGX_">",1)_$$^WWWAMTAG()_$PIECE(YAMLINE,"</"_YAMTAG_">",2)
	. . . QUIT
	. . QUIT
	. ;
	. ;SEARCH IN HTML-CODE FOR INPUT FIELDS: ;within 
	. ;-------------------------------------
	. ;example: original  = <input name='fieldname' size=...>
	. ;         processed = <input name='fieldname' onClick='function();' size=...>
	. FOR YAMINPUT="INPUT","input" DO 
	. . IF $FIND(YAMLINE,"<"_YAMINPUT) DO
	. . . FOR YAMNAME="NAME","name","ID","id" DO
	. . . . IF $FIND(YAMLINE,"<"_YAMINPUT_" "_YAMNAME_"=") DO
	. . . . . SET YAMUPER=$EXTRACT($PIECE(YAMLINE,"<"_YAMINPUT_" "_YAMNAME_"=",2),1)
	. . . . . IF YAMUPER'="'" IF YAMUPER'="""" SET YAMUPER=""
	. . . . . IF YAMUPER'="" SET YAMFIELDN=$PIECE($PIECE(YAMLINE,"<"_YAMINPUT_" "_YAMNAME_"="_YAMUPER,2),YAMUPER,1)
	. . . . . IF YAMUPER="" SET YAMFIELDN=$PIECE($PIECE(YAMLINE,"<"_YAMINPUT_" "_YAMNAME_"=",2)," ",1)
	. . . . . QUIT:YAMFIELDN=""
	. . . . . ;FOR YAMFIELD=YAMFIELDN,$$^WWWUPER(YAMFIELDN),$$^WWWLOW(YAMFIELDN) IF $DATA(^WWWAMDEFINPUT(0,YAMFIELD,1)) DO  QUIT
	. . . . . FOR YAMFIELD=YAMFIELDN,$zconvert(YAMFIELDN,"U"),$zconvert(YAMFIELDN,"L") IF $DATA(^WWWAMDEFINPUT(0,YAMFIELD,1)) DO  QUIT
	. . . . . . SET YAMLINE=$PIECE(YAMLINE,"<"_YAMINPUT_" "_YAMNAME_"="_YAMUPER_YAMFIELDN_YAMUPER,1)_"<"_YAMINPUT_" "_YAMNAME_"="_YAMUPER_YAMFIELDN_YAMUPER_$$^WWWAMINPUT()_$PIECE(YAMLINE,"<"_YAMINPUT_" "_YAMNAME_"="_YAMUPER_YAMFIELDN_YAMUPER,2)
	. . . . . . QUIT
	. . . . . QUIT
	. . . . QUIT
	. . . QUIT
	. . QUIT
	. ;
	. SET YAMANZ=YAMANZ+1
	. SET YAMNLINE(YAMANZ)=YAMLINE
	. QUIT
	;
	;WRITE HTML-CODE
	SET YAMANZ="" FOR  SET YAMANZ=$ORDER(YAMNLINE(YAMANZ)) QUIT:YAMANZ=""  DO
	. SET YAMNLINE1=$GET(YAMNLINE(YAMANZ))
	. FOR I=1:1 SET YAMNLINE2=$PIECE(YAMNLINE1,YCR,I) QUIT:$PIECE(YAMNLINE1,YCR,I,999)=""  DO
	. . WRITE YAMNLINE2
	. . WRITE $CHAR(13)_$CHAR(10)  ;YCR
	. . QUIT
	. QUIT
	;
	QUIT
]]></Routine>
</Export>