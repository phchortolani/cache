<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWEVENT4" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWEVENT4(YLOCK,YREQUEST)
	
#include COMSYS
	
	;--------------------------------------------------------------------------------
	; Description of Function :
	;		25774,EVENT BACKGROUND CHECKUP
	; 
	; Called by: JavaScript EventValue calls - see WWWFORM re ""WWWEVENT4""
	; 
	; Inputs : 
	;	YREQUEST	= REQUEST TYP:
	;			EventRequest = Standard Anfrage (Lockprüfung, Messageprüfung, ...)
	;			DataRequest  = Daten neu laden# ;Data recent 
	;	YLOCK		= INKL. LOCKPRÜFUNG JA/NEIN
	;
	; ByRef :
	;
	;
	; Returns :
	;	YRETVAL = RÜCKGABE:
	;		""			= LOCK NOCH GÜLTIG ODER LOCK NEU GESETZT !
	;		"REFRESH"	= SEITE NEU LADEN (REFRESH)
	;		"RELOAD"	= LOCK NICHT MEHR GÜLTIG -> DATENSATZ VERÄNDERT, LOCK IST ABER WIEDER FREI
	;		"INVALID"	= LOCK NICHT MEHR GÜLTIG -> LOCK NICHT MEHR FREI
	;
	; History :
	; 03-May-2010	GRF		SR15961: use dteToday for efficiency
	; 19-Feb-2009	FIS		SR16065: log session action timestamp
	; 02-May-2007	GRF		SR15508: Naked References; quits
	; 02-Sep-2005	JW		SR12966: WWWUSER is shared
	; 20.06.2005	FIS		CORRECTION MADE
	; 16.07.2004	FIS
	;--------------------------------------------------------------------------------
	NEW dteToday,YRETVAL
	
	SET YRETVAL = ""
	SET YFORM   = $GET(YFORM)
	SET YUSER   = $GET(YUSER)
	
	IF YUSER=""                  QUIT YRETVAL
	IF $GET(YM)=""               QUIT YRETVAL
	IF $GET(YBED)=""             QUIT YRETVAL
	IF '$DATA(^WWWUSER(0,YUSER)) QUIT YRETVAL
	
	set dteToday = +$horolog     ; SR15961
	
	do LogAction^WWWUSER(YUSER)  //SR16065 ;log last action timestamp
	
	IF $GET(YREQUEST)="EventRequest" DO
	. NEW YKEY,YFELD
	. ;
	. ;PRÜFEN LOCK ; check lock
	. IF $GET(YLOCK)=1 IF YFORM'="" IF $DATA(^WWW120(0,YFORM)) DO
	. . NEW YDATEI,%SCHLUESSEL,LOCK
	. . QUIT:$EXTRACT(YFORM,1,6)="WWW127"                            ;NICHT BEI HILFE ;Not next to succour 
	. . SET YKEY = $GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",1))   ;KEY HOLEN ;KEY send for 
	. . QUIT:YKEY=""
	. . IF $FIND(YKEY,"+") QUIT                                      ;NUR WENN NICHT NEUERFASSUNG ;only when Not 
	. . SET YFELD = $GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1))  ;DATENSATZ HOLEN ;data record send for 
	. . QUIT:YFELD=""
	. . ;
	. . SET LOCK = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1))
	. . if $translate(LOCK," ")'="" do
	. . . QUIT:$FIND(LOCK,"+")         ;KEIN NEUER DATENSATZ ;no data record 
	. . . IF '$DATA(@(LOCK)) DO  QUIT  ;NEU SETZEN WENN NICHT MEHR GÜLTIG ;recent typeset when Not more valuable 
	. . . . SET YDATEI=""
	. . . . SET %SCHLUESSEL = $TRANSLATE($PIECE(LOCK,",",3),"""")
	. . . . IF $PIECE($PIECE(%SCHLUESSEL,"/",1),"^",2)'="" IF +$PIECE($GET(^WWW001(0,$PIECE($PIECE(%SCHLUESSEL,"/",1),"^",2),1)),Y,6)=0 QUIT  ;KEINE SATZLOCKSEKUNDEN ;no 
	. . . . IF %SCHLUESSEL'="" SET YDATEI = $PIECE(%SCHLUESSEL,"/",1)_"("_$$^WWWKEYBUILD($TRANSLATE($PIECE(%SCHLUESSEL,"/",2),".",","))_")"
	. . . . IF $FIND(YDATEI,",,") QUIT  ;UNGÜLTIGE REFERENZ (BEI "." IM KEY Z.B. WWW004)
	. . . . IF YDATEI'="" IF $DATA(@YDATEI) IF $TRANSLATE($GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2)),";,.|0123456789 "_Y_"""")'=$TRANSLATE(@YDATEI,";,.|0123456789 "_Y_"""") DO  QUIT  ;DATA RECORD HAS CHANGED
	. . . . . IF $PIECE($GET(^WWW120(0,YFORM,1)),Y,84)'="" SET YRETVAL="INVALID" QUIT  ;FIS;20.06.05;27905;IF DATAFIELD MAKES FORM READONLY (NOT CHECKING THE EXACT FIELDS YET)
	. . . . . SET YRETVAL="RELOAD"  ;RELOAD ?
	. . . . ;
	. . . . SET ^WWW006(0,dteToday,%SCHLUESSEL,1) = YUSER_Y_$PIECE($HOROLOG,",",2)  ;SPEICHERN NEUEN LOCK ;Save 
	. . . . SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1) = "^WWW006(0,"_dteToday_","_""""_%SCHLUESSEL_""""_",1)"
	. . . ;
	. . . SET %SCHLUESSEL = $TRANSLATE($PIECE(LOCK,",",3),"""")
	. . . SET LOCK(1) = @(LOCK) IF $PIECE(LOCK(1),Y,1)'=YUSER  SET YRETVAL = "INVALID" QUIT  ;NICHT MEHR BESITZER DES DATENSATZES ;Not more 
	. . . ;
	. . . IF $PIECE($PIECE(%SCHLUESSEL,"/",1),"^",2)'="" IF +$PIECE($GET(^WWW001(0,$PIECE($PIECE(%SCHLUESSEL,"/",1),"^",2),1)),Y,6)=0 QUIT  ;KEINE SATZLOCKSEKUNDEN ;no 
	. . . ;
	. . . SET YDATEI=""
	. . . IF %SCHLUESSEL'="" SET YDATEI = $PIECE(%SCHLUESSEL,"/",1)_"("_$$^WWWKEYBUILD($TRANSLATE($PIECE(%SCHLUESSEL,"/",2),".",","))_")"
	. . . IF $FIND(YDATEI,",,") QUIT  ;UNGÜLTIGE REFERENZ (BEI "." IM KEY: Z.B. MENÜPUNKTE IN WWW004)
	. . . // AUSGESCHALTET, DA PROBLEM: WWWDATEN,2 UND @YDATEI STIMMEN NICHT ÜBEREIN, DA KLEINE UNTERSCHIEDE (Z.B. ÄNDERUNGSDATUM, ZAHLENFORMATE,...)
	. . . ;IF YDATEI'="" IF $DATA(@YDATEI) IF $TRANSLATE($GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2)),";,.|0 "_"""")'=$TRANSLATE(@YDATEI,";,.|0 "_"""") SET YRETVAL="RELOAD" QUIT  ;DATENSATZ WURDE VERÄNDERT. NEU LADEN ?
	. . . ;IF YDATEI'="" IF $DATA(@YDATEI) IF $TRANSLATE($GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2)),";,.|0123456789 "_Y_"""")'=$TRANSLATE(@YDATEI,";,.|0123456789 "_Y_"""") SET YRETVAL="RELOAD" QUIT  ;DATENSATZ WURDE VERÄNDERT. NEU LADEN ?
	. ;
	. ;PRÜFEN MELDUNGEN ;check 
	. IF YRETVAL="" IF $GET(YBED)'="" IF $DATA(^WWW013Ms(0,1," ",YBED)) DO
	. . NEW TXT,TXT1,YFORM,YVOR,YOK
	. . SET TXT1 = ""
	. . SET TXT  = $ORDER(^WWW013Ms(0,1," ",YBED,""))
	. . IF TXT'="" SET TXT1 = $GET(^WWW013M(0,YBED,TXT,1))
	. . IF TXT'="" IF $TRANSLATE(TXT1,Y)="" DO ^WWWSKILL("WWW013M",YBED_","_TXT) QUIT
	. . IF TXT1'="" DO
	. . . SET YRETVAL = "!"
	. . . IF $PIECE(TXT1,Y,4)'="" SET YRETVAL = YRETVAL_"("_$PIECE(TXT1,Y,4)_":) "
	. . . SET YRETVAL = YRETVAL_$PIECE(TXT1,Y,1)
	. . ;
	. . SET $PIECE(TXT1,Y,2) = dteToday
	. . SET $PIECE(TXT1,Y,3) = $PIECE($HOROLOG,",",2)
	. . SET YOK = $$^WWWSPEI("WWW013M",YBED_","_TXT,TXT1,1)
	
	
	IF $GET(YREQUEST)="DataRequest" DO
	. NEW YDATEI,%SCHLUESSEL,LOCK
	. SET YFELD = ""
	. SET LOCK=$get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1))
	. if $translate(LOCK," ")'="" do
	. . QUIT:$FIND(LOCK,"+")           ;KEIN NEUER DATENSATZ ;no data record 
	. . ;
	. . SET YDATEI=""
	. . SET %SCHLUESSEL = $TRANSLATE($PIECE(LOCK,",",3),"""")
	. . IF %SCHLUESSEL'="" SET YDATEI = $PIECE(%SCHLUESSEL,"/",1)_"("_$$^WWWKEYBUILD($TRANSLATE($PIECE(%SCHLUESSEL,"/",2),".",","))_")"
	. . IF $FIND(YDATEI,",,") QUIT     ;UNGÜLTIGE REFERENZ (BEI "." IM KEY Z.B. WWW004)
	. . IF '$DATA(@(LOCK)) DO          ;NEU SETZEN WENN NICHT MEHR GÜLTIG ;recent typeset when Not more valuable 
	. . . SET ^WWW006(0,dteToday,%SCHLUESSEL,1) = YUSER_Y_$PIECE($HOROLOG,",",2)  ;SPEICHERN NEUEN LOCK ;Save 
	. . . SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1) = "^WWW006(0,"_dteToday_","_""""_%SCHLUESSEL_""""_",1)"
	. . ;
	. . SET LOCK(1) = @(LOCK)
	. . IF $PIECE(LOCK(1),Y,1)'=YUSER QUIT  ;NICHT DER EIGENE LOCK ;Not the 
	. . IF (YDATEI'="") && $DATA(@YDATEI) SET YFELD = @YDATEI
	. ;
	. IF YFELD'="" IF YFELD'=$GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1)) DO
	. . SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2) = $GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1))
	. . SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1) = YFELD
	. . SET YRETVAL = $$^WWWREFRESH(YUSER,YFORM)
	. . SET ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2) = YFELD
	. ;
	. IF (YFELD="") || (YRETVAL="") SET YRETVAL = "!"_$$^WWWTEXT(392,,1)  ; "Another User has changed the data record. Please Refresh This Page. Save is not possible."
	
	
	IF $GET(YREQUEST)="DeleteLock" DO
	. NEW LOCK
	. SET LOCK = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1))
	. if $translate(LOCK," ")'="" do
	. . QUIT:$FIND(LOCK,"+")           ;KEIN NEUER DATENSATZ ;no data record 
	. . KILL ^WWWDATEN(YM,dteToday,YUSER,YFORM,"LOCK",1)
	
	
	IF $GET(YREQUEST)="RefreshCheck" DO
	. NEW YKEY
	. SET YRETVAL = ""
	. SET YKEY    = $GET(^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",1))
	. QUIT:$FIND(YKEY,"+")
	. SET YKEY = $TRANSLATE(YKEY,"][\}{|~ ,;:'()@#$%^&*_=+<>?/"_$CHAR(128)_"""")
	. IF YKEY'="" IF YKEY'=YLOCK SET YRETVAL = YLOCK
	
	QUIT YRETVAL
	
]]></Routine>
</Export>