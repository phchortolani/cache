<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPSupplierUtils" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPSupplierUtils
	#include COMSYS
	#include FINConst
 
GetRemittanceName(pidSupplier,pblnReturnSame=$$$YES)
	;-------------------------------------------------------------------------------
	; Get the name of the remittance supplier.
	;
	; Params:	pidSupplier		: Supplier number
	; 			pblnReturnSame	: Whether to return the name if the remittance
	; 							  is the same supplier.
	;
	; ByRefs:
	;
	; Returns: strName
	;
	; History:
	; 25-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new strName,objFINAPSupplier,idRemittance,objRemittance
 
	set strName = ""
	
	if $get(pidSupplier)'="" {
		set idRemittance=$$GetRemittanceSupplier(pidSupplier)
		if pblnReturnSame || (idRemittance'=pidSupplier) {
			set objRemittance = $get(^FINAPSupplier(YM,idRemittance,1))
			set strName = $$$FINAPSupplierSupplierName(objRemittance)
		}
	}
 
	quit strName
	
	
GetRemittanceSupplier(pidSupplier)
	;-------------------------------------------------------------------------------
	; Get the id of the remittance supplier.
	;
	; Params:	pidSupplier	: Supplier number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 25-Sep-2006	Steve S		SR15012: Created
	;-------------------------------------------------------------------------------
	new idRemittance,objFINAPSupplier
 
	set idRemittance = ""
	
	if $get(pidSupplier)'="" {
		set objFINAPSupplier = $get(^FINAPSupplier(YM,pidSupplier,1))
		set idRemittance = $$$FINAPSupplierRemittancetoDifferentSupp(objFINAPSupplier)
	}
	
	if idRemittance="" {
		set idRemittance = pidSupplier
	}
 	
	quit idRemittance
	
	
GetRemittanceObject(pidSupplier)
	;-------------------------------------------------------------------------------
	; Get the data record of the supplier to remit to.
	;
	; Params:	pidSupplier - FINAPSupplier id (mandatory - non null)
	;
	; ByRefs:
	;
	; Returns:	string - FINAPSupplier object
	;
	; History:
	; 25-Sep-2006	JW		SR15038: Created
	;-------------------------------------------------------------------------------
	new idRemitTo,objRemitTo
	
	set idRemitTo = $$GetRemittanceSupplier(pidSupplier)
	set objRemitTo = $get(^FINAPSupplier(YM,idRemitTo,1))
	
	quit objRemitTo
	
]]></Routine>
</Export>