<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSOHSubTypeMap" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSOHSubTypeMap
	
#include COMSYS
	
OnBeforeUpdateField()
	;-------------------------------------------------------------------------------
	; Called by the 'Update Field' field on INSOHSubTypeMap which relates directly to 
	; the class alSOH.dPeriodSubType
	; 'Free Field 1' will relate to the pair of fields 'QtyFree1' and 'ValueFree1' in the class
	; Sets the List
	;
	; Inputs:None
	;
	; Returns:None
	;
	; History:
	; 11-Nov-2009	PP		SR17026: Created
	;-------------------------------------------------------------------------------
	new idField
	
	kill ^COMTempList(0,YUSER,"MAP")
	
	for idField=1:1:10 {
		set ^COMTempList(0,YUSER,"MAP",idField,1)="Free Field #"_idField
	}

	set ^COMTempList(0,YUSER,"MAP","Consumption",1)="Consumption"
	
	quit

OnBeforeFieldDisplaySubType(pYKEY)
	;-------------------------------------------------------------------------------
	; Called by field 'SubType' on the INSOHSuyTypeMap form
	; The Combobox is updated based on the Movement Type selected
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Nov-2009	PP		SR17023: Created
	;-------------------------------------------------------------------------------
	new idMovement
	new idSubType
	
	set idMovement 	= $$$KEY1(pYKEY)
	set idSubType 	= $$UpdateSubTypes(idMovement,"Y"_YFORM_"P2")
	
	//set %TXT(1)=%TXT(1)_"#"_"Y"_YFORM_"P2~"_idSubType
	
	quit


UpdateSubTypes(pidMovement,YUPDATE)
	;-------------------------------------------------------------------------------
	; The Combobox is updated based on the Movement Type selected
	; Currently only Adjustments have SubTypes (Reason Codes)
	;
	; Inputs:
	; pidMovement - Movement Type
	;
	; Returns: SubType Id - The default SubType
	;
	; History:
	; 11-Nov-2009	PP		SR17026: Created
	;-------------------------------------------------------------------------------
	new idLanguage
	new idRC
	new YI
	new idSubType
	
	kill ^COMTempList1(0,YUSER,"MAP")

	//Adjustment
	if pidMovement = 40 {
		set idLanguage = $get(SPRACHE,"EN")
		set idRC = ""
		for YI(1)=1:1 {
			set idRC = $order(^INPARA(0,"AUSBUCHUNG",idLanguage,idRC))
			quit:idRC=""
	
			//set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_idRC_"~"_idRC_" - "_$piece($get(^INPARA(0,"AUSBUCHUNG",idLanguage,idRC,1)),Y,1)_"~"_YI(1)
			set ^COMTempList1(0,YUSER,"MAP",idRC,1)=$piece($get(^INPARA(0,"AUSBUCHUNG",idLanguage,idRC,1)),Y,1)

		}
		set idSubType = $order(^INPARA(0,"AUSBUCHUNG",idLanguage,""))  
	}

	set ^COMTempList1(0,YUSER,"MAP",999,1)="All"
	if $get(idSubType)="" {
		set idSubType = 999
	}
	
	quit idSubType

OnBlurMovement(pYKEY)
	;-------------------------------------------------------------------------------
	; Called By OnBlur of the MovementType
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 11-Nov-2009	PP		SR17026: Created
	;-------------------------------------------------------------------------------
	new idMovement
	new idSubType
	
	set idMovement 	= $$$KEY1(pYKEY)
	set idSubType 	= $$UpdateSubTypesOnBlur(idMovement,"Y"_YFORM_"P2")
	
	set %TXT(1)=%TXT(1)_"#"_"Y"_YFORM_"P2~"_idSubType
	
	quit

UpdateSubTypesOnBlur(pidMovement,YUPDATE)
	;-------------------------------------------------------------------------------
	; Update the SubType List based on the Movement ID provided
	;
	; Inputs:
	;	pidItem
	;	YUPDATE in the form  "Y"_YFORM_YFIELD_FIELD
	;
	; Returns:
	;
	; History:
	; 11-Nov-2009	PPP		SR17026: Created
	;-------------------------------------------------------------------------------
	new idLanguage,idRC,idSubType,YI
	
	set %TXT(1)=""
	if $get(pidMovement)="" {
		set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~REMOVE"
		set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_0
		quit ""  ; early exit DWR
	}
	
	set YI=0
	set YI(1)=1
	set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~REMOVE"
	set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_YI

	//Adjustment
	if pidMovement = 40 {

		set idLanguage = $get(SPRACHE,"EN")
		
		set idRC = ""
		for YI(1)=1:1 {
			set idRC = $order(^INPARA(0,"AUSBUCHUNG",idLanguage,idRC))
			quit:idRC=""
		
			set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_idRC_"~"_idRC_" - "_$piece($get(^INPARA(0,"AUSBUCHUNG",idLanguage,idRC,1)),Y,1)_"~"_YI(1)
		}
		
		set idSubType = $order(^INPARA(0,"AUSBUCHUNG",idLanguage,""))
	}
	
	set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_999_"~"_999_" - "_"All"_"~"_(YI(1))

	if $get(idSubType)="" {
		set idSubType = 999
	}

	if $get(YFOCUS)'="" set %TXT(1) = %TXT(1)_"#FUNCTION~document."_YHTMFORM_"."_YUPDATE_".focus();"

	quit idSubType


]]></Routine>
</Export>