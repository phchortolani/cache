<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMAT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMAT(YDATEI,YART,YLFN,YINHALT,YRETURN)
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Set format for output tables
	;		SETZEN AUSGABEFORMAT FÜR TABELLEN
	;
	; Inputs : 
	;	YDATEI		Class ID
	;	YART		"P" or "D"
	;	YLFN		Class field no (key or data)
	;	YINHALT		Input Data (converted to output format and returned)
	;	YRETURN		Operation to be performed
	;					=0	Equivalent to 3
	;					1	Only format
	;					2	Relationship Only
	;					3	Format, Relationship and Colour [for WWW101 or INPARA Relations]
	;							
	;					4	Format, Data Export
	;
	; ByRef :
	;
	; Returns :		Formatted Data
	;
	; History :
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 09-Apr-2009	GRF		Doco; quits; remove old commented code
	; 11.02.2001	DT		Created
	;-------------------------------------------------------------------------------
	NEW YINHALTK,YQ,YKE1,YDATA,YFE,YDAT,YKE,YSAT,YSPR,YKE3,YTYP,YII,YFARBCODE
	
	SET YINHALT = $GET(YINHALT)
	IF $GET(YDATEI)="" QUIT YINHALT
	IF $GET(YLFN)=""   QUIT YINHALT
	
	SET YRETURN = $GET(YRETURN)
	
	IF $GET(YART)="P" DO
	. DO KEY
	. IF +YRETURN=0 DO
	. . IF YINHALTK'="" SET YINHALT=YINHALTK_" ("_YINHALT_")"
	. ;
	. IF YRETURN=2 SET YINHALT=YINHALTK
	. IF YRETURN=4 IF YINHALTK'="" SET YINHALT=YINHALTK
	
	IF $GET(YART)="D" DO
	. IF '$FIND(YINHALT,";") DO  QUIT
	. . DO DATEN
	. . IF (+YRETURN=0) || (YRETURN=3) DO
	. . . IF YINHALTK'="" IF YINHALT'="" SET YINHALT="("_YINHALT_") "_YINHALTK  ;IF YINHALT'=YINHALTK
	. . . IF YINHALTK'="" IF YINHALT=""  SET YINHALT=YINHALTK
	. . ;
	. . IF YRETURN=2 SET YINHALT=YINHALTK
	. . IF YRETURN=4 IF YINHALTK'="" SET YINHALT=YINHALTK
	. ;
	. IF $FIND(YINHALT,";") IF $LENGTH(YINHALT,";")<10 DO  QUIT
	. . SET YINHALTX=YINHALT
	. . FOR YII=1:1 SET YINHALT=$PIECE(YINHALTX,";",YII) QUIT:YINHALT=""  DO  SET $PIECE(YINHALTX,";",YII)=YINHALT
	. . . DO DATEN
	. . . IF (+YRETURN=0) || (YRETURN=3) DO
	. . . . IF YINHALTK'="" IF YINHALT'="" SET YINHALT="("_YINHALT_") "_YINHALTK  ;IF YINHALT'=YINHALTK
	. . . . IF YINHALTK'="" IF YINHALT=""  SET YINHALT=YINHALTK
	. . . ;
	. . . IF YRETURN=2 SET YINHALT=YINHALTK
	. . ;
	. . SET YINHALT=YINHALTX
	
	IF YRETURN=3 IF $TRANSLATE($GET(YFARBCODE)," ")'="" SET YINHALT=YINHALT_Y_YFARBCODE  ;FIS;25.03.03;23255;ANZEIGEN IN FARBE
	QUIT YINHALT
	
 
	
KEY
	;-------------------------------------------------------------------------------
	; FORMAT KEY  ACHTUNG EINSPRUNG zb aus ^WWWFORM4   (EINSPRUNG MIT YDATEI,YLFN,YINHALT)
	;
	; Params:
	;
	; ByRefs:	YINHALTK
	;
	; Returns:
	;
	; History:
	; 11-Feb-2010	shobby	SR17139:  Don't show Primary Key if flag is marked on form field.
	; 							Some assumptions  - This is a primary key so YLFN can be used in WWW121
	; 											  - Some additional checking that YFORM is valid.
	; 13-Dec-2006	JW		BR014285: Corrected language relation lookup
	;-------------------------------------------------------------------------------
	new strGlobal,strRel,strKeys
	
	SET YINHALTK = ""
	; FIXME : Get objKeyField once <GRF>
	SET YTYP = $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,3)
	SET YQ   = 0
	IF YINHALT'="" IF $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,8)'="" DO   ;suchen alle key ;seek All 
	. IF YLFN=1  QUIT:+$PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,20)'=0
	. IF YLFN'=1 QUIT:$PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,20)>1
	. NEW YDAT,YKE,YFE,YSAT,YSPR,YDATA
	. SET YDATA = $GET(^WWW001(0,YDATEI,1))
	. SET YDAT  = $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,8)   ;relationsdatei
	. SET YKE   = $PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,9)   ;relationskey
	. SET YKE1=0
	. IF YKE'="" DO  QUIT:YKE1=1
	. . FOR YKE2=1:1 SET YKE3=$PIECE(YKE,",",YKE2) QUIT:YKE3=""  DO 
	. . . QUIT:$EXTRACT(YKE3)=""""  
	. . . IF $EXTRACT(YKE3)'="""" DO  
	. . . . IF '$DATA(@(YKE3))  SET YKE1=1   ;NICHT DA  ;Not yonder 
	. . . . IF $GET(@(YKE3))="" SET YKE1=1   ;KEIN WERT ;no value 
	. ;
	. SET YFE=+$PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,10)  ;relationsdatenfeld
	. IF +YFE=0 SET YFE=1
	. ;
	. // BR014285 vvvvvvvv
	. set strKeys = $$$QUOTE(YINHALT)
	. IF YKE'="" IF $EXTRACT(YKE)'="," set strKeys = YKE_","_strKeys
	. ;
	. set strRel=""
	. ;
	. SET YSPR=$$$WWW001LanguageClassForRelations(YDATA)  // Check language relation
	. if YSPR'="" do
	. . set strGlobal = $$$NodeString(YSPR,strKeys_",SPRACHE")
	. . set strRel = $PIECE($$^WWWSETL(strGlobal),Y,1)
	. ;
	. if strRel="" do 									// If no language relation
	. . set strGlobal = $$$NodeString(YDAT,strKeys)
	. . SET strRel=$PIECE($$^WWWSETL(strGlobal),Y,YFE)   ;lesen datensatz relation ;read 
	. ;
	. SET YTYP=$PIECE($GET(^WWW003(0,YDAT,YFE,1)),Y,3)
	. IF strRel'="" SET YINHALTK=strRel SET YQ=1
	. ;
	. if $get(YFORM)'="" do
	. . if $$$WWW120ClassUsedInForm($get(^WWW120(0,YFORM,1)))=YDATEI do
	. . . if $$$WWW121DoNotShowPrimaryKey($$Get^WWW121(YFORM,YLFN)) set YRETURN=4 ;SR17139
	
	IF YQ=0 DO FORMAT
	IF YQ=1 IF YTYP'="" DO FORMATK
	QUIT
	
DATEN
	;-------------------------------------------------------------------------------
	; FORMAT DATEN  ACHTUNG EINSPRUNG VON AUSSEN  (YDATEI,LFN,YINHALT)
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 07-Sep-2012	SCR		SR18105: Color code for form VARPARA
	; 22-Feb-2007	JW		SR15452: Display "" as No for booleans.
	; 13-Dec-2006	JW		BR014285: Corrected language relation lookup
	;-------------------------------------------------------------------------------
	new strGlobal,strRel,strKeys
	
	SET YINHALTK=""
	; FIXME : Get objDataField once <GRF>
	SET YTYP=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,3)
	set:YTYP=2 YINHALT=+YINHALT		//SR15452
	
	SET YQ=0
	IF YINHALT'="" IF $PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,8)'="" DO  QUIT
	. ;WENN KEIN JA/NEIN DANN NICHT WENN NICHT ALLE PARAMETER ANGEZEIGT WERDEN SOLLEN ;when no Not when Not parameter will should 
	. IF $PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,3)'=2 IF $PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,28)'=1 QUIT:+$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,20)'=0
	. NEW YDAT,YKE,YFE,YSAT,YSPR,YDATA,YV
	. SET YDAT = $PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,8) 	// Database
	. SET YKE  = $PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,9)	// Keys
	. IF YKE'="" IF $EXTRACT(YKE)'="""" IF '$FIND(YKE,",")  QUIT:'$DATA(@(YKE))  QUIT:$GET(@(YKE))=""
	. ;FIS;24571;07.11.03;KEY VORDATEI MIT MEHREREN VARIABLEN;NUR WENN NICHT "," AN 1.STELLE;TYBD;15,12,2004;ERROR
	. IF YKE'="" IF $EXTRACT(YKE)'=","  IF $FIND(YKE,",")  FOR YV=1:1  QUIT:$PIECE(YKE,",",YV,99)=""  IF $EXTRACT($PIECE(YKE,",",YV))'="""" SET:$GET(@($PIECE(YKE,",",YV)))="" YKE=""  QUIT:YKE=""
	. SET YTYP=""
	. SET YFE = +$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,10)
	. IF +YFE=0 SET YFE=1
	. ;
	. // BR014285 vvvvvvvv
	. SET YDATA=$GET(^WWW001(0,YDAT,1))
	. ;
	. set strKeys = $$$QUOTE(YINHALT)
	. IF YKE'="" set strKeys = YKE_","_strKeys
	. ;
	. QUIT:$FIND(strKeys,",,")      ; FALSCHER KEY
	. QUIT:$FIND(strKeys,"""""")    ; FALSCHER EINTRAG
	. QUIT:$FIND(strKeys,";")       ; KEIN MULTIFELD
	. ;
	. SET YTYP=$PIECE($GET(^WWW003(0,YDAT,YFE,1)),Y,3)
	. ;
	. set strRel=""
	. ;
	. SET YSPR=$$$WWW001LanguageClassForRelations(YDATA)  // Check language relation
	. if YSPR'="" do
	. . set strGlobal = $$$NodeString(YSPR,strKeys_",SPRACHE")
	. . set strRel = $PIECE($$^WWWSETL(strGlobal),Y,1)
	. ;
	. if strRel="" do 									// If no language relation
	. . set strGlobal = $$$NodeString(YDAT,strKeys)
	. . SET strRel=$PIECE($$^WWWSETL(strGlobal),Y,YFE)   ;lesen datensatz relation ;read 
	. ;
	. ; IF YRETURN=3 IF $FIND(YDAT,"WWW101") || ($FIND(YDAT,"INPARA")) SET YFARBCODE=$PIECE($$^WWWSETL(strGlobal),Y,2)  ;FIS;25.03.03;23255;ANZEIGEN IN FARBE
	. IF YRETURN=3 IF $FIND(YDAT,"WWW101") || ($FIND(YDAT,"INPARA")) || ($FIND(YDAT,"VARPARA")) SET YFARBCODE=$PIECE($$^WWWSETL(strGlobal),Y,2)  ; SR18105
	. ;
	. quit:strRel=""
	. ;
	. SET YINHALTK=$EXTRACT($TRANSLATE(strRel,"|"," "),1,40)
	. // TODO: Is this a sensible condition for NOT showing the stored value??
	. IF $EXTRACT(YKE)="""" DO
	. . SET YINHALT=""
	. 
	. SET YQ=1
	. IF YTYP'="" DO FORMATK
	
	IF YQ=0 DO FORMAT
	QUIT
 
 	; ----------------------------
	; YTYP		Field type
	; 3			Memo             ; FIXME : May have had more than 200 chars before $e/$tr but never get "..." - both subr below <GRF>
	; 5			Password
	; 8 		Currency
	; ----------------------------
 
FORMAT
	;-------------------------------------------------------------------------------
	; ByRef :
	; 	YINHALT		Value in and out
	;	YTYP		Field Type
	;	YWHR		idCurrency (^WWWWAE)
	;-------------------------------------------------------------------------------
	QUIT:YTYP=""
	QUIT:YINHALT=""
	SET YINHALT = $$GetLiteral^WWWTR(YTYP,YINHALT)
	
	IF YTYP=8 DO
	. IF $GET(YWHR)'="" DO   ;WÄHRUNG ANZEIGEN;TYBD;15.04.2003 ;money standard display 
	. . IF +$PIECE($GET(^WWWWAE(0,YWHR,1)),Y,4)=0 SET YINHALT=$$^WWWWHR(YWHR)_" "_YINHALT QUIT 
	. . SET YINHALT=YINHALT_" "_$$^WWWWHR(YWHR)
	
	IF YTYP=5 SET YINHALT = $EXTRACT("*****************",1,$LENGTH(YINHALT)) QUIT
	IF YTYP=3 SET YINHALT = $TRANSLATE($EXTRACT($PIECE(YINHALT,"|",1),1,200),"|"," ") SET:$EXTRACT(YINHALT,200)'="" YINHALT=YINHALT_"..." QUIT
	QUIT
	
	
	
FORMATK
	;-------------------------------------------------------------------------------
	; ByRef :
	; 	YINHALTK	Value in and out
	;	YTYP		Field Type
	;	YWHR		idCurrency (^WWWWAE)
	;-------------------------------------------------------------------------------
	QUIT:YTYP=""
	QUIT:YINHALTK=""
	SET YINHALTK = $$GetLiteral^WWWTR(YTYP,YINHALTK)
	
	IF YTYP=8 IF $GET(YWHR)'="" SET YINHALTK=YINHALTK_" "_$$^WWWWHR(YWHR)   ;WÄHRUNG ANZEIGEN ;money standard display 
	IF YTYP=5 SET YINHALTK = $EXTRACT("*****************",1,$LENGTH(YINHALTK)) QUIT
	IF YTYP=3 SET YINHALTK = $TRANSLATE($EXTRACT($PIECE(YINHALTK,"|",1),1,200),"|"," ") SET:$EXTRACT(YINHALTK,200)'="" YINHALTK=YINHALTK_"..." QUIT
	QUIT
	
]]></Routine>
</Export>