<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewDistribute" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewDistribute ; Distributes COMViews to client sites
#include COMConst
#include COMSYS
	
Distribute(pidModule="")
    ;-------------------------------------------------------------------------------
    ; Look through all views for views to distribute
    ;
    ; Inputs:
    ;	pidModule		Module prefix (Upper Case)
    ;
    ; History:
    ; 24-Oct-2008	SCR		SR16107: Removed case conversion on module (should always
    ; 							 be upper case); moved length function outside loop 
    ; 30-Oct-2008	PPP		SR16107: Check for module based on uppercase characters
    ; 01-Mar-2005	Paul K	SR11984: Added support for modules.
    ; 30-Mar-2005	Paul K	SR11984: Created
    ;-------------------------------------------------------------------------------
	new idClass,idView,objView,intLen
	
	set intLen  = $length(pidModule)
	set idClass = "" //SR16107
	for {
		set idClass = $order(^COMView(0,idClass))
		quit:idClass=""
		continue:$zcvt($extract(idClass,1,intLen),"U")'=pidModule  //SR16107
		
		set idView = ""
		for {
			set idView = $order(^COMView(0,idClass,idView))
			quit:idView=""
			
			set objView = $get(^COMView(0,idClass,idView,1))
			if $$$COMViewDistribute(objView) {
				do DistributeView(idClass,idView)
			}
		}
	}
	
	quit
	
	
DistributeView(pidClass,pidView)
    ;-------------------------------------------------------------------------------
    ; Distribute a single view
    ;
    ; History:
    ; 26-Apr-2007	FrankF	SRBR014441: Translation for favourites.
    ; 18-Jul-2005	RobertW	SR12981: Moved KillSet/Set/Kill Commands to COMDistUtilities
    ; 30-Mar-2005	Paul K	Created (SR#11984)
    ;-------------------------------------------------------------------------------
    new idLanguage
    
	if (pidClass'="")&&(pidView'="") {
		do KillSet^COMDistUtilities("^COMView(0,",pidClass_""","""_pidView)
		do KillSet^COMDistUtilities("^COMViewFilter(0,",pidClass_""","""_pidView)
		do KillSet^COMDistUtilities("^COMViewColumn(0,",pidClass_""","""_pidView)
		
		// SRBR014441: vvv
		set idLanguage = ""
		for {
			set idLanguage = $order(^WWW100(0,"SPRACHE","EN",idLanguage))
			quit:idLanguage=""
		
			// The Language cannot be the last primary key of the class COMViewLang because it's being used
			// in a Grid Edit component. 
			do KillSet^COMDistUtilities("^COMViewLang(0,",pidClass_""","""_idLanguage_""","""_pidView)
		}
		// SRBR014441: ^^^
	}
	quit
	
]]></Routine>
</Export>