<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEGESAMT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEGESAMT(SWE,AUF,APO,ALL)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ALLE POSITIONEN EINES AUFTRAGES EINLAGERN   ; STORE ALL LINES OF AN ORDER
	;
	; SOLLTEN VORHER AUFPOS MANUELL EINGELAGERT WORDEN SEIN, WERDEN DIESE
	; NICHT NACHTRÄGLICH ALS ZU VERZOLLEND DEKLARIERT!
	; 
	; Called By : Routine - INWEAUF (Store all order lines when "All Line Items Received" set)
	; 
	; Updates : ^INWESCHNELL	Group Receiving : Item, Locn, Ord, Line, Supplier
	;           ^INWEAUF		Incoming Goods  : Ord, Line, Locn, Storage, Date.seq
	; 
	; Inputs : 
	;	SWE		blnEdit ?	1= NUR SCHNELL-WE ERFASSEN ;only Edit	
	;	AUF		idOrder		AUFTRAG ;order
	;	APO		idLine		AUFTRAGSPOSITION, DIE ERFASST WURDE
	;	ALL		blnAll		1 = ALLE AUFPOS ZU VERZOLLEN
	;						0 = NICHT ALLE AUFPOS ZU VERZOLLEN ;within Not within 
	;
	; ByRef : YM, YLOCATION
	;
	; Returns : Nothing
	;
	; History :
	; 01-Sep-2008	HQN		Added doco for ByRef variables
	; 12-Oct-2006	GRF		Doco
	; 29-Sep-2006	GRF		Doco; quits
	; 14-Apr-2005	shobby	Included changes as per Mr Fischer's email (SR12101)
	; 09.10.2001	FIS
	;-------------------------------------------------------------------------------
	NEW AUF1,BETR,DAT,ERLEDIGT,LAGER,LAGERX,POS,POS1,SATZ,SATZ1,YFELD,YI
	
	SET SWE  = +$GET(SWE)
	SET APO  = +$GET(APO)
	SET ALL  = +$GET(ALL)
	SET AUF  =  $GET(AUF)                 QUIT:AUF=""
	SET AUF1 =  $GET(^INAUF(YM,AUF,1))    QUIT:AUF1=""
	
	SET BETR=$PIECE(AUF1,Y,6)  ;BETRIEB
	IF BETR="" SET BETR=YLOCATION
	SET LAGER=$PIECE($GET(^INVORGB(YM,YM,BETR,1)),Y,1)
	IF LAGER="" SET LAGER=$ORDER(^INLP(YM,BETR,""))
	IF LAGER="" SET LAGER=0
	
	;----- INFORMATIONEN AUS ERFASSTEN WARENEINGÄNGEN ------
	SET SATZ=""
	SET DAT=0
	IF $DATA(^INWEAUF(YM,AUF)) DO
	. NEW POS,BET,LAP,WED
	. SET POS=""
	. FOR  SET POS=$ORDER(^INWEAUF(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET BET=""
	. . FOR  SET BET=$ORDER(^INWEAUF(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . SET LAP=""
	. . . FOR  SET LAP=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . SET WED=$ORDER(^INWEAUF(YM,AUF,POS,BET,LAP,""),-1)
	. . . . QUIT:WED=""
	. . . . SET ERLEDIGT(POS)=""
	. . . . IF WED>DAT IF POS=APO DO
	. . . . . SET SATZ=$GET(^INWEAUF(YM,AUF,POS,BET,LAP,WED,1))
	. . . . . SET BETR  = BET
	. . . . . SET DAT   = WED
	. . . . . SET LAGER = LAP
	
	IF $DATA(^INWEAUFALT(YM,AUF)) DO
	. NEW POS,BET,LAP,WED
	. SET POS=""
	. FOR  SET POS=$ORDER(^INWEAUFALT(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET BET=""
	. . FOR  SET BET=$ORDER(^INWEAUFALT(YM,AUF,POS,BET)) QUIT:BET=""  DO
	. . . SET LAP=""
	. . . FOR  SET LAP=$ORDER(^INWEAUFALT(YM,AUF,POS,BET,LAP)) QUIT:LAP=""  DO
	. . . . SET WED=$ORDER(^INWEAUFALT(YM,AUF,POS,BET,LAP,""),-1)
	. . . . QUIT:WED=""
	. . . . SET ERLEDIGT(POS)=""
	. . . . IF WED>DAT IF POS=APO DO
	. . . . . SET SATZ=$GET(^INWEAUFALT(YM,AUF,POS,BET,LAP,WED,1))
	. . . . . SET BETR  = BET
	. . . . . SET DAT   = WED
	. . . . . SET LAGER = LAP
	
	;------  SPEICHERN WE 
	IF SWE'=1 DO  ;WE BUCHEN ;In 
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . ;
	. . QUIT:$DATA(ERLEDIGT(POS))  ;POSITION BEREITS EINGELAGERT ;yet 
	. . QUIT:$PIECE(POS1,Y,4)=""   ;KEIN ARTIKEL ;no item 
	. . QUIT:$PIECE(POS1,Y,7)'=1   ;KEINE BESTELLUNG ;no sales order 
	. . ;
	. . SET YFELD=""
	. . SET $PIECE(YFELD,Y,49)=APO               ;TATSÄCHLICH ERFASSTE POSITION ;really 
	. . SET $PIECE(YFELD,Y,10)=0                 ;WARENEINGANG OK ;receiving 
	. . SET $PIECE(YFELD,Y,1)=$PIECE(SATZ,Y,1)   ;WARENEINGANGSNUMMER
	. . SET $PIECE(YFELD,Y,4)=$PIECE(POS1,Y,5)   ;MENGE   ;quantity 
	. . SET $PIECE(YFELD,Y,7)=$PIECE(AUF1,Y,12)  ;LIEFERANT LF-BESTELLUNG ;supplier 
	. . IF $PIECE(YFELD,Y,7)="" SET $PIECE(YFELD,Y,7)=$PIECE(POS1,Y,12)  ;LIEFERANT KUNDENAUFTRAG ;supplier customer´s order 
	. . IF $PIECE(YFELD,Y,7)="" SET $PIECE(YFELD,Y,7)=$PIECE(SATZ,Y,7)   ;LIEFERANT AUS VORGABEN ;supplier out of 
	. . IF $PIECE(YFELD,Y,7)=$PIECE(SATZ,Y,7) FOR YI=11,12,13,14,15 SET $PIECE(YFELD,Y,YI)=$PIECE(SATZ,Y,YI)  ;ALTE WE INFO AUFTRAG ;In order 
	. . IF $PIECE(POS1,Y,47)'="" DO
	. . . NEW MENGE
	. . . ;
	. . . ; FIXME : SR15107 : Use D109 Received quantity if non-null? <GRF>    <DEFERRED FOR PURCHASE ORDER REVIEW>
	. . . ;         May not be updated yet if this is the actual receipting process
	. . . ; 
	. . . SET MENGE=$PIECE(POS1,Y,5)
	. . . IF +MENGE=0 SET MENGE=1
	. . . SET $PIECE(YFELD,Y,8)=$PIECE(POS1,Y,47)/MENGE*$$^INQTYUNIT(,AUF,POS)  ;EK ;Planned Cost
	. . ;  
	. . IF $PIECE(POS1,Y,47)="" IF $PIECE(POS1,Y,4)'="" SET $PIECE(YFELD,Y,8)=$$^INARTKOST($PIECE(POS1,Y,4))  ;EK ;Planned Cost
	. . SET $PIECE(YFELD,Y,8)=$JUSTIFY($PIECE(YFELD,Y,8),0,2)
	. . ;
	. . SET LAGERX=LAGER
	. . IF $DATA(^INARTLP(YM,$PIECE(POS1,Y,4),BETR)) DO
	. . . SET LAGERX=$ORDER(^INARTLP(YM,$PIECE(POS1,Y,4),BETR,""))
	. . . IF '$DATA(^INLP(YM,BETR,LAGERX,1)) SET LAGERX=LAGER
	. . ;
	. . IF ALL=1 SET $PIECE(YFELD,Y,100)=1    ;WEM;24071;03.11.2003;JEDE AUFPOS ALS ZU VERZOLLEND DEKLARIEREN
	. . ;
	. . DO IMPORT^INWEAUF1  ; update YFELD (objINWEAUF)  -  IMPORT SERVICE COSTS (FIELD 115);FIS;13.04.05
	. . ;
	. . DO
	. . . NEW YKEY
	. . . SET YKEY=AUF_","_POS_","_BETR_","_LAGERX_","_DAT
	. . . SET YOK=$$^WWWSPEI("INWEAUF",YKEY,YFELD,1)
	. . . DO ^INWEAUF  ;BUCHEN WARENEINGANG ;receiving 
	
	IF SWE=1 DO  ;NUR SCHNELL-WE ;only 
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . ;
	. . QUIT:$DATA(ERLEDIGT(POS))  ;POSITION BEREITS EINGELAGERT ;yet 
	. . QUIT:$PIECE(POS1,Y,4)=""   ;KEIN ARTIKEL ;no item 
	. . QUIT:$PIECE(POS1,Y,7)'=1   ;KEINE BESTELLUNG ;no sales order 
	. . ;
	. . SET YFELD=$GET(SWEFELD)    ;SWEFELD=VORGABE AUS SCHNELLWE ;out of 
	. . SET $PIECE(YFELD,Y,1)=$PIECE(POS1,Y,5)    ;POSITIONSMENGE
	. . IF $PIECE(POS1,Y,40)'="" SET $PIECE(YFELD,Y,3)=$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(POS1,Y,40),1)),Y,1)  ;EINHEIT ;unit
	. . IF '$DATA(^INWESCHNELL(YM,$PIECE(POS1,Y,4),BETR,AUF,POS)) DO
	. . . NEW KEY,LIEF
	. . . SET LIEF=$PIECE(AUF1,Y,12)              ;LIEFERANT LF-BESTELLUNG ;supplier 
	. . . IF LIEF="" SET LIEF=$PIECE(POS1,Y,12)   ;LIEFERANT KUNDENAUFTRAG ;supplier customer´s order 
	. . . IF LIEF="" QUIT                         ;KEIN LIEFERANT ;no supplier 
	. . . SET KEY=$PIECE(POS1,Y,4)_","_BETR_","_AUF_","_POS_","_LIEF
	. . . SET YOK=$$^WWWSPEI("INWESCHNELL",KEY,YFELD,1)
	
	QUIT
	
]]></Routine>
</Export>