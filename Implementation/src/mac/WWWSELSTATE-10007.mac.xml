<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSELSTATE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSELSTATE(YINHALT,YUPDATE,YFOCUS)
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		UPDATE STATE IF COUNTRY CHANGES
	;		Replaces State List with customised State List
	;
	; Inputs : 
	;	YINHALT=SELECTED COUNTRY
	;	YUPDATE=FIELD TO BE UPDATED (Class Field Number - not Form)
	;	YFOCUS=SET FOCUS TO STATE FIELD
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 16-Jul-2007	RPW		SRadhoc: QUITS and removed unecessary variable
	; 22-Jun-2005	GRF		SR12323 : Test for YINHALT against WWWDATEN was not
	; 						coping with change and then restore.  e.g. Start with AU
	; 						& see AU states.  Change to US & see US states.  Change
	; 						back to AU - YINHALT and WWWDATEN are *both* the same so
	; 						doesn't update list; Doco; use $$$NULLID, $$$SPACE
	; 12.02.2004	FIS
	;-------------------------------------------------------------------------------
	NEW YI,YNO

	;KEINE ÄNDERUNG, in programm "^INAUTOADDRES";FAN;09.03.04;25150
	;GRF;22-Jun-2005; comment out following line
	;IF $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,YART,2))'="" QUIT:YINHALT=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,YART,2)),Y,YLFN)
	
	SET YUPDATE=$GET(YUPDATE)
	DO
	. IF YUPDATE=""  SET YUPDATE="Y"_YFORM_YART_($GET(YLFN)+1) QUIT     ;NEXT FIELD
	. IF +YUPDATE'=0 SET YUPDATE="Y"_YFORM_YART_+YUPDATE       QUIT     ;OTHER FIELD
	. IF YUPDATE'="" IF $EXTRACT(YUPDATE)'="Y" SET YUPDATE="Y"_YUPDATE  ;OTHER FIELD (OTHER FIELD TYPE)
	
	SET %TXT(1)=""
	
	; NO COUNTRY CODE SPECIFIED YET - Set single null entry
	IF $GET(YINHALT)="" DO  QUIT
	. SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~REMOVE"               ;FIS;26294;17.08.04;LÖSCHEN VOR NEUAUFBAU
	. SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_0
	. ; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;FOR YI=1:1:40 DO
	. . SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~"_YI_"~"_$$$SPACE_"~"_YI
	; ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END	
	
	SET YI=0
	; DELETE BEFORE RECONSTRUCTION ;LÖSCHEN VOR NEUAUFBAU
	SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~REMOVE"                 ;FIS;26294;17.08.04
	SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_YI
	
	; FIXME : What if there are more than 70 states for a country?
	;         - will never be able to get to the end of the list <GRF>
	SET YNO=""
	FOR YI(1)=1:1:70 SET YNO=$ORDER(^WWW102(0,"BUNDESLAND",YINHALT,YNO)) QUIT:YNO=""  DO
	. SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~"_YNO_"~"_YNO_" - "_$PIECE($GET(^WWW102(0,"BUNDESLAND",YINHALT,YNO,1)),Y,1)_"~"_YI(1)
	
	; vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	;IF YI<79 FOR YI=YI+1:1:70 DO
	. SET %TXT(1)=%TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_YI
	;IF YKEY="" IF YFORM="INKUNDE"!(YFORM="INLIEF")!(YFORM="INWERBADR") DO  QUIT
	. DO ^INAUTOADDRESS($PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,YART,1)),Y,12))   ;12 IST PLZ;FAN;9.3.04;25150 
	; ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	
	IF $GET(YFOCUS)'="" SET %TXT(1)=%TXT(1)_"#FUNCTION~document."_YHTMFORM_"."_YUPDATE_".focus();"
	QUIT
]]></Routine>
</Export>