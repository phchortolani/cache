<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMC2012" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include WWWFORM
	

Button(YA,YBACK,&YI,YPARA,YMANDANT,YOPTION,YFORM,SPRACHE,YLFN,pblnIncludeContainer=$$$YES) 
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 14-Jun-2012	shobby		SR18033: Created. Based on Button^WWWFORMB
	;-------------------------------------------------------------------------------
	new HTML,strPic,strTitle,xferVar,newForm,strText
	
	set HTML=""
	
	set Q     = ""
	set YQ    = ""

	if $extract($$$WWW124TargetFrameName(YA))="@" set YBACK  = ""   ;TYBD;OHNE YBACK ;without 

	if '$$DontShow^WWWFORMC() {
		if $$^WWWACCESS($$$WWW124Authorization1(YA),$$$WWW124Module1(YA))=$$$YES {
			if '((($$$WWW124ExecuteOnClick(YA)="")                &&
			          ($$$WWW124NewFormOnClick(YA)="")            &&
			          ($$$WWW124LinkWithexeOrHttp(YA)="")         &&
			          ($$$WWW124DownloadFileOnClick(YA)="")       &&
			          ($$$WWW124FunctionJavaScriptOnClick(YA)="") &&
			          ($$$WWW124ExecuteForManualLink(YA)="")      &&
			          ($$$WWW124SearchFunctionOnClick(YA)="")     &&
			          ($$$WWW124ChangeoversWithOnClick(YA)="")    &&
			          ($$$WWW124HyperEventOnClick(YA)="")            )) {

				if pblnIncludeContainer {
					set HTML=HTML_"<TD><DIV id='Y"_YFORM_"D"_YLFN_"CONTAINER'><TABLE><TR>"
				} else {
					set HTML=HTML_"<TABLE><TR>"
				}
				set strTitle = $$GetButtonDescription^WWW124(YFORM,SPRACHE,YLFN)

				;---------------------------------------
				; If the
				;---------------------------------------
				set xferVar = $$$WWW124TransferVariableIdentifie(YA)
				if xferVar'="" {
					if $data(@xferVar) {
						set @xferVar = $get(%(YQUERY,xferVar))
					}
				}

				set YI = YI+1

				;---------------------------------------
				; Using images for buttons but none specified
				; so use first character of description as
				; the image name. ("Post" => "p.gif")
				;---------------------------------------
				set strPic  = $$$WWW124PictureFileOnButton(YA)
				if blnButtons && (strPic="") {
					set strPic = $zconvert($extract($$^WWWUMLAU($$$WWW124ButtonDescription(YA),3)),"L")
					if '$find("abcdefghijklmnopqrstuvwxyzöäüß@?1234567890",strPic) set strPic = "x"
					set strPic = strPic_".gif"
				}
				;---------------------------------------

				set newForm  = $$$WWW124NewFormOnClick(YA)
				set YTARGETF = $$$WWW124TargetFrameName(YA)
				if $extract(YTARGETF)="@" set YTARGETF = @$extract(YTARGETF,2,99)  ;ZIELTARGET AUS PARAMETER ;out of parameter 

				set YCONF1 = ""  ; Start JS
				set YCONF2 = ""  ; End JS

				if $$Disabled(newForm,blnButtons,strPic,$$$YES,.YCONF1,.YCONF2,YLFN,.strText) {
					set HTML=HTML_$$OpenTD^WWWFORMCOMMON(1)
					if $$$WWW124ImageFileForDisabledButto(YA)'="" {
						set strPic = $$$WWW124ImageFileForDisabledButto(YA)  ;NEUER DISABLED BUTTON
					} elseif $extract(strPic,2)="." {
						set $piece(strPic,".",1) = $extract(strPic)_"_dis"		//SR15240
					}
			
					if strPic'="" {
						if $$$WWW012DisplayButtonTooltips($get(^WWW012(0,YM,1))) {		//SR15240
							set HTML=HTML_$$StopButtonEx^WWWFORMCOMMON($$$HTMLText(strText),strPic,"Y"_$get(YFORM)_"D"_$get(YLFN)_"_"_$get(YLFN)_"IMG","","","cursor:default")
						} else {
							if strText'="" set HTML=HTML_"<A onClick='return doLink(this)' HREF=""JavaScript:"_$$$HTMLText("alert('"_$$$JSText(strText)_"');")_""">"		//SR14979 ;16819
							set HTML=HTML_$$StopButtonEx^WWWFORMCOMMON($$^WWWUML($$$WWW124ButtonDescription(YA)),strPic,"Y"_$get(YFORM)_"D"_$get(pidYLFN)_"_"_$get(YLFN)_"IMG","","","cursor:default")
							if strText'="" set HTML=HTML_"</A>"
						}
					}
					set HTML=HTML_$$$CRLF
					set HTML=HTML_$$CloseTD^WWWFORMCOMMON()
				} else {
					// Add confirmation text

					if ($$$WWW124AcknowledgementTextBefore(YA)'="") {
						$$$Append(YCONF1," if (confirm('"_$translate($$$WWW124AcknowledgementTextBefore(YA),"'""","´´")_"')) {")
						$$$Prepend(YCONF2,"} ")
					}

					if $$$WWW124AutomaticSUBMIT(YA) if newForm'="" if $$$WWW124ExecuteOnClick(YA)="" set $$$WWW124ExecuteOnClick(YA)="SUBMIT"   ;AUTOSUBMIT
					;-------------------------------
					set HTML=HTML_$$OpenTD^WWWFORMCOMMON($$$NO)
					if $$$WWW124FunctionJavaScriptOnClick(YA)'="" {
						;set blnButtons=$$$YES
						set HTML=HTML_$$Java(strTitle)
					}
					set HTML=HTML_$$CloseTD^WWWFORMCOMMON()
				}
				if pblnIncludeContainer {
					set HTML=HTML_"</TR></TABLE><DIV></TD>"
				} else {
					set HTML=HTML_"</TR></TABLE>"
				}
			}
		}
	}
	quit HTML

	 
Java(pstrTitle) ; FIXME : Pass blnButtons
	;-------------------------------------------------------------------------------
	;-------------------------------------------------------------------------------
	new HTML

	set HTML=""

	if blnButtons {
		set HTML=HTML_"<A onClick='return doLink(this)' HREF=""JavaScript:"_YCONF1
	} else {
		set HTML=HTML_"<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""" onClick="""_YCONF1
	}
	if $$$WWW124AutomaticSUBMIT(YA)=1    set HTML=HTML_"retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','','9');"
	if $$$WWW124SetVariableYPARA(YA)'="" set HTML=HTML_"document.WWW.YPARA.value='"_$$$WWW124SetVariableYPARA(YA)_"'; "  ;PARAMETER NEU
	set HTML=HTML_$$$WWW124FunctionJavaScriptOnClick(YA)
	if '$find($$$WWW124FunctionJavaScriptOnClick(YA),";") set HTML=HTML_";"
	set HTML=HTML_YCONF2
	set HTML=HTML_""""
	if YTARGETF'="" set HTML=HTML_" TARGET="""_YTARGETF_""""
	set HTML=HTML_">"
	if blnButtons {
		set HTML=HTML_$$StopButtonEx^WWWFORMCOMMON($$^WWWUML(pstrTitle),strPic,$get(YFORM)_"_"_YLFN)
		set HTML=HTML_"</A>"
	}
	quit HTML

Disabled(newForm,blnButtons,strPic,pblnTD=$$$NO,&pstrStartJS,&pstrEndJS,pidYLFN,&pstrText)
	;-------------------------------------------------------------------------------
	; Works out if button should be disabled, and if so, displays it as such.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns: whether button should be disabled
	;
	; History:
	; 17-Aug-2009	shobby	SR16819: When displaying javascript alerts on disabled
	; 							buttons don't crash if the text contains a "
	; 22-Sep-2008	shobby	SRBR014939: Moved DataItemInUse check not to depend on
	; 							whether there is ExecuteToCheckForDisabled code.
	; 23-Jul-2008	GRF		SRBR014939: use macro & revised property name for WWW124
	; 30-Apr-2008	shobby	SRBR014939: Need to check YBEARB to find out if the
	; 							disable was because it is 'In Use'
	; 29-Apr-2008	shobby	SRBR014939: Disable buttons when Data Item In Use
	; 25-Jun-2007	shobby	SRBR014409: pidYLFN as a parameter
	; 25-Jun-2007	shobby	SRBR014409: More changes to the button id.
	; 11-Jun-2007	shobby	SRBR014409: Changes to button id to guarantee uniqueness
	; 08-Jun-2007	GRF		SRBR014417: property name changed
	; 03-Jan-2007	JW		SR15240: Revision to OpenTD macro for Disabled buttons
	; 25-Oct-2006	JW		BR014279: Grid check for selected record
	; 12-Sep-2006	JW		Convert alert text to JS
	; 16-Dec-2005	JW		SR13195: Created
	;-------------------------------------------------------------------------------
	new Q,YQ,blnDisabled,strText,strExec,blnHidden ;SR18033
	
	set blnDisabled = $$$NO
	set blnHidden   = $$$NO
	
	;AUS WWWFORMC ÜBERNOMMEN ;FIS;24.02.03 ;out of
		;	 5 = "Read Only"
	if ($$$WWW120AuthorizationToModifyData($get(YVOR))=$$$EnumReadOnly) && '$$$WWW124AvailWhenDataItemIsInUse(YA) && ($get(YBEARB)=4) {  ;BR014939
		set blnDisabled = $$$YES                                                ;BR014939
		set pstrText     = $$$SysEnum("BEARBEITUNG",4)   ; "Data Item In Use"    ;BR014939
	} else {  ;SR18033
		set strExec=$$$WWW124ExecuteToCheckForDisabled(YA)
		if strExec'="" {   ;PRUEFEN DISABLED
		;-----------------------------------
		; *** EXECUTE # ***
		;-----------------------------------
			xecute strExec
			
			if (+$get(Q)=2) || (+$get(YQ)=2) {
				set blnHidden = $$$YES
				
			} elseif (+$get(Q)=1) || (+$get(YQ)=1) {
				set pstrText=""   ;S pstrText=$$^WWWTEXT(32072)
				if $extract($get(Q),2,999)'=""  set pstrText = $extract(Q,2,999)
				if $extract($get(YQ),2,999)'="" set pstrText = $extract(YQ,2,999)
				set blnDisabled = $$$YES
			}
		}
	}
	if 'blnHidden {
		if 'blnDisabled { ;SR18033
			;If there needs to be a key and there isn't one, disable.
			;
			;NOTE - TODO - this check should probably be BEFORE the xecute above.
			set blnDisabled=$$$NO ;SR18033
			; SRBR014417
			if $$$WWW124OnlyWhenRecordSelected(YA) || ((newForm'="") && (+$$$WWW121FixedInputForHiddenField($get(^WWW121(0,newForm,1,1)))'=0)) {
				if (YFOART=12) {		// BR014279 - Grid
					set pstrText     = $$^WWWTEXT("WWW00053")	; "Please select a saved grid record."
					set pstrStartJS = "if "_$$GetDisabledCheck^COMGridEdit31J_" { alert('"_pstrText_"'); } else { "
					set pstrEndJS   = " }"
					
				} else {				// Not grid
					if blnButtons && $$IsNewRecord^WWWFORMStatus() {		// Don't know why only for buttons...
						set pstrText     = $$^WWWTEXT(32072)       ; "Please Select A Data Record First."
						set blnDisabled = $$$YES
					}
				}
			}
		}
		
	}
	quit (blnDisabled || blnHidden)
]]></Routine>
</Export>