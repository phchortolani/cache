<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPPaymentLine2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPPaymentLine2
	#include COMSYS
	#include FINConst
	#include WWWConst
 
GetGroupPayment(pstrYKEY)
    /*-------------------------------------------------------------------------------
    ; Return the total amount being paid over account groups for this
    ; supplier payment line.
    ;
    ; Params: pstrYKEY		The Supplier Payment Line ID.
    ;
    ; ByRefs:
    ;
    ; Returns: curAmount
    ;
    ; History:
    ; 10-Feb-2006	RPW			SR13953: Peer Review, use correct naming convention
    ; 							set pstrYKEY to $get of itself once.
    ; 18-Jan-2006	Steve S		SR13953: Created
    ;-------------------------------------------------------------------------------*/
	new curAmount,idPay,idPayLine,idAccGrp,objFINAPPaymentGroup
 
	set curAmount=0
	
	;<TODO> Do we need an FCUpdate at the end of this?
	
	set pstrYKEY	= $get(pstrYKEY)
	set idPay		= $$$KEY1(pstrYKEY)
	set idPayLine	= $$$KEY2(pstrYKEY)
	
	if (idPay'="")&&(idPayLine'="") {
		set idAccGrp=""
		for {
			set idAccGrp=$order(^FINAPPaymentGroup(YM,idPay,idPayLine,idAccGrp))
			quit:(idAccGrp="")
			
			set objFINAPPaymentGroup=$get(^FINAPPaymentGroup(YM,idPay,idPayLine,idAccGrp,1))
			set curAmount=$$$FCAdd(curAmount,$$$FINAPPaymentGroupTotal(objFINAPPaymentGroup))
		}	
	}
 
	quit curAmount
	
	
GetDept(pidUser)
	;-------------------------------------------------------------------------------
	; Return a user's department for a payment line
	;
	; Params: pidUser		: The user we want
	;
	; ByRefs:
	;
	; Returns: idDept		: The department for that user
	;
	; History:
	; 17-Oct-2006	Steve S		SR15086: Created
	;-------------------------------------------------------------------------------
	new idDept
	
	set idDept=""
	
	if ($get(pidUser)'="") {
		set idDept = $$$WWW013HomeDepartment($get(^WWW013(0,pidUser,1)))
	}
	
	quit idDept
 
]]></Routine>
</Export>