<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReqLineScan" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReqLineScan
	
#include COMGridEdit31
#include INConst
#include %occInclude
	
AddScanLine(pidScan)
    ;-------------------------------------------------------------------------------
    ; Routine to Add a new line to a Grid after a Scan is validated
    ; 
    ; FIXME : perhaps should check for pidScan containing INARTPACK Barcode as
    ; alternative to Item Number - could then be scanning multiple units for an item
    ;
    ; Assumptions :
    ;  YFORM	-	 Form Name
    ; 
    ; Returns:
    ; 	Status
    ;
    ; History:
    ; 02-Feb-2011	GRF		SR17579: initial setting - just set to 1
    ; 03-Nov-2009	DWR		SR16983: change how unit is set, dont need the entire call.
    ; 20-Jul-2009	PPP		SR16773: ScanDelimiter is a function now
    ; 25-May-2009	PPP		SR16565: Scanning Prompt for Grid
    ;-------------------------------------------------------------------------------
	new arrFields,idClass,idItem,idLine,idRowAdded,objHeader,objLine,objOldData
 	
	set objHeader = $$$GRIDYFELDContainer
	
	if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
		if ($$getControlarAutorizacaoProdutos^VARParametroCliente(YM)){
			new idReq,objReq,idStatus,idToLocn,idItemReq,objReqLine,strStatus
			set idReq = $$$GRIDYKEYContainer
			
			if (idReq '= ""){
				set objReq 	  = $get(^INReq(YM,idReq,1))
				set idStatus  = $$$INReqStatus(objReq)
				set idToLocn  = $$$INReqToLocn(objReq)
				set idItemReq = $piece(pidScan,$$$ScanDelim,1)

				if ((idToLocn'="")&&(idItemReq'="")&&(idStatus=1)){
					if ($$GetDRPRecord^INDRPITEM(idToLocn,idItemReq) = "") {
						$$$Alert("Produto "_idItemReq_" nÃ£o autorizado para o local "_idToLocn_".")			
						quit $$$OK
					}
				}
			}
		}
	}
	
	if $get(YFORM)'="" {
		set idClass = $$$GetClass(YFORM)
		set objLine = ""
		set idItem  = $piece(pidScan,$$$ScanDelim,1)
		
		//Get the data for the Item if it already exists
		set idLine = $$GetRowLine^COMGridEdit31Scan(idItem, $$$FldINReqLineItem)

		if idLine {
			set (objLine, objOldData) = $$$GRIDGetYFELD(idLine)
			set $$$INReqLineQtyOrdered(objLine)       = $$$INReqLineQtyOrdered(objLine)  + 1
			set $$$INReqLineQtyRequired(objLine)      = $$$INReqLineQtyRequired(objLine) + 1
			
			set arrFields($$$FldINReqLineQtyOrdered)  = $$$INReqLineQtyOrdered(objLine)
			set arrFields($$$FldINReqLineQtyRequired) = $$$INReqLineQtyRequired(objLine)
			
			//Updates the Row and Displays it (for existing lines)
			//If the row is on another page displays the lines at the top of the grid
			do UpdateRow^COMGridEdit31Scan(YFORM,idLine,.arrFields,objOldData)
			
		} else {
			set $$$INReqLineItem(objLine)				= idItem
			set $$$INReqLineQtyOrdered(objLine)			= 1         ; SR17579
			set $$$INReqLineQtyRequired(objLine)		= 1         ; SR17579
			set $$$INReqLineUnit(objLine)				= $$$INARTUnitofMeasure($get(^INART(0,idItem,1)))
			set $$$INReqLineFromStockLocn(objLine)		= $$$INReqFromLocn(objHeader)
			set $$$INReqLineStatus(objLine)				= 1
			set $$$INReqLineToSOH(objLine)				= ##class(alSOH.iSOH).GetQtyAvailable(idItem, $$$INReqToLocn(objHeader),,$$$YES)
			set $$$INReqLineFromSOH(objLine)			= ##class(alSOH.iSOH).GetQtyAvailable(idItem, $$$INReqFromLocn(objHeader),,$$$YES)
			set $$$INReqLineMonthlyConsumption(objLine) = $$GetLocationConsumption^INARTConsumption(idItem,$$$INReqToLocn(objHeader))
			
			$$$GRIDAddLine(objLine,,idClass,,,,,,.idRowAdded)
		//	do SimulateBlurOnAdd^COMGridEdit31Scan(idRowAdded,objLine,"",YFORM,$$$FldINReqLineItem)
		}
	}
	quit $$$OK
	
]]></Routine>
</Export>