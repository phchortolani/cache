<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMTableTypeTotals" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMTableTypeTotals
 
#include COMSYS
 
	;-------------------------------------------------------------------------------
	; Display table with currency amounts, including line, row and sum totals
	;
	; Params:	pobjTable		Values to be displayed
	; 			plstCols		Columns names
	; 			pstrText		Caption to be displayed above table
	; 			pintSize		Font size of text in table
	; 			pintCaptionSize	Font size of caption
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 22-Mar-2006	PO		SR14407: Created
	;-------------------------------------------------------------------------------
CurrencyTable(pobjTable,plstCols,pstrText,pintSize=2,pintCaptionSize=2)
	new objLocation,idLocation,fltTotal,fltSumTotal,fltAmount,idMonth,blnDisplayValue,blnDisplayValue2
 
	do Start^COMTable(plstCols,pstrText,,,,,pintSize,pintCaptionSize)
	set idLocation = ""
	for {
		set idLocation = $order(pobjTable(idLocation))
		quit:idLocation=""
 
		do NewLine^COMTable()
		do InsertCell^COMTable(idLocation,,,,"right",,,1)
		do InsertCell^COMTable($get(pobjTable(idLocation)),,,,"center",,,pintSize)
		set fltTotal = 0
		set blnDisplayValue = $$$NO
		for idMonth = 0:1:12 { ; TODO: Step through idMonth instead of hardcoded
			set fltAmount = $get(pobjTable(idLocation,idMonth))
			if fltAmount '= "" {
				set fltTotal = $increment(fltTotal,fltAmount)
				do InsertCurrencyCell^COMTable(fltAmount,,,,,,,pintSize)
				set blnDisplayValue = $$$YES
			} else {
				do InsertCurrencyCell^COMTable("",1,,,,,,pintSize)
			}
		}
		if blnDisplayValue {
			do InsertCurrencyCell^COMTable(fltTotal,,,,,,,pintSize)
		} else {
			do InsertCurrencyCell^COMTable("",1,,,,,,pintSize)
		}
		do EndLine^COMTable()
	}
	do NewLine^COMTable()
	do InsertCell^COMTable($$$Text(350),,,,,,,pintSize)
	do InsertCell^COMTable("",,,,,,,pintSize)
	set fltSumTotal = 0
	set blnDisplayValue = $$$NO
	for idMonth = 0:1:12 { ; TODO: Step through idMonth instead of hardcoded
		set idLocation = ""
		set fltTotal = 0
		set blnDisplayValue2 = $$$NO
		for {
			set idLocation = $order(pobjTable(idLocation))
			quit:idLocation=""
 
			if $get(pobjTable(idLocation,idMonth)) '= "" {
				set fltTotal = fltTotal + $get(pobjTable(idLocation,idMonth))
				set blnDisplayValue2 = $$$YES
			}
		}
		if blnDisplayValue2 {
			set fltSumTotal = fltSumTotal + fltTotal
			do InsertCurrencyCell^COMTable(fltTotal,,,,,,,pintSize)
			set blnDisplayValue = $$$YES
		} else {
			do InsertCurrencyCell^COMTable("",1,,,,,,pintSize)
		}
	}
	if blnDisplayValue {
		do InsertCurrencyCell^COMTable(fltSumTotal,,,,,,,pintSize)
	} else {
		do InsertCurrencyCell^COMTable("",1,,,,,,pintSize)
	}
	do EndLine^COMTable()
	do Stop^COMTable()
 
	quit
 
	;-------------------------------------------------------------------------------
	; Display table with numeric values, including line, row and sum totals
	;
	; Params:	objTable		Values to be displayed
	; 			plstCols		Columns names
	; 			pstrText		Caption to be displayed above table
	; 			pintSize		Font size of text in table
	; 			pintCaptionSize	Font size of caption
	;
	; ByRefs: 
	;
	; Returns: Nothing
	;
	; History:
	; 22-Mar-2006	PO		SR14407: Created
	;-------------------------------------------------------------------------------
NumericTable(pobjTable,plstCols,pstrText,pintSize=2,pintCaptionSize=2)
	new objLocation,idLocation,fltAmount,fltTotal,fltSumTotal,idMonth,blnDisplayValue,blnDisplayValue2
 
	; Do we want to separate column heading sizes from table caption?
	do Start^COMTable(plstCols,pstrText,,,,,pintSize,pintCaptionSize)
	set idLocation = ""
	for {
		set idLocation = $order(pobjTable(idLocation))
		quit:idLocation=""
 
		do NewLine^COMTable()
		do InsertCell^COMTable(idLocation,,,,"right",,,pintSize)
		do InsertCell^COMTable($get(pobjTable(idLocation)),,,,"center",,,pintSize)
		set fltTotal = 0
		set blnDisplayValue = $$$NO
		for idMonth = 0:1:12 { ; TODO: Step through idMonth instead of hardcoded
			set fltAmount = $get(pobjTable(idLocation,idMonth))
 
			if fltAmount '= "" {
				set fltTotal = $increment(fltTotal,fltAmount)
				set fltAmount = $justify(fltAmount,"",2)
				do InsertCell^COMTable(fltAmount,,,,"right",,,pintSize)
				set blnDisplayValue = $$$YES
			} else {
				do InsertCell^COMTable("",,,,,,,pintSize)
			}
		}
 
		; Display the total of the whole period for current location
		if blnDisplayValue {
			set fltTotal = $justify(fltTotal,"",2)
			do InsertCell^COMTable(fltTotal,,,,"right",,,pintSize)
		} else {
			do InsertCell^COMTable("",,,,,,,pintSize)
		}
		do EndLine^COMTable()
	}
 
	; Display the total across all location for each part of the whole period
	do NewLine^COMTable()
	do InsertCell^COMTable($$$Text(350),,,,,,,pintSize)
	do InsertCell^COMTable("",,,,,,,pintSize)
	set fltSumTotal = 0
	set blnDisplayValue = $$$NO
	for idMonth = 0:1:12 { ; TODO: Step through idMonth instead of hardcoded
		set idLocation = ""
		set fltTotal = 0
		set blnDisplayValue2 = $$$NO
		for {
			set idLocation = $order(pobjTable(idLocation))
			quit:idLocation=""
 
			if $get(pobjTable(idLocation,idMonth)) '= "" {
				set fltTotal = fltTotal + $get(pobjTable(idLocation,idMonth))
				set blnDisplayValue2 = $$$YES
			}
		}
		if blnDisplayValue2 {
			set fltSumTotal = fltSumTotal + fltTotal
			set fltTotal = $justify(fltTotal,"",2)
			do InsertCell^COMTable(fltTotal,,,,"right",,,pintSize)
			set blnDisplayValue = $$$YES
		} else {
			do InsertCell^COMTable("",,,,,,,pintSize)
		}
	}
 
	; Display the total across all locations for whole period
	if blnDisplayValue {
		set fltSumTotal = $justify(fltSumTotal,"",2)
		do InsertCell^COMTable(fltSumTotal,,,,"right",,,pintSize)
	} else {
		do InsertCell^COMTable("",,,,,,,pintSize)
	}
	do EndLine^COMTable()
	do Stop^COMTable()
 
	quit
 
	;-------------------------------------------------------------------------------
	; Display table of percentages, including line, row and sum totals
	;
	; NOTE: Not using InsertCellPercentage as don't want to display "0 %"
	;
	; Params:	pobjNumerator	Numerator values for division
	; 			pobjDenominator	Denominator values for division
	; 			plstCols		Columns names
	; 			pstrText		Caption to be displayed above table
	; 			pintSize		Font size of text in table
	; 			pintCaptionSize	Font size of caption
	;
	; ByRefs: pobjNumerator, pobjDenominator
	;
	; Returns: Nothing
	;
	; History:
	; 23-Mar-2006	PO		SR14407: Created
	;-------------------------------------------------------------------------------
PercentageTable(pobjNumerator,pobjDenominator,plstCols,pstrText,pintSize=2,pintCaptionSize=2)
	new objLocation,idLocation,fltAmount,fltTotal,fltSumTotal,idMonth,blnDisplayValue,blnDisplayValue2
 
	; Do we want to separate column heading sizes from table caption?
	do Start^COMTable(plstCols,pstrText,,,,,pintSize,pintCaptionSize)
	set idLocation = ""
	for {
		set idLocation = $order(pobjNumerator(idLocation))
		quit:idLocation=""
 
		do NewLine^COMTable()
		do InsertCell^COMTable(idLocation,,,,"right",,,pintSize)
		do InsertCell^COMTable($get(pobjNumerator(idLocation)),,,,"center",,,pintSize)
 
		set fltTotal("N") = 0
		set fltTotal("D") = 0
		set blnDisplayValue = $$$NO
		for idMonth = 0:1:12 { ; TODO: Step through idMonth instead of hardcoded
			set fltAmount("N") = $get(pobjNumerator(idLocation,idMonth))
			set fltAmount("D") = $get(pobjDenominator(idLocation,idMonth))
 
			set fltTotal("N") = $increment(fltTotal("N"),fltAmount("N"))
			set fltTotal("D") = $increment(fltTotal("D"),fltAmount("D"))
 
			if +fltAmount("D") '= 0 {
				set fltAmount = ((fltAmount("N")/fltAmount("D"))-1)*100
			} else {
				set fltAmount = 0
			}
 
			if fltAmount("N")="" {
				do InsertCell^COMTable("",,,,,,,pintSize)
			} else {
				set fltAmount = $justify(fltAmount,"",2)_" %"
				do InsertCell^COMTable(fltAmount,,,,"right",,,pintSize)
				set blnDisplayValue = $$$YES
			}
		}
 
		; Display the total of the whole period for current location
		if blnDisplayValue {
			if +fltTotal("D") '= 0 {
				set fltTotal = ((fltTotal("N")/fltTotal("D"))-1)*100
			} else {
				set fltTotal = 0
			}
			set fltTotal = $justify(fltTotal,"",2)_" %"
			do InsertCell^COMTable(fltTotal,,,,"right",,,pintSize)
		} else {
			do InsertCell^COMTable("",,,,,,,pintSize)
		}
		do EndLine^COMTable()
	}
 
	; Display the total across all location for each part of the whole period
	do NewLine^COMTable()
	do InsertCell^COMTable($$$Text(350),,,,,,,pintSize)
	do InsertCell^COMTable("",,,,,,,pintSize)
 
	set fltSumTotal("N") = 0
	set fltSumTotal("D") = 0
	set blnDisplayValue = $$$NO
	for idMonth = 0:1:12 { ; TODO: Step through idMonth instead of hardcoded
		set idLocation = ""
 
		set fltTotal("N") = 0
		set fltTotal("D") = 0
		set blnDisplayValue2 = $$$NO
		for {
			set idLocation = $order(pobjNumerator(idLocation))
			quit:idLocation=""
 
			if $get(pobjNumerator(idLocation,idMonth)) '= "" {
				set fltTotal("N") = fltTotal("N") + $get(pobjNumerator(idLocation,idMonth))
				set fltTotal("D") = fltTotal("D") + $get(pobjDenominator(idLocation,idMonth))
				set blnDisplayValue2 = $$$YES
			}
		}
		if blnDisplayValue2 {
			if +fltTotal("D") '= 0 {
				set fltTotal = ((fltTotal("N")/fltTotal("D"))-1)*100
			} else {
				set fltTotal = 0
			}
 
			set fltSumTotal("N") = fltSumTotal("N") + fltTotal("N")
			set fltSumTotal("D") = fltSumTotal("D") + fltTotal("D")
			set fltTotal = $justify(fltTotal,"",2)_" %"
			do InsertCell^COMTable(fltTotal,,,,"right",,,pintSize)
			set blnDisplayValue = $$$YES
		} else {
			do InsertCell^COMTable("",,,,,,,pintSize)
		}
	}
 
	; Display the total across all locations for whole period
	if blnDisplayValue {
		if +fltSumTotal("D") '= 0 {
			set fltSumTotal = ((fltSumTotal("N")/fltSumTotal("D"))-1)*100
		} else {
			set fltSumTotal = 0
		}
		set fltSumTotal = $justify(fltSumTotal,"",2)_" %"
		do InsertCell^COMTable(fltSumTotal,,,,"right",,,pintSize)
	} else {
		do InsertCell^COMTable("",,,,,,,pintSize)
	}
	do EndLine^COMTable()
	do Stop^COMTable()
 
	quit
]]></Routine>
</Export>