<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INFIBPAR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INFIBPAR
	
#include COMSYS
#include INConst
	
OnBeforeSave(pidKey,pYFELD)
	;-------------------------------------------------------------------------------
	; Update CostCentre:Location list (^INCCLocation) Before Save of INFIBPAR form
	;
	; Params:
	;	pidKEY		YM,Locn
	;	pYFELD		objINFIBPAR
	;	
	; Returns: none
	;
	; History:
	; 23-Nov-2009	GRF		SR16871: Prior validation may have blocked save
	; 23-Oct-2009	GRF		SR16871: Created
	;-------------------------------------------------------------------------------
	new idLocn,idParent,objLocation,strStatus
	
	set strStatus = $$$OK
	set idLocn    = $$$KEY2(pidKey)
	
	;set strStatus = $$OnBeforeSave^WWW0121Rules(idCompany,idLocn,pYFELD,pYFORM)
	set objLocation = $get(^WWW0121(0,0,idLocn,1))
	set idParent    = $$$WWW0121ParentLocn(objLocation)
	if (idParent="") {
		if ($$$INFIBPARCostCentre(pYFELD)="") set strStatus = $$$MakeStatus("IN01144")
	;     "A Cost Centre must be specified for a top-level location"
	
	} elseif $$GetCostCentre^INCostCentre(idLocn)="" {
		set strStatus = $$$MakeStatus("IN01144")
		; "Assign a Cost Centre to the top-level Parent Location first"
	}
	
	if $$$ISERR(strStatus) {
		$$$DecomposeStatus(strStatus)
		$$$Alert(strStatus)
		set Q = $$$QDontSave
		
	; Prior validation may have blocked save
	} elseif $get(Q)'=$$$QDontSave {
		do OnSaveLocation^INCostCentre(idLocn,$$$INFIBPARCostCentre(pYFELD))
	}
	quit
	
	
OnBeforeDelete(pidKey,pYFELD)
	;-------------------------------------------------------------------------------
	; Update CostCentre:Location list (^INCCLocation) Before Deletion of INFIBPAR form
	;
	; Params:
	;	pidKEY		YM,Locn
	;	pYFELD		objINFIBPAR
	;	
	; Returns: none
	;
	; History:
	; 23-Oct-2009	GRF		SR16871: Created
	;-------------------------------------------------------------------------------
	new idLocn
	
	set idLocn = $$$KEY2(pidKey)
	do OnDeleteLocation^INCostCentre(idLocn)
	quit
	
	
IsCCReadOnly(pidFinParam)
	;-------------------------------------------------------------------------------
	; A Cost Centre can only be specified where Location Type on WWW0121 is
	; 0 "Entity" or 1 "Location".  It is inherited where Type is 2 "Department"
	; 
 	; History:
	; 23-Oct-2009	GRF		SR16871: Created
	;-------------------------------------------------------------------------------
	new blnReadOnly,idLocn,objLocation
	
	set idLocn = $$$KEY2(pidFinParam)
	if idLocn="" quit $$$YES
	
	set blnReadOnly = $$$NO
	set objLocation = $get(^WWW0121(0,0,idLocn,1))
	if ($$$WWW0121LocationType(objLocation)=2)  ||
	   ($$$WWW0121LocationType(objLocation)="")    {
	
		set blnReadOnly = $$$YES
	}
	quit blnReadOnly
	
]]></Routine>
</Export>