<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMB" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMB
	
#include WWWFORM
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		FORMBUTTONS BUTTON NEBEN FELD - Form buttons beside field
	;		Builds the HTML for the button - actual execution on clicking the button
	;		occurs elsewhere.
	; 
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 13-Nov-2007	shobby	SRBR014771: Wrapper the EXEC call in <TD> otherwise
	; 							multiple buttons against a field will appear on
	; 							different lines.  (INWEINVD)
	; 04-Jul-2007	RPW		SRBR014409: Now don't forget at the end that YLFN
	; 							MUST be set back to idYLFN.
	; 25-Jun-2007	shobby	BR014409:Remember what YLFN was before this routine
	; 							is called.  It is used in the ID of the images.
	; 11-Jun-2007	shobby	BR014409:Don't use YLFN as the loop for buttons.
	; 							We need that to preserve the id of the associated
	; 							 field (set somewhere up the stack)
	; 07-May-2007	GRF		SR15511: Doco
	; 16-Dec-2005	JW		SR13195: Rewrote in brace syntax / functions
	; 15-Aug-2005	Steve S	Doco
	; 28-Jul-2005	RPW		SR11983: Check for YOPTION="" when piece 50 of the button def is not ""
	; 30-May-2005	RPW		SR12056: Attempt at Performance Increase
	; 24-May-2005	Paul K	Problem with the use of variable "Q" instead of "YQ"
	; 							causing multiple buttons showing. SR:12351
	; 21.08.1998	DT
	;-------------------------------------------------------------------------------
	QUIT:YBUTT=""
	NEW YPRINT,YQ,YTARGET,blnButtons,newForm,strPic,idType,oldYBACK,idYLFN
	
	set blnButtons = $$$WWW120PicturesAsButtons($get(YVOR))
	set idYLFN     = $get(YLFN)
	
	;IF YBEDBER=1 WRITE YCR,YCR,"<!-- ************************* EXTRA BUTTON (WWWFORMB) ************************* -->",YCR,YCR
	
	SET YBUTT=$TRANSLATE(YBUTT,";",",")
	
	;---------------------------------------
	; YA   = Button Record
	; YVOR = Form Definition
	;---------------------------------------
	
	set oldYBACK = YBACK
	
	for YI=1:1 {
		quit:$PIECE(YBUTT,",",YI)=""
		
		set YLFN = $piece(YBUTT,",",YI)	;NÄCHSTER BUTTEN
		set YA   = $get(^WWW124(0,YFORM,SPRACHE,YLFN,1))
		continue:YA=""
		
 		set YBACK = oldYBACK
		if $EXTRACT($$$WWW124TargetFrameName(YA))="@" set YBACK=""   ;TYBD;OHNE YBACK ;without 
		
		set strPic = $$$WWW124PictureFileOnButton(YA)
		if blnButtons && (strPic="") {
			set strPic = $zconvert($extract($$^WWWUMLAU($$$WWW124ButtonDescription(YA),3)),"L")
			if '$find("abcdefghijklmnopqrstuvwxyzöäüß@?1234567890",strPic) SET strPic="x"
			set strPic = strPic_".gif"  ;KEIN BUTTON ABER BUTTON ;no yet 
		}
		
		set newForm = $$$WWW124NewFormOnClick(YA)
		continue:$$Disabled(newForm,blnButtons,strPic,1,"","",idYLFN)  ;BR014409
		continue:$$^WWWACCESS($$$WWW124Authorization1(YA),$$$WWW124Module1(YA))'=1  ;KEIN ZUGANG ;no
		continue:$$DontShow()
				
		if $$$WWW124AutomaticSUBMIT(YA)         &&
		   (newForm'="")                        &&
		   ($$$WWW124HyperEventOnClick(YA)'="") &&
		   ($$$WWW124ExecuteOnClick(YA)="")        {
			   
			set $$$WWW124ExecuteOnClick(YA)="SUBMIT"   ;AUTOSUBMIT
		}
		set YPRINT=""
		if $$$WWW124OutputWithWindowprint(YA)=$$$YES set YPRINT=$$$YES  ;ZIEL DRUCKEN ;target print
		
		set YTARGETF = $$$WWW124TargetFrameName(YA)
		if $extract(YTARGETF)="@" set YTARGETF = @$extract(YTARGETF,2,99)  ;FIS;26.09.03;ZIELTARGET AUS PARAMETER ;out of parameter 
		set YTARGET = YTARGETF
		if $get(YVOR1)'=""        set YTARGET = $piece($$$WWW012TargetFrameName(YVOR1),"/",1)_YUSER
		
		set YCONF1=""  ;VOR URL ;pre- URL 
		set YCONF2=""
		if $$$WWW124AcknowledgementTextBefore(YA)'="" {   ; FIXME : Internationalise
			set YCONF1 = "if (confirm('"_$TRANSLATE($$$WWW124AcknowledgementTextBefore(YA),"'""","´´")_"')) {"  ;FIS;31.01.05
			set YCONF2 = "}"
		}
		
		;-----------------------------------
		; *** EXECUTE # ***
		;-----------------------------------
		
		if $$$WWW124SearchFunctionOnClick(YA)'="" {
			set $$$WWW124DoNotPrepareHTML(YA)=0
			set idType = $case(YART,"M":3,"D":2,"P":1,:"")
			if idType'="" {
				set $$$WWW124ExecuteOnClick(YA) = "D ^WWWSBU("_idType_")"
			}
		}
		
		if $$$WWW124HyperEventOnClick(YA)'="" {
			do HYPER
		
		} elseif $$$WWW124ExecuteForManualLink(YA)'="" {
			do MANU
		
		} elseif $$$WWW124ChangeoversWithOnClick(YA)'="" {
			do SEITE
		
		} else {
			if $$$WWW124QueryOnClick(YA)'="" set $$$WWW124ExecuteOnClick(YA)="QUERY,"_$$$WWW124QueryOnClick(YA)  ;QUERY AUFRUFEN
			
			if newForm'="" {
				do NORM
			
			} elseif ($$$WWW124ExecuteOnClick(YA)'="") || ($$$WWW124FunctionJavaScriptOnClick(YA)'="") {
				write "<TD ALIGN='ABSTOP'>"  	;BR014771
				do EXEC
				write "</TD>"					;BR014771
			
			} elseif $$$WWW124LinkWithexeOrHttp(YA)'="" {
				do VERKN
			}
		}
	}
	
	set YLFN=idYLFN  ;BR014409
	
	QUIT
	
	
Disabled(newForm,blnButtons,strPic,pblnTD=$$$NO,&pstrStartJS,&pstrEndJS,pidYLFN)
    ;-------------------------------------------------------------------------------
    ; Works out if button should be disabled, and if so, displays it as such.
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns: whether button should be disabled
    ;
    ; History:
    ; 17-Aug-2009	shobby	SR16819:	When displaying javascript alerts on disabled buttons don't crash if
    ; 							the text contains a "
    ; 22-Sep-2008	shobby	SRBR014939: Moved DataItemInUse check to not be dependent on whether
    ; 									there is ExecuteToCheckForDisabled code.
    ; 23-Jul-2008	GRF		SRBR014939: use macro & revised property name for WWW124
    ; 30-Apr-2008	shobby	SRBR014939: Need to check YBEARB to find out if the
    ; 							disable was because it is 'In Use'
    ; 29-Apr-2008	shobby	SRBR014939: Disable buttons when Data Item In Use
    ; 25-Jun-2007	shobby	SRBR014409: pidYLFN as a parameter
    ; 25-Jun-2007	shobby	SRBR014409: More changes to the button id.
    ; 11-Jun-2007	shobby	SRBR014409: Changes to button id to guarantee uniqueness
    ; 08-Jun-2007	GRF		SRBR014417: property name changed
	; 03-Jan-2007	JW		SR15240: Revision to OpenTD macro for Disabled buttons
    ; 25-Oct-2006	JW		BR014279: Grid check for selected record
    ; 12-Sep-2006	JW		Convert alert text to JS
    ; 16-Dec-2005	JW		SR13195: Created
    ;-------------------------------------------------------------------------------
	new Q,YQ,blnDisabled,strText,strExec,blnHidden
	
	set blnDisabled = $$$NO
	set blnHidden   = $$$NO
	
	;AUS WWWFORMC ÜBERNOMMEN ;FIS;24.02.03 ;out of
		;	 5 = "Read Only"
	if ($$$WWW120AuthorizationToModifyData($get(YVOR))=$$$EnumReadOnly) && '$$$WWW124AvailWhenDataItemIsInUse(YA) && ($get(YBEARB)=4) {  ;BR014939
		set blnDisabled = $$$YES                                                ;BR014939
		set strText     = $$$SysEnum("BEARBEITUNG",4)   ; "Data Item In Use"    ;BR014939
	} else {
		set strExec=$$$WWW124ExecuteToCheckForDisabled(YA)
		if strExec'="" {   ;PRUEFEN DISABLED
		
		;-----------------------------------
		; *** EXECUTE # ***
		;-----------------------------------
			xecute strExec
			
			if (+$get(Q)=2) || (+$GET(YQ)=2) {
				set blnHidden = $$$YES
				
			} elseif (+$get(Q)=1) || (+$GET(YQ)=1) {
				set strText=""   ;S strText=$$^WWWTEXT(32072)
				if $extract($get(Q),2,999)'=""  set strText = $extract(Q,2,999)
				if $extract($get(YQ),2,999)'="" set strText = $extract(YQ,2,999)
				set blnDisabled = $$$YES
			}
		}
	}
	
	if 'blnHidden {
		if 'blnDisabled {
			;If there needs to be a key and there isn't one, disable.
			;
			;NOTE - TODO - this check should probably be BEFORE the xecute above.
			
			; SRBR014417
			if $$$WWW124OnlyWhenRecordSelected(YA) || ((newForm'="") && (+$$$WWW121FixedInputForHiddenField($GET(^WWW121(0,newForm,1,1)))'=0)) {
				if (YFOART=12) {		// BR014279 - Grid
					set strText     = $$^WWWTEXT("WWW00053")	; "Please select a saved grid record."
					set pstrStartJS = "if "_$$GetDisabledCheck^COMGridEdit31J_" { alert('"_strText_"'); } else { "
					set pstrEndJS   = " }"
					
				} else {				// Not grid
					if blnButtons && $$IsNewRecord^WWWFORMStatus() {		// Don't know why only for buttons...
						set strText     = $$^WWWTEXT(32072)       ; "Please Select A Data Record First."
						set blnDisabled = $$$YES
					}
				}
			}
		}
		
		if blnDisabled {
			//if pblnTD $$$OpenTD			//SR15240
			if pblnTD $$$OpenTD($$$YES)
			if $$$WWW124ImageFileForDisabledButto(YA)'="" {
				set strPic = $$$WWW124ImageFileForDisabledButto(YA)  ;NEUER DISABLED BUTTON
				
			} elseif $extract(strPic,2)="." {
				set $piece(strPic,".",1) = $extract(strPic)_"_dis"		//SR15240
			}
			
			if strPic'="" {
				if $$$WWW012DisplayButtonTooltips($get(^WWW012(0,YM,1))) {		//SR15240
					do StopButton^WWWFORMCOMMON($$$HTMLText(strText),strPic,"Y"_$GET(YFORM)_"D"_$get(pidYLFN)_"_"_$GET(YLFN)_"IMG","","","cursor:default")
				} else {
					if strText'="" write "<A onClick='return doLink(this)' HREF=""JavaScript:"_$$$HTMLText("alert('"_$$$JSText(strText)_"');")_""">"		//SR14979 ;16819
					do StopButton^WWWFORMCOMMON($$^WWWUML($$$WWW124ButtonDescription(YA)),strPic,"Y"_$GET(YFORM)_"D"_$get(pidYLFN)_"_"_$GET(YLFN)_"IMG","","","cursor:default")
					if strText'="" write "</A>"
				}
			}
			write YCR
			if pblnTD $$$CloseTD
		}
	}
	quit (blnDisabled || blnHidden)
	
	
DontShow()
    ;/*-------------------------------------------------------------------------------
    ; Works out whether a button should not be displayed
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 20-Dec-2005	JW		SR13195: Created
    ;-------------------------------------------------------------------------------*/
	new blnDontShow,strOnlyShow
	
	set blnDontShow = $$$NO
	
	if $$$WWW124DisplayOnlyWhenMenuParam(YA)'="" && '$FIND(","_YPARA_",",","_$$$WWW124DisplayOnlyWhenMenuParam(YA)_",") {  ;NICHT ANZEIGE WEIL PARAMETER FALSCH ;Not Show since parameter wrong 
		set blnDontShow = $$$YES
		
	} else {
		set strOnlyShow = $$$WWW124ShowOnlyWhenYOPTION(YA)
		if (strOnlyShow'="") && (YOPTION="" || '$FIND(","_strOnlyShow_",",","_YOPTION_",")) {
			set blnDontShow = $$$YES
		}
	}
	quit blnDontShow
	
	
NORM ;NORMAL MIT FORMULAR ;normal by means of form 
	;-------------------------------------------------------------------------------
	; 		call form 
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 23-Oct-2008	FIS		SR15962: removed quote masking again (not ok anymore
	; 							since change in SAVENOW() was made)
	; 25-Sep-2008	FIS		SR15962: allow multiple mapping values (same as WWWFORMC)
	; 25-Sep-2008	FIS		SR15964: mask quotes to stay as text string in SAVENOW()
	;-------------------------------------------------------------------------------
	new MAPPING,strExec,strKEY,idX,strMAP,blnNewWindow
	
	set MAPPING = $$$WWW124DataItemMapToNewFormNnnn(YA)
	
	set blnNewWindow = ((YTARGETF'="") && (YTARGETF'=YTARGET))
	
	if 'blnNewWindow {  ;NEUES FORMULAR ;something new form 
		if MAPPING'="" set MAPPING="|"_$$^WWWTRANSLATE(MAPPING,"""","&quot;")  ;FIS;13.01.04;ANFÜHRUNGSZEICHEN ÜBERGEBEN
		
		set strExec = "document.WWW.YBACK.value='"_YBACK_YFORM_",'; "
		set strExec = strExec_"document.WWW.target='"_$GET(YTARGETF)_"';"
		if ($zconvert($$$WWW124ExecuteOnClick(YA),"U")="SUBMIT") || $$$WWW124AutomaticSUBMIT(YA) {
			set strExec = strExec_"retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','','9');"
		}
		
		set strExec = strExec_"document.WWW.YSEITE.value=1;"
		set strExec = strExec_"SAVENOW('SAVEFORM"_$$$WWW124NewFormOnClick(YA)_MAPPING_"');"
	;	set strExec = strExec_"SAVENOW('\'SAVEFORM"_$$$WWW124NewFormOnClick(YA)_MAPPING_"\'');"  ;SR15964 ;SR15962 ;not required anymore since change in SAVENOW() was made
		
		
	} else {  ;FORMULAR IN SEPARATEM FENSTER;FIS;16.04.04;25534
		set strExec = "WWWFORM&amp;YFORM="_$PIECE(YA,Y,7)
		if MAPPING'="" {
			SET strKEY=""
			
			/*IF $EXTRACT($PIECE(MAPPING,"=",1))="""" {
				SET $PIECE(strKEY,",",+$PIECE(MAPPING,"=",2))=$TRANSLATE($PIECE(MAPPING,"=",1),"""")  ;FESTER WERT ;worthy 
			} else {
				IF $EXTRACT(MAPPING)="P" {
					SET strKEY="' + document."_YHTMFORM_".Y"_YFORM_"P"_+$EXTRACT($PIECE(MAPPING,"=",1),2,99)_".value + '"  ;AUS PRIMÄRSCHLÜSSEL;FIS;27.09.04;26416
				} else {
					SET strKEY="' + document."_YHTMFORM_".Y"_YFORM_"D"_+$PIECE(MAPPING,"=",1)_".value + '"  ;AUS DATENSATZ ;out of data record 
				}
			}*/
			for idX=1:1 {  //allow multiple values (same as in WWWFORMC) ; vvv SR15962
				set strMAP=$piece(MAPPING,";",idX)
				quit:strMAP=""
				
				set:idX>1 strKEY = strKEY_","
				if $EXTRACT($PIECE(strMAP,"=",1))="""" {
					SET $PIECE(strKEY,",",+$PIECE(strMAP,"=",2))=$TRANSLATE($PIECE(strMAP,"=",1),"""")  ;fix value
				} else {
					IF $EXTRACT(strMAP)="P" {
						SET strKEY="' + document."_YHTMFORM_".Y"_YFORM_"P"_+$EXTRACT($PIECE(strMAP,"=",1),2,99)_".value + '"  ;primary key
					} else {
						SET strKEY="' + document."_YHTMFORM_".Y"_YFORM_"D"_+$PIECE(strMAP,"=",1)_".value + '"  ;data field
					}
				}
			}                                                            ; ^^^ SR15962
			
			set strExec = strExec_"&YKEY="_strKEY
		}
	}
	do Button(strExec,blnNewWindow)
	
	QUIT
	
SEITE   ;SEITENWECHSEL 
	new strExec
	
	set strExec = "document.WWW.target=''; document.WWW.YINSEITE.value=''; document.WWW.YSEITE.value='"_$$$WWW124ChangeoversWithOnClick(YA)_"'; SAVENOW(2);"
	do Button(strExec)
	
	QUIT
	
HYPER
	;-------------------------------------------------------------------------------
	; 		HYPER EVENT BEI ONCLICK ;next to 
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 23-Aug-2007	Frank	SRBR014566: Replace EventCall for CallBackNow, and remove
	; 						the property 'Set Variable YPARA' as the first parameter.
	;-------------------------------------------------------------------------------
	new strExec,strHyper
	
	set strHyper = $$$WWW124HyperEventOnClick(YA)
	
	; SRBR014566 vvvvvvv
	 set strExec = "	retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','"_$piece(strHyper,"(",1)_"','"_$$$WWW124SetVariableYPARA(YA)_"','6','"_$piece($piece(strHyper,"(",2),")",1)_"');"
	/*set strParams    = $piece($piece(strHyper,"(",2),")",1)
	set strRoutine   = $piece(strHyper,"(",1)
	
	set strParam1 = $piece(strParams, ",", 1)
	
	set strParams = $piece(strParams, ",", 2)
	set strParam2 = $piece(strParams, ",", 1)
	
	set strParams = $piece(strParams, ",", 2)
	set strParam3 = $piece(strParams, ",", 1)
	
	set strParams = $piece(strParams, ",", 2)
	set strParam4 = $piece(strParams, ",", 1)
	
	set strExec = "   CallBackNow('"_strRoutine_"','"_strParam1_"','"_strParam2_"','"_strParam3_"','"_strParam4_"');"
	; SRBR014566 ^^^^^^^
	*/ 
	
	do Button(strExec)
	
	QUIT
	
EXEC
    ;-------------------------------------------------------------------------------
    ; 		WENN MIT EXECUTERUECKSPRUNG ;when by means of 
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
	; 25-Jun-2007	shobby	BR014490: Reverted previous change.
	; 11-Jun-2007	shobby	BR014409: Don't use YLFN as the loop for buttons.  We
	; 						need that to preserve the id of the associated field
	; 						(set somewhere up the stack)
    ;-------------------------------------------------------------------------------
	new blnNewWindow,preExec,strExec,YHTML
	
	set blnNewWindow = $$$NO
	
	if $zconvert($$$WWW124ExecuteOnClick(YA),"U")="SUBMIT" {    ;SUBMIT BEI EXECUTE ;next to EXECUTE 
		set strExec = "SAVENOW();"
		
	} elseif $$$WWW124FunctionJavaScriptOnClick(YA)'="" {       ;FUNCTION BEI ONCLICK ;next to 
		set strExec = $$$WWW124FunctionJavaScriptOnClick(YA)_";"
	
	} else {
		if (YTARGETF="") || (YTARGETF=YTARGET) {                ; MANUELLE ROUTINE
			set YHTML = "EXEC"                                  ; MIT HTML ;by means of HTML 
			if $$$WWW124DoNotPrepareHTML(YA) SET YHTML = "MANU" ; OHNE HTML ;without HTML 
			
			set strExec = "document.WWW.YNUMMER.value='"_$GET(YNUMMER)_"'; "_
			              "document.WWW.YBUTTON.value='"_YLFN_"'; "_
			              "document.WWW.YSEITE.value='"_YSEITE_"'; "_
			              "SAVENOW('SAVE"_YHTML_$$$WWW124ExecuteOnClick(YA)_"');"		
							
		} else {  ; FORM IN SEPARATE WINDOW ;FIS;16.04.04;25534
			set blnNewWindow = $$$YES
			if '$$$WWW124DoNotPrepareHTML(YA) {
				set strExec = "WWWMANU&amp;YFORM="_YFORM_"&amp;YEXEC="_$$$WWW124ExecuteOnClick(YA)
			} else {
				set strExec = "WWWMANU1&amp;YEXEC="_$$$WWW124ExecuteOnClick(YA)
			}
		}
	}
	
	if 'blnNewWindow {
		set preExec = ""
		if $$$WWW124AutomaticSUBMIT(YA) set preExec = "retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','','9');"
		set strExec = preExec_"document.WWW.target='"_$GET(YTARGETF)_"';"_strExec
	}
	
	do Button(strExec,blnNewWindow)	// Display button
	
	quit
	
VERKN 
    ;-------------------------------------------------------------------------------
    ; 		VERKNÜPFUNG MIT BILDANZEIGE ;by means of
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jun-2007	shobby	BR014490: Adjusted id of image
	; 11-Jun-2007	shobby	BR014409: Don't use YLFN as the loop for buttons.  We
	; 						need that to preserve the id of the associated field
	; 						(set somewhere up the stack)
    ;-------------------------------------------------------------------------------
	new strLink
	
	if 'blnButtons {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""""
	} else {
		write "<A class=link "
	}
	
	$$$OpenOnClick
	$$$StartWindow
	
	set strLink = $$$WWW124LinkWithexeOrHttp(YA)
	
	if '$find(strLink," ^") {   ;NORMALE VERKNÜFUNG
		if '$find(strLink,":") write YGIF
		write strLink
		
	} else {
		xecute strLink  ;HTTP: ODER MAILTO AUS EXECUTE ;Or out of EXECUTE 
	}
	
	$$$EndWindow
	$$$CloseOnClick
	
	write ">"
	
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$$WWW124ButtonDescription(YA),strPic,"Y"_$get(YFORM)_"D"_$get(idYLFN)_"_"_$get(YLFN)_"IMG")
		write "</A>"
	}
	quit
	
	
MANU
    ;-------------------------------------------------------------------------------
    ; 		MANUELLER LINK DURCH EXECUTE ;trans- EXECUTE 
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jun-2007	shobby	BR014409: Adjusted id of image
	; 11-Jun-2007	shobby	BR014409: Don't use YLFN as the loop for buttons.  We
	; 						need that to preserve the id of the associated field
	; 						(set somewhere up the stack)
    ;-------------------------------------------------------------------------------
	new LINK,EXEC,strLink
	
	set LINK=0
	set strLink = $$$WWW124ExecuteForManualLink(YA)
	
	if $EXTRACT(strLink,1,2)="$$" {
		set EXEC = "SET LINK="_strLink
		XECUTE EXEC
		
	} elseif $EXTRACT(strLink,1)="@" {
		set LINK = @$EXTRACT(strLink,2,99)
	}
	if 'blnButtons {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$$WWW124ButtonDescription(YA))_""" onClick="""
	} else {
		write "<A onClick='return doLink(this)' HREF="""
	}
	if LINK'=0 {
		write LINK
	} else {
		XECUTE strLink
	}
	write """"
	if YTARGETF'="" write " TARGET="""_YTARGETF_""""
	write ">"
	if blnButtons {
		do StopButton^WWWFORMCOMMON($$$WWW124ButtonDescription(YA),strPic,"Y"_$GET(YFORM)_"D"_$get(idYLFN)_"_"_$GET(YLFN)_"IMG")
		write "</A>"
	}
	QUIT
	
Button(pstrExec,pblnNewWindow=$$$NO) 
    ;-------------------------------------------------------------------------------
    ; Generic button display
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 02-Dec-2008	FIS		SR16205: bugfix
    ; 28-Jun-2007	shobby	BR014409: YCONF1 may already have something in in so preserve that
    ; 11-Jun-2007	shobby	BR014409: Give the <A> object an id so that we can
    ; 						determine later if the button is read only.  Make the action
    ; 						dependent on whether this object has been set to read only.
    ; 25-Oct-2006	JW		BR014279: Use Macros.
    ; 16-Dec-2005	JW		SR13195: Created
    ;-------------------------------------------------------------------------------
	new strDesc
	
	set strDesc = $$^WWWUML($$$WWW124ButtonDescription(YA))
	
	if 'blnButtons {
		write "<INPUT TYPE=""BUTTON"" "
		if $$$WWW120StylesheetCSSFile(YVOR)'="" write " class=""button"" "
	;	write "VALUE="""_YAM_strDesc_""      ; SR16205
		write "VALUE="""_YAM_strDesc_""""
	} else {
		write "<A class=link id=""Y"_$get(YFORM)_$get(YART)_$get(idYLFN)_"_"_$get(YLFN)_""" "
	}
 
	//write " onClick="""_YCONF1		BR014279
	set YCONF1=$get(YCONF1)_"if (this.readOnly!=true) {"  ;BR014409
	$$$OpenOnClick
	
	if 'pblnNewWindow {
		if $$$WWW124SetVariableYPARA(YA)'="" write "document.WWW.YPARA.value='"_$$$WWW124SetVariableYPARA(YA)_"'; "  ;PARAMETER NEU
		write pstrExec
		
	} else {
		write " var parameter=window.open('"_YAKTION_"EP="_strExec
		if $$$WWW124SetVariableYPARA(YA)'="" write "&amp;YPARA="_$$$WWW124SetVariableYPARA(YA)  ;PARAMETER NEU ;parameter recent 
		write "&amp;YLOCKBACK="_YTRAKT  ;AKTUELLE LOCKDATEI BEI SCHLIESSEN ZURÜCKHOLEN;FIS;25534;20.04.04
		new YKEY,YFKEY,YBACK
		do ^WWWCGI
		write "','"_YTARGETF_"','resizable=yes, scrollbars=yes');"
	}
	
	//write YCONF2				BR014279
	write "}" ;BR014409
	//write """>"
	$$$CloseOnClick
	write ">"
	if blnButtons {
		do StopButton^WWWFORMCOMMON(strDesc,strPic,"Y"_$get(YFORM)_"D"_$get(idYLFN)_"_"_$get(YLFN)_"IMG")
		write "</A>"
	}
	quit
	
]]></Routine>
</Export>