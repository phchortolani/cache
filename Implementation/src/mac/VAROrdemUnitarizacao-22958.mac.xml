<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VAROrdemUnitarizacao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VAROrdemUnitarizacao
	#include COMSYS
	#include VARConst
	#include INConst
	#define CAMPOMARCA 39

	;Status Cabeçalho
	#define OrdemConcluida 4
	#define EncerradaManualmente 5
	
	;Status Linha
	#define EmAndamento 0
	#define Concluido 1
	quit

CanDelete(pYKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Valida se o usuário pode deletar a ordem.
	;
    ; Histórico:
    ;   13-Out-2011	 Rubens		Created
    ;-------------------------------------------------------------------------------
	new objUnitarizacao
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,pYKEY,1))
	
	if ($$$VAROrdemUnitarizacaoStatus(objUnitarizacao) = $$$OrdemConcluida) {
		quit 0_"Não é possivel excluir a ordem de fracionamento, a ordem já foi concluída."
	} elseif ($$$VAROrdemUnitarizacaoStatus(objUnitarizacao) = $$$EncerradaManualmente) {
		quit 0_"Não é possível excluir a ordem de fracionamento, a ordem foi encerrada manualmente."
	}

	quit $$$YES

	
OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Validações antes de salvar a ordem/linhas da ordem.
	;
    ; Histórico:
    ;   03-Jan-2013	 Gustavo	Adaptado para nova estrutura de etapas (grid).
    ;-------------------------------------------------------------------------------
	new objUnitarizacao, fltQuantidade, blnConfFisicaConcluido, blnConfBulaConcluido,	
		qtyTotalTodasEtapas, date, gridLine, qtyEtapa, qtyPerda, qtyTotalEtapa,
		statusUnitarizacao, linhaSemQuantidade, idEtapa
	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
	set fltQuantidade = $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao)
	
	set qtyTotalTodasEtapas = 0
	set date = $piece($horolog,",",1)	
	
	set linhaSemQuantidade = $$$NO
	
	set gridLine = ""
	for {
		set gridLine = $order(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEROW",gridLine))
		quit:(gridLine = "")
		
		;Desconsidera linhas que foram deletadas
		continue:( ($get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","KILLEDRECORD",gridLine)) = 1) )
		
		set idEtapa = $get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEROW",gridLine,1))

		set qtyEtapa = $get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEVALUE","Y"_idEtapa_"_2",1))
		set qtyPerda = $get(^WWWDATEN(YM,date,YUSER,"VAROrdemUnitarizacaoEtapa","V","REFERENCEVALUE","Y"_idEtapa_"_3",1))
		set qtyTotalEtapa = qtyEtapa + qtyPerda		
				
		if (qtyTotalEtapa = 0) {
			set linhaSemQuantidade = $$$YES
			quit
		}
		
		set qtyTotalTodasEtapas = qtyTotalTodasEtapas + qtyTotalEtapa
	}
	
	if (linhaSemQuantidade = $$$YES) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError("Existe(m) uma ou mais linhas sem quantidade de sel./etiq. e sem quantidade de perda (Linha "_idEtapa_"). Favor verificar.")
		quit
	}
	
	if (qtyTotalTodasEtapas > fltQuantidade) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError("A quantidade nas etapas ("_qtyTotalTodasEtapas_") não pode ser maior que a quantidade a ser fracionada ("_fltQuantidade_"). Favor verificar.")
		quit		
	}

	// Atualiza o status da ordem de fracionamento
	set statusUnitarizacao = $$$VAROrdemUnitarizacaoStatus(pYFELD)
	
	;Aba Conferência
	set blnConfFisicaConcluido = $$$VAROrdemUnitarizacaoConfFisicaConcluido(pYFELD)
	set blnConfBulaConcluido   = $$$VAROrdemUnitarizacaoConfBulaConcluido(pYFELD)	

	;0-Em Aberto, 1-Em Conferência
	if ( (statusUnitarizacao = 0) || (statusUnitarizacao = 1) ) {
	
		if ( (blnConfFisicaConcluido '= $$$YES) && (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 0
			
		} elseif ( (blnConfFisicaConcluido = $$$YES) && (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 1
			
		} elseif ( (blnConfFisicaConcluido '= $$$YES) && (blnConfBulaConcluido = $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 1
					
		} elseif ( (blnConfFisicaConcluido = $$$YES) && (blnConfBulaConcluido = $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 2		
		}
	
	;Status 2-Conferido
	} elseif (statusUnitarizacao = 2) {

		if ( (blnConfFisicaConcluido '= $$$YES) && (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 0
			
		} elseif ( (blnConfFisicaConcluido '= $$$YES) || (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 1			
		
		} elseif (qtyTotalTodasEtapas > 0) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 3
		}
		
	;Status 3-Em Execução
	} elseif (statusUnitarizacao = 3) {
		
		if (qtyTotalTodasEtapas = 0) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 2
		}
	}
	
	quit

	
	/* Old OnBeforeSave

OnBeforeSave(pYKEY,&pYFELD)
	new objUnitarizacao, fltQuantidade, blnConfFisicaConcluido, blnConfBulaConcluido, qtySelagemEtapa1,
		qtySelagemEtapa2, qtySelagemEtapa3, qtyEtiqEtapa1, qtyEtiqEtapa2, qtyEtiqEtapa3, qtyPerda,
		statusUnitarizacao

	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
	set fltQuantidade = $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao)
	
	;Aba Conferência
	set blnConfFisicaConcluido = $$$VAROrdemUnitarizacaoConfFisicaConcluido(pYFELD)
	set blnConfBulaConcluido   = $$$VAROrdemUnitarizacaoConfBulaConcluido(pYFELD)	
	
	;Aba Selagem/Etiquetagem
	set qtySelagemEtapa1 = $$$VAROrdemUnitarizacaoSelagEtapa1Quant(pYFELD)
	set qtySelagemEtapa2 = $$$VAROrdemUnitarizacaoSelagEtapa2Quant(pYFELD)
	set qtySelagemEtapa3 = $$$VAROrdemUnitarizacaoSelagEtapa3Quant(pYFELD)
	set qtyEtiqEtapa1 	 = $$$VAROrdemUnitarizacaoEtiqEtapa1Quant(pYFELD)		
	set qtyEtiqEtapa2 	 = $$$VAROrdemUnitarizacaoEtiqEtapa2Quant(pYFELD)
	set qtyEtiqEtapa3 	 = $$$VAROrdemUnitarizacaoEtiqEtapa3Quant(pYFELD)
	set qtyPerda	 	 = $$$VAROrdemUnitarizacaoQuantidadePerda(pYFELD)
	
	// Verifica os saldos de selagem
	if ( (qtySelagemEtapa1 + qtySelagemEtapa2 + qtySelagemEtapa3 + qtyPerda) > fltQuantidade ) {		
		set Q = $$$QDontSave
		do ReturnError^COMUtilError("A quantidade na etapa de selagem não pode ser maior que a quantidade sendo unitarizada. Favor verificar.")
		quit		
	}	
	
	// Verifica os saldos de etiquetagem
	if ( (qtyEtiqEtapa1 + qtyEtiqEtapa2 + qtyEtiqEtapa3 + qtyPerda) > fltQuantidade ) {		
		set Q = $$$QDontSave
		do ReturnError^COMUtilError("A quantidade na etapa de etiquetagem não pode ser maior que a quantidade sendo unitarizada. Favor verificar.")
		quit		
	}
	
	// Atualiza o status da ordem de fracionamento
	set statusUnitarizacao = $$$VAROrdemUnitarizacaoStatus(pYFELD)
	
	;0-Em Aberto, 1-Em Conferência
	if ( (statusUnitarizacao = 0) || (statusUnitarizacao = 1) ) {
	
		if ( (blnConfFisicaConcluido '= $$$YES) && (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 0
			
		} elseif ( (blnConfFisicaConcluido = $$$YES) && (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 1
			
		} elseif ( (blnConfFisicaConcluido '= $$$YES) && (blnConfBulaConcluido = $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 1
					
		} elseif ( (blnConfFisicaConcluido = $$$YES) && (blnConfBulaConcluido = $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 2		
		}
	
	;Status 2-Conferido
	} elseif (statusUnitarizacao = 2) {

		if ( (blnConfFisicaConcluido '= $$$YES) && (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 0
			
		} elseif ( (blnConfFisicaConcluido '= $$$YES) || (blnConfBulaConcluido '= $$$YES) ) {
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 1			
		
		} elseif ( (qtySelagemEtapa1 '= "") || (qtySelagemEtapa2 '= "") || (qtySelagemEtapa3 '= "") ||
			 	   (qtyEtiqEtapa1 '= "")    || (qtyEtiqEtapa2 '= "")    || (qtyEtiqEtapa3 '= "") ) {
					
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 3
		}
		
	;Status 3-Em Execução
	} elseif (statusUnitarizacao = 3) {
		
		if ( (qtySelagemEtapa1 = "") && (qtySelagemEtapa2 = "") && (qtySelagemEtapa3 = "") &&
			 (qtyEtiqEtapa1 = "")    && (qtyEtiqEtapa2 = "")    && (qtyEtiqEtapa3 = "") ) {
					
			set $$$VAROrdemUnitarizacaoStatus(pYFELD) = 2
		}
	}
	
	quit
	
	*/

OnAfterPrimaryKey(YKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe o cabeçalho da ordem de unitarização.
	;
    ; Histórico:
    ;   03-Jan-2013	 Gustavo	Adaptado para exibir múltiplos documentos de
    ;							movimentação (nova estrutura de etapas c/ grid).
    ;-------------------------------------------------------------------------------
	quit:( $$$NoKey(YKEY) || (YKEY = " ") )
	
	new objUnitarizacao, idLocal, idProduto, idRecebimento, fltQuantidade, idStatus, lote, 
		param, descStatus, dteValidade, idMarca, strRotuloMarca, idMOV, MOVLink, isMedicamentoRisco,
		text, listaMovimentacoes, cnt
	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))

	set idLocal       = $$$VAROrdemUnitarizacaoLocal1(objUnitarizacao)
	set strEndereco   = $$$VAROrdemUnitarizacaoEndArmazenagem(objUnitarizacao)
	set idProduto     = $$$VAROrdemUnitarizacaoProduto(objUnitarizacao)
	set idRecebimento = $$$VAROrdemUnitarizacaoRecebimento(objUnitarizacao)
	set fltQuantidade = $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao)
	set idStatus      = $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)
	set lote	      = $$$VAROrdemUnitarizacaoLote(objUnitarizacao)
	set dteValidade	  = $$$VAROrdemUnitarizacaoValidade(objUnitarizacao)
	set idMarca		  = $$$VAROrdemUnitarizacaoMarca(objUnitarizacao)
	set idMOV		  = $$$VAROrdemUnitarizacaoCodMovimentacao(objUnitarizacao)
	
	set isControlado	   = $piece($get(^INART(YM,idProduto,1)),Y,292)
	set isMedicamentoRisco = $piece($get(^INART(YM,idProduto,1)),Y,289)
	
	set param = "STATUSUNITARIZACAO"_Y_idStatus
	
	if (idStatus '= "") {
		set descStatus = $$SQLGetUserParamDescVAR^VARSQL(param)
	} else {
		set descStatus = ""
	}
	
	if dteValidade '= "" set dteValidade = $zdate(dteValidade,4)	
	if idRecebimento = "" set idRecebimento = "Ordem manual"
	
	set strRotuloMarca = $$$WWW122ManualCaption($$Get^WWW122(YFORM, $$$CAMPOMARCA))

	write "<div style='line-height:19px;border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:8px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px'>"
	write "	<table style='font-size:13px;'>"
	write "		<tr>"
	write "			<td><strong>Produto:</strong></td>"
	write "			<td colspan='7'><strong>"_$$SQLGetDescricaoProduto^VARSQL(idProduto)_"</strong></td>"
	write "		</tr>"
	write "		<tr>"
	write "			<td><em>Local:</em></td>"
	write "			<td colspan='4'>"_$$SQLGetLocationName^VARSQL(idLocal)_" ("_strEndereco_")</td>"	
	
	if (isMedicamentoRisco = $$$YES) {
		write "			<td valign='middle' align='right' style='padding-right:3px'>"
		write "				<img src='"_YGIF_"alerta_animado.gif' title='Medicamento de Risco' border='0'>"
		write "			</td>"
		write "			<td colspan='2' valign='middle'>"
		write "				<font color='red'>Medicamento de Risco</font></td>"
		write "			</td>"
	}	
	
	write "		</tr>"
	write "		<tr>"
	write "			<td><em>Recebimento:</em></td>"
	write "			<td width='120'>"_idRecebimento_"</td>"
	write "			<td><em>Status:</em></td>"
	write "			<td colspan='2'><font color='blue'>"_descStatus_"</font></td>"
	
	if (isControlado '= "") {
		write "			<td valign='middle' align='right' style='padding-right:3px'>"
		write "				<img src='"_YGIF_"alerta_animado.gif' title='Medicamento Controlado' border='0'>"
		write "			</td>"
		write "			<td colspan='2' valign='middle'>"
		write "				<font color='green'>Medicamento Controlado</font></td>"
		write "			</td>"
	}
	write "		</tr>"
	write "		<tr>"
	write "			<td><em>Quantidade:</em></td>"
	write "			<td>"_$$^WWWTR(0,12,fltQuantidade)_"</td>"
	write "			<td><em>"_strRotuloMarca_":</em></td>"
	write "			<td width='120'>"_$$GetBrandName^VARINBrand(idMarca)_"</td>"	
	write "			<td><em>Lote:</em></td>"
	write "			<td width='90'>"_lote_"</td>"
	write "			<td><em>Validade:</em></td>"
	write "			<td>"_dteValidade_"</td>"	
	write "		</tr>"
	
	write "	</table>"

	write "</div>"
	
	if ( (idStatus = $$$OrdemConcluida) ) { //4-Concluído
 		
 		set listaMovimentacoes = $$GetListaMovimentacoes^VAROrdemUnitarizacaoEtapa(YKEY)
 		
 		if $length(listaMovimentacoes) > 1 {
	 		set text = "das movimentações"
 		} else {
	 		set text = "da movimentação"
 		}
 		
 		set MOVLink = YAKTION_"EP=WWWFORM&YFORM=INMOV&YUCI="_YUCI_"&YBED="_YBED_"&YM="_YM_"&YUSER="_YUSER_"&YKEY="
	
		write "<div style='line-height:18px;border-color:999999;border-width:1px;border-style=solid;margin-top:8px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px;background-color:lightsteelblue'>"
		write "	<table style='font-size:13px;'>"
		write " 	<tr>"
		write " 		<td colspan='8'>O produto foi movimentado para o endereço disponível por meio "_text_" de estoque nº. "
		
						for cnt = 1:1:$length(listaMovimentacoes) {
			
							set idMOV = $piece(listaMovimentacoes,";",cnt)
							quit:(idMOV = "")	
							if cnt > 1 write ", "
							write "<strong><a href="_MOVLink_idMOV_" target='_blank'>"_idMOV_"</a></strong>"
						}
					
		write ".</td>"
		
		write " 	</tr>"
		write "	</table>"
		write "</div>"
	
	} elseif (idStatus = $$$EncerradaManualmente) {
		
		set dteEncerramento  = $$$VAROrdemUnitarizacaoEncerradaManualmenteEm(objUnitarizacao)
		set userEncerramento = $$$VAROrdemUnitarizacaoEncerradaManualmentePor(objUnitarizacao)
		set strMotivoEncerra = $$$VAROrdemUnitarizacaoMotivodoencerramento(objUnitarizacao)
		
			
		write "<div style='font-size:12px;line-height:18px;border-color:999999;border-width:1px;border-style=solid;margin-top:8px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px;background-color:#f4a3a3'>"
		write "		<strong>Atenção:</strong> esta ordem foi encerrada manualmente por <strong>"_userEncerramento_"</strong> em <strong>"_$zdate(dteEncerramento,4)_"</strong>."
		
		if (strMotivoEncerra '= "") {
			write "	<br />Motivo: "_strMotivoEncerra
		}
		
		write "</div>"
	
	}

	quit
	
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Imprime o log de utilização do relatório de etiquetas na aba Controle.
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	;Aba Controle
	if (YSEITE = 3) {		
		do PrintLogEtqReport(pYKEY)
	}
	
	do PrintJS(YVOR)
	
	quit


CanConcluirEtapa(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Verifica se o usuário pode concluir uma ou mais etapas da ordem de unitarização.
	;
	; Chamado por:
	;   Botão Concluir na tela da ordem de unitarização.
	;
    ; Histórico:
    ;   03-Jan-2013	 Gustavo	Adaptado para a nova estrutura de etapas (grid).
    ;-------------------------------------------------------------------------------
 	quit:(pYKEY = "")
 	
	set YQ = $$$YQEnable
	
	new idOrdemFracion, objUnitarizacao, status, blnConfFisicaConcluido, blnConfBulaConcluido,
		idEtapa, objEtapa, situacaoEtapa, blnConcluir, fltQuantEtapa, listItensAConcluir
	
	set idOrdemFracion  = $$$KEY1(pYKEY)
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,idOrdemFracion,1))
	set status		    = $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)

	if (status = $$$OrdemConcluida) {
		set YQ = $$$YQDisable("Fracionamento já concluído.")
		quit
	}
	
	;Aba Conferência
	set blnConfFisicaConcluido = $$$VAROrdemUnitarizacaoConfFisicaConcluido(objUnitarizacao)
	set blnConfBulaConcluido   = $$$VAROrdemUnitarizacaoConfBulaConcluido(objUnitarizacao)

	// Verificando a aba conferência
	if ( (blnConfFisicaConcluido '= $$$YES) || (blnConfBulaConcluido '= $$$YES) ) {
		set YQ = $$$YQDisable("A etapa de conferência ainda não foi concluída. Favor verificar.")
		quit
	}
	
	set listItensAConcluir = $$GetLinhasAConcluir^VAROrdemUnitarizacaoEtapa(idOrdemFracion)	
	if (listItensAConcluir = "") {
		set YQ = $$$YQDisable("Nenhuma etapa foi marcada para ser concluída ainda.")
	}
	
	/* Verificando se a ordem das etapas sendo concluídas está correta. Pode concluir consecutivamente 
	   para a seguinte, mas não deve permitir pular uma etapa anterior. */	
	$$$Order3(^VAROrdemUnitarizacaoEtapa,YM,idOrdemFracion,idEtapa)
		
		set objEtapa 	  = $get(^VAROrdemUnitarizacaoEtapa(YM,idOrdemFracion,idEtapa,1))
		set situacaoEtapa = $$$VAROrdemUnitarizacaoEtapaSituacao(objEtapa)
		set blnConcluir   = $$$VAROrdemUnitarizacaoEtapaConcluir(objEtapa)
		
		;Verifica, caso a linha esteja marcada para concluir, se alguma quantidade foi definida nas duas colunas
		if ( (blnConcluir = $$$YES) && (situacaoEtapa '= $$$Concluido) ) {
			set fltQuantEtapa  = $$$VAROrdemUnitarizacaoEtapaQuantSelEtiq(objEtapa) + $$$VAROrdemUnitarizacaoEtapaQuantPerda(objEtapa)
			if (fltQuantEtapa = 0) {
				set YQ = $$$YQDisable("Não é possível concluir uma linha sem nenhuma quantidade definida.")
				quit
			}
		}
		
		;Não considerar etapas previamente concluídas.
		if (situacaoEtapa = $$$Concluido) {
			continue
		} else {			
			;Verifica se a primeira etapa disponível para conclusão está marcada ou não. Caso não esteja já vai embora.
			if (blnConcluir '= $$$YES) {
				set YQ = $$$YQDisable("Selecione a(s) etapa(s) a ser(em) concluída(s). Caso escolha mais de uma, é preciso que estejam na ordem correta.")			
			}		
			quit		
		}
	$$$End
	
	quit
	
ConcluirEtapa(YKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Faz validações e chama o processamento das transações que envolvem a conclusão
    ;	das etapas da ordem de unitarização selecionadas pelo usuário.
	;
	; Chamado por:
	;   Botão Concluir na tela da ordem de unitarização.
	;
    ; Histórico:
    ;   03-Jan-2013	 Gustavo	Adaptado para a nova estrutura de etapas (grid).
    ;-------------------------------------------------------------------------------
	quit:(YKEY = "")

	new strStatus, idOrdemFracion, objUnitarizacao, strLinhasAConcluir, fltQuantAConcluir

	set strStatus = $$$OK	
	set idOrdemFracion = $$$KEY1(YKEY)
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,idOrdemFracion,1))
	
	set strLinhasAConcluir = $$GetLinhasAConcluir^VAROrdemUnitarizacaoEtapa(idOrdemFracion)
	quit:(strLinhasAConcluir = "")
	
	set fltQuantAConcluir = +$$GetQuantidadeAConcluir^VAROrdemUnitarizacaoEtapa(idOrdemFracion,strLinhasAConcluir)
	
	if (fltQuantAConcluir = 0) {
		set fltQuantPerda = +$$GetQuantidadePerda^VAROrdemUnitarizacaoEtapa(idOrdemFracion,strLinhasAConcluir)
				
		if (fltQuantPerda = 0) {
			set strStatus = "Não é possível concluir pois a quantidade total é igual a zero. É preciso selecionar ao menos uma etapa com quantidade acima de 0."
		} else {
			set strStatus = $$AtualizaStatusLinhas^VAROrdemUnitarizacaoEtapa(idOrdemFracion,strLinhasAConcluir)
		}
	
	} else {
		set strStatus = $$ConcluirEtapaTxn(YKEY,objUnitarizacao,strLinhasAConcluir,fltQuantAConcluir)
	}

	if $$$ISERR(strStatus) {
		$$$StartScript()
		write "alert('Não foi possível concluir a etapa da ordem de fracionamento.\nMotivo: "_strStatus_"');"
		$$$EndScript()
	}
	
	do RedirectForm^COMUtilForm("VAROrdemUnitarizacao",idOrdemFracion,"VAROrdemUnitarizacao",YPARA,YSEITE)

	quit

	/* OLD CanConcluir e Concluir
	
CanConcluir(pYKEY,YFELD)
 	quit:(pYKEY = "")
 	
	set YQ = $$$YQEnable

	new objUnitarizacao, fltQuantidade, blnConfFisicaConcluido, blnConfBulaConcluido, qtySelagemEtapa1,
		qtySelagemEtapa2, qtySelagemEtapa3, qtyEtiqEtapa1, qtyEtiqEtapa2, qtyEtiqEtapa3, qtyPerda,
		qtySelada, qtyEtiq, status
	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
	set fltQuantidade   = $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao)
	set status		    = $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)

	if (status = 4) {
		set YQ = $$$YQDisable("Fracionamento já concluído.")
		quit
	}
	
	;Aba Conferência
	set blnConfFisicaConcluido = $$$VAROrdemUnitarizacaoConfFisicaConcluido(objUnitarizacao)
	set blnConfBulaConcluido   = $$$VAROrdemUnitarizacaoConfBulaConcluido(objUnitarizacao)
	
	;Aba Selagem/Etiquetagem
	set qtySelagemEtapa1 = $$$VAROrdemUnitarizacaoSelagEtapa1Quant(objUnitarizacao)
	set qtySelagemEtapa2 = $$$VAROrdemUnitarizacaoSelagEtapa2Quant(objUnitarizacao)
	set qtySelagemEtapa3 = $$$VAROrdemUnitarizacaoSelagEtapa3Quant(objUnitarizacao)
	set qtyEtiqEtapa1 	 = $$$VAROrdemUnitarizacaoEtiqEtapa1Quant(objUnitarizacao)		
	set qtyEtiqEtapa2 	 = $$$VAROrdemUnitarizacaoEtiqEtapa2Quant(objUnitarizacao)
	set qtyEtiqEtapa3 	 = $$$VAROrdemUnitarizacaoEtiqEtapa3Quant(objUnitarizacao)
	set qtyPerda	 	 = $$$VAROrdemUnitarizacaoQuantidadePerda(objUnitarizacao)
	
	set qtySelada = qtySelagemEtapa1 + qtySelagemEtapa2 + qtySelagemEtapa3
	set qtyEtiq   = qtyEtiqEtapa1 + qtyEtiqEtapa2 + qtyEtiqEtapa3
	
	// Verificando a aba conferência
	if ( (blnConfFisicaConcluido '= $$$YES) || (blnConfBulaConcluido '= $$$YES) ) {
		set YQ = $$$YQDisable("A etapa de conferência ainda não foi concluído.")
	
	// Verifica os saldos de selagem
	} elseif (qtySelada = 0) {
		set YQ = $$$YQDisable("É necessário informar a quantidade de selagem. Favor verificar.")
	
	} elseif ( (qtySelada + qtyPerda) '= fltQuantidade) {
		set YQ = $$$YQDisable("A quantidade na etapa de selagem não pode diferente da quantidade a ser unitarizada. Favor verificar.")
	
	// Verifica os saldos de etiquetagem
	} elseif (qtyEtiq = 0) {
		set YQ = $$$YQDisable("É necessário informar a quantidade de selagem. Favor verificar.")

	} elseif ( (qtyEtiq + qtyPerda) > fltQuantidade ) {		
		set YQ = $$$YQDisable("A quantidade na etapa de etiquetagem não pode ser maior que a quantidade sendo unitarizada. Favor verificar.")		
	}

	quit

Concluir(YKEY)
	new strStatus, objUnitarizacao

	set strStatus = $$$OK	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
	
	set strStatus = $$ConcluirTxn(YKEY,objUnitarizacao)
	
	if $$$ISOK(strStatus) {
		set $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)       =  4 ;4-Concluído
		set $$$VAROrdemUnitarizacaoConcluidoEm(objUnitarizacao)  =  $horolog
		set $$$VAROrdemUnitarizacaoConcluidoPor(objUnitarizacao) =  YBED
		
		set $$$VAROrdemUnitarizacaoCodMovimentacao(objUnitarizacao) = $get(^VARTempOrdemUnitarizacaoMOV(YBED))
		kill ^VARTempOrdemUnitarizacaoMOV(YBED)
	
		set strStatus = $$$Save("VAROrdemUnitarizacao",YKEY,objUnitarizacao,1)
	}
	
	if $$$ISERR(strStatus) {
		$$$StartScript()
		write "alert('Não foi possível concluir o fracionamento.\nMotivo: "_strStatus_"');"
		$$$EndScript()
	}
	
	do GoToForm^COMUtilForm("VAROrdemUnitarizacao",YKEY)

	quit

	*/

ConcluirEtapaTxn(pidOrdemUnitarizacao,pobjOrdemUnitarizacao,pstrLinhasAConcluir,pfltQuantidade)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Executa as transações que envolvem o processamento da conclusão da(s) etapa(s)
    ;   selecionadas pelo usuário da ordem de unitarização.
	;
	; Chamado por:
	;   ConcluirEtapa^VAROrdemUnitarizacao.mac.
	;
    ; Histórico:
    ;   03-Jan-2013	 Gustavo	Adaptado para a nova estrutura de etapas (grid).
    ;-------------------------------------------------------------------------------
	quit:( (pidOrdemUnitarizacao = "") || (pobjOrdemUnitarizacao = "") ) ""
	
	new strStatus, idLocal, EndOrigem, idProduto, idRecebimento, fltQuantidade, idStatus,
		lote, dteValidade, idMarca, strBundleInfo, idBundle, qtyBundle, EndDestino, idMOV, 
		strObserv, strStatus, objMOV, idMOVLinha, objMOVLinha, idKeyLinha, objLocal
		
	set strStatus = $$$OK
	
	set idLocal       = $$$VAROrdemUnitarizacaoLocal1(pobjOrdemUnitarizacao)
	set EndOrigem     = $$$VAROrdemUnitarizacaoEndArmazenagem(pobjOrdemUnitarizacao)
	set idProduto     = $$$VAROrdemUnitarizacaoProduto(pobjOrdemUnitarizacao)
	set idRecebimento = $$$VAROrdemUnitarizacaoRecebimento(pobjOrdemUnitarizacao)
	
	//Utilizar a quantidade que vem como parâmetro (pfltQuantidade), que soma todas as linhas marcadas
	;set fltQuantidade = $$$VAROrdemUnitarizacaoQuantidade(pobjOrdemUnitarizacao)
	
	set idStatus      = $$$VAROrdemUnitarizacaoStatus(pobjOrdemUnitarizacao)
	set lote	      = $$$VAROrdemUnitarizacaoLote(pobjOrdemUnitarizacao)
	set dteValidade	  = $$$VAROrdemUnitarizacaoValidade(pobjOrdemUnitarizacao)
	set idMarca		  = $$$VAROrdemUnitarizacaoMarca(pobjOrdemUnitarizacao)
	
	set objLocal = $get(^WWW0121(YM,YM,idLocal,1))	
	set EndDestino = $$$WWW0121FREE19(objLocal) //Unitarização Concluída
	
	if '$data(^INLP(YM,idLocal,EndDestino)) {
		set strStatus = "O endereço de destino ("_EndDestino_") não existe como endereço de armazenagem."
		quit strStatus
	}
	
	//Cria o cabeçalho da movimentação
	set idMOV = $$^WWWNEXT("INMOV")
	set strObserv = "Movimentação criada automaticamente a partir da Ordem de Fracionamento "_pidOrdemUnitarizacao_", etapa(s) "_$$GetListaEtapasFromLinhas^VAROrdemUnitarizacaoEtapa(pidOrdemUnitarizacao,pstrLinhasAConcluir)_"."
	
	set objMOV = ""
	set $$$INMOVStockMovementDate(objMOV) = $piece($h,",",1) ;Data
	set $$$INMOVLocation(objMOV) 	= idLocal	;Local
	set $$$INMOVMoveType(objMOV) 	= 0		    ;Movement Type (0 - Estoque, 1 - Palete)
	set $$$INMOVStatus(objMOV) 	 	= 1 		;Status (1 - Em andamento)
	set $$$INMOVNotes(objMOV)    	= strObserv ;Observações
	set $$$INMOVDateEntered(objMOV) = $horolog  ;Criado Em
	set $$$INMOVCreatedBy(objMOV)   = YBED	    ;Criado Por
		
   	set strBundleInfo = $$GetBundleInfo(idProduto,idLocal,EndOrigem,lote,dteValidade)
		
	set idBundle  = $piece(strBundleInfo,";",1)
	set qtyBundle = $piece(strBundleInfo,";",2)
	
	//Validando o bundle
	if (idBundle = "") {
		set strStatus = "Nenhum lote definido. Não é possível processar esta transação."
	}
	
	;Caso não tenha quantidade suficiente no bundle
	elseif (qtyBundle < pfltQuantidade) {
		set strStatus = "Não existe quantidade suficiente no lote."	
	}
	
	if $$$ISOK(strStatus) {	
		set strStatus = $$$Save("INMOV",idMOV,objMOV,1)
	}
	
	if $$$ISOK(strStatus) {

   		set idMOVLinha = 1
		
		//Cria a linha da movimentação
		set objMOVLinha = ""
		set $piece(objMOVLinha,Y,1)  = idProduto
		set $piece(objMOVLinha,Y,2)  = EndOrigem
		set $piece(objMOVLinha,Y,7)  = EndDestino
		set $piece(objMOVLinha,Y,21) = pfltQuantidade
		set $piece(objMOVLinha,Y,24) = idBundle
		set $piece(objMOVLinha,Y,25) = $$SQLGetUnitMeasure^VARSQL(idProduto)
		set $piece(objMOVLinha,Y,27) = pfltQuantidade
		   	
		set idKeyLinha = idMOV_$$$COMMA_idMOVLinha
		set strStatus = $$$Save("INMOVLine",idKeyLinha,objMOVLinha,1)
	}
	
	//Movimenta os produtos	
	if $$$ISOK(strStatus) {			
		set strStatus = $$CanPost^INMOV(idMOV)
		
		if $$$ISOK(strStatus) {
			set strStatus = $$Post^INMOV(idMOV)
			
			if $$$ISOK(strStatus) {
				kill ^VARTempOrdemUnitarizacaoMOV(YBED)
				set ^VARTempOrdemUnitarizacaoMOV(YBED) = idMOV	
			}		
		}
	}
	
	;Se a movimentação tiver sido realizada com sucesso, atualiza o status das linhas
	if $$$ISOK(strStatus) {
		set strStatus = $$AtualizaStatusLinhas^VAROrdemUnitarizacaoEtapa(pidOrdemUnitarizacao,pstrLinhasAConcluir,idMOV)
	}

	if $$$ISOK(strStatus) {	
		//Verifica se as quantidades das etapas concluídas (sel./etiq. + perdas) correspondem ao total da ordem
		if ($$ContemQuantidadePendente(idOrdemFracion) = $$$NO) {

			set $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)       =  4 ;4-Concluído
			set $$$VAROrdemUnitarizacaoConcluidoEm(objUnitarizacao)  =  $horolog
			set $$$VAROrdemUnitarizacaoConcluidoPor(objUnitarizacao) =  YBED
			
			//Isso está em desuso. Agora a informação do código da movimentação é controlada nas linhas.
			;set $$$VAROrdemUnitarizacaoCodMovimentacao(objUnitarizacao) = $get(^VARTempOrdemUnitarizacaoMOV(YBED))
			;kill ^VARTempOrdemUnitarizacaoMOV(YBED)

			set strStatus = $$$Save("VAROrdemUnitarizacao",idOrdemFracion,objUnitarizacao,$$$YES)
		}
	}
	
	quit strStatus


GetBundleInfo(pidItem,pidLocal,pidEndereco,pstrLote="",pdteValidade="")	
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna o código do bundle de um produto, local, endereço, lote e validade
	;
    ; Histórico:
    ; 	30-Jan-2012  Gustavo	Criado
    ;   12-Abr-2012	 Rubens		Correção para criar SQL dinâmico, levando em consideração
    ;							que um produto pode não ter lote e validade    
    ;-------------------------------------------------------------------------------
	quit:( (pidLocal = "") || (pidEndereco = "") ) $$$NO	
	
	new idBundle, qtyBundle
	
	set idBundle  = ""
	set qtyBundle = ""
	set return=""
	
	//Retrieving Movements
    set tSQL="SELECT TOP 1 alSOH.dBundleStock.Bundle->ID AS idBundle, "_
    		 "+alSOH.dBundleStock.QtyOnHand as qtyBundle "_
    	     "FROM alSOH.dBundleStock "_
    	     "WHERE alSOH.dBundleStock.Item = '"_pidItem_"' "_
    	     "AND alSOH.dBundleStock.Storage->Location = '"_pidLocal_"' "_
			 "AND alSOH.dBundleStock.Storage->Code = '"_pidEndereco_"' "
	if pstrLote'="" set tSQL=tSQL_"AND alSOH.dBundleStock.Bundle->LotNumber = '"_pstrLote_"' "
	if pdteValidade'="" set tSQL=tSQL_"AND alSOH.dBundleStock.Bundle->UseByDate = '"_pdteValidade_"' "
 
 	Set tRs = ##class(%Library.ResultSet).%New()
 	Set tRs.RuntimeMode = "0"    
 	Do tRs.Prepare(tSQL) 
 	Do tRs.Execute()
 	While tRs.Next() 
 		{
	 		set idBundle=tRs.Data("idBundle")
	 		set qtyBundle=tRs.Data("qtyBundle")
	 		set return=idBundle_";"_qtyBundle
 	}
    If $IsObject(tRs)
		{
		Do tRs.Close()
	}
    quit return
	
OnBlurD8(YINHALT,YFELD)	
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Chamada OnBlur do Campo "Conferência Física - Concluído"
	;
	; Chamado por:
	;   Evento OnBlur do campo.
    ;-------------------------------------------------------------------------------	
	if (YINHALT = 1) {			
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",9,YBED)
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",10,$horolog)
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",9,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",10,"")
	}

	quit
	
	
OnBlurD11(YINHALT,YFELD)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Chamada OnBlur do Campo "Controle de Bula - Concluído"
	;
	; Chamado por:
	;   Evento OnBlur do campo.
    ;-------------------------------------------------------------------------------
	if (YINHALT = 1) {		
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",12,YBED)
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",13,$horolog)
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",12,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",13,"")
	}

	quit
	
	
	/* OLD (rotinas descontinuadas após a criação da nova estrutura de etapas c/ grid.)	
	   AINDA É NECESSÁRIO remover também os métodos mais abaixo blnSaldoSelagemCompleto e blnSaldoEtiqCompleto	 */
	
OnBlurD14(YINHALT,YFELD)

	new SelagEtapa1Resp, SelagEtapa1Data, objUnitarizacao, objUnitarizacao, dataSelagEtapa1Quant
	
	set SelagEtapa1Resp = $$$VAROrdemUnitarizacaoSelagEtapa1Resp(YFELD)
	set SelagEtapa1Data = $$$VAROrdemUnitarizacaoSelagEtapa1Data(YFELD)

	if (YINHALT '= "") {
		
		set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
		
		//Seta os dados de log
		set dataSelagEtapa1Quant = $$$VAROrdemUnitarizacaoSelagEtapa1Quant(objUnitarizacao)
		
		if ( (SelagEtapa1Data = "") || ((dataSelagEtapa1Quant '= $zstrip(YINHALT,"*E'N")) && (dataSelagEtapa1Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",15,$horolog)
		}
		
		if ( (SelagEtapa1Resp = "") || ((dataSelagEtapa1Quant '= $zstrip(YINHALT,"*E'N")) && (dataSelagEtapa1Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",16,YBED)
		}		

	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",15,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",16,"")
	}

	quit
	
OnBlurD17(YINHALT,YFELD)

	new SelagEtapa2Resp, SelagEtapa2Data, objUnitarizacao, dataSelagEtapa2Quant
	
	set SelagEtapa2Resp = $$$VAROrdemUnitarizacaoSelagEtapa2Resp(YFELD)
	set SelagEtapa2Data = $$$VAROrdemUnitarizacaoSelagEtapa2Data(YFELD)

	if (YINHALT '= "") {		

		set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))				
		set dataSelagEtapa2Quant = $$$VAROrdemUnitarizacaoSelagEtapa2Quant(objUnitarizacao)

		if ( (SelagEtapa2Data = "") || ((dataSelagEtapa2Quant '= $zstrip(YINHALT,"*E'N")) && (dataSelagEtapa2Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",18,$horolog)
		}
		
		if ( (SelagEtapa2Resp = "") || ((dataSelagEtapa2Quant '= $zstrip(YINHALT,"*E'N")) && (dataSelagEtapa2Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",19,YBED)
		}
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",18,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",19,"")
	}

	quit
	
	
OnBlurD20(YINHALT,YFELD)

	new Se]]><![CDATA[lagEtapa3Resp, SelagEtapa3Data, objUnitarizacao, dataSelagEtapa3Quant
	
	set SelagEtapa3Resp = $$$VAROrdemUnitarizacaoSelagEtapa3Resp(YFELD)
	set SelagEtapa3Data = $$$VAROrdemUnitarizacaoSelagEtapa3Data(YFELD)

	if (YINHALT '= "") {
		
		set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))				
		set dataSelagEtapa3Quant = $$$VAROrdemUnitarizacaoSelagEtapa3Quant(objUnitarizacao)				

		if ( (SelagEtapa3Data = "") || ((dataSelagEtapa3Quant '= $zstrip(YINHALT,"*E'N")) && (dataSelagEtapa3Quant '= "")) ) {		
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",21,$horolog)
		}
		
		if ( (SelagEtapa3Resp = "") || ((dataSelagEtapa3Quant '= $zstrip(YINHALT,"*E'N")) && (dataSelagEtapa3Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",22,YBED)
		}
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",21,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",22,"")
	}

	quit
	
	
OnBlurD23(YINHALT,YFELD)

	new EtiqEtapa1Resp, EtiqEtapa1Data, objUnitarizacao, dataEtiqEtapa1Quant
	
	set EtiqEtapa1Resp = $$$VAROrdemUnitarizacaoEtiqEtapa1Resp(YFELD)
	set EtiqEtapa1Data = $$$VAROrdemUnitarizacaoEtiqEtapa1Data(YFELD)
	
	if (YINHALT '= "") {
		
		set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))				
		set dataEtiqEtapa1Quant = $$$VAROrdemUnitarizacaoEtiqEtapa1Quant(objUnitarizacao)
		
		if ( (EtiqEtapa1Data = "") || ((dataEtiqEtapa1Quant '= $zstrip(YINHALT,"*E'N")) && (dataEtiqEtapa1Quant '= "")) ) {			
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",24,$horolog)
		}
		
		if ( (EtiqEtapa1Resp = "") || ((dataEtiqEtapa1Quant '= $zstrip(YINHALT,"*E'N")) && (dataEtiqEtapa1Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",25,YBED)
		}
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",24,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",25,"")
	}

	quit
	
OnBlurD26(YINHALT,YFELD)

	new EtiqEtapa2Resp, EtiqEtapa2Data, objUnitarizacao, dataEtiqEtapa2Quant
	
	set EtiqEtapa2Resp = $$$VAROrdemUnitarizacaoEtiqEtapa2Resp(YFELD)
	set EtiqEtapa2Data = $$$VAROrdemUnitarizacaoEtiqEtapa2Data(YFELD)

	if (YINHALT '= "") {
		
		set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))				
		set dataEtiqEtapa2Quant = $$$VAROrdemUnitarizacaoEtiqEtapa2Quant(objUnitarizacao)
		
		if ( (EtiqEtapa2Data = "") || ((dataEtiqEtapa2Quant '= $zstrip(YINHALT,"*E'N")) && (dataEtiqEtapa2Quant '= "")) ) {		
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",27,$horolog)
		}
		
		if ( (EtiqEtapa2Resp = "") || ((dataEtiqEtapa2Quant '= $zstrip(YINHALT,"*E'N")) && (dataEtiqEtapa2Quant '= "")) ) {		
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",28,YBED)
		}
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",27,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",28,"")
	}

	quit
	
	
OnBlurD29(YINHALT,YFELD)

	new EtiqEtapa3Resp, EtiqEtapa3Data, objUnitarizacao, dataEtiqEtapa3Quant
	
	set EtiqEtapa3Resp = $$$VAROrdemUnitarizacaoEtiqEtapa3Resp(YFELD)
	set EtiqEtapa3Data = $$$VAROrdemUnitarizacaoEtiqEtapa3Data(YFELD)

	if (YINHALT '= "") {		

		set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))				
		set dataEtiqEtapa3Quant = $$$VAROrdemUnitarizacaoEtiqEtapa3Quant(objUnitarizacao)
		
		if ( (EtiqEtapa3Data = "") || ((dataEtiqEtapa3Quant '= $zstrip(YINHALT,"*E'N")) && (dataEtiqEtapa3Quant '= "")) ) {
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",30,$horolog)
		}
		
		if ( (EtiqEtapa3Data = "") || ((dataEtiqEtapa3Quant '= $zstrip(YINHALT,"*E'N")) && (dataEtiqEtapa3Quant '= "")) ) {		
			do PopulateDataField^COMUtils("VAROrdemUnitarizacao",31,YBED)
		}
		
	} else {
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",30,"")
		do PopulateDataField^COMUtils("VAROrdemUnitarizacao",31,"")
	}

	quit

blnSaldoSelagemCompleto(YKEY,YFELD,pTipo)
	quit:($$$NoKey(YKEY)) $$$NO

	new blnSaldoSelagemCompleto, objUnitarizacao, fltQuantidade, qtySelagemEtapa1, qtySelagemEtapa2,
		qtyPerda, saldo
	
	set blnSaldoSelagemCompleto = $$$NO
	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
	set fltQuantidade 	= $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao)
	
	set qtySelagemEtapa1 = $$$VAROrdemUnitarizacaoSelagEtapa1Quant(YFELD)
	set qtySelagemEtapa2 = $$$VAROrdemUnitarizacaoSelagEtapa2Quant(YFELD)
	set qtyPerda		 = $$$VAROrdemUnitarizacaoQuantidadePerda(YFELD)
	
	
	if (pTipo = "SelagEtapa2") {
		
		set saldo = (fltQuantidade - qtySelagemEtapa1 - qtyPerda)
		
		if (saldo <= 0) {
			set blnSaldoSelagemCompleto = $$$YES
		}
	
	} elseif (pTipo = "SelagEtapa3") {
				
		set saldo = (fltQuantidade - qtySelagemEtapa1 - qtySelagemEtapa2 - qtyPerda)
		
		if (saldo <= 0) {
			set blnSaldoSelagemCompleto = $$$YES
		}		
		
	}
	
	quit blnSaldoSelagemCompleto
	
blnSaldoEtiqCompleto(YKEY,YFELD,pTipo)
	quit:($$$NoKey(YKEY)) $$$NO

	new blnSaldoEtiqCompleto, objUnitarizacao, fltQuantidade, qtyEtiqEtapa1, qtyEtiqEtapa2,
		qtyPerda, saldo
	
	set blnSaldoEtiqCompleto = $$$NO
	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,YKEY,1))
	set fltQuantidade 	= $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao)
	
	set qtyEtiqEtapa1 = $$$VAROrdemUnitarizacaoEtiqEtapa1Quant(YFELD)
	set qtyEtiqEtapa2 = $$$VAROrdemUnitarizacaoEtiqEtapa2Quant(YFELD)
	set qtyPerda	  = $$$VAROrdemUnitarizacaoQuantidadePerda(YFELD)
	
	if (pTipo = "EtiqEtapa2") {
		
		set saldo = (fltQuantidade - qtyEtiqEtapa1 - qtyPerda)
		
		if (saldo <= 0) {
			set blnSaldoEtiqCompleto = $$$YES
		}
	
	} elseif (pTipo = "EtiqEtapa3") {
				
		set saldo = (fltQuantidade - qtyEtiqEtapa1 - qtyEtiqEtapa2 - qtyPerda)
		
		if (saldo <= 0) {
			set blnSaldoEtiqCompleto = $$$YES
		}
	}
	
	quit blnSaldoEtiqCompleto


CanImprimir(pYKEY)
	new objUnitarizacao, strStatus, strMensagem
	
 	if ($$$NoKey(pYKEY)) quit
 	
	set YQ = $$$YQEnable

	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM, pYKEY, 1))
	set strStatus = $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)

	if (strStatus '= $$$OrdemConcluida) {
		set strMensagem = "Este relatório só pode ser impresso quando o fracionamento estiver concluído."
		set YQ = $$$YQDisable(strMensagem)
		quit
	}

	quit

ImprimeRelatorio(pYKEY)
	do RunReportOrdemUnitarizacao^VARJasperRunReport(pYKEY)
	quit
	
ImprimeEtiquetas(pYKEY)
	quit:(pYKEY = "")
	new objUnitarizacao, idProduto, strLote
	
	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM,pYKEY,1))
	
	set idProduto = $$$VAROrdemUnitarizacaoProduto(objUnitarizacao)
	set strLote	  = $$$VAROrdemUnitarizacaoLote(objUnitarizacao)
	set validade  = $piece(($get(^VAROrdemUnitarizacao(YM,pYKEY,1))),Y,6)
	quit:((idProduto = "") || (strLote = ""))
	
	set $piece(^VARTempProdutoEtq(YM,YBED,1),Y,1) = idProduto //item
	set $piece(^VARTempProdutoEtq(YM,YBED,1),Y,2) = strLote   //lote
	set $piece(^VARTempProdutoEtq(YM,YBED,1),Y,3) = validade  //validade
	
	do GoToForm^COMUtilForm("VARProdutoEtq",pYKEY)	
		
	;do RunReportProdutoEtq^VARJasperRunReport(idProduto,strLote)
	;do LogEtqReportOnClick(pYKEY)
	
	quit
	
LogEtqReportOnClick(pYKEY)
	;-------------------------------------------------------------------------------
    ; Loga todos os usuários e data em que clicaram no botão de imprimir etiqueta (HEVA-286)
    ;
    ; Chamado por:
    ; 	Método ImprimeEtiquetas^OrdemUnitarizacao.mac.
	;
    ; History:
    ; 	20-Abr-2012		Gustavo Fiuza	Criado
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new ID, geradoEm, geradoPor

	set ID = $$GetNextID(pYKEY)
	quit:(ID = "")
	
	set geradoEm  = $horolog
	set geradoPor = YBED
	
	;Não foi considerado necessário gravar logs de logs, portanto salvando direto na global
	set ^VAROrdemUnitarizacaoEtqLog(YM,pYKEY,ID,1) = geradoEm_Y_geradoPor
	
	quit

GetNextID(pYKEY)
	;-------------------------------------------------------------------------------
    ; Retorna o próximo registro na tabela de log de impressão de etiquetas.
    ;
    ; Chamado por:
    ; 	Método LogEtqReportOnClick^OrdemUnitarizacao.mac.
	;
    ; History:
    ; 	20-Abr-2012		Gustavo Fiuza	Criado
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "") ""
	
	new ultimoRegistro, nextID
	
	set ultimoRegistro = ""	
	set ultimoRegistro = $order(^VAROrdemUnitarizacaoEtqLog(YM,pYKEY,ultimoRegistro),-1)
	
	set nextID = $increment(ultimoRegistro)
	
	quit nextID
	
	
PrintLogEtqReport(pYKEY)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe o log de impressão de etiquetas.
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	quit:('$data(^VAROrdemUnitarizacaoEtqLog(YM,pYKEY)))
	
	new lstHeader, noImpressao, objImpressao

	//Monta cabeçalho da tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","Data","Hora","Usuário")

  	if $$Start^COMTable(lstHeader,"Log de utilização da impressão de etiquetas:",,,$$$NO) {
	
		$$$Order3(^VAROrdemUnitarizacaoEtqLog,YM,pYKEY,noImpressao)
		
			set objImpressao = $get(^VAROrdemUnitarizacaoEtqLog(YM,pYKEY,noImpressao,1))
			
			set geradoEm  = $$$VAROrdemUnitarizacaoEtqLogGeradoem(objImpressao)
			set geradoPor = $$$VAROrdemUnitarizacaoEtqLogGeradopor(objImpressao)
			
   			do NewLine^COMTable()
   			do InsertCell^COMTable(noImpressao)
   			do InsertCell^COMTable($zdate($piece(geradoEm,",",1),4))
   			do InsertCell^COMTable($ztime($piece(geradoEm,",",2)))
   			do InsertCell^COMTable($$SQLGetUserName^VARSQL(geradoPor))
   			do EndLine^COMTable()   			
					
		$$$End
	
		do Stop^COMTable()
  	}
	
	quit
	
ContemQuantidadePendente(pidOrdemFrac)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna booleano se a ordem de unitarização ainda possui ou não	quantidade
    ;	pendente a ser concluída em etapas. Caso não tenha, o sistema usa essa 
    ;	informação para fechar o status da ordem de unitarização em si. Ou seja,
    ;	se todas as linhas (etapas) = Concluído, o pai (ordem) = Concluído também.
    ;
    ; Chamado por:
    ; 	Método ConcluirEtapaTxn^OrdemUnitarizacao.mac.
	;
    ; History:
    ; 	03-Jan-2013  Gustavo	Criado
    ;-------------------------------------------------------------------------------
	quit:(pidOrdemFrac = "") ""
	
	new objOrdem, fltQuantOrdem, fltQuantLinhas, idEtapa, objEtapa, fltQuantEtapa
	
	set objOrdem = $get(^VAROrdemUnitarizacao(YM,pidOrdemFrac,1))
	set fltQuantOrdem = $$$VAROrdemUnitarizacaoQuantidade(objOrdem)
	
	set fltQuantLinhas = 0
	
	$$$Order3(^VAROrdemUnitarizacaoEtapa,YM,pidOrdemFrac,idEtapa)
		
		set objEtapa = $get(^VAROrdemUnitarizacaoEtapa(YM,pidOrdemFrac,idEtapa,1))		
		
		;Considerando somente as linhas já concluídas
		continue:($$$VAROrdemUnitarizacaoEtapaSituacao(objEtapa) = $$$EmAndamento)
		
		set fltQuantEtapa  = $$$VAROrdemUnitarizacaoEtapaQuantSelEtiq(objEtapa) + $$$VAROrdemUnitarizacaoEtapaQuantPerda(objEtapa)
		set fltQuantLinhas = fltQuantLinhas + fltQuantEtapa
		
	$$$End
	
	if (fltQuantLinhas < fltQuantOrdem) {
		quit $$$YES
	}
	
	quit $$$NO
	
	
GetProdutoeEtapa(pidOrdemFrac,pnumEtapa)
	quit:(pidOrdemFrac = "") ""	
	new idProduto	
	set idProduto = $piece($get(^VAROrdemUnitarizacao(0,pidOrdemFrac,1)),"~",3)
	quit idProduto_";"_pnumEtapa
	
CanEncerrarManualmente(pYKEY)
	new objUnitarizacao, statusUnit, strMensagem
	
 	if ($$$NoKey(pYKEY)) quit
 	
	set YQ = $$$YQEnable

	set objUnitarizacao = $get(^VAROrdemUnitarizacao(YM, pYKEY, 1))
	set statusUnit 		= $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)
	set strMensagem 	= ""
	
	if (statusUnit = $$$OrdemConcluida) {
		set strMensagem = "A ordem já foi concluída e não pode ser encerrada manualmente."
	} elseif (statusUnit = $$$EncerradaManualmente) {	
		set strMensagem = "A ordem já foi encerrada manualmente."
	}
	
	set:(strMensagem '= "") YQ = $$$YQDisable(strMensagem)

	quit
	
	
EncerrarManualmente(pYKEY,pstrMotivo)
	new objOrdem, strStatus
 
 	set strStatus = $$$OK
 	 	
 	set:($$$NoKey(pYKEY)) strStatus = "Nenhuma ordem definida." 	

	set objOrdem = $get(^VAROrdemUnitarizacao(YM,pYKEY,1))
	set:(objOrdem = "") strStatus = "Nenhum objeto encontrado para esta ordem."
		 		 
	if ($$$VAROrdemUnitarizacaoStatus(objOrdem) = $$$OrdemConcluida) {
		 set strStatus = "Esta ordem já foi concluída e não pode ser manualmente encerrada."
	} elseif ($$$VAROrdemUnitarizacaoStatus(objOrdem) = $$$EncerradaManualmente) {
		set strStatus = "Esta ordem já foi encerrada manualmente."		
	}
	
	if $$$ISOK(strStatus) {	 		 			
		//Atualizando os dados da ordem com o encerramento
		set $$$VAROrdemUnitarizacaoStatus(objOrdem) 				 = $$$EncerradaManualmente
		set $$$VAROrdemUnitarizacaoEncerradaManualmenteEm(objOrdem)  = $horolog
		set $$$VAROrdemUnitarizacaoEncerradaManualmentePor(objOrdem) = YBED
		set $$$VAROrdemUnitarizacaoMotivodoencerramento(objOrdem)	 = pstrMotivo

		//Salvando o objeto
		set strStatus = $$$Save("VAROrdemUnitarizacao",pYKEY,objOrdem,$$$YES)
	}

	if $$$ISERR(strStatus) { 
		do StartScript^COMUtiljavascript()
		write "alert('Não foi possível encerrar a ordem de fracionamento. Motivo: "_strStatus_".'); "
		do EndScript^COMUtiljavascript()
	}	
 
	do GoToForm^COMUtilForm("VAROrdemUnitarizacao", pYKEY)

	quit ""
	
PrintJS(pobjForm)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VAROrdemUnitarizacao.mac
    ; 
    ; Histórico:
    ; 	27-Fev-2013		Criado
    ;-------------------------------------------------------------------------------
	new strP1Name
	set strP1Name = "YVAROrdemUnitarizacaoP1"
	
	write !, "<script language=""javascript"">"
	&js<
	var idOrdemUnitarizacao = document.#(YHTMFORM)#.#(strP1Name)#.value;
 
	function iePrompt(message) {
		var settings = "dialogWidth: 420px; dialogHeight: 200px; center: yes; " +
			"edge: raised; scroll: no; status: no";		
		return window.showModalDialog("#(YGIF)#VARPromptSize.html", message,settings);
	}
	
	function encerrarOrdem() {
		CallBackNow("EncerrarOrdemOnClickJS^VAROrdemUnitarizacao", idOrdemUnitarizacao);
	}

	>
	write !, "</script>"
	quit
	
	
EncerrarOrdemOnClickJS(pidOrdemUnitarizacao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função PrintJS^VAROrdemUnitarizacao.mac
    ; 
    ; Histórico:
    ; 	27-Fev-2013		Criado
    ;-------------------------------------------------------------------------------
	new objOrdemUnitarizacao
 
	if ($length($get(pidOrdemUnitarizacao)) = 0) {
		set objOrdemUnitarizacao = ""
	}
	else {
		set objOrdemUnitarizacao = $get(^VAROrdemUnitarizacao(0,pidOrdemUnitarizacao,1))
	}
	set YQ = 0
	do CanEncerrarManualmente(pidOrdemUnitarizacao)
	if ($extract(YQ, 1, 1) '= $$$YQEnable) {
		&js<
		    alert('#($piece(YQ, "1", 2))#');
		>
		quit
	}
 
	&js<
	    var motivo = iePrompt('Favor inserir o motivo do encerramento da ordem de fracionamento:');
		if (motivo) {
			CallBackNow("EncerrarManualmente^VAROrdemUnitarizacao", '#(pidOrdemUnitarizacao)#', motivo);
		}
	> 
	quit
]]></Routine>
</Export>