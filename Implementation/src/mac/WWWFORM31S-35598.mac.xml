<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM31S" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORM31S(YINHALT,YLFDAT)
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORM31S("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SPEICHERN ÄNDERUNGEN IN BEARBEITUNGSTABELLE
	;		
	; Called By: WWWFORMF, WWWFORM31     as js Event return values
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns : YRETVAL (several exits)
	;
	;
	; History :
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 03-Jan-2007	GRF		Doco; quits
	; 03.02.2004	FIS
	;-------------------------------------------------------------------------------
	new YBBN,YDATEI,YDATEI1,YERR,YFELD,YFIELDNAME,YHTML,YINHALT1,YKEY,YKILL,YLFN
	new YNEW,YOK,YOLD,YRETVAL,YROW,YTEXT,YYFELD,YYLFDAT
	
	set YRETVAL=""
	set YERR=0
	set YLFDAT=$get(YLFDAT)
	
	$$$LogR("",YINHALT_"<"_YLFDAT)
	
	;FOCUS VERSCHIEBEN ;defer 
	;---------------------------------------
	if YINHALT="MOVE" do  quit YRETVAL
	. set YMAX   = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","DIMENSION",1))
	. set YRICHT = $extract(YLFDAT,1,2)
	. set YROW   = $piece($extract(YLFDAT,6,99),"_",1)
	. set YCOL   = $piece(YLFDAT,"_",2)
	. ;
	. if YRICHT="37" do                                              ; LEFT
	. . for  set YCOL=YCOL-1 quit:YCOL<1  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL)) 
	. ;
	. if YRICHT="38" do                                              ; UP
	. . for  set YROW=YROW-1 quit:YROW<1  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL))&&('$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)))
	. ;
	. if YRICHT="39" do                                              ; RIGHT
	. . for  set YCOL=YCOL+1 quit:YCOL>$piece(YMAX,"_",3)  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL))
	. ;
	. if YRICHT="40" do                                              ; DOWN
	. . for  set YROW=YROW+1 quit:YROW>$piece(YMAX,"_",1)  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL))&&('$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)))
	. ;
	. if YRICHT="33" do                                              ; QUICK UP
	. . set YROW=YROW-10
	. . for  quit:YROW<1  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL))&&('$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)))  set YROW=YROW-1 
	. ;
	. if YRICHT="34" do                                              ; QUICK DOWN
	. . set YROW=YROW+10
	. . for  quit:YROW>$piece(YMAX,"_",1)  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL))&&('$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)))  set YROW=YROW+1 
	. ;
	. if YRICHT="35" set YCOL=$piece(YMAX,"_",3)                     ; FIRST COLUMN 
	. if YRICHT="36" set YCOL=0 do                                   ; PAGE DOWN
	. . for  set YCOL=YCOL+1 quit:YCOL>$piece(YMAX,"_",3)  quit:$data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE","Y"_YROW_"_"_YCOL))
	. ;
	. if YROW<1                  set YROW = 1
	. if YROW>$piece(YMAX,"_",1) set YROW = $piece(YMAX,"_",1)
	. if YCOL<1                  set YCOL = $piece(YLFDAT,"_",2)     ; OLD POSITION
	. if YCOL>$piece(YMAX,"_",3) set YCOL = $piece(YLFDAT,"_",2)     ; OLD POSITION
	. set YRETVAL="tdY"_YROW_"_"_YCOL
	
	;FOCUS NEUANLEGE VERSCHIEBEN ;defer 
	;---------------------------------------
	if YINHALT="MOVEKEY" do  quit YRETVAL
	. set YDATEI=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. if YDATEI="" quit
	. set YMAXKEY = $order(^WWW002(0,YDATEI,""),-1)  ;LETZTER KEY ;last KEY 
	 .set YRICHT  = $extract(YLFDAT,1,2)
	 .set YROW    = $piece($piece(YLFDAT,"key",2),"_",1)
	 .set YCOL    = $piece($piece(YLFDAT,"key",2),"_",2)
	. if YRICHT="37" set YCOL=YCOL-1                                 ; LEFT
	. if YRICHT="39" set YCOL=YCOL+1                                 ; RIGHT
	. if YCOL<1 set YCOL=1
	. if YCOL>YMAXKEY set YCOL=YMAXKEY
	. set YRETVAL="key"_YROW_"_"_YCOL
	
	
	;TABULATER -> VOR ODER NEUANLAGE ;pre- Or 
	;---------------------------------------
	if YINHALT="TABKEY" do  quit YRETVAL
	. set YMAX=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","DIMENSION",1))
	. set YROW=$piece($extract(YLFDAT,4,99),"_",1)
	. set YCOL=$piece(YLFDAT,"_",2)
	. do
	. . if YROW=$piece(YMAX,"_",1) if YCOL=$piece(YMAX,"_",3) set YRETVAL=45 quit  ;NEUANLAGE
	. . if YCOL=$piece(YMAX,"_",3)                            set YRETVAL=99 quit  ;ANFANG NEUE ZEILE ;outset 
	. . set YRETVAL=39
	
	
	;NEUEN DATENSATZ SPEICHERN ;data record Save 
	;---------------------------------------
	if YINHALT="SAVENEWLINE" do  quit YRETVAL
	. new YMAX,KEY,YERR
	. set YMAX   = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","DIMENSION",1))
	. set YDATEI = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. quit:YDATEI=""
	. set YMAXKEY = $order(^WWW002(0,YDATEI,""),-1)  ;LETZTER KEY ;last KEY 
	. set YLFN=1
	. for YLFN(1)=1:1:(YMAXKEY-$piece(YMAX,"_",4)) set YLFN=YLFN+1  ;START BEI KEY ;take-off next to KEY 
	. set YOLDKEY=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",$piece(YMAX,"_",1),1))
	. set YNEWKEY=""
	. set YERR=0
	. for YI=1:1:(YLFN-1)  set YNEWKEY=""""_$translate($piece(YOLDKEY,",",YI),"""")_""""_","
	. for YI=YLFN:1:YMAXKEY set KEY="add"_($piece(YMAX,"_",1)+1)_"_"_(YI-YLFN+1) do
	. . if $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",KEY,1))="" set YERR=1 quit
	. . set YNEWKEY=YNEWKEY_""""_$translate($get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",KEY,1)),"""")_""""_","
	. . kill ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",KEY)
	. . set YRETVAL=YRETVAL_" document.getElementById("_""""_"key"_$extract(KEY,4,99)_""""_").style.backgroundColor="_""""_"silver"_""""_";"
	. . set YRETVAL=YRETVAL_" document."_YHTMFORM_"."_KEY_".style.backgroundColor="_""""_"silver"_""""_";"
	. . set YRETVAL=YRETVAL_" document."_YHTMFORM_"."_KEY_".readOnly="_""""_"true"_""""_";"
	. . set YRETVAL=YRETVAL_" document."_YHTMFORM_"."_KEY_".unselectable="_""""_"true"_""""_";"
	. ;
	. if $extract($reverse(YNEWKEY))="," set YNEWKEY=$reverse($extract($reverse(YNEWKEY),2,999))
	. if ($translate(YNEWKEY,",")="") || (YERR=1) set YRETVAL="#FUNCTION~"_"alert("_""""_$$^WWWTEXT(391,,1)_" "_$$^WWWTEXT(32292,,1)_""""_"); yaddline=2;" quit  ;DATENSATZ BEREITS ANGELEGT
	. set YDATEI1="^"_YDATEI_"("_""""_$$^WWWYM(YDATEI)_""""_","_YNEWKEY_",1)"
	. if $get(@YDATEI1)'="" set YRETVAL="#FUNCTION~"_"alert("_""""_$$^WWWTEXT(3188,,1)_""""_"); yaddline=2;" quit  ;DATENSATZ BEREITS ANGELEGT
	. do
	. . new YFORM,YVOR,YOK,YEXEC
	. . set YFORM=YDATEI  ;WEGEN PROTOKOLL;FIS;09.02.05
	. . set YOK=$$^WWWSPEI(YDATEI,YNEWKEY,"",1)  ;SAVE
	. ;
	. set YNEWFOCUS=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","Y1_"))
	. if YNEWFOCUS'="" set YNEWFOCUS="tdY"_($piece(YMAX,"_",1)+1)_"_"_$piece(YNEWFOCUS,"_",2)
	. if YNEWFOCUS'="" set YRETVAL=YRETVAL_" moveFocus("_""""_YNEWFOCUS_""""_",1,"_""""_""""_",1);"
	. if YNEWFOCUS'="" set YRETVAL=YRETVAL_" updateRecord("_""""_($piece(YMAX,"_",1)+1)_""""_");"
	. set $piece(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","DIMENSION",1),"_",1)=($piece(YMAX,"_",1)+1)
	. set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",($piece(YMAX,"_",1)+1),1)=YNEWKEY
	. set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","NEWRECORD",($piece(YMAX,"_",1)+1),1)=YNEWKEY
	. set YLFDAT="Y1_"
	. for  set YLFDAT=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT)) quit:YLFDAT=""  quit:$piece($piece(YLFDAT,"_",1),"Y",2)'=1  do
	. . set YLFDAT1="Y"_($piece(YMAX,"_",1)+1)_"_"_$piece(YLFDAT,"_",2)
	. . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT1,1)=""
	. . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT1,2)=""
	. . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE",YLFDAT1,1)=""
	. ;
	. if YRETVAL'="" set YRETVAL="#FUNCTION~"_YRETVAL
	
	
	;ANLAGE NEUEN DATENSATZ ABBRECHEN ;layout data record Cancel 
	;---------------------------------------
	if YINHALT="KILLNEWLINE" do  quit YRETVAL
	. new YNEWFOCUS,YMAX,YI
	. set YMAX   = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","DIMENSION",1))
	. set YDATEI = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. if YDATEI="" quit
	. set YMAXKEY=$order(^WWW002(0,YDATEI,""),-1)  ;LETZTER KEY ;last KEY 
	. set YLFN=1
	. for YLFN(1)=1:1:(YMAXKEY-$piece(YMAX,"_",4)) set YLFN=YLFN+1  ;START BEI KEY ;take-off next to KEY 
	. for YI=YLFN:1:YMAXKEY set KEY="add"_($piece(YMAX,"_",1)+1)_"_"_(YI-YLFN+1) do
	. . kill ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",KEY)
	. ;
	. set YRETVAL=YRETVAL_"#FUNCTION~"_"document.all.tablegrid.deleteRow("_($piece(YMAX,"_",1)+1)_");"
	. set YRETVAL=YRETVAL_" document.getElementById("_""""_"focusfield"_""""_").value="_""""_""""_";"
	. set YNEWFOCUS=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","Y1_"))
	. if YNEWFOCUS'="" set YRETVAL=YRETVAL_" moveFocus("_""""_"td"_YNEWFOCUS_""""_",1,"_""""_""""_",1);"
	
	
	;NEUEN DATENSATZ ANLEGEN ;data record put onto 
	;---------------------------------------
	if YINHALT="CREATE" do  quit YRETVAL
	. new YDATEI,YMAXKEY,YSEI,YZEILE,YSPL,YBBN,YLFN,YTYP
	. set YMAX=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","DIMENSION",1))
	. ;SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","DIMENSION",1),"_",1)=$PIECE(YMAX,"_",1)+1
	. ;
	. set YDATEI=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. if YDATEI="" quit
	. set YMAXKEY=$order(^WWW002(0,YDATEI,""),-1)  ;LETZTER KEY ;last KEY 
	. set YLFN=1
	. for YLFN(1)=1:1:(YMAXKEY-$piece(YMAX,"_",4)) set YLFN=YLFN+1  ;START BEI KEY ;take-off next to KEY 
	. if YLFN'<YMAXKEY quit
	. set YHTML=""
	. set YHTML=YHTML_"  newRow=document.all.tablegrid.insertRow("_($piece(YMAX,"_",1)+1)_");"
	. ;
	. ;-------------------------------------
	. ;PRIMÄRSCHLÜSSEL
	. ;-------------------------------------
	. for YI=1:1:$piece(YMAX,"_",4) do
	. . set YTYP=$piece($get(^WWW002(0,YDATEI,(YLFN+YI-1),1)),Y,3)
	. . set YTEXT=""
	. . set YTEXT=YTEXT_"<input name='add"_($piece(YMAX,"_",1)+1)_"_"_YI_"' type=text maxlength=60"
	. . set YTEXT=YTEXT_" style='"
	. . set YTEXT=YTEXT_" border:none; padding-top:0; padding-bottom:0;"
	. . set YTEXT=YTEXT_" background-color:"_"lightgoldenrodyellow"_";"
	. . set YTEXT=YTEXT_" font-family:arial; font-size:10pt; font-weight:normal;'"
	. . set YTEXT=YTEXT_" onmousedown='if (yaddline==1) moveFocus("_""""_"key"_($piece(YMAX,"_",1)+1)_"_"_YI_""""_",1)'"
	. . set NEXT=YI+1
	. . if YI=$piece(YMAX,"_",4) set NEXT=1
	. . set YTEXT=YTEXT_" onKeyDown='if (event.keyCode == 13 || event.keyCode == 9) {"
	. . set YTEXT=YTEXT_"  moveFocus("_""""_"key"_($piece(YMAX,"_",1)+1)_"_"_NEXT_""""_",1,1);"
	. . set YTEXT=YTEXT_"  window.setTimeout("_""""_"document."_YHTMFORM_".add"_($piece(YMAX,"_",1)+1)_"_"_NEXT_".focus()"_""""_",10);"
	. . set YTEXT=YTEXT_"  window.event.returnValue=false; window.event.cancelBubble=true;"
	. . set YTEXT=YTEXT_" }'"
	. . set YTEXT=YTEXT_" onhelp="_""""_"javascript: window.event.returnValue = false; window.event.cancelBubble = true;"_""""
	. . set YTEXT=YTEXT_" onFocus='select();'"
	. . set YTEXT=YTEXT_" onBlur='validateField("_""""_"add"_($piece(YMAX,"_",1)+1)_"_"_YI_""""_",this.value,"_""""_YTYP_""""_","_""""_""""_");'"
	. . set YTEXT=YTEXT_" >"
	. . ;
	. . set YHTML=YHTML_"    newCell=newRow.insertCell(-1);"
	. . set YHTML=YHTML_"    newCell.id="_""""_"key"_($piece(YMAX,"_",1)+1)_"_"_YI_""""_";"
	. . set YHTML=YHTML_"    newCell.style.font="_""""_"10pt arial"_""""_";"
	. . set YHTML=YHTML_"    newCell.style.border="_""""_"1px outset"_""""_";"
	. . set YHTML=YHTML_"    newCell.style.backgroundColor="_""""_"lightgoldenrodyellow"_""""_";"
	. . set YHTML=YHTML_"    var ytext="_""""_$$ESC^WWWENCODE(YTEXT)_""""_";"
	. . set YHTML=YHTML_"    newCell.innerHTML=unescape(ytext);"
	. ;
	. ;-------------------------------------
	. ;DATENFELDER
	. ;-------------------------------------
	. set YSEI=""
	. for  set YSEI=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),YSEI)) quit:YSEI=""  do
	. . set YZEILE=""
	. . for  set YZEILE=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),YSEI,YZEILE)) quit:YZEILE=""  do
	. . . set YSPL=""
	. . . for  set YSPL=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),YSEI,YZEILE,YSPL)) quit:YSPL=""  do
	. . . . set YBBN=""
	. . . . for  set YBBN=$order(^WWW122s(0,3,$$^WWWUMLAU(YFORM,1),YSEI,YZEILE,YSPL,YFORM,YBBN)) quit:YBBN=""  do
	. . . . . new YTXT,YLFN,YTYP,REL,PARA,RELF,SUCH,YFIELDNAME,YLENGTH,YWIDTH,YCOLON,YCOLOFF,YVALIGN,YPADDING,YHEIGHT,YFONT,YFONTSIZE,YFOCUS
	. . . . . do PARA^WWWFORM31("D",YBBN)  ;SET PARAMETER ;table-mat parameter 
	. . . . . quit:$piece(YTXT,Y,2)=15  ;HIDDEN
	. . . . . set YFIELDNAME="tdY"_($piece(YMAX,"_",1)+1)_"_"_YBBN
	. . . . . ;
	. . . . . set YTEXT=""
	. . . . . if $piece(YTXT,Y,23)=5 set YTEXT=YTEXT_"<input type="_""""_"hidden"_""""_" id="_""""_"read"_$extract(YFIELDNAME,3,99)_""""_" value="_""""_1_""""_">"
	. . . . . set YHTML=YHTML_"    newCell=newRow.insertCell(-1);"
	. . . . . set YHTML=YHTML_"    newCell.id="_""""_YFIELDNAME_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.font="_""""_YFONTSIZE_"pt "_YFONT_" font-weight:normal;"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.border="_""""_"1px outset"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.backgroundColor="_""""_YCOLOFF_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.width="_""""_(YWIDTH+4)_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.height="_""""_(YHEIGHT+4)_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.marginTop="_""""_"0pt"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.marginLeft="_""""_"0pt"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.marginBottom="_""""_"0pt"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.marginRight="_""""_"0pt"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.padding="_""""_"0"_""""_";"
	. . . . .;SET YHTML=YHTML_"    newCell.style.paddingLeft="_""""_"2px"_""""_";"
	. . . . .;SET YHTML=YHTML_"    newCell.style.paddingRight="_""""_"2px"_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.style.backgroundColor="_""""_YCOLOFF_""""_";"
	. . . . . if (YTYP'=8) && (YTYP'=4) set YHTML=YHTML_"  newCell.style.textAlign="_""""_"left"_""""_";"
	. . . . . if (YTYP=8)  || (YTYP=4)  set YHTML=YHTML_"  newCell.style.textAlign="_""""_"right"_""""_";"
	. . . . . set YHTML=YHTML_"    var ytext="_""""_$$ESC^WWWENCODE(YTEXT)_""""_";"
	. . . . . set YHTML=YHTML_"    newCell.innerHTML=unescape(ytext);"
	. ;
	. set YHTML=YHTML_" moveFocus("_""""_"key"_($piece(YMAX,"_",1)+1)_"_"_"1"_""""_",1);"
	. set YHTML=YHTML_" window.setTimeout("_""""_"document."_YHTMFORM_".add"_($piece(YMAX,"_",1)+1)_"_"_"1.focus()"_""""_",10);"
	. set YRETVAL=YRETVAL_"#FUNCTION~"_YHTML
	
	
	;
	;ÄNDERUNGEN RÜCKGÄNGIG MACHEN ;making 
	;----------------------------
	if YINHALT="UNDO" do  quit YRETVAL
	. new YCHANGE
	. set YCHANGE=""
	. set YDATEI=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. quit:YDATEI=""
	. set YROW=""
	. for  set YROW=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",YROW)) quit:YROW=""  do
	. . set YKEY=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",YROW,1))
	. . set YDATEI1="^"_YDATEI_"("_""""_$$^WWWYM(YDATEI)_""""_","_$$^WWWKEYBUILD(YKEY)_",1)"
	. . ;IF '$DATA(YDATEI1) QUIT
	. . set YFELD=$get(@YDATEI1)
	. . set YYFELD=YFELD
	. . ;
	. . set YLFDAT="Y"_YROW_"_"
	. . for  set YLFDAT=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT)) quit:YLFDAT=""  quit:$piece($piece(YLFDAT,"_",1),"Y",2)'=YROW  do
	. . . set YNEW=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,1))  ;AKTUELLER INHALT ;purport 
	. . . set YOLD=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,2))  ;URSPRÜNGLICHER INHALT ;purport 
	. . . if $data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)) set YNEW="######"
	. . . if $data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","NEWRECORD",YROW))    set YNEW="######"
	. . . if YNEW=YOLD quit                          ;KEINE ÄNDERUNG ;no alteration 
	. . . set YBBN=$piece(YLFDAT,"_",2)
	. . . set YLFN=$piece($get(^WWW122(0,YFORM,+YBBN,1)),Y,1)
	. . . quit:+YLFN=0
	. . . set YTYP=$piece($get(^WWW003(0,YDATEI,YLFN,1)),Y,3)
	. . . set YTEXT=$$GetLiteral^WWWTR(YTYP,YOLD)
	. . . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE",YLFDAT,1)=YTEXT  ;SPEICHERN ANZEIGEFORMAT ;Save 
	. . . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,1)=YOLD                    ;SPEICHERN INHALT ;Save purport 
	. . . if $piece(YFELD,Y,YLFN)=YOLD quit                                            ;KEINE ÄNDERUNG ;no alteration 
	. . . set $piece(YFELD,Y,YLFN)=YOLD
	. . ;
	. . if YFELD'=YYFELD do
	. . . ;IF '$DATA(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","NEWRECORD",YROW)) DO
	. . . do
	. . . . new YFORM,YVOR,YOK
	. . . . set YFORM=YDATEI  ;WEGEN PROTOKOLL;FIS;09.02.05
	. . . . set YOK=$$^WWWSPEI(YDATEI,YKEY,YFELD,1)
	. . . ;
	. . . ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv Disabled block start
	. . . ;IF $DATA(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","NEWRECORD",YROW)) DO
	. . . . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)=1
	. . . . new YFORM,YVOR,YOK
	. . . . set YFORM=YDATEI  ;WEGEN PROTOKOLL;FIS;09.02.05
	. . . . do ^WWWKILL(YDATEI,YKEY)
	. . . ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ Disabled block end
	. . . set YCHANGE=YCHANGE_" updateRecord("_""""_YROW_""""_");"
	. . . kill ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)
	. ;
	. if YCHANGE'="" set YRETVAL="#FUNCTION~"_YCHANGE
	
	
	;UPDATE ALLER FELDER 
	;-------------------
	if YINHALT="UPDATE" do  quit YRETVAL
	. set YDATEI=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. if YDATEI="" quit
	. if $get(YLFDAT)="" quit
	. ;
	. if +YLFDAT'=0 do  quit  ;UPDATE GANZE ZEILE
	. . set YROW=+YLFDAT
	. . set YKEY=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",+YROW,1))
	. . if YKEY="" quit
	. . set YLFDAT="Y"_YROW_"_"
	. . for  set YLFDAT=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT)) quit:YLFDAT=""  quit:$piece($piece(YLFDAT,"_",1),"Y",2)'=YROW  do
	. . . set YBBN=$piece(YLFDAT,"_",2)
	. . . if +YBBN=0 quit
	. . . new YTXT,YLFN,YTYP,REL,PARA,RELF,SUCH,YFIELDNAME,YLENGTH,YWIDTH,YCOLON,YCOLOFF,YVALIGN,YPADDING,YHEIGHT,YFONT,YFONTSIZE
	. . . set YFIELDNAME=YLFDAT
	. . . do PARA^WWWFORM31("D",YBBN)
	. . . if +YLFN=0 quit
	. . . set YINHALT=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,1))  ;AUS WWWDATEN HOLEN (SPEICHERFORMAT) ;out of send for 
	. . . set YTEXT=$$GetLiteral^WWWTR(YTYP,YINHALT)
	. . . if YTYP=2 set YTEXT=+YTEXT  ;FIS;31.08.04;26346
	. . . if $data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)) set YTEXT="######"
	. . . set YHTML=$$TEXT^WWWFORM31F()
	. . . if YHTML'="" do
	. . . . set YHTML=$$ESC^WWWENCODE(YHTML)
	. . . . set YRETVAL=YRETVAL_" inactivateField("_""""_YLFDAT_""""_","_""""_YHTML_""""_","_""""_YCOLOFF_""""_");"
	. . ;
	. . if YRETVAL'="" set YRETVAL="#FUNCTION~"_YRETVAL
	. ;
	. if +YLFDAT=0 do  ;UPDATE NACH FELDÄNDERUNG ;within 
	. . new YCOLOFFX
	. . set YROW=$extract($piece(YLFDAT,"_",1),2,999)
	. . if +YROW=0 quit
	. . set YKEY=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",YROW,1))
	. . if YKEY="" quit
	. . set YHTML=""
	. . do
	. . . if $data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)) quit
	. . . set YDATEI1="^"_YDATEI_"("_""""_$$^WWWYM(YDATEI)_""""_","_$$^WWWKEYBUILD(YKEY)_",1)"
	. . . if '$data(YDATEI1) quit
	. . . set YFELD=@YDATEI1
	. . . set YYLFDAT=YLFDAT
	. . . set YLFDAT="Y"_YROW_"_"
	. . . for  set YLFDAT=$order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT)) quit:YLFDAT=""  quit:$piece($piece(YLFDAT,"_",1),"Y",2)'=YROW  do
	. . . . quit:YLFDAT=YYLFDAT
	. . . . set YINHALT=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,1))  ;AKTUELLER INHALT ;purport 
	. . . . set YBBN=$piece(YLFDAT,"_",2)
	. . . . if +YBBN=0 quit
	. . . . set YLFN=$piece($get(^WWW122(0,YFORM,YBBN,1)),Y,1)
	. . . . quit:+YLFN=0
	. . . . if YINHALT=$piece(YFELD,Y,YLFN) quit  ;KEINE ÄNDERUNG ;no alteration 
	. . . . ;
	. . . . new YTXT,YLFN,YTYP,REL,PARA,RELF,SUCH,YFIELDNAME,YLENGTH,YWIDTH,YCOLON,YCOLOFF,YVALIGN,YPADDING,YHEIGHT,YFONT,YFONTSIZE,YFOCUS
	. . . . set YFIELDNAME=YLFDAT
	. . . . do PARA^WWWFORM31("D",YBBN)
	. . . . set YTEXT=$$GetLiteral^WWWTR(YTYP,$piece(YFELD,Y,YLFN))
	. . . . if YTYP=2 set YTEXT=+YTEXT  ;FIS;31.08.04;26346
	. . . . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE",YLFDAT,1)=YTEXT  ;SPEICHERN ANZEIGEFORMAT ;Save 
	. . . . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,1)=$piece(YFELD,Y,YLFN)    ;SPEICHERN INHALT ;Save purport 
	. . . . ;
	. . . . set YHTML=$$TEXT^WWWFORM31F()
	. . . . ;SET YCOLLOFFX=YCOLLOFF
	. . . . set YCOLOFFX=YCOLOFF       ;BEC;27422;02.03.05;
	. . ;
	. . if YHTML'="" do
	. . . set YHTML=$$ESC^WWWENCODE(YHTML)
	. . . set YRETVAL=YRETVAL_" inactivateField("_""""_YLFDAT_""""_","_""""_YHTML_""""_","_""""_$get(YCOLOFFX)_""""_");"
	. . ;
	. . if YRETVAL'="" set YRETVAL="#FUNCTION~"_YRETVAL
	
	
	;  Save Changes            ; ÄNDERUNGEN SPEICHERN
	;---------------------------------------
	if YLFDAT'="" do
	. set YDATEI=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCECLASS",1))
	. if YDATEI=""        set YERR=1 quit
	. set YROW=$extract($piece(YLFDAT,"_",1),2,999)
	. if +YROW=0          set YERR=1 quit
	. set YKEY=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEKEY",YROW,1))
	. if YKEY=""          set YERR=1 quit
	. if $find(YKEY,",,") set YERR=1 quit
	. set YBBN=$piece(YLFDAT,"_",2)
	. if +YBBN=0          set YERR=1 quit
	. ;
	. new YTXT,YLFN,YTYP,REL,PARA,RELF,SUCH,YFIELDNAME,YLENGTH,YWIDTH,YCOLON,YCOLOFF,YVALIGN,YPADDING,YHEIGHT,YFONT,YFONTSIZE,YFOCUS
	. set YFIELDNAME=YLFDAT
	. do PARA^WWWFORM31("D",YBBN)
	. if +YLFN=0          set YERR=1 quit
	. ;
	. set YKILL=0
	. if YINHALT="KILL" set YKILL=1
	. if $data(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)) set YKILL=2  ;BEREITS GELÖSCHT 
	. ;
	. ;-------------------------------------
	. ;  Save                               ;SPEICHERN
	. ;-------------------------------------
	. if YKILL=0 do 
	. . set YINHALT=$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V",YLFDAT,1))  ;AUS WWWDATEN HOLEN (SPEICHERFORMAT) ;out of send for 
	. . set YTEXT=$$GetLiteral^WWWTR(YTYP,YINHALT)
	. . if YTYP=2 set YTEXT=+YTEXT  ;FIS;31.08.04;26346
	. . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE",YLFDAT,1)=YTEXT  ;SPEICHERN ANZEIGEFORMAT ;Save 
	. . ;
	. . set YDATEI1="^"_YDATEI_"("_""""_$$^WWWYM(YDATEI)_""""_","_$$^WWWKEYBUILD(YKEY)_",1)"
	. . if '$data(YDATEI1) set YERR=1 quit
	. . set YFELD=@YDATEI1
	. . if $piece(YFELD,Y,YLFN)=YINHALT quit                ;KEINE ÄNDERUNG ;no alteration 
	. . set $piece(YFELD,Y,YLFN)=YINHALT
	. . do
	. . . new YFORM,YVOR,YOK,YEXEC
	. . . set YFORM=YDATEI                                  ;WEGEN PROTOKOLL;FIS;09.02.05
	. . . set YOK=$$^WWWSPEI(YDATEI,YKEY,YFELD,1)           ;SAVE
	. . ;
	. . ;-----------------------------------
	. . ; *** EXECUTE ***                   $$$WWW120ExecuteOnAfterSaveData()
	. . ;-----------------------------------
	. . if $piece($get(^WWW120(0,YFORM,1)),Y,16)'="" do     ;EXECUTE AFTER SAVE ;BEC;25866;07.06.04;DA ZENTRALE DATEI
	. . . set YEXEC=$piece($get(^WWW120(0,YFORM,1)),Y,16)   ;BEC;25866;07.06.04;DA ZENTRALE DATEI
	. . . xecute YEXEC
	. ;
	. ;-------------------------------------
	. ;  Delete                             ;LÖSCHEN
	. ;-------------------------------------
	. if YKILL=1 do
	. . set YDATEI1="^"_YDATEI_"("_""""_$$^WWWYM(YDATEI)_""""_","_$$^WWWKEYBUILD(YKEY)_",1)"
	. . if '$data(YDATEI1) set YERR=1 quit
	. . do
	. . . new YFORM,YVOR,YOK,YEXEC,YDATEI1
	. . . set YFORM=YDATEI                                  ;WEGEN PROTOKOLL;FIS;09.02.05
	. . . do ^WWWKILL(YDATEI,YKEY)                          ;DELETE
	. . ;
	. . ;-----------------------------------
	. . ; *** EXECUTE ***                   $$$WWW120ExecuteAfterDataDeleted()
	. . ;-----------------------------------
	. . if $piece($get(^WWW120(0,YFORM,1)),Y,17)'="" do     ;EXECUTE AFTER DELETE
	. . . set YEXEC=$piece($get(^WWW120(0,YFORM,1)),Y,17)
	. . . xecute YEXEC
	. . ;
	. . set YTEXT=""
	. . set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","KILLEDRECORD",YROW)=1
	. ;
	. ;-------------------------------------
	. ;  Killed Record                      ;BEREITS GELÖSCHT  /  Already Cleared
	. ;-------------------------------------
	. if YKILL=2 do
	. . set YTEXT="######"
	. ;
	. set YHTML=$$TEXT^WWWFORM31F()
	. ;
	. if YHTML'="" do
	. . set YHTML   = $$ESC^WWWENCODE(YHTML)
	. . set YINHALT = $$ESC^WWWENCODE(YINHALT)
	. . set YRETVAL = "#FUNCTION~inactivateField("_""""_YLFDAT_""""_","_""""_YHTML_""""_","_""""_YCOLOFF_""""_",1);"
	. . if YKILL=1 set YRETVAL=YRETVAL_" deleteRow("_""""_YROW_""""_");"
	
	
	if YERR'=0 set YRETVAL="!"_$$^WWWTEXT(391,,1)_" "_$$^WWWTEXT(32292,,1)  ; "Not Saved! Check Please!"
	quit YRETVAL
	
]]></Routine>
</Export>