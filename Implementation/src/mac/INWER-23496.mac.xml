<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWER" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWER
#include INConst
#include COMSYS
	/*-------------------------------------------------------------------------------
	; Description of Function :
	;		WAREN RESERVIERUNG 
	;		Reserve Goods
	;
	; Inputs : 
	;	YKEY
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 26-Feb-2007	GRF		SR15456: Stop second "OnAfterSave" call being processed
	; 							if form is reloaded  ("Sticker" button needs to use
	; 							browser back arrow);  Class Macros
	; 13-Nov-2006	GRF		SR15216: Remove FIXME (done); !=>||; doco
	; 05-Apr-2006	GRF		Doco
	; 01.Oct.2001	FAN		Created
	;-----------------------------------------------------------------------------*/
	NEW ART,BET,LAP,WED,INWE1,MNG
	
	QUIT:$PIECE($GET(YKEY),",",1)=""          SET ART  = $PIECE($GET(YKEY),",",1)    ;ARTIKEL          ; Item 
	QUIT:$PIECE($GET(YKEY),",",2)=""          SET BET  = $PIECE($GET(YKEY),",",2)    ;BETRIEB          ; Site Location
	QUIT:$PIECE($GET(YKEY),",",3)=""          SET LAP  = $PIECE($GET(YKEY),",",3)    ;LAGER            ; Stock Location
	QUIT:$PIECE($GET(YKEY),",",4)=""          SET WED  = $PIECE($GET(YKEY),",",4)    ;WEDATUM          ; Date
	QUIT:$PIECE($GET(YKEY),",",5)=""          SET RENR = $PIECE($GET(YKEY),",",5)    ;RESERVIERUNGS NR ; Reservation Number
	
	quit:$$^WWWRELOAD()    ; SR15456
	
	;+++++++++++++++++++++++++++++++++++++++
	; INWER1		objReserve		Reservation
	; 		D4				$$$INWERLaybyQuantity()
	; 		D10				$$$INWERQuantity()
	; 
	; INWE1			objOnHand		Quantity On Hand
	; 		D4				$$$INWEQuantity()
	; 		D37				$$$INWETotalReservedQuantity()
	;+++++++++++++++++++++++++++++++++++++++
	
	SET INWER1=$GET(^INWER(YM,ART,BET,LAP,WED,RENR,1))
	QUIT:INWER1="" 
	
	SET INWE1=$GET(^INWE(YM,ART,BET,LAP,WED,1))
	QUIT:INWE1=""
	
	if ($$$INWERLaybyQuantity(INWER1)'>0) || ($$$INWERLaybyQuantity(INWER1)>($$$INWEQuantity(INWE1))) {
		DO LOST
		DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$$^WWWTEXT(31407)_")")            ; "Check Please ! (Quantity)
		SET Q=1
	} elseif ($$$INWERHoldUntilDate(INWER1)'="") && ($$$INWERHoldUntilDate(INWER1)<$HOROLOG) {
		DO LOST
		DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$$^WWWTEXT(124)_")")              ; "Check Please ! (Date)
		SET Q=1
	} else {
		DO RESER
	}
	QUIT
	
LOST
	;-------------------------------------------------------------------------------
	;	Cancel the save if an error occurs by deleting the saved record.
	;	SHOULD be onBeforeSave validation!
	;		
	;	LÖSCHEN DATENSATZ BEI ERSSTMAL ERFASSUNGEN ;Delete data record next to 
	; No Qty
	; 
	; FIXME : Does this leave zero (i.e. non zero) values still present? <GRF>
	;-------------------------------------------------------------------------------
	IF $$$INWERQuantity(INWER1)="" DO 
	. KILL ^INWER(YM,ART,BET,LAP,WED,RENR)
	. DO ^WWWSKILL("INWER",ART_","_BET_","_LAP_","_WED_","_RENR)
	
	QUIT
	
RESER
	;-------------------------------------------------------------------------------
	; 	Add to Reservation     ;RESERVIERUNG ANLEGEN
	;		INWER D10 IST FÜR ÄNDERUNG GEDACHT. ;to alteration 
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 26-Feb-2007	GRF		SR15456: uncomment line to increment INWE Reserved Qty.
	; 21-Aug-2006	GRF		SR15216: Add Audit as 8th parameter to INARTHIST call
	; 09-Nov-2006	PO		SR15209: Log SOH movement before and after
	; 23-Aug-2006	JW		SR14934: INARTHIST call should be information only.
   	; 05-Jul-2006	Pablo	SRBR014077: Update the INARTHISTL global with the quantity deleted
	;-------------------------------------------------------------------------------
	NEW TEXT
	
	;UMBUCHEN ;rebook 
	SET $$$INWETotalReservedQuantity(INWE1) = $$$INWETotalReservedQuantity(INWE1) + $$$INWERLaybyQuantity(INWER1)   ; SR15456
	SET $$$INWEQuantity(INWE1)              = $$$INWEQuantity(INWE1)              - $$$INWERLaybyQuantity(INWER1)
	
	if $get(^SysSetup(15209,"BOR")) do Log^INReservationHistory($$$INWERQuantity(INWER1), ART, BET, LAP, WED, $$$YES, "BOR", RENR) // SR15209
	
	DO
	. NEW KEY,YFORM,YVOR
	. SET KEY=ART_","_BET_","_LAP_","_WED
	. SET OK=$$^WWWSPEI("INWE",KEY,INWE1,1)
	
	if $get(^SysSetup(15209,"BOR")) do Log^INReservationHistory($$$INWERQuantity(INWER1), ART, BET, LAP, WED, $$$NO, "BOR", RENR) // SR15209
	
	; Reserved for unknown person     ;Reservieren für  XXXX ?
	if ($$$INWERForCustomer(INWER1)="")  && 
	   ($$$INWEROrForAddress(INWER1)="") &&
	   ($$$INWEROrForOffer(INWER1)="")   &&
	   ($$$INWEROrForPerson(INWER1)="")  &&
	   ($$$INWEROrForOrder(INWER1)="")      { 
		set $$$INWEROrForPerson(INWER1)="?"
		set $$$INWEROrForPerson(^INWER(YM,ART,BET,LAP,WED,RENR,1))="?"
	}
	
	; "Customer"/"Customer"/"Order"/"Offer"/"Others"
	;            (Address)                  (Person)
	SET TEXT=""
	
	set idDetail = $$$INWERForCustomer(INWER1)
	if idDetail'="" set TEXT=$$^WWWTEXT(32231)_": ("_idDetail_") "_$PIECE($GET(^INKUNDE(YM,idDetail,1)),Y,8) 
	
	set idDetail = $$$INWEROrForAddress(INWER1)
	if idDetail'="" set TEXT=$$^WWWTEXT(32669)_": ("_idDetail_") "_$PIECE($GET(^INWERBADR(YM,idDetail,1)),Y,8) 
	
	set idDetail = $$$INWEROrForOrder(INWER1)
	if idDetail'="" set TEXT=$$^WWWTEXT(32047)_": ("_idDetail_") "_$PIECE($GET(^INAUF1(YM,idDetail,1)),Y,1) 
	
	set idDetail = $$$INWEROrForOffer(INWER1)
	if idDetail'="" set TEXT=$$^WWWTEXT(32169)_": ("_idDetail_") "_$PIECE($GET(^INANG1(YM,idDetail,1)),Y,1) 
	
	set idDetail = $$$INWEROrForPerson(INWER1)
	if idDetail'="" set TEXT=$$^WWWTEXT(32347)_": "_idDetail
	
	; "Reserved For ###"		   ;Z.B. RESERVIERT FÜR KUNDE 55(TEU)
	set TEXT=$$^WWWTEXT(32480)_" "_$$^WWWTEXT(32807)_" "_TEXT
	set TEXT=TEXT_" / "_ART_" / "_BET_" / "_LAP_" / "_$$^WWWDATE(WED)_" / "_RENR // SRBR014077
	
	;do Information^INARTHIST(ART,TEXT,BET,,$$$INWERLaybyQuantity(INWER1),,LAP)	               // SR14934: Don't post to FIN
	do Information^INARTHIST(ART,TEXT,BET,,$$$INWERLaybyQuantity(INWER1),,LAP,"RESER^INWER")	; SR15216
	
	QUIT
	
AUS(ART,BET,LAP,WED,RENR,VON)   
	;-------------------------------------------------------------------------------
	; RESERVIERUNG AUFLÖSEN -> AUFRUF
	; 
	; Cancel a reservation if the HoldUntilDate is past
	;
	; Called By :
	; 	Routines - INAUFWH, WER^INTAGES6, INWERES, INWERMENGE
	; 	Buttons on form INWER (obsolete?)
	; 	
	; Params:
	; 	ART		- Item Id
	; 	BET		- Site Location
	; 	LAP		- Stock Location
	; 	WED		- Receipt Date
	; 	RENR	- Reservation Id
	; 	VON		- What cancelled the reservation - e.g. text 33557 "System"
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 26-Feb-2007	GRF		SR15456: Class Macros; obj* variables
	; 21-Aug-2006	GRF		SR15216: Add Audit as 8th parameter to INARTHIST call
	; 09-Nov-2006	PO		SR15209: Log SOH movement before and after
	; 23-Aug-2006	JW		SR14934: INARTHIST call should be information only.
	; 06-Jul-2006	PO		SR14669: When the reservation is cancelled and the
	; 						reservation quantity is added back to SOH the reserved
	; 						quantity is now also removed from the Total Reserved Quantity.
   	; 05-Jul-2006	Pablo	SRBR014077: Update the INARTHISTL global with the quantity deleted
   	; 04.08.2003	FAN		24045
	;-------------------------------------------------------------------------------
	new INWE1,objOnHand,objReserve,TEXT
	
	IF ($GET(RENR)="") || ($GET(WED)="") || ($GET(LAP)="") || ($GET(BET)="") || ($GET(ART)="") DO  QUIT
	. IF YFORM'="INWER" QUIT
	. SET %("VAR","YFORM")="INWER"
	. DO ^WWWFORM
	
	SET objReserve=$GET(^INWER(YM,ART,BET,LAP,WED,RENR,1))
	IF (objReserve="") || ($$$INWERReservationcancledAt(objReserve)'="") DO  QUIT  ;IF BEREITS AUFGELÖST, DENN IST NICHT MEHR ZURÜCKBUCHEN.
	. IF YFORM'="INWER" QUIT
	. SET %("VAR","YFORM")="INWER"
	. DO ^WWWFORM
	
	SET objOnHand = $GET(^INWE(YM,ART,BET,LAP,WED,1))  ;FIS;02.01.04;RESERVIERUNG IMMER AUFLÖSEN ! ;constantly undo 
	
	if $get(^SysSetup(15209,"BOR")) do Log^INReservationHistory($$$INWERQuantity(objReserve), ART, BET, LAP, WED, $$$YES, "CBR", RENR) // SR15209
	
	;UMBUCHEN ;rebook 
	set $$$INWETotalReservedQuantity(objOnHand) = $$$INWETotalReservedQuantity(objOnHand) - $$$INWERLaybyQuantity(objReserve) ; SR14669
	set $$$INWEQuantity(objOnHand)              = $$$INWEQuantity(objOnHand)              + $$$INWERLaybyQuantity(objReserve)
	
	DO
	. NEW KEY,YFORM,YVOR
	. SET KEY=ART_","_BET_","_LAP_","_WED
	. SET OK=$$^WWWSPEI("INWE",KEY,objOnHand,1)
	
	SET $$$INWERReservationcancledAt(objReserve) = +$HOROLOG   ;AUFGELÖST AM
	SET $$$INWERReservationCancledBy(objReserve) = $GET(VON)   ;AUFGELÖST VON
	DO
	. NEW KEY,YFORM,YVOR
	. SET KEY=ART_","_BET_","_LAP_","_WED_","_RENR
	. SET YOK=$$^WWWSPEI("INWER",KEY,objReserve,1)
	
	if $get(^SysSetup(15209,"BOR")) do Log^INReservationHistory($$$INWERQuantity(objReserve), ART, BET, LAP, WED, $$$NO, "CBR", RENR) // SR15209
	
	; "Undo Layby"		    ;MENGE reserviert AUFLÖSEN
	SET TEXT=$$^WWWTEXT(33066)_" / "_ART_" / "_BET_" / "_LAP_" / "_$$^WWWDATE(WED)_" ("_$GET(VON)_") / "_RENR // SRBR014077
	
	;do Information^INARTHIST(ART,TEXT,BET,$$$INWERLaybyQuantity(objReserve),,,LAP)	           // SR14934: Don't post to FIN
	do Information^INARTHIST(ART,TEXT,BET,$$$INWERLaybyQuantity(objReserve),,,LAP,"AUS^INWER")	; SR15216
	
	IF YFORM="INWER" DO
	. DO ^INWERLIST($PIECE($GET(YKEY),",",1),$PIECE($GET(YKEY),",",2),$PIECE($GET(YKEY),",",3),$PIECE($GET(YKEY),",",4))
	
	QUIT
	
]]></Routine>
</Export>