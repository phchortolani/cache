<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSMTP2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSMTP2
#include COMSYS
	QUIT
	
BIGSEND(SMTPDOMAIN,FROM,TO,SUBJECT,TIMEZONE,SMTPPORT,DISPLAYFROM,DISPLAYTO,USER,PASS,REQUEST)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		EMAIL MIT CSP
	;
	; Inputs : 
	;	SMTPDOMAIN		Host                    e.g. "mail.service.com"
	;	FROM			Sender                  e.g. "support@service.com"
	;	TO				Recipient               e.g. "user.name@customer.com"
	;	SUBJECT			E-mail Subject Line     e.g. "Server Alert"
	;	TIMEZONE		GMT                     from $$$WWW012GMTOffsethour()
	;	SMTPPORT		Port Number             from $$$WWW012PortNumberSMTP()
	;	DISPLAYFROM		Displayed Sender        e.g. "Support Centre"
	;	DISPLAYTO		Displayed Recipient     e.g. "John Smith"
	;	USER			User Name               Where mail requires authorisation (?)
	;	PASS			Password					ditto
	;
	; ByRef :
	;	REQUEST				"MAILOUT"_YUSER
	;	^WWWSOR(REQUEST)	contains text to be output
	;
	; Returns :
	;
	; History :
	; 22-Mar-2007	GRF		SR15046: Restored BIGSEND and created SMTPSend from 
	; 						20-Mar-2007 version; add REQUEST argument to restored
	; 						BIGSEND but not HTML flag.
	; 18-Jan-2007	GRF		SR15046: doco; dot levels
	; 22.07.2004	BEC		SERVICE 25806
	;-------------------------------------------------------------------------------
	NEW attached,err,m,nestedm,s,status
	
	SET SMTPDOMAIN  = $GET(SMTPDOMAIN)
	SET FROM        = $GET(FROM)
	SET TO          = $GET(TO)
	SET SUBJECT     = $GET(SUBJECT)
	SET TIMEZONE    = $GET(TIMEZONE)
	SET SMTPPORT    = $GET(SMTPPORT)
	SET DISPLAYFROM = $GET(DISPLAYFROM)
	SET DISPLAYTO   = $GET(DISPLAYTO)
	SET USER        = $GET(USER)              ;FOR MAIL WITH AUTH
	SET PASS        = $GET(PASS)
	
	SET REQUEST     = $GET(REQUEST)
	
	SET $ZTRAP="^WWWERROR"
	DO LOW^%PRIO
	IF SMTPPORT="" SET SMTPPORT=25            ;DEFAULT
	
	SET s=##class(%Net.SMTP).%New()
	SET s.smtpserver=SMTPDOMAIN               ;SMTPHOST
	SET s.port=SMTPPORT
	SET m=##class(%Net.MailMessage).%New()
	
	IF USER'="" IF PASS'="" DO
	. set auth=##class(%Net.Authenticator).%New() ; use default authentication list
	. set auth.UserName=USER
	. set auth.Password=PASS
	. set s.authenticator=auth
	
	SET m.From=DISPLAYFROM_" <"_FROM_">"      ;FROM ADRESS
	DO m.To.Insert(DISPLAYTO_" <"_TO_">")     ;TO ADRESS
	SET m.Subject=SUBJECT                     ;MAILSUBJECT
	SET m.Charset="iso-8859-1"
	SET m.IsHTML=1
	IF TIMEZONE'="" SET s.timezone=TIMEZONE
	
	;TEXT:/ATTACH IN ^WWWSOR(REQUEST)
	NEW NUM,LINE,ATTACH
	
	SET ATTACH=""
	;M ^TEST1=^WWWSOR(REQUEST)
	IF REQUEST'="" SET NUM="" FOR  SET NUM=$ORDER(^WWWSOR(REQUEST,NUM)) QUIT:NUM=""  DO
	. SET LINE=$GET(^WWWSOR(REQUEST,NUM))
	. IF '$FIND(LINE,"#attach") DO
	. . DO m.TextData.Write(LINE)
	. . DO m.TextData.Write($char(13,10))     ;NEW LINE
	. ;
	. IF $FIND(LINE,"#attach") DO
	. . NEW PATH,FILE,TR
	. . IF $FIND($ZVERSION,"Windows")  SET LINE=$TRANSLATE(LINE,"/","\") SET TR="\"
	. . IF '$FIND($ZVERSION,"Windows") SET LINE=$TRANSLATE(LINE,"\","/") SET TR="/"
	. . SET FILE=$TRANSLATE($PIECE(LINE,"#attach",2)," ")
	. . SET FILE=$PIECE(FILE,TR,$LENGTH(FILE,TR))
	. . SET PATH=$TRANSLATE($PIECE(LINE,"#attach",2)," ")
	. . SET PATH=$PIECE(PATH,FILE,1)
	. . SET status=m.AttachFile(PATH,FILE)
	. . SET ATTACH=1
	
	IF ATTACH=1 SET nestedm=m.AttachNewMessage()
	SET status=s.Send(m)     ;SEND MAIL
	DO NORMAL^%PRIO
	QUIT status
	
	
SMTPSend(REQUEST,SMTPDOMAIN="",FROM="",TO="",SUBJECT="",TIMEZONE="",SMTPPORT=25,DISPLAYFROM="",DISPLAYTO="",USER="",PASS="",pblnHTML=$$$YES,SSLConfig="",MessageStream="")
	;-------------------------------------------------------------------------------
	;
	; Inputs : 
	;	REQUEST			Array identifier		"MAILOUT"_YUSER
	;	SMTPDOMAIN		Host                    e.g. "mail.service.com"
	;	FROM			Sender                  e.g. "support@service.com"
	;	TO				Recipient               e.g. "user.name@customer.com"
	;	SUBJECT			E-mail Subject Line     e.g. "Server Alert"
	;	TIMEZONE		GMT                     from $$$WWW012GMTOffsethour()
	;	SMTPPORT		Port Number             from $$$WWW012PortNumberSMTP()
	;	DISPLAYFROM		Displayed Sender        e.g. "Support Centre"
	;	DISPLAYTO		Displayed Recipient     e.g. "John Smith"
	;	USER			User Name               Where mail requires authorisation (?)
	;	PASS			Password					ditto
	;	pblnHTML		Set output to HTML format (or plain text if NO)
	;	
	; ByRef :
	;	^WWWSOR(REQUEST,NUM)	contains text to be output
	;
	; Returns :
	;	send mail status
	;	
	; History :
	; 23-Mar-2007	GRF		SR15046: Don't write "<BR>" as new line in HTML - may be
	; 						passing a table structure - include in "text to array" routine.
	; 22-Mar-2007	GRF		SR15046: Restored BIGSEND and created SMTPSend from 
	; 						20-Mar-2007 version; adjust argument line and test REQUEST
	; 20-Mar-2007	GRF		SR15046: Change IsHTML flag; boolean macros; brace format;
	; 						initialise parameters instead of set/get; explicitly pass
	; 						REQUEST; allow choice of HTML or not; use %Library.File functions.
	;-------------------------------------------------------------------------------
	new auth,blnAttach,m,nestedm,NUM,strLine,s,status,strFile,strNewLine,strOldTR,strPath,strTR
	
	if (($get(REQUEST)="") && ('$IsObject(MessageStream))) set status=$$$NO quit    ; FIXME : error handling <GRF>
	
	set $ztrap="^WWWERROR"
	do LOW^%PRIO
	if SMTPPORT="" set SMTPPORT=25               ; DEFAULT
	set strNewLine = $char(13,10)
	
	if SSLConfig'="" {
		set s = ##class(VAR.infra.mail.SMTP).%New()
		set s.SSLConfig=SSLConfig
	} else {
		set s = ##class(%Net.SMTP).%New()
	}
	set s.smtpserver = SMTPDOMAIN                ; SMTPHOST
	set s.port       = SMTPPORT
	set m = ##class(%Net.MailMessage).%New()
	
	if (USER'="") && (PASS'="") {
		set auth=##class(%Net.Authenticator).%New() ; use default authentication list
		set auth.UserName   = USER
		set auth.Password   = PASS
		set s.authenticator = auth
	}
	
	set m.From    = DISPLAYFROM_" <"_FROM_">"    ; FROM ADRESS
	do m.To.Insert(DISPLAYTO_" <"_TO_">")        ; TO ADRESS
	set m.Subject = SUBJECT                      ; MAILSUBJECT
	set m.Charset = "iso-8859-1"
	set m.IsHTML  = ''pblnHTML                   ; force boolean    ;SR15046
	
	if TIMEZONE'="" set s.timezone=TIMEZONE
	
	;TEXT:/ATTACH IN ^WWWSOR(REQUEST)
	set blnAttach = $$$NO
	if ($IsObject(MessageStream)) {
		if (MessageStream.SizeGet() > 0) {
			do m.TextData.CopyFrom(MessageStream)
		}
	}
	else {
		if REQUEST'="" {
			set NUM=""
			for {
				set NUM=$order(^WWWSOR(REQUEST,NUM))
				quit:NUM=""
			
				set strLine = $get(^WWWSOR(REQUEST,NUM))
			
				if $find(strLine,"#attach") {                                       ; #attach //server/dir1/.../dirN/filename
				;	NOTE: If this is \\server\share under unix, strPath will
				;	      be blank - doesn't support UNC in the code!
					set strPath   = $translate($PIECE(strLine,"#attach",2)," ")
					set strFile   = ##class(%Library.File).GetFilename(strPath)     ; filename
					set strPath   = ##class(%Library.File).GetDirectory(strPath)    ; //server/dir1/.../dirN/
				;	Third parameter - isBinary - not specified (attaching text file = $$$NO, pics?)   TODO : Test (#attbin?)
					set status    = m.AttachFile(strPath,strFile)
					set blnAttach = $$$YES
				} else {
					do m.TextData.Write(strLine)
					do m.TextData.Write(strNewLine)
				}
			}
		}
	}
	
	; see INTERSYSTEMS example - attachment to message  This appears totally separate from previous attachment
	if blnAttach=$$$YES {
		set nestedm = m.AttachNewMessage()
	;	set status=nestedm.AttachFile("c:\cachesys\bin","test.bin")
	}
 
	set status = s.Send(m)                         ; SEND MAIL
	do NORMAL^%PRIO
	
	quit status
	
 
 
]]></Routine>
</Export>