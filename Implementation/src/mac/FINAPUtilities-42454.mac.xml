<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPUtilities" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPUtilities
	
#include COMSYS
#include FINConst
#include COMSYS
#include COMConst
	
CheckGLEffectiveDateExpiration()
    ;-------------------------------------------------------------------------------
    ; If the User Session Date has expired, let the user know that it has.
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 29-Jun-2005	RobertW		SR12230: Created
    ;-------------------------------------------------------------------------------
	if $$HasExpired^FINAPUserSessionDate() {  ; "GL Effective Date has expired. It will now be %1."
		$$$AlertInScript($listbuild("Fin00821",$$^WWWDATE($$GetGLEffectiveDate^FINAPUserSessionDate())))
	}
	
	quit
	
	
GLEffectiveDateAfterConstruction(pfldField=$$$FldFINAPInvGLEffectiveDate)
    ;-------------------------------------------------------------------------------
    ; When the form is constructed, we must get the default value for the GL Effective
    ; Date. If the General Parameters says to use Invoice Date, then default this to
    ; blank
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; SR14461
    ; 30-Jun-2005	RobertW		SR12230: Created
    ;-------------------------------------------------------------------------------
	new dteDate
	
	if $$$NoKey(YKEY) {
		if $$IsGLEffectiveDateInvoiceDate^FINGeneralSetup() {
			set dteDate="" //SR14461
		} else {
			set dteDate=$$GetGLEffectiveDate()
		}
	} else {
		set dteDate=$piece(YFELD,Y,pfldField)
	}
	
	quit $$^WWWDATE(dteDate)
	
	
IsDateInOpenPeriod(pdteDate)
    ;-------------------------------------------------------------------------------
    ; This method determines if the GL Effective Date is inside an Open Period.
    ;
    ; Params:
    ;
    ; Returns:
    ; Boolean	Whether we are in a valid open period
    ;
    ; History:
    ; 01-Jul-2005	RobertW		SR12230: Created
    ;-------------------------------------------------------------------------------
	new intCount,intLoop,idPeriod,objPeriod,blnOpen,lstPeriods
	
	set blnOpen=$$$NO
 
	; We have to force the GetPeriodsForDate otherwise we will only now the valid ones and not the invalid ones
	if $$$ISOK($$GetPeriodsForDate^FINGLPeriod(pdteDate,.lstPeriods,1)) {
		set intCount=0
		for intLoop=1:1:$listlength(lstPeriods) {
			set idPeriod=$listget(lstPeriods,intLoop)
			
			set objPeriod=$get(^FINGLPeriod(YM,idPeriod,1))
			if objPeriod'="" {
				if '$$$FINGLPeriodOpentoPosting(objPeriod) {
					set intCount=$increment(intCount)
				}
			}
		}
		
		set blnOpen = (intCount=0)
	}
	
	quit blnOpen
	
	
GetGLEffectiveDate(pdteInvoice,pdteGLEffectiveDate,pblnAlert=$$$YES,&pstrStatus,pstrType,pfldGLEffectiveDate)
    ;-------------------------------------------------------------------------------
    ; Check if the invoice date is in an open period and if not, get the next
    ; available date. If this date is not the same as the current effective date, 
    ; let the users know with an alert.
    ;
    ; Params:
    ;
    ; Returns:
    ; Date		The date we can post to.
    ;
    ; History:
    ; 01-Jul-2005	RobertW		SR12230: Created
    ;-------------------------------------------------------------------------------
	new dteNext,dteInvoice,blnMove
	
	set pstrStatus = $$$OK
	
	set dteInvoice = $$GetGLEffectiveDate^FINAPUserSessionDate()
	
	if +pdteInvoice=0 {
		set pdteInvoice = dteInvoice
	}
	set dteNext=pdteInvoice
	
	if '$$IsDateInOpenPeriod(pdteInvoice) {
		set dteNext = $$GetTransactionDate^FININTBatch(dteInvoice,pstrType,.blnMove) // SR13156
		if 'blnMove set dteNext = pdteGLEffectiveDate
		//set dteNext=$$GetTransactionDate^FININTBatch(pdteInvoice,"APINV")
		
		if (dteNext'=$get(pdteGLEffectiveDate)) {
			set pstrStatus = $listbuild("Fin00826",$$^WWWFELDNAME(YFORM,"D",pfldGLEffectiveDate),$$^WWWDATE(dteNext))
			if pblnAlert $$$Alert(pstrStatus)  ;GL Effective Date must be in an active and open period. Next matching period begins on %1.
		} /* elseif (dteNext<$get(pdteGLEffectiveDate)) {
			set pstrStatus=$listbuild("Fin01047",$$^WWWDATE(pdteGLEffectiveDate))  ;Unable to post there are no active and open periods that include %1
			if pblnAlert $$$Alert(pstrStatus)
			set dteNext=$$GetGLEffectiveDate(dteNext,dteNext,pblnAlert,.pstrStatus,pstrType)
		} */
	}
 
	quit dteNext
	
	
GetKey(pKey,idPay,idPayLine,idPayInvoice)
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Returns:
    ;
    ; History:
    ; 11-Mar-2009	GRF		KEY macros
    ; 15-Jul-2005	RPW		SR12981: Moved from FINAPPaymentLine
    ; 03-May-2004   Shobby   Replaced YKOMMA with ","
    ;-------------------------------------------------------------------------------
	new blnExists
	
	set idPay        = $$$KEY1(pKey)
	set idPayLine    = $$$KEY2(pKey)
	set idPayInvoice = $$$KEY3(pKey)
	
	set blnExists    = $$$NO
	
	if (idPay'="") && (idPayLine'="") && (idPayLine'="+") set blnExists=$$$YES
	
	quit blnExists
	
]]></Routine>
</Export>