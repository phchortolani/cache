<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLProfileConv" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLProfileConv
	;-------------------------------------------------------------------------------
	; Convert Profile to Account
	; 
	; History:
	; 15-Mar-2005	GRF		SR10478 : Create
	;-------------------------------------------------------------------------------
#include COMSYS
#include FINConst
#include INConst
	
SALConvert(pProfile="",pLocn="",pGroup="",pExpense="")
	;-------------------------------------------------------------------------------
	; Given a Account String containing {PROFILE} codes, attempt to return
	; the corresponding account.
	; 
	; Currently requires SAL Module to be present.
	; 
	; ByRef : pProfile    The initial string and the resultant account
	; 
	; Returns : Status
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	quit:pProfile="" strStatus
	quit:(pProfile'["{")&&(pProfile'["}") strStatus
	quit:'$$EXIST^%R("SALGLAccount.OBJ",$get(YUCI)) strStatus
 
	set strStatus = $$DecodeProfile^SALGLAccount(.pProfile,pLocn,pGroup,pExpense,$$$YES)
	quit strStatus
	
	
Convert(pProfile="",pobjAccounts="",pProduct="")    ;pLocn="",pGroup="")
	;-------------------------------------------------------------------------------
	; Given a Account String containing {PROFILE} codes, attempt to return
	; the corresponding account.
	; 
	; Currently requires IN Module to be present.
	; 
	; ByRef : pProfile    The initial string and the resultant account
	; 
	; Returns : Status
	;-------------------------------------------------------------------------------
	new Group,ProfileType,Replace,strStatus
	
	set strStatus = $$$OK
	
	for {
		quit:'$find(pProfile,"{")
		quit:$$$ISERR(strStatus)
	
		set ProfileType = $$$Index($piece($piece(pProfile,"{",2),"}",1))
		set ProfileType = $order(^FINGLProfileCodeTypes(YM,1,ProfileType,""))
		set Replace     = ""
		
		if ProfileType=1 {                                               ; Location
			set Replace=$piece(pobjAccounts,Y,3)
			
		} elseif ProfileType=2 {                                         ; Category
			if pProduct'="" {
				set Group = $$$INARTItemGroup($get(^INART(YM,pProduct,1)))
				if Group'=""	set Replace = $$$INARTGROUPCategory($get(^INARTGROUP(YM,Group,1)))
				if Replace="" {
					if $$$INARTGROUPCategory($get(^INARTGROUP(YM,"XX",1)))'="" {
						set Replace = $$$INARTGROUPCategory($get(^INARTGROUP(YM,"XX",1)))
					} else {
						
						; FIXME : 
						; If Group matches a valid ledger code this might post
						; instead of being caught at health check.  Should the
						; 'else' be removed altogether?
						
						set Replace = Group
					}
				}
			}
		}
		if Replace'="" {
			set pProfile=$$Replace^COMUtilStr(pProfile,"{"_$piece($piece(pProfile,"{",2),"}",1)_"}",Replace)
		} else {
			if $$$ISOK(strStatus) set strStatus = $listbuild("Fin00638",pProfile)
		}  ; "Unable to decode profile code for account %1."
	}
	quit strStatus   ; TODO : COSTCENTRE (see SR16871 & DecodeProfile^SALGLAccount)
	
]]></Routine>
</Export>