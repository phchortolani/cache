<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWDMEU" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWDMEU ;WWWDMEU;DT;Euro-Umstellung;05.11.1998
	;------------------------------------------------------------------\
	; Description of Function :
	;		Euro-Umstellung ; Euro-Conversion
	;
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
    ; 29-Mar-2006	SC			SR13942: Use JobWrapper^COMUtilJob.
	; 28-Oct-2005	JW			SR13074: Translate Exchange Rate
	; 30-May-2005	RobertW		SR12056: Attempt at Performance Increase
	; DT	05.11.1998
	;------------------------------------------------------------------/
	SET VORG(1)=+$TRANSLATE(VORG(1),",",".")
	
	set VORG(1) = $$^WWWTR(1,18,VORG(1))		//SR13074
	
	IF VORG(2)=1 DO SUCHE QUIT  ;ZUSAMMENSTELLEN DER UMSETZDATEI ;the 
	IF +VORG(1)'=0 DO
	. NEW VORG1
	. S VORG1=VORG(1)
	;. do JobWrapper^COMUtilJob("UMSTELL^WWWDMEU("_VORG1_")",YUSER,2) ;SR13942
	. JOB UMSTELL^WWWDMEU(VORG1)::2   ;AUSWERTEN UND UMRECHNEN ;And 
	. DO ^WWWINFO($$^WWWTEXT(383))  ;GESTARTET
	QUIT
	
UMSTELL(VORG1) ;UMSTELLUNG ;changeover 
	SET VORG(1)=VORG1
	DO ^WWWVAR ;SR13942 reversed
	NEW DATEI,EELD,BETRAG,SATZ,FAKTOR
	IF $PIECE(^WWW012(0,YM,1),Y,75)="EUR" QUIT   ;STANDARD IST EURO ;Euro- 
	IF $PIECE(^WWW012(0,YM,1),Y,75)'="DEM" QUIT   ;STANDARD IST EURO ;Euro- 
	QUIT:+$HOROLOG<58700   ;ZU FRUEH ;within 
	QUIT:+$HOROLOG>58836   ;ZU SPÄT ;within tardy 
	LOCK +^WWWDMEU(0):1 QUIT:'$T
	QUIT:$GET(^WWWDMEU(0))="ok"  ;schon gestartet ;yet Started 
	SET FAKTOR=VORG(1)
	QUIT:FAKTOR'=1.95583
	SET EDAT="" FOR  SET EDAT=$ORDER(^WWWDMEU(0,EDAT)) QUIT:EDAT=""  DO 
	. SET EELD="" FOR  SET EELD=$ORDER(^WWWDMEU(0,EDAT,EELD)) QUIT:EELD=""  DO 
	. . QUIT:$PIECE($GET(^WWWDMEU(0,EDAT,EELD,1)),Y,2)=1
	. . SET DATEI="^"_EDAT_"(-1)"
	. . IF $PIECE($GET(^WWWDMEU(0,EDAT,EELD,1)),Y,3)'="" SET DATEI=$PIECE(^(1),Y,3,99)  ;RECOVER NACH CRASH ;within 
	. . FOR  DO  QUIT:DATEI=""
	. . . SET DATEI=$QUERY(@DATEI)
	. . . SET $PIECE(^WWWDMEU(0,EDAT,EELD,1),Y,3,99)=""  ;SAVE FÜR CRASH ;to 
	. . . SET $PIECE(^WWWDMEU(0,EDAT,EELD,1),Y,3)=DATEI  ;SAVE FÜR CRASH ;to 
	. . . QUIT:DATEI=""
	. . . SET SATZ=@DATEI
	. . . SET BETRAG=$PIECE(SATZ,Y,EELD)
	. . . IF BETRAG="DEM" SET BETRAG="EUR"   ;UMSTELLUNG DES KENNZEICHEN ;changeover characteristic 
	. . . IF +BETRAG'=0 SET BETRAG=$JUSTIFY(BETRAG/FAKTOR,0,2)
	. . . SET $PIECE(SATZ,Y,EELD)=BETRAG
	. . . SET @DATEI=SATZ
	. . . QUIT
	. . SET $PIECE(^WWWDMEU(0,EDAT,EELD,1),Y,2)=1  ;DATENFELD OK ;data item 
	. . QUIT
	. SET VORG(1)=EDAT DO ^WWWSORT  ;SORTKEY
	. QUIT
	SET ^WWWDMEU(0)="ok"
	LOCK -^WWWDMEU(0)
	SET $PIECE(^WWWWAE(0,"EUR",1),Y,5)=1
	SET $PIECE(^WWWWAE(0,"DEM",1),Y,5)=$JUSTIFY(1/FAKTOR,0,6)
	SET $PIECE(^WWW012(0,YM,1),Y,75)="EUR"  ;STANDARD IST EURO ;Euro- 
	QUIT
	
SUCHE ;ZUSAMMENSTELLEN UMSETZDATEI
	NEW ART,LAN,TXT,PARA
	;KILL ^WWWDMEU(0)
	SET DATEI="" FOR  SET DATEI=$ORDER(^WWW003(0,DATEI)) QUIT:DATEI=""  DO 
	. SET FELD="" FOR  SET FELD=$ORDER(^WWW003(0,DATEI,FELD)) QUIT:FELD=""  DO 
	. . SET ART=$PIECE($GET(^WWW003(0,DATEI,FELD,1)),Y,3)
	. . ;SET TXT=$$^WWWUPER($PIECE($GET(^WWW003(0,DATEI,FELD,1)),Y,2))
	. . SET TXT=$zconvert($PIECE($GET(^WWW003(0,DATEI,FELD,1)),Y,2),"U")
	. . SET LAN=$PIECE($GET(^WWW003(0,DATEI,FELD,1)),Y,4)
	. . SET PARA=$PIECE($GET(^WWW003(0,DATEI,FELD,1)),Y,9)
	. . I $F(TXT,"%") Q
	. . IF ART=12 I $F(TXT,"PREIS") I '$F(TXT,"%") SET $P(^WWWDMEU(0,DATEI,FELD,1),Y,1)=TXT_" ("_LAN_")" WRITE DATEI," ",FELD," ",TXT,"<BR>" Q
	. . IF ART=12 I TXT="BETRAG" I '$F(TXT,"%") SET $P(^WWWDMEU(0,DATEI,FELD,1),Y,1)=TXT_" ("_LAN_")" WRITE DATEI," ",FELD," ",TXT,"<BR>" Q
	. . IF ART=12 I TXT="WERT" I '$F(TXT,"%") SET $P(^WWWDMEU(0,DATEI,FELD,1),Y,1)=TXT_" ("_LAN_")" WRITE DATEI," ",FELD," ",TXT,"<BR>" Q
	. . IF ART=8 SET $P(^WWWDMEU(0,DATEI,FELD,1),Y,1)=TXT_" ("_LAN_")" WRITE DATEI," ",FELD," ",TXT,"<BR>" Q
	. . IF PARA'="" IF $FIND(PARA,"WAEHRUNG") SET $P(^WWWDMEU(0,DATEI,FELD,1),Y,1)=TXT_" ("_LAN_")" WRITE DATEI," ",FELD," ",TXT,"<BR>" Q
	. . ;I ART=12 W "NICHT " WRITE DATEI," ",FELD," ",TXT,"<BR>" 
	. . QUIT
	. QUIT
	QUIT
]]></Routine>
</Export>