<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="WWWFORM1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[WWWFORM1
	
#include COMSYS
#include WWWConst
#include WWWFORM
	
    ;-------------------------------------------------------------------------------
    ; 	Button Display			BUTTON ANZEIGEN
    ; 
    ; Inputs:
    ; 	YFORM		Form Name
    ; 	YVOR		objWWW120
    ; 	YFOART		Form Type
    ; 					1	Standard Form
    ; 					2	List Generator
    ; 					3	Grid Form
    ; 					4	Manual Input (with Button)
    ; 					5	Manual Input (without Button)
    ; 					6	Menu Input Type
    ; 					7	Search Engine
    ; 					8	Wizard
    ; 					9	BitMap Search
    ; 					10	Gantt Chart
    ; 					11	Edit Table
    ; 					12	Grid Edit Only
    ; 	SPRACHE		Language
    ; 	YTIMEFORM	
    ; 	YMENU
    ;
    ; Returns:
    ;
    ; History:
    ; 26-Aug-2010	GRF		SR17515: YTABLEANZ should be incr not decr on <TABLE>
	; 24-Jan-2008	shobby	SRBR014526: Call to standard Coolbar function.
    ; 20-Nov-2007	shobby	Optionally add buttons to popup up forms (specified by
    ;							user) in a new window.
    ; 12-Nov-2007	GRF		Doco; !=>||
    ; 24-Aug-2007	GRF		Uncomment closing angle bracket for TABLE HTML tag
	; 07-May-2007	GRF		SR15511: Doco; quits
    ; 21-Dec-2006	JW		SR14235: Added reference to child window
    ; 19-Dec-2006	JW		BR014262: Call GetType, js fn showMenu.  Don't show
    ;							button for full screen mode.
    ; 10-Nov-2006	JW		BR014276: Replace idMenuType with YMENU (already defined)
    ; 20-Oct-2006	Steve S BR014276: Added button for menu type 4
    ; 01-Sep-2006	HeberB	SR14404 : Translation link with current key affects help text
    ; 21-Nov-2005	GRF		SR13171 : Doco
    ; 14-Oct-2005	Steve S	SR13651: Add translation tool for reports
    ; 05.08.1997	DT		Created
    ;-------------------------------------------------------------------------------
	quit:$get(YFORM)="WWWBLANK"
	new YYQ
	
	; FORM WITHOUT BUTTONS					D10		$$$WWW124ButtonBelongsToADataItem()
	;---------------------------------------
	; FIXME : does this assume buttons will always be in sequence i.e. no gaps?  No button 2 with value in button 1?
	set YYQ = $$$NO
	if YFOART=5 {                                        ; Manual Input (without Button)
		if '$data(^WWW124(0,YFORM)) set YYQ = $$$YES     ; no manual buttons exist
		if '$data(^WWW124(0,YFORM,SPRACHE,2,1)) if $piece($get(^WWW124(0,YFORM,SPRACHE,1,1)),Y,10)=$$$YES set YYQ = $$$YES
	}
	
	quit:YYQ=$$$YES
	
	if YBEDBER=1 write YCR,YCR,"<!-- ************************* STANDARD BUTTON (WWWFORM1/WWWFORMF) ************************* -->",YCR,YCR
	
	; Form Centred							D10		$$$WWW120FormCentered()
	;---------------------------------------
	if (YFOART=5)||(YFOART=8) if $piece(YVOR,Y,10)=$$$YES do
	. if YFOART=5 if $data(^WWW124(0,YFORM)) quit
	. write "<CENTER>"
	
	if $get(YMENU)="" set YMENU = $$GetType^WWWMENU()
	
	;------------------------------------------------------------------------
	set YTABLEANZ = $get(YTABLEANZ)+1
	set ^CacheTempToolbar(YUSER)=1 ;SR18053
	write $$Coolbar^WWWFORMCOMMON(YVOR)
	
	;+++++++++++++++++++++++++++++++++++++++ ( * Boolean, # System Param)
	; * D10		$$$WWW120FormCentered()
	; * D13		$$$WWW120DisplayFrames()
	; * D45		$$$WWW120PicturesAsButtons()
	;   D46		$$$WWW120StandardSubmit()
	; # D94		$$$WWW120DoNOTDisplayStandardButto()
	; * D106	$$$WWW120FormForEffectiveDate()
	;   D126	$$$WWW120ExecuteDuringButtonLine()
	;+++++++++++++++++++++++++++++++++++++++
	
	;---------------------------------------
	; $$SR16455 : Currently returns $$$NO
	;---------------------------------------
	set $piece(YVOR,Y,94) = $translate($piece(YVOR,Y,94),";",",")  ;UMSETZEN
	
	if '$$$WWW120PicturesAsButtons(YVOR) write YCR,"<TR>"
	
	; STANDARD BUTTONS
	;---------------------------------------
	if $$$WWW120StandardSubmit(YVOR)'="," do
	. if YFOART=1                 do HEAD1^WWWFORMF    ; STANDARD
	. if (YFOART=2) || (YFOART=9) do HEAD2^WWWFORMM    ; LIST GENERATOR or BIT MAP SEARCH (same as HEAD4^WWWFORMM)
	. if YFOART=3                 do HEAD3^WWWFORMF    ; GRID
	. ;
	. if +$piece(YVOR,Y,45)=$$$NO do
	. . write YCR,"</TABLE>" 
	. . set YTABLEANZ = $get(YTABLEANZ)-1
	. . write YCR,"<TABLE CELLSPACING=0 BORDER=0>"     ;TEXTE ALS BUTTON ;when 
	. . set YTABLEANZ = YTABLEANZ+1    ;SR17515 was -1
	. ;
	. if YFOART=4  do HEAD4^WWWFORMM set YKEY = ""     ;MANUELLE
	. if (YFOART=5) || (YFOART=8) if $data(^WWW122(0,YFORM)) do HEAD4^WWWFORMM  ;OHNE BUTTON ABER MIT OK! ;without yet by means of 
	. if YFOART=6  do HEAD4^WWWFORMM,DELETE^WWWFORMF
	. if YFOART=7  do HEAD4^WWWFORMM
	
	
	;+++++++++++++++++++++++++++++++++++++++
	;  D66		$$$WWW120PositioningOfButtonLine()
	;	0	Buttons Above
	;	1	Button Down
	;	2	Text Border Left
	;	3	Selection
	;+++++++++++++++++++++++++++++++++++++++
	;SONDERBUTTON  ;wenn keine ;when None 
	;write "<td width='25' style='text-align:center;border:0px;padding:2px 2px 2px 0px;'>" 
	;do MANUAL^WWWFORMF
	;write "</td>"
	
	if ($get(YTIMEFORM)'=1) && ($piece(YVOR,Y,66)'=2) && ($order(^WWW124s(0,2,$$^WWWUMLAU(YFORM,1),SPRACHE,""))'="" || (+$get(YSCREENM)'=0)) {
		if $piece(YVOR,Y,45) if YFOART'=4 if YFOART'=8 if YFOART'=5 write $$Delimiter^WWWFORMCOMMON()
		
		do $case($$$WWW120PositioningOfButtonLine(YVOR), 3:^WWWFORMC3,: ^WWWFORMC)
	}
	
	if $get(YTIMEFORM)'=1 if $piece(YVOR,Y,46)'="," do   ;WENN , DANN KEINEN
	. ;WV  WIEDERVORLAGE
	. if '$find(","_$piece(YVOR,Y,94)_",",",14,") if (YFOART=1)||(YFOART=3) do WV^WWWFORMF  ;WIEDERVORLAGE
	. ;
	. ;COPY  KOPIEREN DATENSATZ ;data record 
	. if '$find(","_$piece(YVOR,Y,94)_",",",15,") if (YFOART=1)||(YFOART=3) if $data(^WWW121(0,YFORM)) do COPY^WWWFORMF  ;COPY
	. ;
	. ;ZEITABHÄNGIGE ERFASSUNG;BEC;24383;18.11.03
	. ;
	. do
	. . new BER,MOD
	. . set BER=""
	. . set MOD=""
	. . if $get(YM)'="" if $get(YFORM)'="" if $data(^WWW121D(0,YFORM,1,YM,1)) do
	. . . set BER=$piece($get(^WWW121D(0,YFORM,1,YM,1)),Y,64)   ;BERECHTIGUNG NUR AUS DEM ERSTEN PRIMÄRSCHLÜSSEL ;only out of 
	. . . set MOD=$piece($get(^WWW121D(0,YFORM,1,YM,1)),Y,65)   ;BERECHTIGUNG NUR AUS DEM ERSTEN PRIMÄRSCHLÜSSEL ;only out of 
	. . ;
	. . if (BER'="")||(MOD'="") quit:$$^WWWACCESS(BER,MOD)'=$$$YES  ;KEINE BERECHTIGUNG ;no 
	. . if $piece(YVOR,Y,106)=$$$YES if (YFOART=1)||(YFOART=3) if YKEY'="" if $data(^WWW121(0,YFORM)) do TM^WWWFORMF  ;ERFASSUNG BEI ZEIT ;logging next to time 
	
	;-------------------------------------------------------------------------------
	
	
	
	if ($$$WWW013CompilerTranslator($get(^WWW013(0,YBED,1)))'="") && $find(","_$$^WWWBEDBER(YBED)_",",",1,") {
		if ($get(YFORM)'="") && (YFORM'="WWWLNG") && (YFORM'="WWWBLANK") && $$$WWW120PicturesAsButtons(YVOR) {
			do TranslationButton()
		}
	}
	do CreatePopupShortcutsLink()
	
	if (YMENU=4) && $$$IsTopUser(YUSER) && '$$$WWW013useFullScreenandHeader($get(^WWW013(0,YBED,1))) {
		write "<TD WIDTH=10"
		if +$$$WWW120PicturesAsButtons(YVOR)=$$$YES write " class=""coolButton"">"  ;MOUSEEFFECT
		write "<A class=link onClick=""parent.showMenu(0);"">"
		
		write "<IMG SRC="""_YGIF_"baum.gif"" TITLE="""_$$$Text("WWW00048")_""" border=0></A>" ; "Menu Window"
		write "</TD>"	
	}
	
	;-------------------------------------------------------------------------------
	; Execute During Button Line                 EXECUTE ZWISCHEN BUTTON          ; *** EXECUTE ***
	; D126		$$$WWW120ExecuteDuringButtonLine()
	;-------------------------------------------------------------------------------
	if $piece(YVOR,Y,126)'=""  do  ;TYBD;EXECUTE VERSCHOBEN VOR DEN DELIMITER; TYBD;5.09.2003
	. ;IF YBEDBER=1 WRITE YCR,YCR,"<!-- ************************* EXECUTE DURING BUTTON LINE  :"_$PIECE(YVOR,Y,126)_" (WWWFORM1) ************************** -->",YCR,YCR
	. if $get(YTIMEFORM)'=1 xecute $piece(YVOR,Y,126)  ;EXE ZWISCHEN DEN BUTTON ;inter- 
	
	if $$MenuType^WWW013()'=14{
		if $piece(YVOR,Y,45)=$$$YES if YFOART'=5 if YFOART'="" write "<TD><IMG SRC="""_YGIF_"delimiter.gif""></TD>"
	}
	write YCR,"<TD> </TD>"
	write YCR,"</TR>"
	write YCR,"</TABLE>"
	set YTABLEANZ=$get(YTABLEANZ)-1
	
	;------------------------------------------------------------------------
	;MANUELLE OHNE BUTTON ;without 
	if +$piece(YVOR,Y,45)=$$$YES if $piece(YVOR,Y,13)'=$$$YES write YCR,"<HR>"
	if (YFOART=5) || (YFOART=8)  if $piece(YVOR,Y,10) =$$$YES write "</CENTER>"
	if '$$$WWW120PicturesAsButtons(YVOR) write YCR,"</TR>"
	kill ^CacheTempToolbar(YUSER) ;SR18053
	quit
	
	
TranslationButton()
	;-------------------------------------------------------------------------------
	; Show the translation button
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 20-Dec-2006	JW		SR14235: Encapsulated. Added reference to child window
	;-------------------------------------------------------------------------------
	new YKEY,strURL
	
	$$$OpenTD($$$NO)
	
	set strURL = YAKTION_"EP=WWWMANU&amp;YFORM="
	
	; Link to REPLanguage for reporting forms
	if $extract(YFORM,1,6)'="REPRUN" {
		$$$Append(strURL,"WWWLNG&amp;YEXEC=D|^WWWLNG3(\'"_YFORM_"\')")
	} else {
		$$$Append(strURL,"REPLanguage&amp;YEXEC=do|WriteLanguageTable^REPLanguage(\'"_$select($get(^CacheTempReport(YUSER,YFORM))="":YPARA,1:$get(^CacheTempReport(YUSER,YFORM)))_"\')")
	}
	$$$Append(strURL,$$WWWCGI2^WWWCGI($$$YES))
	
	write "<A href='' onclick=""subWindow('"_strURL_"','LANGUAGE'); return false;"">"
	do StopButton^WWWFORMCOMMON("translation","text.gif","BUTTON_TRANSLATION")
	write "</A>"
	quit
	
	
CreatePopupShortcutsLink()
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 20-Nov-2007	shobby	SRBR014812: Created
	;-------------------------------------------------------------------------------
	new strURL,objWWW013,lstPopupForms,intLoop
	
	if YBED'="" {
		set objWWW013     = $get(^WWW013(0,YBED,1))
		set lstPopupForms = $$$WWW013PopupShortcuts(objWWW013)
		for intLoop=1:1:$length(lstPopupForms,";") {
			if $piece(lstPopupForms,";",intLoop)'="" {
				set strURL = YAKTION_"EP=WWWFORM&YFORM="_$piece(lstPopupForms,";",intLoop)_"&YKEY=&YUSER="_YUSER_"&YBED="_YBED
				$$$OpenTD($$$NO)
			 	write "<A onClick="""
				write " window.open('"_strURL_"' ,'Parameter',''); "
				write """>"
				do StopButton^WWWFORMCOMMON($piece(lstPopupForms,";",intLoop),"open.gif","BUTTON_POPUP_"_intLoop)
				write "</A>"
				write "</TD>"
			}
		}
	}
	quit]]></Routine>
</Export>