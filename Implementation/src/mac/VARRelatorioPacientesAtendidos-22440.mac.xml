<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARRelatorioPacientesAtendidos" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[VARRelatorioPacientesAtendidos
#include COMSYS
#include INConst
#include MEDConst
 
	quit
 
OnAfterDataFields()
	new objConsulta
	set objConsulta = $get(^VARTempRelPacientesAtendidos(YBED))
	
	quit:(objConsulta="")

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	do PrintConsulta(objConsulta)
	
	quit
 
OnAfterSave() 
 	new idLocal, DtInicial, DtFinal, produto,centroCusto,grupo,nivelAtencao
 
	set idLocal			= $get(VORG(1))	
	set DtInicial 		= $get(VORG(2))	
	set DtFinal 		= $get(VORG(3))
	set produto 		= $get(VORG(4))
	set centroCusto		= $get(VORG(5))
	set grupo	 		= $get(VORG(6))
	set nivelAtencao 	= $get(VORG(7))
	
	if ( DtInicial > DtFinal ){
		$$$JS("alert('A data inicial não pode ser maior que a data final.')")
		do GoToForm^COMUtilForm("VARRelatorioPacientesAtendidos","")
		quit
	}
	
	set ^VARTempRelPacientesAtendidos(YBED) = idLocal_Y_DtInicial_Y_DtFinal_Y_produto_Y_centroCusto_Y_grupo_Y_nivelAtencao

	do VARLogAfterSave^VARUtil(YBED,YFORM)
 	do GoToForm^COMUtilForm("VARRelatorioPacientesAtendidos","")
 	
 	quit
 
PrintConsulta(pobjConsulta)
	new rs,strSQL,count,lstHeader,idLocal,DtInicial,DtFinal,produto,
		fldNome,fldDataNascimento,fldProntuario,fldCNS,fldDispensacao,fldDataAtendimento,fldQuantidade,fldUnit,centroCusto,grupo,idCentroCusto,fldLocal,somaQnt
 
	set rs = ##class(%ResultSet).%New()
 
	set idLocal			= $piece(pobjConsulta,Y,1)
	set DtInicial 		= $piece(pobjConsulta,Y,2)
	set DtFinal 		= $piece(pobjConsulta,Y,3)
	set produto 		= $piece(pobjConsulta,Y,4)
	set centroCusto 	= $piece(pobjConsulta,Y,5)
	set grupo			= $piece(pobjConsulta,Y,6)
	set nivelAtencao	= $piece(pobjConsulta,Y,7)
	
	set ^VARTempRelPacientesAtendidosReport(YBED) = $$$YES
 
	set strSQL =	" SELECT "
	set strSQL =	strSQL_" Nome,"
	set strSQL =	strSQL_" Prontuario,"
	set strSQL =	strSQL_" CNS,"
	set strSQL =	strSQL_" to_char(DataNascimento,'DD/MM/YYYY') AS DataNascimento,"
	set strSQL =	strSQL_" VARPacientesAtendidos.IssueNumber,"
	set strSQL =	strSQL_" to_char(DataDispensacao,'DD/MM/YYYY') AS DataDispensacao,"
	set strSQL =	strSQL_" idLocal"
	if (produto '= ""){
		set strSQL =	strSQL_", QtyRequired as Quantidade,"
		set strSQL =	strSQL_" Unit as Unidade "
	}
	set strSQL =	strSQL_" FROM Report.VARPacientesAtendidos"
	set strSQL = 	strSQL_" JOIN INIssueLine ON VARPacientesAtendidos.IssueNumber = INIssueLine.IssueNumber "
	
	set strSQL =	strSQL_" WHERE "
	set strSQL =	strSQL_" TO_DATE(TO_CHAR(DataDispensacao,'DD/MM/YYYY'),'DD/MM/YYYY') BETWEEN "_DtInicial_" AND "_DtFinal_""
	if (idLocal'="") {
		set strSQL =	strSQL_" AND idLocal = "_idLocal
	}
	
	if (nivelAtencao '= ""){
		set strSQL = strSQL_" AND $$SQLGetNivelAtencaoID^VARSQL(INIssueLine.Item) LIKE '%"_nivelAtencao_"%' "		
	}
	
	if (produto '= ""){
		set strSQL = strSQL_" AND INIssueLine.Item = '"_produto_"' "		
	}
	if (grupo '= ""){
		set strSQL = strSQL_" AND $$SQLGetGrupoItem^VARSQL(INIssueLine.Item) = '"_grupo_"' "		
	}
	
	if (produto = ""){
		set strSQL = strSQL_" GROUP BY VARPacientesAtendidos.IssueNumber "
	}
	
	do rs.Prepare(strSQL)
	do rs.Execute()
	if (rs.Next()=0) {
		set ^VARTempRelPacientesAtendidosReport(YBED) = $$$NO
		write "<p><b>Nenhum registro encontrado.</b></p>"
		quit
	}
 	
	if idLocal="" {
		set lstHeader = $listbuild("#","Local","Nome","Data de Nasc.","Prontuário","CNS","N.Dispensação","Data de Atendimento")
	}else {
		set lstHeader = $listbuild("#","Nome","Data de Nasc.","Prontuário","CNS","N.Dispensação","Data de Atendimento")
	}
	if (produto '= ""){
		;set lstHeader = lstHeader _$listbuild("Unidade do Produto","Quantidade")
		set lstHeader = lstHeader _$listbuild("Quantidade")
	}
	
	if $$Start^COMTable(lstHeader) {		
		
		set count = 0
		set somaQnt = 0
		
		for {
			set continue=1
			
			set fldNome				= rs.GetData(1)
			set fldProntuario   	= rs.GetData(2)
			set fldCNS          	= rs.GetData(3)
			set fldDataNascimento	= rs.GetData(4)
			set fldDispensacao		= rs.GetData(5)
			set fldDataAtendimento	= rs.GetData(6)
			set fldLocal			= rs.GetData(7)
			;set idCentroCusto=$$getCentroCusto(fldDispensacao)
			
			if centroCusto'="" {
				if idCentroCusto'=centroCusto set continue=0
			}

			if continue=1 {
				set count = $i(count)
				if (produto '= ""){
					set fldQuantidade		= rs.GetData(6)
					set fldUnit				= rs.GetData(7)
				}
				
				set idCentroCusto=$$getCentroCustoDesc(fldDispensacao)
				
				do NewLine^COMTable()
				do InsertCell^COMTable(count,,,,"CENTER")
				if idLocal="" do InsertCell^COMTable(fldLocal_" - "_$$SQLGetLocationName^VARSQL(fldLocal),,,,"LEFT")
				do InsertCell^COMTable(fldNome)
				do InsertCell^COMTable(fldDataNascimento,,,,"CENTER")
				do InsertCell^COMTable(fldProntuario,,,,"CENTER")
				do InsertCell^COMTable(fldCNS,,,,"CENTER")
	   			do InsertCellCallback^COMTable("<center> "_fldDispensacao_"</center>","GoToForm^COMUtilForm","INDispenseToPatient,"_fldDispensacao_",,3,"_$$$YES)
				do InsertCell^COMTable(fldDataAtendimento,,,,"CENTER")
				;do InsertCell^COMTable(idCentroCusto,,,,"LEFT")
				if (produto '= ""){
					;do InsertCell^COMTable($$SQLGetDescCompletaUnit^VARSQL(fldUnit),,,,"CENTER")
					do InsertCell^COMTable($$^WWWTR(0,12,fldQuantidade),,,,"RIGHT")	
					
					set somaQnt = somaQnt + fldQuantidade
				}
				
			
				
				do EndLine^COMTable()
			}
			quit:(rs.Next()=0)
		}
		
		if (produto '= ""){
			do NewLine^COMTable("lightsteelblue")
			
			if idLocal'="" {
				do InsertCell^COMTable("<strong>Total</strong>",,,,"RIGHT",,7)
			} else {
				do InsertCell^COMTable("<strong>Total</strong>",,,,"RIGHT",,8)
			}
			
		    do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,somaQnt)_"</strong>",,,,"RIGHT")	
			do EndLine^COMTable()
		}
	
		do Stop^COMTable()
		do rs.Close()
	}
 
	quit
 
geraRelatorio()
 	new idLocal,DtInicial,DtFinal,produto,objConsulta
 
	//if ($order(^VARTempRelPacientesAtendidos(YBED,""))'=YUSER)&&($data(^VARTempRelPacientesAtendidos(YBED))>0) {
	//	$$$Alert("É preciso salvar a consulta antes de gerar o relatório.")
	//	do GoToForm^COMUtilForm("VARRelatorioPacientesAtendidos","")
	//	quit
	//}
 
	set objConsulta = $get(^VARTempRelPacientesAtendidos(YBED))
 
	set idLocal		= $piece(objConsulta,Y,1)
	set DtInicial 	= $piece(objConsulta,Y,2)
	set DtFinal 	= $piece(objConsulta,Y,3)
	set produto 	= $piece(objConsulta,Y,4)
	set centroCusto	= $piece(objConsulta,Y,5)
	set grupo	 	= $piece(objConsulta,Y,6)
	set nivelAtencao= $piece(objConsulta,Y,7)
	
	if ( DtInicial > DtFinal ){
		$$$JS("alert('A data inicial não pode ser maior que a data final.')")
		do GoToForm^COMUtilForm("VARRelatorioPacientesAtendidos","")
		quit
	} elseif ($get(^VARTempRelPacientesAtendidosReport(YBED)) = $$$NO){
		$$$JS("alert('Nenhum registro foi encontrado para gerar o relatório.')")
		do GoToForm^COMUtilForm("VARRelatorioPacientesAtendidos","")
		quit
	}
 
	do RunReportPacientesAtendidos^VARJasperRunReport(idLocal,DtInicial,DtFinal,produto,centroCusto,grupo,nivelAtencao)
	
	quit
	
getCentroCusto(idIssue)
	$$$VAR
	new idCentroCusto,idMEDAdmissao,idWWW0121e
	set idCentroCusto=""
	set idMEDAdmissao = $$$INIssueFREE7($get(^INIssue(YM, idIssue, 1)))
	if ($length(idMEDAdmissao) > 0) {
		set idWWW0121 = $$$MEDAdmissionLocation($get(^MEDAdmission(0, idMEDAdmissao, 1)))
		set idCentroCusto = $$SQLGetCentroCusto^VARSQL(idWWW0121)
	}
	q idCentroCusto
	
getCentroCustoDesc(idIssue)
	$$$VAR
	new idCentroCusto,idMEDAdmissao,idWWW0121e
	set idCentroCusto=""
	set idMEDAdmissao = $$$INIssueFREE7($get(^INIssue(YM, idIssue, 1)))
	if ($length(idMEDAdmissao) > 0) {
		set idWWW0121 = $$$MEDAdmissionLocation($get(^MEDAdmission(0, idMEDAdmissao, 1)))
		set idCentroCusto = $$SQLGetCentroCusto^VARSQL(idWWW0121)_" - "_$$SQLGetLocationName^VARSQL(idWWW0121)
	}
	q idCentroCusto

CanViewReport()
	new objConsulta,DtInicial,DtFinal,strStatus

	set objConsulta = $get(^VARTempRelPacientesAtendidos(YBED))
 
	set DtInicial 	= $piece(objConsulta,Y,2)
	set DtFinal 	= $piece(objConsulta,Y,3)
	
	if ( (DtInicial = "") || (DtFinal = "") ){
	    set strStatus = "Preencha todos o campos obrigatórios."	
	    $$$YQHandler(strStatus)
	}
	
	quit]]></Routine>
</Export>