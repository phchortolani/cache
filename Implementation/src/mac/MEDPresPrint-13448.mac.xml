<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPresPrint" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPresPrint
	
#include INConst
#include MEDConst
#include COMSYS
#include WWWConst
	
PrintPickList(pidIssue)
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the MED Prescription Issue
    ;
    ; Params: None
    ;
    ; Returns: None
    ;
    ; History:
    ; 04-Nov-2010	GRF		SR16642: Remove commented code copied when created
    ; 22-Oct-2010	PPP		SR16642: Created  (this is a temporary print format)
    ;							Will be superseded by a report.
    ;							Curently Views exist for the Issue Header and Lines
    ;							and CS can write their own reports using these views
    ;							if necessary
	;-------------------------------------------------------------------------------
	new ANZM,strStatus,YDDSATZ
	
	kill ^WWWSOR(YUSER)
	set ANZM      = 0
	set strStatus = $$CreateLines(pidIssue)
 
	if $$$ISOK(strStatus) {
		set YNOFOOT = 1
		do ^WWWBACK
		do List(pidIssue)
		if ANZM=0 do ^WWWINFO($$^WWWTEXT(46),1,,1)   ; "No Data Available"
	}
	
	kill ^WWWSOR(YUSER)
 	$$$YQHandler(strStatus)
 
	quit
	
	
CreateLines(pidIssue)
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the Issue
    ;
    ; Params: None
    ;
    ; Returns: None
    ;
    ;
    ; History:
    ; 04-Nov-2010	GRF		SR16642: Replace early exit with $$$ISOK
    ; 22-Oct-2010	PPP		SR16642: Created  
	;-------------------------------------------------------------------------------
	new blnNext,intCol,intCols,intLn,objQuery,strData,strQuery,strStatus
	
	set strStatus = $$$OK
	
	set objQuery = ##class(%ResultSet).%New("%DynamicQuery.SQL")
	set strQuery = "select LineNumber, FromStorage, Item, QtyIssued, Unit"
	set strQuery = strQuery_" from INIssueLine"
	set strQuery = strQuery_" where IssueNumber = '"_pidIssue_"'"
	set strQuery = strQuery_" order by FromStorage, Item"
 
	set strStatus = objQuery.Prepare(strQuery)
	
	;if $$$ISERR(strStatus) {   ; "Invalid Query"
	;	;	set strStatus = $$$MakeStatus("INISS03")     ; FIXME : add error message string as comment
	;	quit strStatus
	;}
	if $$$ISOK(strStatus) {
	 	set strStatus = objQuery.Execute()
		set intCols   = objQuery.GetColumnCount()
	 	
		set intLn = 0
		for { 
			set blnNext = objQuery.Next()
			quit:'blnNext
			
			set intLn = intLn+1
			for intCol=1:1:intCols {
				set strData = objQuery.GetData(intCol)
				set $piece(^WWWSOR(YUSER,pidIssue,intLn),Y,intCol) = strData
			}
		}
	}
	quit strStatus
	
	
List(pidIssue) ; FIXME : New code - should explicitly pass ANZM
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the Issue
    ;
    ; Params: None
    ;
    ; Returns: None
    ;
    ;
    ; History:
    ; 22-Oct-2010	PPP		SR16642: Created  
	;-------------------------------------------------------------------------------
	new intLn,objVORGLANG,UES
	
	set VORG1       = $GET(^INVORG(YM,YM,1))
	set objVORGLANG = $get(^INVORGLANG(YM,YM,SPRACHE,1))
	set TEXT        = $$$INVORGLANGTextAfterPickList(objVORGLANG)
 
	set ANZSEIT = $PIECE(VORG1,Y,82)    ; Number within side 
	if ANZSEIT="" SET ANZSEIT = 30
	
	set UES = $$^WWWTEXT(207)           ; Number
	set UES = UES_Y_$$^WWWTEXT(32054)   ; Storage
	set UES = UES_Y_$$^WWWTEXT(32024)   ; Item
	set UES = UES_Y_$$^WWWTEXT(31407)   ; Quantity 
	set UES = UES_Y_$$^WWWTEXT(32030)   ; Unit of Measure
	set UES = UES_Y_$$^WWWTEXT("AL00016")_"/"_$$^WWWTEXT("AL00017") 		; "Lot Number / Use By Date"
	
	set intLn   = 0
	set ANZM    = 0
	do Header(pidIssue)		
	set YDDSATZ = 0
 
	for {
		set intLn = $order(^WWWSOR(YUSER,pidIssue,intLn))
		quit:intLn=""
		
		set LFN   = ""
		set YFELD = $get(^WWWSOR(YUSER,pidIssue,intLn))
 
		IF ANZM'=0 {
			IF ANZM#ANZSEIT=0 {
				write "</TD></TR></TABLE>"
				do FF^WWWW()
				do Header(pidIssue)
			}
		}
		do ANZ(pidIssue,$piece(YFELD,Y,1))
	}
	
	DO LEER^WWWTAB(15,6)
	DO STOP^WWWTAB
	DO ZUSATZTEXT
	
	QUIT
	
	
ANZ(pidIssue,pidLn)	;
	;-------------------------------------------------------------------------------
    ; Print the Pick List for the Issue
    ;
    ; Params: None
    ;
    ; Returns: None
    ;
    ; History:
	; 04-Apr-2011	shobby	SR17714.4: Redirected previous change to INLP.
	; 28-Apr-2011	shobby	SR17714.2: Use CodeWithTSU formatting.
    ; 22-Oct-2010	PPP		SR16642: Created  
	;-------------------------------------------------------------------------------
	new blnHide,idItem,idUnit,objItem,strBundleValues,strItemDescription
	
	set blnHide = +$$$WWW012HideRelationClassIDs($get(^WWW012(0,YM,1)))
 
	SET ANZM=1+ANZM
	WRITE YCR
	DO NL^WWWTAB    
	DO NF^WWWTAB
	WRITE $PIECE(YFELD,Y,1),"&nbsp;" 	    ;Number
	
	DO ZWL^WWWTAB
	;SR17714.2 WRITE $PIECE(YFELD,Y,2),"&nbsp;"        ;Storage	
	write $$CodeWithTSU2^INLP($$$INIssueFromLocn($get(^INIssue(0,pidIssue,1))),$PIECE(YFELD,Y,2))	;SR17714.4
	
	DO ZWL^WWWTAB
	set idItem  = $piece(YFELD,Y,3)          ;Item
	set objItem = $get(^INART(YM,idItem,1))
	set strItemDescription = $$$INARTSearchName(objItem)
	if $length(strItemDescription) > 30 {
		set strItemDescription = $extract(strItemDescription,1,30)_"..."
	}
	write idItem_" ("_strItemDescription_")","&nbsp;"
	
	DO ZWR^WWWTAB
	write $PIECE(YFELD,Y,4),"&nbsp;"        ;Quantity 
 
	DO ZWL^WWWTAB
	set idUnit = $piece(YFELD,Y,5)          ;Unit
	write $$$AppEnum("EINHEIT",idUnit),"&nbsp;"     ; always hide unit id
	
	; Bundle Values
	;---------------------------------------
	do ZWL^WWWTAB
	set strBundleValues = $$GetBundleDesc^INDispenseToPatientLine(pidIssue,pidLn)
	write strBundleValues,"&nbsp;"

	DO EL^WWWTAB
	
	QUIT
 
 
ZUSATZTEXT
	IF ANZM'=0 IF TEXT'="" DO
	. WRITE "<BR>"
	. IF '$FIND(TEXT,"<") DO  QUIT
	. . FOR YI=1:1  QUIT:$PIECE(TEXT,"|",YI,9999)=""  WRITE $PIECE(TEXT,"|",YI),"<BR>",YCR  ;NUR TEXT ;only Text 
	. ;
	. FOR YI=1:1  QUIT:$PIECE(TEXT,"|",YI,9999)=""  WRITE $PIECE(TEXT,"|",YI),YCR  ;HTML
	
	WRITE YCR
	DO FF^WWWW()
	WRITE YCR,YCR
	QUIT
	
Header(pidIssue)
	DO Title(pidIssue)
	DO START100^WWWTAB  ;STARTEN DER ÃœBERSCHRIFT ;launching the superscription 
	WRITE "<THEAD>" 
	DO NL^WWWTAB
	FOR I=1:1 QUIT:$PIECE(UES,Y,I,99)=""  DO NHWZ^WWWTAB WRITE $PIECE(UES,Y,I),"&nbsp;" DO EH^WWWTAB
	DO EL^WWWTAB
	WRITE "</THEAD>"
	QUIT
	
	
Title(pidIssue)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 23-Nov-2011	PPP		SR17958: Added SPRACHE parameter
    ; 22-Oct-2010	PPP		SR16642: Created  
	;-------------------------------------------------------------------------------
	new dteTransfer,idFromLocn,idRef,idToLocn,intStatus,objIssue,objRecord,strDescription
	
	set objIssue   = $get(^INIssue(YM,pidIssue,1))
	set idFromLocn = $$$INIssueFromLocn(objIssue)
	set idToLocn   = $$$INIssueCustomerID(objIssue)
	if idToLocn'="" {
		set idToLocn = idToLocn_" ("_$$$MEDPatientSurname($get(^MEDPatient(0,idToLocn,1)))_")"	
	}

	set idRef      = $$$INIssueReference(objIssue)
	set intStatus  = $$$INIssueStatus(objIssue)
 
	DO START1000^WWWTAB
	WRITE YCR,"<TR>"
	WRITE YCR,"<TD>"
	WRITE "<NOBR>"
	
	;---------------------------------------
	
	WRITE YCR,YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	WRITE YCR,"<TR>"
	WRITE YCR,"<TD ALIGN=CENTER COLSPAN=2>"
	WRITE "<FONT SIZE=3>"
	WRITE "<B>"
	WRITE $$^WWWFORMNAME("INIssue")_" "_$$^WWWTEXT(32503)                           ; "Picklist"
	WRITE "</B>"
	WRITE "</FONT>"
	WRITE "<BR>"
	WRITE YCR,"<FONT SIZE=2>"    ; "Issued By  "  "Location"
	WRITE $$^WWWTEXT(33404)_" "_$$^WWWTEXT(388)_": "_idFromLocn_" ("_$PIECE($GET(^WWW0121(0,YM,idFromLocn,1)),Y,1)_")"  ;NAME
	WRITE "</FONT>"
	WRITE YCR,"</TD>"
	WRITE YCR,"</TR>"
	WRITE YCR,"</TABLE>"
	
	;---------------------------------------
	
	DO
	. WRITE YCR,YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	. WRITE "<TR><TD WIDTH=33%>",YCR
	. WRITE "<FONT SIZE=2>",YCR
	. WRITE "<B>"
	. WRITE $$^WWWTEXT(32807)_" "_$$^WWWTEXT(388)_": "  ; "For" "Location"
	. WRITE idToLocn									; To location 
	. WRITE "<BR>"
	. WRITE $$^WWWTEXT(32247)_": "                                                  ; "Reference" 
	. WRITE idRef
	. WRITE "</B>"
	. WRITE YCR,"</TD>",YCR
	. ;
	. WRITE "<TD ALIGN=CENTER VALIGN=BOTTOM WIDTH=33%>",YCR
	. WRITE "<FONT SIZE=2>",YCR
	. WRITE "<B>"
	. WRITE " "
	. WRITE "<BR>"
	. ;SR17958
	. ;WRITE $$^WWWTEXT(32781)_": "_$$GetDescription^WWWStatus("INIssue",intStatus)	; "Status"
	. WRITE $$^WWWTEXT(32781)_": "_$$GetDescription^WWWStatus("INIssue",intStatus,SPRACHE) ; "Status"
	. WRITE "</B>"
	. WRITE YCR,"</TD>",YCR
	. ;
	. WRITE "<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>",YCR
	. WRITE "<FONT SIZE=2>",YCR
	. WRITE "<B>"
	. if pidIssue '= "" do
	. . write $$^WWWFELDNAME("INIssue","P",1)_": "_pidIssue
	. . write "<br />"_$$$Text(32168)_": "                                          ; "Delivery Date  "
	. WRITE "</B>",YCR
	. ;
	. WRITE "</TD></TR>",YCR
	
	WRITE "</TABLE>",YCR
	
	;---------------------------------------
	
	DO EH^WWWTAB
	DO EL^WWWTAB
	DO STOP^WWWTAB
 
	QUIT
	
]]></Routine>
</Export>