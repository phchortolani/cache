<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPROENDE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPROENDE ;INPROENDE;FIS;ERFASSEN FERTIGUNGSABSCHLUSS;29.10.2002
#include COMSYS
#include INConst
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		ERFASSEN FERTIGUNGSABSCHLUSS
	;		     EINSPRUNG AUCH AUS INPROSTEMP
	;		Production Completion - called by form INPROENDE "Finish Work Order"
	;		     and re-entry point for Routine INPROSTEMP
	;
	; Inputs : 
	;	VORG(1)		Work Order
	;	VORG(2)		Employee
	;	VORG(5)		New Serial No.
	;	VORG(6)		Lot No.
	;	VORG(7)		Output Qty
	;	VORG(8)		Start Statistical Cost Accounting (On Main Item)    [Boolean]
	;   NODISPLAY
	; 
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 25-Nov-2005	GRF		SR13914 : pass environment to job session rather than
	; 						recreating variables and losing significant details
	; 08-Jul-2005	GRF		SR12863 : Boolean Macros
	; 29.10.2002	FIS
	;-----------------------------------------------------------------------------*/
	NEW NOTIME
	
	SET VORG(1)  = $GET(VORG(1))  ;Work Order      STEMPEL
	SET VORG(2)  = $GET(VORG(2))  ;Employee        MITARB
	SET VORG(5)  = $GET(VORG(5))  ;New Serial No   SN
	SET VORG(6)  = $GET(VORG(6))  ;Lot No
	SET VORG(7)  = $GET(VORG(7))  ;Output Qty      PROD.MENGE
	SET VORG(8)  = $GET(VORG(8))  ;Start Statistical Cost Acctg (boolean)
	;                              NACHKALKULATION SOFORT DURCHFÜHREN;29.11.04;26744
	SET VORG(98) = 0              ;BESTAND NICHT KORRIGIEREN: 0=NEIN == BESTAND BUCHEN JA !
	
	SET VORG(99) = $$$YES                    ;ANZEIGE AN  ;Show ON
	IF $GET(NODISPLAY)=1 SET VORG(99)=$$$NO  ;ANZEIGE AUS ;Show OFF
	IF VORG(1)=""                      IF VORG(99)=$$$YES DO ^WWWINFO($$^WWWTEXT(32292),0) QUIT  ;BITTE PRÜFEN! ;please check
	IF VORG(7)'="" IF $GET(VORG(7))'>0 IF VORG(99)=$$$YES DO ^WWWINFO($$^WWWTEXT(32292),0) QUIT  ;BITTE PRÜFEN! ;please check
	
	SET NOTIME=1                  ;KEINE ZEITEN SPEICHERN ;no Save 
	KILL ^WWWSOR(YUSER_"SNMESS")
	SET ^WWWSOR(YUSER_"SNMESS")=""
	IF VORG(8)=$$$YES KILL ^WWWSOR(YUSER_"NKALK")  ;29.11.04;26744
	
	;++++++++++++++++++++++++++++++++++++++++++++
	; Call to INPROFERTIG
	;  - calls INARTPLUS & INARTMINUS
	;++++++++++++++++++++++++++++++++++++++++++++ 
	DO ^INPROFERTIG
	
	
	; TODO : Is it better to start a single job to run through the entries?  [YES]
	;        Can we run out of available jobs?  Do we need to check if job
	;        started okay and handle error situation? <GRF> SR12724
	IF VORG(8)=$$$YES IF $DATA(^WWWSOR(YUSER_"NKALK")) DO
	. NEW AUF,POS
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^WWWSOR(YUSER_"NKALK",AUF)) QUIT:AUF=""  DO  
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^WWWSOR(YUSER_"NKALK",AUF,POS)) QUIT:POS=""  DO 
	. . . ;JOB NACHKALK(AUF,POS,YM,SPRACHE,YBED)       ; SR13914
	. . . job NACHKALK(AUF,POS):(:1::)
	. ;
	. KILL ^WWWSOR(YUSER_"NKALK")  ;29.11.04;26744
	
	IF VORG(99)=$$$YES DO ^WWWINFO($$^WWWTEXT(32132)_" "_VORG(1)_" "_$$^WWWTEXT(151))   ; Production ### Stopped
	IF VORG(99)=$$$YES DO  ;SN-BEARBEITUNGSVERMERKE;FIS;30.12.03;24854
	. NEW AUF,POS,TEIL,SN,MESS,YDDSATZ
	. SET YDDSATZ=0
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^WWWSOR(YUSER_"SNMESS",AUF)) QUIT:AUF=""  DO
	. . SET POS=""
	. . FOR  SET POS=$ORDER(^WWWSOR(YUSER_"SNMESS",AUF,POS)) QUIT:POS=""  DO
	. . . WRITE "<BR><BR>"
	. . . DO START100^WWWTAB
	. . . DO NL^WWWTAB
	. . . DO NH^WWWTAB
	. . . WRITE "<FONT SIZE=2>"
	. . . WRITE $$^WWWTEXT(32116)  ;SN
	. . . WRITE "&nbsp;",YCR
	. . . DO EH^WWWTAB
	. . . DO NH^WWWTAB
	. . . WRITE "<FONT SIZE=2>"
	. . . WRITE $$^WWWTEXT(33708)  ;BEARBEITUNGSVERMERK
	. . . WRITE "&nbsp;",YCR
	. . . DO EH^WWWTAB
	. . . DO EL^WWWTAB
	. . . ;
	. . . SET TEIL=""
	. . . FOR  SET TEIL=$ORDER(^WWWSOR(YUSER_"SNMESS",AUF,POS,TEIL)) QUIT:TEIL=""  DO
	. . . . SET SN=""
	. . . . FOR  SET SN=$ORDER(^WWWSOR(YUSER_"SNMESS",AUF,POS,TEIL,SN)) QUIT:SN=""  DO
	. . . . . DO NL^WWWTAB
	. . . . . DO NF^WWWTAB
	. . . . . WRITE "<FONT SIZE=2>"
	. . . . . WRITE SN
	. . . . . WRITE "&nbsp;",YCR
	. . . . . DO ZW^WWWTAB
	. . . . . WRITE "<FONT SIZE=2>"
	. . . . . SET MESS=+$PIECE($GET(^WWWSOR(YUSER_"SNMESS",AUF,POS,TEIL,SN)),Y,1)
	. . . . . IF MESS>1 WRITE "<FONT COLOR="_YRED_">"
	. . . . .;WRITE $PIECE($GET(^WWW101(0,"SN-VERMERK",SPRACHE,MESS,1)),Y,1)     ;BEC;25866;07.06.04;DA ZENTRALE DATEI
	. . . . . WRITE $PIECE($GET(^WWW101(0,"SN-VERMERK",SPRACHE,MESS,1)),Y,1)
	. . . . . WRITE "&nbsp;",YCR
	. . . . . DO EF^WWWTAB
	. . . . . DO EL^WWWTAB
	. . . ;
	. . . DO STOP^WWWTAB
	
	KILL ^WWWSOR(YUSER_"SNMESS")
	QUIT
	
	;NACHKALK(AUF,POS,YM,SPRACHE,YBED)		; SR13914
NACHKALK(AUF="",POS="")
	;-------------------------------------------------------------------------------
	;  STARTEN NACHKALKULATION SOFORT
	;  Start Post Calculation immediately
	;  
	;  Called from above as a BACKGROUND Process
	;-------------------------------------------------------------------------------
	SET $ZTRAP="^WWWERROR"
	
	;DO		; SR13914
	if '$data(YM) do
	. NEW YM,SPRACHE,YBED
	. DO ^WWWVAR
	
	;SET AUF=$GET(AUF)
	;SET POS=$GET(POS)
	QUIT:AUF=""
	QUIT:POS=""
	
	; FIXME : Should there be some error logging if this process failed
	;         to call INTAGES due to some blockage?  Do we end up with
	;         INTAGES never being called or INPROFERTIG being called
	;         more than once?  There are 5 instances of this particular
	;         lock being used.  <GRF>  SR12725
	NEW YI,YQ
	SET YQ=0
	FOR YI=1:1:10  DO  QUIT:YQ=1  HANG 50  ;MAX. 10 VERSUCHE
	. LOCK +^INBUCHLOCK(YM,AUF,POS):10 QUIT:'$TEST  ;AUFTRAG GGF. NOCH DURCH INPROSTEMPU GESPERRT
	.;IF $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,267)="" DO FERTKOST1^INTAGES
	. if $$$INAUFPStatisticalCostCalculated($get(^INAUFP(YM,AUF,POS,1)))="" DO FERTKOST1^INTAGES
	. SET YQ=1
	. LOCK -^INBUCHLOCK(YM,AUF,POS)
	
	QUIT
	
INFO   ;ANZEIGEN INFORMATIONEN ;display 
	IF $$$INVORGFinishOnlyForMainParts($GET(^INVORG(YM,YM,1)))'=$$$YES QUIT
	DO FS^WWWTAB($$^WWWTEXT(32721))  ;INFO
	WRITE "<center><b>"
	WRITE $$^WWWTEXT(33160)  ;Nach Fertigung des Hauptteils werden die Unterteile im Bestand korrigiert. ;post- Production will who Quantity 
	WRITE "<br>"
	WRITE $$^WWWTEXT(33159)  ;Achtung, dieser Vorgang kann mehrere Minuten dauern !
	WRITE "<br><FONT COLOR="_YRED_">"
	WRITE $$^WWWTEXT(33230)  ;Die hier eingegebene Fertigstellung kann NICHT rückgängig gemacht werden ! ;here is finishing Not will 
	WRITE "</b></center>"
	DO FSE^WWWTAB
	QUIT
]]></Routine>
</Export>