<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMValidation" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMValidation
	#include COMSYS
 
ValidateCPForCNPJ(pstrCode="")
    ;-------------------------------------------------------------------------------
    ; Checks whether the passed in code is either a CPF or CNPJ
    ; 
    ; Params:	pstrCode:	The code to be checked.
    ; 
    ; Returns:
    ;			strStatus:Either true or an error message
    ;			
    ; History:
	; 09-Nov-2006	shobby			Created: SRBR014161
    ; 
    ;-------------------------------------------------------------------------------
	;
	new strStatus
	
	set strStatus=$$$OK
	if ((+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,0,1)))) 
	&& ($Get(YBED)="WEBSERVICE")) {
		quit $$$OK
	}
	if pstrCode="" {
		;Code is not mandatory so "" is ok
	} elseif $length(pstrCode)=14 {
		set strStatus=$$ValidateCNPJ(pstrCode)
	} else {
		set strStatus=$$ValidateCPF(pstrCode)
	}
	quit strStatus
	
 
ValidateCNPJ(pstrCode="")
    ;-------------------------------------------------------------------------------
    ; Checks whether the passed in code is a valid CNPJ
    ; 
    ; Params:	pstrCode:	The code to be checked.
    ; 
    ; Returns:
    ;			strStatus:Either true or an error message
    ;			
    ; History:
    ; 22-Oct-2008	shobby			SRBR014968: Changed $listbuild to $$$MakeStatus
    ; 15-Dec-2006	shobby			SRBR014161: Corrected failure when first digit is a zero.
    ; 29-Nov-2006	shobby			SRBR014161: Not necessary to append 1st check digit to code when calculating 2nd digit. 	
	; 09-Nov-2006	shobby			SRBR014161: Created
    ; 
    ;-------------------------------------------------------------------------------
	new strStatus,strMultiplier,intX1,intX2
	if ((+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,0,1)))) 
	&& ($Get(YBED)="WEBSERVICE")) {
		quit $$$OK
	}

	set strStatus=$$$OK
	if (pstrCode?14.14n) {
		set intX1=$$GetDigit(pstrCode,"5,4,3,2,9,8,7,6,5,4,3,2")
		set intX2=$$GetDigit(pstrCode,"6,5,4,3,2,9,8,7,6,5,4,3,2")
		if ($extract(pstrCode,13)'=intX1)||($extract(pstrCode,14)'=intX2) {
			set strStatus=$$$MakeStatus("Com00271",pstrCode,"CNPJ") ;%1 is an Invalid %2 Code
		}
	} else {
		set strStatus=$$$MakeStatus("Com00271",pstrCode,"CNPJ") ;%1 is an Invalid %2 Code
		; No description why.  The intent is not to prompt the user to put a valid code in
		; but to validate that the code that they are copying is legitimate.
		}
	quit strStatus
 
 
ValidateCPF(pstrCode="")
    ;-------------------------------------------------------------------------------
    ; Checks whether the passed in code is a valid CPF
    ; 
    ; Params:	pstrCode:	The code to be checked.
    ; 
    ; Returns:
    ;			strStatus:Either true or an error message
    ;			
    ; History:
    ; 29-Nov-2013	shobby			SMSPE-5: Exclude addional know invalid CPFs
    ; 22-Oct-2008	shobby			SRBR014968: Changed $listbuild to $$$MakeStatus
    ; 15-Dec-2006	shobby			SRBR014161: Corrected failure when first digit is a zero.
    ; 29-Nov-2006	shobby			SRBR014161: Not necessary to append 1st check digit to code when calculating 2nd digit. 	
	; 09-Nov-2006	shobby			SRBR014161: Created
    ; 
    ;-------------------------------------------------------------------------------
	new strStatus,strMultiplier,intX1,intX2
	if ((+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,0,1)))) 
	&& ($Get(YBED)="WEBSERVICE")) {
		quit $$$OK
	}
	set strStatus=$$$MakeStatus("Com00271",pstrCode,"CPF") ;%1 is an Invalid %2 Code
		; No description why.  The intent is not to prompt the user to put a valid code in
		; but to validate that the code that they are copying is legitimate.
	if (pstrCode?11.11n) {
		if pstrCode="00000000000" {
		} elseif pstrCode="11111111111" {
		} elseif pstrCode="22222222222" {
		} elseif pstrCode="33333333333" {
		} elseif pstrCode="44444444444" {
		} elseif pstrCode="55555555555" {
		} elseif pstrCode="66666666666" {
		} elseif pstrCode="77777777777" {
		} elseif pstrCode="88888888888" {
		} elseif pstrCode="99999999999" {
		} else  {
			set strStatus=$$$OK
			set intX1=$$GetDigit(pstrCode,"10,9,8,7,6,5,4,3,2")
			set intX2=$$GetDigit(pstrCode,"11,10,9,8,7,6,5,4,3,2")
			if ($extract(pstrCode,10)'=intX1)||($extract(pstrCode,11)'=intX2) {
				set strStatus=$$$MakeStatus("Com00271",pstrCode,"CPF") ;%1 is an Invalid %2 Code
			}
		}
	}
	quit strStatus
 
 
GetDigit(pintCode="",pintMultiplier="")
	;-------------------------------------------------------------------------------
	; Determines the check digit based on the passed in code and the multiplier.
	; 
	; Parameters
	; 	pintCode:		The partial CPF or CNPJ code used to calculate the check digit.
	; 	pintMultiplier:	The amount that each digit in the partial code is multiplied by.
	; 
	; Returns
	; 	intDigit:  The check digit.
	; 	
	; History:
	; 09-Nov-2006	shobby			Created: SRBR014161
	;-------------------------------------------------------------------------------
	new intSum,idx,intDigit
	
	set intSum=0
	for idx=1:1:$length(pintMultiplier) {
		set intSum=intSum+($extract(pintCode,idx)*$piece(pintMultiplier,",",idx))
	}
	set intDigit=intSum\11
	set intDigit=intSum-(intDigit*11)
	if intDigit<2 {
		set intDigit=0
	} else {
		set intDigit=(11-intDigit)
	}
	quit intDigit
	
]]></Routine>
</Export>