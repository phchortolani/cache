<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescription" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescription
	
#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst
	
#define EnumMakeItem 3


CreateLock(dteLocked,pstrKey)
	;-------------------------------------------------------------------------------	
	; 
	; History:
	; 30-May-2014	shobby		CORE-385.5 Created (Moved MED code out of WWWFORMLocking)
	;-------------------------------------------------------------------------------
	if ($replace($piece(pstrKey,"/",1),"^","") = "MEDPrescription"){
		if ($$$MEDPrescriptionStatus($get(^MEDPrescription(YM,$piece(pstrKey,".",2),1))) > 1){
			do KillLock^WWWFORMLocking(dteLocked,pstrKey)
		}
	}
	quit
	
		
OnBeforeButtonLine(pidPres,pobjPres,&pYVOR)
	;-------------------------------------------------------------------------------
	; Set form to readonly if not editable.
	;
	; Params:	pidPres		- Prescription id
	; 			pobjPres	- Prescription object
	;
	; Returns:	
	;
	; History:
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	//do CreateBaseItemList()
	
	if '$$$NoKey(pidPres) {
		if '$$Editable(pidPres) {
			set $$$WWW120AuthorizationToModifyData(pYVOR) = $$$EnumReadOnly
		}
	}
	quit
	
Enabled(YFELD)	;SR17228
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescription : Grid Tab (COMGridEditParameter)
	;
	;
	; History:
	; 05-Nov-2010	PPP		SR16642: Created
	;-------------------------------------------------------------------------------
	new enumPresType
	new blnEnabled
	
	set blnEnabled = $$$YES
	
	set enumPresType = $$$MEDPrescriptionPrescriptionType(YFELD)
	
	if enumPresType = 1 {
		set blnEnabled = ($$$MEDPrescriptionStatus(YFELD) = 1)
	
	} elseif enumPresType = 2 {                        ; Custom Manufacture
		set blnEnabled = $$$NO
	}

	quit blnEnabled
	
Editable(pidPres="",pblnHosp=0) 
	;-------------------------------------------------------------------------------
	; Is form editable (unprocessed) ?
	;
	; Params:	pidPres - Prescription id
	;			pblnHosp - new Hops Rx, Open = is status 2
	;
	; Returns:	status
	;
	; History:
	; 19-May-2012	SCR		SR17993: Allow for null prescription id.
	; 16-May-2012	SCR		SR17993: Changed for Hosp Prescription
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new strStatus,objPres
	
	set strStatus = $$$OK
	quit:pidPres="" strStatus
	if $data(^MEDPrescription(0,pidPres,1)) {
		set objPres = $get(^MEDPrescription(0,pidPres,1))
		if pblnHosp=1 {              ; SR17993
			if +$$$MEDPrescriptionStatus(objPres) '= 0  {   ;"Open"
				set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"
			}
		} else {
			if $$$MEDPrescriptionStatus(objPres) '= 1  {   ;"Open"
				set strStatus = $$$MakeStatus("IN00377")  ; "Record already processed"
			}
		}
	}
	
	quit strStatus
	
	
OnAfterDataFields(pidPres,pobjPres)
	;-------------------------------------------------------------------------------
	; Code to run when called by the AfterDataField event on the @netManager form.
	;
	; History:
	; 15-Sep-2010	PPP		SR16642: Added the List of Issues COMTable
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	
	if '$$$NoKey(pidPres) {
		do LoadGrid(pidPres,pobjPres)
		
		if ($order(^INIssues(0,7,$$$Index(pidPres),""))'="") {   // SR16642:Issues/Dispenses
			do ShowIssue^MEDPrescriptionShow(pidPres)
		}
	}
	quit
	
	
LoadGrid(pYKEY,pobjPres)
	;-------------------------------------------------------------------------------
	; Code for Grid Edit , depending on Prescription Type Hide / columns that are not 
	; required.
	;
	; Returns:
	;
	; History:
	; 05-Nov-2010	PPP		SR16642:Showing of grid is automatic if configured 
	;						in Form metadata.  Only hide cols here
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new enumPresType
	
	set enumPresType = $$$MEDPrescriptionPrescriptionType(pobjPres)
	
	if enumPresType = 2 {                        ; Custom Manufacture
		
		// Hide specified columns
		$$$StartScript()
		$$$GRIDHideShowColumns("4;5;6;7;8;9;10;11",$$$YES,"MEDPrescriptionLine")
		$$$EndScript()
	}
	
	quit
	
	
OnBeforeSave(pidPres,pobjPres)
	;-------------------------------------------------------------------------------
	; Before save check
	; 
	; Params:	pidPrec - MEDPrescription id
	; 
	; Returns: Q (ByRef)
	;
	; History:
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$Editable(pidPres)
	if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
		if $$$ISOK(strStatus) {
			set strStatus = $$OnBeforeSave^VARMEDPrescriptionLine(pidPres) ;Karine
		}
	}
	
	//if $$$ISOK(strStatus) set strStatus = $$$GRIDSave(pidPres)
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}
	quit
	
	
OnAfterSave(pidPres,pobjPres)
	;-------------------------------------------------------------------------------
	; Creates the MEDPrescription lines based on the base item's manufacture bill of
	; components. should only be called if there are no lines already on the grid, 
	; and is a custom manufacture prescription ONLY.
	; 
	; Also populate patient Name with details from MEDPatient
	; 
	; Inputs:
	;
	; Returns: strStatus
	;
	; History:
	; 16-Jul-2009	DWR		SR16303: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pidPres) {
		if $$$MEDPrescriptionPrescriptionType(pobjPres) = 2 { ;Custom Manufacture
			if '$data(^MEDPrescriptionLine(0,pidPres)) {
				set strStatus = $$CustomPres^MEDPrescriptionLine(pidPres,pobjPres)
			}
		}
	}
	quit strStatus
	
	
IsManuallyClosable(pidPres,objPres)	
	;-------------------------------------------------------------------------------
	; Test if buttons should be disabled
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 26-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new blnClosable,idStatus
	
	set blnClosable = $$$YES
 	if '$$$NoKey(pidPres) {
	 	set idStatus = $$$MEDPrescriptionStatus(objPres)
	 	if idStatus = 1 {
			set blnClosable = $$$MakeStatus("MED00358")         ; "Prescription not yet Firmed"
	 	}
		if $$$ISOK(blnClosable) {	
	 		if (idStatus = 8) || (idStatus = 9) {
		 		set blnClosable = $$$MakeStatus("MED00359")     ; "Prescription is Closed"
	 		} elseif idStatus = 2 {
		 		set blnClosable =  $$$YES
	 		}
	 	}
	}
 	$$$YQHandler(blnClosable)
	quit blnClosable
	
	
ManuallyClose(pidPres)
	;-------------------------------------------------------------------------------
	; Manually close the prescription so no more dispensing can take place under it.
	; 
	; Inputs:
	;
	; Returns: strStatus
	;
	; History:
	; 26-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new objPres,strStatus
	
	set strStatus = $$$OK
	 
	if '$$$NoKey(pidPres) {
		set objPres = $get(^MEDPrescription(0,pidPres,1))
		if objPres'="" {
			set $$$MEDPrescriptionStatus(objPres) = 8    ; M-Close
			set strStatus = $$$Save("MEDPrescription",pidPres,objPres,$$$YES)
		}
		if $$$ISOK(strStatus) {
			do ReloadForm^COMUtilForm()
		} 
	}	
	quit 
	
	
IsFirmable(pidPres,objPres,pblnHosp=0)
	;-------------------------------------------------------------------------------
	; Inputs: check for disable of firm button
	;
	; Returns: strStatus
	;
	; History:
	; 18-Oct-2012	shobby	SR18130.2: Check for active Admission
	; 16-May-2012	SCR		SR17993: Added Hosp Option
	; 26-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pidPres) {
		set strStatus = $$Editable(pidPres,pblnHosp)
		;set strStatus = $$Editable(pidPres)
		
		// Check for a existance of line(s)		
		if $$$ISOK(strStatus) {
			if '$data(^MEDPrescriptionLine(0,pidPres)) {
				set strStatus = $$$MakeStatus("MED00356")  ; "Prescription Has no lines"
			}
		}
		if $$$ISOK(strStatus) set strStatus=$$CheckAdmitted^MEDPatient("","",$$$MEDPrescriptionPatientID(objPres)) ;SR18130.2
		
	} else {
		set strStatus = $$$MakeStatus("MED00357")          ; "No Prescription Selected"
	}
 	$$$YQHandler(strStatus)
	quit strStatus		
	
	
Firm(pidPres,pblnReload=$$$YES)
	;-------------------------------------------------------------------------------
	; Firm Prescription wrapper
	;
	; Returns:Status
	;
	; History:
	; 20-Jan-2014	shobby	SMSPE-15: Optionally reload form.
	; 26-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$Transaction^COMTransaction("FirmTxn^MEDPrescription("""_pidPres_""")",$$$YES)
	
	if $$$ISOK(strStatus)&&pblnReload { ;SMSPE-15
		do ReloadForm^COMUtilForm()
	} 
	quit strStatus
	
	
FirmTxn(pidPres)
	;-------------------------------------------------------------------------------
	; Firm Prescription
	;
	; Returns:Status
	;
	; History:
	; 
	; 22-Oct-2014	SCR		HEVA-1580: Change Status to 1 for non SES-PE clients
	; 15-Jan-2014	shobby	SMSPE-15: On Firm change status to 2
	; 24-Apr-2013	SCR		HEVA-965: Update Issue Type based on setup
	; 13-Jun-2012	SCR		SR17993: Check for suspended statup
	; 09-Jun-2012	SCR		SR17993: Change Status to 1 (Awaiting Validation)
	; 07-Jun-2012	SCR		SR17993: Update Line Status
	; 26-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new objPres,strStatus,idLine,objLine,objSetup
	
	set strStatus = $$$OK
		
	if '$$$NoKey(pidPres) {
		set objPres = $get(^MEDPrescription(0,pidPres,1))
		quit:$$$MEDPrescriptionStatus(objPres)=6 strStatus
		if objPres'="" {
			if +$$$WWWClientParamCoreChangesSESPE($get(^WWWClientParam(YM,YM,1))) {
				set $$$MEDPrescriptionStatus(objPres) = 2 ; SR17993 2  ; "Awaiting Dispensation" ;SMSPE-15
			} else {
				set $$$MEDPrescriptionStatus(objPres) = 1 
			}
			; HEVA-965 vvvv
			set objSetup=$get(^MEDPrescriptionSetup(0,YM,1))
			if $$$MEDPrescriptionIssueType(objPres)="" {
				set $$$MEDPrescriptionIssueType(objPres) = $$$MEDPrescriptionSetupIssueType(objSetup)
			}
			; HEVA-965 ^^^^
			if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
				set $$$MEDPrescriptionFREE6(objPres) = $h
				set $$$MEDPrescriptionFREE7(objPres) = $get(YBED)
			}
			set strStatus = $$$Save("MEDPrescription",pidPres,objPres,$$$YES)
			; SR17993 vvvv
			if strStatus = $$$OK {
				set idLine=""
				for {
					set idLine=$order(^MEDPrescriptionLine(0,pidPres,idLine))
					quit:idLine=""
					quit:strStatus'=$$$OK
					set objLine=$get(^MEDPrescriptionLine(0,pidPres,idLine,1))
					continue:$$$MEDPrescriptionLineStatus(objLine)=6
					set $$$MEDPrescriptionLineStatus(objLine)=1 ; SR17993  
					set strStatus = $$$Save("MEDPrescriptionLine",pidPres_","_idLine,objLine,$$$YES)
				
				}
			}
			; SR17993 ^^^^
			if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(YM,YM,1)))) {
				if ($$$ISOK(strStatus)) {
					set strStatus = $$CreateDose^VARDose(pidPres)
				}
			}
		}
	}
	quit strStatus
	
	
CanDispense(pidPres,pobjPres)
	;-------------------------------------------------------------------------------
	; Check if can dispense
	; Copy of RequestDispense^MEDWSConsumptionInterface with some adjustments
	; Called by: 
	; 	a) - Create Dispense Button (MEDPrescription)
	;	b) - Create Dispense (MEDPrescriptionIssue)
	; 
	; Inputs:
	;	pidPres  - 
	;	pobjPres - ^MEDPrescription(0,pidPres,1)
	;
	; History:
	; 15-Jul-2010	PPP		SR16642: Updated with other Checks
	; 26-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new dteCreated,fltDispensed,fltFrequency,fltRepeats,idStatus,strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pidPres) {
		set idStatus = $$$MEDPrescriptionStatus(pobjPres)
		if '$data(^MEDPrescriptionLine(0,pidPres)) {
				set strStatus = $$$MakeStatus("MED00356")    ; "Prescription Has no lines
		}
		if $$$ISOK(strStatus) {
			if idStatus = 1 {                                ; Open
				set strStatus = $$$MakeStatus("MED00358")    ; "Prescription not yet Firmed"
			
			} elseif (idStatus = 8) || (idStatus = 9) {      ; M-Closed , A-Closed
				set strStatus = $$$MakeStatus("MED00359")    ; "Prescription is Closed"
			} 
		}
	} else {
		set strStatus = $$$MakeStatus("#MED00364: "_$$$Text("MED00364"))      ; "No Prescription number"	
	}
	
	if $$$ISOK(strStatus) {
		set idStatus     = $$$MEDPrescriptionStatus(pobjPres)
		set fltRepeats   = $$$MEDPrescriptionNumberOfRepeats(pobjPres)
		set fltFrequency = $$$MEDPrescriptionFrequency(pobjPres)
		set fltDispensed = $$$MEDPrescriptionRepeatsDispensed(pobjPres)
		set dteCreated   = $$$MEDPrescriptionDateCreated(pobjPres)
	
		if idStatus > 3 {
			set strStatus = $$$MakeStatus($$$Text("MED00359"))         ; "Prescription is Closed"
		}
		if $$$ISOK(strStatus) {
			if fltDispensed >= (fltRepeats + 1) {
				set strStatus = $$$MakeStatus($$$Text("MED00368"))     ; "No repeats remaining"
			}
		}
	}
 	$$$YQHandler(strStatus)
	quit strStatus
	
	
IsDispenseDue(pYKEY)
	;-------------------------------------------------------------------------------
	; Check if next Prescription Dispense is due based on Frequency Days
	; when the Firm/Create button is clicked
	;
	; Called by 
	; 	a) - Create Dispense Button on MEDPrescription
	;	b) - Create Dispense (MEDPrescriptionIssue)
	; 
	; History:
	; 15-Jul-2010	PPP		SR16642: Created
	;-------------------------------------------------------------------------------
	new dteIssued,fltDaysChck,fltDispensed,fltElapsed,fltFrequency,idPres,objPres
	
	if ($get(pYKEY) '= "") {
		set idPres = pYKEY
	} else {
		set idPres = $$GetKey^COMGridEdit31G()
	}
	
	set objPres = $get(^MEDPrescription(0,idPres,1))
	set fltFrequency = $$$MEDPrescriptionFrequency(objPres)
	set fltDispensed = $$$MEDPrescriptionRepeatsDispensed(objPres)
	set dteIssued    = $$$MEDPrescriptionLastIssued(objPres)
	
	// check the frequency of a prescription repeat against the elapsed days since created to check if an early dispense
	set fltDaysChck = fltFrequency   //* fltDispensed
	set fltElapsed = +$horolog - dteIssued
	
	if (fltElapsed < fltDaysChck) {      ; "MED00369"  "WARNING: Prescription due only on %1"
		$$$StartScript()
		write "if (confirm('"_$$$Text($listbuild("MED00369",$$^WWWDATE(dteIssued+fltDaysChck)))_"')) { CallBackNow('ContinueDispense^MEDPrescription','"_idPres_"'); }"
		$$$EndScript()
		
	} else {
		do ContinueDispense(idPres)
	}
	quit
	
	
ContinueDispense(pidPRES)
	;-------------------------------------------------------------------------------
	; Continue creating an Prescription Issue/Dispense
	; 
	; Called By: IsDispenseDue^MEDPrescription
	; 
	; History:
	; 05-Nov-2010	GRF		SR16642: Removed unnecessary $$$OK return value
	; 15-Jul-2010	PPP		SR16642: Created
	;-------------------------------------------------------------------------------
	do FulfilPrescription^MEDPrescriptionIssue(pidPRES)
	quit
	
	
	/*
CreateBaseItemList()
	;-------------------------------------------------------------------------------
	; Create a list of "Make" type items 
	; 
	; Called By: OnBeforeButtonLine^MEDPrescription     FIXME : Search for "MEDBaseItem" shows COMTempList doesn't appear to be used
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Jul-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new idItem,objItem
	
	kill ^COMTempList(0,YUSER,"MEDBaseItem")
	
	set idItem = ""
	for {
		set idItem = $order(^INARTs(0,41,$$$EnumMakeItem,idItem)) ;only 'make' type items
		quit:idItem=""
		
		set objItem = $get(^INART(0,idItem,1))
		set ^COMTempList(0,YUSER,"MEDBaseItem",idItem,1) = $$$INARTSearchName(objItem)
	}
	quit 
	*/
	
CheckPresType()
	;-------------------------------------------------------------------------------
	; Disable the PrescriptionType on Save. This is done via core rule to disable
	; the field if 
	;
	; History:
	; 17-Jul-2009	DWR		SR16642	Created
	;-------------------------------------------------------------------------------
	new blnReadOnly
	
	set blnReadOnly = $$$NO
	if $$$MEDPrescriptionPrescriptionType($get(^WWWDATEN(0,+$horolog,$get(YUSER),"MEDPrescription","D",2)))'="" {
		set blnReadOnly = $$$YES
	}
	quit blnReadOnly
	
	
HasRepeats(pobjPres)
	;-------------------------------------------------------------------------------
	; Check if this prescription has repeats
	; 
	; Called by:
	; Core Rule 3 - MedPrescription
	;
	; Inputs:
	;
	; Returns: Yes / No
	;
	; History:
	; 16-Jul-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new blnHasRepeats
	
	set blnHasRepeats = $$$NO
	if $$$MEDPrescriptionNumberOfRepeats(pobjPres)>0 {
		set blnHasRepeats = $$$YES
	}
	quit blnHasRepeats

OnBlur(pFormFieldId,pYFELD)
	;-------------------------------------------------------------------------------
	; Populate the issuing location with the ward ID from the patients' admission 
	; 
	; ByRef:
	; 	YM		Company ID
	; 	YFORM	Form ID (Should be INReq)
	; 
	; History:
	; 14-Feb-2012	SCR		SR17979: Created
	;-------------------------------------------------------------------------------
	new idAdmission,objAdmission,idPatient
	
	if pFormFieldId=$$$FldMEDPrescriptionPatientID {
		set idPatient = $$$MEDPrescriptionPatientID(pYFELD)
		if idPatient'= "" {
			set idAdmission=$$GetAdmission^MEDAdmission(idPatient)
			if idAdmission '= "" {
				set objAdmission = $get(^MEDAdmission(YM,idAdmission,1))
				set %TXT(1) = %TXT(1)_"#Y"_YFORM_"D"_$$$FldMEDPrescriptionIssuingLocation_"~"_$$$MEDAdmissionLocation(objAdmission)
			}
		}
	}
	quit


OnFilter(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Filters the current item. It is shown only if this routine returns $$$YES.
	; 
	; Called by: OnBeforeDataAccess Class Hook for class MEDPrescription
	; 
	; Params:
	;
	; Returns:
	; $$$YES - the item will be shown
	; $$$NO  - the item won't be shown because it failed the criteria
	;
	; History:
	; 23-May-2012	PPP		SR18002: Created
	;-------------------------------------------------------------------------------
	new objPres
	new idPrescription
	new blnStatus
	
	set blnStatus = $$$YES
	
	if ($get(YFORM)="MEDDispense") {
		
		set idPrescription = $$$MEDDispensePrescription(YFELD)
		if idPrescription'="" {
			set objPres = $get(^MEDPrescription(0,idPrescription,1))
			if ($$$MEDPrescriptionStatus(objPres)<1) || ($$$MEDPrescriptionStatus(objPres)>2) {
				set blnStatus = $$$NO
			}
		}
		
	}

	quit blnStatus	

CanSuspend(pstrKey)
	;-------------------------------------------------------------------------------
	; Check Suspend button avialabilty 
	; 
	; Param YKEY = Rx or Rx,Line  
	;
	; History:
	; 15-Jul-2012	SCR		SR17993: Can only suspend after processed
	; 12-Jul-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new idLine,idRx,idStatus,strStatus
	set strStatus = $$$OK
	set idRx	= $piece(pstrKey,",",1)
	set idLine	= $piece(pstrKey,",",2)
	if $$$NoKey(idRx) {
		set strStatus = $$$MakeStatus($$$Text("MED00364"))      ; "No Prescription number"	
	} else {
		set idStatus =  $$$MEDPrescriptionStatus($get(^MEDPrescription(YM,idRx,1)))
		if idStatus <1 {
			set strStatus = $$$MakeStatus($$$Text("MED01281"))   ;Can not Suspend when Prescription is not processed
		}
	}
	
 	$$$YQHandler(strStatus)
	quit
	
Suspend(pstrKey)
	;-------------------------------------------------------------------------------
	; Suspend Rx or Rx Line 
	; 
	; Param YKEY = Rx or Rx,Line  
	;
	; History:
	; 12-Jul-2012	SCR		SR17979: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$Transaction^COMTransaction("SuspendTxn^MEDPrescription("""_pstrKey_""")",$$$YES)
	
	if $$$ISOK(strStatus) {
		do ReloadForm^COMUtilForm()
	} 
	quit strStatus
	
	
SuspendTxn(pstrKey,pstrReason="")
	;-------------------------------------------------------------------------------
	; Suspend Rx or Rx Line Transaction
	; 
	; Param YKEY = Rx or Rx,Line  
	;
	; History:
	; 21-Feb-2013	SCR		HEVA-862: Only suspend the current line of the solution
	; 21-Nov-2012	SCR		SR18204: if the whole Rx is suspended, Update dispense for all lines
	; 02-Oct-2012	shobby	SR18127: Refuse the Dispense if it has already been created.
	; 28-Jul-2012	SCR		SR17993: Record Reason on Lines 
	; 15-Jun-2012	SCR		SR17993: If a Solution line is Suspended, all lines are Suspended for the solution
	; 12-Jul-2012	SCR		SR17993: Created
	;-------------------------------------------------------------------------------
	new idLine,idRx,intSol,objLine,objRx,strRemark,strStatus,strText
 
	set strStatus = $$$OK
	set idRx	= $piece(pstrKey,",",1)
	set idLine	= $piece(pstrKey,",",2) 
	if '$$$NoKey(idRx) {
		set strText= $$$Text($listbuild("MED01290",$$$WWW013Name($get(^WWW013(0,YBED,1))),YBED,$$^WWWTR(0,1,+$h)_" "_$$^WWWTR(0,7,$piece($h,",",2)),pstrReason))  ;Suspended by User %1 (%2) at %3 Reason: %4
		if $$$NoKey(idLine) {
			if strStatus=$$$OK {
				set objRx=$get(^MEDPrescription(YM,idRx,1))
				set strRemark= $$$MEDPrescriptionNotes(objRx)
				set strRemark=strText_$select(strRemark="":"",1:"|----|"_strRemark) 
				set $$$MEDPrescriptionNotes(objRx)=strRemark
				set strStatus=$$$Save("MEDPrescription",idRx,objRx,1) 	
			}
			
			; SR18204 vvvv
			if strStatus=$$$OK {
				set idLine=""
				for {
					set idLine=$order(^MEDPrescriptionLine(YM,idRx,idLine))
					quit:idLine=""
					quit:strStatus'=$$$OK
					set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
					if $$$MEDPrescriptionLineStatus(objLine) '= 6 {
						set strStatus=$$Refuse2^MEDDispenseLine(idRx,idLine,$$$MEDPrescriptionNotes(objRx))
					}
				}		
			}
			; SR18204 ^^^^
			
			if strStatus=$$$OK {
				set strStatus=$$UpdateRxStatus^MEDPrescriptionAutoClose(idRx,"",6) ; force all lines to suspended
			}
		} else {
			set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
			if $$$MEDPrescriptionLineStatus(objLine) '= 6 {
			
				set $$$MEDPrescriptionLineStatus(objLine) = 6
				set strRemark= $$$MEDPrescriptionLineRemarks(objLine)
				set strRemark=strText_$select(strRemark="":"",1:"|"_strRemark) 
				set $$$MEDPrescriptionLineRemarks(objLine)=strRemark
				set strStatus=$$$Save("MEDPrescriptionLine",idRx_","_idLine,objLine,1)
				set intSol= $$$MEDPrescriptionLineSolution(objLine)
				/* HEVA-862 vvvv
				if (strStatus=$$$OK)&&(intSol) {
					set idOriginal=idLine
					set idLine=""
					for {
						set idLine=$order(^MEDPrescriptionLine(YM,idRx,idLine))
						quit:idLine=""
						quit:strStatus'=$$$OK
						set objLine=$get(^MEDPrescriptionLine(YM,idRx,idLine,1))
						continue:$$$MEDPrescriptionLineSolution(objLine)'=intSol ; only for same solution
						continue:idOriginal=idLine  ; dont do the original line
						set $$$MEDPrescriptionLineStatus(objLine) = 6
						set $$$MEDPrescriptionLineRemarks(objLine)=strRemark
						set strStatus=$$$Save("MEDPrescriptionLine",idRx_","_idLine,objLine,1) 	
					}
				}
				   HEVA-862 ^^^^ */
				if strStatus=$$$OK {
					set strStatus=$$UpdateRxStatus^MEDPrescriptionAutoClose(idRx) ; check if all lines have beed suspended and update header
				}
			}
			if $$$ISOK(strStatus) set strStatus=$$Refuse2^MEDDispenseLine(idRx,idLine,$$$MEDPrescriptionLineRemarks(objLine)) ;SR18127
		}
	}
	quit strStatus

OnBeforeSQL(&YSQL)
	;-------------------------------------------------------------------------------
	; The OnBeforeSQL code will behave differently according to what the calling form is.
	; This routine redirects to a procedure based on the form name.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-Oct-2014	SCR		HEVA-1580: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK

	if $data(^rOBJ(YFORM_"OBSQL")) xecute "set strStatus=$$OnBeforeSQL^"_YFORM_"OBSQL(.YSQL)"		;SR17034
	
	quit strStatus

DoseStatus(pidRx)
	;-------------------------------------------------------------------------------
	; Dose Status for SQL. Based on external time frame
	; This will be very slow.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 07-Oct-2014	SCR		HEVA-1580: Use Dates
	; 13-Oct-2014	SCR		HEVA-1580: Created
	;-------------------------------------------------------------------------------
	new dtEnd,dtStart,idStatus,objRx,strStatus,strTimes
	
	set strStatus	= $$$OK
	set idStatus	= ""
	set Y="~"
	set YM=0

	set strTimes	= $get(^WWWDATEN(0,+$h,YUSER,"VARDispensacaoViaPrescricao","M",1))
	set dtStart		= $piece(strTimes,Y,3)_","_$piece(strTimes,Y,1)
	set dtEnd		= $piece(strTimes,Y,4)_","_$piece(strTimes,Y,2)
	if pidRx'="" {
		if ((+dtStart>0)&&(+dtEnd>0)) {
		    if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
				;do SetTime^VARDispensacaoViaPrescricaoLinha(.dtStart,.dtEnd) ; Convert Time to Date and Time
		    }
			set idStatus	= $$DoseStatusTime(pidRx,dtStart,dtEnd)
		} else {
			set objRx		= $get(^MEDPrescription(YM,pidRx,1))
			set idStatus	= 1
		}
	}
	
	quit idStatus

DoseStatusTime(pidRx,pdtStart,pdtEnd,&pidStatus)
	;-------------------------------------------------------------------------------
	; Dose Status for SQL. Based on external time frame
	; This will be very slow.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-Oct-2014	SCR		HEVA-1580: Return Rx Status
	; 13-Oct-2014	SCR		HEVA-1580: Created
	;-------------------------------------------------------------------------------
	new arrDoses,blnDisp,blnNotValid,blnValid,dtRxStart,fltDispTotal,fltDoseQty
	new fltQty,fltReq,idLine,idStatus,objLine,objRx,strStatus,idDispSta
	kill arrDoses
	set strStatus	= $$$OK
	set idStatus	= 0
	set objRx		= $get(^MEDPrescription(YM,pidRx,1))
	set idLine		= ""
	set blnValid	= $$$NO
	set blnNotValid	= $$$NO
	set blnDisp		= $$$NO
	set pidStatus	= 0
	for {
		set idLine		= $order(^MEDPrescriptionLine(YM,pidRx,idLine))
		quit:idLine=""
		set objLine		= $get(^MEDPrescriptionLine(YM,pidRx,idLine,1))
		set fltDoseQty  = $$$MEDPrescriptionLineDoseQty(objLine) 
		set dtRxStart	= $$$MEDPrescriptionLineFromDate(objLine)_","_
							 $$$MEDPrescriptionLineFromTime(objLine)
		set fltReq		= $$DoseTotal^MEDPrescriptionLine(objLine,,fltDoseQty,pdtStart,pdtEnd,.arrDoses)
		if $data(arrDoses) {
			set fltDispTotal	= $$DispensedByTime^MEDDispenseCalc(pidRx,idLine,.arrDoses,.idDispSta)
			if ('pidStatus)||(idDispSta<pidStatus) {
				set pidStatus	= idDispSta
			}
			set fltQty          = fltReq - fltDispTotal
			if fltQty>0 {
				set idStatus	= 1
			}
		}
	}
	quit idStatus
	
Validated(pidRx,pidRxLine)
	;-------------------------------------------------------------------------------
	; Dose Status for SQL. Based on external time frame
	; This will be very slow.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-Oct-2014	SCR		HEVA-1580: Created
	;-------------------------------------------------------------------------------
	
	quit 0
]]></Routine>
</Export>