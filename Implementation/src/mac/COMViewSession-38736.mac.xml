<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewSession" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewSession
	#include COMView
	#include COMConst
 
 
SaveCache()
	;-------------------------------------------------------------------------------
	; Save the filter cache for the current session
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 22-Feb-2007	Steve S		SR15440: Created (code moved from DisplayGrid^COMViewFilter)
	;-------------------------------------------------------------------------------
	new idParentForm,idFilter,strRef
	
	set idParentForm=$get(^CacheTempView(YUSER,YUCI,"ParentForm"))
	
	quit:(idParentForm="")
	
	kill ^CacheTempViewSave(YUSER,YUCI,idParentForm,"Filter")
	merge ^CacheTempViewSave(YUSER,YUCI,idParentForm,"Filter")=^CacheTempView(YUSER,"Filter")
	
	$$$Order4(^CacheTempView,YUSER,YUCI,"External",idFilter)
		set strRef = $get(^CacheTempView(YUSER,YUCI,"External",idFilter))
		kill ^CacheTempViewSave(YUSER,YUCI,idParentForm,"Filter",strRef)
	$$$End
	
	quit
	
	
ClearCache()
	;-------------------------------------------------------------------------------
	; Clear the filter cache for the current session
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 22-Feb-2007	SteveS	SR15440: Created
	;-------------------------------------------------------------------------------
	new idParentForm
	
	set idParentForm = $get(^CacheTempView(YUSER,YUCI,"ParentForm"))
	quit:(idParentForm="")
	
	kill ^CacheTempViewSave(YUSER,YUCI,idParentForm)
 
	quit
 
StoreSize(pintWidth,pintHeight)
	;-------------------------------------------------------------------------------
	; Store the changed size of the COMView for this field for this user
	;
	; Called By: PrintUnloadConfirm^WWWBODY as callback
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 25-Jul-2007	GRF/RPW	SRBR014600: key order reversed for cleanup of obsolete users
	; 24-Jul-2007	RPW		SRBR014600: Created
	;-------------------------------------------------------------------------------
	new objViewUser,strField,strStatus
	
	set objViewUser = ""
	set $$$COMViewUserSizeDialogWidth(objViewUser)  = pintWidth
	set $$$COMViewUserSizeDialogHeight(objViewUser) = pintHeight
	
	set strField = $get(^CacheTempView(YUSER,"Relation"))
	
	if strField'="" {
		set strStatus = $$$Save("COMViewUserSize",YBED_$$$COMMA_strField,objViewUser,$$$YES)
	}
	quit
	
	
GetSize(pstrField,pstrDummy) ; Transient data set by StoreSize?  No class so not retained.
	;-------------------------------------------------------------------------------
	; Get's the size of the COMView for this field and user
	;
	; Called By: Macro "GetSizeEvent" - Start^COMViewCustom; PARASUCH^WWWFORM75
	; 
	; Inputs:
	;
	; Returns: 
	;
	; History:
	; 27-Jul-2007	RPW		SRBR014600: Minimum sizings 
	; 25-Jul-2007	GRF/RPW	SRBR014600: key order reversed for cleanup of obsolete users
	; 24-Jul-2007	RPW		SRBR014600: Created
	;-------------------------------------------------------------------------------
	new intHeight,intWidth,objSize
	
	; DEFAULT SIZES
	; NOTE : under MSIE6 will be reduced by -27 and -8 respectively in each save cycle even if not resized
	set intHeight = 530
	set intWidth  = 570

	if $data(^COMViewUserSize(0,YBED,pstrField)) {
		set objSize   = $get(^COMViewUserSize(0,YBED,pstrField,1))
		set intHeight = $$$COMViewUserSizeDialogHeight(objSize)
		set intWidth  = $$$COMViewUserSizeDialogWidth(objSize)
		
		if intHeight<250 set intHeight = 250
		if intWidth<250  set intWidth  = 250
	}
	
	quit intHeight_Y_intWidth
]]></Routine>
</Export>