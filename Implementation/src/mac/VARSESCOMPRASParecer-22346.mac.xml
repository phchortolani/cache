<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESCOMPRASParecer" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESCOMPRASParecer
#include COMSYS
#include VARSESConst
 
OnAfterDataFields(pYKEY)
	;-------------------------------------------------------------------------------
    ; Mostra a tabela com as propostas para cadastro de parecer.
    ; 
    ; History:
    ; 22-Aug-2009	Gustavo Fiuza		Created
    ; 04-Apr-2011	Gustavo Fiuza		Updated - Simplificado o processo de parecer.
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	kill ^VARTempCompraParecer(YM,pYKEY)
	
	new Item, objCompraLinha, Produto, Fornecedor, chave, objProposta, Parecer, strParecer, resultado,
		AprovarImg, DesaprovarImg, AguardarImg, strCallback, chaveAprovar, chaveDesaprovar, chaveAguardar,
		vlueUnitario, quantOfertada, prazoEntrega, beginStyle, endStyle, blnItemEncerrado, situacaoItem,
		countItens, horaProposta, titleimg
 
	//Tabela
   	new lstHeader
 	set lstHeader = ""
  	;set lstHeader = lstHeader_$listbuild("Item / Fornecedor ","Data da<br />&nbsp;&nbsp;&nbsp;Proposta","Valor Unitário","Valor Total","&nbsp;&nbsp;&nbsp;Aprovado&nbsp;&nbsp;","&nbsp;Não Aprovado","Aguardando&nbsp;&nbsp;<br />&nbsp;&nbsp;Cumprir Exigência")
  	set lstHeader = lstHeader_$listbuild("Item / Fornecedor ","&nbsp;Data (Hora)","&nbsp;Quantidade","&nbsp;Valor Unitário","&nbsp;Valor Total","&nbsp;Prazo de Entrega","&nbsp;&nbsp;&nbsp;Vencedor&nbsp;&nbsp;","")
	
	set beginStyle = "<font style='line-height:26px'>"
	set endStyle = "</font>"	
	
  	if $$Start^COMTable(lstHeader,"Propostas Cadastradas (ordenadas pelo menor preço)",,,$$$NO) {
 		
	 	$$$Order3(^VARSESCOMPRASPropostaLinha,YM,pYKEY,Item)
 				
			set objCompraLinha = $get(^VARSESCOMPRASLinha(YM,pYKEY,Item,1))
			set Produto	= $piece(objCompraLinha,Y,2)
	 	
	 		do NewLine^COMTable("lightsteelblue")
	    	do InsertCell^COMTable(beginStyle_"<strong>Item "_Item_" - Produto: "_Produto_" - "_$extract($$SQLGetDescricaoProduto^VARSQL(Produto),1,60_"</strong>")_endStyle,,,,,,8)
	 		do EndLine^COMTable()
	 		
 		 	set blnItemEncerrado = $$isItemEncerrado^VARSESCOMPRASPDPASLinha(pYKEY,Item)
 		 	set situacaoItem = $$GetSituacaoItem^VARSESCOMPRASPDPASLinha(pYKEY,Item)
 		 	
 		 	if (blnItemEncerrado = $$$YES) {	 		 	
	 			do NewLine^COMTable("#E0E0E0")
	    		do InsertCell^COMTable(beginStyle_"<font color=red>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Item encerrado</font> - "_situacaoItem_endStyle,,,,,,8)
	 			do EndLine^COMTable()
	 			do NewLine^COMTable("#BABABA")
	    		do InsertCell^COMTable("",,,,,,8)
	    		do EndLine^COMTable()
	 			continue
 		 	}
 				
 			&sql(DECLARE Propostas CURSOR FOR
 
				 SELECT Fornecedor, Precounitario, PrazodeEntrega, DatadaProposta, HoradaProposta
				   INTO :Fornecedor, :vlueUnitario, :prazoEntrega, :datadaProposta, :horaProposta				 
				   FROM VARSESCOMPRASPropostaLinha
				  WHERE $UPPER(NoProcessodeCompra) = :pYKEY
				    AND $UPPER(Item) = :Item
				    AND PrecoUnitario > 0
				    AND QuantidadeOfertada > 0				    
			   ORDER BY PrecoUnitario ASC, PrazodeEntrega ASC, DatadaProposta ASC, HoradaProposta ASC
			      
 			)
 
 			&sql(OPEN Propostas )
 			&sql(FETCH Propostas )
 			
 			set countItens = 0
 			
 			while ('SQLCODE) {			
				
				set chave = pYKEY_YKOMMA_Item_YKOMMA_Fornecedor
				
				set countItens = $i(countItens) 
				
				set objProposta   = $get(^VARSESCOMPRASPropostaLinha(YM,pYKEY,Item,Fornecedor,1))				
				set dteProposta   = $$GetDataProposta^VARSESCOMPRASProposta(objProposta)
				set quantOfertada = $piece(objProposta,Y,16)
				set vlueTotal     = $piece(objProposta,Y,18)
				set resultado     = $piece(objProposta,Y,26)
 				set casasDecimais = $$GetQtCasasDecimais^VARSESCOMPRASPropostaLinhaAlt(pYKEY,Item,Fornecedor)
 				
                if (prazoEntrega = 0) {
                    set prazoEntrega = "Imediata"
                } elseif (prazoEntrega > 0) {
                    set prazoEntrega = prazoEntrega_" dia(s)"
                }							
				
				set AprovarImg    = "v02pb.gif"
				set DesaprovarImg = "v02pb.gif"
				set AguardarImg   = "v02pb.gif"
				
				set strCallback = "Marcar^VARSESCOMPRASParecer"
				
				set chaveAprovar    = chave_YKOMMA_1
				set chaveDesaprovar = chave_YKOMMA_2
				set chaveAguardar   = chave_YKOMMA_3
				
				set titleimg = "Proposta não aprovada para este participante."
				
				if (resultado = 1) { 
					set AprovarImg = "v02.gif"
					set chaveAprovar = chave_YKOMMA_0
					set titleimg = "Proposta aprovada para este participante."
				}
				elseif (resultado = 2) {
					set DesaprovarImg = "v02.gif"
					set chaveDesaprovar = chave_YKOMMA_0				
				}
				elseif (resultado = 3) {
					set AguardarImg = "v02.gif"
					set chaveAguardar = chave_YKOMMA_0
				}
				
				do NewLine^COMTable("white")
	 			do InsertCell^COMTable(beginStyle_"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"_$extract($$SQLGetDescFornecedor^VARSQL(Fornecedor),1,60)_" ("_Fornecedor_")"_endStyle,"VARSESCOMPRASPropostaLinha",chave)

               	if ($$isPropostaZerada^VARSESCOMPRASProposta(objProposta) = $$$YES) && $data(^VARSESCOMPRASPropostaLinha(YM,pYKEY,Item,Fornecedor)) {
                	do InsertCell^COMTable(beginStyle_"<font color=gray>Fornecedor foi contactado, mas não apresentou proposta para o item.</font>"_endStyle,,,,"LEFT",,6)
               	
               	} else {
   		
	    			if (horaProposta = "") {	    		
	    				do InsertCell^COMTable(beginStyle_dteProposta_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"LEFT")
	    			} else {
		    			do InsertCell^COMTable(beginStyle_dteProposta_" ("_$ztime(horaProposta)_")"_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"LEFT")
	    			}
               		  		
	    			do InsertCell^COMTable(beginStyle_$$^WWWTR(0,12,quantOfertada)_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"RIGHT")
	    			//do InsertCell^COMTable(beginStyle_$$FormatCurrency^COMTable(vlueUnitario,,,,casasDecimais)_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"RIGHT")
	    			//do InsertCell^COMTable(beginStyle_$$FormatCurrency^COMTable(vlueTotal,,,,casasDecimais)_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"RIGHT")
	    			do InsertCell^COMTable(beginStyle_$$FormatCurrency^COMTable(vlueUnitario)_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"RIGHT")
	    			do InsertCell^COMTable(beginStyle_$$FormatCurrency^COMTable(vlueTotal)_endStyle,"VARSESCOMPRASPropostaLinha",chave,,"RIGHT")
	    			do InsertCell^COMTable(beginStyle_prazoEntrega_endStyle,"VARSESCOMPRASPropostaLinha",chave)
					
					if resultado > 0 { 
						;do InsertCell^COMTable("<b>Sim</b>",,,,"CENTER") 
						do InsertCellCheckbox^COMTable(chaveAprovar,"MarcarNovo^VARSESCOMPRASParecer",1,0)
						set ^VARTempCompraParecer(YM,pYKEY,Item,Fornecedor) = 1
				
					} else {
						;do InsertCell^COMTable("<b>Não</b>",,,,"CENTER") 
		 				do InsertCellCheckbox^COMTable(chaveAprovar,"MarcarNovo^VARSESCOMPRASParecer",0,0)
		 				set ^VARTempCompraParecer(YM,pYKEY,Item,Fornecedor) = 0	 					
					}
					do InsertCell^COMTable("<IMG SRC="_YGIF_AprovarImg_" TITLE='"_titleimg_"' border=0 align=""center"" >")
					
					;do InsertCellCallback^VARSESCOMPRASParecer("<IMG SRC="_YGIF_AprovarImg_" TITLE='Aprovar a proposta do participante' border=0 align=""center"" >",strCallback,chaveAprovar)
	    			;do InsertCellCallback^VARSESCOMPRASParecer("<IMG SRC="_YGIF_DesaprovarImg_" TITLE='Não aprovar a proposta do participante' border=0 align=""center"" >",strCallback,chaveDesaprovar)
	    			;do InsertCellCallback^VARSESCOMPRASParecer("<IMG SRC="_YGIF_AguardarImg_" TITLE='Proposta do participante aguardando cumprir exigências' border=0 align=""center"" >",strCallback,chaveAguardar)	    		
               	}		
	    	
	    		do EndLine^COMTable()
	 		
	 			&sql(FETCH Propostas )
 			}
 			
 			&sql(CLOSE Propostas )

 			if (countItens = 0) {
	 			do NewLine^COMTable("#E0E0E0")
	    		do InsertCell^COMTable("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<em>Nenhuma proposta cadastrada para o item.</em>",,,,,,8)
	    		do EndLine^COMTable() 
 			} 			
	 		
	 		do NewLine^COMTable("#BABABA")
	    	do InsertCell^COMTable("",,,,,,8)
	    	do EndLine^COMTable() 		
	 		
	 	$$$End
	 	
	 	do Stop^COMTable()
 	}	
	
	quit
 
MarcarNovo(pYKEY="",pChecked="")
	
	new objPropostaLinha, keyPropostaLinha, strStatus, isReadOnly, existeVencedor, fltTotalCompradoAno,
		anoCorrente, fltTotalSendoComprado, lmtMaxixmo, idModalidade, idEnquadramentoLegal, fltTotalGeral,
		fltValorExcedente, objCompra, idModalidade, statusProcesso, objCompraNovo, idProduto, idUnidade, checked
 	 	
 	new pNoProcessoCompra, pItem, pParticipante
 	
 	set pNoProcessoCompra = $piece(pYKEY,$$$COMMA,1)
 	set pItem			  = $piece(pYKEY,$$$COMMA,2)
 	set pParticipante	  = $piece(pYKEY,$$$COMMA,3)
 	set pTipoResultado	  = $piece(pYKEY,$$$COMMA,4)
 	
 	;Verifica a existência de um vencedor para o item do processo de compra
 	if ($piece(pChecked,",",1) = "true"){
 		if (pTipoResultado = 1) {
		 	set existeVencedor = $$CheckVencedorMarcado(pNoProcessoCompra,pItem)  ;$data(^VARSESCOMPRASPropostaLinhas(0,1,1,pNoProcessoCompra,pItem))
	 		if (existeVencedor > 0) {
			 	$$$Alert("Um fornecedor já foi selecionado como vencedor deste item. Para alterar o fornecedor do item, primeiro desmarque aquele que está definido como vencedor.")
 				set checked = $piece(pChecked,$$$COMMA,2)
 				&js< document.getElementById('#(checked)#').checked = 0 >
		 		quit $$$NO
	 		}
 		}
 	}
 	else {
		set pTipoResultado = 0
 	}
 	
 	;Verifica se o processo está editável ou não 	 	
	set isReadOnly = $$CheckProcessoReadOnly^VARSESCOMPRAS(pNoProcessoCompra)
	if (isReadOnly = $$$YES) {
		$$$Alert("Não é possível alterar. Processo somente-leitura para este usuário ou posição.")
		set checked = $piece(pChecked,$$$COMMA,2)
		&js< 
			if (document.getElementById('#(checked)#').checked == 1){
				document.getElementById('#(checked)#').checked = 0
			}
			else {
				document.getElementById('#(checked)#').checked = 1
			}
		>
		quit $$$NO
	}
	
	;Verifica se ainda não extrapolou o limite da compra para o item em questão	
	if (pTipoResultado = 1) {
		set idProduto = $$GetProdutoFromParecer(pNoProcessoCompra,pItem)
		set idLocal   = $$GetLocaldeCompra^VARSESCOMPRAS(pNoProcessoCompra)
		set idUnidade = $$getUnidadeSaudeLocal^VARSESWWW0121(idLocal)
		set anoCorrente = $extract($zdate($horolog,4),7,10)
	
		set fltTotalCompradoAno   = $$GetTotalCompradoAno^VARSESCOMPRAS(idProduto,idUnidade,anoCorrente)
		set fltTotalSendoComprado = $$GetTotalPropostaItem(pNoProcessoCompra,pItem,pParticipante)
		
		set idModalidade = $$GetModalidade^VARSESCOMPRAS(pNoProcessoCompra)
		set idEnquadramentoLegal = $$GetEnquadramentoLegal^VARSESCOMPRAS(pNoProcessoCompra)			
		set lmtMaximo = $$GetLimiteMaximoModalidade^VARSESCOMPRAS(idModalidade,idEnquadramentoLegal)
		
		set fltTotalGeral = fltTotalCompradoAno + fltTotalSendoComprado
		set fltValorExcedente = (lmtMaximo - fltTotalGeral) * -1
		
		if ( (fltTotalCompradoAno + fltTotalSendoComprado) > lmtMaximo ) {
			
			write "javascript:alert('Não é possível executar esta ação pois a compra extrapola o limite legal definido para o produto no ano. \n\n"
			write "Valor já comprado no ano: "_$$FormatCurrency^COMTable(fltTotalCompradoAno)_" \n"
			write "Valor desta compra: "_$$FormatCurrency^COMTable(fltTotalSendoComprado)_" \n"
			write "Total: "_$$FormatCurrency^COMTable(fltTotalGeral)_" \n"
			write "Limite legal: "_$$FormatCurrency^COMTable(lmtMaximo)_" \n"
			write "Valor excedente: "_$$FormatCurrency^COMTable(fltValorExcedente)_"');"
 			set checked = $piece(pChecked,$$$COMMA,2)
			&js< 
				if (document.getElementById('#(checked)#').checked == 1){
					document.getElementById('#(checked)#').checked = 0
				}
				else {
					document.getElementById('#(checked)#').checked = 1
				}
			>
			quit $$$NO
		}
	}
	
	;Verifica se já teve algum recebimento para o processo e produto	
	set idProduto = $$GetCodigoItem^VARSESCOMPRAS(pNoProcessoCompra,pItem)
	
	if ($$CheckRecebimento^VARRecRegional(pNoProcessoCompra,,idProduto) = $$$YES) {
		write "javascript:alert('Não é possível alterar o vencedor pois já houve recebimento no local de entrega para este item.');"
		set checked = $piece(pChecked,$$$COMMA,2)
		&js< 
			if (document.getElementById('#(checked)#').checked == 1){
				document.getElementById('#(checked)#').checked = 0
			}
			else {
				document.getElementById('#(checked)#').checked = 1
			}
		>
		quit $$$NO
	}
	set ^VARTempCompraParecer(YM,pNoProcessoCompra,pItem,pParticipante) = pTipoResultado
	
	set YKEY = pNoProcessoCompra
 	
	quit $$$YES
 
Marcar(pNoProcessoCompra,pItem,pParticipante,pTipoResultado)
	;-------------------------------------------------------------------------------
    ; Marca o resultado da análise da proposta do fornecedor para o item.
    ; pTipoResultado pode ser: 	
    ; 	1 - Aprovado
    ; 	2 - Não Aprovado
    ; 	3 - Aguardando Cumprir Exigências
    ; 
    ; History:
	; 26-May-2011	Gustavo Fiuza		Updated - utilizar a unidade de saúde ao 
	;									invés do local no total comprado
	;
	; 30-Apr-2011	Gustavo Fiuza		Updated - atualiza log de conclusão do
	;									processo na compra
	; 04-Apr-2011	Gustavo Fiuza		Updated - Verifica se já existe vencedor
	;									e se existe limite financeiro para a compra.
    ; 02-Oct-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	new objPropostaLinha, keyPropostaLinha, strStatus, isReadOnly, existeVencedor, fltTotalCompradoAno,
		anoCorrente, fltTotalSendoComprado, lmtMaxixmo, idModalidade, idEnquadramentoLegal, fltTotalGeral,
		fltValorExcedente, objCompra, idModalidade, statusProcesso, objCompraNovo, idProduto, idUnidade
 	
 	;Verifica a existência de um vencedor para o item do processo de compra
 	if (pTipoResultado = 1) {
	 	set existeVencedor = $data(^VARSESCOMPRASPropostaLinhas(0,1,1,pNoProcessoCompra,pItem))
	 	if (existeVencedor > 0) {
		 	$$$Alert("Um fornecedor já foi selecionado como vencedor deste item. Para alterar o fornecedor do item, primeiro desmarque aquele que está definido como vencedor.")
		 	quit
	 	}
 	}
 	
 	;Verifica se o processo está editável ou não 	 	
	set isReadOnly = $$CheckProcessoReadOnly^VARSESCOMPRAS(pNoProcessoCompra)
	if (isReadOnly = $$$YES) {
		$$$Alert("Não é possível alterar. Processo somente-leitura para este usuário ou posição.")
		quit
	}
	
	;Verifica se ainda não extrapolou o limite da compra para o item em questão	
	if (pTipoResultado = 1) {
		set idProduto = $$GetProdutoFromParecer(pNoProcessoCompra,pItem)
		set idLocal   = $$GetLocaldeCompra^VARSESCOMPRAS(pNoProcessoCompra)
		set idUnidade = $$getUnidadeSaudeLocal^VARSESWWW0121(idLocal)
		set anoCorrente = $extract($zdate($horolog,4),7,10)
	
		set fltTotalCompradoAno   = $$GetTotalCompradoAno^VARSESCOMPRAS(idProduto,idUnidade,anoCorrente)
		set fltTotalSendoComprado = $$GetTotalPropostaItem(pNoProcessoCompra,pItem,pParticipante)
		
		set idModalidade = $$GetModalidade^VARSESCOMPRAS(pNoProcessoCompra)
		set idEnquadramentoLegal = $$GetEnquadramentoLegal^VARSESCOMPRAS(pNoProcessoCompra)			
		set lmtMaximo = $$GetLimiteMaximoModalidade^VARSESCOMPRAS(idModalidade,idEnquadramentoLegal)
		
		set fltTotalGeral = fltTotalCompradoAno + fltTotalSendoComprado
		set fltValorExcedente = (lmtMaximo - fltTotalGeral) * -1
		
		if ( (fltTotalCompradoAno + fltTotalSendoComprado) > lmtMaximo ) {
			
			write "javascript:alert('Não é possível executar esta ação pois a compra extrapola o limite legal definido para o produto no ano. \n\n"
			write "Valor já comprado no ano: "_$$FormatCurrency^COMTable(fltTotalCompradoAno)_" \n"
			write "Valor desta compra: "_$$FormatCurrency^COMTable(fltTotalSendoComprado)_" \n"
			write "Total: "_$$FormatCurrency^COMTable(fltTotalGeral)_" \n"
			write "Limite legal: "_$$FormatCurrency^COMTable(lmtMaximo)_" \n"
			write "Valor excedente: "_$$FormatCurrency^COMTable(fltValorExcedente)_"');"
 
			quit
		}
	}
	
	;Verifica se já teve algum recebimento para o processo e produto	
	set idProduto = $$GetCodigoItem^VARSESCOMPRAS(pNoProcessoCompra,pItem)
	
	if ($$CheckRecebimento^VARRecRegional(pNoProcessoCompra,,idProduto) = $$$YES) {
		write "javascript:alert('Não é possível alterar o vencedor pois já houve recebimento no local de entrega para este item.');"
		
		quit
	}
	
	
	//Atualiza o check (marcar ou desmarcar)	
	if pTipoResultado = 0 set pTipoResultado = ""
	set objPropostaLinha = $get(^VARSESCOMPRASPropostaLinha(YM,pNoProcessoCompra,pItem,pParticipante,1))
 
	set $piece(objPropostaLinha,Y,26) = pTipoResultado
	set keyPropostaLinha = pNoProcessoCompra_","_pItem_","_pParticipante
	
	set strStatus = $$$OK
	set strStatus = $$$Save("VARSESCOMPRASPropostaLinha",keyPropostaLinha,objPropostaLinha,1)
		
	if (strStatus = $$$OK) {	
		;Atualizando a posição do processo		
		set objCompra = $get(^VARSESCOMPRAS(YM,pNoProcessoCompra,1))		
		set objCompraNovo = objCompra
		
		set statusProcesso = $piece(objCompra,Y,7)
		
		set idModalidade = $$GetModalidade^VARSESCOMPRAS(pNoProcessoCompra)	
 
		;Verifica se a etapa de parecer está concluído e atualiza o processo caso esteja
		set isParecerRealizado = $$$NO
		set isParecerRealizado = $$checkCadastroParecer(pNoProcessoCompra,objCompra)
		
		;PositionHere -> 8 - Aguardando Parecer de Propostas (Avançando)
		if ( (statusProcesso = 8) && (isParecerRealizado = $$$YES) ) {			
		
			if (idModalidade = 10) { ;PDPAS
					
				;PositionHere -> 11 - Processo de Compra Finalizado
				set $piece(objCompraNovo,Y,7) = 11
					
				; Atualiza log de conclusão do processo de compra
				set $piece(objCompraNovo,Y,38) = $piece($horolog,",",1)
				set $piece(objCompraNovo,Y,39) = $piece($horolog,",",2)
				set $piece(objCompraNovo,Y,40) = YBED
			
			} else {
				;PositionHere -> 9 - Aguardando Ordenador de Despesas
				set $piece(objCompraNovo,Y,7) = 9
				set ^VARSESTempDispensaVoltar(YBED) = 1	
			}
		
		;PositionHere -> 11 - Processo Finalizado (Retornando)
		} elseif ( (isParecerRealizado '= $$$YES) && (pTipoResultado '= 1) ) {
			
			if ( ((statusProcesso = 11) && (idModalidade = 10)) || ((statusProcesso = 9) && (idModalidade '= 10)) ) {
				;PositionHere -> 8 - Aguardando Parecer de Propostas
				set $piece(objCompraNovo,Y,7) = 8
			}
		}
		
		if (objCompraNovo '= objCompra) {
			set strStatus = $$$Save("VARSESCOMPRAS",pNoProcessoCompra,objCompraNovo,1)
		}
	}
 
	if $$$ISERR(strStatus) {
		do ReturnError^COMUtilError(strStatus)
	}		
	
	set YKEY = pNoProcessoCompra
	do RefreshForm^COMUtilForm()
 
	quit
 
checkCadastroParecer(pNoCompra,pobjCompra)
	;-------------------------------------------------------------------------------
    ; Verifica a posição do processo caso ao menos um fornecedor para cada item tenha
    ; tido sua proposta aprovada e caso positivo, retorna que a compra deve ser finalizada
    ; 
    ; History:
    ; 30-Apr-2011	Gustavo Fiuza		Updated - Lógica de encerramento
    ; 02-Oct-2009	Gustavo Fiuza		Changed
    ; 29-Aug-2009	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	new count, blnItemHasParecer, idLinha, blnAtualizaProcesso, idFornecedor, objParecer, 
		ResultadoAnalise
	
	set count = 0
	set blnItemHasParecer   = $$$NO
	set blnAtualizaProcesso = $$$NO
 	
	$$$Order3(^VARSESCOMPRASPropostaLinha,YM,pNoCompra,idLinha)			
 
 		//Se o item estiver encerrado, não considera como critério para atualizar o processo.
 		continue:($$isItemEncerrado^VARSESCOMPRASPDPASLinha(pNoCompra,idLinha) = $$$YES)
		
		if (blnItemHasParecer = $$$YES) {
			set blnAtualizaProcesso = $$$YES
		}
			
		elseif ((blnItemHasParecer = $$$NO) && (count > 0)) {
			set blnAtualizaProcesso = $$$NO
			quit
		}
					
		//Iniciando um novo item
		set count = $i(count)
		set blnItemHasParecer = $$$NO
			
		$$$Order4(^VARSESCOMPRASPropostaLinha,YM,pNoCompra,idLinha,idFornecedor)
			
			set objParecer = $get(^VARSESCOMPRASPropostaLinha(YM,pNoCompra,idLinha,idFornecedor,1))
			
			set ResultadoAnalise = $piece(objParecer,Y,26)
			if (ResultadoAnalise = 1) {
				set blnItemHasParecer = $$$YES
				set blnAtualizaProcesso = $$$YES
				quit
			}
			else {
				set blnItemHasParecer   = $$$NO
				set blnAtualizaProcesso = $$$NO
			}
		
		$$$End
	$$$End
	
	quit blnAtualizaProcesso
	
	
InsertCellCallback(pstrText,pstrRoutine,pstrParams="")
	new strHTML,idx,intLoop
	set intLoop=$length(pstrParams,",")
	set strHTML="<a onclick='doLink(this);' href='javascript:CallBack("""_pstrRoutine_""","
	
	for idx=1:1:intLoop {
		set strHTML=strHTML_""""_$piece(pstrParams,",",idx)_""""
		if (intLoop'=1)&&(idx'=intLoop) set strHTML=strHTML_","
	}
	
	set strHTML=strHTML_");'>"_pstrText_"</a>"
 
	do InsertCell^COMTable(strHTML,,,,"CENTER")
 
	quit
	
HideAbaParecerJuridico(pYKEY)
	;-------------------------------------------------------------------------------
    ; Esconde a aba parecer jurídico dependendo da modalidade de compra.
    ; 
    ; History:
    ; 30-Mar-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	new Modalidade	
	set Modalidade = $$GetModalidade^VARSESCOMPRAS(pYKEY)
	
	if (Modalidade = 10) {  ;10-PDPAS	
		quit $$$YES
	} else {
		quit $$$NO
	}
 
	quit $$$NO
	
GetProdutoFromParecer(pNoProcessoCompra,pItem)	
	new idProduto
	
	&sql(SELECT Produto
		INTO  :idProduto
		FROM  VARSESCOMPRASPropostaLinha
   		WHERE NoProcessodeCompra = :pNoProcessoCompra
   		  AND Item = :pItem
    )
    
    quit idProduto
    
    
GetTotalPropostaItem(pNoProcessoCompra,pItem,pidFornecedor)	
	new fltPrecoTotal	
	set fltPrecoTotal = ""
	
	&sql(SELECT PrecoTotal
		INTO  :fltPrecoTotal
		FROM  VARSESCOMPRASPropostaLinha
   		WHERE NoProcessodeCompra = :pNoProcessoCompra
   		  AND Item = :pItem
   		  AND Fornecedor = :pidFornecedor
    )
    
    quit fltPrecoTotal
    
GetVencedorFornecedor(pNoProcessoCompra,pItem)
	;-------------------------------------------------------------------------------
    ; Retorna o fornecedor que venceu o item do processo de compra.
    ; 
    ; History:
    ; 28-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pNoProcessoCompra = "") ""
	quit:(pItem = "") ""
 
	new idFornecedor	
	set idFornecedor = ""
	
	&sql(SELECT Fornecedor
		 INTO  :idFornecedor
		 FROM  VARSESCOMPRASPropostaLinha
		 WHERE NoProcessodeCompra = :pNoProcessoCompra
		   AND Item = :pItem
		   AND $UPPER(Resultado) = 1
	)	
	
	quit idFornecedor
	
GetVencedorPrecoUnitario(pNoProcessoCompra,pItem)
	;-------------------------------------------------------------------------------
    ; Retorna o preço unitário do item que venceu o processo de compra.
    ; 
    ; History:
    ; 28-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pNoProcessoCompra = "") ""
	quit:(pItem = "") ""
 
	new fltPrecoUnitario	
	set fltPrecoUnitario = ""
	
	&sql(SELECT PrecoUnitario
		 INTO  :fltPrecoUnitario
		 FROM  VARSESCOMPRASPropostaLinha
		 WHERE NoProcessodeCompra = :pNoProcessoCompra
		   AND Item = :pItem
		   AND $UPPER(Resultado) = 1
	)	
	
	quit fltPrecoUnitario
	
GetVencedorPrecoTotal(pNoProcessoCompra,pItem)
	;-------------------------------------------------------------------------------
    ; Retorna o preço total do item que venceu o processo de compra.
    ; 
    ; History:
    ; 28-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pNoProcessoCompra = "") ""
	quit:(pItem = "") ""
 
	new fltPrecoTotal
	set fltPrecoTotal = ""
	
	&sql(SELECT PrecoTotal
		 INTO  :fltPrecoTotal
		 FROM  VARSESCOMPRASPropostaLinha
		 WHERE NoProcessodeCompra = :pNoProcessoCompra
		   AND Item = :pItem
		   AND $UPPER(Resultado) = 1
	)	
	
	quit fltPrecoTotal	
	
GetVencedorQuantidade(pNoProcessoCompra,pItem)
	;-------------------------------------------------------------------------------
    ; Retorna a quantidade do item que venceu o processo de compra.
    ; 
    ; History:
    ; 28-Apr-2011	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pNoProcessoCompra = "") ""
	quit:(pItem = "") ""
 
	new fltQuantidadeOfertada	
	set fltQuantidadeOfertada = ""
	
	&sql(SELECT QuantidadeOfertada
		 INTO  :fltQuantidadeOfertada
		 FROM  VARSESCOMPRASPropostaLinha
		 WHERE NoProcessodeCompra = :pNoProcessoCompra
		   AND Item = :pItem
		   AND $UPPER(Resultado) = 1
	)	
	
	quit fltQuantidadeOfertada
	
OnAfterSave(pYKEY)
	quit:(pYKEY="")
	
	new idCompra, linhaProduto, idFornecedor
	
	set idCompra = pYKEY
	set linhaProduto = ""
	
	for {
		set linhaProduto = $order(^VARTempCompraParecer(YM,idCompra,linhaProduto))
		quit:(linhaProduto = "")
		
		set idFornecedor = ""
		
		for {
			set idFornecedor = $order(^VARTempCompraParecer(YM,idCompra,linhaProduto,idFornecedor))
			quit:(idFornecedor = "")
			
			set objCompraTemp = $get(^VARTempCompraParecer(YM,idCompra,linhaProduto,idFornecedor))
						
			set chave = idCompra_$$$COMMA_linhaProduto_$$$COMMA_idFornecedor
			set pTipoResultado = $piece(objCompraTemp,$$$COMMA,1)
	
			set objProposta   = $get(^VARSESCOMPRASPropostaLinha(YM,idCompra,linhaProduto,idFornecedor,1))				
			set dteProposta   = $$GetDataProposta^VARSESCOMPRASProposta(objProposta)
			set quantOfertada = $piece(objProposta,Y,16)
			set vlueTotal     = $piece(objProposta,Y,18)
			set resultado     = $piece(objProposta,Y,26)
	
		    //Atualiza o check (marcar ou desmarcar)	
			if pTipoResultado = 0 set pTipoResultado = ""
			set objPropostaLinha = $get(^VARSESCOMPRASPropostaLinha(YM,idCompra,linhaProduto,idFornecedor,1))

			set $piece(objPropostaLinha,Y,26) = pTipoResultado
			set keyPropostaLinha = idCompra_","_linhaProduto_","_idFornecedor

			set strStatus = $$$OK
			set strStatus = $$$Save("VARSESCOMPRASPropostaLinha",keyPropostaLinha,objPropostaLinha,1)

			if (strStatus = $$$OK) {	
				;Atualizando a posição do processo		
				set objCompra = $get(^VARSESCOMPRAS(YM,idCompra,1))		
				set objCompraNovo = objCompra

				set statusProcesso = $piece(objCompra,Y,7)

				set idModalidade = $$GetModalidade^VARSESCOMPRAS(idCompra)	

				;Verifica se a etapa de parecer está concluído e atualiza o processo caso esteja
				set isParecerRealizado = $$$NO
				set isParecerRealizado = $$checkCadastroParecer(idCompra,objCompra)

				;PositionHere -> 8 - Aguardando Parecer de Propostas (Avançando)
				if ( (statusProcesso = 8) && (isParecerRealizado = $$$YES) ) {			

					if (idModalidade = 10) { ;PDPAS
		
						;PositionHere -> 11 - Processo de Compra Finalizado
						set $piece(objCompraNovo,Y,7) = 11
		
						; Atualiza log de conclusão do processo de compra
						set $piece(objCompraNovo,Y,38) = $piece($horolog,",",1)
						set $piece(objCompraNovo,Y,39) = $piece($horolog,",",2)
						set $piece(objCompraNovo,Y,40) = YBED

					} else {
						;PositionHere -> 9 - Aguardando Ordenador de Despesas
						set $piece(objCompraNovo,Y,7) = 9
						set ^VARSESTempDispensaVoltar(YBED) = 1	
					}

				;PositionHere -> 11 - Processo Finalizado (Retornando)
				} elseif ( (isParecerRealizado '= $$$YES) && (pTipoResultado '= 1) ) {

					if ( ((statusProcesso = 11) && (idModalidade = 10)) || ((statusProcesso = 9) && (idModalidade '= 10)) ) {
						;PositionHere -> 8 - Aguardando Parecer de Propostas
						set $piece(objCompraNovo,Y,7) = 8
					}
				}

				if (objCompraNovo '= objCompra) {
					set strStatus = $$$Save("VARSESCOMPRAS",idCompra,objCompraNovo,1)
				}
			}

			if $$$ISERR(strStatus) {
				do ReturnError^COMUtilError(strStatus)
			}		

			set YKEY = idCompra
		}
	}

	quit
	
CheckVencedorMarcado(pidCompra,plinhaProduto)
	
	new status, linhaProduto, count, idFornecedor
	
	set status = $$$NO
		
	set count = 0
	set linhaProduto = ""
	
	for {
		set linhaProduto = $order(^VARTempCompraParecer(YM,pidCompra,linhaProduto))
		quit:(linhaProduto = "")
		continue:(linhaProduto '= plinhaProduto)
		
		set idFornecedor = ""
		
		for {
			set idFornecedor = $order(^VARTempCompraParecer(YM,pidCompra,linhaProduto,idFornecedor))
			quit:(idFornecedor = "")
			
			set objCompraTemp = $get(^VARTempCompraParecer(YM,pidCompra,linhaProduto,idFornecedor))
			
			if ($piece(objCompraTemp,$$$COMMA,1) = 1){
				set count = $increment(count)
			}
		}		
	}

	if (count > 0){
		set status = $$$YES
	}
	
	quit status
]]></Routine>
</Export>