<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPPaymentCopy" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPPaymentCopy
	#include FINConst
	#include COMSYS
 
DoCopy(pidSource)
    ;-------------------------------------------------------------------------------
    ; Routine to copy a payment run
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Oct-2006	Steve S		SR15074: Don't copy Cheque Print Date
    ; 29-Sep-2006	Steve S		SR15012: Do not copy report ID's.
    ; 21-Feb-2006	SC+JW		SR14016: Remove cheque number from line. Update Cheque Numbers
    ; 31-Jan-2006	SC			SR13216: Remove additional form for copying, automatically 
    ; 							select new payment number. 
    ; 							
    ; 17-Oct-2005	Steve S		SR13216: Created
    ;-------------------------------------------------------------------------------
	new strStatus,objNewPay,idLine,objLine,idInvoice,objInvoice,strText,intOldCheque
 	new idDest,objOldPay,intNumber,dteToday
 	
	tstart
	set strStatus=$$$OK
 
	set idDest=$$$NextKey("FINAPPayment")
 
	/*
	if $$$FINAPPaymentIsCopied($get(^FINAPPayment(YM,pidSource,1)) {
		set strStatus=$listbuild("Fin00962",pidSource) ;Cannot copy.  Payment Run %1 has already been copied.
	} elseif $get(pidSource)="" {
		set strStatus=$lb("Fin00915",$$^WWWFELDNAME("FINAPPaymentCopy","M",1))  ;Require value for field `%1`.	
	} elseif $get(idDest)="" {
		set strStatus=$lb("Fin00915",$$^WWWFELDNAME("FINAPPaymentCopy","M",2))  ;Require value for field `%1`.
	} elseif $data(^FINAPPayment(YM,idDest)) {
		set strStatus=$lb("Fin00916",idDest)  ;Payment run `%1` already exists!	
	 */ 
	//} else {
		set strStatus=$$CanCopy(pidSource)
		if $$$ISOK(strStatus) {
			;if idDest="+" set idDest=$$$NextKey("FINAPPayment")
			
			set objNewPay=$get(^FINAPPayment(YM,pidSource,1))
			set dteToday = +$horolog
			
			set $$$FINAPPaymentEnteredDate(objNewPay)		 =dteToday
			set $$$FINAPPaymentPaymentDate(objNewPay)		 =dteToday
			set $$$FINAPPaymentPostedDate(objNewPay)		 =""
			set $$$FINAPPaymentModifiedDate(objNewPay)		 =""
			set $$$FINAPPaymentModifedBy(objNewPay)			 =""
			set $$$FINAPPaymentCreatedDate(objNewPay)		 =dteToday
			set $$$FINAPPaymentCreatedBy(objNewPay)			 =YBED
			set $$$FINAPPaymentDueDate(objNewPay)			 =dteToday
			set $$$FINAPPaymentStatus(objNewPay)			 =$$$EnumFINSYSSTATUSUnposted
			set $$$FINAPPaymentDateofcheque(objNewPay)		 =""
			set $$$FINAPPaymentInterfaceBatchID(objNewPay)   =""
			set $$$FINAPPaymentCopiedFrom(objNewPay)		 =pidSource
			set $$$FINAPPaymentAutoCleared(objNewPay)		 =""
			set $$$FINAPPaymentBankReconciliation(objNewPay) =""
			set $$$FINAPPaymentDateofcheque(objNewPay)		 =dteToday  //SR15074
			set $$$FINAPPaymentChequePrinted(objNewPay)		 ="" 		//SR15074
			
			//set intOldCheque=$$$FINAPPaymentNextChequeNumber(objNewPay)
			//set YFELD=objNewPay
			//set $$$FINAPPaymentNextChequeNumber(objNewPay)=$$UpdateLastChequeNumber^FINAPPayment(intOldCheque) ;SR14016
			set intNumber   = $$GetLastNumber^FINAPChequeBook($$$FINAPPaymentBankAccount(objNewPay))
			set $$$FINAPPaymentLastChequeNumber(objNewPay) = intNumber
			do GetNextCheque^FINAPChequeBook($$$FINAPPaymentBankAccount(objNewPay),.intNumber)
	 		set $$$FINAPPaymentNextChequeNumber(objNewPay) = intNumber
	 		
			set strStatus=$$$Save("FINAPPayment",idDest,objNewPay,1)
			
			;SR13216
			if $$$ISOK(strStatus){
				;set source FINAPPayment IsCopied field to 'Yes'
				set objOldPay=$get(^FINAPPayment(YM,pidSource,1)) 
				set $$$FINAPPaymentIsCopied(objOldPay)=$$$YES
				set strStatus=$$$Save("FINAPPayment",pidSource,objOldPay,1)
			}
				
			if $$$ISOK(strStatus) {
				set idLine=""
				for {
					set idLine=$order(^FINAPPaymentLine(YM,pidSource,idLine))
					quit:(idLine="")||($$$ISERR(strStatus))
						
					set objLine=$get(^FINAPPaymentLine(YM,pidSource,idLine,1))
					
					set $$$FINAPPaymentLinePostingBatchNumber(objLine)	  =""
					set $$$FINAPPaymentLinePostingBlock(objLine)		  =""
					set $$$FINAPPaymentLineReverseInterfaceBatch(objLine) =""
					set $$$FINAPPaymentLineReversalUser(objLine)		  =""
					set $$$FINAPPaymentLineReversalDate(objLine)		  =""
					set $$$FINAPPaymentLineStatus(objLine)				  =$$$EnumFINSYSSTATUSUnposted
					set $$$FINAPPaymentLineModifiedon(objLine)			  =""
					set $$$FINAPPaymentLineModifiedby(objLine)			  =""
					set $$$FINAPPaymentLineCreatedon(objLine)			  =""
					set $$$FINAPPaymentLineCreatedby(objLine)			  =""
					set $$$FINAPPaymentLineChequeNumber(objLine)		  ="" //SR14016
					set $$$FINAPPaymentLinePaymentReportID(objLine)		  ="" //SR15012
					set $$$FINAPPaymentLineCancellationReportID(objLine)  ="" //SR15012
					
					set strStatus=$$$Save("FINAPPaymentLine",idDest_","_idLine,objLine,1)
										
					if $$$ISOK(strStatus) {
						set idInvoice=""
						for {
							set idInvoice=$order(^FINAPPaymentInvoice(YM,pidSource,idLine,idInvoice))
							quit:(idInvoice="")||($$$ISERR(strStatus))
							
							set objInvoice=$get(^FINAPPaymentInvoice(YM,pidSource,idLine,idInvoice,1))
							
							set strStatus=$$$Save("FINAPPaymentInvoice",idDest_","_idLine_","_idInvoice,objInvoice,1)	
						}	
					}
				}				
			}
		}
	//}
 
	if $$$ISOK(strStatus) {
		if $tlevel>0 tcommit
		do GoToForm^COMUtilForm(YFORM,idDest) ;SR13216
		;set strText=$$^WWWFELDNAME("FINAPPaymentCopy","M",2)_": "_idDest
		;do ^WWWINFO($$$Text("Fin00840")_" ("_strText_")") ;Copy Successful 
	} else {
		trollback
		do ReturnError^COMUtilError(strStatus)
	}
 
	quit
	
CanCopy(pidPayment)
    ;-------------------------------------------------------------------------------
    ; Determines whether a payment run can be copied (must be cancelled)
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Jan-2006 	SC			SR13798: Decode wrapper removed. Decode in COMSYS.INC
    ; 17-Oct-2005	Steve S		SR13216: Created
    ;-------------------------------------------------------------------------------
	new strStatus,objPayment,intLine, objPaymentLine
 
	set strStatus=$$$OK
 
 	;can copy if Payment record cancelled.
	if $get(pidPayment)'="" {
		set objPayment=$get(^FINAPPayment(YM,pidPayment,1))
		if $$$FINAPPaymentStatus(objPayment)'=$$$EnumFINSYSSTATUSCancelled {
			set strStatus=$lb("Fin00914")  ;Can only copy cancelled payment runs
			
		} elseif $$$FINAPPaymentIsCopied(objPayment) { ;SR13216
			set strStatus=$listbuild("Fin00962",pidPayment) ;Cannot copy.  Payment Run %1 has already been copied.
		}
	}
	
	if $$$ISOK(strStatus) {
		set YQ=$$$YQEnable	
	} else {
		set YQ=$$$YQDisable(strStatus) ;SR13798
	}
 
	quit strStatus
]]></Routine>
</Export>