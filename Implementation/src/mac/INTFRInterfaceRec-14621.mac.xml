<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INTFRInterfaceRec" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INTFRInterfaceRec
#include COMSYS
 
 
OpenReceipt(pidTFR,&pidRec)
	;-------------------------------------------------------------------------------
	; Whether there is an open (unprocessed) receipt for a certain transfer
	;
	; Params:	pidTFR - INTFR id
	;
	; ByRefs:	pidRec - first unprocessed receipt (null if none)
	;
	; Returns:	boolean - whether open receipt exists
	;
	; History:
	; 21-Nov-2006	JW		SR15239: Created
	;-------------------------------------------------------------------------------
	new idxTfr
	
	set idxTfr = $$$Index(pidTFR)
	$$$Order4(^INRECs,YM,1,idxTfr,pidRec)
		quit:$$Editable^INREC($get(^INREC(YM,pidRec,1)))
	$$$End
	
	quit (pidRec'="")
	
ShowReceipts(pidTFR)
	;-------------------------------------------------------------------------------
	; Show the receipts of the transfer
	;
	; Params:	pidTFR - INTFR id
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 21-Nov-2006	JW		SR15239: Created
	;-------------------------------------------------------------------------------
	new idxTfr,arrQuery,intCol
	
	set idxTfr = $$$Index(pidTFR)
	if $data(^INRECs(YM,1,idxTfr)) {
	
		set arrQuery("Class")	= "INREC"
		set arrQuery("Loop")	= "ReceiptsQuery^INTFRInterfaceRec"
		set arrQuery("Get")		= "set id=$get(^INREC(YM,id(1),1))"
		set arrQuery("Link") 	= "INRECTFR~P~1"
		set arrQuery("Title")	= $$^WWWFORMNAME("INRECTFR")
	 
		set arrQuery("Col",$increment(intCol))	= "P~1~L"
		set arrQuery("Col",$increment(intCol))	= "D~6~L"
		set arrQuery("Col",$increment(intCol))	= "D~5~L"
		set arrQuery("Col",$increment(intCol))	= "D~8~L"
		set arrQuery("Col",$increment(intCol))	= "D~7~L"
		set arrQuery("Col",$increment(intCol))	= "D~4~L"
		
		do Query^COMQuery(.arrQuery,idxTfr)
	}
	
	quit
	
	
ReceiptsQuery(parrQuery,&parrStorage,pidxTFR)
	;-------------------------------------------------------------------------------
	; Loop for the COMQuery above - show related receipts
	;
	; Params:	parrQuery	- COMQuery info
	; 			parrStorage	- COMQuery data
	; 			pidxTFR		- indexed INFTR id
	;
	; ByRefs:
	;
	; Returns:	int - row count
	;
	; History:
	; 22-Nov-2006	JW		SR15239: Created
	;-------------------------------------------------------------------------------
	new idRec,intCount
	set intCount=0
	
	$$$Order4(^INRECs,YM,1,pidxTFR,idRec)
		set @parrStorage@($increment(intCount),1) = idRec
	$$$End
	
	quit intCount
	
]]></Routine>
</Export>