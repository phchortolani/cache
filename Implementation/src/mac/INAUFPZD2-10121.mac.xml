<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPZD2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPZD2	;INAUFPZD2;FIS;GESAMTBETRAG PRÜFEN;27.07.2004
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		GESAMTBETRAG PRÜFEN
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	27.07.2004
	;|
	;\------------------------------------------------------------------/
	;
	NEW YFELD,YKEY,AUF,POS,BILL,PAID,DAT1,POS1,DAT
	SET YKEY=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1))
	SET YFELD=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1))
	SET AUF=$PIECE(YKEY,",",1)
	QUIT:AUF=""
	SET AMOUNT=0  //TOTAL ORDER AMOUNT
	SET POS=""
	FOR {
	 SET POS=$ORDER(^INAUFP(YM,AUF,POS))
	 QUIT:POS=""
	 SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	 IF $PIECE($GET(^INAUF(YM,AUF,1)),Y,71)'=0 SET $PIECE(POS1,Y,123)=$PIECE(POS1,Y,123)-($PIECE(POS1,Y,123)/100*$PIECE(^INAUF(YM,AUF,1),Y,71))  //ORDER DISCOUNT
	 SET AMOUNT=AMOUNT+$PIECE(POS1,Y,123)
	}
	QUIT:AMOUNT=0
	SET PAID=0    //PREPAID AMOUNT
	SET DAT=""
	FOR {
	 SET DAT=$ORDER(^INAUFPZ(YM,AUF,DAT))
	 QUIT:DAT=""
	 SET DAT1=$GET(^INAUFPZ(YM,AUF,DAT,1))
	 IF $PIECE(YKEY,",",2)'=DAT {  //NOT THE CURRENT RECORD
		 IF +$PIECE(DAT1,Y,2)'=0 {
			 SET PAID=PAID+$PIECE(DAT1,Y,2)
		 }
		 ELSEIF +$PIECE(DAT1,Y,3)'=0 {
			 SET PAID=PAID+((AMOUNT/100)*$PIECE(DAT1,Y,3))
		 }
	 }
	}
	IF +$PIECE(YFELD,Y,2)'=0 {   //CURRENT RECORD
	 SET PAID=PAID+$PIECE(YFELD,Y,2)
	}
	ELSEIF +$PIECE(YFELD,Y,3)'=0 {
	 SET PAID=PAID+((AMOUNT/100)*$PIECE(YFELD,Y,3))
	}
	IF AMOUNT-PAID<0 SET %TXT(1)="!"_$$^WWWTEXT(33936,,1)_" ("_$$^WWWZAHL(AMOUNT,0,2,YWHR)_")"
	QUIT
]]></Routine>
</Export>