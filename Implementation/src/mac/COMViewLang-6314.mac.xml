<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewLang" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewLang
 
#include COMConst
#include COMSYS
 
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Start the Translation Favourite Grid
	;
	; History:
	; 27-Jun-2007	RPW		SRadhoc: Do not goto the translations when there is no
	;							class or language
    ; 24-Apr-2007	FrankF	SRBR014441: Translation for favourites.
	;-------------------------------------------------------------------------------
	new idClass,idLanguage,YAUSWAHL
	
	set idClass    = $$$KEY1(YPARA)
	set idLanguage = $$$KEY2(YPARA)
	
	quit:$$$NoKey(idClass)||($$$NoKey(idLanguage))
	
	do TitleCOMViewFavourites
	do CreateEmptyTranslations(idClass, idLanguage)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)          = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)       = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)            = "COMViewLang"
	set $$$COMGridEditParameterEnabled(YAUSWAHL)             = $$$YES
	set $$$COMGridEditParameterContainer(YAUSWAHL)           = "COMViewTranslation"
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$YES
	set $$$COMGridEditParameterDontStoreAll(YAUSWAHL)        = $$$YES
	
	do Start^COMGridEdit31("COMViewLang", idClass_$$$COMMA_idLanguage)
 
	quit
	
	
CreateEmptyTranslations(pidClass, pidLanguage)
	;-------------------------------------------------------------------------------
	; If the COMViewLang global is empty for a given pidClass and pidLanguage, no line will
	; show up in the grid, not allowing the user to translate it. In order to make it work,
	; if there is no record, it will create a translation with an empty string.
	; 
	; NOTE: Doesn't translate EN even if present.
	;
	; Params:
	;	pidClass    - the class of the favourite
	;	pidLanguage - the language to be translated
	;
	; History:
    ; 24-Apr-2007	FrankF		SRBR014441: Translation for favourites.
	;-------------------------------------------------------------------------------
	new idFavourite,objFavourite,objFavouriteTranslation,strStatus
	
	set idFavourite = ""
	for {
		set idFavourite = $order(^COMView(0,pidClass,idFavourite))
		quit:idFavourite=""
		
		set objFavourite            = $get(^COMView(0,pidClass,idFavourite,1))
		set objFavouriteTranslation = $get(^COMViewLang(0,pidClass,pidLanguage,idFavourite,1))
		
		if $$$COMViewLangOriginalText(objFavouriteTranslation) = "" {
			set $$$COMViewLangOriginalText(objFavouriteTranslation) = $$$COMViewDescription(objFavourite)
			if pidLanguage = "EN" {
				set $$$COMViewLangDescription(objFavouriteTranslation) = $$$COMViewDescription(objFavourite)
			}
			
			set strStatus = $$$Save("COMViewLang",pidClass_$$$COMMA_pidLanguage_$$$COMMA_idFavourite,objFavouriteTranslation)
		}
	}
	do CopyOriginalText(pidClass, pidLanguage)
	quit
	
	
CopyOriginalText(pidClass, pidLanguage)
	;-------------------------------------------------------------------------------
	; A copy of the original text is copied to the COMViewLang class, so the user can see it
	; as a column of the grid.
	;
	; Params:
	;	pidClass - the class of the favourite
	;	pidLanguage - the language to be translated
	;
	; History:
    ; 24-Apr-2007	FrankF	SRBR014441: Translation for favourites.
	;-------------------------------------------------------------------------------
	new idFavourite,objFavourite,objFavouriteTranslation,strStatus
	
	set idFavourite = ""
	for {
		set idFavourite = $order(^COMView(0, pidClass, idFavourite))
		quit:idFavourite=""
		
		set objFavourite            = $get(^COMView(0,pidClass,idFavourite,1))
		set objFavouriteTranslation = $get(^COMViewLang(0,pidClass,pidLanguage,idFavourite,1))
			
		if $$$COMViewLangOriginalText(objFavouriteTranslation) '= $$$COMViewDescription(objFavourite) {
			set $$$COMViewLangOriginalText(objFavouriteTranslation) = $$$COMViewDescription(objFavourite)
			set strStatus = $$$Save("COMViewLang",pidClass_$$$COMMA_pidLanguage_$$$COMMA_idFavourite,objFavouriteTranslation,$$$YES)
		}
	}
	quit
	
	
SaveTranslation()
	;-------------------------------------------------------------------------------
	; Saves the translations made by the user. This function is triggered when the
	; save button of the grid is pressed.
	;
	; History:
	; 04-May-2007	RPW		SRBR014441: Removed all the $piece functions and left just 1.
    ; 24-Apr-2007	FrankF		SRBR014441: Translation for favourites.
	;-------------------------------------------------------------------------------
	new YKEY
	
	set YKEY = $piece(YPARA,$$$COMMA,1,2)
	do SAVE^COMGridEdit31Save(YKEY) 
	do GoToForm^COMUtilForm("COMViewTranslation",YKEY,,YKEY)
	quit
	
	
SaveHook()
	;-------------------------------------------------------------------------------
	; Sinchronizes the translation of the 'EN' language into the base class.
	;
	; History:
    ; 24-Apr-2007	FrankF	SRBR014441: Translation for favourites.
	;-------------------------------------------------------------------------------
	new idClass, idFavourite, idLanguage, objFavourite, objFavouriteTranslation, strStatus
	
	set strStatus  = $$$OK
	set idLanguage = $$$KEY2(YKEY)
	
	if idLanguage = "EN" {
		set idClass     = $$$KEY1(YKEY)
		set idFavourite = $$$KEY3(YKEY)
		
		if (idClass'="") && (idFavourite'="") {
			set objFavourite            = $get(^COMView(0,idClass,idFavourite,1))
			set objFavouriteTranslation = $get(^COMViewLang(0,idClass,idLanguage,idFavourite,1))
			
			if $$$COMViewDescription(objFavourite) '= $$$COMViewLangDescription(objFavouriteTranslation) {
				set $$$COMViewDescription(objFavourite) = $$$COMViewLangDescription(objFavouriteTranslation)
				
				set strStatus = $$$Save("COMView",idClass_$$$COMMA_idFavourite,objFavourite,$$$YES)
			}
		}			
	} 
	quit strStatus
	
	
SaveHookBeforeSave()
	;-------------------------------------------------------------------------------
	; Sinchronizes the translation of the 'EN' language into the base class.
	;
	; History:
    ; 24-Apr-2007	FrankF	SRBR014441: Translation for favourites.
	;-------------------------------------------------------------------------------
	new idLanguage
	
	set idLanguage = $$$KEY2(YKEY)
	
	if idLanguage = "EN" {
		set $$$COMViewLangOriginalText(YFELD) = $$$COMViewDescription(YFELD)
	} 
	quit $$$OK
	
	
TitleCOMViewFavourites
	;-------------------------------------------------------------------------------
	; Check Language and Compiler/Translator in Employee Master Data form and include
	; these data in Translating COM View Favourites form.
	;
	; History:
    ; 26-Jun-2007	GM		SRBR014464: Created.
	;-------------------------------------------------------------------------------
	new objWWW013,strComp,strCompiler,strLang,strLanguage
	
	set objWWW013 = $get(^WWW013(0,YBED,1))
	
	set strLang = $$$WWW013Language1(objWWW013)          if strLang="" set strLang = "EN"
	set strComp = $$$WWW013CompilerTranslator(objWWW013)
	if strComp'="" {
		set strLanguage = $$$SysEnum("SPRACHE",strLang)
		set strCompiler = $$$SysEnum("SPRACHE",strComp)
	
		write YCR,"<H3 ALIGN=CENTER>"
		write YCR,strLanguage_" -> "_strCompiler
		write YCR,"</H3>"
	}
	quit
	
]]></Routine>
</Export>