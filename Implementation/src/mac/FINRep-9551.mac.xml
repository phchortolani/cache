<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRep" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRep
	
#include COMSYS
	
GetAmounts(pstrNumber,pstrDate,pstrClass)
	;--------------------------------------------------------------------------------
	; Routine to find the Trial Balance amounts.  Used by AgedBalance
	; 
	; History
	; 29-Nov-2005	PO		SR13929: Correction for SR12483
	; 18-Jun-2004	shobby	Foreign Currency
	; 28-Jul-2003	shobby	Moved from FINAPRep
	; 28-Jul-2003	shobby	Creation
	;--------------------------------------------------------------------------------
	new objAmounts,lstTrialBalances,fltTotal,i,curTmp
	
	set objAmounts=""
	
	set lstTrialBalances=""
	xecute "set lstTrialBalances=$$CalcPeriodTotal^"_pstrClass_"(pstrNumber,pstrDate)"
	if lstTrialBalances'="" {
		set fltTotal=0
		for i=1:1:3 { ; SR13929
			set objAmounts=objAmounts_$listget(lstTrialBalances,i)_Y
			set fltTotal=$$$FCAdd(fltTotal,$listget(lstTrialBalances,i))
		}
		set curTmp = $$$FCAdd($listget(lstTrialBalances,i+1),$listget(lstTrialBalances,i+2)) ; SR13929
		set objAmounts=objAmounts_curTmp_Y ; SR13929
		set fltTotal=$$$FCAdd(fltTotal,curTmp) ; SR13929
 
		set objAmounts=objAmounts_fltTotal
	}
	
	quit objAmounts
 
AgedBalance(StartRec,Result,Param,pstrClass)
	;-------------------------------------------------------------------------------
	;
	; History
	; 01-Feb-2005	Steve S	SR11648 Add check for FINARCustomer class
	; 01-Apr-2004	shobby	Protected some variables when setting mtemp.
	; 15-Mar-2004	shobby	Set a value in Rec and Amt to comply with code checker.
	; 28-Aug-2003	Paul K	Made a Default for SortBy
	; 28-Jul-2003	shobby	Made generic.  Can be used by Customers and Suppliers. Moved from FINAPRep
	; 28-Jul-2003	shobby	Reworked to allow sorting of any field.
	; 28-Jul-2003	shobby	New field for Balance.
	; 13-Jun-2003	shobby	Sorted out some variable declarations.
	; 02-Jun-2003	shobby	Find out the selected date.
	; 02-Jun-2003	shobby	Put in calls to CalcPeriodTotal^FINAPSupplier to get aged trial balance data.
	;-------------------------------------------------------------------------------
	new RepSpece,Template,TempSpec,RunCode,Level,Total,Rec,Amt,SortKey
	new SortBy,Count,Number,strDate,SortCode,objAmounts,i,strName,strClass
	
 	set strClass=$case(pstrClass,1:"FINAPSupplier",2:"FINARCustomer")
	Set SortBy=$get(Param("SortBy"))
	if SortBy="" set SortBy="Name"
	set SortKey=$case(SortBy,"Name":1,"Number":2,"Current Amount":3,"1 Month":4,"2 Months":5,"3 Months":6,"Balance":7)
	set SortCode="set SortKey=$piece(objAmounts,Y,"_SortKey_")"
	;
	set strDate=Param("Date")
	;
	kill ^mtemp("Rep",YUSER)
	kill ^mtemp("RepDrill",YUSER)
	set Number=""
	set Rec=""
	set Amt=""
 
	; Scan all Suppliers
 
	for {
		if strClass="FINAPSupplier" {
			set Number=$order(^CacheTempAPTB(YUSER,Number))
		} elseif strClass="FINARCustomer" { ;SR11648
			set Number=$order(^CacheTempARTB(YUSER,Number))
		} else {
			xecute "set Number=$order(^"_strClass_"(YM,Number))"
		}
		quit:Number=""
		xecute "set Rec=$get(^"_strClass_"(YM,Number,1))"
		set strName=$piece(Rec,Y,1)
		;set strName=$$$FINAPSupplierSupplierName(Rec)
		;set strName=$$$FINARCustomerCustomerName(Rec)
		xecute "set Amt=$get(^FINA"_$extract(strClass,5,5)_"Balance(YM,Number,1))"
		if Amt {
			set objAmounts=strName_Y_Number_Y_$$GetAmounts^FINRep(Number,strDate,strClass)
			xecute SortCode
			if $get(SortKey)="" set SortKey=" "
			if $get(Number)="" set Number=" "
			set ^mtemp("Rep",YUSER,1,SortKey_"",Number)=objAmounts ;Amt
		}
	}	
	set SortKey=""
	set Count=0
	for {
		set SortKey=$order(^mtemp("Rep",YUSER,1,SortKey))
		quit:SortKey=""
		set Number=""
		for {
			set Number=$order(^mtemp("Rep",YUSER,1,SortKey,Number))
			quit:Number=""
			set objAmounts=$get(^mtemp("Rep",YUSER,1,SortKey,Number))
			set Count=Count+1
			xecute "set ^mtemp(""Rep"",YUSER,2,Count)=$get(^"_strClass_"(YM,Number,1))"
			set ^mtemp("Rep",YUSER,2,Count,"id")=Number
			set ^mtemp("Rep",YUSER,2,Count,"Form")=strClass
			set ^mtemp("Rep",YUSER,2,Count,"Amt")=$piece(objAmounts,Y,3)
			set ^mtemp("Rep",YUSER,2,Count,"Amt1")=$piece(objAmounts,Y,4)
			set ^mtemp("Rep",YUSER,2,Count,"Amt2")=$piece(objAmounts,Y,5)
			set ^mtemp("Rep",YUSER,2,Count,"Amt3")=$piece(objAmounts,Y,6)
			set ^mtemp("Rep",YUSER,2,Count,"Balance")=$piece(objAmounts,Y,7)
		}
	}
	set Result="^mtemp(""Rep"","""_YUSER_""",2)"
	quit
 
Input(Report,Param)
	;--------------------------------------------------------------------------
	;
	; Notes
	; ^REPRepCells(YM,Index,Report,Grid,Row,Report,Grid,Cell)
	; 
	; History
	; 06-Jul-2004	shobby		Removed an error if no heading.
	; 28-Jun-2004	shobby		idxReport, UMLAU transalation of Report.
	; 28-Jul-2003	shobby		Moved from FINAPRep
	; 28-Jul-2003	shobby		Populate SortBy combo.
	;-------------------------------------------------------------------------------
	new idx,objREPRepCell,idxReport,strHeading
	
	kill ^REPRepLookUp(YM,YUSER,"SortBy")
	set idxReport=$$Index^FINSYS(Report)
	set idx=""
	for {
		set idx=$order(^REPRepCells(YM,2,idxReport,1,0,Report,1,idx))
		quit:idx=""
		set objREPRepCell=$get(^REPRepCell(YM,Report,1,idx,1))
		set strHeading=$piece(objREPRepCell,Y,5)
		if strHeading="" set strHeading=" "
		set ^REPRepLookUp(YM,YUSER,"SortBy",strHeading,1)=""
	}
	quit
	
]]></Routine>
</Export>