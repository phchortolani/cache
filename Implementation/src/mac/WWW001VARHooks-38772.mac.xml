<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW001VARHooks" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW001VARHook
	
#include COMSYS
#include WWWConst
	
ExecuteVARHook(pidClass="",penumEvent,&YKEY="",&YFELD="",YFORM="",YFELDOLD="",YFIELDNAME="",pblnCheck=$$$NO) ;CORE-286
	;-------------------------------------------------------------------------------
	; Checks if a VARHook exists for a given form/event, and runs the VARHook code.
	;
	; Params: 	Class Name, 
	; 			Event Type : WWW101("WWWEVENTTYPE") enumerator,
	; 			YKEY, YFELD, YFORM are all available as parameters to the function
	; 			YFELDOLD - Record state before event (kept in same format as the other params)
	;
	; ByRefs:
	;
	; Returns: none
	;
	; History:
	; 17-Oct-2013	shobby	CORE-286: Return a flag if a hook is found
	; 21-Aug-2013	SCR		CORE-247: Handle Expressions Example:'(($GET(YFORM)="INDispenseToPatient") && (YLOCATION="1.1"))
	; 20-Sep-2011	GRF		SR17847: penumEvent not pintEvent; Check if null
	; 26-Aug-2011	shobby	SR17847: Allow non $$ routines.
	; 26-Oct-2010	GRF		SR17541: Extended byRef YKEY to VARHook processing
	; 29-Jan-2007	JW		SR15304: Added YFELDOLD
	; 28-Sep-2006	GRF		Document ByRef argument
	; 07-Jul-2006	SC		SR14710: Skip VARHook check if no class - ie. Manual Fields.
	; 							Make pClass, YKEY, YFELD, YFORM default to null.
	; 10-Apr-2006	SC		SR14414: Created
	;-------------------------------------------------------------------------------
	new objVARHooks,strStatus,strCode
	
	set strStatus = $$$OK
 	
	if (pidClass'="") && ($get(penumEvent)'="") {
		set objVARHooks = $get(^WWW001VARHooks(0,pidClass,penumEvent,1))
		if objVARHooks '= "" {                                  ; *** EXECUTE Kill 2b ***
			set strCode		= $$$WWW001VARHooksCode(objVARHooks)
			if '((strCode?1A.AN1"^"1A.E)!(strCode?1"^"1A.E)!
				(strCode?1A.AN1"^%"1A.E)!(strCode?1"^%"1A.E)) {
				xecute "set strStatus="_strCode
			} else {
				xecute "do "_strCode
			}
			set pblnCheck=$$$YES ;CORE-286
		}
	}
	quit strStatus
	
]]></Routine>
</Export>