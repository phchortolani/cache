<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDDispenseManPickLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDDispenseManPickLine

#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst
#define YCR	"|"
#define FORMFldMEDDispenseManPickLineRxItem 	1
Enabled(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By : Form MEDDispenseMan : Grid Tab (COMGridEditParameter)
	;
	;
	; History:
	; 30-May-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new blnEnabled
	
	set blnEnabled = $$$YES
	
	if '$$$NoKey(pYKEY) {

		if $$$MEDDispenseStatus(pYFELD) > 3   {   ;"Partially Processed"
			set blnEnabled = $$$NO
		}
	}
	
	quit blnEnabled

LoadLines(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By :  MEDDispenseMan.mac
	;
	;
	; History:
	; 30-May-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new fltQtyReq,idCount,idItem,idLine,idLocn,idPPickUOM,idPat,idSrcDisp,idSrcLine
	new idToLocn,idToStorage,objLine,objResultSet,objSrcDisp,objSrcLine,sc,strSQL
	new strStatus
	do ClearDispenseLines(pYKEY)
	if $data(^MEDDispensePickLine(YM,pYKEY)) {
		quit
	}
	set strStatus = $$$OK
	set idPat	= $$$MEDDispensePatient(pYFELD)
	set idCount	= 0
	set strSQL	= "select Dispensation,MEDDispenseLine.Line FROM MEDDispense "
	set strSQL	= strSQL _ "INNER JOIN MEDDispenseLine "
	set strSQL	= strSQL _ "ON MEDDispense.Company = MEDDispenseLine.Company AND MEDDispense.Dispensation = MEDDispenseLine.Dispense "
	set strSQL	= strSQL _ "LEFT JOIN MEDPrescription "
	set strSQL	= strSQL _ "ON MEDPrescription.Company = MEDDispense.Company AND MEDPrescription.PrescriptionNumber = MEDDispense.Prescription "
	set strSQL	= strSQL _ "LEFT JOIN INART "
	set strSQL	= strSQL _ "ON MEDDispenseLine.Company = INART.Company AND MEDDispenseLine.ItemName = INART.ItemNumber "
	set strSQL	= strSQL _ "LEFT JOIN MEDPrescriptionLine ON INART.Company = MEDPrescriptionLine.Company "
	set strSQL	= strSQL _ "AND MEDDispense.Prescription = MEDPrescriptionLine.PrescriptionNumber "
	set strSQL	= strSQL _ "AND MEDDispenseLine.Line = MEDPrescriptionLine.Line "
	set strSQL	= strSQL _ "WHERE "
	set strSQL	= strSQL _ "MEDDispenseLine.Company = 0 "
	set strSQL	= strSQL _ "AND MEDDispenseLine.UDO = 1 "
	set strSQL	= strSQL _ "AND MEDDispenseLine.ValidatedBy IS NOT NULL "
	set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 10 "
	set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 6 "
	set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 9 "
	set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 8 "
	set strSQL	= strSQL _ "and patient = ?"
	set objResultSet	= ##class(%Library.ResultSet).%New()
	set sc				= objResultSet.Prepare(strSQL)
	set:sc sc			= objResultSet.Execute(idPat)
	set idLine	= 0

	set idLocn			= $$$MEDDispenseSupplyingLocation(pYFELD)
 	set idToLocn		= $$$MEDDispenseDestinationLocation(pYFELD)
	if sc {
		while objResultSet.Next() {
			quit:$$$ISERR(strStatus)
			set idSrcDisp	= objResultSet.Data("Dispensation")
			set idSrcLine	= objResultSet.Data("Line")

			set objSrcDisp	= $get(^MEDDispense(YM,idSrcDisp,1))
			set objSrcLine	= $get(^MEDDispenseLine(YM,idSrcDisp,idSrcLine,1))
			set objLine		= ""			
			set idItem		= $$$MEDDispenseLineItemName(objSrcLine)
			set fltQtyReq	=  $$$MEDDispenseLineIssueQuantity(objSrcLine)
			set idPPickUOM	= $$$MEDDispenseLineIssueUOM(objSrcLine)
			if idToLocn'="" {
				set idToStorage = $$GetStorage^INIssueAuto(idItem,idToLocn,,.idToStorage)
			}
			;set $$$MEDDispensePickLineItem(objLine)				= idItem
			set $$$MEDDispensePickLineRequestedQty(objLine)		= fltQtyReq
			set $$$MEDDispensePickLineRequestedQty(objLine)		= fltQtyReq
			;set $$$MEDDispensePickLineIssueQty(objLine)			= fltQtyIssued
			;set $$$MEDDispensePickLineIssueUOM(objLine)			= idBaseUnit
			set $$$MEDDispensePickLinePickUOM(objLine)			= idPPickUOM
			;set $$$MEDDispensePickLinePickQty(objLine)			= ""
			set $$$MEDDispensePickLineToStorage(objLine)		= idToStorage
			set $$$MEDDispenseLineUDODispense(objSrcLine)		= idSrcDisp
			set $$$MEDDispenseLineUDODispenseLine(objSrcLine)	= idSrcLine
			set idLine		= idLine + 1
			set strStatus	=  $$$Save("MEDDispenseLine",pYKEY_","_idLine,objSrcLine,$$$YES)
			quit:$$$ISERR(strStatus)
			set strStatus	=  $$$Save("MEDDispensePickLine",pYKEY_","_idLine,objLine,$$$YES)
			quit:$$$ISERR(strStatus)
		}
	}
	quit
ClearDispenseLines(pidDisp)
	;-------------------------------------------------------------------------------
	; Clear Dispense Line if there is no Pick Line
	;
	;
	; History:
	; 12-June-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new arrLine,idLine,strStatus
	set idLine	= ""
	for {
		set idLine	= $order(^MEDDispensePickLine(YM,pidDisp,idLine))
		quit:idLine=""
		set arrLine(idLine\1)=""
	}
	set idLine	= ""
	for {
		set idLine	= $order(^MEDDispenseLine(YM,pidDisp,idLine))
		quit:idLine=""
		if '$data(arrLine(idLine)) {
			set strStatus = $$$Kill("MEDDispenseLine",pidDisp_","_idLine)		
		}
	}
		
	quit
OnBeforeFormat(pYFIELDNAME,pobjLine)  
	;-------------------------------------------------------------------------------
	; set the format
	;	
	; Called By :
	;	Form - GridEdit:MEDDispenseManPickLine
	;	Routines -
	;		
	; Inputs : 
	;	pYFIELDNAME	-	Browser Element ID "tdY#row#_#col#"
	;	pobjLine	-	@nM INIssueLine Data record
	;
	; ByRef :
	;
	; Returns : 
	;
	; History :
	; 02-Jun-2014	SCR		HEVA1505: Created
	;-------------------------------------------------------------------------------
	new blnOK,idBundle,idClassField,idFreq,idItem,idManDisp,idManLine,idRx,idRxItem
	new intCol,intRow,objBundle,objDisp,objDispLine,objManDisp,objManDispLine,objRxLine

	quit:YKEY=""

	; Grid Position
	;---------------------------------------
	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)
	
	set blnOK		= $$GetVars(YKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)

	quit:'blnOK

	if (intCol = 1) {	
		set YTEXT	= "("_idRxItem_") "_$$^INARTNAME(idRxItem)
	}
	if (intCol = 2) {
		set YTEXT	=  $$$MEDPrescriptionLineDoseQty(objRxLine)_" "_$$$AppEnum("EINHEIT", $$$MEDPrescriptionLineDoseUOM(objRxLine))
	}
	if (intCol = 3) {
		set idFreq	= $$$MEDPrescriptionLineFrequency(objRxLine)
		quit:idFreq=""
		set YTEXT	= $$$MEDFrequencyFrequency($get(^MEDFrequency(YM,idFreq,1)))
	}
	if (intCol = 4) {	
		set idItem	= $$$MEDDispensePickLineItem(pobjLine)
		if idItem'="" {
			set YTEXT	= "("_idItem_") "_$$^INARTNAME(idItem)
		}
	}

	if (intCol = 7) {	
		set idBundle	= $$$MEDDispensePickLineBundle(pobjLine)
		if idBundle'="" {
			set objBundle	= ##class(alSOH.dBundle).%OpenId(idBundle)
			set YTEXT		= ##class(alSOH.iUSOH).RenderBundle(objBundle,SPRACHE)
		}
	}
	quit

GetVars(YKEY,&pidManDisp,&pidManLine,&pobjManDisp,&pobjManDispLine,&pobjDisp,&pobjDispLine,&pidRx,&pobjRxLine,&pidRxItem)	
	;-------------------------------------------------------------------------------
	; Get Vars
	;	
	;
	; History :
	; 02-Jun-2014	SCR		HEVA1505: Created
	;-------------------------------------------------------------------------------
	new blnOK,pidDisp,pidDispLine,pidRxLine
	set blnOK		= $$$YES
	set pidManDisp	= $$$KEY1(YKEY)
	set pidManLine	= $$$KEY2(YKEY)
	set:pidManLine="" blnOK	= $$$NO
	if $$$ISOK(blnOK) {
		set pobjManDisp		= $get(^MEDDispense(YM,pidManDisp,1))
		set pobjManDispLine	= $get(^MEDDispenseLine(YM,pidManDisp,pidManLine,1))
		set pidDisp			= $$$MEDDispenseLineUDODispense(pobjManDispLine)
		set pidDispLine		= $$$MEDDispenseLineUDODispenseLine(pobjManDispLine)
		set:pidDispLine="" blnOK	= $$$NO
	}
	if $$$ISOK(blnOK) {
		set pobjDisp			= $get(^MEDDispense(YM,pidDisp,1))
		set pobjDispLine		= $get(^MEDDispenseLine(YM,pidDisp,pidDispLine,1))
		set pidRxLine		= pidDispLine
		set pidRx			= $$$MEDDispensePrescription(pobjDisp)
		set:pidRx="" blnOK	= $$$NO
	}
	if $$$ISOK(blnOK) {
		set pobjRxLine		= $get(^MEDPrescriptionLine(YM,pidRx,pidRxLine,1))
		set pidRxItem		= $$$MEDPrescriptionLineItem(pobjRxLine)
	}
	quit blnOK
	
OnBeforeFormatClass(pYFIELDNAME,pobjLine)
	;-------------------------------------------------------------------------------
	; Called from class hook via MEDDispensePickLine.mac
	;	
	;		
	; Inputs : 
	;	pYFIELDNAME	-	Browser Element ID "tdY#row#_#col#"
	;	pYFELD		-	@nM INIssueLine Data record
	;
	; ByRef :
	;
	; Returns : 
	;
	; History :
	;
	; 09-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------

	new idClassField,intCol,intRow,objHeader
	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)

	set objHeader  = $$$GRIDYFELDContainer
	
	quit
	
	
	
OnBeforeDisplayLine(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Code to run on before display line for a line in the grid
	;
	; Params: pYKEY,pYFELD	: The YKEY/YFELD values
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Jun-2014	SCR		HEVA-1505: Created 
	;-------------------------------------------------------------------------------
	new blnOK,idManDisp,idManLine,idRx,idRxItem,objDisp,objDispLine,objManDisp
	new objManDispLine,objRxLine
	set blnOK		= $$GetVars(pYKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)
	quit:'blnOK
	; Sets PITEM

	set PITEM	=  idRxItem
	quit 1
	
UpdateDispenseLine(pidDisp,pidLine,pobjLine,pblnKeepUnit=$$$NO,&strStatus,&intDoses,&fltDoseQty)
	;-------------------------------------------------------------------------------
	; Update the Dispense Line based on the Line Item & Unit
	; 	the qty is based on the Rx Total Dose - qty already dispensed
	; 
	; Inputs: 	pidDisp		= Dispense id
	;			pidLine		= Dispense Line id
	;			pobjLine	= Dispense Line object
	;
	; Returns: 	Updated Dispebse Line object
	; 
	; History:
	; 28-Jan-2013	shobby	HEVA-793: Check fltQty>0 (Code from Rubens)
	; 03-Jan-2013	SCR		HEVA-725: Pass back Number of Doses and Dose Qty
	; 19-Dec-2012	SCR		HEVA-725: Created
	;-------------------------------------------------------------------------------
	new fltDispTotal,fltDoseTotal,fltQty,idIssueType,idItem,idRx,idUnit,objDisp
	new objRxLine
	
	set strStatus	= $$$OK
	set objDisp 	= $get(^MEDDispense(0,pidDisp,1))
	set idUnit		= $$$MEDDispenseLineIssueUOM(pobjLine)
	set idItem		= $$$MEDDispenseLineItemName(pobjLine)
	set idIssueType	= $$$MEDDispenseIssueType(objDisp)
	set idRx		= $$$MEDDispensePrescription(objDisp)
	set intDoses	= 0
	set fltDoseQty	= 0
	
	if pblnKeepUnit=$$$NO {
		set idUnit		= $$GetPickUOM^INUOMConversion(idItem,idIssueType,,$$$YES)

		if (strStatus=$$$OK) {
			set $$$MEDDispenseLineIssueUOM(pobjLine) = idUnit
		}
	}
	
	if (idRx="") { 
		quit pobjLine  ; Quick exit

	}
	if (strStatus=$$$OK) {
		set objRxLine		= $get(^MEDPrescriptionLine(YM,idRx,pidLine,1))
		set fltDoseTotal 	= $$DoseTotal^MEDPrescriptionLine(objRxLine,$$$YES)
		set fltDispTotal	= $$$MEDPrescriptionLineDispensedQty(objRxLine)
		set fltQty          = fltDoseTotal - fltDispTotal	;HEVA-793
	}
	if (strStatus=$$$OK) && (fltQty>0) {					;HEVA-793
		set fltQty 		= fltDoseTotal - fltDispTotal
		set intDoses	= $$Doses^MEDDispenseCalc(fltQty,objRxLine)
		
		set fltQty		= $$DispenseDoseQty^MEDDispenseCalc(idItem,idUnit,objRxLine,.strStatus)
		set fltDoseQty	= fltQty
		; Multply by the number of Doses
		set fltQty			= fltQty * intDoses
		set $$$MEDDispenseLineIssueQuantity(pobjLine) = fltQty

	}
	quit pobjLine

QtyOK(pYKEY,pfltPickQty)
	;-------------------------------------------------------------------------------
	; Check the Qty
	;
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 06-Jun-2014	SCR		HEVA-1505: Created 
	;-------------------------------------------------------------------------------
	new blnOK,fltDoseQty,fltRxQty,idManDisp,idManLine,idRx,idRxItem,intDoses,objDisp
	new objDispLine,objManDisp,objManDispLine,objRxLine,objTestLine
	
	do GetVars(pYKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)
	set objTestLine	= $$UpdateDispenseLine^MEDDispenseCalc(idManDisp,idManLine,objManDispLine,$$$YES,,.intDoses,.fltDoseQty)
	set fltRxQty	= $$$MEDDispenseLineIssueQuantity(objTestLine)
	if $$MultidoseInjectable^INART(idRxItem) {
		set fltRxQty	= fltRxQty+.999999\1 
	}
	if (pfltPickQty> fltRxQty ) {
		set blnOK	= $$$NO
	} else {
		set blnOK	= $$$YES
	}
	quit blnOK
	
OnBlurItem(pYKEY, pidGridRow, pidItem, &pobjLine)
	;-------------------------------------------------------------------------------
	; On Blur from Item
	;	Update Dyna Table
	;
	;
	; History :
	; 
	; 06-Jun-2014	SCR		HEAV-1505: Created
	;-------------------------------------------------------------------------------
	write "DrawDynTable_GridFieldFocus("_pidGridRow_");"
	quit
OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
	; OnBeforeSave of the MEDDispenseLine
	;
	; History :
	; 
	; 06-Jun-2014	SCR		HEAV-1505: Created
	;-------------------------------------------------------------------------------
	set $$$MEDDispensePickLineIssueQty(pYFELD)	= $$$MEDDispensePickLinePickQty(pYFELD)
	quit
]]></Routine>
</Export>