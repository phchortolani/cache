<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRP1099Report" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
 
FINRP1099Report
	; Routines for 1099 Tax Reporting
	
	#include FINConst
	#include COMSYS
 
GetAddress(pidSupplier,pidCompany)
    ;-------------------------------------------------------------------------------
    ; Return a suppliers address in a string for the 1099 report
    ;
    ; Params:
    ;
    ; Returns: string
    ;
    ; History:
    ; 17-Nov-2005	RPW			SR13749: Join POBOx and Street together unless POBox is blank.
    ; 28-Jun-2005	Steve S		SR10421 Created
    ;-------------------------------------------------------------------------------
	new strAddress,objSupplier,strStreet,strPOBox
	
	set strAddress=""
	
	if ($get(pidSupplier)'="")&&($get(pidCompany)'="") {
		set objSupplier=$get(^FINAPSupplier(pidCompany,pidSupplier,1))
		
		set strStreet =$$$FINAPSupplierStreet(objSupplier)
		set strPOBox  =$$$FINAPSupplierPOBox(objSupplier)
 
		/* Block removed for SR13749
		/if strStreet'="" {
			set strAddress=strStreet
		} else {
			set strAddress=strPOBox
		}*/
		
		
		set strAddress=strStreet_$select(strPOBox'="":", "_strPOBox,1:"")
	}
 
	quit strAddress
	
	
GetCityStateCode(pidSupplier,pidCompany)  // NOT IN USE
    ;-------------------------------------------------------------------------------
    ; Get the "city, state, country" string for a supplier
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 28-Jun-2005	Steve S		SR10421: Created
    ;-------------------------------------------------------------------------------
	new strCityStateCode,objSupplier,strState,strCountry,strCity
	
	set strCityStateCode=""
	
	if ($get(pidSupplier)'="")&&($get(pidCompany)'="") {
		set objSupplier=$get(^FINAPSupplier(pidCompany,pidSupplier,1))
			
		set strState   =$$$AppEnum("BUNDESLAND",$$$FINAPSupplierState(objSupplier))
		set strCountry =$$$SysEnum("LAND",$$$FINAPSupplierCountry(objSupplier))
		set strCity	   =$$$FINAPSupplierCity(objSupplier)
		
		set strCityStateCode=strCity
		if strCity'="" set strCityStateCode=strCityStateCode_", "
		
		set strCityStateCode=strCityStateCode_strState
		if strState'="" set strCityStateCode=strCityStateCode_", "
		
		set strCityStateCode=strCityStateCode_strCountry
		
	}
 
	quit strCityStateCode
	
	
GetLocationString(pidCompany,pidSupplier,pidLanguage)
    ;-------------------------------------------------------------------------------
    ; Get the location of a supplier (state, city, etc)
    ;
    ; Returns: String
    ;
    ; History:
    ; 17-Nov-2005	RPW			SR13749: Do not translate the State via BUNDESLAND.
    ; 22-Sep-2005	Steve S		SR10421: Created
    ;-------------------------------------------------------------------------------
	new strText,objFINAPSupplier,strCity,strState
 
	set strText=""
	
	if ($get(pidSupplier)'="")&&($get(pidCompany)'="")&&($get(pidLanguage)'="") {
		set objFINAPSupplier=$get(^FINAPSupplier(pidCompany,pidSupplier,1))
				
		set strCity=$$$FINAPSupplierCity(objFINAPSupplier)
		;set strState=$$Enum^REPUtilities("BUNDESLAND",pidLanguage,$$$FINAPSupplierState(objFINAPSupplier)) ; SR13749
		set strState=$$$FINAPSupplierState(objFINAPSupplier) ; SR13749
		
		set strText=strCity
		if strCity'="" set strText=strText_", "
		
		set strText=strText_strState
	}
 
	quit strText
]]></Routine>
</Export>