<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWEVENT3" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWEVENT3
	
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		NEW = DEL ALL FIELDS
	;
	; Inputs : 
	;
	;
	; ByRef :
	;	%REQUEST="NAMESPACE.USER.FORM.FELDNUMMER.FUNKTION.FIXKEY.YVARIABLE"				;SR18075
	;	%REQUEST="NAMESPACE.USER.FORM.FELDNUMMER.FUNKTION.FIXKEY.SPRACHE.YVARIABLE"		;SR18075
	;	%REQUEST(1)="VALUE"
	;
	; Returns :
	;
	;
	; History :
	; 02-Aug-2012	shobby	SR18075: piece 7 of %REQUEST is now SPRACHE
	; 04-May-2010	GRF		SR15961: use dteToday for efficiency; dot to brace
	; 07-Jul-2005	shobby	SR12892: WWW1261 is no longer shared.
	; 24.08.2002	DT
	;-------------------------------------------------------------------------------
	new dteToday,YFKEY,YKEY,YUSER,YFORM,YDATEI,YFELD,YSATZ,YINHALT,YBED,YVAR,YART,YDATA,YPR,YPR1
	
	set %TXT(1) = " "
	set YRICHT  = $get(%REQUEST(1))
	set YBED    = ""
	set YFKEY   = $extract($piece($get(%REQUEST),".",6),4,99)  ;FIX_VERBUNDEN MIT KEY ;by means of KEY 
	set YINHALT = $get(%REQUEST(1))  ;EINGEGEBENER TEXT ZUR PRUEFUNG
	set YSATZ   = ""
	set YUSER   = $piece($get(%REQUEST),".",2)
	if YUSER'="" {
		set YBED = $piece($get(^WWWUSER(0,YUSER,1)),Y,2)
		set YM   = $piece($get(^WWWUSER(0,YUSER,1)),Y,20)
	}
	if $data(YBED) && (YBED'="") {    ;SPRACHE DES USERS ;Language 
		if YM="" set YM = $piece($get(^WWW013(0,YBED,1)),Y,5)
		if YM="" set YM = 0
		set YLOCATION = $piece($get(^WWW013(0,YBED,1)),Y,44)
		set SPRACHE   = $$^WWWLANGU(YBED)
		set YVOR1     = $get(^WWW012(0,YM,1))
		set YWHR      = $piece(YVOR1,Y,75)   ;STANDARD WÄHRUNGSKZ
		set YMANDANT  = $piece(YVOR1,Y,94)   ;VORGABE FÜR MANDANTENKENNUNG ;default to 
		set YDECIMAL  = ","
		if $piece(YVOR1,Y,73)'="" set YDECIMAL = $piece(YVOR1,Y,73)
		if SPRACHE="EN" set YDECIMAL = "."
	}
	
	set YFORM = $piece($get(%REQUEST),".",3)
	quit:YFORM=""  ;FORMULAR NICHT VORHANDEN ;form Not on hand 
	
	set dteToday = +$horolog     ; SR15961
	
	set YKEY = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",1))
	if YKEY'="" if '$find(YKEY,"+") do ^WWWEVENT2
	set YKEY = $translate(YKEY,"+") 
	if %TXT(1)'=" " quit   ;FALSCHER KEY /FEHLER
	
	;SR18075 set YVAR = $piece($get(%REQUEST),".",7)
	set YVAR = $piece($get(%REQUEST),".",8)			;SR18075 
	set YART = $translate($piece($piece($get(%REQUEST),".",4),YFORM,2),"0123456789")
	if YART="" set YART = "D"
	set YLFN   = $piece($piece($get(%REQUEST),".",4),YFORM_YART,2)
	set YVOR   = $get(^WWW120(0,YFORM,1))
	set YDATEI = $$$WWW120ClassUsedInForm(YVOR)   ; D11
	
	kill ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D")
	set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1) = $get(^WWW1261(YM,"INDASTAMM",1))
	kill ^WWWDATEN(YM,dteToday,YUSER,YFORM,"P")
	kill ^WWWDATEN(YM,dteToday,YUSER,YFORM,"M")
	
	if YRICHT'="" {
		set $piece(^WWWUSER(0,YUSER,1),Y,15) = ""
		do ^WWWFORMS                         ;NEXT DATENSATZ ;data record 
		do ^WWWLESE(YDATEI,YKEY)
		set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1) = YFELD
		set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",1) = YKEY
		kill ^WWWDATEN(YM,dteToday,YUSER,YFORM,"M")
	}
	set %TXT(1) = $$^WWWREFRESH(YUSER,YFORM)
	set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2) = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1))
	set ^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",2) = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",1))
	quit
	
]]></Routine>
</Export>