<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWERMENGE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWERMENGE(ART,BET,LAP,WED,RES,KORR)
#include COMSYS
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		RÜCKGABE DER RESERVIERTEN MENGE
	;		RETURN OF THE RESERVED QUANTITY
	; 
	; Inputs : 
	;	ART   			idItem				ARTIKEL
	;	BET   			idSiteLocn			BETRIEB
	;	LAP   			idStockLocn			LAGERPLATZ
	;	WED   			dteReceived			WE-DATUM
	;	RES   			ReservationNo		RESERVIERUNGSNUMMER
	;	KORR  			blnCancelExpired	ABGELAUFENE RESERVIERUNGEN AUFLÖSEN JA/NEIN
	;
	;
	; ByRef :
	;
	;
	; Returns :
	; 	Quantity
	;
	; History :
	; 23-Nov-2006	GRF		SR15232: Doco; Boolean Macro
	; 27-Jan-2006	GRF		SR14203: Doco
	; 31.Dec.2003	FIS		Created
	;-----------------------------------------------------------------------------*/
	NEW MENGE,SATZ
	
	SET ART   =  $GET(ART) 
	SET BET   =  $GET(BET)
	SET LAP   =  $GET(LAP)
	SET WED   =  $GET(WED)
	SET RES   =  $GET(RES)
	SET KORR  = +$GET(KORR) 
	SET MENGE = 0
	
	DO:ART'=""  IF ART="" FOR  SET ART=$ORDER(^INWER(YM,ART)) QUIT:ART=""  DO
	. DO:BET'=""  IF BET=""  FOR  SET BET=$ORDER(^INWER(YM,ART,BET)) QUIT:BET=""  DO
	. . DO:LAP'=""  IF LAP="" FOR  SET LAP=$ORDER(^INWER(YM,ART,BET,LAP)) QUIT:LAP=""  DO
	. . . DO:WED'=""  IF WED="" FOR  SET WED=$ORDER(^INWER(YM,ART,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . DO:RES'=""  IF RES=""  FOR  SET RES=$ORDER(^INWER(YM,ART,BET,LAP,WED,RES)) QUIT:RES=""  DO
	. . . . . SET SATZ=$GET(^INWER(YM,ART,BET,LAP,WED,RES,1))
	. . . . . ; Already cancelled
	. . . . . IF $PIECE(SATZ,Y,8)'="" QUIT                           ;RESERVIERUNG AUFGELÖST
	. . . . . ; If in Correction Mode, Cancel reservation once Hold Until Date passed       ;NICHT MEHR MITZÄHLEN
	. . . . . IF KORR=$$$YES IF $PIECE(SATZ,Y,5)<$HOROLOG DO  QUIT
	. . . . . . DO AUS^INWER(ART,BET,LAP,WED,RES,$$^WWWTEXT(33557))  ; "System"  ;RESERVIERUNG AUFLÖSEN VON SYSTEM
	. . . . . ;
	. . . . . SET MENGE=MENGE+$PIECE(SATZ,Y,4)                       ;MENGE ; Sum quantity reserved
	
	QUIT MENGE
	
]]></Routine>
</Export>