<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMUtilForm" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMUtilForm
    ;-------------------------------------------------------------------------------
    ; Common Utilities for @netManager Forms
    ; 
    ; See also : 
    ; 	COMUtilDate		Date/Time Utilities
    ; 	COMUtilStr		String Manipulation
    ; 	COMUtilGlo		Global Manipulation
    ; 	COMUtilClass	@netManager Class Utilities
	; 
	; History:
	; 23-Feb-2005	GRF		Created; moved some routines from COMUtils
    ;-------------------------------------------------------------------------------
#include COMConst
#include WWWConst
#include COMSYS
	
GetFieldDetails(pidField,&pidForm,&pidFieldType,&pidFieldNo)
    ;-------------------------------------------------------------------------------
    ; Provided a field name eg. YFINAPInvD22 break up into
    ; Form name, Field type, Field number
    ;
    ; Returns: Nothing, all done through arguments
    ;
    ; History:
    ; 14-Apr-2005	PO		Created SR:
    ;-------------------------------------------------------------------------------
	new intLoop
	
	set pidForm      = ""
	set pidFieldType = ""
	set pidFieldNo   = ""
	
	for intLoop=$length(pidField):-1:1 { ; Should always leave by quit, for loop limit used as a safeguard
		set pidFieldNo = $extract(pidField,intLoop)_pidFieldNo
		quit:+$extract(pidField,intLoop)'=$extract(pidField,intLoop)
	}
	
	set pidFieldType = $extract(pidFieldNo)
	set pidFieldNo   = $extract(pidFieldNo,2,$length(pidFieldNo))
	set pidForm      = $extract(pidField,2,intLoop-1) ; strip off the "Y"	
	quit
	
	
GoToForm(pstrForm,pidKey,plngPage,pstrParam,pblnOpenNewWindow=$$$NO,pstrWindowName="",
		 pstrYAUSWAHL,pblnModalWin=$$$NO,&parrParams="") ;HEVA-1148
	;-------------------------------------------------------------------------------
	; Utility to present the nominated form
    ; NOTE: This is preferable over RedirectForm
	;
	; Inputs :
	;   pstrForm	Form Name
	;   pidKey		Keys
	;   plngPage	Tab Number
	;   pstrParam	YPARA
	;   pblnOpenNewWindow  Currently only applicable for HyperEvents
	;   pstrWindowName
	;   pstrYAUSWAHL	YAUSWAHL
	;   pblnModalWin Open as Modal Window
	; 
	; History.
	; 14-Oct-2013	shobby	HEVA-1148: Allow passing of additonal parameters.
	; 26-Aug-2011	GRF		SR17866.2: Preserve PDA variable YSTARTAT
	; 27-Sep-2010	shobby	SR17541: Can use showmodaldialog with firefox.
	; 12-Jul-2010	FIS		SR17426: use this.location rather document.parent in FF
	; 01-Jun-2010	FIS		SR17343: use & rather &amp; in URL for firefox
	; 16-Dec-2008	FIS		SR16235: Open form in modal window
	; 21-Dec-2006	JW		SR14235: Added reference to child window
	; 11-Oct-2006	PO		SR15090: Include script tags as OnAfterSave^INTFR
	; 							pretending to be in hyperevent to make use of
	; 							setting location of main frame.
	; 15-Aug-2006	PO		SR14582: Prevent js error from being displayed (have
	; 							not been able to figure out real solution)
	; 28-Jun-2006	GRF		& => &&
	; 09-Feb-2006	JW		SR14274: Added YAUSWAHL param
	; 10-Nov-2005	JW		SR13817: Change YUSER for pop-up window
	; 03-Nov-2005	PO		SR13578: Include js form loading.
	; 21-Jul-2005	PO		SR13009 Handling of YBACK now done correctly by
	; 						WWW code - changes reverted until complete
	; 06-Dec-2004 	JW		SR10541: Removed change and created new function
	;  							LoadFormNow^COMGridEdit31G
	; 01-Dec-2004	shobby/JW	SR11066: Allow overriding of the back button.
	;-------------------------------------------------------------------------------
	new strBack,strURL
	
	set strBack = $get(YBACK)     ; "A,B,C,D,E,"
	
	; FIXME : <GRF> SR15976 - it appears that this should not be performed since
	;               it makes the back arrow call the CALLED form before returning
	;               to the ORIGINAL form on the second click.
	;               Still need to be able to return with the key for the earlier
	;               form rather than using an inappropriate YKEY.
	
	if (YFORM'="") && ($piece(strBack,",",$length(strBack,",")-1)'=YFORM) && (strBack'=YFORM) {
		set strBack = strBack_YFORM_","  ;only add YFORM to the stack if not last element
	}
	
	if $$$ALWAYS { ;$get(%("%KEY","HYPEREVENT")) {
		set strURL = YAKTION_"EP=WWWFORM&amp;YFORM="_pstrForm
		if pblnOpenNewWindow {
			set strURL = strURL_"&amp;YBACK=,"
		} elseif strBack '= "" {
			set strURL = strURL_"&amp;YBACK="_strBack
		}
		set strURL = strURL_"&amp;YKEY="_pidKey
		if $get(pstrParam)'="" set strURL = strURL_"&amp;YPARA="_pstrParam
		if $get(plngPage)'=""  set strURL = strURL_"&amp;YSEITE="_plngPage
		if $get(YFKEY)'=""     set strURL = strURL_"&amp;YFKEY="_YFKEY
		set strURL = strURL_"&amp;YBED="_YBED
		
		set strURL = strURL_"&amp;YM="_YM
		set strURL = strURL_"&amp;YUCI="_YUCI
		
		if $get(pstrYAUSWAHL)'="" set strURL = strURL_"&amp;YAUSWAHL="_pstrYAUSWAHL
		if $get(YSTARTAT)'="" set strURL = strURL_"&amp;YSTARTAT="_YSTARTAT    ; SR17866.2
		
		new idParam																		;HEVA-1148
		set idParam="" for { set idParam=$order(parrParams(idParam)) quit:idParam=""	;HEVA-1148
			set strURL=strURL_"&amp;"_idParam_"="_parrParams(idParam)					;HEVA-1148
		}																				;HEVA-1148
		$$$StartScript()
		
		if pblnOpenNewWindow {
			set strURL = strURL_"&amp;YUSER="_$$GetChildUser^WWWUSER(YUSER)
	;SR17541 if (pblnModalWin = $$$YES) && (YUSERAGENT = "MSIE") {
			if (pblnModalWin = $$$YES) {                            ;SR17541 
				write YCR," var left = (screen.width/2)-(650/2);"
				write YCR," var top = (screen.height/2)-(450/2);"
				write YCR," window.open('"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"&Timeout=' + new Date().getSeconds(),'"_pstrWindowName_"','toolbar=0,scrollbars=0,resizable=1,width=650,height=450,left='+left+',top='+top);"
				;write "	window.showModalDialog('"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"&Timeout=' + new Date().getSeconds(),'"_pstrWindowName_"','DialogWidth: 650px; DialogHeight: 450px; resizable: yes; status: no;');"
			
			} elseif pstrWindowName="" {
				write "subWindow('"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"');"            //SR17343
			
			} else {
				write "try { subWindow('"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"','"_pstrWindowName_"'); } catch(e) {}"  //SR17343
			}
			
		} else {
			set strURL = strURL_"&amp;YUSER="_YUSER
		//	write "document.frames.parent[document.frames.parent.length-1].location = '"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"';"
			;SR17875 the following gives an error in Windows Mobile 6.1
			write "parent.frames[parent.frames.length-1].location = '"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"';"
		//	write " if (document.frames.parent && document.frames.parent.length > 0) document.frames.parent[document.frames.parent.length-1].location = '"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"';"  //SR17426
		//	write " else this.location.href = '"_$$Replace^COMUtilStr(strURL,"&amp;","&")_"';"  //SR17426
		}
		
		$$$EndScript()
		
	} else {   ; $$$NEVER
		set %("VAR","YBACK")  = strBack
		if $get(pstrParam)'=""    set %("VAR","YPARA")    = pstrParam
		if $get(pstrYAUSWAHL)'="" set %("VAR","YAUSWAHL") = pstrYAUSWAHL
		if $get(plngPage)'=""     set %("VAR","YSEITE")   = plngPage
		if $get(YFKEY)'=""        set %("VAR","YFKEY")    = YFKEY
	    set %("VAR","YKEY")  = pidKey
		set %("VAR","YFORM") = pstrForm
		
	   	do ^WWWFORM
	}
	quit
	
	
GoToGridOnForm(pstrForm,pidKey,plngPage="",pstrParam="",pblnOpenNewWindow=$$$NO,pstrWindowName="",pstrYAUSWAHL="",pintGridLine)
	;-------------------------------------------------------------------------------
	; Extension of GoToForm^COMUtilForm
	; Redirects to a specified form(pstrForm) and gives focus to a line on that form's grid.
	;
	; Inputs :
	;   1 pstrForm			Form Name
	;   2 pidKey			Keys
	;   3 plngPage			Tab Number
	;   4 pstrParam			YPARA
	;   5 pblnOpenNewWindow Currently only applicable for HyperEvents
	;   6 pstrWindowName
	;   7 pstrYAUSWAHL		YAUSWAHL
	;   8 pintGridLine 		Line Number on the Grid. Go to this line.
	; 
	; History
	; 21-Apr-2006 	SC		SR14427: Created.
	;-------------------------------------------------------------------------------
	set ^CacheTemp(YUSER,"GridLine") = pintGridLine
	do GoToForm(pstrForm,pidKey,plngPage,pstrParam,pblnOpenNewWindow,pstrWindowName,pstrYAUSWAHL)
	quit	
	
	
RefreshForm()
    ;-------------------------------------------------------------------------------
    ; Refreshes the current form and resets YBACK so as not to have to press the back
    ; button multiple times to get to the previous form.
    ;
    ; Returns:
    ;
    ; History:
    ; 21-Jul-2005	PO			SR13009 Handling of YBACK now done by WWW code
    ; 								- changes reverted until complete
    ; 17-Aug-2004	Shobby		Created
    ;-------------------------------------------------------------------------------
	if $length(YBACK,",")>0 {
		set YBACK = $piece(YBACK,",",1,$length(YBACK,",")-1)     ; A,B,C,D,E, => A,B,C,D,E  ; FIXME : YBACK normally finishes with comma ? <GRF>
	}
	do RedirectForm(YFORM,YKEY,YBACK,YPARA,YSEITE)
	quit
	
	
ReloadForm(pstrParam)
    ;-------------------------------------------------------------------------------
    ; Reload the current form. Same functionality as RefreshForm, but takes less time.
    ; Note: This is preferrable over RefreshForm()
    ; 
    ; Params: pstrParam	- (optional) value for YPARA
    ; 
    ; Returns: nil
    ; 
    ; History:
    ; 15-Jul-2014	shobby	HEVA-1565: MenuType should also consider WWW012
    ; 16-May-2013	shobby	CORE-81: SR17998 change to be patched to 1.70.4
    ; 30-Jun-2006	JW		SR14528: Added pstrParam
    ; 21-Jul-2005	PO		SR13009 Handling of YBACK now done correctly by WWW code
    ; 							- changes reverted until complete
    ;  3-Feb-2005	JW		Created
    ;-------------------------------------------------------------------------------
    ;HEVA-1565 if $$$WWW013MenuType($get(^WWW013(0,YBED,1)))=13 {
	if $$MenuType^WWW013()=13||($$MenuType^WWW013()=14){    ;HEVA-1565 
    	do GoToForm^COMUtilForm(YFORM,$g(YKEY)) ;SR17998
	} else {
		new lenBack
	
		; remove YFORM from YBACK
		set lenBack = $length(YBACK,",")
		set $piece(YBACK,",",lenBack-1,lenBack) = ""     ; A,B,C,D,E, => A,B,C,D,
	
		set %("VAR","YBACK") = YBACK
		if $data(pstrParam) set %("VAR","YPARA") = pstrParam
	
		do ^WWWFORM
	}
	quit
	
	
RedirectForm(YFORM,YKEY,YBACK,YPARA,YSEITE,pblnSeach=$$$NO,pblnPopup=$$$NO) ;CORE-107.1
    ;-------------------------------------------------------------------------------
	; Utility to redirect the current form to another
	; Note: this uses HTML REDIRECT thereby changing the
	; current URL
	; 
	; NOTE: Use GoToForm instead of this function when possible ******
	; 
	; 
	; 22-May-2013	shobby	CORE-107.1: Additional parameter so that Menu is not shown on a search function within a popup.
	; 29-Jul-2012	SCR		SR17993: Start with Search option
	; 08-Feb-2006	RPW		SR13655: Hyper events
    ;-------------------------------------------------------------------------------
	new strEnd,strEP
	
	set strEP="WWWFORM"
	set:pblnSeach strEP="WWWSEAR"
	
	if $get(%("%KEY","HYPEREVENT")) {
		write "window.location='"_YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YKEY="_YKEY_"&amp;YBACK="_YBACK ; SR17993
		;write "window.location='"_YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YKEY="_YKEY_"&amp;YBACK="_YBACK
		set strEnd = "'"
		
	} else {
		write "<META HTTP-EQUIV=""refresh"" CONTENT=""0; URL="
		write YAKTION_"EP="_strEP_"&amp;YFORM="_YFORM_"&amp;YKEY="_YKEY_"&amp;YBACK="_YBACK ; SR17993
		;write YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YKEY="_YKEY_"&amp;YBACK="_YBACK
		set strEnd = """>"
	}
	if YPARA'=""  write "&amp;YPARA="_YPARA
	if YSEITE'="" write "&amp;YSEITE="_YSEITE
	if pblnPopup write "&amp;YPOPUP=1" ;CORE-107.1
	
	set YFORM  = ""
	set YKEY   = ""
	set YBACK  = ""
	set YPARA  = ""
	set YSEITE = ""
	do ^WWWCGI
	write strEnd
	
	quit
	
	
CopyForm(pidForm,pstrToNameSp,pstrFromNameSp)
	;-------------------------------------------------------------------------------
	; Copy a form and the associated class from one namespace (source) to another
	; (destination)
	; 
	; History:
	; 20-Sep-2011	GRF		SR17847: standard prefixes
	; 08-Sep-2011	shobby	SR17847: Add WWW120Hook
	; 21-Jul-2009	GRF		Add WWW122C2 - CoreRules
	; 17-May-2007	Karine	BR014458 Added WWW001Hook to the List
	; 22-Jun-2005	SCR		SR12755 Added WWW003Calc to the List
	; 15-Mar-2005	Paul K	Use Macros instead of $piece(x,Y,n)
	; 23-Feb-2005	GRF		Simplify strings now subroutines combined
	; 05-Nov-2004	GRF		Split "Form" line for easier reading
	;-------------------------------------------------------------------------------
	new loop,strClassList,strGlobal,strNamespace
	
	if (pstrFromNameSp'="") && (pstrToNameSp'="") && (pidForm'="") {
		if $get(Y)="" do ^WWWVAR
		set strClassList = "WWW120,WWW1201,WWW1203,WWW121,WWW1210,WWW1211,WWW122,WWW1221,WWW122C2,WWW120Hook"
		set strClassList = strClassList_",WWW123,WWW1231,WWW1232,WWW124,WWW1251,WWW126"
		set strClassList = strClassList_",WWW127,WWW129,WWW1291,WWW1290,WWW131,WWW132,WWW133,WWW003Calc"   ;Form
		set strClassList = strClassList_",WWW001,WWW0011,WWW0021,WWW002,WWW003,WWW0031,WWW001Hook"         ;Class
		
		set strNamespace = $zutil(5)
		do $zutil(5,pstrToNameSp)
		for loop=1:1:$length(strClassList,",") {
			set strGlobal = $piece(strClassList,",",loop)
			
			xecute "kill ^"_strGlobal_"(0,"""_pidForm_""")"
			xecute "merge ^"_strGlobal_"(0,"""_pidForm_""")=^["""_pstrFromNameSp_"""]"_strGlobal_"(0,"""_pidForm_""")"
			do ^WWWSSORT(strGlobal,pidForm)
			
			; Special processing
			if strGlobal="WWW120" set $$$WWW120FormInformation(^WWW120(0,pidForm,1)) = ""		
		}
		do $zutil(5,strNamespace)
	}
	quit
	
	
SetNewSearch(pstrModule="")
	;-------------------------------------------------------------------------------
	; Set new search option for a Module
	;
	; Returns:
	;
	; History:
	; 21-Jul-2009	GRF		variable prefixes
	; 22-Feb-2005	Paul K	Reduce incidence of calling WWWVAR
	; 05-Nov-2004	GRF		Missing QUIT
	; 27-Sep-2004	SCR		Created
	;-------------------------------------------------------------------------------
	new idField,idForm,objFieldRec
	
	if $get(Y)="" do ^WWWVAR
	set pstrModule = $zconvert(pstrModule,"u")
	
	set idForm = ""
	for  {
		set idForm = $order(^WWW122(0,idForm))
		quit:idForm=""
		
		; Check Form Name for matching Module prefix
		if (pstrModule'="") && ($extract($zconvert(idForm,"u"),1,$length(pstrModule))=pstrModule) {
			set idField = ""
			for {
				set idField = $order(^WWW122(0,idForm,idField))
				quit:idField=""
				
				set objFieldRec = $get(^WWW122(0,idForm,idField,1))
				; If old search is on and new is off
				if ($$$WWW122RelationWithSearchFuncti(objFieldRec)) && ('$$$WWW122SearchAsFullTextSearch(objFieldRec)) {
					write !,idForm," ",idField
					set $$$WWW122SearchAsFullTextSearch(objFieldRec) = $$$YES ; set new search on
					set ^WWW122(0,idForm,idField,1) = objFieldRec
				}
			}
		}
	}
	quit
	
	
GetFormData(pstrForm,pType="D",pblnOld=$$$NO)
	;-------------------------------------------------------------------------------
    ; Return a form's current data (YFELD or YMFELD or YKEY) from WWWDATEN
    ; 
    ; Params: 	pstrForm - overrides YFORM (eg for Grid)
    ; 			pType - type of data:	D - YFELD (default)
    ; 									M - YMFELD
    ; 									P - YKEY
    ; 			pblnOld: Value before screen controls are changed.
    ; 
    ; Returns: data
    ; 
    ; History:
    ; 04-Jan-2008	shobby	SRBR014815: Sometimes need to know what the value was at
    ; 							time of screen load.
    ; 14-Mar-2005	JW		Created
	;-------------------------------------------------------------------------------
	new strYFELD,strGlobal
	
	if $get(pstrForm)="" set pstrForm = $get(YFORM)
	
	set strYFELD = ""
	if ($get(YUSER)'="") && (pstrForm'="") && (pType'="") {
		if 'pblnOld {
	 		set strYFELD = $$$GetFormData(pstrForm,pType)
		} else {
	 		set strYFELD = $$$GetFormDataOld(pstrForm,pType)
		}
	}
	quit strYFELD
	
	
ToggleReadOnly(pFields,blnReadOnly=$$$YES,pstrForm,pstrActiveColour="",pstrFieldType="D")
	;-------------------------------------------------------------------------------
    ; Toggle read only status of form fields.
    ;
    ; Params:	pFields - fields to make readonly (';' delimited)
    ; 			blnReadOnly - yes (default) or no 
    ; 			pstrForm - overrides YFORM
    ; 			pstrFieldType - the field type D/M
    ;
    ; Returns: nil
    ;
    ; History:
    ; 19-Oct-2010	GRF		SR17295: i=>loop
    ; 12-May-2010	shobby	SR17295: ResetFieldFormat can't be relied on for colour
    ; 12-May-2010	shobby	SR17295: Correction for manual fields.
    ; 17-Jan-2007	SteveS	SR15355: Change tooltips on Calender Buttons
    ; 10-Oct-2006	SteveS	SR15012: Added pstrFieldType parameter
    ; 26-Jul-2006	JW		SR14581: Set tabindex. Use original colour.
    ; 22-Jul-2005	JW		SR12615: Updated call to UpdateFieldFormat
    ; 27-May-2005	JW		Removed pstrFieldType as don't want to toggle a manual
    ; 							field.  Checked usages and updated.
    ; 06-Apr-2005	SteveS	Pass in pstrFieldType
    ; 10-Mar-2005	JW		Created
	;-------------------------------------------------------------------------------
    new blnGrid,colour,idClass,idField,intFormField,loop,numFields,strField,strReadOnly
    
	if $get(pstrForm)="" {
		set pstrForm = $get(YFORM)
	}
	
	if ($get(pFields)'="") && ($get(pstrForm)'="") {
		set numFields = $length(pFields,";")
		
		if blnReadOnly {
			set colour      = YGRAY
			set strReadOnly = "true"
		} else {
			set colour      = pstrActiveColour ; TODO: may want to make it so multiple active colours can be passed in
			set strReadOnly = "false"
		}
		
		set idClass = $$$WWW120ClassUsedInForm($get(^WWW120(0,pstrForm,1)))
		for loop=1:1:numFields {
			set idField = $piece(pFields,";",loop)
			if $length(idField,"_")>1 {
				set strField = "tdY"_idField                          ; FIXME : idField is the form field from the grid here<GRF>
				set blnGrid  = $$$YES
			} else {
				set strField = "Y"_pstrForm_pstrFieldType_idField     ; FIXME : idField is the (P?/)D/M field here <GRF>  
				set blnGrid  = $$$NO
			}
			
			write "var button = document.getElementById('CalendarButton"_pstrFieldType_idField_"');"
			
			if blnReadOnly {
				do UpdateFieldFormat^COMUtils(strField,"tabIndex","")
				write "if (button != null) button.title = '"_$$$JSText($$^WWWTEXT(86))_"';" ; "Not available"
			
			} else {
				do ResetFieldFormat^COMUtils(strField,"tabIndex")
				write "if (button != null) button.title = '"_$$$JSText($$^WWWTEXT(124))_"';" ; "Date"
			}
			
			
			if blnGrid {
				set idField = $$$GetClassField(YFORM,$piece(strField,"_",2))     ; FIXME : idField is the class field here <GRF>
			}
			
			if $$$WWW003InputType($$GetRelation^COMViewUtils(idClass,"D"_idField,pstrForm))'=2 {
				
				if pstrFieldType="M" {
					set intFormField = idField		; SR17295
				} else {
					set intFormField = $order(^WWW122s(0,4,idField,pstrForm,""))     ; FIXME : idField is the class field - "M" should set intFormField to initial idField <GRF>
				}
				if $$$WWW122DataInputType($get(^WWW122(0,pstrForm,intFormField,1)))=4 {
					do UpdateFieldFormat^COMUtils(strField,"disabled",strReadOnly)
				}
				
				do UpdateFieldFormat^COMUtils(strField,"readOnly",strReadOnly)
				if 'blnReadOnly && (colour="") {
					do UpdateFieldFormat^COMUtils(strField_".style","backgroundColor",colour)		;SR17295 
		;SR17295	do ResetFieldFormat^COMUtils(strField_".style","backgroundColor")
				} else {
					do UpdateFieldFormat^COMUtils(strField_".style","backgroundColor",colour)
				}
				
			} else {
				do UpdateFieldFormat^COMUtils(strField,"disabled",strReadOnly)
			}
		}
	}
	quit
	
	
SwitchCurrencies
    ;-------------------------------------------------------------------------------
    ; Switch between currencies displayed on form
    ;
    ; History:
    ; 09-Jun-2005	PO		SR#: Set to use ReloadForm
    ; 22-Mar-2005	PO		SR11349 Created
    ;-------------------------------------------------------------------------------
	if $$$CurrencyToUse = "Foreign" {
		set ^CacheTemp(YUSER,YFORM,"Toggles","Currency") = "Base"
	} else {
		set ^CacheTemp(YUSER,YFORM,"Toggles","Currency") = "Foreign"
	}
	set ^CacheTemp(YUSER,YFORM,"Flags","SwitchCurrencies") = ""
	do ReloadForm()
	quit
	
	
ClearMRUList(pidForm)
	;-------------------------------------------------------------------------------
	; Code to clear an MRU list for a given form for the current user
	;
	; Returns:
	;
	; History:
	; 07-Jul-2005	shobby	SR12892: WWW1262 is no longer shared.
	; 09-May-2005	SteveS	Created
	;-------------------------------------------------------------------------------
	new idKey,idKeyPart,strMessage
	
	set strMessage = ""
	
	if ($data(^WWW120(0,pidForm))) {
		set idKey = ""
		for {
			set idKey = $order(^WWW1262(0,pidForm,YBED,idKey))
			quit:idKey=""
			
			set idKeyPart = ""
			for {
				set idKeyPart = $order(^WWW1262(0,pidForm,YBED,idKey,idKeyPart))
				quit:idKeyPart=""
				
				do KILL^COMUtils("WWW1262",pidForm_$$$COMMA_YBED_$$$COMMA_idKey_$$$COMMA_idKeyPart)
			}	
		}
		set strMessage = $$$Text("Com00076")                         ; "Delete performed"
		
	} else {
		set strMessage = $$$Text($listbuild("Com00188",pidForm))     ; "Form %1 not found."
	}
	
	do ReturnError^COMUtils(strMessage)
	quit
	
	
DisplayText(strText,strColour="",strAlign="right",strSize="2",blnIncludeBreaks=0)
	;-------------------------------------------------------------------------------
	; Display text on a form
	;
	; Returns: Nothing
	;
	; NOTE: The HTML output here can end up being rendered underneath controls that
	; are written out standardly using WWW code. I think the reason this problem is
    ; occurring is since the width of the base* table containing the WWW2 form is
    ; specified.
    ; (* Not actually certain it is a base table.)
	;
	; History:
	; 08-Jun-2005	PO		Included br tags to ensure grid does not render over
	; 							the top of text
	; 30-May-2005	PO		SR12050: Created
	;-------------------------------------------------------------------------------
	if strColour="" set strColour = $get(YRED)
	write "<table style='border:0;' align='"_strAlign_"'><tr><td><font color='"_strColour_"' size="_strSize_">"_strText_"</font></td></tr></table>"
	if blnIncludeBreaks write "<br /><br />"
	quit
	
	
ListExecs(pidForm="")
	;-------------------------------------------------------------------------------
	; Display executable commands associated with a form
	;
	; Inputs :
	;	pidForm		: Form name
	; 
	; Returns: Nothing
	;
	; History:
	; 19-Dec-2005	GRF		Created (for checking SR14022)
	;-------------------------------------------------------------------------------
	new blnHead,ClassNo,Command,FieldName,FieldNo,FormName,lengthName,objButton,objData
	new ObjectName,objForm,objKey,objWWW003,objWWW012,Seq
	
	quit:pidForm=""
	
	do:'$data(Y) ^WWWVAR
	
	; TODO : Add customisation entries
	; TODO : Check all fields being checked
	
	set lengthName = 40
	
	set objForm  = $get(^WWW120(0,pidForm,1))
	set FormName = $$$WWW120FormHeaderOrImageFile(objForm)
	
	write !,"Executable Commands for form : ",pidForm," (",FormName,")",!
	
	write !,"System",!!
	set objWWW012 = $get(^WWW012(0,0,1))
	for FieldNo=29,30 {
		set Command = $piece(objWWW012,Y,FieldNo)
		if Command'="" {
			set FieldName = $$$WWW003CaptionInForms($get(^WWW003(0,"WWW012",FieldNo,1)))
			set FieldName = FieldName_$justify("",lengthName)
			write FieldNo,?5,$extract(FieldName,1,lengthName)," : ",Command,!
		}
	}
	
	write !,"Form",!!
	for FieldNo=14:1:18,59,86,87,89,93,126 {
		set Command = $piece(objForm,Y,FieldNo)
		if Command'="" {
			set FieldName = $$$WWW003CaptionInForms($get(^WWW003(0,"WWW120",FieldNo,1)))
			set FieldName = FieldName_$justify("",lengthName)
			write FieldNo,?5,$extract(FieldName,1,lengthName)," : ",Command,!
		}
	}
	
	write !,"Keys",!!
	set Seq = ""
	for {
		set Seq = $order(^WWW121(0,pidForm,Seq))
		quit:Seq=""
		
		set blnHead = $$$YES
		set objKey  = $get(^WWW121(0,pidForm,Seq,1))
		for FieldNo=19,25,26,92,97 {
			set Command = $piece(objKey,Y,FieldNo)
			if Command'="" {
				if blnHead {
					set ObjectName = $$$WWW002TextInForms($get(^WWW002(0,pidForm,Seq,1)))
					write Seq,?5,ObjectName,! 
					set blnHead = $$$NO
				}
				set FieldName = $$$WWW003CaptionInForms($get(^WWW003(0,"WWW121",FieldNo,1)))
				set FieldName = FieldName_$justify("",lengthName)
				write ?5,FieldNo,?10,$extract(FieldName,1,lengthName)," : ",Command,!
			}
		}
	}
	
	;---------------------------------------
	;	D25		$$$WWW122ExecuteOnBlur
	;	D92		$$$WWW122ExecuteBeforeDataField		On Form Construction (before)
	;	D97		$$$WWW122ExecuteAfterDataField		On Form Construction (after)
	;	D99		$$$WWW122ExecuteWithinDataField		On Form Construction (within)
	;---------------------------------------
	write !,"Data",!!
	set Seq = ""
	for {
		set Seq = $order(^WWW122(0,pidForm,Seq))
		quit:Seq=""
		
		set blnHead = $$$YES
		set objData = $get(^WWW122(0,pidForm,Seq,1))
		set ClassNo = $$$WWW122SequenceNumber(objData)
		if ClassNo="" {
			set ObjectName = $$$WWW122ManualCaption(objData)
		} else {
			set objWWW003  = $get(^WWW003(0,pidForm,ClassNo,1))
			set ObjectName = $$$WWW003CaptionInForms(objWWW003)
		}
		
		for FieldNo=25,92,97,99 {
			set Command = $piece(objData,Y,FieldNo)
			if Command'="" {
				if blnHead {
					write Seq,?5,ObjectName,! 
					set blnHead = $$$NO
				}
				set FieldName = $$$WWW003CaptionInForms($get(^WWW003(0,"WWW122",FieldNo,1)))
				set FieldName = FieldName_$justify("",lengthName)
				write ?5,FieldNo,?10,$extract(FieldName,1,lengthName)," : ",Command,!
			}
		}
	}
	
	;---------------------------------------
	;	D6		$$$WWW124ExecuteOnClick()
	;	D25		$$$WWW124ExecuteForManualLink()
	;	D52		$$$WWW124ExecuteAfterBackwardStep()
	;	D62		$$$WWW124ExecuteToCheckForDisabled()
	;	D65		$$$WWW124HyperEventOnClick()
	;---------------------------------------
	
	write !,"Buttons",!!
	set:$get(SPRACHE)="" SPRACHE="EN"
	set Seq = ""
	for {
		set Seq = $order(^WWW124(0,pidForm,SPRACHE,Seq))
		quit:Seq=""
		
		set blnHead   = $$$YES
		set objButton = $get(^WWW124(0,pidForm,SPRACHE,Seq,1))
		for FieldNo=6,25,52,62,65 {
			set Command = $piece(objButton,Y,FieldNo)
			if Command'="" {
				if blnHead {
					set ObjectName = $$$WWW124ButtonDescription(objButton)
					write Seq,?5,ObjectName,! 
					set blnHead = $$$NO
				}
				set FieldName = $$$WWW003CaptionInForms($get(^WWW003(0,"WWW124",FieldNo,1)))      ; D2
				set FieldName = FieldName_$justify("",lengthName)
				write ?5,FieldNo,?10,$extract(FieldName,1,lengthName)," : ",Command,!
			}
		}
	}
	write !,"*****************************",!
	quit
	
	
Delete(pidForm)
	;-------------------------------------------------------------------------------
	; Wrapper for DEL^WWWFLOE. @Net form deletion.
	;
	; Params:	pidForm - (mandatory) form to delete
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Apr-2006	JW		SR14429: Created
	;-------------------------------------------------------------------------------
	new VORGX,VORG,YQ
	
	if ($get(pidForm)'="") && $data(^WWW120(0,pidForm,1)) {
		set VORGX   = pidForm
		set VORG(2) = $$$YES
		set YQ      = 1
		do DEL^WWWFLOE
	}
	quit
	
	
FormRedirect(pidForm, pidKey)
	;-------------------------------------------------------------------------------
	; Redirect to appropriate to form. Also lock current form and display
	; message prior to redirection.
	;
	; Params:
	; pidForm - Form Id
	; pidKey - Key
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 21-Jul-2009	GRF		Enum Macro
	; 27-Oct-2006	PO		SR15143: Created
	;-------------------------------------------------------------------------------
	set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
	
	$$$Alert($listbuild("Com00263", $$^WWWFORMNAME(pidForm)))  ; "Redirecting to the form %1."
	
	do RedirectForm^COMUtilForm(pidForm,pidKey,$get(YBACK),"","")
	quit

AfterFieldText(pstrText)
	;-------------------------------------------------------------------------------
	; Display Text after the field
	; Params:
	;	pstrText
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 04-Oct-2012	shobby	SR18129: Better alignment of caption and input controls
	; 15-May-2012	PO		SR17993: Created
	;-------------------------------------------------------------------------------
	new strFont
	set:$get(YFORM)'="" strFont=$PIECE($GET(^WWW120(0,YFORM,1)),Y,7)
	if $get(strFont)="" set strFont=$PIECE($get(YVOR),Y,7)
	if strFont="" set strFont=2
	

	;SR18129 write "<FONT SIZE="""_strFont_""">",$$$Text(pstrText)_"</FONT>"
	write "<FONT SIZE="""_strFont_""""															 ;SR18129
	if YVAUS="TOP" write " style='vertical-align:top; position:relative; top:"_YPADDINGTOP_";' " ;SR18129
	write ">",$$$Text(pstrText)_"</FONT>"														 ;SR18129
	quit
]]></Routine>
</Export>