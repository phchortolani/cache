<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPRUN6" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPRUN6
#include INConst
#include COMSYS
#define Log(%1) do Log^INDRPRUNLog(%1)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PRÜFEN UND WARENBESCHAFFUNG
	;
	; Inputs : 
	;
	; ByRef :
	;	BESTAND		Current Stock on Hand (INARTMENGE Type 2)
	;	EINSPRUNG
	;	KBESTAND
	;	ATPREQUEST
	;	ART
	;
	; Returns :
	;
	;
	; History :
    ; 12-Mar-2009	PPP		SR16397: Added Logging for the process
	; 09-Jul-2007	FIS		SR15564 order time phases
	; 30-Nov-2006	GRF		SR15234: doco
	; 26-Sep-2006	RPW		SR14933: If this is coming from the POS and there is
	; 						sufficient quantity, and we ARE creating the MO, ignore
	; 						the quantity and go ahead and create the order.
	; 13-Dec-2005	JW		SR14012: Quit if only an ATPRequest.
	; 25.04.2005	FIS		SR12200
	; 08.02.2005	FIS		27255
	; 07.02.2005	FIS		26299
	; 26.09.2001	FIS
	;-------------------------------------------------------------------------------
	new blnFakeIt,BESTAND1,BESTELLMENGE,BESTELLMENGE1,YONCE,YQ
	
	quit:$get(EINSPRUNG)=1     ;NICHT WENN AUS NETTOBEDARFSÜBERSICHT ;Not when out of
	quit:$get(KBESTAND)="X"    ;KEINE BESTELLUNG (AUSSERHALB BESTELLZEIT BEI REGEL O UND B)<
	quit:$get(ATPREQUEST)=1		//SR14012
	
	IF ART'="" DO
	. SET BESTAND1=BESTAND
	. IF $PIECE($GET(^INDRPSYS(YM,BETRIEB,1)),Y,42)=1 SET BESTAND1=BESTAND1+$GET(RMAMENGE)  ;FIS;19.12.03;24842;OFFENE RMA ALS SUPPLY BERÜCKSICHTIGEN
	. ;
	. SET BESTELLMENGE=$$^INDRPBESMENGE(ART,BETRIEB,BESTAND1)
	. $$$Log($$$Text($lb("IN00989",BESTELLMENGE)))		//Supply Qty : %1
	. ;
	. IF BESTELLMENGE="##" DO ^INDRPRUN7() QUIT  ;WARENHERKUNFT FESTLEGEN
	. ;
	. QUIT:$GET(YSOURCING)=1  ;NICHT WENN NUR FESTLEGEN WARENHERKUNFT;FIS;21.09.04;26407
	. ;
	. ; ONLY THE NEED WITHIN LEADTIME ORDERING / or withine time phase
	. ; NUR DEN BEDARF INNERHALB DER LEADTIME BESTELLEN    ;FIS;08.02.05;27255
	. ;IF $$$INDRPSYSOnlyOrderWithinLeadTime($GET(^INDRPSYS(YM,BETRIEB,1)))=1 IF KBESTAND'="" DO
	. IF $$$INDRPSYSOnlyOrderWithinLeadTime($GET(^INDRPSYS(YM,BETRIEB,1)))=1!(+$get(dteTimePhaseEnd)'=0) IF KBESTAND'="" DO  ;SR15564;09-Jul-2007;FIS;order time phases
	. . SET BESTELLMENGE1=$$^INDRPBESMENGE(ART,BETRIEB,KBESTAND) $$$Log($$$Text($lb("IN00990",ART,BETRIEB,KBESTAND,BESTELLMENGE1))) 	//DRP Calculated Qty within leadtime %1 / %2 / %3 : %4
	. . IF BESTELLMENGE1'<0 IF BESTELLMENGE1<BESTELLMENGE SET BESTELLMENGE=BESTELLMENGE1
	. . IF BESTELLMENGE1<0 SET BESTELLMENGE=0
	. ;
	. 
	. ; GOODS PROCUREMENT     ; WARENBESCHAFFUNG
	. SET YONCE=""
	. IF ($PIECE(ART2,Y,8)="O") || ($PIECE(ART2,Y,8)="B")             SET YONCE=1             ;KEINE DOPPLETE BEARBEITUNG BEI BESTELLREGEN O UND B
	. IF ($PIECE(ART2,Y,8)="Q") || ($PIECE(ART2,Y,8)="O") IF TYPE'="" SET YONCE=1_"|"_$PIECE(SATZ(1),Y,111)  ;CREATE SUPPLY ONLY ONCE AND SAVE REFERENCE ;FIS;25.04.05;SR12200
	. ;
	. set blnFakeIt = $$$INDRPITEMAutoCreateManufacturingOr(ART2)&&(+BESTELLMENGE=0)&&($get(BGJOB("No MO"))=1) // SR14933
	. if blnFakeIt set BESTELLMENGE=$get(MENGE(1)) // SR14933
	. IF +BESTELLMENGE>0 $$$Log($$$Text($lb("IN00991",BESTELLMENGE))) DO ^INDRPRUN11(ART,BESTELLMENGE,YONCE)	//Create One Off Supply : %1
	. ;
	. ;MIN/MAX BESTELLMENGEN GELTEN NUR JE AUFTRAG,
	. ;DAHER KÖNNEN MEHERER AUFTRÄGE ANGELEGT WERDEN
	. IF $GET(BGJOB)'=1 $$$Log($$$Text("IN00992")) QUIT		//DRP run as a foreground process.  Exiting...
	. ;IF $PIECE(ART2,Y,8)="O"!($PIECE(ART2,Y,8)="B") QUIT  ;NICHT BEI BACK-TO-BACK/ORDER-TO-ORDER
	. IF ($PIECE(ART2,Y,8)="O") || ($PIECE(ART2,Y,8)="B") || ($PIECE(ART2,Y,8)="Q") QUIT  ;NICHT BEI BACK-TO-BACK/ORDER-TO-ORDER ;OR Q ;FIS;25.04.05;SR12200
	. quit:blnFakeIt  // SR14933
	. IF $$$INDRPSYSCountMinMaxQtyOnlyOnce($GET(^INDRPSYS(YM,BETRIEB,1)))'=1 QUIT  ;FIS;07.02.05;26299
	. ;
	. SET BESTAND1=BESTAND1+BESTELLMENGE
	. SET KBESTAND=KBESTAND+BESTELLMENGE
	. SET YQ=0
	. FOR  QUIT:YQ=1  DO
	. . SET BESTELLMENGE=$$^INDRPBESMENGE(ART,BETRIEB,BESTAND1)
	. . ;
	. . $$$Log($$$Text($lb("IN00993",BESTELLMENGE)))		//Revised Supply Qty : %1
	. . 
	. . ;NUR DEN BEDARF INNERHALB DER LEADTIME BESTELLEN          ;FIS;08.02.05;27255
	. . IF $PIECE($GET(^INDRPSYS(YM,BETRIEB,1)),Y,45)=1 IF KBESTAND'="" DO
	. . . SET BESTELLMENGE1=$$^INDRPBESMENGE(ART,BETRIEB,KBESTAND)
	. . . IF BESTELLMENGE1'<0 IF BESTELLMENGE1<BESTELLMENGE SET BESTELLMENGE=BESTELLMENGE1
	. . . IF BESTELLMENGE1<0 SET BESTELLMENGE=0
	. . ;
	. . IF +BESTELLMENGE'>0 SET YQ=1 QUIT
	. . $$$Log($$$Text($lb("IN00994",BESTELLMENGE)))	//Final Supply Qty : %1
	. . DO ^INDRPRUN11(ART,BESTELLMENGE)  ;WARENBESCHAFFUNG
	. . SET BESTAND1 = BESTAND1 + BESTELLMENGE
	. . SET KBESTAND = KBESTAND + BESTELLMENGE
	
	QUIT
]]></Routine>
</Export>