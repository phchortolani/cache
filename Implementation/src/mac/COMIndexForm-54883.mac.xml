<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMIndexForm" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMIndexForm
 
#include COMSYS
#include WWWConst
#include COMConst
 
AfterDataFields(Class="")
    ;-------------------------------------------------------------------------------
    ; Show History
    ;
    ; Returns:
    ;
    ; History:
    ; 14-Dec-2005	GRF		Boolean Macros
    ; 06-Apr-2005	SCR		Created SR12040
    ;-------------------------------------------------------------------------------
	new Query,VORG
	
	if Class="" quit
	
	set VORG(1)=Class
	set Query("Name")="History"
	set Query("Class")="COMIndexHist"
	set Query("Col",1)="P~2"
	set Query("Col",2)="D~1"
	set Query("Col",3)="D~2"
	set Query("Col",4)="D~3"
	set Query("Col",5)="D~4"
	set Query("Col",6)="D~5~L"
	set Query("Col",7)="D~6~L"
	set Query("Col",8)="D~7~L"
	set Query("Loop")="HistoryLoop^COMIndexForm"
	set Query("Get")="set id=$get(^COMIndexHist(YM,id(1),id(2),1))"
	Do Query^COMQuery(.Query,.VORG)
 
	quit
	
HistoryLoop(Query,Storage,VORG)
    ;-------------------------------------------------------------------------------
    ; Show History Loop
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Apr-2005	SCR		Created SR12040
    ;-------------------------------------------------------------------------------
	new count,id,Class
 
 	set id(1)=$get(VORG(1))
	set count=0
	set id(2)=""
	for {
		set id(2)=$order(^COMIndexHist(YM,id(1),id(2)),-1)
		quit:id(2)=""
		
		set count=count+1
		merge @Storage@(count)=id
	}
	quit count
 
ReIndexAllForm(Classes="")
    ;-------------------------------------------------------------------------------
    ; ReIndex All Classes (Form Interface)
    ; 
    ; Optional Classes list ';' delimeted ( Used by @NM Recreate Index Keys option WWWSORT & WWWSORTGEN )
    ; Returns:
    ;
    ; History:
    ; 29-Mar-2006	SC		SR13942: Use JobWrapper^COMUtilJob.
    ; 22-Feb-2006	JW		SR13942: Rolled back change
    ; 20-Dec-2005	RPW		SR13942: Pass the current environment through to the job.
    ; 06-Apr-2005	SCR		Created SR12040
    ;-------------------------------------------------------------------------------
    new Log
	
	do StartLog^COMLog("ReIndex all Classes",.Log) 
	;job ReIndexAll^COMIndex($$$NO,Log,YUSER,YBED,Classes) //:(:1::)    ; Environment created for new job NFA SR13942
	do JobWrapper^COMUtilJob("ReIndexAll^COMIndex("_$$$NO_","""_Log_""","""_Classes_""")",YUSER)     ; Environment created for new job NFA SR13942
 
	hang 2 ;wait for the process to start up
 
	; Ready to goto the Detail form
	do RedirectForm^COMUtils("COMMsg",Log,",","","")
	quit
 
		
ReIndexForm(Class="")
    ;-------------------------------------------------------------------------------
    ; ReIndex a Class (Form Interface)
    ;
    ; Returns:
    ;
    ; History:
    ; 29-Mar-2006	SC			SR13942: Use JobWrapper^COMUtilJob.
    ; 20-Dec-2005	RPW			SR13942: Pass the current environment through to the job.
    ; 06-Apr-2005	SCR		Created SR12040
    ;-------------------------------------------------------------------------------
    new Log
	
	do StartLog^COMLog("ReIndex Class "_Class,.Log) 
	;job ReIndex^COMIndex(Class,$$$NO,Log,YUSER,YBED):(:1::)    ; Environment created for new job NFA SR13942
	do JobWrapper^COMUtilJob("ReIndex^COMIndex("""_Class_""","_$$$NO_","""_Log_""")",YUSER)
 
	hang 2 ;wait for the process to start up
 
	; Ready to goto the Detail form
	do RedirectForm^COMUtils("COMMsg",Log,",","","")
	quit
	
]]></Routine>
</Export>