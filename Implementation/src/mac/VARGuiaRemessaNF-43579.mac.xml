<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARGuiaRemessaNF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARGuiaRemessaNF
#include COMSYS
#include INConst
#include VARConst
	;-------------------------------------------------------------------------------
    ; A Guia de Remessa de Notas Fiscais controla a remessa de notas entre a unidade
    ; que faz o recebimento físico e o departamento financeiro para que possa ser feito 
    ; o pagamento do fornecedor.
    ; 
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------    
    quit
	
PostableEnviar(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se é possível enviar a guia.
    ;
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new idGuia, objGuia, statusGuia
 
	set idGuia     = pYKEY
	set objGuia    = $get(^VARGuiaRemessaNF(YM,idGuia,1))	
	set statusGuia = $$$VARGuiaRemessaNFStatus(objGuia)
	
	if (statusGuia > 0) {
		set YQ = 1_"Essa guia já foi enviada."
		quit
	
	} elseif '$data(^VARGuiaRemessaNFLinha(YM,idGuia)) {
		set YQ = 1_"Nenhum recebimento foi inserido na guia."
		quit
	}
		
	do ValidaRecebimentoRepetidoNaGuia(idGuia)
	
	quit
 
EnviarRemessa(pYKEY)
	;-------------------------------------------------------------------------------
    ; Confirma o envio da remessa, atualizando o status e a data de envio. 
    ; Esse método também atualiza free fields na INREC.
    ;
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:(pYKEY = "")
	
	new idGuia, objGuia, strStatus 
	set idGuia = pYKEY
 
	set strStatus = $$AtualizaINREC(idGuia,"ENVIO")
	
	if $$$ISOK(strStatus) {
	
		set objGuia = ""
		set objGuia = $get(^VARGuiaRemessaNF(YM,idGuia,1))
	
		set $$$VARGuiaRemessaNFStatus(objGuia) 		  = 1
		set $$$VARGuiaRemessaNFDataEnvioGuia(objGuia) = $horolog
		set $$$VARGuiaRemessaNFEnvioConfirmadoPor(objGuia) = YBED
	
		set strStatus = $$Save^COMUtils("VARGuiaRemessaNF",idGuia,objGuia,$$$YES)
	}
		
	if $$$ISERR(strStatus) { 
		do StartScript^COMUtiljavascript()
		write "alert('Não foi possível salvar os dados.'); "
		do EndScript^COMUtiljavascript()
	}
		
	do GoToForm^COMUtilForm("VARGuiaRemessaNF",idGuia)		
 
	quit
 
PostableConfirmarRecebimento(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se é possível confirmar o recebimento da guia.
    ;
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	new idGuia, objGuia, statusGuia
 
	set idGuia     = pYKEY
	set objGuia    = $get(^VARGuiaRemessaNF(YM,idGuia,1))		
	set statusGuia = $$$VARGuiaRemessaNFStatus(objGuia)
	
	if (statusGuia = 0) {
		set YQ = 1_"Essa guia ainda não foi enviada."
		quit
	
	} elseif (statusGuia = 2) {
		set YQ = 1_"O recebimento dessa guia já foi pré-recebido."
		quit
	
	} elseif '$data(^VARGuiaRemessaNFLinha(YM,idGuia)) {
		set YQ = 1_"Nenhum recebimento foi inserido na guia."
		quit 		
	}
	
	quit
 
ConfirmarRecebimentoRemessa(pYKEY)
	;-------------------------------------------------------------------------------
    ; Confirma que a guia foi recebida.
    ; Esse método também atualiza free fields na INREC.
    ;
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new idGuia, objGuia, strStatus
 	
	set idGuia 	  = pYKEY
	set strStatus = $$$OK
	
	set strStatus = $$AtualizaINREC(idGuia,"CONFIRMACAORECEBIMENTO")
	
	if $$$ISOK(strStatus) {
	
		set objGuia = $get(^VARGuiaRemessaNF(YM,idGuia,1))
	
		set $$$VARGuiaRemessaNFStatus(objGuia) = 2
		set $$$VARGuiaRemessaNFDataConfirmacaoRecGuia(objGuia) = $horolog
		set $$$VARGuiaRemessaNFRecebimentoConfirmadoPor(objGuia) = YBED
	
		set strStatus = $$Save^COMUtils("VARGuiaRemessaNF",idGuia,objGuia,$$$YES)
	}
		
	if $$$ISERR(strStatus) {
		do StartScript^COMUtiljavascript()
		write "alert('Não foi possível salvar os dados.'); "
		do EndScript^COMUtiljavascript()
	}
		
	do GoToForm^COMUtilForm("VARGuiaRemessaNF",idGuia)
 
	quit
	
ValidaRecebimentoRepetidoNaGuia(NumGuia)
	;-------------------------------------------------------------------------------
    ; Valida se existe algum recebimento repetido nas linhas.
    ;
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new LinhaGuia, objLinhaGuia, NoRecebimento, ArrayRecebimentos, Count,
		ExisteRepeticao, ArrayRepetidos, ArrayMensagens, cnt, ArrayLinhas, 
		Count1, LinhaGuia1, Rec, Count2, Count3
	
	set LinhaGuia = ""
	set ArrayRecebimentos = ""
	set Count = 0
	set ArrayRepetidos = ""
	set ExisteRepeticao = 0
	set Count3 = 0
	
	for {
   		set LinhaGuia = $order(^VARGuiaRemessaNFLinha(YM,NumGuia,LinhaGuia))            
   		quit:(LinhaGuia = "")
   	
   		set Count = $increment(Count)
   		
    	set objLinhaGuia = $get(^VARGuiaRemessaNFLinha(YM,NumGuia,LinhaGuia,1))
    				    			
    	set NoRecebimento = $piece(objLinhaGuia,Y,1)    	    	    	
 
		if ($find(ArrayRecebimentos,NoRecebimento) && (Count > 1)) {
			
			set ExisteRepeticao = 1
			
			if '$find(ArrayRepetidos,NoRecebimento) {
				if Count3 = 0 {
					set ArrayRepetidos = NoRecebimento
				}
				else { 
					set ArrayRepetidos = ArrayRepetidos_";"_NoRecebimento
				}
				set Count3 = Count3 + 1
			}											
		}  
		
		set ArrayRecebimentos = ArrayRecebimentos_";"_NoRecebimento_"("_Count_")"
		 		
	}
	
	set ArrayMensagens = ""
	
 	for cnt=1:1:$length(ArrayRepetidos) {
		
		set Rec = $piece(ArrayRepetidos,";",cnt)
		quit:(Rec = "")
			
		set Count1 = 0
		set LinhaGuia1 = ""
		set ArrayLinhas = ""
		set Count2 = 0
		
		for {
   			set LinhaGuia1 = $order(^VARGuiaRemessaNFLinha(YM,NumGuia,LinhaGuia1))            
   			quit:(LinhaGuia1 = "")
   			
   			set Count1 = $increment(Count1)
   		
    		set objLinhaGuia = $get(^VARGuiaRemessaNFLinha(YM,NumGuia,LinhaGuia1,1))
    				    			
    		set NoRecebimento = $piece(objLinhaGuia,Y,1)
    	
    		if $piece(ArrayRepetidos,";",cnt) = NoRecebimento {
	  		  	if Count2 = 0 {	
	  		  		set ArrayLinhas = LinhaGuia1
	  		  	}
	  		  	else { 
	  		  		set ArrayLinhas = ArrayLinhas_", "_LinhaGuia1 
	  		  	}
	  		  	set Count2 = $increment(Count2)
    		}    			
		 	
		}
		
		set Mensagem = "Recebimento "_Rec_" -> Seq. "_ArrayLinhas
		set ArrayMensagens = ArrayMensagens_$char(10)_Mensagem
		 		
 	}
	
	if (ExisteRepeticao = $$$YES) {
		set YQ = 1_"Não é possível enviar porque existem recebimentos repetidos nessa guia. "_
		            $char(10)_""_ArrayMensagens_""
		quit			
	}  			   		
 
	quit
 
CanViewReport(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Verifica se o relatório da Guia de Remessa de NF pode ser impresso.
    ;
    ; History:
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
 	quit:(pYKEY = "")
 	
 	new objGuia
 	set objGuia = $get(pYFELD)
 	
 	if $$$VARGuiaRemessaNFStatus(objGuia) = 0 {
		set YQ = 1_"O relatório só fica disponível após o envio da Guia para o Pagamento "_$c(10)_
		           "ou a confirmação de recebimento pelo Pagamento."	
	} else {
		set YQ =  $$$YQEnable
	}

 	quit
	
AtualizaINREC(pNumGuia,pTipoAtualizacao)	
	;-------------------------------------------------------------------------------
    ; Atualiza os recebimentos que foram inseridos na guia.
    ; Informações atualizadas: posição, data de envio e data de confirmação (todos 
    ; FREEs fields).
    ; 
    ; History:
    ; 17-Feb-2014	shobby	SESDF-1095:	Pass in flag to Save to suppress OnBeforeSave
    ; 01-Mar-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------  
	quit:( (pNumGuia = "") || (pTipoAtualizacao = "") )
	
	new idGuiaLinha, objGuiaLinha, NoRecebimento, objRecebimento, strStatus
	
	set idGuiaLinha = ""
 
 	$$$Order3(^VARGuiaRemessaNFLinha,YM,pNumGuia,idGuiaLinha)
	
		set objGuiaLinha   = $get(^VARGuiaRemessaNFLinha(YM,pNumGuia,idGuiaLinha,1))		
		set NoRecebimento  = $piece(objGuiaLinha,Y,1)		
		set objRecebimento = $get(^INREC(YM,NoRecebimento,1))
		
		if pTipoAtualizacao = "ENVIO" {
			set $piece(objRecebimento,Y,38) = 3 	   ;Posição da NF
			set $piece(objRecebimento,Y,39) = $horolog ;Data de Envio da Guia
		}
			
		elseif pTipoAtualizacao = "CONFIRMACAORECEBIMENTO" {
			set $piece(objRecebimento,Y,38) = 4 	   ;Posição da NF
			set $piece(objRecebimento,Y,40) = $horolog ;Data de Confirmação do Recebimento da Guia
		}

		set strStatus = $$Save^COMUtils("INREC", NoRecebimento, objRecebimento, $$$YES,,$$$NO)	;SESDF-1095	
		
		if $$$ISERR(strStatus) { 
			do StartScript^COMUtiljavascript()
			write "alert('Não foi possível salvar os dados.'); "
			do EndScript^COMUtiljavascript()
		}
	
	$$$End
	
	quit strStatus
]]></Routine>
</Export>