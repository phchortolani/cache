<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31A" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMGridEdit31
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMGridEdit31A("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
COMGridEdit31A(YGETDATA,YLFDAT)
    ;-------------------------------------------------------------------------------
    ; Called By : COMGridEdit31J5 : activateNextField() and loadSelectField()
    ; 
    ; Inputs:	YGETDATA	
    ; 			YLFDAT		"Y12_34"   Form field F34 on grid row 12  (could possibly be 12.01 or 12x01)
    ; 
    ; Returns:
    ;
    ; History:
    ; 16-Feb-2005	JW		Only do below for currency fields.
    ; 09-Feb-2005	Shobby	Obtain YINHALT from YLFDAT subscript rather than 
    ; 							REFERENCEVALUE.  This preserves the extra decimal
    ; 							points on currency fields.
    ;-------------------------------------------------------------------------------
	new YRETVAL,YVALUE,YBBN,YEXEC,YDATEI,YKEY
	
	$$$LogR("",$get(YGETDATA)_"<"_$get(YLFDAT))
	
	set YRETVAL=""
	set YLFDAT = $get(YLFDAT)          ; FIELDNUMBER          Yrow_col
	set YBBN   = $piece(YLFDAT,"_",2)  ; FORMULARFELDNUMMER
	set YKEY   = $$GetYKEY^COMGridEdit31G($piece($piece(YLFDAT,"_",1),"Y",2))
	set YDATEI = $$$GRIDClass(YFORM)
	
	if +$get(YGETDATA)'=0 {
		set YRETVAL = $$GetData1(YBBN,YGETDATA)
	} else {
		if (YLFDAT'="") && (+YBBN'=0) && (YDATEI'="") {
			set YRETVAL = $$GetData2(YBBN,YLFDAT)
		}
 	}
 	quit YRETVAL
	
	
GetData1(YBBN,pYGETDATA)
	new strYRETVAL,strYHTML
	
	$$$LogR("GetData1",YBBN_"<"_pYGETDATA)
	
	set strYRETVAL = ""
	set strYHTML   = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,"V","REFERENCESELECT",YBBN,pYGETDATA,1))
	set strYHTML   = $$^WWWTRANSLATE($$^WWWTRANSLATE(strYHTML,"<","_ASCII60_"),">","_ASCII62_")
	if strYHTML'="" {
		set strYHTML   = $$ESC^WWWENCODE(strYHTML)
		set strYRETVAL = strYHTML
	}
	quit strYRETVAL
	
	
GetData2(YBBN,YLFDAT)	
	;-------------------------------------------------------------------------------
	;
	; Returns:
	;
	; History:
	; 10-Nov-2011	shobby	SR17725: FATSEARCH
	; 25-Jan-2011	GRF		SR17579: Get YFELD for use in  PARA to generate DLDP and
	;							DLDPStorage
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 05-Jan-2010	GRF		SR15525: Call to $$Convert^WWWTR rather than $$^WWWTR
	; 14-Oct-2009	shobby	SR16894: Use JSESC function to handle characters such as | and ~.
	; 22-Sep-2009	shobby	SR16894: Changed the encoding of the text passed to activateFieldNow as it sets
	; 							the value with innerHTML.
	; 02-Jul-2007	GRF		SRBR014567: revise to allow discarding SysSetup flag
	; 							after Peer Review
	; 29-Jun-2007	shobby	SRBR014567:	Previous change based on SysSetup flag.
	; 28-Jun-2007	shobby	SRBR014567: Handle European style number formats.
	; 06-Mar-2007	PO		SR15466: Pass YDAT by reference
	;  2-Aug-2006	JW		SR14754 Set the focus field - needed, especially for COMView.
	; 02-Jun-2005	PO		SR12050 Set strYINHALT to appropriate amount if currency type
	;-------------------------------------------------------------------------------
	new PARA,REL,RELF,SUCH,YCOLOFF,YCOLON,YFIELDNAME,YFONT,YFONTSIZE,YFTYPE,YHEIGHT
	new YLENGTH,YLFN,YPADDING,YROW,YTXT,YTYP,YVALIGN,YWIDTH
	new strYHTML,strYINHALT,strYRETVAL

	$$$LogR("GetData2",YBBN_"<"_YLFDAT)
	
	$$$SetFocusField(YLFDAT)
	
	set strYRETVAL=""

	set YROW  = $piece($piece(YLFDAT,"Y",2),"_",1)
	set YFELD = $$GetYFELD^COMGridEdit31G(YFORM,YROW)   ; SR17579
	
	
	set YFIELDNAME = YLFDAT
	do PARA^COMGridEdit31Body("D",YBBN,.YWIDTH,.YVALIGN,.YHEIGHT,.YFONT,.YFONTSIZE,.YTYP,.YFORM,.YLFN,.YTXT,.YDAT)
	quit:YFTYPE=""
	
	if (YFTYPE="INPUT") && (YVALIGN=" text-align:right;") set YPADDING = " padding-right:4px;"
	
	;+++++++++++++++++++++++++++++++++++++++
	;  Currency value in WWWDATEN is in internal format.
	;  Other values under "REFERENCEVALUE" are in external format (locale-based).
	;  
	;  YTEXT will be in external format.  Currency needs to be converted to external format for return value
	;+++++++++++++++++++++++++++++++++++++++
	
	if (YTYP=8) { ; Currency   [pcurAmount,pstrYFORM]
		set strYINHALT = $$AmountForCurrentMode^COMUtilCurrency($get(^WWWDATEN(0,+$horolog,YUSER,YFORM,"V",YLFDAT,1)),$get(^CacheTemp(YUSER,"Grid","Container")))
		set strYINHALT = $$GetLiteral^WWWTR(YTYP,strYINHALT) 
		set YTEXT      = strYINHALT
	
	} else {
		set strYINHALT = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,"V","REFERENCEVALUE",YLFDAT,1))
	;	set YTEXT      = $$GetLiteral^WWWTR(YTYP,strYINHALT)   ; SR17579
		set YTEXT      = $$GetLiteral^WWWTR(YTYP,strYINHALT,DLDP)
	}
	
	
	if YTYP=2 set YTEXT = +YTEXT
	set strYHTML=""
	if YFTYPE="SELECT" kill ^WWWDATEN(0,+$horolog,YUSER,YFORM,"V","REFERENCESELECT",YBBN)
	if (YFTYPE="TEXT") || (YFTYPE="INPUT") || (YFTYPE="CURRENCY") || (YFTYPE="CHECK") || (YFTYPE="SELECT") {
	 	;TODO sort out for other values of YFTYPE
		xecute "set strYHTML=$$"_YFTYPE_"^COMGridEdit31F(,YFIELDNAME,YTXT,PARA,REL,YTEXT,YTYP)"
	} elseif (YFTYPE="FATSEARCH") {
		set strYHTML=$$FATSEARCH^COMGridEdit31FATSEARCH(,YFIELDNAME,YTXT,PARA,REL,YTEXT,YTYP) ;SR17725
	} else {
	 	xecute "set strYHTML=$$"_YFTYPE_"^COMGridEdit31F()"
	}
	set strYINHALT = $$JSESC^WWWENCODE(strYINHALT)
	if strYHTML'="" {
		set strYHTML   = $$JSESC^WWWENCODE(strYHTML)
		set strYRETVAL = "#FUNCTION~activateFieldNow("""_YLFDAT_""","""_strYHTML_""","""_strYINHALT_""","""","""_YFORM_""");" ;SR17630
	
	} else {
		set strYRETVAL = "#FUNCTION~activateFieldNow("""_YLFDAT_""","""","""_strYINHALT_""",1,"""_YFORM_""");" ;SR17630
	}
	quit strYRETVAL
	
]]></Routine>
</Export>