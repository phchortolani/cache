<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INUPricePlan" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INUPricePlan
	
#include COMSYS
#include WWWConst
#include INConst
#include COMConst
	
OnAfterDataFields(pidPricePlan)
	;-------------------------------------------------------------------------------
	; After Data Fields Displayed Load the Edit Grid
	;
	; Params:
	; pidPricePlan - Price Plan Id
	;
	; ByRefs: None
	;
	; Returns: None
	;
	; History:
	; 04-Dec-2006	PO		SR15276: Created
	;-------------------------------------------------------------------------------
	do LoadGrid(pidPricePlan)
	quit
	
	
LoadGrid(pidPricePlan)
	;-------------------------------------------------------------------------------
	; Load the Edit Grid
	;
	; Params:
	; pidPricePlan - Price Plan Id
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
	; 04-Dec-2006	PO		SR15276: Created
	;-------------------------------------------------------------------------------
	new YKEY,YFORM,YAUSWAHL,YFELD
	
	set YFORM = "INPricePlanLine"
	set YFELD = ""
	set YKEY  = $get(pidPricePlan)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 300
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "INPricePlan"
 
	do ^COMGridEdit31
	quit
	
	
QuantityBreaks(pidEvent,pidParameters)
	;-------------------------------------------------------------------------------
	; Display the Quantity Breaks table
	;
	; Called By : Form INPricePlan : Dynamic Table Custom Code
	;
	; Params:
	;	pidForm - Form Id
	;	pidRow - Row Id
	;	pidCol - Col Id
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 09-Feb-2009	GRF		SR17552:Ensure ^WWW120DynTable 5th parameter is defined ("") 
	; 11-Jan-2007	PO		SR15339: Table heading
	; 04-Jan-2007	PO		SR15351: Now using generic Dynamic Table
	; 06-Dec-2006	PO		SR15276: Created
	;-------------------------------------------------------------------------------
	new idKey,idPlanLine,idPricePlan,idQuantityBreak,lstColTypes,lstHeaders,objLine
 
	set lstHeaders = $listbuild($$^WWWFELDNAME("INPricePlanLineQty","D",$$$FldINPricePlanLineQtyPricingFormula),
								$$^WWWFELDNAME("INPricePlanLineQty","D",$$$FldINPricePlanLineQtyMinQuantity))
 
	set lstColTypes = $listbuild($listbuild("left","display"),$listbuild("left","display"))
 
	set idKey = $$ReferenceKey^COMGridEdit31Interface($$$DYNTABLEGridLineFocusGrid(pidParameters),$$$DYNTABLEGridLineFocusRow(pidParameters))
	set idPricePlan = $$$KEY1(idKey)
	set idPlanLine  = $$$KEY2(idKey)
 
	set idQuantityBreak = ""
	for {
		set idQuantityBreak = $order(^INPricePlanLineQty(0,idPricePlan,idPlanLine,idQuantityBreak))
		quit:idQuantityBreak=""
		
		set objLine = $get(^INPricePlanLineQty(0,idPricePlan,idPlanLine,idQuantityBreak,1))
 
		set ^CacheTempDynamicTable(YUCI,YUSER,1,idQuantityBreak) = $$$INPricePlanLineQtyPricingFormula(objLine)_Y_$$$INPricePlanLineQtyMinQuantity(objLine)
	}
 
	do DrawTable^WWW120DynTable(lstHeaders,1,lstColTypes,pidParameters,"",pidEvent)		//SR17552
 
	quit
	
	
OpenRule(pidPricePlan)
	;-------------------------------------------------------------------------------
	; Open the Generation Rule for this price plan
	;
	; Params:
	; pidPricePlan - Price Plan id
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 06-Dec-2006	PO		SR15276: Created
	;-------------------------------------------------------------------------------
	new objPricePlan
 
	if '$$$NoKey(pidPricePlan) {
		set objPricePlan = $get(^INPricePlan(0,pidPricePlan,1))
 
		do GoToForm^COMUtilForm("INPPGenRule",$$$INPricePlanSourceRule(objPricePlan))
	}
	quit
	
]]></Routine>
</Export>