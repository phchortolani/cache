<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPARInv" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPARInv
	#include COMSYS
	#include FINConst
	#include WWWConst
 
GetInvoiceTitle(pidCompany,pidInvoice,pidLang)
    ;-------------------------------------------------------------------------------
    ; Get the title of this invoice
    ;
    ; Returns: string
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strTitle,objInvoice,SPRACHE
 
	set strTitle=""
 
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="")&&($get(pidLang)'="") {
		set objInvoice=$get(^FINARInv(pidCompany,pidInvoice,1))
		set SPRACHE=$get(pidLang)
		if $$$FINARInvCredit(objInvoice) {
			set strTitle=$$DecodeError^COMUtilError("Fin00935",,SPRACHE)  ;Credit Invoice	
		} else {
			set strTitle=$$DecodeError^COMUtilError("Fin00433",,SPRACHE)  ;Invoice
		}	
	}
 
	quit $zconvert(strTitle,"U")
	
	
GetCustName(pidCompany,pidCustomer,pblnFormat=$$$YES)
    ;-------------------------------------------------------------------------------
    ; Gets/formats a customer's name
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strName,objCustomer
 
	set strName=""
	
	if ($get(pidCompany)'="")&&($get(pidCustomer)'="") {
		set objCustomer=$get(^FINARCustomer(pidCompany,pidCustomer,1))
		set strName=$$$FINARCustomerCustomerName(objCustomer)
		
		if (strName'="") {
			if pblnFormat set strName=pidCustomer_": "_strName	
		} else {
			set strName=pidCustomer	
		}
	}
 
	quit strName
	
	
GetTermString(pidCompany,pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Gets the term description for an invoice
    ;
    ; Returns: string
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new YM,strTerm,objInvoice,idTerm,idCust,objTerm
 
	set strTerm=""
	
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="") {
		set objInvoice=$get(^FINARInv(pidCompany,pidInvoice,1))
		set idTerm=$$$FINARInvTerms(objInvoice)
		
		if (idTerm="") {
			set YM=pidCompany
			set idCust=$$$FINARInvCustomer(objInvoice)
			set idTerm=$$GetTerm^FINARCustomer(idCust)
		}
		
		if (idTerm'="") {
			set objTerm=$get(^FINSYSTerm(pidCompany,idTerm,1))
			set strTerm=$$$FINSYSTermDescription(objTerm)	
		}
	}
 
	quit strTerm
	
	
GetTotal(pidCompany,pidInvoice,pstrAmount="TOTAL")
    ;-------------------------------------------------------------------------------
    ; Gets the "total" of an invoice.
    ; 
    ; pstrAmount: "Total" 	 : Grand total of the invoice
    ; 			  "SubTotal" : Total (Ex Tax) of the invoice
    ; 			  "Tax"		 : Total Tax on the invoice
    ;
    ; Returns: currency
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new YM,curAmount
 
	set curAmount=0
	set pstrAmount=$zconvert(pstrAmount,"U")
	
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="") {
		set YM=pidCompany
		if pstrAmount="SUBTOTAL" {
			set curAmount=$$GetSubTotal^FINARInv(pidInvoice)	
		} elseif pstrAmount="TOTAL" {
			set curAmount=$$GetTotal^FINARInv(pidInvoice)	
		} elseif pstrAmount="TAX" {
			set curAmount=$$$FINARInvTax($get(^FINARInv(pidCompany,pidInvoice,1)))	
		}
	}
 
	quit $$FCBase^COMSYSFC(curAmount)
	
	
FormatAmount(pidCompany,pidInvoice,pcurAmount=0,pstrCurrency)
    ;-------------------------------------------------------------------------------
    ; Format a line amount appropriately (negatives in brackets)
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strAmount,objInvoice,strCurFormatted,YM
 
	set strAmount=pcurAmount
 
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="") {
		set YM=pidCompany
		set objInvoice=$get(^FINARInv(YM,pidInvoice,1))
		if $$$FINARInvCredit(objInvoice) {
			set pcurAmount=-pcurAmount
		}	
	}
	
	if (pcurAmount<0) {
		set strAmount="("_$$FormatCurrency^COMTable($zabs(pcurAmount),$$$NO,$get(pstrCurrency))_")"
	} else {
		set strAmount=$$FormatCurrency^COMTable(pcurAmount,$$$NO,$get(pstrCurrency))		
	}
	
	quit strAmount
	
	
GetContact(pidCompany,pidLocation)
    ;-------------------------------------------------------------------------------
    ; Get contact information for an invoice
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strContact,objLocation,idContact
 
	set strContact=""
	
	if ($get(pidCompany)'="")&&($get(pidLocation)'="") {
		set objLocation=$get(^WWW0121(0,pidCompany,pidLocation,1))
		set idContact=$$$WWW0121Responsible(objLocation)
		
		if (idContact'="") {
			set strContact=$$$WWW013Name($get(^WWW013(0,idContact,1)))
			set strContact=strContact_" @ "_$$$WWW0121Tel(objLocation)
		} else {
			set strContact="---"	
		}
	}
 
	quit strContact
	
 
GetCopyText(pidCompany,pidInvoice,pidLang)
    ;-------------------------------------------------------------------------------
    ; If an invoice has been printed, return "copy". Otherwise, return nothing and
    ; store todays printed date.
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new SPRACHE,strText,objInvoice,YM
	
	;TODO: Pass back YBED instead of using "UNKNOWN" ???
 
	set strText=""
	
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="") {
		set SPRACHE=$get(pidLang)
		set objInvoice=$get(^FINARInv(pidCompany,pidInvoice,1))
		
		if $$$FINARInvPrintedDate(objInvoice)'="" {
			set strText=$$DecodeError^COMUtilError("Com00194",,SPRACHE) ;Copy
		} else {
			set $$$FINARInvPrintedDate(objInvoice)=$h
			do ^WWWVAR
			set YM=pidCompany
			do Save^COMUtils("FINARInv",pidInvoice,objInvoice,1)	
		}
	}
 
	quit strText
	
	
GetInvoiceText(pidCompany,pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Get invoice text for an invoice
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strText,objInvoice,idCustomer
 
	set strText=""
	
	if ($get(pidCompany)'="")&&($get(pidInvoice)'="") {
		set objInvoice=$get(^FINARInv(pidCompany,pidInvoice,1))
		set idCustomer=$$$FINARInvCustomer(objInvoice)
		
		if (idCustomer'="") {
			set strText=$$$FINARCustomerInvoiceText($get(^FINARCustomer(pidCompany,idCustomer,1)))
			set strText=$translate(strText,"|"," ")	
		}
	}
 
	quit strText
	
	
OnBeforeRunning(pidInvoice)
    ;-------------------------------------------------------------------------------
    ; Routine to run before attempting to run an invoice report
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Nov-2005	Steve S		SR12482: Created
    ;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus=$$CanPrint^FINARInvPrint(pidInvoice)
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)	
	}
 
	quit strStatus
	
	
GetAddressLine(pidCompany,pidCustomer,pidLanguage,pidLocation,pidLine)
	;-------------------------------------------------------------------------------
	; Return a single address line for the invoice.
	; If an address line is equal to the country of the current location, then
	; return blank (we don't want to see it on the invoice).
	;
	; Params:	pidCompany	: The YM value
	; 			pidCustomer	: The customer number
	; 			pidLanguage	: The SPRACHE value
	; 			pidLocation	: The current (user) location
	; 			pidLine		: The address line we'd like retrieved
	;
	; ByRefs:
	;
	; Returns:	strLine		: The address line
	;
	; History:
	; 23-Mar-2006	Steve S		SR14442: Created
	;-------------------------------------------------------------------------------
	new strLine,YM,SPRACHE,YLOCATION,objFINARCustomer,strAddress,objWWW0121
	new idCountry,strCountry,intEnd
 
	set strLine=""
	
	set YM			=$get(pidCompany)
	set SPRACHE		=$get(pidLanguage)
	set YLOCATION	=$get(pidLocation)
	
	if (YM'="")&&($get(pidCustomer)'="") {
		set objFINARCustomer=$get(^FINARCustomer(YM,pidCustomer,1))
		set strAddress=$$$FINARCustomerAddress(objFINARCustomer)
		
		if $get(pidLine)'="" {
			set strLine=$$$TRIMWS($$$UPPER($piece(strAddress,"|",pidLine)))
			if (SPRACHE'="")&&(YLOCATION'="") {
				set objWWW0121=$get(^WWW0121(0,YM,YLOCATION,1))
				set idCountry=$$$WWW0121Country(objWWW0121)		
				if (idCountry'="") {
					set strCountry=$$$UPPER($$$WWW100Text($get(^WWW100(0,"LAND",SPRACHE,idCountry,1))))
					if (strCountry=strLine) {
						set strLine=""
					} elseif ($find(strLine,strCountry)=($length(strLine)+1)) { //end of string
						set intEnd=$length(strLine)-$length(strCountry)
						while ",. " [ $extract(strLine,intEnd) {
							set intEnd=intEnd-1	
						}
						set strLine=$extract(strLine,1,intEnd)
					}
				}
			}
		}
	}
 
	quit strLine
]]></Routine>
</Export>