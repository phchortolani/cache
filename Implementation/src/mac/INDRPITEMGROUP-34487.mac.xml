<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPITEMGROUP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPITEMGROUP
#include COMSYS
#include INConst
	
OnBeforeSave(pobjRecord)
	;-------------------------------------------------------------------------------
	; Prevent save if any of the day offsets are negative
	; 
	; If we can access the stock from other locations, we must be able to see the
	; stock from these locations as well
	;
	; Returns:
	;
	; History:
	; 24-Feb-2009	GRF		SR16394: Clarify idClassField rather than idField
	; 24-Jan-2007	GRF		SR15344: INDRPITEMSSupplierDays macro correction
	; 12-Jan-2007	PO		SR15344: Created
	;-------------------------------------------------------------------------------
	new idClassField,strStatus
	
	set strStatus = $$$OK
	
	for idClassField = $$$FldINDRPITEMSFirm,$$$FldINDRPITEMSSource,$$$FldINDRPITEMSRaise,$$$FldINDRPITEMSPlace,$$$FldINDRPITEMSConfirm,$$$FldINDRPITEMSSupplierDays {
		if $piece(pobjRecord,Y,idClassField) < 0 {
			set strStatus = $$$MakeStatus("IN00638",$$^WWWFELDNAME(YFORM,"D",idClassField))  ;%1 number of days must be greater than or equal to zero.
			quit
		}
	}
	
	if $$$ISOK(strStatus) {
		for idClassField = $$$FldINDRPITEMSDispatchPlanning,$$$FldINDRPITEMSSupply,$$$FldINDRPITEMSDistribution {
			if $piece(pobjRecord,Y,idClassField) < 0 {
				set strStatus = $$$MakeStatus("IN00638",$$^WWWFELDNAME(YFORM,"D",idClassField))  ;%1 number of days must be greater than or equal to zero.
				quit
			}
		}
	}
	
	if $$$ISERR(strStatus) {
		$$$DecomposeStatus(strStatus)
		$$$Alert(strStatus)
		set Q = $$$QDontSave
	}
	
	quit

GetProductionTime(YFELD)
	;-------------------------------------------------------------------------------
	; Get Total Production Time (Replacing OnBlur functionality with Core Rules)
	; 
	; Returns: Nothing
	;
	; History:
	; 10-Jun-2009	shobby	SR16534: Created
	;-------------------------------------------------------------------------------
	new fltTotal
	set fltTotal=$$$INDRPITEMSFirm(YFELD)+$$$INDRPITEMSSource(YFELD)+$$$INDRPITEMSRaise(YFELD)+$$$INDRPITEMSPlace(YFELD)+$$$INDRPITEMSConfirm(YFELD)+$$$INDRPITEMSSupplierDays(YFELD)
	if fltTotal=0 set fltTotal=""
	quit fltTotal

GetEntireRunTime(YFELD)
	;-------------------------------------------------------------------------------
	; Get Entire Run Time (Replacing OnBlur functionality with Core Rules)
	; 
	; Returns: Nothing
	;
	; History:
	; 10-Jun-2009	shobby	SR16534: Created
	;------------------------------------------------------------------------------
	new fltTotal
	set fltTotal=$$$INDRPITEMSDispatchPlanning(YFELD)+$$$INDRPITEMSSupply(YFELD)+$$$INDRPITEMSDistribution(YFELD)
	set fltTotal=fltTotal+$$GetProductionTime(YFELD)
	if fltTotal=0 set fltTotal=""
	quit fltTotal
]]></Routine>
</Export>