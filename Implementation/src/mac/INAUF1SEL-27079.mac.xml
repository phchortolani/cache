<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUF1SEL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUF1SEL(EXECUTE="")
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SELEKIEREN INAUF1SEL
	;
	; Inputs : 
	;	EXECUTE		Boolean; 1: foreground, else: background
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 07-Aug-2006	GRF		Doco
	; 21.06.2002	FIS/DT	Created
	;-------------------------------------------------------------------------------
	NEW AUF,AUF1,DATEIXL,ENDE,FERT,KOMM,LFD,LFN,POS,SATZ,SERIE
	new TOTAL,TOTALP,TOTALL,TEIL,TEIL1,TERMIN,TERMINZ,TEXT
 
	IF $GET(EXECUTE)'=1 LOCK +^INPROPJOB(YM):0 IF '$TEST DO  QUIT  ;HINTERGRUND AKTIV ;background ENABLED 
	. DO  ;DELETE EXISTING REQUESTS FOR RECREATION OF INAUF1SEL;FIS;27.04.05;SR12200
	. . NEW NEXT
	. . SET NEXT=""
	. . FOR  SET NEXT=$ORDER(^INARTJOB(YM,NEXT)) QUIT:NEXT=""  DO
	. . . IF $FIND($GET(^INARTJOB(YM,NEXT,1)),"DO ^INAUF1SEL(1)") KILL ^INARTJOB(YM,NEXT)
	. ;
	. SET ^INARTJOB(YM,$$^WWWNEXT("INARTJOB"),1)="DO ^INAUF1SEL(1)"  ;EXECUTE
	
	IF $GET(EXECUTE)'=1 LOCK -^INPROPJOB(YM)
	SET AUF=$ORDER(^INAUF1SEL(YM,""))
	IF AUF'="" IF $PIECE($GET(^INAUF1SEL(YM,AUF,1)),Y,31)=+$HOROLOG IF $PIECE($GET(^INAUF1SEL(YM,AUF,1)),Y,30)>$PIECE($HOROLOG,",",2) QUIT
	SET TOTAL=0   ;AUFTRÄGE
	SET TOTALP=0  ;POSITIONEN
	SET TOTALL=0  ;LEISTUNGEN
	
	KILL ^INAUF1SEL(YM)
	KILL ^INAUF1SELT(YM)
	KILL ^INAUF1SEL3(YM)
	
	SET AUF=""
	FOR  SET AUF=$ORDER(^INAUF1(YM,AUF)) QUIT:AUF=""  DO 
	. SET AUF1=$GET(^INAUF(YM,AUF,1))
	. QUIT:AUF1=""
	. QUIT:$PIECE(AUF1,Y,2)=2  ;LIEFERANTENBESTELLUNG
	. ;
	. SET FERT=""
	. SET POS=""
	. FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS),-1) QUIT:POS=""  DO
	. . ;QUIT:$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,161)=1  ;SCHON GEPLANT
	. . QUIT:$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,9)=1     ;STORNIERT ;TYBD
	. . ;
	. . IF $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,7)=3 DO    ;EIGENFERTIGUNG
	. . . QUIT:$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,9)=1   ;STORNIERT ;TYBD
	. . . QUIT:'$DATA(^INAUFPXL(YM,AUF,POS))      ;KEINE UNTERTEILE  ;FIS, 03.01.03
	. . . QUIT:$DATA(^INAUFPXL(YM,AUF,POS))'=11   ;KEINE UNTERTEILE  ;FIS, 03.01.03
	. . . IF FERT'="" SET FERT=FERT_";"_POS
	. . . IF FERT="" SET FERT=POS
	. . . SET TOTALP=TOTALP+1
	. . . ;
	. . . SET DATEIXL="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS_")"
	. . . FOR  DO  QUIT:DATEIXL=""
	. . . . SET DATEIXL=$QUERY(@DATEIXL)
	. . . . ;IF $PIECE(DATEIXL,",",2)'=AUF SET DATEIXL="" QUIT         ;02.07.2004;FAN;alphanumerische Aufträge ;25888  ;25794
	. . . . IF $TRANSLATE($PIECE(DATEIXL,",",2),"""")'=AUF SET DATEIXL="" QUIT        ;02.07.2004;FAN;alphanumerische Aufträge ;25888  ;25794
	. . . . IF $PIECE(DATEIXL,",",3)'=POS                  SET DATEIXL="" QUIT
	. . . . SET TEIL=$PIECE(DATEIXL,",",4,99)
	. . . . SET TEIL=$TRANSLATE(TEIL,",)","..")
	. . . . SET TEIL1=@DATEIXL
	. . . . ;
	. . . . IF $PIECE(TEIL1,Y,4)=""  QUIT
	. . . . IF $PIECE(TEIL1,Y,26)="" IF $PIECE(TEIL1,Y,4)'=""  SET $PIECE(TEIL1,Y,26)=$PIECE($GET(^INART(YM,$PIECE(TEIL1,Y,4),1)),Y,26)
	. . . . IF $PIECE(TEIL1,Y,26)'=2 QUIT   ;NUR LEISTUNGEN ;only LEISTUNGEN 
	. . . . SET TERMIN=$PIECE(TEIL1,Y,168)  ;FERTIGUNGSTERMIN
	. . . . IF TERMIN=""  SET TERMIN=" "
	. . . . IF +TERMIN'=0 SET ^INAUF1SEL3(YM,TERMIN,AUF,POS,TEIL,1)=""  ;SPEICHERN LEISTUNGEN MIT TERMIN ;Save LEISTUNGEN by means of 
	. . . . ;
	. . . . IF $DATA(^INAUFPTZ(YM,AUF,POS,TEIL))  DO  ;GESPLITTETE LEISTUNGEN;FIS;01.12.03;24419
	. . . . . SET LFD=0
	. . . . . FOR  SET LFD=$ORDER(^INAUFPTZ(YM,AUF,POS,TEIL,LFD)) QUIT:LFD=""  DO
	. . . . . . SET TERMINZ=$PIECE($GET(^INAUFPTZ(YM,AUF,POS,TEIL,LFD,1)),Y,1)
	. . . . . . IF +TERMINZ'=0 SET ^INAUF1SEL3(YM,TERMINZ,AUF,POS,TEIL,1)=""  ;SPEICHERN LEISTUNGEN MIT TERMIN ;Save LEISTUNGEN by means of 
	. . . . ;
	. . . . SET TOTALL=TOTALL+1
	. ;
	. QUIT:FERT=""
	. ;
	. SET TEXT=""
	. DO
	. . IF $PIECE(AUF1,Y,2)=1 SET TEXT=" "_$$^WWWTEXT(32037) QUIT
	. . QUIT:$PIECE(AUF1,Y,1)=""
	. . SET TEXT=" "_$PIECE($GET(^INKUNDE(YM,$PIECE(AUF1,Y,1),1)),Y,8)
	. . SET TEXT=TEXT_", "_$PIECE($GET(^INKUNDE(YM,$PIECE(AUF1,Y,1),1)),Y,16)
	. ;
	. ;SPEICHERN ;Save 
	. ;
	. IF $PIECE(AUF1,Y,8)="" SET $PIECE(AUF1,Y,8)=$PIECE($GET(^INAUFP(YM,AUF,1,1)),Y,56)   ;SERIENNUMMER!!! ERSTE POSITION ;premier 
	. ;
	. SET SATZ=""
	. SET $PIECE(SATZ,Y,1)  = TEXT                ;AUFTRAGSTEXT
	. SET $PIECE(SATZ,Y,2)  = $PIECE(AUF1,Y,2)    ;AUFTRAGSART
	. SET $PIECE(SATZ,Y,3)  = FERT                ;POSITIONEN MIT FERTIGUNGEN ;by means of 
	. SET $PIECE(SATZ,Y,4)  = $PIECE(AUF1,Y,4)    ;AUFTRAGSDATUM
	. SET $PIECE(SATZ,Y,8)  = $PIECE(AUF1,Y,8)    ;PROJEKT ;scheme 
	. SET $PIECE(SATZ,Y,19) = $PIECE(AUF1,Y,19)   ;TERMIN
	. SET $PIECE(SATZ,Y,30) = $PIECE($HOROLOG,",",2)+60  ;KEINE NEUBERECHUNG INNERHALB DER NÄCHSTEN 60 SEKUNDEN ;no inside the 
	. SET $PIECE(SATZ,Y,31) = +$HOROLOG
	. SET ^INAUF1SEL(YM,AUF,1)=SATZ
	. SET TOTAL=TOTAL+1
	
	SET ^INAUF1SELT(YM,YM,1)=TOTAL_Y_TOTALP_Y_TOTALL
	QUIT
	
]]></Routine>
</Export>