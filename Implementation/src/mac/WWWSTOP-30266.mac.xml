<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSTOP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSTOP
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		STOP HTML SEITE
	;
	; Inputs : 
	;
	;
	; ByRef :
	;	SET YNOFOOT=1 ==> KEINE FUSSNOTE
	;
	;
	; Returns :
	;
	;
	; History :
	; 11-Dec-2009	GRF		SR16871: get data once; macros
	; 19-Nov-2009	shobby	SR17032: Disabled 'NumberOfHits' functionality.
	; 07-May-2007	GRF		SR15511: Doco; quits
	; 03-May-2006	Steve S	SR14592: End the div
	; 03.06.1998	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	new idNewForm,objForm,objSystem,strData,strEmail,strKey
	
	set objSystem = $get(^WWW012(0,0,1))
	
	; Display footer?
	;---------------------------------------
	IF $GET(YFORM)="" SET YNOFOOT=1
	IF $GET(YNOFOOT)="" IF $GET(YFORM)'="" DO
	. set objForm   = $get(^WWW120(0,YFORM,1))
	. SET YNOFOOT=1
	. IF $$$WWW120PageEndWithDateAndTime(objForm)=$$$YES      SET YNOFOOT="" QUIT
	. IF $$$WWW120InheritCompanyProperties(objForm)=$$$YES DO
	. . IF $$$WWW012PageEndWithDateAndTime(objSystem)=$$$YES  SET YNOFOOT=""
	
	
	;IF YBEDBER=1 WRITE YCR,"<!-- ************************* FORM END "_$GET(YUSER)_" (WWWSTOP) ************************* -->"

	; FIXME : Doesn't close HTML on early exit - is this right?
	IF $GET(YUSER)'="" IF +$$$WWWUSERHTMLStarted($GET(^WWWUSER(0,YUSER,1)))=$$$NO DO  QUIT     ; *** EARLY EXIT *** 
	. SET $$$WWWUSERHTMLStarted(^WWWUSER(0,YUSER,1))         = 0   ;HTML AUS
	. SET $$$WWWUSERBODYStarted(^WWWUSER(0,YUSER,1))         = 0   ;BODY AUS
	. SET $$$WWWUSERFormStarted(^WWWUSER(0,YUSER,1))         = 0   ;FORMULAR AUS ;form
	. SET $$$WWWUSERFormHeaderDisplayed(^WWWUSER(0,YUSER,1)) = 0   ;KOPF AUS     ;heading 
	. SET $$$WWWUSERLastSave(^WWWUSER(0,YUSER,1))            = ""  ;SAVE 
	. SET YNOFOOT = 1
	
	IF (YFIXHEADER'=1) && ($GET(YNOFOOT)="") DO
	. NEW HELP
	. SET HELP=0
	. IF $FIND(","_$TRANSLATE($$^WWWBEDBER(YBED),";",",")_",",",1,") SET HELP=1
	. ;WRITE "<BR>"
	. WRITE "<FONT SIZE=1>"
	. WRITE "["
	. IF HELP=1 DO  ;FIS; HILFE FÜR ADMIN: ANZEIGE NAME
	. . WRITE "<A HREF=""#"""
	. . WRITE " TITLE="""_YUSER_" ("_$PIECE($GET(^WWW013(0,YBED,1)),Y,1)_")"">"
	. ;
	. WRITE YBED
	. IF HELP=1 WRITE "</A>"
	. WRITE ", "
	. IF HELP=1 DO  ;FIS; HILFE FÜR ADMIN: ANZEIGE TAGESZAHL
	. . WRITE "<A HREF=""#"""
	. . WRITE " TITLE="""_+$HOROLOG_""">"
	. ;
	. WRITE $$^WWWDATE($HOROLOG)
	. IF HELP=1 WRITE "</A>"
	. WRITE ", "
	. WRITE $$^WWWTIME($HOROLOG)
	. WRITE "]["
	. set strEmail = $$$WWW012EMailForHelp(objSystem)
	. IF strEmail'="" DO
	. . WRITE "<A"
	. . WRITE " HREF=""mailto:"
	. . write strEmail
	. . write "?subject=" IF $GET(YFORM)'="" WRITE $$$WWW120FormHeaderOrImageFile(objForm)
	. . WRITE "&body=%0D%0ADate: "_$$^WWWDATE($HOROLOG)
	. . WRITE "%0D%0ATime: "_$$^WWWTIME($HOROLOG)
	. . WRITE "%0D%0AMenu: "_$GET(YANZ)
	. . WRITE "%0D%0AForm: "_$GET(YFORM)
	. . WRITE "%0D%0AYUCI: "_YUCI
	. . WRITE "%0D%0AYBED: "_YBED_" ("_$$$WWW013Name($get(^WWW013(YM,YBED,1)))_")"
	. . WRITE "%0D%0AYUSER: "_YUSER
	. . WRITE "%0D%0AYLOCATION: "_YLOCATION
	. . WRITE "%0D%0AYM: "_YM
	. . WRITE "%0D%0AYKEY: "_$GET(YKEY)
	. . WRITE "%0D%0AAlphalinc Version: "_$$GetVersion^WWWKOPF()
	. . WRITE "%0D%0ACache Version: "_$zv
	. . WRITE "%0D%0AMachine: "_$zu(110)
	. . WRITE "%0D%0AInfo: "_$GET(YERROR)
	. . WRITE """>"
	. . write strEmail
	. . WRITE "</A>"
	. ; 
	. WRITE "]"
	. WRITE "</font>"
	
	WRITE YCR,"</div>"
	WRITE YCR,"</BODY></HTML>",YCR
	
	IF $GET(YUSER)'="" DO
	. IF $GET(YFORM)'="" SET ^WWWPAGE(0,YUSER,YFORM,1) = $GET(YSEITE)_Y_$GET(YINSEITE)  ;SEITENWERTE SPEICHERN ;Save 
	. QUIT:'$DATA(^WWWUSER(0,YUSER))
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,5)  = 0    ;HTML AUS     ; $$$WWWUSERHTMLStarted
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,6)  = 0    ;BODY AUS
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,9)  = 0    ;FORMULAR AUS  ;form
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,10) = 0    ;KOPF AUS      ;heading
	. SET $PIECE(^WWWUSER(0,YUSER,1),Y,22) = ""   ;SAVE
	. LOCK +^WWWSOR(YUSER,"PAGE"):2               ;CHECK AUF SEITE;TYBD;19,12,2003
	. LOCK -^WWWSOR(YUSER,"PAGE")
	. KILL ^WWWSOR(YUSER)
	
	DO CLOSE^WWWSTART
 
	QUIT
	
]]></Routine>
</Export>