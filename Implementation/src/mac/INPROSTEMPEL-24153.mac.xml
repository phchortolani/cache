<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPROSTEMPEL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPROSTEMPEL(YAUF,YPOS,YDATUM,YWIEDER,YFERTNEST,YALLES)        ;INPROSTEMPEL;DT;DRUCKEN DER PRODUKTIONSANWEISUNGEN;06.09.2000
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	; 		Print Production Instructions
	;		DRUCKEN DER PRODUKTIONSANWEISUNGEN
	;	PROGRAMM STEICHER DIE STRUKTUR DATEN UNTER STEMPELNUMMERN INPROSTEMP UND YLFPOS
	;	programme who texture under
	;
	; Inputs : 
	;	YAUF		AUFTRAG                     ; order
	;	YPOS		POSITION                    ; order line
	;	YDATUM		DATUM DER PRODUKTION        ; production date
	;	YWIEDER		1=WIEDERHOLUNG              ; repeat (?)
	;	YFERTNEST	FERTIGUNGSPLÄTZE			; manufacturing place
	;	YALLES		ALLE FERTIGUNGSAUFTRÄGE DRUCKEN ;print all manufacturing orders
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; DT	06.09.2000
	;-------------------------------------------------------------------------------
	new NODRUCK,SUCH,SUCH1,YLFN,YLFNUM,YTEIL,YUEBERT,YUEBERT1,YXL
	
	SET YALLES	  = $GET(YALLES)
	SET YWIEDER   =+$GET(YWIEDER)
	SET YDATUM	  = $GET(YDATUM)
	SET YFERTNEST = $GET(YFERTNEST)
	SET YAUF	  = $GET(YAUF)				QUIT:YAUF=""
	SET YPOS      = $GET(YPOS)
	
	SET YTEIL=$TRANSLATE($PIECE(YPOS,",",2,99),"."_"""",",")  ;PUNKTE UMWANDELN IN KOMMA FÜR INAUFPXL ;transmute within comma to 
	IF $EXTRACT(YTEIL,$LENGTH(YTEIL))="," SET YTEIL=$EXTRACT(YTEIL,1,$LENGTH(YTEIL)-1)  ;LETZES KOMMA ENTFERNEN ;comma eject 
	
	SET YPOS=$PIECE(YPOS,",",1)				QUIT:YPOS=""
	
	SET YFERTNEST=$TRANSLATE($GET(YFERTNEST),";",",")
	
	;+++++++++++++++++++++++++++++++++++++++
	; YXL		objINAUFPXL
	;+++++++++++++++++++++++++++++++++++++++
	
	SET YXL=$GET(^INAUFPXL(YM,YAUF,YPOS))
	IF YTEIL="" SET YLFN=YAUF_","_YPOS DO SAVE(YLFN,YXL) IF YALLES'=1 QUIT  ;GESAMT TEIL/ODER NUR ANWEISUNG ;total only 
	
	SET SUCH="^INAUFPXL("_""""_YM_""""_","_""""_YAUF_""""_","_YPOS_")"
	FOR  DO  QUIT:SUCH=""
	. SET SUCH=$QUERY(@SUCH)
	. IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=YAUF SET SUCH="" QUIT  ;07.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. ;IF $PIECE(SUCH,",",2)'=YAUF SET SUCH="" QUIT  ;07.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. IF $PIECE(SUCH,",",3)=YPOS DO
	. . IF YTEIL'="" IF $EXTRACT($TRANSLATE($PIECE(SUCH,",",4,99),""""),1,$LENGTH(YTEIL))'=YTEIL QUIT  ;NUR DIE UNTERTEILE ;only who 
	. . IF YTEIL'="" IF $EXTRACT($TRANSLATE($PIECE(SUCH,",",4,99),""""),$LENGTH(YTEIL)+1)'="," IF $EXTRACT($TRANSLATE($PIECE(SUCH,",",4,99),""""),$LENGTH(YTEIL)+1)'=")" QUIT
	. . IF YALLES'=1 IF YTEIL'="" IF $LENGTH($TRANSLATE($PIECE(SUCH,",",4,99),""""),",")'=$LENGTH(YTEIL,",") QUIT  ;NUR DIE UNTERTEILE ;only who 
	. . SET YXL = @SUCH
	. . ;-----------------------------------
	. . ;
	. . ;		D162		$$$INVORGPartsParameterDoNotPrint()
	. . ; FIXME : This is based on a user definable class INARTPARA - see PPS tab of General Parameters incl help text
	. . ;         There might not be a code 9 or it might have an opposite description. <GRF>
	. . SET NODRUCK=$PIECE($GET(^INVORG(YM,YM,1)),Y,162)  ;AUS SYSTEMVORGABEN;FIS;23.12.03
	. . IF NODRUCK="" SET NODRUCK=9
	. . ;
	. . ;-----------------------------------
	. . IF $PIECE(YXL,Y,20)=NODRUCK DO  QUIT  ;WENN ALLES DANN NICHT DIE DRUCKEN, DIE EINE 9 IM TEILEPARAMETER ENTHALTEN;TYBD 9.11.2001
	. . . NEW POSP
	. . . SET POSP=$REVERSE($PIECE(SUCH,",",4,99))
	. . . SET $PIECE(POSP,",",1)=99999999_","_$TRANSLATE($PIECE(POSP,",",1),")")
	. . . SET $PIECE(SUCH,",",4,99)=$REVERSE(POSP)_")"
	. . . QUIT
	. . ;
	. . SET YLFN=$PIECE($PIECE(SUCH,")",1),",",2,99)
	. . SET SUCH1=$QUERY(@SUCH)
	. . QUIT:SUCH1=""
	. . QUIT:'$FIND($TRANSLATE(SUCH1,""""),","_$TRANSLATE(YLFN,""""))  ;HAT KEINE UNTERTEILE ;no 
	. . DO SAVE(YLFN,YXL)
	. . QUIT
	. QUIT
	QUIT
	
SAVE(YLFN,YXL)
	;-------------------------------------------------------------------------------
	;  SPEICHERN, UMD DIE REIHEFOLGE DES DRUCKES ZU DREHEN VOM KLEINARTIKEL ZUM GROSSARTIKEL
	;
	; History :
	; 26-Jul-2005	GRF		SR13071 : YLFN contains YAUF,YPOS[,TEIL] - only want
	; 						TEIL for INPROXR.
	; 25-Jul-2005	GRF		SR13071 (& 13024,12968) : Build cross-reference entry
	; 						to allow order details to be retrieved from work order.
	;-------------------------------------------------------------------------------
	new objINPROXR,ok
	
	SET YSORT=$PIECE(YXL,Y,4)  ;ARTIKEL ALS SORT? GGF ARBEITSPLATZ ;item when work station 
	SET YSORT=$LENGTH(YLFN,",")  ;WENN STRUKTURTIEFE ;when 
	SET YSORT=" "
	IF YDATUM'="" QUIT:YDATUM'=$PIECE(YXL,Y,168)   ;PRODUKTIONSBEGINN NICHT RICHTIG ;Not rightly 
	IF YFERTNEST'="" IF $PIECE(YXL,Y,61)="" QUIT   ;KEIN FERTIGUNGSPLATZ ANGEGEBEN ;no 
	IF YFERTNEST'="" IF '$FIND(","_YFERTNEST_",",","_$PIECE(YXL,Y,61)_",") QUIT  ;FALSCHER FERTIGUNGSPLATZ 
	
	;SUCHEN ÜBERTEIL ;seek 
	SET YUEBERT  = ""
	SET YUEBERT1 = $PIECE(YLFN,",",3,99)
	SET YUEBERT1 = $PIECE(YUEBERT1,",",$LENGTH(YUEBERT1,",")-1)
	IF YUEBERT1=""  {
		SET YUEBERT=$PIECE($GET(^INAUFP(YM,YAUF,YPOS,1)),Y,165)  ;LETZTE FERTIGUNG ;last 
	} else {
		SET YUEBERT=$PIECE($GET(^INAUFPT(YM,YAUF,YPOS,$TRANSLATE(YUEBERT1,",","."),1)),Y,165)  ;UEBERGEORDNETE FERTIGUNG
	}
	
	SET YSTEMPEL=$PIECE(YXL,Y,165)  ;ALTE FERTIGUNGSNUMMER
	IF YALLES'=1 IF +YWIEDER=0 IF YSTEMPEL'="" QUIT  ;KEINE WIEDERHOLUNG ; WENN WIEDERHOLUNG DANN ALLE
	IF YSTEMPEL="" SET YSTEMPEL=$$^WWWNEXT("INPROSTEMP")  ;NEXT FERTIGUNGSNUMMER
	if YAUF_","_YPOS=YLFN SET $PIECE(^INAUFP(YM,YAUF,YPOS,1),Y,165)=YSTEMPEL  ;SPEICHERN DER STEMPELNUMMER WENN HAUPTARTIKEL
	
	; SR12968,SR13024,SR13071 build cross-reference from work order to manufacturing order
	set objINPROXR = YAUF_Y_YPOS_Y_$piece(YLFN,$$$COMMA,3,99)
	set ok=$$^WWWSPEI("INPROXR",YSTEMPEL,objINPROXR)
	
	;offene stempelnummern
	IF '$DATA(^INPROSTEMP(YM,YSTEMPEL,1)) IF '$DATA(^INPROSTEMP1(YM,YSTEMPEL,1)) DO
	. SET ^INPROSTEMP(YM,YSTEMPEL,1)=YAUF_Y_$PIECE($GET(^INAUF(YM,YAUF,1)),Y,13)_Y_YPOS_Y_$PIECE(YXL,Y,4)_Y_$PIECE(YXL,Y,1)_Y_$TRANSLATE(YLFN,"""")_Y_$PIECE(YXL,Y,168)_Y_Y_Y_Y_Y_Y_Y_Y_Y_Y_Y_Y_Y_$PIECE(YXL,Y,61)_Y_$PIECE(YXL,Y,5)_Y_Y_YUEBERT  ;FIS;YLFN OHNE ";25927;25.06.04
	. ;SET $PIECE(^INPROSTEMP(YM,YSTEMPEL,1),Y,1)=YAUF
	. ;SET $PIECE(^INPROSTEMP(YM,YSTEMPEL,1),Y,3)=YPOS
	. ;SET $PIECE(^INPROSTEMP(YM,YSTEMPEL,1),Y,6)=YLFN
	. ; note the following lines overwrite fields set above (#61 & #5 respectively)
	. SET $PIECE(^INPROSTEMP(YM,YSTEMPEL,1),Y,21)=$PIECE(YXL,Y,39)   ;FERTIGUNGSMENGE
	. SET $PIECE(^INPROSTEMP(YM,YSTEMPEL,1),Y,22)=$PIECE(YXL,Y,245)  ;LAGERBERICHTIGUNG
	. QUIT
	
	IF $PIECE(^INAUFP(YM,YAUF,YPOS,1),Y,83)="" DO
	. SET $PIECE(^INAUFP(YM,YAUF,YPOS,1),Y,83)=+$HOROLOG
	. SET $PIECE(^INAUFP(YM,YAUF,YPOS,1),Y,84)=$PIECE($HOROLOG,",",2)
	. SET $PIECE(^INAUFP(YM,YAUF,YPOS,1),Y,85)=YBED
	. QUIT
	
	IF $GET(YSORT)="" SET YSORT=" "  ;ARTIKELNUMMER
	SET ^WWWSOR(YUSER,YSORT,YSTEMPEL)=YSTEMPEL  ;DRUCKSORTIERUNG
	QUIT 
		
]]></Routine>
</Export>