<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPT
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ZURUECKSPEICHERN DER ÄNDERUNGEN/LÖSCHUNG DER BAUMSTRUKTUR
	;		Record changes and deletions to the parts tree structure (?)
	;
	; Updates : ^INAUF1SEL3, ^WWW126, ^INAUFPTZ, ^INAUF, ^INAUFP, ^INAUFPT, ^INAUFPXL
	; 			^INDRPNETCHANGE, ^INDRPDEMAND
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 28-Sep-2006	GRF		Doco; quits; single call using NoOfLots=$$^INPROLOS; !=>||
	; 29.09.2000	DT
	;-------------------------------------------------------------------------------
	
SET
	;-------------------------------------------------------------------------------
	;SPEICHERN DATEN ;Save 
	;TERMINÄNDERUNGEN WERDEN NUR BERÜCKSICHTIGT, WENN DER TERMIN EINER LEISTUNG GEÄNDERT WIRD
	;-------------------------------------------------------------------------------
	NEW ART,AUF,BETRIEB,DATEI,DRP,LENGTH,OBER,OTEIL,POS,SATZ,SUCH,TEILE,TEILSTART,TEILX,UNTER,YI
	
	;+++++++++++++++++++++++++++++++++++++++
	;	SATZ		objINDRPDEMAND
	;	@SATZ		objINAUFPXL
	;	@OBER		objINAUFPXL
	;	@UNTER		objINAUFPXL
	;	@DATEI		objINAUFPXL
	;	@SUCH		objINAUFPXL
	;+++++++++++++++++++++++++++++++++++++++
	
	QUIT:$GET(YKEY)=""
	QUIT:$GET(YFELD)="" 
	SET AUF=$PIECE(YKEY,",",1)         QUIT:AUF=""
	SET POS=$PIECE(YKEY,",",2)         QUIT:POS=""
	SET ^WWW126(YM,"INAUFP",YBED,1,1)=AUF
	SET ^WWW126(YM,"INAUFP",YBED,2,1)=POS
	SET TEILE=$PIECE(YKEY,",",3)       QUIT:TEILE=""
	
	SET BETRIEB=$PIECE($GET(^INAUF(YM,AUF,1)),Y,6)
	IF BETRIEB="" SET BETRIEB=$GET(YLOCATION)  ;BETRIEB
	IF $PIECE(YFELD,Y,57)'="" DO               ;MASCHINE     ;machine 
	. QUIT:$PIECE(YFELD,Y,61)'=""              ;SCHON BELEGT ;already occupied 
	. IF $PIECE($GET(^INMASCH(YM,$PIECE(YFELD,Y,57),1)),Y,9)'="" DO  ;FERTIGUNGSNEST
	. . SET $PIECE(YFELD,Y,61)=$PIECE($GET(^INMASCH(YM,$PIECE(YFELD,Y,57),1)),Y,9)  ;VORGABE   ;default 
	
	;NEUER FERTIGUNGSTERMIN
	;IF $PIECE(YFELD,Y,168)'="" DO
	DO
	. NEW SATZ,TERMINALT
	. QUIT:$PIECE(YFELD,Y,26)'=2
	. SET SATZ="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. FOR YI=1:1 QUIT:$PIECE(TEILE,".",YI)=""  SET SATZ=SATZ_","_$PIECE(TEILE,".",YI)
	. SET SATZ=SATZ_")"
	. SET TERMINALT=$PIECE(@SATZ,Y,168)
	. IF $PIECE(YFELD,Y,168)'=TERMINALT DO
	. . IF $GET(YFORM)="INAUFPT" DO ^WWWSKILL("INAUFPTZ",AUF_","_POS_","_TEILE)  ;LÖSCHEN STARTZEITEN FÜR FEINPLANUNG;FIS;29.09.03;24108
	. . IF $PIECE(YFELD,Y,168)="" SET $PIECE(YFELD,Y,194)="" QUIT  ;TERMIN GELÖSCHT
	. . SET $PIECE(YFELD,Y,194)=0     ;TERMIN MANUELL EINGETRAGEN ;regd. 
	. . SET $PIECE(YFELD,Y,199)=YBED  ;DURCH MITARBEITER ;trans- 
	. . ;
	. . ;ÜBERTRAGEN TERMIN NACH INAUF1SEL3;FIS;01.12.03;24419
	. . IF TERMINALT'="" IF $DATA(^INAUF1SEL3(YM,TERMINALT,AUF,POS,TEILE)) KILL ^INAUF1SEL3(YM,TERMINALT,AUF,POS,TEILE)
	. . IF $PIECE(YFELD,Y,168)'="" SET ^INAUF1SEL3(YM,$PIECE(YFELD,Y,168),AUF,POS,TEILE,1)=""
	
	;ÜBERTRAG FERTIGUNGSTERMIN IN ÜBERGEORDNETEN ARTIKEL ;within item 
	IF $PIECE(YFELD,Y,26)=2 DO  ;NUR BEI LEISTUNGEN ;only next to LEISTUNGEN 
	. SET DRP=""
	. SET TEILSTART=$PIECE(YFELD,Y,168)
	. SET OBER=""
	. SET OBER="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. FOR YI=1:1 QUIT:$PIECE(TEILE,".",YI+1)=""  SET OBER=OBER_","_$PIECE(TEILE,".",YI)  ;ÜBERGEORDNETER KEY
	. SET OBER=OBER_")"
	. QUIT:'$DATA(OBER)
	. ;
	. SET LENGTH=$QLENGTH(OBER)
	. SET OTEIL=$PIECE($PIECE($PIECE(OBER,"(",2),")",1),",",3,LENGTH)  ;NUR TEILEKEY ;only 
	. SET UNTER=OBER
	. FOR  DO  QUIT:UNTER=""
	. . SET UNTER=$QUERY(@UNTER)
	. . ;IF $PIECE(UNTER,",",2)'=AUF SET UNTER="" QUIT  ;02.07.2004;FAN;alphanumerische Aufträge ;25888  ;25794
	. . IF $TRANSLATE($PIECE(UNTER,",",2),"""")'=AUF SET UNTER="" QUIT  ;02.07.2004;FAN;alphanumerische Aufträge ;25888  ;25794
	. . IF $PIECE(UNTER,",",3)'=POS SET UNTER="" QUIT  ;FALSCHE POSITION
	. . IF $PIECE($PIECE($PIECE(UNTER,")",1),"(",2),",",3,LENGTH)'=OTEIL SET UNTER="" QUIT  ;FALSCHES OBERTEIL
	. . IF $QLENGTH(UNTER)>(LENGTH+1) QUIT  ;UNTER UNTERTEIL (FALSCHE EBENE) ;under 
	. . IF $PIECE($PIECE(UNTER,")",1),",",4,LENGTH+1)_","=$TRANSLATE(TEILE,".",",") QUIT  ;EIGENER DATENSATZ ;data record 
	. . IF $PIECE(@(UNTER),Y,26)'=2   QUIT  ;KEINE LEISTUNG ;no performance 
	. . IF $PIECE(@(UNTER),Y,200)'="" SET DRP($PIECE(@(UNTER),Y,200))=""  ;DEMANDNUMMER SPEICHERN (S.UNTEN) ;Save 
	. . IF $PIECE(@(UNTER),Y,168)'="" DO
	. . . IF TEILSTART=""                     SET TEILSTART=$PIECE(@(UNTER),Y,168) QUIT
	. . . IF $PIECE(@(UNTER),Y,168)<TEILSTART SET TEILSTART=$PIECE(@(UNTER),Y,168)
	. ;
	. DO  ;TERMIN ÜBERTRAGEN ;transport 
	. . NEW PARENT,YI
	. . SET $PIECE(@(OBER),Y,168)=TEILSTART
	. . IF $PIECE(TEILE,".",2)="" QUIT  ;KEIN OBERTEIL ;no 
	. . SET PARENT="^INAUFPT("_""""_YM_""""_","_""""_AUF_""""_","_POS_","_""""
	. . FOR YI=1:1 QUIT:$PIECE(TEILE,".",YI+1)=""  SET PARENT=PARENT_$PIECE(TEILE,".",YI)_"."  ;ÜBERGEORDNETER KEY
	. . SET PARENT=PARENT_""""_",1)"
	. . SET $PIECE(@(PARENT),Y,168)=TEILSTART
	. . ;DO TEILEKILL
	. ;
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,70)=1 DO  ;ÄNDERUNG IM DRP ;alteration DRP 
	. . QUIT:BETRIEB=""
	. . IF $PIECE(@(OBER),Y,200)'="" SET DRP($PIECE(@(OBER),Y,200))=""
	. . SET ID=""
	. . FOR  SET ID=$ORDER(DRP(ID)) QUIT:ID=""  DO
	. . . NEW SATZ,YFORM,YVOR,YOK
	. . . SET SATZ=$GET(^INDRPDEMAND(YM,BETRIEB,ID,1))
	. . . QUIT:SATZ=""
	. . . QUIT:$PIECE(SATZ,Y,98)=1  ;GELÖSCHT 
	. . . QUIT:$PIECE(SATZ,Y,99)=1  ;ERLEDIGT
	. . . SET $PIECE(SATZ,Y,6)=TEILSTART
	. . . SET YOK=$$^WWWSPEI("INDRPDEMAND",BETRIEB_","_ID,SATZ,1)
	. . . ;DO ^WWWSKILL("INDRPDEMAND",BETRIEB_","_ID,1)  ;LÖSCHEN SORTKEY
	. . . ;SET $PIECE(^INDRPDEMAND(YM,BETRIEB,ID,1),Y,6)=TEILSTART
	. . . ;DO ^WWWSSORT("INDRPDEMAND",BETRIEB_","_ID)    ;AUFBEREITEN SORTKEY
	
	;BEARBEITEN
	IF $PIECE(YFELD,Y,7)=1!($PIECE(YFELD,Y,7)=2) DO   ;BESTELLEN ODER LAGER ;book Or stock location 
	. IF $PIECE(YFELD,Y,7)=1 IF $PIECE(YFELD,Y,188)'="" QUIT  ;BEREITS ÜBERTRAGEN;FIS;06.05.04 ;yet transport 
	. NEW YAUFTRAG,YPOS,YOK,YARTIKEL,YMENGE,YI,YQ,YAUFTRAG1,YARTIKEL
	. SET YAUFTRAG=$PIECE($GET(^INAUF(YM,AUF,1)),Y,188)  ;AUFTRAGSPOSITION
	. SET YARTIKEL=$PIECE(YFELD,Y,4)
	. IF YARTIKEL="" QUIT
	. IF $PIECE(YFELD,Y,7)=1 DO
	. . IF +$TRANSLATE(YAUFTRAG,";")=0 SET YAUFTRAG=$$^INAUFCOPY(AUF) QUIT  ;NEUR AUFTRAG AUS ALTEM AUFTRAG WENN BESTELLUNG
	. . IF $ORDER(^INARTK(YM,YARTIKEL,""))="" SET YAUFTRAG=$$^INAUFCOPY(AUF) QUIT  ;UNBEKANNTER LIEFERANT ;supplier 
	. . SET YQ=0
	. . FOR YI=1:1  QUIT:$PIECE(YAUFTRAG,";",YI,999)=""  SET YAUFTRAG1=$PIECE(YAUFTRAG,";",YI) DO  QUIT:YQ=1
	. . . QUIT:YAUFTRAG1=""
	. . . IF $ORDER(^INARTK(YM,YARTIKEL,""))=$PIECE($GET(^INAUFP(YM,YAUFTRAG1,1,1)),Y,12) SET YAUFTRAG=YAUFTRAG1 SET YQ=1  ;GLEICHER LIEFERANT ;same supplier 
	. . ;
	. . IF YQ=0 SET YAUFTRAG=$$^INAUFCOPY(AUF) QUIT  ;ANDERER LIEFERANT ;other supplier 
	. ;
	. ;QUIT:YAUFTRAG=""  ;KEINE NEUE AUFTRAGSNUMMER
	. IF $PIECE(YFELD,Y,7)=1 QUIT:YAUFTRAG=""  ;KEINE NEUE AUFTRAGSNUMMER ;no 
	. SET YARTIKEL=$PIECE(YFELD,Y,4)
	. SET YMENGE=$PIECE(YFELD,Y,39)
	. IF +YMENGE=0 SET YMENGE=$PIECE(YFELD,Y,5)
	. IF $PIECE(YFELD,Y,40)=2 IF $PIECE(YFELD,Y,45)'="" SET YMENGE=$PIECE(YFELD,Y,45)\60\60
	. IF $PIECE(YFELD,Y,40)=10 IF $PIECE(YFELD,Y,45)'="" SET YMENGE=$PIECE(YFELD,Y,45)\60
	. IF $PIECE(YFELD,Y,40)=11 IF $PIECE(YFELD,Y,45)'="" SET YMENGE=$PIECE(YFELD,Y,45)\100
	. ;
	. ;NUR NEUANLAGE BEI BESTELLUNG ;only next to sales order 
	. IF $PIECE(YFELD,Y,7)=1 SET YPOS=$$^INAUFPOS(YAUFTRAG,YARTIKEL,YMENGE,$PIECE(YFELD,Y,7))  ;ANLEGEN NEUE AUFTRAGSPOSITION MIT WH ;put onto by means of 
	. IF $PIECE(YFELD,Y,7)=1 IF YPOS'="" SET $PIECE(YFELD,Y,189)=YPOS  ;NEUE POSITIONSNUMMER DER BESTELLUNG ;the sales order 
	. IF $PIECE(YFELD,Y,7)=1 SET $PIECE(YFELD,Y,188)=YAUFTRAG  ;WENN BESTELLUNG ;when sales order 
	. ;
	. SET DATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. FOR YI=1:1 QUIT:$PIECE(TEILE,".",YI)=""  SET DATEI=DATEI_","_$PIECE(TEILE,".",YI)
	. SET DATEI=DATEI_")"
	. ;
	. IF $PIECE(YFELD,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=1 SET ^INDRPNETCHANGE(YM,$PIECE(YFELD,Y,4),1)=""  ;FIS;29.04.05;SR12200
	. ;
	. ;BEDARF AUS DRP LÖSCHEN (DEMANDS DER WEITEREN UNTERTEILE) ;out of DRP Delete the 
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,70)=1 DO
	. . NEW LENGTH,DATEI1,DEMAND
	. . QUIT:BETRIEB=""
	. . SET SUCH=DATEI
	. . FOR  DO  QUIT:SUCH=""
	. . . SET SUCH=$QUERY(@SUCH)
	. . . IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. . . ;IF $PIECE(SUCH,",",2)'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794;  ;FALSCHER AUFTRAG
	. . . IF $PIECE(SUCH,",",3)'=POS SET SUCH="" QUIT  ;FALSCHE POSITION
	. . . SET LENGTH=$QLENGTH(DATEI)
	. . . SET DATEI1=$PIECE($PIECE($PIECE(DATEI,"(",2),")",1),",",3,LENGTH)  ;NUR TEILEKEY ;only 
	. . . IF $PIECE($PIECE($PIECE(SUCH,")",1),"(",2),",",3,LENGTH)'=DATEI1 SET SUCH="" QUIT  ;FALSCHES TEIL ;part 
	. . . IF $PIECE(@SUCH,Y,200)'="" DO
	. . . . NEW SATZ,YFORM,YVOR,YOK
	. . . . SET DEMAND=$PIECE(@SUCH,Y,200)
	. . . . QUIT:DEMAND=""
	. . . . SET SATZ=$GET(^INDRPDEMAND(YM,BETRIEB,DEMAND,1))
	. . . . QUIT:SATZ=""
	. . . . SET $PIECE(SATZ,Y,98)=1  ;VORGANG GELÖSCHT
	. . . . SET $PIECE(SATZ,Y,72)=""  ;TEILE-EINTRAG
	. . . . SET $PIECE(SATZ,Y,115)=$$^WWWTEXT(33692)  ;GELÖSCHT DURCH FESTLEGEN WH ;trans- 
	. . . . SET YOK=$$^WWWSPEI("INDRPDEMAND",BETRIEB_","_DEMAND,SATZ,1)  ;FIS;04.12.03
	. . . . ;DO ^WWWSKILL("INDRPDEMAND",BETRIEB_","_DEMAND,1)  ;LÖSCHEN SORTKEY
	. . . . ;SET $PIECE(^INDRPDEMAND(YM,BETRIEB,DEMAND,1),Y,98)=1  ;VORGANG GELÖSCHT
	. . . . ;SET $PIECE(^INDRPDEMAND(YM,BETRIEB,DEMAND,1),Y,72)=""  ;TEILE-EINTRAG
	. . . . ;DO ^WWWSSORT("INDRPDEMAND",BETRIEB_","_DEMAND)  ;AUFBEREITEN SORTKEY
	. . . ;
	. . . IF $PIECE(@SUCH,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=1 SET ^INDRPNETCHANGE(YM,$PIECE(@SUCH,Y,4),1)=""  ;FIS;29.04.05;SR12200
	. ;
	. ;LÖSCHEN SERIENNUMMERN-ZUORDNUNG;24854;FIS;16.01.04
	. IF $DATA(^INAUFPTSN(YM,AUF,POS,TEILE)) DO ^WWWSKILL("INAUFPTSN",AUF_","_POS_","_TEILE)
	. SET TEILX=TEILE FOR  SET TEILX=$ORDER(^INAUFPTSN(YM,AUF,POS,TEILX)) QUIT:TEILX=""  QUIT:$EXTRACT(TEILX,1,$LENGTH(TEILE))'=TEILE  DO
	. . DO ^WWWSKILL("INAUFPTSN",AUF_","_POS_","_TEILX)
	. ;
	. IF $PIECE(YFELD,Y,7)=1 QUIT:$PIECE($GET(^INVORG(YM,YM,1)),Y,69)=1  ;UNTERTEILESTRUKTUR BEHALTEN
	. KILL @(DATEI)  ;LÖSCHEN DATEN TEILESTUKTUR MIT UNTERSTUKTUR ;Delete by means of 
	. SET ^INAUFPXL(YM,AUF,POS,TEILE)=YFELD  ;RÜCKSICHERN DES HAUPTTEILS TEILE
	. DO TEILEKILL   ;LÖSCHEN UNTERTEILE DER UNTERTEILE, DA ANDERE WH
	. SET ^INAUFPT(YM,AUF,POS,TEILE,1)=YFELD  ;RÜCKSICHERN DES HAUPTTEILS TEILE
	
	IF ($PIECE(YFELD,Y,40)=2) || ($PIECE(YFELD,Y,40)=10) || ($PIECE(YFELD,Y,40)=11) {
		;NEUE PREIS ERRECHNUNG INKL VERSCHNITT ;new price calculation for one product
		SET $PIECE(YFELD,Y,47)=$$^INARTKOSTT($PIECE(YFELD,Y,4),$PIECE(YFELD,Y,5),$PIECE(YFELD,Y,45),$PIECE(YFELD,Y,43))
	}
	DO ^WWWSPEI("INAUFPT",YKEY,YFELD,1)
	SET DATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	FOR YI=1:1 QUIT:$PIECE(TEILE,".",YI)=""  SET DATEI=DATEI_","_$PIECE(TEILE,".",YI)
	SET DATEI=DATEI_")"
	SET @(DATEI)=YFELD                         ;SPEICHERN DATEN ;Save 
	SET $PIECE(^INAUF(YM,AUF,1),Y,190)=1       ;BEARBEITET
	SET $PIECE(^INAUFP(YM,AUF,POS,1),Y,190)=1  ;BEARBEITET
	DO  ;NEURECHNEN HERSTELLKOSTEN;FIS;13.09.04;26268
	. NEW EXTRA,WERKZEUG,NoOfLots
	. SET EXTRA   =$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",2)),Y,74)
	. SET WERKZEUG=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",2)),Y,75)
	. IF +$PIECE(YFELD,Y,74)=+EXTRA IF +$PIECE(YFELD,Y,75)=+WERKZEUG QUIT
	. ;DO ^INAUFHKNEU(AUF,POS)
	. set NoOfLots = $$^INPROLOS($PIECE(YFELD,Y,4),$PIECE(YFELD,Y,5))                                         ; 28-Sep-2006 vvv
	. SET $PIECE(YFELD,Y,47)=$PIECE(YFELD,Y,47)-(EXTRA*NoOfLots)-(WERKZEUG*NoOfLots)
	. SET $PIECE(YFELD,Y,47)=$PIECE(YFELD,Y,47)+($PIECE(YFELD,Y,74)*NoOfLots)+($PIECE(YFELD,Y,75)*NoOfLots)   ; 28-Sep-2006 ^^^
	. SET $PIECE(YFELD,Y,47)=$JUSTIFY($PIECE(YFELD,Y,47),0,2)
	. SET $PIECE(@DATEI,Y,47)=$PIECE(YFELD,Y,47)
	. SET $PIECE(^INAUFPT(YM,AUF,POS,TEILE,1),Y,47)=$PIECE(YFELD,Y,47)  ;RÜCKSICHERN DES HAUPTTEILS TEILE
	
	QUIT
	
KILL ;LÖSCHEN DATEN   ;Delete 
	NEW YI,AUF,POS,TEILE,DATEI,BETRIEB
	QUIT:$GET(YKEY)=""
	QUIT:$GET(YFELD)=""
	SET AUF   = $PIECE(YKEY,",",1)     QUIT:AUF=""
	SET POS   = $PIECE(YKEY,",",2)     QUIT:POS=""
	SET TEILE = $PIECE(YKEY,",",3)
	
	SET BETRIEB=$PIECE($GET(^INAUF(YM,AUF,1)),Y,6)
	IF BETRIEB="" SET BETRIEB=$GET(YLOCATION)  ;BETRIEB
	SET DATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	FOR YI=1:1 QUIT:$PIECE(TEILE,".",YI)=""  SET DATEI=DATEI_","_$PIECE(TEILE,".",YI)
	SET DATEI=DATEI_")"
	
	;BEDARF AUS DRP LÖSCHEN (DEMANDS DER WEITEREN UNTERTEILE UND EIGENER DEMAND) ;out of DRP Delete the And 
	IF $PIECE($GET(^INVORG(YM,YM,1)),Y,70)=1 DO
	. NEW LENGTH,DATEI1,DEMAND
	. QUIT:BETRIEB=""
	. SET SUCH=DATEI
	. FOR  DO  QUIT:SUCH=""
	. . SET SUCH=$QUERY(@SUCH)
	. . IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. . ;IF $PIECE(SUCH,",",2)'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794;  ;FALSCHER AUFTRAG
	. . IF $PIECE(SUCH,",",3)'=POS SET SUCH="" QUIT  ;FALSCHE POSITION
	. . SET LENGTH=$QLENGTH(DATEI)
	. . SET DATEI1=$PIECE($PIECE($PIECE(DATEI,"(",2),")",1),",",3,LENGTH)                    ;NUR TEILEKEY ;only 
	. . IF $PIECE($PIECE($PIECE(SUCH,")",1),"(",2),",",3,LENGTH)'=DATEI1 SET SUCH="" QUIT    ;FALSCHES TEIL ;part 
	. . IF $PIECE(@SUCH,Y,200)'="" DO  ;UNTERTEILE
	. . . NEW SATZ,YFORM,YVOR,YOK
	. . . SET DEMAND=$PIECE(@SUCH,Y,200)
	. . . QUIT:DEMAND=""
	. . . SET SATZ=$GET(^INDRPDEMAND(YM,BETRIEB,DEMAND,1))
	. . . QUIT:SATZ=""
	. . . ;
	. . . SET $PIECE(SATZ,Y,98)=1  ;VORGANG GELÖSCHT
	. . . SET $PIECE(SATZ,Y,72)=""  ;TEILE-EINTRAG
	. . . SET $PIECE(SATZ,Y,115)=$$^WWWTEXT(33693)  ;UNTERTEIL AUS TEILESTRUKTUR GELÖSCHT ;out of 
	. . . SET YOK=$$^WWWSPEI("INDRPDEMAND",BETRIEB_","_DEMAND,SATZ,1)  ;FIS;04.12.03
	. . . ;DO ^WWWSKILL("INDRPDEMAND",BETRIEB_","_DEMAND,1)  ;LÖSCHEN SORTKEY
	. . . ;SET $PIECE(^INDRPDEMAND(YM,BETRIEB,DEMAND,1),Y,98)=1  ;VORGANG GELÖSCHT
	. . . ;DO ^WWWSSORT("INDRPDEMAND",BETRIEB_","_DEMAND)  ;AUFBEREITEN SORTKEY
	. . ;
	. . IF $PIECE(@SUCH,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=1 SET ^INDRPNETCHANGE(YM,$PIECE(@SUCH,Y,4),1)=""  ;FIS;29.04.05;SR12200
	. ;
	. IF $PIECE(@DATEI,Y,200)'="" DO  ;HAUPTTEIL/TEIL SELBST ;self 
	. . NEW SATZ,YFORM,YVOR,YOK
	. . SET DEMAND=$PIECE(@DATEI,Y,200)
	. . QUIT:DEMAND=""
	. . SET SATZ=$GET(^INDRPDEMAND(YM,BETRIEB,DEMAND,1))
	. . QUIT:SATZ=""
	. . ;
	. . SET $PIECE(SATZ,Y,98)=1  ;VORGANG GELÖSCHT
	. . SET $PIECE(SATZ,Y,72)=""  ;TEILE-EINTRAG
	. . SET $PIECE(SATZ,Y,115)=$$^WWWTEXT(33693)  ;UNTERTEIL AUS TEILESTRUKTUR GELÖSCHT ;out of 
	. . SET YOK=$$^WWWSPEI("INDRPDEMAND",BETRIEB_","_DEMAND,SATZ,1)  ;FIS;04.12.03
	. . ;DO ^WWWSKILL("INDRPDEMAND",BETRIEB_","_DEMAND,1)       ;LÖSCHEN SORTKEY
	. . ;SET $PIECE(^INDRPDEMAND(YM,BETRIEB,DEMAND,1),Y,98)=1   ;VORGANG GELÖSCHT
	. . ;SET $PIECE(^INDRPDEMAND(YM,BETRIEB,DEMAND,1),Y,72)=""  ;TEILE EINTRAG
	. . ;DO ^WWWSSORT("INDRPDEMAND",BETRIEB_","_DEMAND)         ;AUFBEREITEN SORTKEY
	
	IF $PIECE(YFELD,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=1 SET ^INDRPNETCHANGE(YM,$PIECE(YFELD,Y,4),1)=""  ;FIS;29.04.05;SR12200
	KILL @(DATEI)                    ;LÖSCHEN DATEN TEILESTUKTUR ;Delete 
	KILL ^INAUFPT(YM,AUF,POS,TEILE)  ;ZWISCHENDATEI
	DO TEILEKILL
	QUIT
	
TEILEKILL ;NEUAUFBAU DER TEILE ;the 
	NEW YAUFTRAG,YPOS,SATZ
	
	SET YAUFTRAG=AUF
	SET YPOS=POS
	SET SATZ=$GET(^INAUFP(YM,YAUFTRAG,YPOS,1))
	DO ^INAUFTEILE(YAUFTRAG,YPOS)  ;NEUAUFBAU DER POSITIONSTEILE ;the 
	QUIT
]]></Routine>
</Export>