<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWVAR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWVAR
	
#include COMSYS
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWVAR("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LADEN VARIABLEN
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 03-Jan-2014 	shobby 	CORE-303: CacheIndex
	; 18-Dec-2013	shobby	CORE-291: YUSENAME
	; 22-May-2013	shobby	CORE-107.1: Additional YPOPUP so that Menu is not shown on a search function within a popup.
	; 27-Jul-2012	SCR		SR18070: Record IP Address
	; 06-Aug-2010	shobby	SR17488: call to SET^WWWUSERAGENT
	; 27-Jul-2010	shobby	SR17457: YUSERAGENT if form is a popup window.
	; 18-Nov-2009	GRF		SR16871: Default to top level location rather than first
	; 							in order; clear out old comment code blocks
	; 21-Oct-2009	shobby	SR16948: Retrieve value of original URL (YXURL) for
	; 							storage in CacheTempURL
	; 02-Mar-2009	GRF		SR16065: Reverted change - breaks Help button
	; 27-Feb-2009	FIS		SR16065: force new YUSER after Cache re-start
	; 15-Jan-2009	GRF		SR15853: Pass audit string to NewUserSession
	; 20-Oct-2008	FIS		SR15824: pass flag YLOCKKILL to kill any lock during
	; 							form load
	; 19-Aug-2008	FIS		SR15853: create session in common routine
	; 01-Nov-2007	SCR		SR15606: Moved Session creation to a function.
	; 26-Oct-2007	SCR		SR15606: Added new Session Variable/Object %alSession
	; 							used for V2 code
	; 28-Jun-2006	GRF		Naked reference; quits; Doco; !=>||
	; 10-Nov-2005	JW		SR13817: Enable child users
	; 30-May-2005	RobertW	SR12056: Attempt at Performance Increase
	; 25.11.2004	FIS		BROWSER TYP
	; 08.04.2004	FIS		25459;IMMER ALLE VARIABLEN LADEN
	; 10.03.2004	FIS		CUSTOMIZING;25301
	; 03.06.1998	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	do ^WWWVORG                              ; set standard variables
	new idCountry,tmsNow,dteNow,tmeNow
	
	set tmsNow = $horolog
	set dteNow = +tmsNow
	set tmeNow = $piece(tmsNow,$$$COMMA,2)
	
	SET YBED  = $GET(%(YQUERY,"YBED"))   ;BEDIENER
	SET YUSER = $GET(%(YQUERY,"YUSER"))  ;USER NUMMER ;numeral 
	SET YPWD  = $GET(%(YQUERY,"YPWD"))   ;PASSWORD
	SET YXURL = $GET(%(YQUERY,"YXURL"))  ;SR16948 (required)
	set YPOPUP= $get(%(YQUERY,"YPOPUP")) ;CORE-107.1
		
	IF $GET(%("env","REMOTE_PASSWD"))'="" SET YPWD=$GET(%("env","REMOTE_USER"))
	
	
	; FIXME:
	; For appropriate license count the User should request a new Session Id (YUSER) after Cache restart.
	; Because not all SessionId's are logged in CacheTemp (child users !) this test causes trouble in sub-windows (endless loop)
	;if ((YUSER '= "") && ('$data(^CacheTempSession(YUSER)))) set YUSER = ""  //after Cache re-start SR16065 - reverted
	
	set YBED=$$$UPPER(YBED)
	if YBED="" {
		set YBED="UNKNOWN"
		set YUSER = $$NewUserSession^WWWUSER("WWWVAR:1")
		set %(YQUERY,"YUSER")=YUSER
		set ^WWWUSER(0,YUSER,1)=Y_YBED
	}
	
	IF YBED="" SET YBED = "^^^"
	
	;+++++++++++++++++++++++++++++++++++++++
	;  YBEX		objWWW013		personnel
	;  YUSER1	objWWWUSER
	;  YVOR1	objWWW012
	;+++++++++++++++++++++++++++++++++++++++
	
	IF $GET(YUSER)="" IF YBED'="" DO  ;NEUER USER ; new user
	. set YUSER = $$NewUserSession^WWWUSER("WWWVAR:2")
	. SET %(YQUERY,"YUSER")=YUSER
	. SET ^WWWLOGIN(0,dteNow,tmeNow,YBED,1)=YUSER_Y_$$IPAddress()   ;LOGIN FILE ;SR18070
	. SET ^WWWUSER(0,YUSER,1)=$zconvert($GET(YPWD),"U")_Y_YBED_Y_dteNow_Y_tmeNow
	. IF $DATA(^WWW013(0,YBED,1)) DO
	. . NEW YBEX,YDH
	. . SET YBEX = ^WWW013(0,YBED,1)
	. . SET $PIECE(^WWWUSER(0,YUSER,1),Y,20) = $PIECE(YBEX,Y,5)
	. . SET $PIECE(^WWWUSER(0,YUSER,1),Y,19) = $$^WWWLANGU(YBED)
	. . SET $PIECE(^WWWUSER(0,YUSER,1),Y,35) = $$^WWWUSERAGENT()  ; BROWSER TYPE (MSIE, NETSCAPE, ...)
	. . SET YMENU = $PIECE(YBEX,Y,11)
	. . SET YMFA  = $PIECE(YBEX,Y,10)
	. . SET YA    = $PIECE(YBEX,Y,13)+1
	. . ;
	. . ; FIXME : usage log should not kept withing employee master data. Should move to separate structure. <GRF>
	. . ;
	. . SET $PIECE(YBEX,Y,13) = YA
	. . ;  "Your Visit No.: ##,  Last Visit: ##/##/##,##:##:##"
	. . SET $PIECE(YBEX,Y,16) = $$^WWWTEXT(290)_" "_YA_",  "_$$^WWWTEXT(291)_" "_$$^WWWDATE($PIECE(YBEX,Y,14))_", "_$$^WWWTIME($PIECE(YBEX,Y,15))
	. . SET $PIECE(YBEX,Y,14) = dteNow
	. . SET $PIECE(YBEX,Y,15) = tmeNow
	. . SET ^WWW013(0,YBED,1) = YBEX
	. . SET YDH = $ORDER(^WWWZWS(0,""))    IF YDH'="" IF +YDH'=dteNow KILL ^WWWZWS(0,YDH)
	. . SET YDH = $ORDER(^WWWZWS(0,""),-1) IF YDH'="" IF +YDH'=dteNow KILL ^WWWZWS(0,YDH)
	. ;
	. SET YA=""
	. FOR  SET YA=$ORDER(^WWWUSER(0,YA)) QUIT:YA=""  DO
	. . IF $GET(YMFA)'=0 QUIT:YA=YUSER
	. . IF $GET(YMFA)=1 IF $PIECE($GET(^WWWUSER(0,YA,1)),Y,2)=YBED KILL ^WWWUSER(0,YA) KILL ^WWWUSER1(0,YA) QUIT  ;FIS;06.05.04;NEU:WWWUSER1
	. . IF ($PIECE($GET(^WWWUSER(0,YA,1)),Y,3)+1)<dteNow           KILL ^WWWUSER(0,YA) KILL ^WWWUSER1(0,YA) QUIT  ;FIS;06.05.04;NEU:WWWUSER1
	
	;SAVE USER VORGABEN;FIS;06.05.04;25460
	;WIRD EINE ROUTINE MIT JOB AUFGERUFEN, SIND DIE CSP-OBJEKTE NICHT MEHR VORHANDEN. IN DIESEM FALL
	;KANN ES WÜNSCHENSWERT SEIN, DIE CGI-VARIABLEN ZURPÜCKZUHOLEN (IN WWWVORG)
	IF YHYPER'=0 IF $GET(YUSER)'="" DO
	. NEW YVAR
	. IF '$DATA(^WWWUSER1(0,YUSER,"%KEY")) DO  ;IF NEW USER ONLY !
	. . SET YVAR(1) = ""
	. . FOR  SET YVAR(1) = $ORDER(%KEY(YVAR(1))) QUIT:YVAR(1)=""  DO
	. . . IF $GET(%KEY(YVAR(1)))'=""     SET ^WWWUSER1(0,YUSER,"%KEY",YVAR(1)) = %KEY(YVAR(1)) QUIT  ;NUR EIN TEIL ;only uni- part 
	. . . IF $ORDER(%KEY(YVAR(1),""))="" SET ^WWWUSER1(0,YUSER,"%KEY",YVAR(1)) = %KEY(YVAR(1)) QUIT  ;NUR EIN TEIL ;only uni- part 
	. . . SET YVAR(2) = ""
	. . . FOR  SET YVAR(2) = $ORDER(%KEY(YVAR(1),YVAR(2))) QUIT:YVAR(2)=""  DO
	. . . . SET ^WWWUSER1(0,YUSER,"%KEY",YVAR(1),YVAR(2)) = %KEY(YVAR(1),YVAR(2))
	. ;
	. IF '$DATA(^WWWUSER1(0,YUSER,"%CGIEVAR")) DO  ;IF NEW USER ONLY !
	. . SET YVAR(1) = ""
	. . FOR  SET YVAR(1)=$ORDER(%CGIEVAR(YVAR(1))) QUIT:YVAR(1)=""  DO
	. . . IF $GET(%CGIEVAR(YVAR(1)))'=""     SET ^WWWUSER1(0,YUSER,"%CGIEVAR",YVAR(1)) = %CGIEVAR(YVAR(1)) QUIT  ;NUR EIN TEIL ;only uni- part 
	. . . IF $ORDER(%CGIEVAR(YVAR(1),""))="" SET ^WWWUSER1(0,YUSER,"%CGIEVAR",YVAR(1)) = %CGIEVAR(YVAR(1)) QUIT  ;NUR EIN TEIL ;only uni- part 
	. . . SET YVAR(2) = ""
	. . . FOR  SET YVAR(2) = $ORDER(%CGIEVAR(YVAR(1),YVAR(2))) QUIT:YVAR(2)=""  DO
	. . . . SET ^WWWUSER1(0,YUSER,"%CGIEVAR",YVAR(1),YVAR(2)) = %CGIEVAR(YVAR(1),YVAR(2))
	
	// FIXME : How much of the following is really necessary
	SET YM = $GET(%(YQUERY,"YM"))  ;Company
	IF YM="" SET YM = 0
	
	set YLOCATION = $$GetTopLevel^WWW0121()	;CORE-303
	if YLOCATION="" set YLOCATION = 1                  ; FIXME : May not exist - see "get top level entity" logic elsewhere
	
	if $piece($get(^WWWUSER(0,YUSER,1)),Y,35)'="" { ;SR17488
		do SET^WWWUSERAGENT($piece($get(^WWWUSER(0,YUSER,1)),Y,35))
	
	} else {
		do SET^WWWUSERAGENT($piece($get(^WWWUSER(0,$piece(YUSER,"x",1),1)),Y,35))
	}
	;SR17488SET YUSERAGENT = $PIECE($GET(^WWWUSER(0,YUSER,1)),Y,35)  ;FIS;25.11.2004;BROWSER TYP
	;SR17488if YUSERAGENT="" SET YUSERAGENT = $PIECE($GET(^WWWUSER(0,$piece(YUSER,"x",1),1)),Y,35) ;SR17454
 
	;SR17488IF YUSERAGENT="" SET YUSERAGENT = "MSIE"
	
	if $get(YBED)'="" {
		; FIXME : Change YM to be always 0  - remove from WWWUSER and WWW013 once deprecated <GRF>
		if YUSER'="" set YM = $$$WWWUSERLastCompany($get(^WWWUSER(0,YUSER,1)))
		if YM'=""	 set YM = $$$WWW013HomeCompany($get(^WWW013(0,YBED,1)))
		if YM=""	 set YM = 0
		
		set YLOCATION = $$$WWW013HomeLocation($get(^WWW013(0,YBED,1)))
		if YLOCATION="" set YLOCATION = $$GetTopLevel^WWW0121()	;CORE-303

		if YLOCATION="" set YLOCATION = 1
		
		if $get(%(YQUERY,"YM"))'="" set YM = %(YQUERY,"YM")
		set SPRACHE = $$^WWWLANGU(YBED)
		if $get(%(YQUERY,"YLOCATION"))'="" set YLOCATION = $get(%(YQUERY,"YLOCATION"))
	}
	
	set YUSER = $get(%(YQUERY,"YUSER"))
	
	do CopyParent^WWWUSER(YUSER) //SR13942
	
	;IF ($GET(YFORM)="") || ($EXTRACT($GET(EP),1,7)="WWWMANU") I $G(YUSER)'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,5)="",$PIECE(^WWWUSER(0,YUSER,1),Y,6)="",$PIECE(^WWWUSER(0,YUSER,1),Y,10)=""  ;NEUE SEITE
	;IF $GET(YFORM)="" I $G(YUSER)'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,5)="",$PIECE(^WWWUSER(0,YUSER,1),Y,6)="",$PIECE(^WWWUSER(0,YUSER,1),Y,10)=""  ;NEUE SEITE
	SET YFORM 		= $GET(%(YQUERY,"YFORM"))        ;FORMULAR ;form 
	SET YMENUROT 	= $GET(%(YQUERY,"YMENUROT"))        ;ROTINA VINDA DO MENU
	IF $GET(YHTMFORM)="" SET YHTMFORM = "WWW"  ;KEIN SAVE ;no 
	if (YHTMFORM="WWW") && (YFORM'="") && ($$$WWW120SaveServerdata($get(^WWW120(0,YFORM,1)))) set YHTMFORM="WWW2"
	
	IF YUSER'="" DO                          ;SUCHEN ALTE VARIABLEN ;seek 
	. NEW YUSER1
	. SET YUSER1 = $GET(^WWWUSER(0,YUSER,1))
	. SET YPWD   = $PIECE(YUSER1,Y,1)
	. IF $PIECE(YUSER1,Y,20)'="" SET YM        = $PIECE(YUSER1,Y,20)
	. IF $PIECE(YUSER1,Y,21)'="" SET YLOCATION = $PIECE(YUSER1,Y,21)
	. SET YBSZ   = ""
	. SET YTRAKT = +$GET(%(YQUERY,"YTRAKT"))
	. ; NOTE : No class structure for ^WWWZWS
	. IF '$DATA(^WWWZWS(0,dteNow,YUSER,YTRAKT)) SET YTRAKT = 0  ; VARIABLEN AUS SICHERHEITSKOPIE  ;FIS,06.03.03
	. ;IF $PIECE($GET(^WWW012(0,YM,1)),Y,159)=1 SET YTRAKT = 0  ; IMMER ALLE VARIABLEN LADEN      ;FIS;25459;08.04.04
	. IF $PIECE(YVOR1,Y,159)=1                  SET YTRAKT = 0  ; IMMER ALLE VARIABLEN LADEN
	. FOR YBSI=1:1 SET YBSZ=$ORDER(^WWWZWS(0,dteNow,YUSER,YTRAKT,YBSZ)) QUIT:YBSZ=""  DO
	. . QUIT:YBSZ="YBSZ"
	. . IF $EXTRACT(YBSZ)="V" IF $GET(LASTUSEDFORM)'="" IF $GET(YFORM)'=$GET(LASTUSEDFORM) QUIT   ;WENN FORMULARTAUSCH, DANN NICHT X VARIABLEN ÜBERTRAGEN
	. . QUIT:YBSZ="SPRACHE"
	. . QUIT:YBSZ="LANGUAGE"
	. . QUIT:$FIND(YBSZ," ")
	. . QUIT:$FIND(YBSZ,YQUERY)
	. . QUIT:$FIND(YBSZ,$CHAR(9))
	. . IF $DATA(^WWWZWS(0,dteNow,YUSER,YTRAKT,YBSZ,1)) SET @YBSZ = ^WWWZWS(0,dteNow,YUSER,YTRAKT,YBSZ,1)  ;USER VARIABLEN ZURÜCKHOLEN  ;FIS, 06.03.03
	
	if (+YM=0) && (YM'=0) set @YM=YM   ; VARIABLE=MANDANTENNAME, WENN NUMERISCH ;ACHTUNG, WENN YM GLEICH WICHTIGER NAME
	
	SET YCOUNTRY   = "DE"
	set objWWW0121 = $get(^WWW0121(0,YM,YLOCATION,1))
	set idCountry  = $$$WWW0121Country(objWWW0121)
	if idCountry'=""    set YCOUNTRY = idCountry
	IF $GET(COUNTRY)="" SET COUNTRY  = YCOUNTRY     ;TYBD;19,2,2004;UNDEF IN LISTEN IN DENEN BUNDESLAND GENUZTZ WIRD
	
	SET YTARGET  = $PIECE($PIECE(YVOR1,Y,19),"/",1)_YUSER
	SET YTARGET2 = $PIECE($PIECE(YVOR1,Y,19),"/",2)
	
	;=======================================
	;  Extract Variables from %("%KEY")
	;=======================================
 
	SET YPARA    = $GET(%(YQUERY,"YPARA"))    ; PARAMETER - (Can be set by button definition)
	
	$$$LogRx("WWWVAR-YPARA"_YPARA)
	
	SET YAENBER  = $GET(%(YQUERY,"YAENBER"))  ; PARAMETER     ; 23883;TYBD;04,07,2003
	IF $PIECE(YPARA,Y,2)'="" SET YAENBER = $PIECE(YPARA,Y,2)  ; Change Permission AUS NEUEM MENUPUNKT;23883;TYBD;04,07,2003
	SET YPARA    = $PIECE(YPARA,Y,1)          ; PARAMETER
	
	;---------------------------------------
 
	SET YANZ     = $GET(%(YQUERY,"YANZ"))      ; Previous MENU Item
	SET YAUSWAHL = $TRANSLATE($GET(%(YQUERY,"YAUSWAHL")),"|"," ")                  ; Search Value
	SET YBACK    = $GET(%(YQUERY,"YBACK"))     ; List of Back forms
	SET YBUTTON  = $GET(%(YQUERY,"YBUTTON"))   ; Last Button ;charge Button 
	SET YEXEC    = $TRANSLATE($GET(%(YQUERY,"YEXEC")),"'`|°"_$CHAR(194),""""_""""_" %") ; EXECUTE
	SET YFKEY    = $TRANSLATE($GET(%(YQUERY,"YFKEY")),"~|","# ")                   ; FIX KEY'S  XX,XXX,XX
	SET YFORM    = $GET(%(YQUERY,"YFORM"))     ; form 
	SET YINSEITE =+$GET(%(YQUERY,"YINSEITE"))  ; Inner page of a form
	;---------------------------------------
	SET YKEY     = $TRANSLATE($GET(%(YQUERY,"YKEY")),"~|"_$CHAR(194),"# ")         ; KEY'S  XX,XXX,XX
	;SET YKEY=$$^WWWTRANSLATE(YKEY,"CHARACTERPLUS","+")
	IF $EXTRACT(YKEY,$LENGTH(YKEY))="," SET YKEY=$EXTRACT(YKEY,1,$LENGTH(YKEY)-1)  ; Correct Key
	SET YKEY1    = $TRANSLATE($GET(%(YQUERY,"YKEY1")),"|"_$CHAR(194)," ")          ; KEY1'S  XX,XXX,XX ; 8 Bit checking
	IF $EXTRACT(YKEY1,$LENGTH(YKEY1))="," SET YKEY1=$EXTRACT(YKEY1,1,$LENGTH(YKEY1)-1)  ; More Correction
	;---------------------------------------
	SET YKILL    = $GET(%(YQUERY,"YKILL"))     ; 1=Kill it ;2=Delete after Showing it (Half Kill)
	SET YMOUSETR = $GET(%(YQUERY,"YMOUSETR"))  ; MOUSE TRAIL
	SET YNAME    = $TRANSLATE($GET(%(YQUERY,"YNAME")),"|"," ")  ; Header
	SET YNEW     =+$GET(%(YQUERY,"YNEW"))      ; 1=NEW BUTTON Pressed
	SET YNOKEY   = $GET(%(YQUERY,"YNOKEY"))    ; Back jump with no keys
	SET YNUMMER  = $GET(%(YQUERY,"YNUMMER"))   ; Last used Number  ;charge Number 
	;SET YOLDVAL = $GET(%(YQUERY,"YOLDVAL"))   ; ALTER VALUE; old value
	SET YOPEN    = $GET(%(YQUERY,"YOPEN"))     ; 1=OPEN OLD Record
	SET YOPTION  = $GET(%(YQUERY,"YOPTION"))   ; PARAMETER
	SET YOPTION1 = $GET(%(YQUERY,"YOPTION1"))  ; PARAMETER
	SET YORIENT  = $GET(%(YQUERY,"YORIENT"))   ; Orientation
	SET YPRINT   = $GET(%(YQUERY,"YPRINT"))    ; 1=WINDOW.PRINT()
	SET YRETURN  = $GET(%(YQUERY,"YRETURN"))   ; 1=Back from a previous form
	SET YLOCKKILL= $GET(%(YQUERY,"YLOCKKILL")) ; 1=Kill any lock when loading form ;SR15824
	SET YUSENAME = $GET(%(YQUERY,"YUSENAME"))  ; Use Menu name to overwrite form description. CORE-291
	;---------------------------------------
	SET YRICHT   = $GET(%(YQUERY,"YRICHT"))    ; Next / Last Key ;charge Key 
	IF $GET(%(YQUERY,"YRICHT1"))'="" SET YRICHT = $GET(%(YQUERY,"YRICHT1")) SET %(YQUERY,"YRICHT")=YRICHT
	;---------------------------------------
	SET YSCREENM = $GET(%(YQUERY,"YSCREENM"))  ; Test Mode 
	IF YSCREENM="" SET YSCREENM = $GET(%(YQUERY,"YSCREENM",1))  ;SCREEN MOVEMENT TEST
	;---------------------------------------
	SET YSEITE   =+$GET(%(YQUERY,"YSEITE"))    ; Tab Number
	;IF YSEITE=0 SET YSEITE = 1                ; MINDESTENS DIE SEITE 1 ;table-mat who side 
	;---------------------------------------
	SET YSIZE    = $GET(%(YQUERY,"YSIZE"))     ; Text Size
	SET YSUCH    = $GET(%(YQUERY,"YSUCH"))     ; Search Number
	;---------------------------------------
	SET YUCI     = $GET(%(YQUERY,"YUCI"))      ; NAMESPACE
	IF YUCI="" SET YUCI = $ZUTIL(5)            ; EIGENER NAMESPACE; TYBD;18,12,2003
	IF YUCI="" SET YUCI = "USER"               ; DFLT NAMESPACE
	;---------------------------------------
	SET YUSERPROFILE = $GET(%(YQUERY,"YUSERPROFILE"))
	SET YVORGABE     = $TRANSLATE($GET(%(YQUERY,"YVORGABE")),"|"," ")  ;Hidden Search Value 
	
	;=======================================
	
	IF SPRACHE="" SET SPRACHE = "PT"
	SET LANGUAGE = SPRACHE
	
	;Test for back jump KEY´S
	IF YNOKEY'="" IF YFORM=YNOKEY SET YKEY="" SET %(YQUERY,"YKEY")="",%KEY("YKEY")="" KILL YNOKEY  ; If a back with other vars, change the back key
	IF (+YSCREENM=1) || (+YSCREENM=3)  DO      ;Change Screen Position ;FIS;CUSTOMIZING;10.03.04;25301
	. SET YSCR   = $GET(%(YQUERY,"YSCR"))      ;Screen Mode 
	. IF YSCR="" SET YSCR=$GET(%(YQUERY,"YSCR",1))  ;
	. IF YSCR'="" IF $EXTRACT(YSCR,1,4)'="CUST" DO ^WWWSCRA   ; Edit Field Positions 
	. IF YSCR'="" IF $EXTRACT(YSCR,1,4)="CUST"  DO ^WWWSCRAD  ; Edit Field Positions CUSTOMIZING 
	. SET %("VAR","YSCR")=""
	
	SET YANZAHL = $GET(%(YQUERY,"YANZAHL"))    ;Number of items shown on search
	SET YSORT   = $GET(%(YQUERY,"YSORT"))      ;Sort Index
	IF YSORT<0 SET YSORT=0
	IF YSORT'="" IF YSORT'=0 SET YAUSWAHL=$$^WWWUMLAU(YAUSWAHL,2),YVORGABE=$$^WWWUMLAU(YVORGABE,2)
	IF $TRANSLATE(YBACK,",")="" SET YBACK=""
	IF YUSER'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,20) = YM
	IF YUSER'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,21) = YLOCATION
	IF $GET(%(YQUERY,"YLOCKBACK"))'="" SET $PIECE(^WWWUSER(0,YUSER,1),Y,25)=$GET(%(YQUERY,"YLOCKBACK"))  ;FIS;19.04.04;TRAKTNR. FÜR RÜCKHOLEN LOCK BEI SCHLIESSEN;25534
	
	; Transaction Tracking
	SET YTRAKT    = $GET(%(YQUERY,"YTRAKT"))
	SET YTRAKTOLD = YTRAKT
	IF (YUSER'="") && (+YTRAKT=0) {       ;if new option, start at top
		set $PIECE(^WWWUSER(0,YUSER,1),Y,5)  = 0     ; $$$WWWUSERHTMLStarted
		set $PIECE(^WWWUSER(0,YUSER,1),Y,6)  = 0
		set $PIECE(^WWWUSER(0,YUSER,1),Y,9)  = 0
		set $PIECE(^WWWUSER(0,YUSER,1),Y,10) = 0
	}
	set $piece(^WWWUSER(0,YUSER,1),Y,36)=$$IPAddress() ;SR18070
	SET YTRAKT0 = 0
	;I +YTRAKT'=0 I YUSER'="" I (YTRAKT+12)<$P($G(^WWWUSER(0,YUSER,1)),Y,7) S YTRAKT0=1  ;NUR WENN RÜCK VERBOTEN
	IF YUSER'="" IF '$FIND($GET(YOPEN),"^") SET YTRAKT=$$^WWWTRAKT(YUSER)  ; Next Session sequence number (transaction number)
	IF $GET(YBED)'="" SET SPRACHE=$$^WWWLANGU(YBED)
	;K %(YQUERY,"YTRAKT")
	;S %("VAR","YTRAKT")=0
	
	do Session
	quit
	
IPAddress()
	;-------------------------------------------------------------------------------
	; Return IP Address if available
	; 
	; 
	; History:
	; 27-Jul-2012	SCR		SR18070: Created
	;-------------------------------------------------------------------------------
	new strIP
	set strIP=""
	if $isobject($get(%request)) {
		set strIP =$get(%request.CgiEnvs("REMOTE_ADDR"))
	}
	quit strIP
	
	
Session
	;-------------------------------------------------------------------------------
	; Create a V2 Session based on V1 Session Variables
	; 
	; Returns (implicit) :
	; 	The %alSession object will contain all the session variable required by @NM
	; 
	; History:
	; 01-Nov-2007	SCR		SR15606: Added new Session Variable/Object %alSession
	; 							used for V2 code
	;-------------------------------------------------------------------------------
	set $ztrap="SessionError"
 
	if '$data(%alSession) {
		set %alSession=##class(alSYS.Session.iSession).Create()
	}
	quit
 
SessionError
	; Session Error handler
	; TODO Something
	quit
	
WSSession(pidUser)
	;-------------------------------------------------------------------------------
	; Create a @NM Session for 
	; 
	; Returns YUSER :
	; 
	; History:
	; 23-Jun-2011	SCR		SR17809: Create a Session and pass back to WS
	;-------------------------------------------------------------------------------
	set YBED=pidUser
	do ^WWWVAR
	quit $Get(YUSER)
]]></Routine>
</Export>