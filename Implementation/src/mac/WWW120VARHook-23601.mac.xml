<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW120VARHook" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW120VARHook
	
#include WWWConst
	
ExecuteVARHook(pidClass="",penumEvent,&YKEY="",&YFELD="",YFORM="",YFELDOLD="",YFIELDNAME="",&pblnCheck=$$$NO) ;CORE-286
	;-------------------------------------------------------------------------------
	; Checks if a VARHook exists for a given form/event, and runs the VARHook code.
	;
	; Params: 	Class Name, 
	; 			Event Type : WWW101("WWWEVENTTYPE") enumerator, 
	; 			YKEY, YFELD, YFORM are all available as parameters to the function
	; 			YFELDOLD - Record state before event (kept in same format as the other params)
	;
	; ByRefs:
	;
	; Returns: none
	;
	; History:
	; 17-Oct-2013	shobby	CORE-286: Return a flag if a hook is found
	; 21-Aug-2013	SCR		CORE-247: Handle Expressions Example:'(($GET(YFORM)="INDispenseToPatient") && (YLOCATION="1.1"))
	; 20-Sep-2011	GRF		SR17847: penumEvent not pintEvent; Check if null; P1 is
	;							YFORM not pidClass
	; 20-Sep-2011	GRF		SR17847: pidForm & penumEvent not pidClass & pintEvent;
	;							Check for null penumEvent
	; 08-Sep-2011	shobby	SR17847: Created (Based on WWW001Hook)
	;-------------------------------------------------------------------------------
	new objVARHooks,strStatus,strCode
	
	set strStatus = $$$OK
 	
	if (YFORM'="") && ($get(penumEvent)'="") {
		set objVARHooks = $get(^WWW120VARHook(0,YFORM,penumEvent,1))
		if objVARHooks '= "" {
			set strCode		= $$$WWW120VARHookCode(objVARHooks)
			if '((strCode?1A.AN1"^"1A.E)!(strCode?1"^"1A.E)!
				(strCode?1A.AN1"^%"1A.E)!(strCode?1"^%"1A.E)) {
				xecute "set strStatus="_$$$WWW120VARHookCode(objVARHooks)
				
			} else {
				xecute "do "_$$$WWW120VARHookCode(objVARHooks)
			}
			set pblnCheck=$$$YES ;CORE-286
		}
	}
	quit strStatus
	
]]></Routine>
</Export>