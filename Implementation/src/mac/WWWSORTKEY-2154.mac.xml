<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSORTKEY" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSORTKEY(YDATEI,pblnForceNoIdx=0)
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		VORGABEN FÜR SORTKEYS IN VARIABLE YSKEY
	;		Build list of index keys for a class for use in updating indices
	;		
	;	D ^WWWSORTKEY(CLASS)
	;
	; Inputs : 
	;	YDATEI		idClass
	;
	; ByRef :
	;	YSKEY(1)  = "K1,F2,K3"               ;VARIABLEN FÜR SORTIERSCHLÜSSEL
	;	YSKEY     = LAST USED SORTKEY 
	;
	;------------------------
	; e.g. Class FINAPInv with index definitions -	   will return -
  	;	 1.1 : 22 : Invoice Supplier				YSKEY     = "FINAPInv"
  	;	 1.2 :  2 : SuppliersInvoiceNumber			YSKEY(1)  = "F22,F2"
  	;	   2 :  3 : Is Voucher						YSKEY(2)  = "F3"
  	;	   3 : 34 : Parent Invoice					YSKEY(3)  = "F34"
  	;	 4.1 : 36 : Repeat Group					YSKEY(4)  = "F36,F34"
  	;	 4.2 : 34 : Parent Invoice					YSKEY(5)  = "F9"
  	;	   5 :  9 : Batch Number					YSKEY(60) = "F28"
  	;	  60 : 28 : Status
  	;------------------------
	;
	; Returns : Nothing
	;
	; History :
	; 20-Sep-2011	SCR		SR17885: Added Force No Index option , used by compiled code (COMIndex,WWW001OO,idx etc)
	; 07-Feb-2008	GRF		Doco
	; 31-May-2007	HeberB	BR014465:Add new characters 
	; 30-Mar-2007	RPW		Possible brace version
	; 02-Mar-2007	GRF		SR15462: Doco; quits; WW120=>WWW120
	; 18:08.2000	DT
	;-------------------------------------------------------------------------------
	NEW YYDATEI,YSORSOR
	
	KILL YSKEY
	
	QUIT:$GET(YDATEI)=""
	
	SET YSKEY=YDATEI  ;LETZTER VERWENDETER KEY
	
	/*
	if '$find("WWW120,WWW124,WWW122,WWW002,WWW003",YDATEI) && ($DATA(^WWW002s(0,4))) {
		SET LC="áàãâéèêíìîóòõôúùûçÜÄÖüäöß][\}{|abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$CHAR(128)_""""_" "   ;SPEEDUP;TYBD;25.09.2004
		SET UC="ÁÀÃÂÉÈÊÍÌÎÓÒÕÔÚÙÛÇUAOUAOSUAOUAOABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
		IF YUMLAU'="" {
			SET YYDATEI=$$^WWWUMLAU(YDATEI,1)
		} else {
			SET YYDATEI=$TRANSLATE(YDATEI,LC,UC)
		}
		
		$$$Order4(^WWW002s,0,4,YYDATEI,YSORSOR)
			$$$Order5(^WWW002s,0,4,YYDATEI,YSORSOR,YLFN)
				continue:YSORSOR'=" "
				set YSKEY=$$$WWW002IndexKey($get(^WWW002(0,YDATEI,YLFN,1)))
				for YI=1:1 {
					set YA=$piece(YSKEY,$$$COMMA,YI)
					quit:YA=""
					set YA(1)=+$piece(YA,".",1)
					continue:YA(1)=0
					
					set YA(2)=+$piece(YA,".",2)
					if YA(2)=0 set YA(2)=1
					set $piece(YSKEY(YA(1)),$$$COMMA,YA(2))="K"_YLFN
				}
			$$$End
		$$$End
		
		$$$Order4(^WWW003s,0,4,YYDATEI,YSORSOR)
			continue:YSORSOR'=" "
			$$$Order5(^WWW002s,0,4,YYDATEI,YSORSOR,YLFN)
				continue:YSORSOR'=" "
				set YSKEY=$$$WWW003IndexKey($get(^WWW003(0,YDATEI,YLFN,1)))
				for YI=1:1 {
					set YA=$piece(YSKEY,$$$COMMA,YI)
					quit:YA=""
					set YA(1)=+$piece(YA,".",1)
					continue:YA(1)=0
					
					set YA(2)=+$piece(YA,".",2)
					if YA(2)=0 set YA(2)=1
					set $piece(YSKEY(YA(1)),$$$COMMA,YA(2))="F"_YLFN
				}
			$$$End
		$$$End
		
		SET YSKEY=YDATEI
		quit
	}
	*/
		
	;IF '$FIND("WWW120,WWW124,WWW122,WWW002,WWW003",YDATEI) IF $DATA(^WWW002s(0,4)) DO  SET YSKEY=YDATEI QUIT  ;MIT SORTKEY;SPEEDUP;TYBD;27,10,2004
	IF ('$FIND("WWW120,WWW124,WWW122,WWW002,WWW003",YDATEI)) && ('pblnForceNoIdx) IF $DATA(^WWW002s(0,4)) DO  SET YSKEY=YDATEI QUIT  ;SR17885 MIT SORTKEY;SPEEDUP;TYBD;27,10,2004 
	. ;BR014465
	. ;SET LC="ÜÄÖüäöß][\}{|abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$CHAR(128)_""""_" "   ;SPEEDUP;TYBD;25.09.2004
	. ;SET UC="UAOUAOSUAOUAOABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	. SET LC="áàãâéèêíìîóòõôúùûçÜÄÖüäöß][\}{|abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$CHAR(128)_""""_" "   ;SPEEDUP;TYBD;25.09.2004
	. SET UC="ÁÀÃÂÉÈÊÍÌÎÓÒÕÔÚÙÛÇUAOUAOSUAOUAOABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	. IF YUMLAU'="" SET YYDATEI=$$^WWWUMLAU(YDATEI,1)
	. IF YUMLAU=""  SET YYDATEI=$TRANSLATE(YDATEI,LC,UC)
	. SET YSORSOR=""
	. FOR  SET YSORSOR=$ORDER(^WWW002s(0,4,YYDATEI,YSORSOR)) QUIT:YSORSOR=""  IF YSORSOR'=" " DO
	. . SET YLFN=""
	. . FOR  SET YLFN=$ORDER(^WWW002s(0,4,YYDATEI,YSORSOR,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. . . SET YSKEY=$PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,6)
	. . . FOR YI=1:1 SET YA=$PIECE(YSKEY,",",YI) QUIT:YA=""  DO
	. . . . SET YA(1)=+$PIECE(YA,".",1)
	. . . . SET YA(2)=+$PIECE(YA,".",2)
	. . . . QUIT:+YA(1)=0
	. . . . IF +YA(2)=0 SET YA(2)=1
	. . . . SET $PIECE(YSKEY(YA(1)),",",YA(2))="K"_YLFN
	. ;
	. SET YSORSOR=""
	. FOR  SET YSORSOR=$ORDER(^WWW003s(0,4,YYDATEI,YSORSOR)) QUIT:YSORSOR=""  IF YSORSOR'=" " DO
	. . SET YLFN=""
	. . FOR  SET YLFN=$ORDER(^WWW003s(0,4,YYDATEI,YSORSOR,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. . . SET YSKEY=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,6)
	. . . FOR YI=1:1 SET YA=$PIECE(YSKEY,",",YI) QUIT:YA=""  DO
	. . . . SET YA(1)=+$PIECE(YA,".",1)
	. . . . SET YA(2)=+$PIECE(YA,".",2)
	. . . . QUIT:+YA(1)=0
	. . . . IF +YA(2)=0 SET YA(2)=1
	. . . . SET $PIECE(YSKEY(YA(1)),",",YA(2))="F"_YLFN
	
	
	;-------------------------------------------------------------------------------
	; OBSOLETE (?) CODE BELOW IS ONLY EXECUTED WHEN ^WWW002s(0,4) DOESN'T EXIST
	; or FOR SPECIAL CLASSES
	;-------------------------------------------------------------------------------
	
	;old version;tybd;28,10,2004
	SET YLFN=""
	FOR  SET YLFN=$ORDER(^WWW002(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. SET YSKEY=$PIECE($GET(^WWW002(0,YDATEI,YLFN,1)),Y,6)
	. FOR YI=1:1 SET YA=$PIECE(YSKEY,",",YI) QUIT:YA=""  DO
	. . SET YA(1)=+$PIECE(YA,".",1)
	. . SET YA(2)=+$PIECE(YA,".",2)
	. . QUIT:+YA(1)=0
	. . IF +YA(2)=0 SET YA(2)=1
	. . SET $PIECE(YSKEY(YA(1)),",",YA(2))="K"_YLFN
	
	SET YLFN=""
	FOR  SET YLFN=$ORDER(^WWW003(0,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. SET YSKEY=$PIECE($GET(^WWW003(0,YDATEI,YLFN,1)),Y,6)
	. FOR YI=1:1 SET YA=$PIECE(YSKEY,",",YI) QUIT:YA=""  DO
	. . SET YA(1)=+$PIECE(YA,".",1)
	. . SET YA(2)=+$PIECE(YA,".",2)
	. . QUIT:+YA(1)=0
	. . IF +YA(2)=0 SET YA(2)=1
	. . SET $PIECE(YSKEY(YA(1)),",",YA(2))="F"_YLFN
	
	SET YSKEY=YDATEI
	QUIT
]]></Routine>
</Export>