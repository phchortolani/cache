<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW124Custom" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW124Custom
	#include COMSYS
	#include WWWConst
 
ClearCustomButtons()
    /*-------------------------------------------------------------------------------
    ; Clear all custom buttons that have been inserted into a form
    ;
    ; Params: pidForm:		The id of the form
    ;
    ; ByRefs:
    ;
    ; Returns: nothing
    ;
    ; History:
    ; 19-Jan-2006	Steve S		SR13171: Created
    ;-------------------------------------------------------------------------------*/
	new idLang,idSeq,strKey,idForm
	
	set idForm=""
	for {
		set idForm=$order(^WWW124s(0,10,$$$YES,idForm))
		quit:(idForm="")
		
		set idLang=""
		for {
			set idLang=$order(^WWW124s(0,10,$$$YES,idForm,idLang))
			quit:(idLang="")
		
			set idSeq=""
			for {
				set idSeq=$order(^WWW124s(0,10,$$$YES,idForm,idLang,idSeq))
				quit:(idSeq="")
		
				set strKey=(idForm_","_idLang_","_idSeq)
		
				do KILL^COMUtils("WWW124",strKey)
			}	
		}		
	}
 
	quit	
	
	
GenerateCustomButtons(pidForm)
    /*-------------------------------------------------------------------------------
    ; For a form, loop through its custom buttons and add them
    ; to the button database for the form.
    ;
    ; Params: pidForm:		The id of the form
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 19-Jan-2006	Steve S		SR13171: Created
    ;-------------------------------------------------------------------------------*/
	new idLang,idSeq,objButton,idNewSeq,strKey
	
	set idLang=""
	for {
		set idLang=$order(^WWW124D(0,pidForm,idLang))
		quit:(idLang="")
			
		set idSeq=""
		for {
			set idSeq=$order(^WWW124D(0,pidForm,idLang,idSeq))
			quit:(idSeq="")
				
			set objButton=$get(^WWW124D(0,pidForm,idLang,idSeq,1))
			
			; objButton is from WWW124D, but will now be
			; merged to WWW124 (which is a mirror class)
				
			set $$$WWW124Custom(objButton)=$$$YES
			
			set idNewSeq=($order(^WWW124(0,pidForm,idLang,""),-1)+1)
			set strKey=(pidForm_","_idLang_","_idNewSeq)
			
			do Save^COMUtils("WWW124",strKey,objButton,1)
		}	
	}
		
	quit
	
	
InsertNow()
    /*-------------------------------------------------------------------------------
    ; Re-insert all defined custom buttons.
    ;
    ; Params:
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Jan-2006	Steve S		SR13171: Created
    ;-------------------------------------------------------------------------------*/
	job InsertButtonsJob^WWW124Custom(YM,YUSER,YBED,SPRACHE)
	do ^WWWINFO($$^WWWTEXT(383)) ;Program Has Started In Background
	
	quit
	
	
InsertButtonsJob(pYM,pYUSER,pYBED,pSPRACHE)
    /*-------------------------------------------------------------------------------
    ; Re-insert all defined custom buttons as a b/ground process.
    ;
    ; Params: pYM:			The YM value
    ; 		  pYUSER:		The YUSER value
    ; 		  pYBED:		The YBED value
    ; 		  pSPRACHE:		The SPRACHE value
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Jan-2006	Steve S		SR13171: Created
    ;-------------------------------------------------------------------------------*/
	new idForm
 
	if $get(Y)="" do ^WWWVAR
	
	set YM		=pYM
	set YUSER	=pYUSER
	set YBED	=pYBED
	set SPRACHE	=pSPRACHE
	
	do ClearCustomButtons()
	
	set idForm=""
	for {
		set idForm=$order(^WWW124D(0,idForm))
		quit:(idForm="")
		
		do GenerateCustomButtons(idForm)
	}
 
	quit
]]></Routine>
</Export>