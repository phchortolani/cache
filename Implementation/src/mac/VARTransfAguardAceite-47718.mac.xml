<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARTransfAguardAceite" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARTransfAguardAceite
#include COMSYS
#include INConst

OnBeforeButtonLine
	do LoadListaLocaisDeEstoque^VAREstoqueZerado
	
	if $get(^VARTempAguardAceite(YM,YBED)) = "" {
		set $piece(^VARTempAguardAceite(YM,YBED),Y,2) = YLOCATION
	}
		
	quit
	
OnAfterSave
	new idLocalAbastecedor, idLocalSolicitante	

	set idLocalAbastecedor = $get(VORG(1))
	set idLocalSolicitante = $get(VORG(2))
	
	set $piece(^VARTempAguardAceite(YM,YBED),Y,1) = idLocalAbastecedor
	set $piece(^VARTempAguardAceite(YM,YBED),Y,2) = idLocalSolicitante
	
 	do GoToForm^COMUtilForm("VARTransfAguardAceite",1)
	
	quit	

OnAfterDataFields
	new filterLocalAbastecedor, filterLocalSolicitante, idTransfer, lstHeader, count, idLocalAbastecedor,
		idLocalSolicitante, strStatus

	//Monta cabeçalho da tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","No. Transferência","Data","Local Abastecedor","Local Solicitante")	

	set filterLocalAbastecedor = $piece($get(^VARTempAguardAceite(YM,YBED)),Y,1)
	set filterLocalSolicitante = $piece($get(^VARTempAguardAceite(YM,YBED)),Y,2)

	set count = 0

 	if $$Start^COMTable(lstHeader,"<div style=margin:3px;>Transferências Aguardando Aceite</div>",,,$$$NO) {
	
		set idTransfer = "" 
		for { 
			set idTransfer = $order(^INTFR(YM,idTransfer))
			quit:idTransfer=""
			
			set strStatus = $$isTransferenciaEstornada^VARINTFR(idTransfer)
			continue:(strStatus = $$$YES)
			set strStatus = $$isTotalmenteAceita^VARINTFR(idTransfer)
			continue:(strStatus = $$$NO)
						
			set objTransfer = $get(^INTFR(YM,idTransfer,1))
			
			set idLocalAbastecedor = $$$INTFRSendingLocation(objTransfer)
			set idLocalSolicitante = $$$INTFRReceivingLocation(objTransfer)
			set dteEnviada = $$$INTFRShipped(objTransfer)
			
			if (filterLocalAbastecedor '= "") {
				continue:(idLocalAbastecedor '= filterLocalAbastecedor)
			}
			
			if (filterLocalSolicitante '= "") {
				continue:(idLocalSolicitante '= filterLocalSolicitante)
			}
			
			set count = $i(count)
			
 			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idTransfer,"INTFR",idTransfer)
			do InsertCell^COMTable($zdate(dteEnviada,4),"INTFR",idTransfer)
			do InsertCell^COMTable(idLocalAbastecedor_" - "_$$SQLGetLocationName^VARSQL(idLocalAbastecedor),"INTFR",idTransfer)
			do InsertCell^COMTable(idLocalSolicitante_" - "_$$SQLGetLocationName^VARSQL(idLocalSolicitante),"INTFR",idTransfer)
		    do EndLine^COMTable()
		    
		}
		
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum resultado encontrado.",,,,,,5)
			do EndLine^COMTable()
		}
 	}

	quit
]]></Routine>
</Export>