<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INForeRun" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INForeRun	; Forecast Run
 #include INConst
 
SetDemand(Item="",Location="",Qty=0,Date="",Note="",Planner="") 
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Jun-2005	GRF		$$$INDRPDEMANDRequestingLocation - name updated
    ; 16-Aug-2004	Unknown127.0.0.1			Created
    ;-------------------------------------------------------------------------------
	new DRPItem,DemandRec,Planners,DemandNo
 
	set DRPItem=$$GetDRPItem(Item,Location)
	
	set DemandRec=""
	set Planners=$translate($$$INDRPITEMPlanner(DRPItem),",",";")
	if ($piece(Planners,";",1)="")||($Find(";"_Planners_";",";"_YBED_";")) {
		set $$$INDRPDEMANDResponsiblePlanner(DemandRec)=YBED ; If the Current User is a planner use it
	} else {
		set $$$INDRPDEMANDResponsiblePlanner(DemandRec)=$piece(Planners,";",1) ; else use the first one!
	}	
	
	set $$$INDRPDEMANDCreatedon(DemandRec)          = +$H 		;Created Date
	set $$$INDRPDEMANDCreatedBy(DemandRec)          = YBED 	;User
	set $$$INDRPDEMANDItemNo(DemandRec)		        = Item		;Item
	set $$$INDRPDEMANDQuantity(DemandRec)	        = Qty 		;Qty
	set $$$INDRPDEMANDDueOnBefore(DemandRec)        = Date 		;Due Date
	if $$$INDRPPLANNERAcceptDemands($$^INDRPACCESS(Location,Planner))'=1 set $$$INDRPDEMANDBlocked(DemandRec) = 1 ;Disabled
	set $$$INDRPDEMANDRequestingLocation(DemandRec) = Location	;Location
	set $$$INDRPDEMANDDemandType(DemandRec)         = 9 		;Demand Type
	set $$$INDRPDEMANDMemo(DemandRec)               = Note	    ;Rule Description
	set DemandNo=$$^WWWNEXT("INDRPDEMAND")
	do Save^COMUtils("INDRPDEMAND",Location_","_DemandNo,DemandRec,1)
	quit DemandNo
 
 
RemoveDemands(Item="",Location="")
    ;-------------------------------------------------------------------------------
    ; Remove Demands Type 9
    ;
    ; Returns:
    ;
    ; History:
    ; 19-Aug-2004		SCR			Created
    ;-------------------------------------------------------------------------------
    new Count,Demand,DemandRec
    
    set Count=0
    if Item="" set Item=" "
    if Location="" set Location=" "
	set Demand=""
	for {
	    set Demand=$order(^INDRPDEMANDs(YM,3,Item,Location,Demand))
	    quit:Demand=""
	    set DemandRec=$get(^INDRPDEMAND(YM,Location,Demand,1))
	    if $$$INDRPDEMANDDemandType(DemandRec)=9 {  ;70
		    do KILL^COMUtils("INDRPDEMAND",Location_","_Demand)
		    set Count=Count+1
	    }
    }
    quit Count
 
GetDRPItem(Item="",Location="") 
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Returns:
    ;
    ; History:
    ; 09-Jun-2005	GRF		$$$INVORGDefaultItemGroup - name updated
    ; 16-Aug-2004	SCR		Created
    ;-------------------------------------------------------------------------------
	new DRPItem,Group
	
	set DRPItem=$Get(^INDRPITEM(YM,Location,Item,1))
	if DRPItem="" {
		set Group=$$$INARTItemGroup($get(^INART(YM,Item,1))) ;ITEM GROUP
		if Group="" SET Group=$$$INVORGDefaultItemGroup($GET(^INVORG(YM,YM,1))) ;DFLT Group.
		if (Group'="") && ($data(^INDRPITEMS(YM,Location,Group,1))) {
			set DRPItem=$get(^INDRPITEMS(YM,Location,Group,1))
		}
	}
	quit DRPItem
 
]]></Routine>
</Export>