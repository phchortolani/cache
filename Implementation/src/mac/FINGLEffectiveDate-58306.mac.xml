<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLEffectiveDate" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLEffectiveDate
	
#include COMSYS
	
OnBlurEffectiveDate(pYFELD="",pfldGLEffectiveDate,&pdteDate)
	;-------------------------------------------------------------------------------
	; On Blur of GL Effective Date
	;
	; Parameters:
	; pYFELD             : This contains the data of the Invoice Match/Voucher
	; pfldGLEffectiveDate: The field number of the effective date
	;
	; Returns:
	;
	; History:
	; 05-Oct-2006	Steve S		SR15074: Return the date by ref.
	; 26-Jun-2006	RPW			SR13173: Pass through the field number
	; 15-Jul-2005	RobertW		SR12958: Pass in 0 instead of 1.
	; 01-Jul-2005	RobertW		SR12230: Created
	;-------------------------------------------------------------------------------
	new dteGLEffective,dteNew
	
	set dteGLEffective = $piece(pYFELD,Y,pfldGLEffectiveDate)
	
	if dteGLEffective'="" {
		set dteNew = $$SetGLEffectiveDate^FINGLEffectiveDate(dteGLEffective,dteGLEffective,pfldGLEffectiveDate)
	} else {
		; Handle when the field has been deleted, there MUST be a value here.
		set dteNew = $$SetGLEffectiveDate^FINGLEffectiveDate(0,dteGLEffective,pfldGLEffectiveDate) ; SR12958
	}
	
	set pdteDate = dteNew //SR15074
	
	quit
	
	
GLEffectiveDateAfterConstruction(pfldGLEffectiveDate)
    ;-------------------------------------------------------------------------------
    ; When the form is constructed, we must get the default value for the GL Effective
    ; Date. If the General Parameters says to use Invoice Date, then default this to
    ; blank
    ;
    ; Params:
    ; pfldGLEffectiveDate: The field number of the effective date
    ;
    ; Returns:
    ;
    ; History:
    ; 26-Jun-2006	RPW		SR13173: Pass through the field number
    ; 30-Jun-2005	RPW		SR12230: Created
    ;-------------------------------------------------------------------------------
	new dteDate
	
	if $$$NoKey(YKEY) {
		if $$IsGLEffectiveDateInvoiceDate^FINGeneralSetup() {
			set dteDate = +$horolog //SR14461
		} else {
			set dteDate = $$GetGLEffectiveDate^FINAPUserSessionDate()
		}
	} else {
		set dteDate = $piece(YFELD,Y,pfldGLEffectiveDate)
	}
	
	quit $$^WWWDATE($$GetGLEffectiveDate(dteDate,dteDate,,,pfldGLEffectiveDate))
	
	
SetGLEffectiveDate(pdteDate,pdteGLEffective,pfldGLEffectiveDate)
	;-------------------------------------------------------------------------------
	; Get the next GL Effective Date and if it's not the same as the current date, then
	; set the field on screen to reflect the new date
	;
	; Params:
	; pdteDate			The current date the user is checking
	; pdteGLEffective	The date that is currently in the GL Effective Date field
	; pfldGLEffectiveDate: The field number of the effective date
	;
	; Returns:
	;
	; History:
	; 05-Oct-2006	SS		SR15074: Return the date
	; 26-Jun-2006	RPW		SR13173: Pass through the field number
	; 15-Jul-2005	RPW		SR12958: Check if the returned date is zero, then
	; 							we couldn't find any open periods, return blank
	; 08-Jul-2005	RPW		SR12230: Fixed date checking due to shortly
	; 01-Jul-2005	RPW		SR12230: Created
	;-------------------------------------------------------------------------------
    new dteNext
    
    set dteNext = $$GetGLEffectiveDate(pdteDate,pdteGLEffective,,,pfldGLEffectiveDate)
	
	if dteNext'=pdteGLEffective {
		set %TXT(1) = "#Y"_YFORM_"D"_pfldGLEffectiveDate_Y_$$^WWWDATE(dteNext)
	}
	
	quit dteNext //SR15074
	
	
GetGLEffectiveDate(pdteDate,pdteGLEffective,pblnAlert=$$$YES,&pstrStatus,pfldGLEffectiveDate)
	;-------------------------------------------------------------------------------
	; Get the GL Effective Date and status if it can't be found.
	;
	; Params:
	; pdteDate			 : The current date the user is checking
	; pdteGLEffective	 : The date that is currently in the GL Effective Date field
	; pblnAlert          : Shall a dialog box be shown with the error message
	; pstrStatus         : Any error messages
	; pfldGLEffectiveDate: The field number of the effective date
	; 
	; ByRefs:
	; pstrStatus
	;
	; Returns:
	; date in $h format
	;
	; History:
	; 26-Jun-2006	RPW		SR13173: Created
	;-------------------------------------------------------------------------------
	new dteNext,strType
	
	set $ztrap  = "GGEDError"
	set strType = ""
	
	xecute "set strType=$$GetType^"_YFORM_"()"
	
	quit:strType=""
	
	set dteNext = $$GetGLEffectiveDate^FINAPUtilities(pdteDate,pdteGLEffective,pblnAlert,.pstrStatus,strType,pfldGLEffectiveDate)
	if dteNext=0 set dteNext = "" ; SR12958
	
	quit dteNext
	
GGEDError ; Interal Tag
	set $ztrap = ""
	quit +$horolog
	
	
ValidGLEffectiveDate(pdteDate,pdteGLEffective,pfldGLEffectiveDate)
	;-------------------------------------------------------------------------------
	; Determine whether the GL Effective Date is valid
	;
	; Params:
	; pdteDate              The current date the user is checking
	; pdteGLEffective       The date that is currently in the GL Effective Date field
	; pfldGLEffectiveDate   The field number of the effective date
	;
	; ByRefs:
	;
	; Returns:
	; strStatus: Blank is ok, otherwise an error message
	;
	; History:
	; 26-Jun-2006	RPW		SR13173: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	do GetGLEffectiveDate(pdteDate,pdteGLEffective,$$$NO,.strStatus,pfldGLEffectiveDate)
	quit strStatus
	
]]></Routine>
</Export>