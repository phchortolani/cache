<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLProfileCodeCopy" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLProfileCodeCopy
 
	#include COMSYS
	#include FINConst
 
 
Copyable(pidProfileCode="")
    ;-------------------------------------------------------------------------------
    ; Determines whether a profile code is "copyable"
    ; (Must be "Location" type)
    ;
    ; Returns:
    ;
    ; History:
    ; 06-Jan-2006 	SC			SR13798: Decode wrapper removed. Decode in COMSYS.INC
    ; 12-Jul-2005	Steve S		SR12573: Created
    ;-------------------------------------------------------------------------------
    
    new idCodeType,strStatus
    
	; FIXME: Finance has hardcoded Location to "1", so this routine will need
	; changing when this is fixed.
 
	set strStatus=$$$OK
	set idCodeType=$piece(pidProfileCode,",",1)
	
	if idCodeType'="" {
		if idCodeType'=1 {
			set strStatus=($lb("Fin00841",1_" - "_$$$FINGLProfileCodeTypeDescription($get(^FINGLProfileCodeType(YM,1,1)))))  ;Can only perform Location Copy for codes of type `%1`.
		}
	} else {
		set strStatus=$lb("Fin00089")  ;No Record Selected.	
	}
	
	if $$$ISOK(strStatus) {
		set YQ=$$$YQEnable	
	} else {
		set YQ=$$$YQDisable(strStatus)	;SR13798
	}
 
	quit
	
	
Copy(pidProfileCode)
    ;-------------------------------------------------------------------------------
    ; Start the copy process (redirect to the appropriate form)
    ;
    ; Returns:
    ;
    ; History:
    ; 12-Jul-2005	Steve S		SR12573: Created
    ;-------------------------------------------------------------------------------
	if $get(pidProfileCode)'="" {
		set ^CacheTempCodeType(YUSER)=$piece(pidProfileCode,",",1)
		set ^CacheTempDefaultToCode(YUSER)=$piece(pidProfileCode,",",2)
		do GoToForm^COMUtilForm("FINGLProfileCodeCopy","","","")
	} else {
		do ReturnError^COMUtilError($lb("Fin00089"))  ;No Record Selected.	
	}
 
	quit
	
	
OnAfterSave(pidCodeType="",pidCode="",pidNewCode="")
	;		VORG(1)		   VORG(2)	  VORG(3)
    ;-------------------------------------------------------------------------------
    ; Routine to execute after submitting the "Copy Profile Code" form
    ; 
    ; Returns:
    ;
    ; History:
    ; 12-Jul-2005	Steve S		SR12573: Created
    ;-------------------------------------------------------------------------------
	new strStatus
 
	set strStatus=$$$OK
	
	if '$data(^FINGLProfileCode(YM,pidCodeType)) {
		set strStatus=$lb("Fin00697",pidCodeType)  ;%1 is an invalid profile code type.	
	} elseif '$data(^FINGLProfileCode(YM,pidCodeType,pidCode)) {
		set strStatus=$lb("Fin00839",pidCode)  ;%1 is an invalid profile code	
	} elseif pidCode=pidNewCode {
		set strStatus=$lb("Fin00842")  ;From Code and To Code cannot be the same.
	} else {
		set strStatus=$$PerformCopy(pidCode,pidNewCode)		
	}
	
	if $$$ISOK(strStatus) {
		do ReturnError^COMUtils($lb("Fin00840")) ;Copy successful
	} else {
		do ReturnError^COMUtils(strStatus)	
	}
	
	quit
	
	
PerformCopy(pidCode,pidNewCode)
    ;-------------------------------------------------------------------------------
    ; Perform the actual copy
    ; 
    ; Returns:
    ;
    ; History:
    ; 12-Jul-2005	Steve S		SR12573: Created
    ;-------------------------------------------------------------------------------
    new idProfile,strStatus,blnChange,objProfile,strInclude,strExclude
    
    set idProfile=""
    set strStatus=$$$OK
    
    for {
	    set idProfile=$order(^FINGLProfile(YM,idProfile))
	    quit:(idProfile="")||($$$ISERR(strStatus))
	    
	    set blnChange=$$$NO
	    set objProfile=$get(^FINGLProfile(YM,idProfile,1))
	    
	    set strInclude=$$$FINGLProfileIncludeLocations(objProfile)
		set strExclude=$$$FINGLProfileExcludeLocations(objProfile)
		
		if $find(";"_strInclude_";",";"_pidCode_";") {
			set strInclude=strInclude_";"_pidNewCode
			set blnChange=$$$YES	
		}
		
		if $find(";"_strExclude_";",";"_pidCode_";") {
			set strExclude=strExclude_";"_pidNewCode
			set blnChange=$$$YES	
		}
		
		if blnChange {
			set $$$FINGLProfileIncludeLocations(objProfile)=strInclude
			set $$$FINGLProfileExcludeLocations(objProfile)=strExclude
			
			set strStatus=$$Save^COMUtils("FINGLProfile",idProfile,objProfile,1)
		}
    }
 
	quit strStatus
]]></Routine>
</Export>