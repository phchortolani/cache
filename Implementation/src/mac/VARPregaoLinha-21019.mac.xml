<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPregaoLinha" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPregaoLinha
#include COMSYS
#include COMConst
#include VARConst
#include VARSESPainelAbastecimento
 
OnBeforeFormat(pstrYFIELDNAME,pstrText,pobjLine)
    ;-------------------------------------------------------------------------------
    ; Colore a linha do grid.
    ;    Amarelo: Aguardando aceitação
    ;    Vermelho: Fracassado
    ;    Verde: Aceito e Habilitado
    ;
    ; History:
    ; 19-Mar-2013   Gustavo Fiuza   Created
    ;-------------------------------------------------------------------------------
    new idField, idKey, idPregao, idPregaoLinha, objPregaoLinha, idSituacao
    
    $$$GRIDSplitKey(pstrYFIELDNAME,intRow,intCol)
    set idField = $$$GetClassField(YFORM,intCol)
 
    set idKey  = $$ReferenceKey^COMGridEdit31Interface(YFORM,intRow)        
    
    set idPregao = $$$KEY1(idKey)
    set idPregaoLinha = $$$KEY2(idKey)
    
    ;quit:(status '= 1) ;Somente aplica cores quando o status for 1-Processado
    ;Aplicar cores somente na coluna 1
    quit:(intCol '= 1)
    
	set objPregaoLinha = $get(^VARPregaoLinha(YM,idPregao,idPregaoLinha,1))	
	set idSituacao = $$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha) ;$$GetSituacaoItem(idPregao,idPregaoLinha)
    
    ;set YTOOLTIP = ?
    
    if (idSituacao = $$$AguardandoAceitacao) {
	    set YBACKGROUNDCOLOR = "#FFFF5B"    ;Amarelo
    
    } elseif ( (idSituacao = $$$Deserto) || (idSituacao = $$$FracassadoPorPreco) || (idSituacao = $$$FracassadoPorDoc) ) {
	    set YBACKGROUNDCOLOR = "#f8d08b"    ;Vermelho claro
	    
    } elseif (idSituacao = $$$AceitoHabilitado) {
	    set YBACKGROUNDCOLOR = "#92d39e"    ;Verde
    }
    
    quit

OnAfterSave(pYKEY)
	quit:((pYKEY = "") || (pYKEY = "+"))
	new idPregao, idLinha, objPregaoLinha, idSRP, idSRPLinha
	
	set idPregao = $$$KEY1(pYKEY)
	set idLinha  = $$$KEY2(pYKEY)
	
	set objPregaoLinha = $get(^VARPregaoLinha(YM,idPregao,idLinha,1))

	if (+$$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha) = 0){
		set idSRP 		= $$$VARPregaoLinhaSRP(objPregaoLinha)
		set idSRPLinha 	= $$$VARPregaoLinhaItemdaSRP(objPregaoLinha)
		set strStatus = $$AtualizaStatusLinha^VARSESSOLICPRECO(idSRP,idSRPLinha,2) // Aguadando Ata
		if $$$ISERR(strStatus){
			$$$Alert($$$MakeStatus(strStatus))
		}
	}

	if ((+$$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha) = 1) ||
		(+$$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha) = 2) ||
		(+$$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha) = 3)){
		set idSRP 		= $$$VARPregaoLinhaSRP(objPregaoLinha)
		set idSRPLinha 	= $$$VARPregaoLinhaItemdaSRP(objPregaoLinha)	
		set strStatus = $$AtualizaStatusLinha^VARSESSOLICPRECO(idSRP,idSRPLinha,4) // Concluído
		if $$$ISERR(strStatus){
			$$$Alert($$$MakeStatus(strStatus))
		}
	}
	
	quit

GetPregaoLinha(pidPregao,pidItem)
	;-------------------------------------------------------------------------------
    ; Retorna a linha do pregão a partir da chave do pregão e do código do produto.
    ;
    ; History:
    ; 15-Jul-2013	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	quit:( (pidPregao = "") || (pidItem = "") ) ""
	
	new idPregaoLinha
	set idPregaoLinha = ""	
	set idPregaoLinha = $Order(^VARPregaoLinhas(YM, 3, $$$Index(pidItem), pidPregao, "")) 
	quit idPregaoLinha

GetQuantItensSituacao(pidPregao,pidSituacao)
	quit:(pidPregao = "") 0
	quit:(pidSituacao = "") 0
 
	new quantItens	
	set quantItens = 0
 
	$$$Order5(^VARPregaoLinhas,YM,1,pidSituacao,pidPregao,idPregaoLinha)		
		set quantItens = $increment(quantItens)		
	$$$End
 
	quit quantItens 
	
GetSituacaoPregaoProduto(pidPregao,pidProduto,pFormato=1)
	/* Retorna a situação do pregão a partir do produto.
		Formato 1 retorna o id da situação
		Formato 2 retorna a descrição da situação 
	*/
	quit:( (pidPregao = "") || (pidProduto = "") ) ""
	
	new objPregaoLinha, idSituacao
	
	set idSituacao = ""
	
	$$$Order5(^VARPregaoLinhas,YM,3,pidProduto,$$$Index(pidPregao),idPregaoLinha)
	
		set objPregaoLinha = $get(^VARPregaoLinha(YM,pidPregao,idPregaoLinha,1))
		set idSituacao = $$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha)
	
	$$$End
	
	;Tentando também sem $$$Index, para alguns casos de chave só funcionou assim
	if (idSituacao = "") {		
		$$$Order5(^VARPregaoLinhas,YM,3,pidProduto,pidPregao,idPregaoLinha)
			set objPregaoLinha = $get(^VARPregaoLinha(YM,pidPregao,idPregaoLinha,1))
			set idSituacao = $$$VARPregaoLinhaSituacaodoPregao(objPregaoLinha)	
		$$$End
	}
	
	
	if (pFormato = 2) {
		if (idSituacao '= ""){
			quit $$SQLGetUserParamDesc^VARSQL("VARSITUACAOPREGAO~"_idSituacao)
		} else {
			quit ""
		}
	}
	
	quit idSituacao
	
	
isItemPregaoAssociadoAta(pYKEY)
	quit:(pYKEY = "") $$$NO
 
	new idPregao, idPregaoLinha
	
	set idPregao 	  = $$$KEY1(pYKEY)
	set idPregaoLinha = $$$KEY2(pYKEY)
	
	if $data(^VARAtaLinhas(YM,4,$$Index^COMUtilIndex(idPregao,"VARAtaLinha"),idPregaoLinha)) {
		quit $$$YES
	}
 
	quit $$$NO
	
getAtaFromPregao(pYKEY)
	quit:(pYKEY = "") $$$NO
 
	new idPregao, idPregaoLinha, idAta, idAtaLinha, chaveAta
	
	set idPregao 	   = $$$Index($$$KEY1(pYKEY))
	set idPregaoLinha  = $$$KEY2(pYKEY)
	
	set chaveAta = ""
	
	$$$Order5(^VARAtaLinhas,YM,4,idPregao,idPregaoLinha,idAta)
		$$$Order6(^VARAtaLinhas,YM,4,idPregao,idPregaoLinha,idAta,idAtaLinha)		
			
			set chaveAta = idAta_$$$COMMA_idAtaLinha
			
		$$$End
	$$$End
 
	quit chaveAta
]]></Routine>
</Export>