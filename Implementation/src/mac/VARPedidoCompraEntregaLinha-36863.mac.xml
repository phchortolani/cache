<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPedidoCompraEntregaLinha" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPedidoCompraEntregaLinha
#include COMSYS
#include COMConst
#include VARConst

LoadGrid(pid="",pObjEntrega)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	OnAfterDataFields^VARPedidoCompraEntrega.mac
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------- 
	new YFORM, YKEY, YAUSWAHL, YFELD
	set YFORM = "VARPedidoCompraEntregaLinha"
	set YFELD = ""
	set YKEY  = $get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = 1
	set $$$COMGridEditParameterGridName(YAUSWAHL)	   = YFORM
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "VARPedidoCompraEntrega"
	
	// Se form master (VARPedidoCompraEntrega) for somente-leitura 
	// ou campo "Entrega Uniforme" for marcado, então grid é somente-leitura:
	if (($$$WWW120AuthorizationToModifyData(YVOR) = 5) || ($$$VARPedidoCompraEntregaEntregauniforme(pObjEntrega) = 1) ) {
		set $$$COMGridEditParameterEnabled(YAUSWAHL) = 0
	}
	else {
		set $$$COMGridEditParameterEnabled(YAUSWAHL) = 1
	}
	
	do ^COMGridEdit31
	
	quit
	
OnExecuteLinhaPedido()
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Verifica se a linha do pedido já foi usada em outra linha da mesma entrega,
	;	retornando 1 se não há erro, e 0 se não há erro na linha do pedido. 
	;
    ; Chamado por:
    ;	Core rule no. 1 do campo (1)-Linha do Pedido de Compra no form VARPedidoCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------- 
	new strStatus, idPedidoCompra, numEntregaCorrente, numSeqLinhaEntregaCorrente, numLinhaPedidoCorrente,
		numSeqLinhaEntrega, objLinhaEntrega, numLinhaPedido
	
	set strStatus = $$$OK	
	set YKEY = $get(YKEY)
	
	set idPedidoCompra = $$$KEY1(YKEY)
	set numEntregaCorrente = $$$KEY2(YKEY)
	
	set numSeqLinhaEntregaCorrente = $$$KEY3(YKEY) 
	quit:(numSeqLinhaEntregaCorrente = "") strStatus
	
	set numLinhaPedidoCorrente = $piece($get(YFELD),Y,1) 
	quit:(numLinhaPedidoCorrente = "") strStatus
	
	//do AlertInScript^COMConst(idPedidoCompra_","_numEntregaCorrente_","_numSeqLinhaEntregaCorrente_","_numLinhaPedidoCorrente)
 
	// Se a Linha do pedido já foi usada em outra linha desta mesma entrega:
	$$$Order4(^VARPedidoCompraEntregaLinha,YM,idPedidoCompra,numEntregaCorrente,numSeqLinhaEntrega)
		set objLinhaEntrega = $get(^VARPedidoCompraEntregaLinha(YM,idPedidoCompra,numEntregaCorrente,numSeqLinhaEntrega,1))		
		set numLinhaPedido = $piece(objLinhaEntrega,Y,1)
		if ((numSeqLinhaEntrega '= numSeqLinhaEntregaCorrente) && (numLinhaPedido = numLinhaPedidoCorrente)) {
			set strStatus = $$$NO
			do AlertInScript^COMConst("Este produto já foi inserido nesta entrega. Veja linha Seq: "_numSeqLinhaEntrega_".")
			quit
		}
		quit:(strStatus = $$$NO)
	$$$End
	
	quit strStatus
	
OnBlurQuantidade(pYKEYEntregaLinha,pYFELDEntregaLinha)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (2)-Quantidade no form VARPedidoCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------- 
	new updateGrid, idPedidoCompra, objEntregaLinha, arrEntregaLinhas, msg, idPedidoCompraLinha, 
		objPedidoCompraLinha, qtyPedido, percentual, percentualCalculado, qtdEntregaLinha, strStatus
	
	set updateGrid = $$$YES
	
	quit:($get(pYKEYEntregaLinha) = "") $$$YES
	
	set idPedidoCompra = $$$KEY1(YKEY)
	quit:($$$KEY3(YKEY) = "") ""
	
	do GetGridContents^COMGridEdit31Interface(.arrEntregaLinhas, "VARPedidoCompraEntregaLinha")
	set objEntregaLinha = $get(arrEntregaLinhas($p(YKEY,YKOMMA,1),$p(YKEY,YKOMMA,2),$p(YKEY,YKOMMA,3)))
	quit:($get(objEntregaLinha) = "") $$$YES
	quit:('$data(arrEntregaLinhas) ) $$$YES
	
	set idPedidoCompraLinha = $piece(objEntregaLinha,Y,1)
	if (idPedidoCompraLinha = "") {
		set $piece(pYFELDEntregaLinha,Y,2) = "" // Zera quantidade
		set $piece(pYFELDEntregaLinha,Y,8) = "" // Zera percentual
 
		set msg = "Primeiro escolha o produto."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_"7"_"',1,1,1);"
		$$$EndScript()
		
		quit ""
	}
	
	set objPedidoCompraLinha = $get(^VARPedidoCompraLinha(YM,idPedidoCompra,idPedidoCompraLinha,1))
	set qtyPedido = $piece(objPedidoCompraLinha,Y,4)
	
	set percentual = $piece(objEntregaLinha,Y,8)
 
	set qtdEntregaLinha = $piece(objEntregaLinha,Y,2)
	if (+qtdEntregaLinha <= 0) {
		set $piece(pYFELDEntregaLinha,Y,2) = "" // Zera quantidade
		set $piece(pYFELDEntregaLinha,Y,8) = "" // Zera percentual
 
		set msg = "Quantidade deve ser maior que zero."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit $$$YES
	}
 
	// from On execute rule: ------------
	set strStatus = $$ChecarQuant(pYKEYEntregaLinha,pYFELDEntregaLinha)
	
	if ($$$ISERR(strStatus)){
		set $piece(pYFELDEntregaLinha,Y,2) = "" // Zera quantidade
		set $piece(pYFELDEntregaLinha,Y,8) = "" // Zera percentual
 
		set msg				= $piece(strStatus,Y,2)
		do AlertInScript^COMConst(msg)
 
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit $$$NO
	}
	// -----------------
 
 
	// Altera o campo de percentual:
	set percentualCalculado = (qtdEntregaLinha / qtyPedido) * 100
	
	if (percentual '= percentualCalculado) {
		set $piece(pYFELDEntregaLinha,Y,8) = percentualCalculado
	}	
	set arrEntregaLinhas($p(YKEY,YKOMMA,1),$p(YKEY,YKOMMA,2),$p(YKEY,YKOMMA,3)) = objEntregaLinha
		
	quit $$$YES
	
	
OnBlurPerc(pYKEYEntregaLinha,pidRow,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (8)-Percentual no form VARPedidoCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------- 
	new updateGrid, idPedidoCompra, objEntregaLinha, idPedidoCompraLinha, msg, objPedidoCompraLinha, 
		qtyPedido, percentual, percentualCalculado, qtdEntregaLinha, strStatus, arrEntregaLinhas
	
	set updateGrid = $$$YES
	
	quit:($get(pYKEYEntregaLinha) = "") ""
	
	set idPedidoCompra = $$$KEY1(YKEY)
	quit:($$$KEY3(YKEY) = "") ""
	
	do GetGridContents^COMGridEdit31Interface(.arrEntregaLinhas, "VARPedidoCompraEntregaLinha")
	set objEntregaLinha = $get(arrEntregaLinhas($p(YKEY,YKOMMA,1),$p(YKEY,YKOMMA,2),$p(YKEY,YKOMMA,3)))
	quit:($get(objEntregaLinha) = "") ""
	quit:('$data(arrEntregaLinhas) ) ""
	
	set idPedidoCompraLinha = $piece(objEntregaLinha,Y,1)
	if ( idPedidoCompraLinha = "") {
		set $piece(pYFELD,Y,8) = "" // Zera percentual
		set $piece(pYFELD,Y,2) = "" // Zera quantidade
 
		set msg = "Primeiro escolha o produto."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_"7"_"',1,1,1);"
		$$$EndScript()
		
		quit ""
	}
	
	set objPedidoCompraLinha = $get(^VARPedidoCompraLinha(YM,idPedidoCompra,idPedidoCompraLinha,1))
	set qtyPedido = $piece(objPedidoCompraLinha,Y,4)
	
	set percentual = $piece(objEntregaLinha,Y,8)
 	if (+percentual <= 0) || (+percentual > 100) {
		set $piece(pYFELD,Y,8) = "" // Zera percentual
		set $piece(pYFELD,Y,2) = "" // Zera quantidade
 
		set msg = "Percentual deve ser maior que zero e menor que 100."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit ""
	}
 
	set qtdEntregaLinha = $NORMALIZE(qtyPedido * (percentual/100),0) // Arredondando
	if (qtdEntregaLinha = 0) { 
		set qtdEntregaLinha = 1 
	}

	if ( qtdEntregaLinha < 0 ) { 
		set qtdEntregaLinha = 0 
	}
 
	set $piece(pYFELD,Y,2)=qtdEntregaLinha
	set $piece(objEntregaLinha,Y,2) = qtdEntregaLinha
 
	set strStatus = $$ChecarQuant(pYKEYEntregaLinha,pYFELD)
	
	if ($$$ISERR(strStatus)) {
		set msg = "O percentual de "_$piece(pYFELD,Y,8)_"% causaria esta quantidade em excesso. Digite um percentual menor."
		set $piece(pYFELD,Y,8) = "" // Zera percentual
		set $piece(pYFELD,Y,2) = "" // Zera quantidade
 
		set msg = $piece(strStatus,Y,2)_YCR_msg
		do AlertInScript^COMConst(msg)
 
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit $$$NO
	}
		
	set percentualCalculado = (qtdEntregaLinha / qtyPedido) * 100
	if (percentual '= percentualCalculado) {
		
		set $piece(objEntregaLinha,Y,8) = percentualCalculado
		set $piece(pYFELD,Y,8) = percentualCalculado
 
		set msg = 	"Você digitou um percentual de "_$fnumber(percentual,".")_"%."_$char(10)_
					"Este percentual corresponde a uma quantidade de "_$fnumber((qtyPedido * (percentual/100)),".")_"."_$char(10)_
					"Esta quantidade será arredondada para "_$fnumber(qtdEntregaLinha,".")_"."_$char(10)_
					"Isto causará a alteração do percentual para "_$fnumber(percentualCalculado,".")_"%."
		do AlertInScript^COMConst(msg)
	} 
	quit percentualCalculado	

ChecarQuant(pYKEYEntregaLinha,pYFELDEntregaLinha)
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Verifica se a quantidade nas diversas entregas não excede a quantidade do pedido.
	;	Retorna 1 se não houver erro e 0 se houver erro na quantidade da linha da entrega.
	;
    ; Chamado por:
    ;	CriaEntregaUniforme^VARPedidoCompraEntrega.mac
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------- 
	new strStatus, idPedidoCompra, numEntregaCorrente, numSeqLinhaEntregaCorrente, 
		numLinhaPedidoCorrente, qtdLinhaEntregaCorrente, qtdOutrasEntregas, numEntrega,
		numSeqLinhaEntrega, objLinhaEntrega, numLinhaPedido, qtd, objPedidoCompraLinha,
		qtdLinhaPedido, idProduto, restoAEntregar
	
	set strStatus = $$$OK
		
	set idPedidoCompra = $piece($get(pYKEYEntregaLinha),YKOMMA,1)
	set numEntregaCorrente = $piece($get(pYKEYEntregaLinha),YKOMMA,2)
	set numSeqLinhaEntregaCorrente = $piece($get(pYKEYEntregaLinha),YKOMMA,3)
	
	set numLinhaPedidoCorrente = $piece($get(pYFELDEntregaLinha),Y,1)
	if (numLinhaPedidoCorrente = "") {
		set strStatus = "0~Não foi possível determinar o número da linha corrente do pedido corrente."
		quit strStatus
	}
	
	set qtdLinhaEntregaCorrente = $piece($get(pYFELDEntregaLinha),Y,2)
	
	//do AlertInScript^COMConst(idPedidoCompra_","_numEntregaCorrente_","_numSeqLinhaEntregaCorrente_","_numLinhaPedidoCorrente)
 
	// Quantidade em outras entregas:
	set qtdOutrasEntregas = 0
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARPedidoCompraEntregaLinha(YM,idPedidoCompra,numEntrega))
		quit:(numEntrega = "")
		
		set numSeqLinhaEntrega = ""
		for {
			set numSeqLinhaEntrega = $order(^VARPedidoCompraEntregaLinha(YM,idPedidoCompra,numEntrega,numSeqLinhaEntrega))
			quit:(numSeqLinhaEntrega = "")
			
			set objLinhaEntrega = $get(^VARPedidoCompraEntregaLinha(YM,idPedidoCompra,numEntrega,numSeqLinhaEntrega,1))
			set numLinhaPedido = $piece(objLinhaEntrega,Y,1)
			set qtd = $piece(objLinhaEntrega,Y,2)
			
			if ((numEntrega = numEntregaCorrente) && (numSeqLinhaEntrega = numSeqLinhaEntregaCorrente)) {
				continue
			}
			if (numLinhaPedido = numLinhaPedidoCorrente)  {
				set qtdOutrasEntregas = qtdOutrasEntregas + qtd
			}
		}
	}
	
	set objPedidoCompraLinha = $get(^VARPedidoCompraLinha(YM,idPedidoCompra,numLinhaPedidoCorrente,1))
	set qtdLinhaPedido = $piece(objPedidoCompraLinha,Y,4)
	set idProduto = $piece(objPedidoCompraLinha,Y,1)
	set restoAEntregar = qtdLinhaPedido - qtdOutrasEntregas
	
	//do AlertInScript^COMConst(qtdLinhaPedido_","_qtdOutrasEntregas_","_restoAEntregar)
	
	if (qtdLinhaEntregaCorrente > restoAEntregar) {
		set strStatus = "0"_Y_"Não é possível definir entrega para o produto "_idProduto_", pois a quantidade no pedido de compra ("_qtdLinhaPedido_") será excedida pela quantidade nas entregas ("_(qtdOutrasEntregas + qtdLinhaEntregaCorrente)_")."_Y_(qtdOutrasEntregas + qtdLinhaEntregaCorrente)_Y_qtdLinhaPedido_Y_idProduto 
	}
	else {
 		set strStatus = $$$YES
	}
	
	quit strStatus

GetQuantEntr(pidPedidoCompra, pidPedidoCompraLinha)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna a quantidade de entregas para a linha do pedido informada.
    ;
    ; Chamado por:
    ;	Função CriaEntregaUniforme^VARPedidoCompraEntrega.mac
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------- 
	new qtdEntregas, numEntrega, numLinhaEntrega, objLinhaEntrega, qtd
	
	set qtdEntregas = 0
 
	if ($get(pidPedidoCompra) = "") || ($get(pidPedidoCompraLinha) = "") {
		quit 0
	}
 	
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARPedidoCompraEntregaLinhas(YM,1,$$^WWWUMLAU(pidPedidoCompra,1),pidPedidoCompraLinha,numEntrega))
		quit:(numEntrega = "")
		
		set numLinhaEntrega = ""
		for {
			set numLinhaEntrega = $order(^VARPedidoCompraEntregaLinhas(YM,1,$$^WWWUMLAU(pidPedidoCompra,1),pidPedidoCompraLinha,numEntrega,numLinhaEntrega))
			quit:(numLinhaEntrega = "")
									
			set objLinhaEntrega = $get(^VARPedidoCompraEntregaLinha(YM,pidPedidoCompra,numEntrega,numLinhaEntrega,1))
			
			set qtd = $piece(objLinhaEntrega,Y,2)
			
			set qtdEntregas = qtdEntregas + qtd
		}
	}

	quit qtdEntregas
	
	
OnBlurProduto(pintCol,pintRow,&pobjYFELD)
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Retorna o no. do seq. da linha do pedido, a partir do código do produto.
	;
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (7)-Produto no form VARPedidoCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;------------------------------------------------------------------------------		
	new idProduto, seqPedidoCompra
	set idProduto = $piece(pobjYFELD,Y,7)
	quit:(idProduto = "")
	
	set seqPedidoCompra = $piece($get(^COMTempList(YM,YBED,"VARTempPedidoCompraLinha",idProduto,1)),Y,2)
	
	set $piece(pobjYFELD,Y,1) = seqPedidoCompra
	
	quit
]]></Routine>
</Export>