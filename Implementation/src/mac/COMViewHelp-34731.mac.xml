<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewHelp" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewHelp
#include COMSYS
#include WWWConst
#include COMConst
	
ShowHelp(pdomField="",pidField="") 
    ;-------------------------------------------------------------------------------
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
	; 01-Sep-2008	shobby	SRBR014976: Support changes in fonts.
	; 15-Jul-2005	shobby	SR12754:Replaced LANGUAGE global (not always reliable)
    ;-------------------------------------------------------------------------------
	new intSize,strGIF,strHTML,strName
	
	set strHTML = ""
	if pidField'="" {
		set strHTML = $$GetHelpForField(pidField)
	} else {
		set strHTML = $$$WWW127HelpText($get(^WWW127(0,"COMViewSearch","M",1,SPRACHE,1)))
		if strHTML="" set strHTML = $$$WWW127HelpText($get(^WWW127(0,"COMViewSearch","M",1,"EN",1)))
		for {
			quit:'$find(strHTML,"***")
			
			set strGIF  = $piece(strHTML,"***",2)
			set strGIF  = " "_strGIF_".gif"
			set strHTML = $piece(strHTML,"***",1)_strGIF_$piece(strHTML,"***",3,999)
		}
	}
	
	for  {
		quit:'$find(strHTML,".gif")	
		
		set strGIF  = $piece(strHTML,".gif",1)
		set strName = ""
		for {
			quit:strGIF=""
			quit:$find(" .,|",$extract(strGIF,$length(strGIF)))
			
			set strName = $extract(strGIF,$length(strGIF))_strName
			set strGIF  = $extract(strGIF,1,$length(strGIF)-1)
		}
		quit:strName=""
		
		set strGIF  = "<img src="_YGIF_strName_".GIF>"
		set strHTML = $piece(strHTML,strName_".gif",1)_strGIF_$piece(strHTML,strName_".gif",2,999)
	}
	
	set intSize = ($length(strHTML)/70*15)+($length(strHTML,"|")*15)+20
	set intSize = intSize\1
	if intSize>500 set intSize = 500
	
	set strHTML = "<DIV style='position:absolute;width:100%;overflow-y:scroll;height:100%;'>"_strHTML
	set strHTML = $$Replace^COMUtilStr(strHTML,"|","<br>")
	set strHTML = strHTML_"</DIV>"
	
	write "Plan.document.body.style.border='1px solid orange';"
	write "Plan.document.body.style.overflowY='auto';"
	write "Plan.document.body.style.fontFamily='"_$$FontFace^WWW012()_"l';"
	write "Plan.document.body.style.fontSize='12px';"
	write "Plan.document.body.style.color='Navy';"
	write "Plan.document.body.style.backgroundColor=NetBorderColor;"
	write "Plan.document.body.innerHTML='"_$zcvt(strHTML,"o","JS")_"';"
	write "var obj=document.getElementById('"_pdomField_"');"
	write "Plan.show(20,20,500,"_intSize_",obj);"
	
	quit $$$OK
	
	
GetHelpForField(pidField)
    ;-------------------------------------------------------------------------------
    ; Returns help fields for forms
    ;
    ; Returns:String
    ;
    ; History:
	; 25-May-2011	shobby	SR16925.2: HelpText->CustomHelpText
	; 25-May-2011	shobby	SR16925.2: IndividualHelpText->CustomHelpText
    ; 18-Sep-2008	shobby	BR014966: Support language texts for customisation help text.
    ; 19-Aug-2007	shobby	SRAdhoc: Replaced call to GetFormDescription with WWWFORMNAME
	; 21-Sep-2006	HeberB	SRBR014196: Added customized help text to F1 at COMView 
	; 15-Jul-2005	shobby	SR12754:Replaced LANGUAGE global (not always reliable)
    ; 10-Feb-2005	Paul K	Added help for forms.
    ;-------------------------------------------------------------------------------
	new arrUsed,blnPoct,idClass,idForm,objWWW121D,objWWW122D
	new strCt,strFieldNumber,strFieldType,strForm,strHTML
	
	set strHTML = ""
	set idClass = $get(^CacheTempView(YUSER,"Class"))
 
	; FIXME : <GRF> GetRelation^COMViewUtils is a function.
	do GetRelation^COMViewUtils(.idClass,.pidField)
	set idForm = $order(^WWW120s(0,1,$$$Index(idClass),""))
	
	if pidField="" set pidField = "M1" ;form definition
	
	set idForm = ""
	for {
		set idForm = $order(^WWW120s(0,1,$$$Index(idClass),idForm))
		quit:idForm=""
		
		set strForm = ""
		set strFieldType   = $extract(pidField,1)
		set strFieldNumber = $extract(pidField,2,99)
		if strFieldType = "P" {	// primary key
			set objWWW121D = $get(^WWW121D(0,idForm,strFieldNumber,YM,1))
			set blnPoct = $$$WWW121DHideAlphalincHelpText(objWWW121D)     ;SR16925.2
			set strCt   = $$$WWW121DCustomHelpText(objWWW121D)            ;SR16925.2
		
		} else {					// data field		
		 	set objWWW122D = $get(^WWW122D(0,idForm,strFieldNumber,YM,1))
			set blnPoct = $$$WWW122DHideAlphalincHelpText(objWWW122D)     ;SR16925.2
			set strCt   = $$CustomHelpText^WWW122D(objWWW122D)            ;SR16925.2
		}
		
		if (strCt'="") && (blnPoct) {		
			set strForm = strCt
		} else {							
		 	set strForm = $$$WWW127HelpText($get(^WWW127(0,idForm,strFieldType,strFieldNumber,SPRACHE,1)))_"<BR>"_strCt
		}
 		
		if strForm="" set strForm = $$$WWW127HelpText($get(^WWW127(0,idForm,strFieldType,strFieldNumber,"EN",1)))
		if (strForm'="") && '$data(arrUsed($extract(strForm,1,255))) {
			set arrUsed($extract(strForm,1,255)) = ""
			set strHTML = strHTML_"||<b title='"_idForm_"'>"_$$^WWWFORMNAME(idForm)_"</b>:|"_strForm
		}
	}
 
	if strHTML="" set strHTML = "||"_$$$Text("Com00125")  ; "No help text available"
	if pidField'="M1" {
		set strHTML = "<B title='"_pidField_"'>"_$$$Text($listbuild("Com00159",$$^WWWFELDNAME(idClass,strFieldType,strFieldNumber)))_"</b>"_strHTML
	} ; "Help for Field : %1"
	quit strHTML
	
	
FormHelp(pblnShift="false")
    ;-------------------------------------------------------------------------------
    ; Shows the popup help text for the current form (called from COMView)
    ;
    ; Params:
    ;	pblnShift :	Javascript 'true' or 'false' determines whether to show the help
    ;				text for the current form or try for a form that might have a bit
    ;				more useful information.  For example form INReqDemand only used
    ;				as a place holder for a COMView.
    ; Returns:
    ;
    ; History:
    ; 08-Jul-2011	GRF		SR16925: idUSER not idUser in new
    ; 23-Jun-2009	shobby	SR16530: Use a different value for YUSER or it will
    ; 					blow away any ^CacheTempView settings.
	; 15-Jul-2005	shobby	SRBR014679: Created.
    ;-------------------------------------------------------------------------------
    new idClass,idForm,idUSER
    
    set idForm = $get(YFORM)
 
    if (idForm'="") && (pblnShift="true") {   ; If this is a form that 
	    if '$data(^WWW122(0,idForm)) {
	    	set idClass = $$$WWW120ClassUsedInForm($get(^WWW120(0,idForm,1)))
	    }
	    if (idClass'="") && ($data(^WWW120(0,idClass))) {
		    set idForm = idClass
	    }
    }
    if idForm'="" {
		$$$StartScript()
		set idUSER = $$GetChildUser^WWWUSER(YUSER)
		write "subWindow('www.cls?EP=WWWHELP&YFORM="_idForm_"&YUCI="_$get(YUCI)_"&YBED="_$get(YBED)_"&YM="_$get(YM)
		write "&YUSER="_$get(idUSER)_"&YTRAKT="_$get(YTRAKT)_"&YANZ="_$get(YANZ)_"&YNAME="_$get(YNAME)
		write "&YSEITE="_$get(YSEITE)_"','HELPFRAME"_$get(idUSER,1)_"');"
		$$$EndScript()
    }
	quit
	
]]></Routine>
</Export>