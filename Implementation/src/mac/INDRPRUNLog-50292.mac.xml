<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPRUNLog" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPRUNLog
	
#def1arg TempStore(%1) ^CacheTemp(YUCI,YUSER,%1)
#include INConst
#include COMSYS 
	
ClearLog()
    ;-------------------------------------------------------------------------------
    ;
    ;-------------------------------------------------------------------------------
	new strSeq,strStatus
	
	set strSeq = ""
	for {
		set strSeq = $order(^INDRPRUNLOGs(0,3,YUSER,strSeq))
		quit:strSeq=""
		
		set strStatus = $$KILL^COMUtils("INDRPRUNLOG",strSeq)	
	}
	quit
	
	
Log(pstrMsg="",pidBackJOB) 
    ;-------------------------------------------------------------------------------
    ; Log Change
    ; 
    ; Variables:
    ;  BETRIEB
    ;  ART
    ;  BGJOB
    ; 
    ; Returns:
    ;
    ; History:
    ; 20-Aug-2009	GRF		SR16397: missed variables added to new
    ; 12-Mar-2009	PPP		SR16397: Added Logging for the process
    ;-------------------------------------------------------------------------------
	new idItem,idLocn,idSeq,strData,strStatus
	
	//set intSeq = $order($$$TempStore("DRPRun",""),-1)+1
	//if $data($$$TempStore("DRPRun",intSeq)) for  quit:'$data($$$TempStore("DRPRun",intSeq))  set intSeq=intSeq+1
	//set $$$TempStore("DRPRun",intSeq) = $zdt($h,3)_Y_pstrMsg
	
	; D216 $$$INVORGDontLogDRPProcesses()
	if (+$piece($get(^INVORG(0,0,1)),Y,216)=$$$YES) && '$get(VORG(5)) {
		quit				//Logging Not Enabled and
							//NOT Manual Display Calc View of Suppy-Demand screen (INDRPRUN)
	}
	set idLocn = $get(BET) 
	set:idLocn="" idLocn = $get(BETRIEB) 
	set:idLocn="" idLocn = " "
	
	set idItem=$get(ART) set:idItem="" idItem=" "
	set:$get(pidBackJOB)="" pidBackJOB = +$get(BGJOB)
	
	//quit:(idLocn="")||(idItem="")
 
	set idSeq   = $$^WWWNEXT("INDRPRUNLOG")
	set strData = $translate($horolog,",",Y)_Y_idLocn_Y_idItem_Y_YBED_Y_+$get(pidBackJOB)_Y_pstrMsg_Y_YUSER
	//set OK=$$^WWWSPEI("INDRPRUNLOG",YKEY,pstrMsg,1)
	set strStatus = $$$Save("INDRPRUNLOG",idSeq,strData,1)
	
	quit
 
 
ShowCalc()
    ;-------------------------------------------------------------------------------
    ; Show Calculation
    ;
    ; Returns:
    ;
    ; History:
    ; 19-Feb-2009	PPP		Created
    ;-------------------------------------------------------------------------------
	new intSeq,lstHeader,objLog,strMsg,strDate,strTime
 
 	set lstHeader = $listbuild("Date","Time","Message")
	do Start^COMTable(lstHeader,,1)
 
	//^INDRPRUNLOGs(0,3,10007515821,"AUS0000002")=
	set intSeq = ""
	for {
		set intSeq = $order(^INDRPRUNLOGs(0,3,YUSER,intSeq))
		quit:intSeq=""
		
		set objLog  = $get(^INDRPRUNLOG(0,intSeq,1))
		set strMsg  = $piece(objLog,Y,7)
		set strDate = $zdate($piece(objLog,Y,1),3)
		set strTime = $ztime($piece(objLog,Y,2),3)
		
		do AddMessage(strDate_Y_strTime_Y_strMsg)
	}
	
	do Stop^COMTable()
	quit
 
 
AddMessage(pstrMsg="",pstrKey="")
    ;-------------------------------------------------------------------------------
    ; Show Messages
    ;
    ; Returns:
    ;
    ; History:
    ; 19-Feb-2009	PPP		Creation :SR
    ;-------------------------------------------------------------------------------
    new idForm,intCnt
    
    set idForm = ""
    if pstrKey'="" set idForm = "INDRPRUN"
	do NewLine^COMTable()
 
	for intCnt=1:1:$length(pstrMsg,Y) {
		do InsertCell^COMTable($piece(pstrMsg,Y,intCnt),idForm,pstrKey)
	}
	do EndLine^COMTable()
 
	quit
 
 
 
Verbose(penumType,pSATZ)
    ;-------------------------------------------------------------------------------
    ; Return the Variable data in a Verbose form
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Feb-2009	PPP		Creation :SR
    ;-------------------------------------------------------------------------------
	new dteDue,fltQty,idLocn,idRef,strRet
	
	set strRet = ""
	
	if $translate($get(pSATZ),Y)="" set strRet = "No Data" quit
	
	set idLocn  = $piece(pSATZ,Y,1)
	set idRef   = $piece(pSATZ,Y,2)
	set fltQty  = $piece(pSATZ,Y,5)
	set dteDue  = $piece(pSATZ,Y,3)
	
	set strRet = $select(penumType=1:"Demand ",1:"Supply ")_"Qty:"_fltQty_" for Loc:"_idLocn_" Ref:"_idRef_" Due:"_dteDue
	
	quit strRet
	
]]></Routine>
</Export>