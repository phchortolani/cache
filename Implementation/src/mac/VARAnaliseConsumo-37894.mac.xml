<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAnaliseConsumo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARAnaliseConsumo
#include COMSYS
#include INConst
#include VARConst
 
MostraCMM() 
	new Item, Mes, Local, Ano
		
	set Item  = $get(VORG(1))
	set Mes   = $get(VORG(2))
	set Local = $get(VORG(3))
	set Ano   = $get(VORG(4))
	
	if ((Mes '= "") && (Ano = "")) {	
		$$$Alert("Favor selecionar o ano para o mês indicado.")
	
	} elseif ((Mes = "") && (Ano '= "")) {	
		$$$Alert("Favor selecionar o mês para o ano indicado.")
	}	
	
	set ^VARTempAnaliseConsumo(YBED) = Item_Y_Mes_Y_Local_Y_Ano
	
	do GoToForm^COMUtilForm("VARAnaliseConsumo",Item)
 	
 	quit
 
SetVariables
	new lastRecord, newRecord
 	
 	if $piece(YKEY,",",2) = "Link" {
	 	set VORG(1) = $piece(YKEY,",",1)
	 	set VORG(2) = ""
	 	set VORG(3) = ""
	 	set VORG(4) = ""
		set ^VARTempAnaliseConsumo(YBED) = VORG(1)_Y_VORG(2)_Y_VORG(3)_Y_VORG(4)
 	}
 	
 	if $extract(YAUSWAHL,1,1) = "D" {
	 	set VORG(1) = $piece(YAUSWAHL,",",2)
	 	set VORG(2) = $piece(YAUSWAHL,",",3)
	 	set VORG(3) = $piece(YAUSWAHL,",",4)
	 	set VORG(4) = $piece(YAUSWAHL,",",5)
		set ^VARTempAnaliseConsumo(YBED) = VORG(1)_Y_VORG(2)_Y_VORG(3)_Y_VORG(4)
 	} 	
 	
 	quit:(YKEY = "NO")
 	quit:((YAUSWAHL = "") && ($get(VORG(1)) = ""))
 	
	set lastRecord = ""
	set lastRecord = $order(^VARTempAnaliseConsumoHistory(YBED,lastRecord),-1)
	
	if lastRecord '= "" {
		set newRecord = lastRecord + 1
	}
	elseif lastRecord = "" {
		set newRecord = 1
 		set ^VARTempAnaliseConsumoHistory(YBED,newRecord) = VORG(1)_Y_VORG(2)_Y_VORG(3)_Y_VORG(4)
 		quit		
	}
 
	if (YAUSWAHL '= "") {	
 		set ^VARTempAnaliseConsumoHistory(YBED,newRecord) = $piece(YAUSWAHL,",",2)_Y_$piece(YAUSWAHL,",",3)_Y_$piece(YAUSWAHL,",",4)_Y_$piece(YAUSWAHL,",",5)
	
	} else {
 		set ^VARTempAnaliseConsumoHistory(YBED,newRecord) = VORG(1)_Y_VORG(2)_Y_VORG(3)_Y_VORG(4)
	}
 	
 	quit 	
	
 
PainelGeral()
	
	kill ^CacheTempAnaliseConsumoXLS
	
 	//Parâmetros que vem da tela
	new Item, Mes, Local, Ano
 
	set Item = $piece($get(^VARTempAnaliseConsumo(YBED)),Y,1)	
	if Item = "" set Item = $get(VORG(1))
 
	set Mes = $piece($get(^VARTempAnaliseConsumo(YBED)),Y,2)
	if Mes = "" set Mes = $get(VORG(2))
	
	set Local = $piece($get(^VARTempAnaliseConsumo(YBED)),Y,3)
	if Local = "" set Local = $get(VORG(3))
	
	set Ano = $piece($get(^VARTempAnaliseConsumo(YBED)),Y,4)
	if Ano = "" set Ano = $get(VORG(4))
			
	quit:(Item = "")
 
	set ^VARTempAnaliseConsumo(YBED) = Item_Y_Mes_Y_Local_Y_Ano
	
	do ImprimeCabecalho(Item,Local)
	
	if ((Mes '= "") && (Ano '= "")) {
		if $data(^VARCMMLinha(YM,Item,Ano,Mes)) = 0 {
			
			set DescMes = $piece($get(^WWW101(0,"COMMONTHSOFYEAR","PT",Mes,1)),Y,1)
			w "<font color = red><br /><strong>&nbsp;Não existem consumos para o período consultado</strong></font>, ou os dados ainda não foram gerados.<br /><br />"
			quit
		}
	}
	
 	if ((Item '= "") && (Mes = "") && (Local = "") &&  (Ano = "")) {
  		do ImprimeConsumoRedeHistorico(Item)
 	} 
	
	elseif ((Item '= "") && (Mes '= "") && (Local = "") && (Ano '= "")) {		
 		do ImprimeConsumoRedeMes(Item,Mes,Ano)
 	}
 
	elseif ((Item '= "") && (Mes = "") && (Local '= "") &&  (Ano = "")) {
 		do ImprimeConsumoLocalHistorico(Item,Local)
 	}
 		 	
 	elseif ((Item '= "") && (Mes '= "") && (Local '= "") && (Ano '= "")) {
		do ImprimeConsumoLocalMes(Item,Mes,Local,Ano)
 	}
 	
 	quit
 	 	
ImprimeCabecalho(Item,Local)
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Imprime o cabeçalho do relatório
	;
    ; Chamado por:
    ;	VARAnaliseConsumo
    ; 
    ; Histórico:
    ; 	16-Dez-2016		FC		LEAN-12: Alteração da função para recuperar o CMML e o CMMRede
    ;-------------------------------------------------------------------------------
	
 	new objItem, DescricaoItem, Unidade, DescricaoUnidade, Grupo, DescricaoGrupo, Data, Hora, 
 		CustoMedio, CMMRede, CMMLocal
 	
 	set objItem = $get(^INART(YM,Item,1))
 	
	set DescricaoItem    = $piece(objItem,Y,1)
	set Unidade          = $piece(objItem,Y,40)
	set Grupo 		     = $piece(objItem,Y,30)
	set CustoMedio       = $piece(objItem,Y,140)
	
	set CMMRede	         = $$GetCMM^VARReposicao(Item) ;LEAN-12
	if Local '= "" 	set CMMLocal = $$GetCMM^VARReposicao(Item,Local) ;LEAN-12
	
	set DescricaoUnidade = $piece($get(^WWW101(YM,"EINHEIT","PT",Unidade,1)),"~",1)
	set DescricaoGrupo   = $piece($get(^INItemGroup(YM,Grupo,1)),"~",2)
	
	write "<hr>" //Desenha 1ª Linha Horizontal
	write "&nbsp;<strong>Produto: "_Item_" - "_DescricaoItem_"</strong>"
	write "<hr>" //Desenha 2ª Linha Horizontal	
	write "<table style=font-size:12px>" //Detalhes do Item
	write "<tr>"
	write " <td><strong>CMM (Rede):</strong></td><td><strong>"_$$^WWWTR(0,12,CMMRede)_"</strong></td>"
	write "</tr>"
	
	if Local '= "" write "<tr> <td><strong>CMML (Local):</strong></td><td>"_$$^WWWTR(0,12,CMMLocal)_"</td></tr>"
	
	write "<tr> <td>Grupo:</td><td>"_Grupo_" - "_DescricaoGrupo_"</td></tr>"
	write "<tr> <td>Unidade:</td><td>"_DescricaoUnidade_"</td></tr>"
	write "<tr> <td>Custo Médio:</td><td>"_$$FormatCurrency^COMTable(CustoMedio)_"</td></tr>"
	write "</table>"
	
	quit
 
ImprimeConsumoRedeHistorico(Item)
	//Tabela
	new year, month, idLocation, idTransaction, qty, qtyMonth, qtyTotal,
   		lstHeader
   		
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Ano","Período","Quant. Consumida na Rede no Período","Detalhes")
  	
  	// BASIC-12: Montagem do Cabeçalho
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,0,1) = "Ano~Período~Quant. Consumida na Rede no Período"
 
	do Start^COMTable(lstHeader,"Histórico do consumo mensal do produto na rede")	  
	
	set qtyTotal = 0
	
	set count = 0
		
	$$$Order3(^VARCMMLinha,YM,Item,year)
			
		$$$Order4(^VARCMMLinha,YM,Item,year,month)
			
			set qtyMonth = 0
				
			$$$Order5(^VARCMMLinha,YM,Item,year,month,idLocation)
					
				$$$Order6(^VARCMMLinha,YM,Item,year,month,idLocation,idTransaction)
					
					set qty = 0
					set qty = $piece($get(^VARCMMLinha(YM,Item,year,month,idLocation,idTransaction,1)),Y,1)
					set qtyMonth = qtyMonth + qty
					
				$$$End
										
			$$$End
			set objExcel = ""					
 			do NewLine^COMTable()
			do InsertCell^COMTable(year,"VARAnaliseConsumo",1,"D"_","_Item_","_month_","_""_","_year,"RIGHT")
			do InsertCell^COMTable($piece($get(^WWW101(0,"COMMONTHSOFYEAR","PT",month,1)),Y,1),"VARAnaliseConsumo",1,"D"_","_Item_","_month_","_""_","_year,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,12,qtyMonth,2),"VARAnaliseConsumo",1,"D"_","_Item_","_month_","_""_","_year,"RIGHT")
			do InsertCell^COMTable("Ver detalhes deste período","VARAnaliseConsumo",1,"D"_","_Item_","_month_","_""_","_year,"RIGHT")
  			do EndLine^COMTable()
			
			set objExcel = year_"~"_$piece($get(^WWW101(0,"COMMONTHSOFYEAR","PT",month,1)),Y,1)_"~"_$$^WWWTR(0,12,qtyMonth,2)
			
			set count = $i(count)
			set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count,1) = objExcel
			
			set qtyTotal = qtyTotal + qtyMonth

		$$$End
		
	$$$End
 
 	do NewLine^COMTable("lightsteelblue")
	do InsertCell^COMTable("")
	do InsertCell^COMTable("","",,"","RIGHT")
	do InsertCell^COMTable("<strong>Consumo total no período:&nbsp;&nbsp;"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>","",,"","RIGHT")
	do InsertCell^COMTable("","",,"","RIGHT")
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count+1,1) = "~<strong>Consumo total no período:</strong>~<strong>R$ "_$$^WWWTR(0,12,qtyTotal,2)_"</strong>"		
  	do EndLine^COMTable()	

	do Stop^COMTable()	
	
	w "<br />Clique em 'Ver detalhes deste período' para ver o detalhamento completo do consumo no período para cada local.<br /><br />"
		 	   	  	   	
	quit
 
ImprimeConsumoRedeMes(Item,Mes,Ano)
	//Tabela
	new year, month, idLocation, idTransaction, qty, qtyLocation, qtyTotal, locationCodeDesc,
		lstHeader

 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Local","Consumo Local&nbsp;&nbsp;<br>&nbsp;em "_$piece($get(^WWW101(YM,"COMMONTHSOFYEAR","PT",Mes,1)),Y,1),"Detalhes","Histórico Local")
 	
 	// BASIC-12: Montagem do Cabeçalho
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,0,1) = "Local~Consumo Local&nbsp;&nbsp;<br>&nbsp;em "_$piece($get(^WWW101(YM,"COMMONTHSOFYEAR","PT",Mes,1)),Y,1)
	
	do Start^COMTable(lstHeader,"Demonstrativo do Consumo do Produto em "_$piece($get(^WWW101(YM,"COMMONTHSOFYEAR","PT",Mes,1)),Y,1)_"/"_Ano_" na Rede")		
		
	set qtyTotal = 0
	set count = 0
	
	$$$Order5(^VARCMMLinha,YM,Item,Ano,Mes,idLocation)
			
		set qtyLocation = 0
					
		$$$Order6(^VARCMMLinha,YM,Item,Ano,Mes,idLocation,idTransaction)
						
			set qty = 0
			set qty = $piece($get(^VARCMMLinha(YM,Item,Ano,Mes,idLocation,idTransaction,1)),Y,1)
			set qtyLocation = qtyLocation + qty
		
		$$$End
	
		set locationCodeDesc = idLocation_" - "_$piece($get(^WWW0121(YM,YM,idLocation,1)),"~",1)		  		
		
		set objExcel = ""
		do NewLine^COMTable()
		do InsertCell^COMTable(locationCodeDesc,"VARAnaliseConsumo",1,"D"_","_Item_","_Mes_","_idLocation_","_Ano,"LEFT")
		do InsertCell^COMTable($$^WWWTR(0,12,qtyLocation,2),"VARAnaliseConsumo",1,"D"_","_Item_","_Mes_","_idLocation_","_Ano,"RIGHT")
		do InsertCell^COMTable("Ver detalhes do consumo no período","VARAnaliseConsumo",1,"D"_","_Item_","_Mes_","_idLocation_","_Ano,"RIGHT")
		do InsertCell^COMTable("Ver histórico do local","VARAnaliseConsumo",1,"D"_","_Item_","_""_","_idLocation,"RIGHT")
		do EndLine^COMTable()
		
		set count = $i(count)
		set objExcel = locationCodeDesc_"~"_$$^WWWTR(0,12,qtyLocation,2)
		set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count,1) = objExcel
		
		set qtyTotal = qtyTotal + qtyLocation
	
	$$$End
	
	do NewLine^COMTable("lightsteelblue")
	do InsertCell^COMTable("<strong>Total Consumido na Rede no Mês</strong>","",,"","RIGHT")
	do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>",,,,"RIGHT")		    			
	do InsertCell^COMTable("")
	do InsertCell^COMTable("")
	do EndLine^COMTable()
	do Stop^COMTable()
	
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count+1,1) = "<strong>Total Consumido na Rede no Mês</strong>~<strong>"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>"
	
	w "<br />Observação: clique em 'Ver Detalhes deste local' para ver o detalhamento completo do consumo no período para o local selecionado.<br /><br />"
 
	quit
 
ImprimeConsumoLocalHistorico(Item,Local)
	new year, month, idLocation, idTransaction, qty, qtyMonth, qtyTotal,
		lstHeader

	//Tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Ano","Período","Quant. Consumida na Rede no Período","Detalhes")
 	
 	// BASIC-12: Montagem do Cabeçalho
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,0,1) = "Ano~Período~Quant. Consumida na Rede no Período"
	
	do Start^COMTable(lstHeader,"Histórico do Consumo Mensal do Produto no local: "_$piece($get(^WWW0121(YM,YM,Local,1)),Y,1))
	  	
	set qtyTotal = 0
	set count = 0
	
	$$$Order3(^VARCMMLinha,YM,Item,year)
		
		$$$Order4(^VARCMMLinha,YM,Item,year,month)
			
			set qtyMonth = 0
			
			$$$Order5(^VARCMMLinha,YM,Item,year,month,idLocation)
					
				continue:(idLocation '= Local)
				
				$$$Order6(^VARCMMLinha,YM,Item,year,month,idLocation,idTransaction)
								
					set qty = 0
					set qty = $piece($get(^VARCMMLinha(YM,Item,year,month,idLocation,idTransaction,1)),Y,1)
					set qtyMonth = qtyMonth + qty
				
				$$$End
			
			$$$End
								
 			set objExcel = ""
			do NewLine^COMTable()
			do InsertCell^COMTable(year,"VARAnaliseConsumo",1,"D"_","_Item_","_month_","_Local_","_year,"RIGHT")
			do InsertCell^COMTable($piece($get(^WWW101(0,"COMMONTHSOFYEAR","PT",month,1)),Y,1),"VARAnaliseConsumo",1,"D"_","_Item_","_month_","_Local_","_year,"RIGHT")
			do InsertCell^COMTable($$^WWWTR(0,12,qtyMonth,2),"VARAnaliseConsumo",1,"D"_","_Item_","_month_","_Local_","_year,"RIGHT")
			do InsertCell^COMTable("Ver detalhes deste período","VARAnaliseConsumo",1,"D"_","_Item_","_month_","_Local_","_year,"RIGHT")
  			do EndLine^COMTable()
  			
  			set count = $i(count)
			set objExcel = year_"~"_$piece($get(^WWW101(0,"COMMONTHSOFYEAR","PT",month,1)),Y,1)_"~"_$$^WWWTR(0,12,qtyMonth,2)
			set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count,1) = objExcel
			set qtyTotal = qtyTotal + qtyMonth
		
		$$$End
	
	$$$End
	
 	do NewLine^COMTable("lightsteelblue")
	do InsertCell^COMTable("")
	do InsertCell^COMTable("","",,"","RIGHT")
	do InsertCell^COMTable("<strong>Consumo total no local no período:&nbsp;&nbsp;"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>","",,"","RIGHT")
	do InsertCell^COMTable("","",,"","RIGHT")		
  	do EndLine^COMTable()
	do Stop^COMTable()
	
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count+1,1) = "<strong>Consumo total no local no período:&nbsp;&nbsp;"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>"
	
	w "<br />Clique em 'Ver detalhes deste período' para ver o detalhamento completo do consumo no período para cada local.<br /><br />"
		 	   	  	   	
	quit
 
ImprimeConsumoLocalMes(Item,Mes,Local,Ano)
	new year, month, idTransaction, qty, qtyTotal, tipo, objCMM, objTransaction, param, form,
		lstHeader,idKeyPopup,strDestino, objDevolucao, idConsumo, objConsumo, objDevolucaoLinha
	
	//Tabela
 	set lstHeader = ""
  	set lstHeader = lstHeader_$listbuild("Seq.","Tipo","No. Documento","Data","Matrícula - Usuário","Setor / Paciente","Quant. Consumida&nbsp;<br> / Devolvida")
 	
 	// BASIC-12: Montagem do Cabeçalho
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,0,1) = "Seq.~Tipo~No. Documento~Data~Matrícula - Usuário~Setor / Paciente~Quant. Consumida / Devolvida"
	
	do Start^COMTable(lstHeader,"Demonstrativo do Consumo do Produto em "_$piece($get(^WWW101(0,"COMMONTHSOFYEAR","PT",Mes,1)),Y,1)_"/"_Ano_" no local: "_$piece($get(^WWW0121(YM,YM,Local,1)),Y,1))
		
	set qtyTotal = 0
	set Num = 0
	set count = 0
				
	
	$$$Order6(^VARCMMLinha,YM,Item,Ano,Mes,Local,idTransaction)
					
		set qty = 0
		set objCMM = $get(^VARCMMLinha(YM,Item,Ano,Mes,Local,idTransaction,1))
		set qty  = $piece(objCMM,Y,1)
		set tipo = $piece(objCMM,Y,3)
		set idKeyPopup = idTransaction
			
		
		set form     = $$GetForm(tipo)			
		
		set objTransaction = $$GetObj(tipo,idTransaction)
 		
 		set descTipo = $$GetDescTipo(tipo,objTransaction) 
 		
   		set Num     = $i(Num)
		set Data 	= $$GetData(tipo,objTransaction)								
		set Usuario = $$GetUsuario(tipo,objTransaction)
		set strDestino = $$GetDestino(tipo,objTransaction)	
			
		set param = ""
		
		if (tipo = 11) { 
			set param = 1
		} elseif (tipo = 21) {
			set param = 3
		}
		
		if (((tipo = 11) || (tipo = 21)) && 
						((form = "INIssue") || (form = "INDispenseToPatient")) ) {		
			if ($$$INIssueDocketNo(objTransaction) '= "") {
				set form 		= "MEDDispense"
				set idKeyPopup 	= $$$INIssueDocketNo(objTransaction)
			}
		}
		if (tipo = 22) {
			set objDevolucao = $get(^INIssueRev(YM,idTransaction,1))
			if ($$$INIssueRevReturnType(objDevolucao) = 1) { //por paciente
				set objDevolucaoLinha = $get(^INIssueRevLine(YM,idTransaction,$$$VARCMMLinhaLinha(objCMM),1))
				set idConsumo = $$$INIssueRevLineIssue(objDevolucaoLinha)
			}
			elseif ($$$INIssueRevReturnType(objDevolucao) = 2) { //por local {
				if ($length($$$INIssueRevIssueNumber(objDevolucao)) > 0) {
					set idConsumo = $$$INIssueRevIssueNumber(objDevolucao)
				}
				else {
					set objDevolucaoLinha = $get(^INIssueRevLine(YM,idTransaction,$$$VARCMMLinhaLinha(objCMM),1))
					set idConsumo = $$$INIssueRevLineIssue(objDevolucaoLinha)
				}
			}
			continue:(idConsumo = "")
			set objConsumo = $get(^INIssue(YM,idConsumo,1))
			if ($$$INIssueType(objConsumo) = 1) {	//Devolução de transferência
				continue:(($$IsStorageLocn^VARSQL($$$INIssueFromLocn(objConsumo))) && ($$IsStorageLocn^VARSQL($$$INIssueToLocn(objConsumo))))
			}
			
		}
		set qtyTotal = qtyTotal + qty			
		
		set objExcel = ""
  		do NewLine^COMTable()
		do InsertCell^COMTable(Num)
		do InsertCell^COMTable(descTipo)
		do InsertCell^COMTable(idKeyPopup,form,idKeyPopup,param,,,,,"_BLANK")
		do InsertCell^COMTable($zdate(Data,4))
		do InsertCell^COMTable(Usuario_" - "_$$SQLGetUserNameOuCodigo^VARSQL(Usuario))
		do InsertCell^COMTable(strDestino)
		do InsertCell^COMTable($$^WWWTR(0,12,qty),,,,"RIGHT")
		do EndLine^COMTable() 
		
		set count = $i(count)
		set objExcel = Num_"~"_descTipo_"~"_idKeyPopup_"~"_$zdate(Data,4)_"~"_Usuario_" - "_$$SQLGetUserNameOuCodigo^VARSQL(Usuario)_"~"_strDestino_"~"_$$^WWWTR(0,12,qty)
		set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count,1) = objExcel
		
	$$$End
		
  	do NewLine^COMTable("lightsteelblue")
	do InsertCell^COMTable("<strong>Total Geral Consumido </strong>",,,,"RIGHT",,6)
	do InsertCell^COMTable("<strong>"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>",,,,"RIGHT")
	do EndLine^COMTable()
	do Stop^COMTable()
	
	set ^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED,count+1,1) = "<strong>Total Geral Consumido </strong>~<strong>"_$$^WWWTR(0,12,qtyTotal,2)_"</strong>"
	
	quit
 
GetDescTipo(pTipo,pobjTransaction="")	
	new descTipo
	
	$$$VAR
	
	if pTipo = 11 {	
		if $piece(pobjTransaction,Y,23)'="" {
			set descTipo = "Paciente"
		}else {
			set descTipo = "Setor"
		}
	}
	
	elseif (pTipo = 21) {
		set descTipo = "Paciente"
	}
	
	elseif (pTipo = 22) {
		set descTipo = "Devolução"
	}
	elseif (pTipo = 31) {
		set descTipo = "Aceite Transferência"
	}
	elseif (pTipo = 35) {
		set descTipo = "Manipulado"
	}
	elseif (pTipo = 37) {									;HEVA-1728
		set descTipo = "Manipulado Devolução"				;HEVA-1728
	}														;HEVA-1728
	else {
		set descTipo = "Documento"
	}
	
	quit descTipo
	
	
GetForm(pTipo)
	new form
	
	if (pTipo = 11) {
		set form = "INIssue"
	}
	elseif (pTipo = 21) {
		set form = "INDispenseToPatient"
	}	
	elseif (pTipo = 22) {
		set form = "INIssueRev"
	}
	elseif (pTipo = 31) {
		set form = "INRECTFR"
	}
	elseif (pTipo = 35) {									;HEVA-1728
		set form = "INIssue"								;HEVA-1728
	}														;HEVA-1728
	elseif (pTipo = 37) {									;HEVA-1728
		set form = "INMORev"								;HEVA-1728
	}														;HEVA-1728
	else {
		set form = ""
	}
	
	quit form
	
GetUsuario(pTipo,pobjTransaction)
	new Usuario	
 
	if ( (pTipo = 11) || (pTipo = 21) || (pTipo = 35) ) {				;HEVA-1728
		set Usuario = $piece(pobjTransaction,"~",10)
		if Usuario = "" set Usuario = $piece(pobjTransaction,"~",8)
		
		;if Usuario = "WEBSERVICE" {
		;	set Usuario = "Prescrição Eletrônica"
		;}		
	}
 
	elseif (pTipo = 22) {
		set Usuario = $piece(pobjTransaction,"~",11)
		if Usuario = "" set Usuario = $piece(pobjTransaction,"~",9)
	}
	
	elseif (pTipo = 31) {
		set Usuario = $piece(pobjTransaction,"~",11)
		if Usuario = "" set Usuario = $piece(pobjTransaction,"~",7)
	}
	elseif (pTipo = 37) { 												;HEVA-1728
		set Usuario = $piece(pobjTransaction,"~",4)						;HEVA-1728
		if Usuario = "" set Usuario = $piece(pobjTransaction,"~",7)		;HEVA-1728
	}
		
	else { 
		set Usuario = ""
	}
	
	quit Usuario
	
GetData(pTipo,pobjTransaction)
	new Data	
 	
	if (pTipo = 11) {	
		set Data = $piece(pobjTransaction,"~",9)
	}
	
	elseif (pTipo = 21) {
		set Data = $piece(pobjTransaction,"~",9)
	}
	elseif (pTipo = 31) {
		set Data = $piece(pobjTransaction,"~",8)
	}
	elseif (pTipo = 35) {
		set Data = $piece(pobjTransaction,"~",9)
	}
	elseif (pTipo = 37) {												;HEVA-1728
		set Data = $piece(pobjTransaction,"~",5)						;HEVA-1728
	}																	;HEVA-1728
	else { 
		set Data = $piece(pobjTransaction,"~",8)
	}
	
	quit Data
	
GetObj(pTipo,pIdTransaction = "")
	new objTransaction
		
	if (pIdTransaction '= "") {
		set idTransaction = pIdTransaction
	}
	if (pTipo = 11) || (pTipo = 21) || (pTipo = 35) {
		set objTransaction = $get(^INIssue(0,idTransaction,1))
		;w !,"Obj INIssue ",!,objTransaction
	}	
	
	elseif (pTipo = 22) {
		set objTransaction = $get(^INIssueRev(0,idTransaction,1))
		;w !,"Obj INIssueRev ",!,objTransaction

	}
	
	elseif (pTipo = 31) {
		set objTransaction = $get(^INREC(0,idTransaction,1))
		;w !,"Obj INREC ",!,objTransaction
	}
	elseif (pTipo = 37) {
		set objTransaction = $get(^INMORev(0,idTransaction,1))
		;w !,"Obj INREC ",!,objTransaction
	}
	
	else {
		set objTransaction = ""
		;W !, "eita "_pTipo
	}
 
	quit objTransaction

GetObjSQL(pTipo,idTransaction)
	new objTransaction
		
	if (pTipo = 11) || (pTipo = 21)  || (pTipo = 35) {						;HEVA-1728
		set objTransaction = $get(^INIssue(0,idTransaction,1))
	}	
	
	elseif (pTipo = 22) {
		set objTransaction = $get(^INIssueRev(0,idTransaction,1))
	}
	
	elseif (pTipo = 31) {
		set objTransaction = $get(^INREC(0,idTransaction,1))
	}
	elseif (pTipo = 37) {													;HEVA-1728
		set objTransaction = $get(^INMORev(0,idTransaction,1))				;HEVA-1728
	}																		;HEVA-1728
	
	else {
		set objTransaction = ""
	}
 
	quit objTransaction	
	
GetDestino(pTipo,pobjTransaction)
	new Destino, idDocOrigem, objDocOrigem, tipoDocOrigem
 	
 	quit:(pobjTransaction="") ""
 	
 	$$$VAR
 	
	if (pTipo = "11") {	
		if $piece(pobjTransaction,Y,23)'="" {
			set Destino = $$SQLGetPatientName^VARSQL($piece(pobjTransaction,Y,23)) //Paciente
		}else {
			set Destino = $piece(pobjTransaction,"~",2)
			set Destino = $$SQLGetLocationName^VARSQL(Destino)
		}
	}
	elseif (pTipo = "21") {
		set Destino = $$SQLGetPatientName^VARSQL($piece(pobjTransaction,Y,23))
	}
	elseif (pTipo = "31") {
		set Destino = $piece(pobjTransaction,"~",2)
	}
	elseif (pTipo = "22") {
		set idDocOrigem = $piece(pobjTransaction,"~",1)
		quit:(idDocOrigem = "") ""
		
		set objDocOrigem = $get(^INIssue(YM,idDocOrigem,1))
		set tipoDocOrigem = $piece(objDocOrigem,"~",24)
	
		if (tipoDocOrigem = "1") {
			set Destino = $$SQLGetLocationName^VARSQL($piece(objDocOrigem,Y,2)) //Local de Destino
		} 
		elseif (tipoDocOrigem = "3") {
			set Destino = $$SQLGetPatientName^VARSQL($piece(objDocOrigem,Y,23)) //Paciente
		}
	}
	else {
		set Destino = "Destino"
	}
	
	quit Destino
 	
VoltarHistorico
	new registroAtual, registroAnterior, objAnterior, idItem
		
	set registroAtual = ""
	set registroAtual = $order(^VARTempAnaliseConsumoHistory(YBED,registroAtual),-1)
	
	if (registroAtual = 1) || (registroAtual = "") {
		do GoToForm^COMUtilForm("VARHistoricoConsumo","NO")
	}		
	
	elseif (registroAtual '= "") {	
		set registroAnterior = registroAtual - 1
		set objAnterior = $get(^VARTempAnaliseConsumoHistory(YBED,registroAnterior))
 
		set ^VARTempAnaliseConsumo(YBED) = objAnterior
		set idItem = $piece(objAnterior,Y,1)
		kill ^VARTempAnaliseConsumoHistory(YBED,registroAtual)
	}
	
	do GoToForm^COMUtilForm("VARAnaliseConsumo","NO")
	
	quit

Imprimir
	
	set vProduto = $get(VORG(1))
	set vLocal = $get(VORG(3))
	set vMes = $get(VORG(2))
	set vAno = $get(VORG(4))
	
	do RunReportAnaliseConsumoPorProduto^VARJasperRunReportContinuacao(vProduto,vLocal,vMes,vAno)

	quit
	
	// BASIC-12
ExportarExcel()
	$$$VAR
	if ('$data(^CacheTempAnaliseConsumoXLS(YM,YUSER,YBED))) {
		$$$Alert("IN00740")
	}
	else {
		do ExportarExcel^VARExportarExcel("CacheTempAnaliseConsumoXLS")
	}
	do GoToForm^COMUtilForm("VARAnaliseConsumo",1,,,,,)
	quit
	
]]></Routine>
</Export>