<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INForeItem" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INForeItem
#include INConst
#include COMSYS 
	
	
ShowDetails(pidUser="")
    ;-------------------------------------------------------------------------------
    ; Show Forecast details
    ;
    ; Returns:
    ;
    ; History:
    ; 13-Sep-2006	JW		SRBR014083: Added Update Settings. Cleaned up
    ; 20-Dec-2004	SCR		Show Details (SR11323)
    ; 10-Aug-2004	SCR		Created
    ;-------------------------------------------------------------------------------
    new Rec,Data,Item,Location,Rule,dteRun
    
    if pidUser="" set pidUser=" "
    
    set Rec=$get(^INForeTest(YM,pidUser,1))
    set Item=		$$$INForeTestItem(Rec)
    set Location=	$$$INForeTestLocation(Rec)
	set Rule=		$$$INForeTestRule(Rec)
	set dteRun=		$$$INForeTestRunDate(Rec)
	
	if 'dteRun set dteRun = +$horolog
 	
 	if (Item'="") && (Location'="") && (Rule'="") {
		if $$$INForeTestShowGraph(Rec) {
			do ShowGraph(Item,Location,Rule,Rec)
		}
 		do ShowCalc(Item,Location,Rule,dteRun,$$$INForeTestCreateDemands(Rec),$$GetSupplier^INForeProc(Item,Location),$$$INForeTestUpdateDRPSettings(Rec))	//BR014083
 	}
 	Quit
 
ShowCalc(Item="",Location="",Rule="",RunDate="",Create=$$$NO,Supplier="",pblnUpdate=$$$NO)
    ;-------------------------------------------------------------------------------
    ; Show Calculation
    ;
    ; Returns:
    ;
    ; History:
    ; 13-Sep-2006	JW		SRBR014083: Added pblnUpdate. Cleaned up. Language text
    ; 20-Dec-2004	SCR		Fixed display (SR11323)
    ; 23-Nov-2004	SCR		Changed Rule Access
    ; 21-Oct-2004	SCR		Created
    ;-------------------------------------------------------------------------------
	new lstHeader,Data,blnCreate
	
	if Rule'="" {
	 	set lstHeader=$listbuild($$$StrINForeLineName,
								$$$StrINForeLineValue1,
								$$$StrINForeLineDescription)
 		
		do Start^COMTable(lstHeader,,1)
 		
		if Create {
			do RemoveDemands^INForeRun(Item,Location)
			set Data("o","Create")=$$$YES
		}
		set Data("o","Update") = pblnUpdate		//BR014083
		
		set blnCreate=$$$NO
		xecute "set blnCreate=$$"_$$Code^INForeProc(Rule,"Item,Location,1,RunDate,.Data,,YBED,Supplier") 
		
 
		if 'blnCreate {
			do Message($$$Text("IN00534"))  ;Short Cut Quit		//BR014083
		}
		do Stop^COMTable()
		
		if pblnUpdate { 
			do SetMasterfile(Item,Location,.Data)
		}
 
	} 
	quit
 
SetMasterfile(pidItem,pidLocation,parrData)
    ;-------------------------------------------------------------------------------
    ; set up temp data for Masterfile change
    ;
    ; Parameters:
    ;	parrData=Array of data from the Forecast rule for an Item/Location
    ;
    ; Returns:
    ;   Nothing
    ; History:
	; 19-Feb-2008		STEVER		SRBR014376: Created
    ;-------------------------------------------------------------------------------
	new intTemp,intData,objSet
	
	set intData=""  ; begining of the Masterfile entries
	write "k"
	; for each masterfile set
	for {
		set intData=$order(parrData("Masterfile",intData))
		quit:intData=""
		set objSet=$get(parrData("Masterfile",intData))
		
	 	do Set^INForeMasterFile(objSet,1)
	}
	
	quit
    
ShowGraph(Item="",Location="",Rule="",Rec="")
    ;-------------------------------------------------------------------------------
    ; Show Results 
    ;
    ; Returns:
    ;
    ; History:
	; 18-Aug-2006		HeberB		SRBR014190: removed hardcoded language
    ; 15-Jan-2005		SCR			Show Graph Details
    ; 15-Jan-2005		SCR			Corrected Start Date
    ; 23-Nov-2004		SCR			Changed Rule Access
    ; 13-Aug-2004		SCR			Created
    ;-------------------------------------------------------------------------------
	new StartDate,Count,Result,ResultName,Duration,Dates
	new FirstDate,Col,Code,Name,ResultColor,Results
	new Data,Header,XLabel,YLabel
 
	kill ^mtemp("ForecastGraph",YUSER)
	set StartDate=	$$$INForeTestResultStartDate(Rec)
	set Count=		$$$INForeTestNumberofResults(Rec)
	set Duration=	$$$INForeTestResultDuration(Rec)
	set Data=""
	for Result=1:1:4 {
		set ResultName(Result)=$piece(Rec,Y,12+(Result*2)) ; Select Fields 14,16,18,20
		set ResultColor(Result)=$piece(Rec,Y,13+(Result*2)) ; Select Fields 15,17,19,21
	}
	
	if Count<12 set Count=12
	if Count>50 set Count=50
	if 'Duration set Duration=7
  	
	set Results=""
	set Dates=""
	if 'StartDate set StartDate=$h
	set FirstDate=StartDate
	for Col=1:1:Count {
		set $piece(Dates,",",Col)=$translate($zdt(StartDate,2)," ")
		kill Data
		
		set Code="do "_$$Code^INForeProc(Rule,"Item,Location,0,StartDate,.Data,,YBED") 
		
		xecute Code
		for Result=1:1:4 {
			set Name=$get(ResultName(Result))
			if Name'="" {
				set $piece(Results(Result),",",Col)=$get(Data("v",Name))
			}
		}
		set Name=""
		for {
			set Name=$order(Data("v",Name))
			quit:Name=""
			set ^mtemp("ForecastGraph",YUSER,1,StartDate,Name)=$get(Data("v",Name))
			set ^mtemp("ForecastGraph",YUSER,2,Name)=""
		}
		set StartDate=StartDate+Duration
	}
 
	;set YLabel="Qty" ; Sales	;SRBR014190
	set YLabel=$$$Text(34451) ; Sales
	;set Header="Results every "_Duration_" Days "	;SRBR014190
	set Header=$$$Text(34450)_" "_Duration_" "_$$$Text(32092)
	set Header=Header_" ("_Item_") "_$$$INARTSearchName($get(^INART(YM,Item,1)))
	;set XLabel="From "_$zdt(FirstDate,3)_" To "_$zdt(StartDate-1,3)	;SRBR014190
	set XLabel=$$$Text(68)_" "_$zdt(FirstDate,3)_" "_$$$Text(69)_" "_$zdt(StartDate-1,3)
 
	do Graph(Header,Dates,XLabel,YLabel,.Results,.ResultName,.ResultColor)
	do ShowGraphDetails()
	kill ^mtemp("ForecastGraph",YUSER)
	quit
 
ShowGraphDetails() 
    ;-------------------------------------------------------------------------------
    ; Show Graph Details
    ;
    ; Returns:
    ;
    ; History:
    ; 15-Jan-2005		SCR			Created
    ;-------------------------------------------------------------------------------
	new lstHeader,Code,Data,Name,Date
	
 	set lstHeader=""
	set lstHeader=lstHeader_$lb($$^WWWFELDNAME("INForeTest","D",5)) ; Run Date
	set Name=""
	for {
		set Name=$order(^mtemp("ForecastGraph",YUSER,2,Name))
		quit:Name=""
		set lstHeader=lstHeader_$lb(Name)
	}
 
	do Start^COMTable(lstHeader,,1)
	set Date=""
	for {
		set Date=$order(^mtemp("ForecastGraph",YUSER,1,Date))
		quit:Date=""
		do NewLine^COMTable()
		set Name=""
		do InsertCellDate^COMTable(Date)
		for {
			set Name=$order(^mtemp("ForecastGraph",YUSER,1,Date,Name))
			quit:Name=""
			set Data=$get(^mtemp("ForecastGraph",YUSER,1,Date,Name))
			do InsertCell^COMTable(Data,,,,"Right")
		}
		do EndLine^COMTable()
	}	
	
	do Stop^COMTable()
 
 
	quit
    quit
	
Message(Name="",Value="",Comment="",Key="")
    ;-------------------------------------------------------------------------------
    ; Show rule Messages
    ;
    ; Returns:
    ;
    ; History:
    ; 13-Aug-2004		SCR			Created
    ;-------------------------------------------------------------------------------
    new Form
    set Form=""
    if Key'="" set Form="INForeLine"
	do NewLine^COMTable()
	do InsertCell^COMTable(Name,Form,Key)
	do InsertCell^COMTable(Value,Form,Key)
	do InsertCell^COMTable(Comment,Form,Key)
	do EndLine^COMTable()
	quit
 
BeforeButtonLine()
    ;-------------------------------------------------------------------------------
    ; Before Button Line
    ;
    ; Returns:
    ;
    ; History:
    ; 27-Nov-2007		GM			BR014808: Get rule of YFELD
    ; 20-Aug-2004		SCR			Created
    ;-------------------------------------------------------------------------------
 
	;do LoadNames() ; Load Line Names in to COMTempList
	do LoadNames($$$INForeTestRule(YFELD)) ; Load Line Names in to COMTempList  ;BR014808
 
	quit
	
LoadNames(RuleMatch="")
    ;-------------------------------------------------------------------------------
    ; Load Rule Line Names in to COMTempList
    ;
    ; Returns:
    ;
    ; History:
    ; 14-Ago-2006		HeberB		BR014164: call to build description based on language
    ; 20-Aug-2004		SCR			Created
    ;-------------------------------------------------------------------------------
	new Rule,Line,LineRec,Name
	
	quit:$get(YUSER)="" ; ************************************ Fast Exit
	
	kill ^COMTempList(YM,YUSER,"LineNames")
	set Rule=""
	for {
		set Rule=$order(^INForeLine(YM,Rule))
		quit:Rule=""
		if (RuleMatch'="") && (Rule'=RuleMatch) Continue
		set Line=""
		for {
			set Line=$order(^INForeLine(YM,Rule,Line))
			quit:Line=""
			set LineRec=$get(^INForeLine(YM,Rule,Line,1))
			set Name=$$$INForeLineName(LineRec)
			; BR014164
			;if Name'="" set ^COMTempList(YM,YUSER,"LineNames",Name,1)=$$$INForeLineDescription(LineRec)
			if Name'="" set ^COMTempList(YM,YUSER,"LineNames",Name,1)=$$BuildINForeLineDescription^INForeLine(LineRec)
		}
	}
 
	quit
 
 
	
Graph(Header,Label,XLabel,YLabel,Values,Labels,Colors)
    ;-------------------------------------------------------------------------------
    ; Show Graph on screen
    ;
    ; Returns:
    ;
    ; History:
    ; 20-Aug-2004		SCR			Created
    ;-------------------------------------------------------------------------------
 
	new YFELD
	
	Kill ^WWWSOR(YUSER)
	kill ^WWWSOR(1)
	WRITE "<CENTER><BR>"
	SET YFELD=""
	SET $PIECE(YFELD,Y,1)="gif"_$PIECE($HOROLOG,",",2)_$RANDOM(10)_".gif"  ;DATEI
	SET $PIECE(YFELD,Y,2)=1      ;0 BOX,1 LINES,2 STACK,3 STOCK
	SET $PIECE(YFELD,Y,3)=Header
	SET $PIECE(YFELD,Y,4)="BLACK"         ;FARBE
	SET $PIECE(YFELD,Y,5)=Label  ;X-achse MONATE
	SET $PIECE(YFELD,Y,6)=XLabel  ;MONAT
	SET $PIECE(YFELD,Y,7)=""   ;Y-ACHSE leer = automatic
	SET $PIECE(YFELD,Y,8)=YLabel  ;Y-ACHSE UMSATZ
	SET $PIECE(YFELD,Y,10)=$get(Labels(1))   ;RUBRIK 1
	SET $PIECE(YFELD,Y,11)=$get(Colors(1))   ;FARBE
	SET $PIECE(YFELD,Y,12)=$get(Values(1))      ;WERTELISTE   NN,NN,NN,
	SET $PIECE(YFELD,Y,13)=$get(Labels(2))    ;RUBRIK 2
	SET $PIECE(YFELD,Y,14)=$get(Colors(2))   ;FARBE
	SET $PIECE(YFELD,Y,15)=$Get(Values(2))       ;WERTELISTE   NN,NN,NN,
	SET $PIECE(YFELD,Y,16)=$get(Labels(3))    ;RUBRIK 3
	SET $PIECE(YFELD,Y,17)=$get(Colors(3))   ;FARBE
	SET $PIECE(YFELD,Y,18)=$get(Values(3))       ;WERTELISTE   NN,NN,NN,
	SET $PIECE(YFELD,Y,19)=$get(Labels(4))    ;RUBRIK 4
	SET $PIECE(YFELD,Y,20)=$get(Colors(4))   ;FARBE
	SET $PIECE(YFELD,Y,21)=$get(Values(4))      ;WERTELISTE   NN,NN,NN,
	SET $PIECE(YFELD,Y,31)="780"       ;breit
	SET $PIECE(YFELD,Y,32)="150"       ;hoch
	;
	DO ^WWWGRAPH(YFELD)
	;WRITE YCR,"<script type="_""""_"text/javascript"_""""_">"
	;WRITE YCR,"<!--"
	;WRITE YCR," alert();"     ;div positionieren
	;WRITE YCR,"//-->"
	;WRITE YCR,"</script>"
 	Write "</CENTER>"
	Kill ^WWWSOR(YUSER)
	kill ^WWWSOR(1)
	quit
			
 
]]></Routine>
</Export>