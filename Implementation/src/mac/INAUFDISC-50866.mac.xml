<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFDISC" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFDISC
	#include INConst
	#include COMSYS
 
Recalculate2(YFELD="",pstrHeaderClass="",pidOrder="",pidLine="",pstrDate="",pfltRate="")
    ;-------------------------------------------------------------------------------
    ; Recalculate details based on Foreign Currency Rate.
    ; Should duplicate and improve INNETTOBRUTTO
    ; YFELD is INAUFPK - may be extended to other classes such as INARTK,INERECH1,INAUFPA,INANGPA which have the same structure.
    ; Rewrite of Recalculate.
    ; 
    ; Returns:
    ;
    ; History:
    ; 16-Jun-2005	RobertW		SR12461: Do not use external call to local routine
    ; 03-Jun-2005	shobby		Created SR12008
    ;-------------------------------------------------------------------------------
    new objHeader,fltRate,blnOverride,NKOMMA,idSupplier,strGlobal
    
    if (YFELD'="") {
	    if (pidLine'="")&&(pidOrder'="") {
		    set blnOverride=$$$NO
	    	if $$$INAUFPKExchangeRate(YFELD)="" {
				set strGlobal="^"_pstrHeaderClass_"(YM,"_$$^WWWKEYBUILD(pidOrder)_",1)"
				set objHeader=$get(@strGlobal)
				set fltRate=0
				set idSupplier=""
				
				if objHeader'="" {
					set idSupplier=$$$INAUFPSupplier(objHeader)
					set fltRate=$$$INAUFExchangeRate(objHeader)
					set blnOverride=$$$INAUFExchangeRateOverride(objHeader)
				}
				if +fltRate=0 set fltRate=$$FindSupplierConversionRate(idSupplier)
				
				if +fltRate'=0 {
				
					set NKOMMA=+$$$INAUFPKNumericDecimalPlace(YFELD)
					IF NKOMMA>4 SET NKOMMA=2
					IF NKOMMA<2 SET NKOMMA=2
					set $$$INAUFPKExchangeRate(YFELD)=fltRate
					set $$$INAUFPKUnitPrice(YFELD)=$JUSTIFY($$$INAUFPKCostFC(YFELD)*fltRate,0,NKOMMA)
					set $$$INAUFPKBasisOriginalPrice(YFELD)= $$$INAUFPKCostFC(YFELD)*fltRate*$$$INAUFPKPurchaseQuantity(YFELD)
					set $$$INAUFPKNetOriginalPrice(YFELD)=$$^INNETTO(YFELD)
				}
	    	}
	    }
    }
	quit
 
 
 
Recalculate(YFELD,pidSupplier="",pstrDate="",pfltRate="")
    ;-------------------------------------------------------------------------------
    ; Recalculate details based on Foreign Currency Rate.
    ; Should duplicate and improve INNETTOBRUTTO
    ; YFELD is INAUFPK - may be extended to other classes such as INARTK,INERECH1,INAUFPA,INANGPA which have the same structure.
    ;
    ; Returns:
    ;
    ; History:
    ; 07-Jul-2005	shobby		SR12922: Check whether pfltRate is 0 before dividing with it.
    ; 16-Jun-2005	RobertW		SR12461: Do not use external call to local routine
    ; 23-May-2005	shobby		Created SR12008
    ;-------------------------------------------------------------------------------
    ;
	new NKOMMA,WHR,objINLIEF,curFCAmount
	
	set curFCAmount=0
	if ($get(YFELD)'="")&&(pidSupplier'="") {
		SET NKOMMA=+$PIECE(YFELD,Y,25)
		IF NKOMMA>4 SET NKOMMA=2
		IF NKOMMA<2 SET NKOMMA=2
		set objINLIEF=$get(^INLIEF(YM,pidSupplier,1))
 		SET WHR=$$$INLIEFCurrency(objINLIEF)
		IF (WHR="")||(WHR=YWHR) {
		} else {
			if +pfltRate=0 {
				if +$$$INAUFPKExchangeRateOverride(YFELD)'=0 {
					set pfltRate=$$$INAUFPKExchangeRate(YFELD)
				}
			}
			if +pfltRate=0 set pfltRate=$$FindSupplierConversionRate(pidSupplier)
			set $$$INAUFPKExchangeRate(YFELD)=pfltRate
			set $$$INAUFPKUnitPrice(YFELD)=$JUSTIFY($$$INAUFPKCostFC(YFELD)*pfltRate,0,NKOMMA)
			set $$$INAUFPKBasisOriginalPrice(YFELD)= $$$INAUFPKCostFC(YFELD)*pfltRate*$$$INAUFPKPurchaseQuantity(YFELD)
			set $$$INAUFPKNetOriginalPrice(YFELD)=$$^INNETTO(YFELD)
			;set $$$INAUFPKNetOriginalPrice(YFELD)=$$^INBRUTTO(YFELD)
			if +pfltRate=0 set pfltRate=1  ;SR12922
			set curFCAmount=$JUSTIFY($$$INAUFPKNetOriginalPrice(YFELD)/pfltRate,0,NKOMMA)
		}
	}
	quit curFCAmount
 
SetSupplierConversionRate(YKEY,&pblnSave=$$$YES)
    ;-------------------------------------------------------------------------------
    ; First checks the Supplier for an override value. If none calls FindConversionRate
    ; 
    ; Parameters:
    ; pidSupplier		The Supplier
    ; pstrTimeStamp		A time stamp in full horolog format.
    ; pblnSave			Whether to save or not.
    ;
    ; Returns:
    ; The floating point conversion rate.
    ;
    ; History:
    ; 17-Nov-2009	shobby		SR17032: Reworked as YFELD is not available after shifting call to this
    ; 								routine.
    ; 06-Feb-2008	shobby		SRAdhoc only save with the pblnSave flag
    ; 25-May-2005	PO			SR:12445 Prevent undefined error on strFC
    ; 10-May-2005	shobby		Created (SR12008)
    ;-------------------------------------------------------------------------------
    ;
	new idSupplier,objINAUF,objINAUFOnDisc

 	if '$$$NoKey(YKEY) {								;SR17032
	 	set objINAUFOnDisc=$get(^INAUF(YM,YKEY,1))
		if $get(objINAUFOnDisc)'="" {
			set idSupplier=$$$INAUFSupplierNumber(objINAUFOnDisc)
			if idSupplier'="" {
				set objINAUF=objINAUFOnDisc
				if '+$$$INAUFExchangeRateOverride(objINAUF) {
					set $$$INAUFExchangeRate(objINAUF)		   = $$FindSupplierConversionRate(idSupplier,$h)
					set $$$INAUFLastAlteration(objINAUF)	   = +$horolog
					set $$$INAUFExchangeRateOverride(objINAUF) = 0
				} elseif $$$INAUFExchangeRate(objINAUF)="" {
					set $$$INAUFExchangeRateOverride(objINAUF) = 1
				}
				;set $$$INAUFPurchaseOrderInformation(objINAUF)	   = $$$INAUFPurchaseOrderInformation(objINAUF)_"."			;SR17032 (testing only)
				if objINAUF'=objINAUFOnDisc {
					; Update the record in case user leaves without saving.
					if pblnSave do Save^COMUtils("INAUF",YKEY,objINAUF,1)
					;set pobjINAUF=objINAUF
				}
			}
		}
 	}
	quit
 
 
FindSupplierConversionRate(pidSupplier="",pstrTimeStamp="")
    ;-------------------------------------------------------------------------------
    ; First checks the Supplier for an override value. If none calls FindConversionRate
    ; 
    ; Parameters:
    ; pidSupplier		The Supplier
    ; pstrTimeStamp		A time stamp in full horolog format.
    ;
    ; Returns:
    ; The floating point conversion rate.
    ;
    ; History:
    ; 16-Jun-2005	RPW			SR12461: if strFC = YWHR, always return "" as the calling routine expects this.
    ; 							as sometimes the strFC=YWHR and this should return 0 to the calling routine.
    ; 25-May-2005	PO			SR:12445 Prevent undefined error on strFC
    ; 10-May-2005	shobby		Created (SR12008)
    ;-------------------------------------------------------------------------------
	new strRate,objINLIEF,strFC
	
	set strRate=""
	set strFC=""
	
	if pidSupplier'="" {
		set objINLIEF=$get(^INLIEF(YM,pidSupplier,1))
		if objINLIEF'="" {
		;	set strRate=$$$INLIEFExchangeRate(objINLIEF)		// SR13151: Commented for build 1.11.2
			set strFC=$$$INLIEFCurrency(objINLIEF)
		}
	}
	
	if (strRate="")&&((strFC'="")&&(strFC'=YWHR)) {
		set strRate=$$FindConversionRate^WWWZAHL(strFC,pstrTimeStamp,0)
	}
	
	quit strRate
	
	
UpdateExchangeRate(pYAUFTRAG,pYPOS,pobjINAUFP,pobjINAUFPK)
    ;-------------------------------------------------------------------------------
    ; Determines any override exchange rate from INAUF or INAUFP
    ;
    ; Returns:
    ;
    ; History:
    ; 26-May-2005	RobertW		SR12461: Cannot assume every order is foreign currency
    ; 							The assumption caused the unit price of each line to be 
    ; 							reduced to zero for a non foreign currency order.
    ; 17-May-2005	shobby		Created SR12008
    ;-------------------------------------------------------------------------------
    ;
	new objINAUF,WHRF,DATE,NKOMMA,PRICE,idSupplier,WHR
	
	if $get(pYAUFTRAG)'="" {
		set objINAUF=$get(^INAUF(YM,pYAUFTRAG,1))
		if objINAUF'="" {
			set idSupplier=$$$INAUFSupplierNumber(objINAUF)
			set WHRF=$$FindSupplierConversionRate(idSupplier)
 
			SET NKOMMA=$PIECE($GET(^INAUFPK(YM,pYAUFTRAG,pYPOS,1)),Y,25)
			IF +NKOMMA=0!(NKOMMA>9) SET NKOMMA=2
			set PRICE=$PIECE($get(^INAUFPK(YM,pYAUFTRAG,pYPOS,1)),Y,68)
			set DATE=+$horolog
 
			if $$$INAUFExchangeRateOverride(objINAUF) {
				if $$$INAUFExchangeRate(objINAUF)'="" {
					set WHRF=$$$INAUFExchangeRate(objINAUF)
					set DATE=$$$INAUFLastAlteration(objINAUF)
				}
			}
			
			if +WHRF'=0 {
			
				set $$$INAUFPKExchangeRateOverride(pobjINAUFPK)=$$$INAUFExchangeRateOverride(objINAUF)
				set $$$INAUFPKBasisOriginalPrice(pobjINAUFPK)		=$JUSTIFY((PRICE*WHRF),0,NKOMMA)
				set $$$INAUFPKUnitPrice(pobjINAUFPK)				=$JUSTIFY((PRICE*WHRF),0,NKOMMA)
				set $$$INAUFPKExchangeRate(pobjINAUFPK)				=WHRF
				set $$$INAUFPKExchangeRateEffectivityDa(pobjINAUFPK)=DATE
			}
		}
	}
	;SET YOK=$$^WWWSPEI("INAUFPK",pYAUFTRAG_","_pYPOS,pobjINAUFPK,1)
	quit
 
 
GetItemSupplierData(LIEF,YAUFTRAG,YPOS,ART)
    ;-------------------------------------------------------------------------------
    ; Wrapper for setting INAUFPK but excluding any exchange rate info.
    ;
    ; Returns:
    ;
    ; History:
    ; 31-Aug-2005	JW			Removed redundant line
    ; 18-May-2005	shobby		Created SR12008
    ;-------------------------------------------------------------------------------
    new objINAUFPK
    
 	set objINAUFPK = $get(^INARTK(YM,ART,LIEF,1))
		
 	if objINAUFPK'="" {
	 	set $$$INAUFPKExchangeRate(objINAUFPK) = ""
 		set ^INAUFPK(YM,YAUFTRAG,YPOS,1) = objINAUFPK
 	}
 	
 	quit
]]></Routine>
</Export>