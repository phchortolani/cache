<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPricePlanData" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPricePlanData
 
#include COMSYS
#include INConst
 
GetPricePlanLine(penumType,pidItem="",pidCustomer="",pidLocation="")
	;-------------------------------------------------------------------------------
	; Find the matching Generated Price Plan Line for a product, customer and location.
	;
	; Params:
	; pidItem    : Item Id
	; pidCustomer: Customer Id
	; pidLocation: Site Location Id
	;
	; ByRefs: None
	;
	; Returns: Price Plan Line Id
	;
	; History:
	; 05-Dec-2006	RPW		SR15279: Get the category from the item/item group heirarchy.
	; 04-Dec-2006	RPW		SR15270: Created
	;-------------------------------------------------------------------------------
 
	new idxItem,idPriceCategory,idPriceCategoryType,idxCategory,idxLocation,idOrder,idOrderLine,idLine,idCategory,SQLCODE,strSql
	new strPlans,enmMarketing,arrLines,intPriority
 
	set idLine = ""
 
 	set idxItem=$$$Index(pidItem)
	set idxCategory=""
	set idPriceCategory=""
	if pidItem'="" {
		set idPriceCategory=$$GetPriceCategoryForProduct^INCategory(pidItem) // SR15279
	}
	set idPriceCategoryType=""
	if (pidCustomer'="")&&(idPriceCategory'="") {
		set idPriceCategoryType=$$$INCustomerCategoryCategoryType($get(^INCustomerCategory(YM,pidCustomer,idPriceCategory,1)))
	}
	
	if ((idPriceCategoryType'="") && (pidCustomer'="")) || (pidCustomer="") {
		
		// If we have a customer we must have this category setup, otherwise we can't match the customer
	
		set idCategory=idPriceCategory_$$$COMMA_idPriceCategoryType
		set idxCategory=$$$Index(idCategory)
		set idxLocation=$$$Index(pidLocation)
		
		$$$Order6(^INPricePlanLines,YM,1,idxItem,idxCategory,idxLocation,idOrder)
			if $$$INPricePlanPricePlanType($get(^INPricePlan(YM,idOrder,1)))=penumType {
				$$$Order7(^INPricePlanLines,YM,1,idxItem,idxCategory,idxLocation,idOrder,idOrderLine)
					set intPriority=$$$INPricePlanLinePriority($get(^INPricePlanLine(YM,idOrder,idOrderLine,1)))
					if intPriority="" set intPriority=1E16
					set arrLines(intPriority)=idOrder_$$$COMMA_idOrderLine
				$$$End
			}
		$$$End
	} else {
		set arrLines(0)=""
	}
	
	quit $get(arrLines(+$order(arrLines(""))))
 
	
GetRelated(pidPricePlan)
 
	quit $$$INPricePlanRelatedParentRule($get(^INPricePlan(YM,pidPricePlan,1)))
]]></Routine>
</Export>