<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPROWEMINUS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPROWEMINUS(AUF,POS,FERT,ART,TEIL)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SUCHEN LAGERPLATZ DES AUSZUBUCHENDEN UNTERTEILS
	;
	; Called By : Routine INARTMINUS             (Form ?)
	; 
	; Inputs : 
	;	AUF		AUFTRAG DES FERTIGUNGSAUFTRAGES
	;	POS		POS. DES FERTIGUNGSAUFTRAGES (HAUPTTEIL)              .
	;	FERT	FERTIGUNGSNUMMER AKTUELLE FERTIGUNG (FERTIGUNGSTEIL)   \.
	;	ART		AUSZUBUCHENDER ARTIKEL (UNTERTEIL)                       \. ;item 
	;	TEIL	
	;
	; ByRef :
	;
	;
	; Returns :	RETVAL   Return Value : tilde-delimited INWE key list where keys are
	; 									comma-delimited (Item,Locn,StockLocn,Date)
	;
	;
	; History :
	; 05-Oct-2006	GRF		Doco; quits
	; 24.02.2003	FIS
	;-------------------------------------------------------------------------------
	NEW RETVAL,DATEI,YI,STEMPEL,STEMP,TEIL1
	
	;+++++++++++++++++++++++++++++++++++++++
	;  TEIL1		objINAUFPT
	;  WEN			objINWE
	;+++++++++++++++++++++++++++++++++++++++
	
	SET RETVAL=""
	SET AUF=$GET(AUF)         QUIT:AUF="" RETVAL
	SET POS=$GET(POS)         QUIT:POS="" RETVAL
	SET FERT=$GET(FERT)       QUIT:FERT="" RETVAL
	SET ART=$GET(ART)         QUIT:ART="" RETVAL
	SET TEIL=$GET(TEIL)
	
	IF FERT'="???" DO  ;ERMITTLUNG DES LAGERPLATZES ANHAND DER FERTIGUNGSNUMMER (EXAKTER LAGERPLATZ) ;inquiry the 
	. KILL STEMPEL
	. DO:TEIL'=""  IF TEIL="" FOR  SET TEIL=$ORDER(^INAUFPTs(YM,1,$$^WWWUMLAU(ART,1),AUF,POS,TEIL)) QUIT:TEIL=""  DO  ;SUCHEN ARTIKEL IN STRUKTUR ;seek item within texture 
	. . SET TEIL1=$GET(^INAUFPT(YM,AUF,POS,TEIL,1))
	. . QUIT:$PIECE(TEIL1,Y,165)=""       ;FERTIGUNGSNR. DES UNTER/FERTIGUNGSTEILS
	. . QUIT:$PIECE(TEIL1,Y,165)=FERT     ;KEINE WEITERE FERTIGUNG ;no 
	. . ;
	. . ;PRÜFEN, OB GEFUNDENER ARTIKEL AUCH TEIL DER AKTUELLEN FERTIGUNG IST
	. . SET DATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	. . FOR YI=1:1  QUIT:$PIECE(TEIL,".",YI+1,999)=""  SET DATEI=DATEI_","_$PIECE(TEIL,".",YI)  ;SETZEN KEY OBERTEIL ;typeset KEY 
	. . SET DATEI=DATEI_")"
	. . QUIT:'$DATA(@DATEI)
	. . IF $PIECE(@DATEI,Y,165)=FERT SET STEMPEL($PIECE(TEIL1,Y,165))=""  ;ARTIKEL IST UNTERTEIL DIESER FERTIGUNG ;item this 
	. ;
	. IF $DATA(STEMPEL) DO
	. . NEW BET,LAP,WED,WEN,YI,STEMP1
	. . SET BET=""
	. . FOR  SET BET=$ORDER(^INWE(YM,ART,BET)) QUIT:BET=""  DO
	. . . SET LAP=""
	. . . FOR  SET LAP=$ORDER(^INWE(YM,ART,BET,LAP)) QUIT:LAP=""  DO
	. . . . SET WED=""
	. . . . FOR  SET WED=$ORDER(^INWE(YM,ART,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . . SET WEN=$GET(^INWE(YM,ART,BET,LAP,WED,1))
	. . . . . ;IF $F($P(WEN,Y,9),";") SET $P(WEN,Y,9)=$P($P(WEN,Y,9),";",2)  ;LAGERPLATZ HINWEIS ENTHÄLT FERTIGUNGSNUMMER
	. . . . . FOR YI=1:1  QUIT:$PIECE($PIECE(WEN,Y,9),";",YI,99)=""   DO
	. . . . . . SET STEMP1=$PIECE($PIECE(WEN,Y,9),";",YI)
	. . . . . . IF STEMP1'="" IF $DATA(STEMPEL(STEMP1)) DO
	. . . . . . . IF RETVAL="" SET RETVAL=ART_","_BET_","_LAP_","_WED QUIT   ;GEFUNDENER LAGERPLATZ DIESER FERTIGUNGSNUMMER ;stock location this 
	. . . . . . . SET RETVAL=RETVAL_Y_ART_","_BET_","_LAP_","_WED            ;GEFUNDENE LAGERPLÄTZE DIESER FERTIGUNGSNUMMER ;this 
	
	IF FERT="???" DO  ;ERMITTLUNG DES LAGERPLATZES ANHAND DES URSPRUNGSAUFTRAGES (UNGENAU WENN ARTIKEL MEHRFACH IN STRUKTUR) ;inquiry when item within 
	. DO
	. . NEW BET,LAP,WED,WEN
	. . SET BET=""
	. . FOR  SET BET=$ORDER(^INWE(YM,ART,BET)) QUIT:BET=""  DO
	. . . SET LAP=""
	. . . FOR  SET LAP=$ORDER(^INWE(YM,ART,BET,LAP)) QUIT:LAP=""  DO
	. . . . SET WED=""
	. . . . FOR  SET WED=$ORDER(^INWE(YM,ART,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . . SET WEN=$GET(^INWE(YM,ART,BET,LAP,WED,1))
	. . . . . IF $PIECE(WEN,Y,28)=AUF IF $PIECE(WEN,Y,29)=POS DO  ;WE AUS FERTIGUNG MIT DIESEM AUFTRAG ;In out of by means of order 
	. . . . . . IF RETVAL="" SET RETVAL=ART_","_BET_","_LAP_","_WED QUIT  ;GEFUNDENER LAGERPLATZ ;stock location 
	. . . . . . SET RETVAL=RETVAL_Y_ART_","_BET_","_LAP_","_WED           ;GEFUNDENE LAGERPLÄTZE
	
	QUIT RETVAL
]]></Routine>
</Export>