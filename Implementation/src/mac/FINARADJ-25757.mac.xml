<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARADJ" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARADJ
	; 08-Jul-2003	SCR			Bug Fixes from Code Checking
 	#Include FINConst
	#Include FIN
	#Include COMConst
	#include WWWConst
 
OnBeforeSave(pYKEY,pYFELD)
    ;-------------------------------------------------------------------------------
    ; Save the line grid
    ;
    ; History:
    ; 10-Nov-2006	JW			Changed AP to AR
    ; 07-Sep-2005	PO			SR12943: On first save store location and department details
    ; 14-Apr-2005	Paul K		Created SR:12136
    ;-------------------------------------------------------------------------------
	new strStatus,objEmployeeData
	
	set strStatus=$$$GRIDSave(pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q=1
	} else {
		if $$$FINARADJCreatedBy(pYFELD) = "" {	// 10-Nov-2006	JW
			set objEmployeeData = $get(^WWW013(0,$get(YBED,"UNKNOWN"),1))
			set $$$FINARADJCreatorsLocation(pYFELD)   = $get(YLOCATION)
			set $$$FINARADJCreatorsDepartment(pYFELD) = $$$WWW013HomeDepartment(objEmployeeData)
		}
	}
	quit
 
 
OnAfterDataFields(pYKEY,plngPage)
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Returns:
    ;
    ; History:
    ; 23-Mar-2005	shobby		Renamed from AfterDataFields
    ;-------------------------------------------------------------------------------
	;
	if plngPage=1 {
		do LoadGrid(pYKEY)
	}
	quit
	
	
LoadGrid(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Display Edit Table form as part of this form.
    ;
    ; Returns:
    ;
    ; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
    ; 30-Sep-2005	JW		SR11573: Simplified update fields
    ; 10-Dec-2004	shobby	Removed null YKEY test (SR11175)
    ; 26-Oct-2004	shobby	Pass the name of the grid in, gets stored in CacheTemp
    ; 							for use later.
    ; 29-Sep-2004	Shobby	Created (SR10468)
    ;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
	
	set YFORM="FINARADJLine"
	set YFELD=""
	set YKEY=$get(pYKEY)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterUpdateFields(YAUSWAHL)  = "D8=3;D9=4;D10=3-4"
	set $$$COMGridEditParameterClearFields(YAUSWAHL)   = "3,4;4,3"
	set $$$COMGridEditParameterDefaultFields(YAUSWAHL) = 7
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "FINARADJ"
	do ^COMGridEdit31
	quit
 
 
OnBeforeButtonLine(pYVOR)
	;-------------------------------------------------------------------------------
	; Routines called Before Button Line
	; 
	; History
	; 16-Jun-2006		RPW			SR13173: check if the gl effective date has expired
	; 23-Mar-2005		shobby		Renamed from BeforeButtonLine
	; 06-Jul-2003		shobby		Creation
	; 
	;-------------------------------------------------------------------------------
	;
	do ReadOnly(YKEY)
	do CheckGLEffectiveDateExpiration^FINAPUtilities() // SR13173
	quit
 
 
Postable(pidAdjustment) 
    ;-------------------------------------------------------------------------------
    ; Determines whether an accounts receivable record can be posted.
    ;
    ; Returns:Status
    ;
    ; History:
    ; 26-Jun-2006	RPW		SR13173: Check if the GL Effective Date is valid
    ; 21-Sep-2005	PO		SR13522: No longer checking whether ref field contains a value already used for customer.
    ;  5-Jul-2005	JW		SR12527: Cleaned up status usage
    ; 31-May-2005	shobby	Quit with strStatus (SR12469)
    ; 01-Oct-2004	Paul K	Modified to look at editable routine
    ;-------------------------------------------------------------------------------
	new strStatus,objAdjustment
	set strStatus=$$$OK
 
	if $$$NoKey(pidAdjustment) {
		set strStatus=$lb("Fin00089")  ;No Record Selected.
	} elseif '$data(^FINARADJLine(YM,pidAdjustment)) {
		set strStatus=$lb("Fin00359")  ;Adjustment must have Lines.
	} else {
		
		set objAdjustment=$get(^FINARADJ(YM,pidAdjustment,1))
		// SR13173
		if $$$ISOK(strStatus) {
			set strStatus=$$ValidGLEffectiveDate^FINGLEffectiveDate($$$FINARADJTransactionDate(objAdjustment),$$$FINARADJTransactionDate(objAdjustment),$$$FldFINARADJTransactionDate)
		}
		if $$$ISOK(strStatus) {
			set strStatus=$$Editable^FINSYSReview("FINARADJ",$$$FINARADJStatus(objAdjustment))
		}
	}
	
	if $$$ISOK(strStatus) {
		set YQ=0
	} else {
		set YQ="1 "_$$$Text(strStatus)
	}
	
	quit strStatus
 
 
Post(pidAdjustment="")
	;-------------------------------------------------------------------------------
	; Post a customer adjustment to interface
	;
	; Params:	pidAdjustment - FINARADJ id
	;
	; ByRefs:
	;
	; Returns:	status
	;
	; History:
	; 24-Oct-2006	JW		SR15101: Created (copied from FINAPADJ)
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$Transaction^COMTransaction("PostTxn^FINARADJ("""_pidAdjustment_""")",1)
	
	if $$$ISOK(strStatus) {
		do ReloadForm^COMUtilForm()
	}
	quit strStatus
 
 
 
PostTxn(pidAdjustment)
    ;-------------------------------------------------------------------------------
    ; Post a customer adjustment to interface
    ;
    ; Returns:Status
    ;
    ; History:
    ; 24-Oct-2006	JW			SR15101: Use COMTransaction. Split into Post and PostTxn.
    ; 19-Oct-2006	JW			SR15134: Cleaned up.
    ; 28-Feb-2006	PO			SR14225: Added Drill Down Detail to Allocate
    ; 20-Oct-2005	PO			SR12943: Include Posted By and changed over to use $$$ISOK
    ; 20-Jun-2005	PO			SR12691: Incrementing the sub block number
    ; 31-May-2005	shobby		Call to the Postable routine (SR12469)
	; 19-Apr-2005	SCR			SR11476 Pass YLOCATION to Allocate Block
    ; 27-Jan-2005	RobertW		Fixed TCOMMIT and TROLLBACK (SR11273)
    ; 01-Oct-2004	Paul K		Added editable routine and cleanup.
	; 21-May-2003	shobby		Fixed some error handling issues.
    ;-------------------------------------------------------------------------------
	new strStatus,Batch,Block,idLine,objAdjustment,blnPost,intStatus
	new idLocation,dteTransaction,idCreator,idDepartment,strForm,strDetail ; SR14225
 
	//set $ztrap="PostError"	SR15101
	set strStatus=$$Postable(pidAdjustment)
 
	set blnPost=1
	set intStatus=""
	
	//tstart		SR15101
	
	/* SR15134: This code is all superfluous, it's in the $$Postable check
	if $$$ISOK(strStatus) if $$$NoKey(pidAdjustment) set strStatus=$$$Text("Fin00089")  ;No Record Selected.
	if $$$ISOK(strStatus) if '$data(^FINARADJLine(YM,pidAdjustment)) set strStatus=$listbuild("Fin00359")  ;Adjustment must have Lines.
	*/
	set objAdjustment=$get(^FINARADJ(YM,pidAdjustment,1))
	if $$$ISOK(strStatus) set strStatus=$$Editable^FINSYSReview("FINARADJ",$$$FINARADJStatus(objAdjustment),.blnPost,.intStatus)
 	
	if $$$ISOK(strStatus)&&(blnPost) {
		set idLocation    = $$$FINARADJCreatorsLocation(objAdjustment)
		set dteTransaction = $$$FINARADJTransactionDate(objAdjustment)
		set idCreator     = $$$FINARADJCreatedBy(objAdjustment)
		set idDepartment  = $$$FINARADJCreatorsDepartment(objAdjustment)
		if $$$ISOK(strStatus) {
			set strForm = $$CheckForm^FINUtils($get(YFORM),"FINARADJ")  ; Get the form using YFORM, with validation
			set strDetail = "Fin00991,"_pidAdjustment  ;Customer Adjustment Number %1
			set strStatus=$$Allocate^FININTBatch(.Batch,.Block,"ARADJ",idLocation,dteTransaction,,idCreator,idDepartment,strForm,pidAdjustment,strDetail)
		}
		;if $$$ISOK(strStatus) set strStatus=$$Allocate^FININTBatch(.Batch,.Block,"ARADJ",idLocation,dteTransaction,,idCreator,idDepartment)
		;if strStatus=1 set strStatus=$$Allocate^FININTBatch(.Batch,.Block,"ARADJ",$get(YLOCATION),$$$FINARADJTransactionDate(objAdjustment))
		set idLine=""
		for {
			set idLine=$order(^FINARADJLine(YM,pidAdjustment,idLine))
			quit:idLine=""
			quit:$$$ISERR(strStatus)
 
			do SubBlockNumber^FININTBatch(.Block)
 
			set strStatus=$$PostLine(pidAdjustment,idLine,Batch,Block)
		}
		if $$$ISOK(strStatus) set strStatus=$$PostBlock^FININTBatch(Batch,Block)
		if $$$ISOK(strStatus) {
			set $$$FINARADJPostedDate(objAdjustment)=$horolog
			set $$$FINARADJPostedBy(objAdjustment)  =$get(YBED,"UNKNOWN")
		}
	}
 
	if $$$ISOK(strStatus) {
		set $$$FINARADJStatus(objAdjustment)=intStatus
		set strStatus=$$$Save("FINARADJ",pidAdjustment,objAdjustment,1)
	}
	/*	SR15101
	if $$$ISOK(strStatus) {
		if $tlevel>0 tcommit
		do ReloadForm^COMUtilForm()
	} else {
		trollback
		do ^WWWINFO($$$Text(strStatus,0))
	} */
	quit strStatus
 
PostError
	trollback
	quit 0  ; This need more work ?????
 
 
PostLine(Number,Line,Batch,Block)
	; -----------------------------------------------------------------------------------------
	; Posts an adjustment line to the interface batch
	; 
	; History:
	; 26-May-2005	Paul K		Modified the source param of the ARADJGL AddLine
	; 07-Oct-2004	Paul K		SR10425: Add the adjustment header comment to the detail area of the invoice.
	; 23-May-2004	shobby		Corrected last to parameters of AddLine for ARADJ
	; 06-Apr-2004	shobby		Null discount treated as null, then discount will be calculated from default terms.
	; 15-Mar-2004	SCR			Treat null discount as zero
	; 29-Jun-2003	SCR			Added Invoice Date
	; 21-May-2003	shobby		Fixed the error handling code
	; -----------------------------------------------------------------------------------------
	new Status,Customer,Control,Account,Invoice,Charge,Credit,Amount,Due,Discount,Freight
	new Params,CusRec,Date,objAdjustment,objAdjustmentLine,Detail
 
	set Status=$$$OK
	set objAdjustmentLine=$get(^FINARADJLine(YM,Number,Line,1))
	set objAdjustment=$get(^FINARADJ(YM,Number,1))
	if objAdjustmentLine="" Set Status='$$$OK
	if Status=$$$OK {
		set Customer=	$$$FINARADJLineCustomer(objAdjustmentLine)
		set Invoice=	$$$FINARADJLineRef(objAdjustmentLine)
		set Charge=		$$$FINARADJLineCharge(objAdjustmentLine)
		set Credit=		$$$FINARADJLineCredit(objAdjustmentLine)
		set Due=		$$$FINARADJLineDue(objAdjustmentLine)
		set Discount=	$$$FINARADJLineDiscount(objAdjustmentLine)
		set Account=	$$$FINARADJLineGLAccount(objAdjustmentLine)
		set Freight=	$$$FINARADJLineFreightAmount(objAdjustmentLine)
		set Date=		$$$FINARADJLineInvoiceDate(objAdjustmentLine)
		set Detail=		$$$FINARADJComment(objAdjustment) ;SR 10425
		set Amount=		Charge-Credit
		
		;if Discount="" set Discount=0
		set Params=Customer_","_Invoice_","_Due_","_Discount_","_Date_","_Freight_","_Detail ;SR 10425
	}
	if Customer="" set Status=$listbuild("Fin00159")  ; Customer Number Required
	if Status=$$$OK {
		set CusRec=$get(^FINARCustomer(YM,Customer,1))
		if CusRec="" set Status=$listbuild("Fin00079",Customer)  ; Customer number %1 not Found
	}
	if Status=$$$OK {
		set Control=$$$FINARCustomerControlAccount(CusRec)
		if Control="" set Status=$listbuild("Fin00081",Customer)  ; Customer control account not valid for customer %1
	}
	if Status=$$$OK {	
		set Status=$$AddLine^FININTBatch(Batch,Block,Control,Amount,"ARADJ",Params,Number_","_Line,Customer_","_Invoice)
	}
	if Status=$$$OK {
		set Status=$$AddLine^FININTBatch(Batch,Block,Account,-Amount,"ARADJGL",Customer,Number_","_Line,Customer_","_Invoice)
	}
 
	quit Status
 
 
LastGLAccount(Key)  ; Get the Last Account , used for a the default GL account the next line
	new LastLine,Number,Acc,Rec
	set Number=$piece(Key,",",1)
	set (Rec,Acc)=""
	if Number'="" {
		set LastLine=$order(^FINARADJLine(YM,Number,""),-1)  ; Get Last Line
		if LastLine {
			set Rec=$get(^FINARADJLine(YM,Number,LastLine,1))
			set Acc=$$$FINARADJLineGLAccount(Rec)
		}
	}
 
	quit Acc
	
	/*
AfterLineFormLoad()
	;-------------------------------------------------------------------------------
	; History:
	; 23-Jun-2006	JW		SR12775: Redundant due to edit grid
	;-------------------------------------------------------------------------------
 
	new Charges,Credits,Total,Line,Number,Rec,lstHeader
	
	set lstHeader=""
	set lstHeader=lstHeader_$lb($$^WWWTEXT("Fin00155"))  ; Adjustment
	set lstHeader=lstHeader_$lb($$^WWWTEXT("Fin00156"))  ; Total Charges
	set lstHeader=lstHeader_$lb($$^WWWTEXT("Fin00157"))  ; Total Credits
	set lstHeader=lstHeader_$lb($$^WWWTEXT("Fin00158"))  ; Adjustment Total
	
	do Start^COMTable(lstHeader)
	
	set Number=$p(YKEY,",",1)
	
	if Number'="" {
		set Rec=$get(^FINARADJ(YM,Number,1))
		do NewLine^COMTable()
		do InsertCell^COMTable(Number)
		do InsertCurrencyCell^COMTable($$$FINARADJTotalCharges(Rec),$$$YES)
		do InsertCurrencyCell^COMTable($$$FINARADJTotalCredits(Rec),$$$YES)
		do InsertCurrencyCell^COMTable($$$FINARADJTotalAdjustments(Rec),$$$YES)
		do EndLine^COMTable()
	}
	
	do Stop^COMTable()
	
	quit
	*/
 
 
Balance(Number,pobjFINARADJ="",pblnSave=$$$YES)
	; Recalculate the balance on the Adjustment from the lines
	; 
	; NOTE: If changes are made here, might need to update for Suppliers also.
	;
	; History
	; 19-Nov-2004	shobby		Optional parameters pobjFINARADJ and pblnSave (SR10468)
	; 26-Oct-2004	shobby		Call to GetBalance (SR10468)
    ; 18-Jun-2004	shobby		Foreign Currency
	;-----------------------------------------------------------------------
	
	new Total,Charges,Credits,Status,Rec,RecSave
	set Status=$$$OK
	if Number="+" set Number=""
	
	if $get(Number)'="" {
		do GetBalance(Number,.Charges,.Credits)
		set Total=$$$FCSub(Charges,Credits)
		if pobjFINARADJ="" set pobjFINARADJ=$get(^FINARADJ(YM,Number,1))
		set RecSave=pobjFINARADJ
		set $$$FINARADJTotalCharges(pobjFINARADJ)=Charges
		set $$$FINARADJTotalCredits(pobjFINARADJ)=Credits
		set $$$FINARADJTotalAdjustments(pobjFINARADJ)=Total
		if pblnSave&&(pobjFINARADJ'=RecSave) {
			set Status=$$Save^FINSYS("FINARADJ",Number,pobjFINARADJ,1)
			}
		}
	quit Status
 
GetBalance(pidNumber,pcurCharges,pcurCredits,pcurTotal)
	; Recalculate the balance on the Adjustment from the lines
	; 
	; NOTE: If changes are made here, might need to update for Suppliers also.
	; 
	; History
	; 26-Oct-2004	shobby		Broken out of Balance (SR10468)
    ; 18-Jun-2004	shobby		Foreign Currency
	;-----------------------------------------------------------------------
	;
	new Line,objFINARADJLine
	
	set Line=""
	set pcurCharges=0
	set pcurCredits=0
 
	if pidNumber="+" set pidNumber=""
	
	if $get(pidNumber)'="" {
		for {
			set Line=$order(^FINARADJLine(YM,pidNumber,Line))
			quit:Line=""
			set objFINARADJLine=$get(^FINARADJLine(YM,pidNumber,Line,1))
			set pcurCharges=$$$FCAdd(pcurCharges,$$$FINARADJLineCharge(objFINARADJLine))
			set pcurCredits=$$$FCAdd(pcurCredits,$$$FINARADJLineCredit(objFINARADJLine))
		}
	}
	quit
 
 
 
ReadOnly(pidAdjustment)
    ;-------------------------------------------------------------------------------
    ; Make Lines Read Only if Posted
    ;
    ; History:
    ; 06-Oct-2004	Paul K		Test for "+"
    ; 01-Oct-2004	Paul K		Made use of editable routine.
    ;-------------------------------------------------------------------------------
	
	set pidAdjustment=$piece(pidAdjustment,",",1)
	if (pidAdjustment'="")&&(pidAdjustment'="+") {
		if '$$Editable^FINSYSReview("FINARADJ",$$$FINARADJStatus($get(^FINARADJ(YM,pidAdjustment,1)))) {
			set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
			set $$$WWW120DoNOTDisplayStandardButto(YVOR)=$$AddItem^FINUtils($$$WWW120DoNOTDisplayStandardButto(YVOR),"15")  ;TR005552
		}
	}
	quit
 
FieldSet
	; Set the Visiblity of Fields on the Adjustment Line
	New Number,Form,Rec,Fld,Control,ControlRec,FreightTrack,Status,Type
	
	set Status=$$$OK
	set Number=""
	set Form="FINARADJLine"
	set Control=$$DefaultControl^FINARCustomer()
	set FreightTrack='$$$OK
	set ControlRec=""
	if Control="" set Status='$$$OK
	if Status=$$$OK {
		set ControlRec=$get(^FINARAccount(YM,Control,1))
		set FreightTrack=$piece(ControlRec,Y,4)
	}
	
	for {  
		set Number=$order(^WWW122(0,Form,Number))  ; Check all Fields looing for Freight
		quit:'Number
		set Rec=$get(^WWW122(0,Form,Number,1))
		set Fld=+Rec
		if Fld=9 {
			set Type=15 						; Default Hidden
			if FreightTrack=$$$OK {
				set Type=2						; Show (Yes/No)
			}
			set $piece(Rec,Y,2)=Type
			set ^WWW122(0,Form,Number,1)=Rec
		}		
	}
 
	quit
 
GetType()
	;-------------------------------------------------------------------------------
	; Get the Type for the Effective Date
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	; The type of the effective date
	;
	; History:
	; 26-Jun-2006	RPW		SR13173: Created
	;-------------------------------------------------------------------------------
	quit "ARADJ"
]]></Routine>
</Export>