<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSKEY" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSKEY(SCHLUESS,SORT) ;WWWSKEY;DT;SUCHEN SORTFELD IN DATENFELDER;01.10.1999
#include WWWConst
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		Get the index values for a given record and index key
	;|
	;| Inputs : SCHLUESS	- string ref of data record. eg '^INAUF(0,"X",1)'
	;|			SORT		- index key
	;|
	;| ByRef :
	;|
	;|
	;| Returns : string - comma separated index values
	;|
	;|
	;| History :
	;|
	;| DT	01.10.1999
	;|
	;\------------------------------------------------------------------/
	;
	;SCHLUESS=GESAMTER DATENSATZ ;data record 
	;SORT=SORTIERSCHLUESSEL
	;RETURN SIND DIE SORTFELDER ;who 
	NEW YID,YFELD,YDATA,YSKEY,YA
	QUIT:'$DATA(SCHLUESS) ""
	QUIT:SCHLUESS="" ""
	QUIT:'$DATA(SORT) ""
	QUIT:+SORT=0 ""
	
	IF $FIND(SCHLUESS,"^[") SET SCHLUESS="^"_$PIECE(SCHLUESS,"]",2,99)
	SET YFELD=$$^WWWSETL(SCHLUESS)
	SET YID=$PIECE($PIECE(SCHLUESS,"(",1),"^",2)  ;Class name 
	QUIT:YID="" ""
	
	SET YA=""
	SET YI="" FOR  SET YI=$ORDER(^WWW002(0,YID,YI)) QUIT:YI=""  DO
	. SET YSKEY=$$$WWW002IndexKey($GET(^WWW002(0,YID,YI,1)))   ;SORTIERSCHLUESSEL  PRIMAERSCHLUESSL
	. QUIT:YSKEY=""
	. FOR YI(1)=1:1 QUIT:$PIECE(YSKEY,",",YI(1))=""  SET YI(2)=$PIECE(YSKEY,",",YI(1)) DO
	. . QUIT:SORT'=$PIECE(YI(2),".",1)
	. . IF +$PIECE(YI(2),".",2)=0 SET $PIECE(YI(2),".",2)=1
	. . SET $PIECE(YA,",",$PIECE(YI(2),".",2))=$$^WWWUMLAU($TRANSLATE($PIECE(SCHLUESS,",",YI+1),""""),1)
	
	SET YI="" FOR  SET YI=$ORDER(^WWW003(0,YID,YI)) QUIT:YI=""  DO
	. SET YSKEY=$$$WWW003IndexKey($GET(^WWW003(0,YID,YI,1)))  ;SORKEY DATENFELD ;data item 
	. QUIT:YSKEY=""
	. FOR YI(1)=1:1 QUIT:$PIECE(YSKEY,",",YI(1))=""  SET YI(2)=$PIECE(YSKEY,",",YI(1)) DO
	. . QUIT:SORT'=$PIECE(YI(2),".",1)
	. . IF +$PIECE(YI(2),".",2)=0 SET $PIECE(YI(2),".",2)=1
	. . SET $PIECE(YA,",",$PIECE(YI(2),".",2))=$$^WWWUMLAU($PIECE(YFELD,Y,YI),1)
	
	IF YA'="" DO  ;SUCHEN LEER FELDER ;seek void 
	. FOR YI=1:1:$LENGTH(YA,",") DO
	. . IF $PIECE(YA,",",YI)="" SET $PIECE(YA,",",YI)=" "
	
	QUIT YA
]]></Routine>
</Export>