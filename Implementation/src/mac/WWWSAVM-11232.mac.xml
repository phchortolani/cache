<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSAVM" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSAVM
	
#include COMSYS
#include WWWConst
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Check Form Manual Data Field	
	;		PRUEFEN MANUELLE FELDER
	;		
	; NOTE: Manual data YM(YI) is in external (display) format
	;
	; Called By : WWWSAVE
	; 
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 01-Jun-2010	GRF		SR17146: Call common code for Dates
	; 15-Mar-2007	GRF		SR12505: Implement code for $$$WWW122DontAllowChars;
	; 							Naked References; Macros; doco
	; 11-Aug-2006	JW		SR13594: Move code into PRUEFM
	; 09.10.1997	DT
	;-------------------------------------------------------------------------------
	SET Q=0,YLFZ=0
	SET YI=""
	FOR  SET YI=$ORDER(^WWW122s(0,4," ",YFORM,YI)) QUIT:YI=""  DO   ;prüfen der manuellen felder ;sift who 
	. NEW YSATZ
	. SET YSATZ=$GET(^WWW122(0,YFORM,YI,1))
	. IF '$DATA(YM(YI)) SET YM(YI)=""
	. IF $$$WWW122InputInVariable(YSATZ)'="" SET @($$$WWW122InputInVariable(YSATZ))=YM(YI)
	. ;
	. set YM(YI) = $$PRUEFM(YFORM,YI,YM(YI))  ;pruefung
	. 
	. SET $PIECE(YMFELD,Y,YI)=YM(YI)
	. IF $$$WWW122DataConversion(YSATZ)>0 DO  ;ALTERNATIVE SPEICHERUNG ;option 
	. . SET YID("S") = Y_Y_$PIECE(YSATZ,Y,5)_Y_$PIECE(YSATZ,Y,6)_Y_Y_Y_Y_Y_$PIECE(YSATZ,Y,32)_Y_$PIECE(SATZ,Y,33)_Y_$PIECE(SATZ,Y,34)
	. . SET YID("Y") = YM(YI)
	. . DO ^WWWTRAN(1,$$$WWW122DataConversion(YSATZ))
	. . SET YM(YI)=YID("Y")
	. ;
	. IF $$$WWW122InputInVariable(YSATZ)'="" SET @($$$WWW122InputInVariable(YSATZ)) = $GET(YM(YI))
	
	SET FMFELD = $TRANSLATE(YMFELD,$CHAR(13,10),"|")  ;MANUELLE
	DO ^WWWFORM2
	
	IF $DATA(^WWWSOR(YUSER)) DO  SET Q=1 QUIT
	. SET YA="",YI=""
	. FOR  SET YI=$ORDER(^WWWSOR(YUSER,YI)) QUIT:YI=""  DO
	. . SET YA=YA_$GET(^WWWSOR(YUSER,YI))
	. ;
	. DO ^WWWINFO(YA)
	
	QUIT
 
	
PRUEFM(YFORM,YI,A)
	;-------------------------------------------------------------------------------
	; Examine Manual Fields
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
 	; 09-Aug-2011	shobby	SR17807: GetFormat has moved.
	; 06-Jan-2011	GRF		SR17579: Use explicit tag
	; 19-Jul-2010	SCR		SR17407: Use strFieldName as Text
	; 20-Oct-2008	GRF		SR12505: Corrections to OnlyAllowChars & DontAllowChars
	; 							tests; Recognise WWW122D
	; 20-Aug-2007	GM		SR13729: Change "FELDFORMAT" parameter to $$GetFormat^INPARA()
	; 15-Mar-2007	GRF		SR12505: Implement code for $$$WWW122DontAllowChars; use
	; 							macros; use strFieldName, idChangeInput
	; 08-Aug-2006	JW		SR13594: Replace YM(YI) with A. Return A. Encapsulated
	;  							validation.
	; 24-Mar-2006	JW		SR14422: Change to internal format
    ; 28-Oct-2005	JW		SR13074: Exchange Rate type
	; 25-Jul-2005	JW		SR12615: Relation validation
	; 30-May-2005	RobertW	SR12056: Attempt at Performance Increase
	;-------------------------------------------------------------------------------
	new internalA,Q,objCustomData,strAllowChars,strBlockChars,strFieldName
	new strStatus,strTestChar,YPR,YPRUEF,YTYP
	
	set YPRUEF        = $get(^WWW122(0,YFORM,YI,1))
	set objCustomData = $get(^WWW122D(0,YFORM,YI,YM,1))
	SET YTYP=$$$WWW122InputType(YPRUEF)
	
	; Field Name - for error reporting
	;---------------------------------------
	IF $DATA(^WWW1221(0,YFORM,YI,SPRACHE,1)) SET $PIECE(YPRUEF,Y,12)=$PIECE(^WWW1221(0,YFORM,YI,SPRACHE,1),Y,1)
	set strFieldName = $PIECE(YPRUEF,Y,12)
	
	IF +$$$WWW122DisplayOnPageNumber(YPRUEF)<1 SET $$$WWW122DisplayOnPageNumber(YPRUEF)=1  ;mindestens auf seite 1; Tab 1 
	IF YSEITE'=+$$$WWW122DisplayOnPageNumber(YPRUEF) QUIT A  ;FALSCHE SEITE ;side 
	
	; Valid Relation, Mandatory Checks
	;---------------------------------------
	set strStatus = $$Validate(YFORM,YI,.A,$$$YES)
	if $$$ISERR(strStatus) {
		set A = ""			; Clear field if not valid
		set ^WWWSOR(YUSER,$increment(YLFZ)) = strFieldName_": "_$$$Text(strStatus)_"|"
	}
	
	; "Min. Input Length"                   ;14=mindeslänge
	;---------------------------------------
	if (+$$$WWW122MinInputLength(YPRUEF)'=0) && ($length(A)<$$$WWW122MinInputLength(YPRUEF)) {
		set ^WWWSOR(YUSER,$increment(YLFZ))= strFieldName_": "_$$^WWWTEXT(268)_" "_+$$$WWW122MinInputLength(YPRUEF)_"|"
	}
	
	; "Only Following Characters Allowed"   ;17=pruefkette
	;---------------------------------------
	set strAllowChars = $$$WWW121OnlyAllowChars(YPRUEF)
	if strAllowChars'="" {
		for YPR1=1:1:$length(A) {
			set strTestChar = $extract(A,YPR1)
			
			if '$find(strAllowChars,strTestChar) {
				set ^WWWSOR(YUSER,$increment(YLFZ))=strFieldName_": "_$$^WWWTEXT(269)_" "_strAllowChars_"|"
				quit
			}
		}		
	}
	
	; "The following characters are not allowed"
	;---------------------------------------
	set strBlockChars = $$$WWW122DDontAllowCharacters(objCustomData)
	if strBlockChars="" set strBlockChars = $$$WWW122DontAllowChars(YPRUEF)
	
	for YPR1=1:1:$length(strBlockChars) {
		set strTestChar = $extract(strBlockChars,YPR1)
		continue:strTestChar=" "
		
		if A[strTestChar {
			set ^WWWSOR(YUSER,$increment(YLFZ)) = strFieldName_": "_$$^WWWTEXT(287)_" "_strBlockChars_"|"
			quit
		}
	}
	
	; Fill with zeros (max 15 leading zeros)
	; FIXME : Strings with leading digits will get zero fill, those with leading alpha will not <GRF> 
	;---------------------------------------
	IF +$$$WWW122ForceStringLengthTo(YPRUEF)'=0 IF +A'=0 SET A=$EXTRACT((1_$EXTRACT("000000000000000",1,+$$$WWW122ForceStringLengthTo(YPRUEF))+A),2,99)
	
	; Character conversion
	;---------------------------------------
	set idChangeInput = $$$WWW122ChangeInputAs(YPRUEF)
	IF +idChangeInput'=0 DO  ;formatumformung
	. IF idChangeInput="1" SET A=$$$UPPER(A)
	. IF idChangeInput="2" SET A=$$$LOWER(A)
	. IF idChangeInput="3" DO  ;NAMENSKONVENTION
	. . SET YPR=A,A=""
	. . FOR YPR1=1:1 SET YPR2=$PIECE(YPR," ",YPR1) QUIT:$PIECE(YPR," ",YPR1,999)=""  SET A=A_$$$UPPER($EXTRACT(YPR2))_$$$LOWER($EXTRACT(YPR2,2,5000))_" "
	. ;
	. IF idChangeInput="4" DO    ;DISABLED 
	. . SET YPR=A,A=""
	. . FOR YPR1=1:2 SET YPR2 = $EXTRACT(YPR,YPR1,YPR1+1) QUIT:YPR2=""  SET:$EXTRACT(YPR2,2)'=" " YPR2=$EXTRACT(YPR2)_" "_$EXTRACT(YPR2,2)_" " SET A=A_YPR2
	. ;
	. IF idChangeInput="5" SET A = $TRANSLATE(A," ")                                     ;OHNE LEER ;without void 
	. IF idChangeInput="6" SET A = $TRANSLATE($$$UPPER(A)," ")                           ;OHNEN LEER UND GROß ;void And macro- 
	. IF idChangeInput="7" SET A = $TRANSLATE(A,"][\}{|~,()@#$%^&*_=+<>?/-.:´`§²³!°µ")  ;OHNE SONDERZEICHEN;FIS;08.04.03 ;without special character 
	
	; SR17146 vvv
	; Check date
	;---------------------------------------
	;if (YTYP=1) && (A'="") set A = $$CheckDate^WWWSAVD(A,SPRACHE,YUSER,YTEXT,.YLFZ)
	if (YTYP=1) && (A'="") set A = $$CheckDate^WWWSAVD(A,SPRACHE,YUSER,strFieldName,.YLFZ) ;SR17407
	
	/*
	; Check date     ; "Wrong Date", "Wrong Date Format"
	;---------------------------------------
	IF YTYP=1 DO
	. QUIT:A=""
	. IF A="." SET A=$$^WWWDATE($HOROLOG)
	. IF $EXTRACT(A)="-" IF +$EXTRACT(A,2,9)'=0 SET A=$$^WWWDATE($HOROLOG-$EXTRACT(A,2,9))
	. IF $EXTRACT(A)="+" IF +$EXTRACT(A,2,9)'=0 SET A=$$^WWWDATE($HOROLOG+$EXTRACT(A,2,9))
	. IF $LENGTH(A,".")=3 IF $LENGTH($PIECE(A,".",3))=2 IF $LENGTH(A)=8 DO 
	. . IF $EXTRACT(A,7,8)>25 SET A=$EXTRACT(A,1,2)_"."_$EXTRACT(A,4,5)_"."_$EXTRACT($PIECE($$^WWWDATE($HOROLOG),".",3),1,2)_$EXTRACT(A,7,8) QUIT
	. . IF $EXTRACT(A,7,8)<26 SET A=$EXTRACT(A,1,2)_"."_$EXTRACT(A,4,5)_"."_20_$EXTRACT(A,7,8)
	. ;
	. IF $LENGTH(A)=8 IF $LENGTH(A,".")'=3 SET A=$EXTRACT(A,1,2)_"."_$EXTRACT(A,3,4)_"."_$EXTRACT(A,5,8)
	. IF A'="" SET YPR=$$^WWWDATE1(A) IF YPR=""                                       SET ^WWWSOR(YUSER,$increment(YLFZ))=""_strFieldName_": "_$$^WWWTEXT(271)_"|"
	. IF SPRACHE="DE" IF A'="" IF '$FIND(A,"W") IF '$FIND(A,"w") IF $LENGTH(A,".")'=3 SET ^WWWSOR(YUSER,$increment(YLFZ))=""_strFieldName_": "_$$^WWWTEXT(272)_"|"
	
	*/
	; SR17146 ^^^
	
	; Time     ; "Wrong Time", "Wrong Time Format"
	;---------------------------------------
	IF YTYP=7 DO
	. QUIT:A=""
	. IF A'="" IF '$FIND(A,":") DO
	. . IF $LENGTH(A)<3 SET A=+A_":00"
	. . IF $LENGTH(A)=3 SET A=+$EXTRACT(A)_":"_+$EXTRACT(A,2,3)
	. . IF $LENGTH(A)=4 SET A=+$EXTRACT(A,1,2)_":"_+$EXTRACT(A,3,4)
	. ;
	. IF A="." SET A=$$^WWWTIME($HOROLOG)
	. IF A'="" SET YPR=$$^WWWTIME1(A) IF YPR="" SET ^WWWSOR(YUSER,$increment(YLFZ))=""_strFieldName_": "_$$^WWWTEXT(273)_"|"
	. IF A'="" IF $LENGTH(A,":")<2              SET ^WWWSOR(YUSER,$increment(YLFZ))=""_strFieldName_": "_$$^WWWTEXT(274)_"|"
	
	; Amount fields  "Wrong Amount"
	;---------------------------------------
	IF (YTYP=8) || (YTYP=12) || (YTYP=18) DO
	. NEW A1,B1,B2,Z
	. SET Z=0
	. DO:'$FIND(A,"@")  IF $FIND(A,"@") SET B1=$PIECE(A,"@",1) SET B2=$PIECE(A,"@",2) SET B3=$PIECE(A,"@",3) NEW A SET A=B1 DO  SET A=B2 DO  SET A=B3 DO  ;FIS;21.05.04;25727;FREMDWÄHRUNG
	. . NEW YYDECIMAL
	. . ;SR17807 SET YYDECIMAL = $$GetFormat^INPARA(8,YDECIMAL)
	. . SET YYDECIMAL = $$GetFormat^WWW100(8,YDECIMAL) ;SR17807 
	. . SET YYDECIMAL = $EXTRACT($TRANSLATE(YYDECIMAL,"nNxX"),2)
	. . IF YYDECIMAL="" SET YYDECIMAL = YDECIMAL
	. . IF $GET(YYDECIMAL)="," IF $LENGTH(A,",")>2 SET:Z=0 YLFZ=YLFZ+1 SET Z=1 SET ^WWWSOR(YUSER,YLFZ)=strFieldName_": "_$$^WWWTEXT(275)_" "_A_"|"
	. . IF $GET(YYDECIMAL)="." IF $LENGTH(A,".")>2 SET:Z=0 YLFZ=YLFZ+1 SET Z=1 SET ^WWWSOR(YUSER,YLFZ)=strFieldName_": "_$$^WWWTEXT(275)_" "_A_"|"
	
	; Pattern Match  "Wrong Input Format"
	;---------------------------------------
	IF $$$WWW122PatternMatch(YPRUEF)'="" SET YPR=$$$WWW122PatternMatch(YPRUEF) IF A?@YPR=0 SET ^WWWSOR(YUSER,$increment(YLFZ))=""_strFieldName_": "_$$^WWWTEXT(276)_"|"
	
	;
	;---------------------------------------
	IF YTYP=1  SET A = $$^WWWDATE1(A)
	IF YTYP=17 SET A = $$GetInternal^WWWTR(17,A)  ;TYBD;UMWANDLUNG FELDTYPE 17;26307;7,9,2004
	IF YTYP=7  SET A = $$^WWWTIME1(A)
	
	IF (YTYP=8) || (YTYP=12) || (YTYP=18) {
		IF $GET(YDECIMAL)'="." {
			SET A=$TRANSLATE(A,",.",".")
		} else {
			SET A=$TRANSLATE(A,",")
		}
	}
	IF YTYP=4 IF A'="" SET A=+A
	
	QUIT A
	
	
Validate(pidForm,pidField,&pstrValue,pblnDisplay=$$$NO)
	;-------------------------------------------------------------------------------
	; Validation of manual field
	;
	; Called By : WWWFORMValidation, PRUEFM^WWWSAVM
	; 
	; Params:	pidForm 	- form id
	; 			pidField	- form field number (WWW122)
	; 			pblnDisplay	- whether pstrValue is display value (or storage)
	;
	; ByRefs:	pstrValue	- field value, can be set to null
	;
	; Returns:	status - whether there is a problem
	;
	; History:
	; 18-May-2012	shobby	SR18015: Validation for mandatory will now also consider core rules.
	; 08-Aug-2006	JW		SR13594: Created
	;-------------------------------------------------------------------------------
	new strStatus,objField,strReason,blnMandatory,strYOPTION,strYOPTION1,blnNull
	new strIn,strOut,idType
	
	set strStatus = $$$OK
	set objField  = $get(^WWW122(0,pidForm,pidField,1))
	set idType    = $$$WWW122InputType(objField)
	
	set strIn = $select(pblnDisplay:$$GetInternal^WWWTR(idType,pstrValue),1:pstrValue)
	
	if '$$ValidRelation^WWWFieldValidation("M","",pidForm,pidField,strIn,$$$NO,.strReason) {
		if $get(strReason)="" {
			set strOut    = $select(pblnDisplay:pstrValue,1:$$GetLiteral^WWWTR(idType,pstrValue))
			set strReason = $listbuild("WWW00028",strOut)                           ; "´%1´ is invalid"
		}
		set pstrValue = ""	;Clear field if not valid
	}
	
	if (pstrValue="") && (idType'=2) {
		set blnMandatory = $$$WWW122MandatoryInputItem(objField)
		if 'blnMandatory {
			if ($get(YM)'="") && $$$WWW122DMandatoryField($get(^WWW122D(0,pidForm,pidField,YM,1))) {
				set blnMandatory=$$$YES
			}
		}
		if 'blnMandatory {  ;SR18015 vvvv
			new YMFELD
			set YMFELD=$$GetYMFELDContainer^COMGridEdit31Interface(1)

			do CheckRules^WWWFORMD(pidForm,pidField,$get(objSrcData),"","","",.blnMandatory,$$$YES,"","","","","","",pstrValue,"")
		}					;SR18015 ^^^^
		
		if blnMandatory {
			set strYOPTION  = $$$WWW122OnlyShowIfOptionYOPTION(objField)
			set strYOPTION1 = $$$WWW122OnlyShowIfOptionYOPTION1(objField)
			set blnNull     = $$$NO
			
			if strYOPTION'="" {
				if $find(","_strYOPTION_",",","_$get(YOPTION)_",")   set blnNull = $$$YES
			} elseif strYOPTION1'="" {
				if $find(","_strYOPTION1_",",","_$get(YOPTION1)_",") set blnNull = $$$YES
			} else {
				set blnNull = $$$YES
			}
			if blnNull {
				set strStatus = $select(strReason="":$listbuild(267),1:strReason)      ; "Mandatory Field"
			}
		}
	}
	quit strStatus
	
]]></Routine>
</Export>