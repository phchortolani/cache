<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARTempImportFornecedorItem" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARTempImportFornecedorItem
	#include INConst
	#include VARConst
	
	quit
	
RodarAntesImportacao()
	;-----------------------------------------------------------------------------
	; Descrição:
	;	Mata a global temporária baseado no usuário e sessão
	;
	; Chamado Por:
	;	Executar rotina antes de importar da configuração no formulário VARImportarConfig, configuração 003
	;
	; Histórico
	;	22-Dez-2016		FC		LEAN-14: Criado
	;-----------------------------------------------------------------------------
	$$$VAR
	
	kill ^VARTempImportFornecedorItem(YM,YBED,YUSER)
	
	quit $$$OK
	
	
RodarAposImportacao()
	;-----------------------------------------------------------------------------
	; Descrição:
	;	Processa os registros que foram salvos na classe temporária, recuperando-os pela sessão / usuário
	;	Mata a global temporária baseado no usuário e sessão
	;
	; Chamado Por:
	;	Executar rotina depois de importar da configuração no formulário VARImportarConfig, configuração 003
	;
	; Histórico
	;	22-Dez-2016		FC		LEAN-14: Criado
	;-----------------------------------------------------------------------------
	new strStatus, sqlForn, objForn, sqlForn, sqlLinha, sqlProd, sqlPref, lstProd
	
	$$$VAR
	
	&SQL(
		DECLARE C CURSOR FOR
		SELECT DISTINCT Fornecedor
		FROM VARTempImportFornecedorItem
		WHERE YBED = :YBED
		AND YUSER = :YUSER
	)
	
	&SQL(OPEN C)
	
	for {
		&SQL(FETCH C INTO :sqlForn)
		quit:(SQLCODE '= 0)
		
		set objForn = $get(^INLIEF(YM,sqlForn,1))
		continue:(objForn = "")
		
		&SQL(
			DECLARE C1 CURSOR FOR
			SELECT Fornecedor, Linha, Produto, Preferencial
			FROM VARTempImportFornecedorItem
			WHERE YBED = :YBED
			AND YUSER = :YUSER
			AND Fornecedor = :sqlForn
		)
		
		&SQL(OPEN C1)
		
		set lstProd = $$$INLIEFFREE15(objForn)
		for {
			&SQL(FETCH C1 INTO :sqlForn, :sqlLinha, :sqlProd, :sqlPref)
			quit:(SQLCODE '= 0)
			
			if ($find(";"_lstProd_";",";"_sqlProd_";") = 0) {
				if (lstProd = "") {
					set lstProd = sqlProd
				}
				else {
					set lstProd = lstProd _ ";" _ sqlProd
				}
			}
			
			if ($zcvt(sqlPref,"U") = "S") {
				set objProd = $get(^INART(YM,sqlProd,1))
				continue:(objProd = "")
				
				set $$$INARTFREE36(objProd) = sqlForn
				
				set strStatus = $$$Save("INART",sqlProd,objProd,1)
			}
		}
		&SQL(CLOSE C1)
		
		set $$$INLIEFFREE15(objForn) = lstProd
		
		set strStatus = $$$Save("INLIEF",sqlForn,objForn,1)
	}
	
	&SQL(CLOSE C)
	
	kill ^VARTempImportFornecedorItem(YM,YBED,YUSER)
	set ^CacheTempFC($i(^CacheTemp),$zn) = strStatus
	quit strStatus
	
ValidaItem(pItem="")
	;-----------------------------------------------------------------------------
	; Descrição:
	;	Valida se o item existe
	;
	; Chamado Por:
	;	Rotina para validar do campo Produto
	;
	; Histórico
	;	22-Dez-2016		FC		LEAN-14: Criado
	;-----------------------------------------------------------------------------
	$$$VAR
	
	new strStatus
	
	set strStatus = $$$OK
	
	if ('$data(^INART(YM,pItem))) {
		set strStatus = "Erro;-Item inválido."
	}
	
	quit strStatus
	
ValidaFornecedor(pForn="")
	;-----------------------------------------------------------------------------
	; Descrição:
	;	Valida se o fornecedor existe
	;
	; Chamado Por:
	;	Rotina para validar do campo Fornecedor
	;
	; Histórico
	;	22-Dez-2016		FC		LEAN-14: Criado
	;-----------------------------------------------------------------------------
	$$$VAR
	
	new strStatus
	
	set strStatus = $$$OK
	
	if ('$data(^INLIEF(YM,pForn))) {
		set strStatus = "Erro;-Fornecedor inválido"
	}
	
	quit strStatus

ValidaPadrao(pPadrao="")
	;-----------------------------------------------------------------------------
	; Descrição:
	;	Valida se a opção é válida
	;
	; Chamado Por:
	;	Rotina para validar do campo Preferencial
	;
	; Histórico
	;	22-Dez-2016		FC		LEAN-14: Criado
	;-----------------------------------------------------------------------------
	$$$VAR
	
	new strStatus
	
	set strStatus = $$$OK
	
	if (($zcvt(pPadrao,"U") '= "S") && ($zcvt(pPadrao,"U") '= "N")) {
		set strStatus = "Erro;-Dado do campo Preferêncial inválido."
	}
	
	quit strStatus


	


	


]]></Routine>
</Export>