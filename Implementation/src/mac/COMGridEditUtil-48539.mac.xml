<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEditUtil" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEditUtil
	
#include COMSYS
#include COMConst
	
	;-------------------------------------------------------------------------------
	;  Utilities relating to Edit Grid Operation - common access
	;-------------------------------------------------------------------------------
	
CellColours(&pstrDisabled,&pstrRequired,&pstrWarning,pobjCompany="",pobjFormFld="",pidForm="",pintCol="")
	; Possibly deprecated with SR15753 - some calls still
	;-------------------------------------------------------------------------------
	; 
	; Inputs :
	;	pobjCompany		Pass in the WWW012 record if needed externally or will get here
	;	pobjFormFld		Pass in the WWW122 customised record if needed externally or
	;						will get here from pidForm and pintCol
	; 
	; Call Example :
	;	set strDefaultColor = $$CellColours(.strDisabledColor,.strRequiredColor,.strWarningColor,"","","INMOVLine",intCol)
	;	set strDefaultColor = $$CellColours(.strDisabledColor,.strRequiredColor,.strWarningColor,"",objFormFldDefn)
	;	set strDefaultColor = $$CellColours(.strDisabledColor,.strRequiredColor,.strWarningColor,objWWW012,"","INMOVLine",intCol)
	;	set strDefaultColor = $$CellColours(.strDisabledColor,.strRequiredColor,.strWarningColor,objWWW012,objWWW122)
	;	set strDefaultColor = $$CellColours(.strDisabledColor,.strRequiredColor,.strWarningColor)             ; set generic default colour
	;	set strDefaultColor = $$CellColours(.strDisabledColor,.strRequiredColor,.strWarningColor,objWWW012)   ; set generic default colour
	; then	
	;	set YBACKGROUNDCOLOR = strDefaultColor   (usually)
	; 
	; Returns : The default color for the field
	; 
	; ByRef : 
	;	YFONTCOLOR
	;	YGRIDWHITE
	;	pstrDisabled
	;	pstrRequired
	;	pstrWarning
	; 
	; History:
	; 20-Mar-2009	GRF		SR16428: Created
	;-------------------------------------------------------------------------------
	new strDefaultColor
	
	; vvvvvvv THIS IS NECESSARY - at least for YFONTCOLOR based on INMOVLine2 - but why? <GRF>
	; Only used when field is set to mandatory, is setup in COMGridEdit31Head but why isn't it referenced down?
	if '$data(YGRIDWHITE) set YGRIDWHITE = "ivory"
	if '$data(YFONTCOLOR) set YFONTCOLOR = "black"
	; ^^^^^^^
	
	set strDefaultColor = YGRIDWHITE
	
	if pobjCompany = "" set pobjCompany = $get(^WWW012(0,0,1))
	
	set pstrWarning  = $$$SysEnum("FARBE",$$$WWW012ColorforWarnings(pobjCompany))
	set pstrRequired = $$$SysEnum("FARBE",$$$WWW012FontColorMandatoryInput(pobjCompany))
	set pstrDisabled = $$$SysEnum("FARBE",$$$WWW012BackgroundColor(pobjCompany))
	
	if ((pobjFormFld '= "") || ((pidForm'="") && (pintCol'=""))) {
		if pobjFormFld = "" set pobjFormFld = $$Get^WWW122(pidForm,pintCol)
		
		if $$$WWW122UserAccessForChange(pobjFormFld) = $$$EnumReadOnly {
			set strDefaultColor = pstrDisabled
			
		} elseif $$$WWW122MandatoryInputItem(pobjFormFld) = $$$YES {
			set strDefaultColor = pstrRequired
			
		} else {
			set strDefaultColor = YGRIDWHITE
		}
	}
	quit strDefaultColor
	
	
	;+++++++++++++++++++++++++++++++++++++++
	; DEPRECATED - Use new function : GetDecimalPlaces^COMUtilNum 
	;+++++++++++++++++++++++++++++++++++++++
	
GetQtyDecimals(pidForm,pidFormFldNo,pidUnit="",pintWWW003dp="")
	;-------------------------------------------------------------------------------
	; LEGACY TAG ONLY - Use direct link. Known calls to this tag have been replaced.
	; 
	; History:
	; 25-May-2011	GRF		SR17250: revised & relocated to GetDecimalPlaces^COMUtilNum
	; 04-Feb-2011	GRF		SR17579: Created from GetDecimalPointNum
	;-------------------------------------------------------------------------------
	new idItem,idUnit,intDecimalPoints,objWWW122
	
	quit:(pidForm="")||(pidFormFldNo="") ""
	
	set objWWW122 = $$Get^WWW122(pidForm,pidFormFldNo)
	set intDecimalPoints = $$$WWW122Decimals(objWWW122)
	
	if (intDecimalPoints="") && (pidUnit'="") {
		set intDecimalPoints = $$$COMUnitDecimalPlaces($get(^COMUnit(0,pidUnit,1)))
	}
	
	if (intDecimalPoints="") && (pintWWW003dp'="") { ; WWW003 NoOfDecimals
		set intDecimalPoints = pintWWW003dp
	}
		
	if (intDecimalPoints="") {
		set intDecimalPoints = $get(YDECIMALLEN,2)
	}
	quit intDecimalPoints
	
]]></Routine>
</Export>