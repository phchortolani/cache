<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INPROPLAN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INPROPLAN(YAUF,YPOS,YSORT,YTERMIN,YALLE) ;INPROPLAN;FIS;PRODUKTIONSPLANUNG;24.04.2001
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		PRODUKTIONSPLANUNG
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	24.04.2001
	;|
	;\------------------------------------------------------------------/
	;
	NEW AUF,POS,SORT,TERMIN,POS1,TEIL,TEIL1,EBENE,SATZ,ARBZEIT,ARBTAGE,ANZFPL,DAT,YLK,AUFLOCK,POSLOCK
	;S ^TEST($H)=YAUF_"/"_YPOS_"/"_YSORT_"/"_YTERMIN
	SET YOK=0
	SET AUF=$GET(YAUF)                    ;AUFTRAGSNUMMER
	QUIT:AUF="" YOK                       ;KEIN AUFTRAG ;no order 
	QUIT:'$DATA(^INAUF(YM,AUF,1)) YOK     ;AUFTRAG EXISTIERT NICHT ;order Not 
	SET POS=$GET(YPOS)                    ;POSITION (KEINE ANGABE = ALLE) ;statement 
	IF POS=0 SET POS=""
	SET YSORT=$GET(YSORT)
	IF YSORT'=1 IF YSORT'=2 SET YSORT=1   ;KEINE ANGABE=VORWÄRTS ;no 
	IF YSORT=1 SET SORT=1                 ;VORWÄRTSRECHNUNG      
	IF YSORT=2 SET SORT=-1                ;RÜCKWÄRTSRECHNUNG
	SET YTERMIN=$PIECE($GET(YTERMIN),".",1)       ;TERMIN NUR GENAUES DATUM KEINE WOCHE ;TYBD 04.10.2002 ;only Date no week 
	SET TERMIN=YTERMIN                    ;START/ENDE
	IF TERMIN="" SET TERMIN=+$HOROLOG           ;KEINE ANGABE=HEUTE ;no 
	IF SORT=-1 IF TERMIN<$HOROLOG QUIT YOK     ;ENDE-TERMIN IST BEREITS VORBEI ;yet straight past 
	SET YALLE=+$GET(YALLE)                ;ALLE TERMINE NEU PLANEN (AUCH MANUELL VERGEBENE) ;FIS;02.12.03;24746
	KILL ^WWWSOR(YUSER)
	SET ^INPROPLAN(YM,AUF,POS)=""
	SET YLK=0
	SET AUFLOCK=AUF
	SET POSLOCK=POS
	IF POS="" LOCK +^INPROPLAN(YM,AUFLOCK):1 QUIT:'$TEST YOK  ;DATENSATZ IN BEARBEITUNG ;data record within adaptation 
	IF POS'="" LOCK +^INPROPLAN(YM,AUFLOCK,POSLOCK):1 QUIT:'$TEST YOK  ;DATENSATZ IN BEARBEITUNG ;data record within adaptation 
	SET YLK=1 
	;MANDANTENVORGABEN
	SET ANZFPL=1  ;ANZAHL FERTIGUNGSPLÄTZE ;Number 
	SET ARBZEIT=$PIECE($GET(^INVORG(YM,YM,1)),Y,16)  ;ARBEITSZEIT AUS MANDANTENVORGABE ;working hours out of 
	IF ARBZEIT="" SET ARBZEIT=8  ;STUNDEN ;procrastinate 
	SET ARBZEIT=ARBZEIT*3600  ;IN SEKUNDEN ;within 
	SET ARBTAGE=$PIECE($GET(^INVORG(YM,YM,1)),Y,17)  ;ARBEITSTAGE AUS MANDANTENVORGABE ;out of 
	IF ARBTAGE="" SET ARBTAGE=5
	IF $P($G(^INVORG(YM,YM,1)),Y,133)'=1 DO AUSLASTUNG  ;MASCHINEN- UND FERTIGUNGSAUSLASTUNG SPEICHERN (^WWWSOR(YUSER,2... BZW. 3...)  ;FIS;26.03.03;;AUSSER BEI UNBESCHRÄNKTER KAPAZITÄT
	DO EBENE       ;ARTIKELSTRUKTUR IN EINZELNE EBENEN SPEICHERN (^WWWSOR(YUSER,1...)
	IF YSORT=1 DO VORW  ;FERTIGUNGSTERMINE BILDEN UND DATEN SPEICHERN (^INPROPLAN)
	IF YSORT=2 DO RUECK   ;FERTIGUNGSTERMINE BILDEN UND DATEN SPEICHERN (^INPROPLAN)
	;IF YLK=1 
	IF POSLOCK="" LOCK -^INPROPLAN(YM,AUFLOCK)
	IF POSLOCK'="" LOCK -^INPROPLAN(YM,AUFLOCK,POSLOCK)
	KILL ^WWWSOR(YUSER)
	IF YOK=0 IF POSLOCK="" KILL ^INPROPLAN(YM,AUFLOCK)
	IF YOK=0 IF POSLOCK'="" KILL ^INPROPLAN(YM,AUFLOCK,POSLOCK)
	QUIT YOK
	
EBENE ;SPEICHERN DER ARTIKELSTRUKTUR IN EINZELNE EBENEN ;Save the within 
	NEW POS1,TEIL,TEIL1,EBENE
	IF AUF'="" IF $DATA(^INAUF1(YM,AUF)) DO
	. ;AUFTRAGSPOSITION
	. DO:POS'=""  IF POS="" FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. . SET EBENE=0
	. . SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	. . IF +$PIECE(POS1,Y,26)'=0 IF +$PIECE(POS1,Y,26)'=2 IF +$PIECE(POS1,Y,26)'=3 QUIT  ;KEIN FERTIGUNGS-TEIL ;no 
	. . IF +$PIECE(POS1,Y,7)'=0 IF +$PIECE(POS1,Y,7)'=3 IF +$PIECE(POS1,Y,7)'=4 QUIT  ;WARENHERKUNFT IST NICHT FERTIGUNG ;Not 
	. . ;IF $EXTRACT($PIECE(POS1,Y,5))="-" QUIT  ;AUFTRAG IST GUTSCHRIFT
	. . IF $PIECE(POS1,Y,5)<0 QUIT:$PIECE($GET(^INAUF(YM,AUF,1)),Y,2)'=1  ;AUFTRAG IST GUTSCHRIFT  ;FIS;15.01.04;22891;AUSSER DEMONTAGE
	. . SET ^WWWSOR(YUSER,1,AUF,EBENE,POS,"0")=POS1  ;SPEICHERN DATENSATZ ;Save data record 
	. . ;
	. . ;ARTIKEL-TEILE/UNTERTEILE
	. . IF $DATA(^INAUFPT(YM,AUF,POS)) DO
	. . . SET SUCH="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS_")"
	. . . FOR  DO  QUIT:SUCH=""
	. . . . SET SUCH=$QUERY(@SUCH)
	. . . . IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. . . . ;IF $PIECE(SUCH,",",2)'=AUF SET SUCH="" QUIT  ;21.06.04;FAN;25794; 
	. . . . IF $PIECE(SUCH,",",3)=POS DO
	. . . . . SET TEIL=$PIECE(SUCH,",",4,99)
	. . . . . SET TEIL=$TRANSLATE(TEIL,",)","..")
	. . . . . SET EBENE=$LENGTH(TEIL,".")
	. . . . . SET TEIL1=@SUCH
	. . . . . ;
	. . . . . IF YALLE=1 DO  ;ALLE TERMINE NEU PLANEN;FIS;02.12.03;24746
	. . . . . . SET $PIECE(TEIL1,Y,168)=""
	. . . . . . SET $PIECE(TEIL1,Y,194)=1
	. . . . . . ;SET $PIECE(@SUCH,Y,168)=""
	. . . . . . ;SET $PIECE(^INAUFPT(YM,AUF,POS,TEIL,1),Y,168)=""
	. . . . . . QUIT
	. . . . . ;
	. . . . . IF +$PIECE(TEIL1,Y,39)=0 SET $PIECE(TEIL1,Y,39)=1
	. . . . . IF $DATA(@SUCH)=11 IF +$PIECE(TEIL1,Y,26)'=0 IF +$PIECE(TEIL1,Y,26)'=2 IF +$PIECE(TEIL1,Y,26)'=3 QUIT  ;KEIN FERTIGUNGS-TEIL ;no 
	. . . . . IF $DATA(@SUCH)=11 IF +$PIECE(TEIL1,Y,7)'=0 IF +$PIECE(TEIL1,Y,7)'=3 IF +$PIECE(TEIL1,Y,7)'=4 QUIT  ;WARENHERKUNFT IST NICHT FERTIGUNG ;Not 
	. . . . . SET ^WWWSOR(YUSER,1,AUF,EBENE,POS,TEIL)=TEIL1  ;SPEICHERN DATENSATZ ;Save data record 
	. . . . . QUIT
	. . . . QUIT
	. . . QUIT
	. . QUIT
	. QUIT
	QUIT
	
VORW  ;GESPEICHERTE DATEN AUFBEREITEN
	NEW RUEST,RUHE,BEWEG,LOS,DAUER,PROTAGE,PROTERM,PROZEIT,TERMEBENE,MASCH,PLATZ,ALTERN,TERMX
	;STARTVORGABEN
	SET PROZEIT=0  ;PRODUKTIONBEGINN (LFD. SEKUNDEN)
	SET TERMEBENE=TERMIN  ;ANFANGSTERMIN DER EBENE ;the plain 
	;FESTLEGUNG DER STARTTERMINE + FERTIGUNGSDAUER DER TEILE JE EBENE ;the the once plain 
	;(DER LETZTE STARTTERMIN ZZGL. FERTIGUNGSDAUER ERGIBT DEN NEUEN ;last 
	;STARTTERMIN FÜR DIE NÄCHSTE EBENE) ;to who next 
	;================================================================
	SET AUF="" FOR  SET AUF=$ORDER(^WWWSOR(YUSER,1,AUF)) QUIT:AUF=""  DO
	. SET EBENE="" FOR  SET EBENE=$ORDER(^WWWSOR(YUSER,1,AUF,EBENE),-1) QUIT:EBENE=""  DO
	. . ;
	. . SET TERMIN=TERMEBENE  ;PRODUKTIONSTERMIN = ENDTERMIN DER EBENE ZUVOR ;the plain previously 
	. . SET PROZEIT=0
	. . ;
	. . SET POS="" FOR  SET POS=$ORDER(^WWWSOR(YUSER,1,AUF,EBENE,POS)) QUIT:POS=""  DO
	. . . SET TEIL="" FOR  SET TEIL=$ORDER(^WWWSOR(YUSER,1,AUF,EBENE,POS,TEIL)) QUIT:TEIL=""  DO
	. . . . SET TEIL1=$GET(^WWWSOR(YUSER,1,AUF,EBENE,POS,TEIL))
	. . . . QUIT:TEIL1=""
	. . . . ;
	. . . . IF $PIECE(TEIL1,Y,45)<0 SET $PIECE(TEIL1,Y,45)=$PIECE(TEIL1,Y,45)*-1  ;DAUER IMMER POSITIV (DEMONTAGE) ;FIS;22891;15.01.04
	. . . . ;
	. . . . SET SATZ=""  ;DATENSATZ FÜR ^INPROPLAN
	. . . . ;
	. . . . ;KEINE LEISTUNG ;no performance 
	. . . . ;(ES WERDEN NUR DIE ARTIKELINFORMATIONEN GESPEICHERT) ;will only who 
	. . . . IF $PIECE(TEIL1,Y,26)'=2 DO  QUIT
	. . . . . SET $PIECE(SATZ,Y,11)=$PIECE(TEIL1,Y,1)   ;BEZEICHNUNG ;notation 
	. . . . . SET $PIECE(SATZ,Y,14)=$PIECE(TEIL1,Y,4)   ;ARTIKEL ;item 
	. . . . . SET $PIECE(SATZ,Y,20)=$PIECE(TEIL1,Y,26)  ;ARTIKEL-ART
	. . . . . SET $PIECE(SATZ,Y,30)=+$H                 ;AUFBEREITUNGSDATUM
	. . . . . SET ^INPROPLAN(YM,AUF,POS,TEIL,1)=SATZ
	. . . . . DO ^WWWSSORT("INPROPLAN",AUF_","_POS_","_TEIL)
	. . . . . QUIT
	. . . . ;
	. . . . SET PROTERM=TERMIN  ;PRODUKTIONSBEGINN
	. . . . ;
	. . . . ;TERMIN NACH ARBEITSTAG UND FEIERTAG PRÜFEN ;within And holiday sift 
	. . . . ;FOR  QUIT:'$DATA(^TERMIN9(YM,SPRACHE,PROTERM))&($$^WWWDAY(PROTERM)'>ARBTAGE)  DO
	. . . . FOR  QUIT:$$^WWWCALDAY(PROTERM,,1)=0  DO  ;KALENDERVORGABE;FIS;05.11.03;21655
	. . . . . SET PROTERM=PROTERM+1
	. . . . . QUIT
	. . . . ;
	. . . . SET STATUS=0  ;ALLES O.K. ;whatsoever 
	. . . . SET MASCH=$PIECE(TEIL1,Y,57)  ;MASCHINE ;machine 
	. . . . SET PLATZ=$PIECE(TEIL1,Y,61)  ;FERTIGUNGSPLATZ
	. . . . ;
	. . . . ;FIXE PRODUKTIONSTERMINE / PRODUKTIONSSTART UNTER BERÜCKSICHTIGUNG DER AUSLASTUNGEN ;under the 
	. . . . ;==================================================================================
	. . . . SET YQ(1)=0
	. . . . DO
	. . . . . ;BEREITS BEGONNENE FERTIGUNGEN ;yet 
	. . . . . ;-----------------------------
	. . . . . IF $PIECE(TEIL1,Y,165)'="" IF $DATA(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165))) DO  ;FERTIGUNGSAUFTRAG
	. . . . . . SET STATUS=3  ;FERTIGUNG BEREITS GESTARTET ;yet 
	. . . . . . NEW STEMP,STEMPTAG,STEMPZEIT
	. . . . . . SET STEMP=$ORDER(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165),""),1)
	. . . . . . SET STEMPTAG=$PIECE($GET(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165),STEMP,1)),Y,14)  ;DATUM FERTIGUNGSSTART ;Date 
	. . . . . . SET STEMPZEIT=$PIECE($GET(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165),STEMP,1)),Y,15)  ;UHRZEIT FERTIGUNGSSTART
	. . . . . . IF STEMPTAG>PROTERM SET STATUS=1  ;FERTIGUNGSTERMIN ZU SPÄT ;within tardy 
	. . . . . . SET PROTERM=STEMPTAG
	. . . . . . SET PROZEIT=STEMPZEIT
	. . . . . . SET YQ(1)=1
	. . . . . . QUIT
	. . . . . QUIT:YQ(1)=1
	. . . . . ;
	. . . . . ;MANUELLE (FIXE) FERTIGUNGSTERMINE
	. . . . . ;---------------------------------
	. . . . . IF $PIECE(TEIL1,Y,168)'="" IF $PIECE(TEIL1,Y,194)'=1 DO  ;MANUELLER FERTIGUNGSTERMIN
	. . . . . . SET STATUS=2  ;MANUELL EINGETRAGENER TERMIN = FIX ;skillful 
	. . . . . . ;IF $PIECE(TEIL1,Y,168)>PROTERM SET STATUS=1  ;FERTIGUNGSTERMIN ZU SPÄT
	. . . . . . SET PROTERM=$PIECE(TEIL1,Y,168)
	. . . . . . SET PROZEIT=0
	. . . . . . SET YQ(1)=1
	. . . . . . QUIT
	. . . . . QUIT:YQ(1)=1
	. . . . . ;
	. . . . . ;
	. . . . . ;SUCHEN DES FRÜHSTMÖGLICHEN STARTTERMINS / ;seek 
	. . . . . ;MASCHINEN- UND FERTIGUNGSPLATZAUSLASTUNG BRÜCKSICHTIGEN ;And 
	. . . . . ;-------------------------------------------------------
	. . . . . SET TERM1=1
	. . . . . SET TERM2=2
	. . . . . SET YQ(4)=""  ;""=ORIGINAL MASCHINE, nr=ALTERNATIV-MASCHINE
	. . . . . ;
	. . . . . FOR  QUIT:TERM1=TERM2  DO
	. . . . . . SET TERM1=PROTERM
	. . . . . . ;
	. . . . . . ;MASCHINE ;machine 
	. . . . . . IF MASCH'="" DO TERM1(MASCH)
	. . . . . . ;
	. . . . . . ;ALTERNATIV-MASCHINE
	. . . . . . IF TERM1'=PROTERM IF MASCH'="" DO TERM3(MASCH,PROTERM)
	. . . . . . ;
	. . . . . . ;FERTIGUNGSPLATZ
	. . . . . . SET TERM2=TERM1
	. . . . . . IF PLATZ'="" DO TERM2(PLATZ)
	. . . . . . ;
	. . . . . . SET PROTERM=TERM2
	. . . . . . QUIT
	. . . . . ;
	. . . . . IF YQ(4)'="" SET MASCH=YQ(4)  ;PRODUKTION MIT ALTERNATIV-MASCHINE  ;FIS;24353;05.11.03
	. . . . . ;
	. . . . . QUIT
	. . . . ;
	. . . . ;FERTIGUNGSDAUER ERMITTELN ;find 
	. . . . ;=============================================================================
	. . . . SET LOS=$PIECE(TEIL1,Y,72)  ;LOSGRÖSSE
	. . . . IF +LOS<1 SET LOS=999999999
	. . . . ;
	. . . . ;RÜSTZEIT UND MASCHINENRUHEZEIT ;setup time And 
	. . . . ;------------------------------
	. . . . SET RUEST=0  ;MASCHINENRÜSTZEIT
	. . . . SET RUHE=0   ;MASCHINENRUHEZEIT
	. . . . IF MASCH'="" DO
	. . . . . SET RUEST=+$PIECE(TEIL1,Y,70)*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL)-1\LOS))  ;RÜSTZEIT JE LOSGRÖSSE ;setup time once 
	. . . . . SET RUHE=+$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,29)
	. . . . . IF RUHE'=0 SET RUHE=RUHE*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL)-1\LOS)) ;RUHEZEIT JE LOSGRÖSSE ;once 
	. . . . . QUIT
	. . . . ;
	. . . . ;WARENBEWEGUNGSZEIT
	. . . . ;------------------
	. . . . SET BEWEG=0  ;WARENBEWEGUNGSZEIT
	. . . . IF PLATZ'="" DO
	. . . . . SET BEWEG=+$PIECE($GET(^INPRONEST(YM,PLATZ,1)),Y,4)
	. . . . . IF BEWEG'=0 SET BEWEG=BEWEG*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL-1)\LOS))  ;BEWEGUNGSZEIT JE LOSGRÖSSE ;once 
	. . . . . QUIT
	. . . . ;
	. . . . ;FERTIGUNGSDAUER
	. . . . ;---------------
	. . . . SET DAUER=($PIECE(TEIL1,Y,45)*$PIECE(TEIL1,Y,39))+RUEST+RUHE+BEWEG    ;AUSFÜHRUNGSZEIT*PROD.MENGE + ...
	. . . . SET PROTAGE=$$^INDAUERTAGE(PROZEIT+DAUER)  ;PRODUKTIONSZEIT IN ARBEITSTAGEN ;within 
	. . . . ;IF PROZEIT'<ARBZEIT SET PROZEIT=0  SET PROTAGE=PROTAGE+1  ;STARTZEIT GRÖßER ARBEITSZEIT ;table-mat table-mat working hours 
	. . . . IF PROTERM+PROTAGE>TERMEBENE SET TERMEBENE=PROTERM+PROTAGE  ;STARTTERMIN FÜR NÄCHSTE EBENE SETZEN ;to next plain typeset 
	. . . . ;
	. . . . ;DATENSATZ SPEICHERN ;data record Save 
	. . . . ;=============================================================================
	. . . . SET $PIECE(SATZ,Y,1)=PROTERM   ;FERTIGUNGSTERMIN
	. . . . SET $PIECE(SATZ,Y,3)=DAUER     ;FERTIGUNGSDAUER
	. . . . SET $PIECE(SATZ,Y,4)=PROTAGE   ;PRODUKTIONSZEIT IN TAGEN ;within 
	. . . . SET $PIECE(SATZ,Y,5)=MASCH     ;MASCHINE ;machine 
	. . . . SET $PIECE(SATZ,Y,6)=RUEST     ;MASCHINENRÜSTZEIT
	. . . . SET $PIECE(SATZ,Y,7)=RUHE      ;MASCHINENRUHEZEIT
	. . . . SET $PIECE(SATZ,Y,8)=PLATZ     ;FERTIGUNGSPLATZ
	. . . . SET $PIECE(SATZ,Y,9)=BEWEG     ;WARENBEWEGUNGSZEIT
	. . . . SET $PIECE(SATZ,Y,10)=STATUS   ;TERMINSTATUS
	. . . . SET $PIECE(SATZ,Y,11)=$PIECE(TEIL1,Y,1)   ;BEZEICHNUNG ;notation 
	. . . . SET $PIECE(SATZ,Y,14)=$PIECE(TEIL1,Y,4)   ;ARTIKEL ;item 
	. . . . IF LOS=999999999 SET LOS=""
	. . . . SET $PIECE(SATZ,Y,15)=LOS                 ;LOSGRÖSSE
	. . . . SET $PIECE(SATZ,Y,20)=$PIECE(TEIL1,Y,26)  ;ARTIKEL-ART
	. . . . SET $PIECE(SATZ,Y,30)=+$H      ;AUFBEREITUNGSDATUM
	. . . . SET $PIECE(SATZ,Y,32)=$PIECE(TEIL1,Y,165)  ;FERTIGUNGSNUMMER (WENN VORHANDEN)
	. . . . SET $PIECE(SATZ,Y,33)=$PIECE(TEIL1,Y,194)  ;TERMIN AUTOM. GEPLANT;FIS;02.12.03;24746
	. . . . SET ^INPROPLAN(YM,AUF,POS,TEIL,1)=SATZ
	. . . . DO ^WWWSSORT("INPROPLAN",AUF_","_POS_","_TEIL)
	. . . . ;
	. . . . ;BELEGUNGSZEITEN SPEICHERN FÜR WEITERE TEILE ;Save to 
	. . . . ;--------------------------------------------------------------------------------
	. . . . DO SPEI(PROTERM,DAUER)
	. . . . ;
	. . . . QUIT
	. . . QUIT
	. . QUIT
	. SET YOK=1
	. QUIT
	QUIT
	
RUECK  ;GESPEICHERTE DATEN AUFBEREITEN
    ;-------------------------------------------------------------------------------
    ; History:
    ; 02-Sep-2005	JW		SR12966: TERMIN9 is shared 
    ;-------------------------------------------------------------------------------
	NEW RUEST,RUHE,BEWEG,LOS,DAUER,PROTAGE,PROSTART,PROSTOP,PROZEIT,STOPEBENE,ALTERN,MASCH,PLATZ,TERMX
	;STARTVORGABEN
	SET PROZEIT=0  ;PRODUKTIONBEGINN (LFD. SEKUNDEN)
	SET STOPEBENE=TERMIN  ;STOPTERMIN DER EBENE ;the plain 
	;FESTLEGUNG DER STOPTERMINE - FERTIGUNGSDAUER DER TEILE JE EBENE ;the the once plain 
	;(DER LETZTE STOPTERMIN ABZGL. FERTIGUNGSDAUER ERGIBT DEN NEUEN ;last 
	;STOPTERMIN FÜR DIE NÄCHSTE EBENE) ;to who next 
	;================================================================
	SET AUF="" FOR  SET AUF=$ORDER(^WWWSOR(YUSER,1,AUF)) QUIT:AUF=""  DO
	. SET EBENE="" FOR  SET EBENE=$ORDER(^WWWSOR(YUSER,1,AUF,EBENE),1) QUIT:EBENE=""  DO
	. . ;
	. . SET TERMIN=STOPEBENE  ;PRODUKTIONSENDE = STARTTERMIN DER EBENE ZUVOR ;the plain previously 
	. . SET PROZEIT=0
	. . ;
	. . SET POS="" FOR  SET POS=$ORDER(^WWWSOR(YUSER,1,AUF,EBENE,POS)) QUIT:POS=""  DO
	. . . SET TEIL="" FOR  SET TEIL=$ORDER(^WWWSOR(YUSER,1,AUF,EBENE,POS,TEIL)) QUIT:TEIL=""  DO
	. . . . SET TEIL1=$GET(^WWWSOR(YUSER,1,AUF,EBENE,POS,TEIL))
	. . . . ;
	. . . . IF $PIECE(TEIL1,Y,45)<0 SET $PIECE(TEIL1,Y,45)=$PIECE(TEIL1,Y,45)*-1  ;DAUER IMMER POSITIV (DEMONTAGE) ;FIS;22891;15.01.04
	. . . . ;
	. . . . SET SATZ=""  ;DATENSATZ FÜR ^INPROPLAN
	. . . . ;
	. . . . ;KEINE LEISTUNG ;no performance 
	. . . . ;(ES WERDEN NUR DIE ARTIKELINFORMATIONEN GESPEICHERT) ;will only who 
	. . . . IF $PIECE(TEIL1,Y,26)'=2 DO  QUIT
	. . . . . SET $PIECE(SATZ,Y,11)=$PIECE(TEIL1,Y,1)   ;BEZEICHNUNG ;notation 
	. . . . . SET $PIECE(SATZ,Y,14)=$PIECE(TEIL1,Y,4)   ;ARTIKEL ;item 
	. . . . . SET $PIECE(SATZ,Y,20)=$PIECE(TEIL1,Y,26)  ;ARTIKEL-ART
	. . . . . SET $PIECE(SATZ,Y,30)=+$H                 ;AUFBEREITUNGSDATUM
	. . . . . SET ^INPROPLAN(YM,AUF,POS,TEIL,1)=SATZ
	. . . . . DO ^WWWSSORT("INPROPLAN",AUF_","_POS_","_TEIL)
	. . . . . QUIT
	. . . . ;
	. . . . SET PROSTOP=TERMIN  ;PRODUKTIONSENDE
	. . . . ;
	. . . . SET STATUS=0  ;ALLES O.K. ;whatsoever 
	. . . . SET MASCH=$PIECE(TEIL1,Y,57)  ;MASCHINE ;machine 
	. . . . SET PLATZ=$PIECE(TEIL1,Y,61)  ;FERTIGUNGSPLATZ
	. . . . ;
	. . . . ;FERTIGUNGSDAUER ERMITTELN ;find 
	. . . . ;=============================================================================
	. . . . SET LOS=$PIECE(TEIL1,Y,72)  ;LOSGRÖSSE
	. . . . IF +LOS<1 SET LOS=999999999
	. . . . ;
	. . . . ;RÜSTZEIT UND MASCHINENRUHEZEIT ;setup time And 
	. . . . ;------------------------------
	. . . . SET RUEST=0  ;MASCHINENRÜSTZEIT
	. . . . SET RUHE=0   ;MASCHINENRUHEZEIT
	. . . . IF MASCH'="" DO
	. . . . . SET RUEST=+$PIECE(TEIL1,Y,70)*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL)-1\LOS))  ;RÜSTZEIT JE LOSGRÖSSE ;setup time once 
	. . . . . SET RUHE=+$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,29)
	. . . . . IF RUHE'=0 SET RUHE=RUHE*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL)-1\LOS)) ;RUHEZEIT JE LOSGRÖSSE ;once 
	. . . . . QUIT
	. . . . ;
	. . . . ;WARENBEWEGUNGSZEIT
	. . . . ;------------------
	. . . . SET BEWEG=0  ;WARENBEWEGUNGSZEIT
	. . . . IF PLATZ'="" DO
	. . . . . SET BEWEG=+$PIECE($GET(^INPRONEST(YM,PLATZ,1)),Y,4)
	. . . . . IF BEWEG'=0 SET BEWEG=BEWEG*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL-1)\LOS))  ;BEWEGUNGSZEIT JE LOSGRÖSSE ;once 
	. . . . . QUIT
	. . . . ;
	. . . . ;FERTIGUNGSDAUER
	. . . . ;---------------
	. . . . SET DAUER=($PIECE(TEIL1,Y,45)*$PIECE(TEIL1,Y,39))+RUEST+RUHE+BEWEG  ;AUSFÜHRUNGSZEIT*PROD.MENGE + ...
	. . . . SET PROTAGE=$$^INDAUERTAGE(DAUER)  ;PRODUKTIONSZEIT IN ARBEITSTAGEN ;within 
	. . . . SET PROSTART=PROSTOP-PROTAGE
	. . . . IF +PROSTART=0 SET PROSTART=+$H
	. . . . ;
	. . . . ;FIXE PRODUKTIONSTERMINE / PRODUKTIONSSTART UNTER BERÜCKSICHTIGUNG DER AUSLASTUNGEN ;under the 
	. . . . ;==================================================================================
	. . . . SET YQ(1)=0
	. . . . DO
	. . . . . ;BEREITS BEGONNENE FERTIGUNGEN ;yet 
	. . . . . ;-----------------------------
	. . . . . IF $PIECE(TEIL1,Y,165)'="" IF $DATA(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165))) DO  ;FERTIGUNGSAUFTRAG
	. . . . . . SET STATUS=3  ;FERTIGUNG BEREITS GESTARTET ;yet 
	. . . . . . NEW STEMP,STEMPTAG,STEMPZEIT
	. . . . . . SET STEMP=$ORDER(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165),""),1)
	. . . . . . SET STEMPTAG=$PIECE($GET(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165),STEMP,1)),Y,14)  ;DATUM FERTIGUNGSSTART ;Date 
	. . . . . . SET STEMPZEIT=$PIECE($GET(^INPROSTEMP0(YM,$PIECE(TEIL1,Y,165),STEMP,1)),Y,15)  ;UHRZEIT FERTIGUNGSSTART
	. . . . . . IF STEMPTAG>PROSTART SET STATUS=1  ;FERTIGUNGSTERMIN ZU SPÄT ;within tardy 
	. . . . . . IF +STEMPTAG'=0 SET PROSTART=STEMPTAG
	. . . . . . SET PROZEIT=STEMPZEIT
	. . . . . . SET YQ(1)=1
	. . . . . . QUIT
	. . . . . QUIT:YQ(1)=1
	. . . . . ;
	. . . . . ;MANUELLE (FIXE) FERTIGUNGSTERMINE
	. . . . . ;---------------------------------
	. . . . . IF $PIECE(TEIL1,Y,168)'="" IF $PIECE(TEIL1,Y,194)'=1 DO  ;MANUELLER FERTIGUNGSTERMIN
	. . . . . . SET STATUS=2  ;MANUELL EINGETRAGENER TERMIN = FIX ;skillful 
	. . . . . . IF $PIECE(TEIL1,Y,168)>PROSTART SET STATUS=1  ;FERTIGUNGSTERMIN ZU SPÄT ;within tardy 
	. . . . . . IF +$PIECE(TEIL1,Y,168)'=0 SET PROSTART=$PIECE(TEIL1,Y,168)
	. . . . . . SET PROZEIT=0
	. . . . . . SET YQ(1)=1
	. . . . . ;
	. . . . . QUIT:YQ(1)=1
	. . . . . ;
	. . . . . ;SUCHEN DES FRÜHSTMÖGLICHEN STARTTERMINS / ;seek 
	. . . . . ;MASCHINEN- UND FERTIGUNGSPLATZAUSLASTUNG BRÜCKSICHTIGEN ;And 
	. . . . . ;-------------------------------------------------------
	. . . . . SET TERM1=1
	. . . . . SET TERM2=2
	. . . . . SET YQ(4)=""  ;""=MASCHINE, nr=ALTERNATIV-MASCHINE
	. . . . . ;
	. . . . . IF +PROSTART'=0 FOR  QUIT:TERM1=TERM2  DO
	. . . . . . SET TERM1=PROSTART
	. . . . . . ;
	. . . . . . ;MASCHINE ;machine 
	. . . . . . IF MASCH'="" DO TERM1(MASCH)
	. . . . . . ;
	. . . . . . ;ALTERNATIV-MASCHINE
	. . . . . . IF TERM1'=PROSTART IF MASCH'="" DO TERM3(MASCH,PROSTART)
	. . . . . . ;
	. . . . . . ;FERTIGUNGSPLATZ
	. . . . . . SET TERM2=TERM1
	. . . . . . IF PLATZ'="" DO TERM2(PLATZ)
	. . . . . . ;
	. . . . . . SET PROSTART=TERM2
	. . . . . ;
	. . . . . ;PRODUKTION MIT ALTERNATIV-MASCHINE ;production by means of 
	. . . . . ;NEUBERECHNUNG FERTIGUNGSDAUER/FERTIGUNGSSTART
	. . . . . IF YQ(4)'="" DO
	. . . . . . SET MASCH=YQ(4)  ;FIS;24353;05.11.03
	. . . . . . SET RUEST=+$PIECE(TEIL1,Y,70)*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL)-1\LOS))  ;RÜSTZEIT JE LOSGRÖSSE ;setup time once 
	. . . . . . SET RUHE=+$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,29)
	. . . . . . IF RUHE'=0 SET RUHE=RUHE*ANZFPL*(1+(($PIECE(TEIL1,Y,39)/ANZFPL)-1\LOS)) ;RUHEZEIT JE LOSGRÖSSE ;once 
	. . . . . . SET DAUER=($PIECE(TEIL1,Y,45)*$PIECE(TEIL1,Y,39))+RUEST+RUHE+BEWEG  ;AUSFÜHRUNGSZEIT ;execution time 
	. . . . . . SET PROTAGE=$$^INDAUERTAGE(DAUER)  ;PRODUKTIONSZEIT IN ARBEITSTAGEN ;within 
	. . . . . . SET PROSTART=PROSTOP-PROTAGE
	. . . . . . IF +PROSTART=0 SET PROSTART=+$HOROLOG
	. . . . ;
	. . . . ;TERMIN NACH ARBEITSTAG UND FEIERTAG PRÜFEN ;within And holiday sift 
	. . . . ;IF +PROSTART'=0 FOR  QUIT:'$DATA(^TERMIN9(YM,SPRACHE,PROSTART))&($$^WWWDAY(PROSTART)'>ARBTAGE)  DO
	. . . . IF +PROSTART'=0 FOR  QUIT:$$^WWWCALDAY(PROSTART,,1)=0  DO  ;KALENDERVORGABE;FIS;05.11.03;21655
	. . . . . SET PROSTART=PROSTART-1  ;MINUS 1 TAG ;minus TAG 
	. . . . 
	. . . . IF PROSTART<$HOROLOG SET STATUS=1  ;TERMIN BEREITS ÜBERSCHRITTEN ;yet 
	. . . . ;
	. . . . ;DATENSATZ SPEICHERN ;data record Save 
	. . . . ;=============================================================================
	. . . . SET $PIECE(SATZ,Y,1)=PROSTART  ;FERTIGUNGSTERMIN
	. . . . SET $PIECE(SATZ,Y,3)=DAUER     ;FERTIGUNGSDAUER
	. . . . SET $PIECE(SATZ,Y,4)=PROTAGE   ;PRODUKTIONSZEIT IN TAGEN ;within 
	. . . . SET $PIECE(SATZ,Y,5)=MASCH     ;MASCHINE ;machine 
	. . . . SET $PIECE(SATZ,Y,6)=RUEST     ;MASCHINENRÜSTZEIT
	. . . . SET $PIECE(SATZ,Y,7)=RUHE      ;MASCHINENRUHEZEIT
	. . . . SET $PIECE(SATZ,Y,8)=PLATZ     ;FERTIGUNGSPLATZ
	. . . . SET $PIECE(SATZ,Y,9)=BEWEG     ;WARENBEWEGUNGSZEIT
	. . . . SET $PIECE(SATZ,Y,10)=STATUS   ;TERMINSTATUS
	. . . . SET $PIECE(SATZ,Y,11)=$PIECE(TEIL1,Y,1)   ;BEZEICHNUNG ;notation 
	. . . . SET $PIECE(SATZ,Y,14)=$PIECE(TEIL1,Y,4)   ;ARTIKEL ;item 
	. . . . IF LOS=999999999 SET LOS=""
	. . . . SET $PIECE(SATZ,Y,15)=LOS                 ;LOSGRÖSSE
	. . . . SET $PIECE(SATZ,Y,20)=$PIECE(TEIL1,Y,26)  ;ARTIKEL-ART
	. . . . SET $PIECE(SATZ,Y,30)=+$H      ;AUFBEREITUNGSDATUM
	. . . . SET $PIECE(SATZ,Y,32)=$PIECE(TEIL1,Y,165)  ;FERTIGUNGSNUMMER (WENN VORHANDEN)
	. . . . SET $PIECE(SATZ,Y,33)=$PIECE(TEIL1,Y,194)  ;TERMIN AUTOM. GEPLANT;FIS;02.12.03;24746
	. . . . SET ^INPROPLAN(YM,AUF,POS,TEIL,1)=SATZ
	. . . . DO ^WWWSSORT("INPROPLAN",AUF_","_POS_","_TEIL)
	. . . . ;
	. . . . ;
	. . . . ;STOPTERMIN FÜR NÄCHSTE EBENE FESTLEGEN ;to next plain 
	. . . . ;--------------------------------------------------------------------------------
	. . . . IF PROZEIT>ARBZEIT SET PROSTART=PROSTART-1
	. . . . IF +PROSTART'=0 FOR  QUIT:'$DATA(^TERMIN9(0,SPRACHE,PROSTART))&($$^WWWDAY(PROSTART)'>ARBTAGE)  DO
	. . . . . SET PROSTART=PROSTART-1  ;MINUS 1 TAG ;minus TAG 
	. . . . . QUIT
	. . . . IF PROSTART<STOPEBENE SET STOPEBENE=PROSTART  ;STARTTERMIN FÜR NÄCHSTE EBENE SETZEN ;to next plain typeset 
	. . . . ;
	. . . . ;BELEGUNGSZEITEN SPEICHERN FÜR WEITERE TEILE ;Save to 
	. . . . ;--------------------------------------------------------------------------------
	. . . . DO SPEI(PROSTART,DAUER)
	. ;
	. SET YOK=1
	
	QUIT
	;***********************************************************************************
	
TERM1(MASCHX) ;SUCHEN FRÜHSTMÖGLICHER TERMIN FÜR MASCHINE ;seek to machine 
	NEW XDAUER,YZ
	SET YQ(2)=0
	;SET YQ(4)=0  ;FIS;24353;05.11.03
	FOR  QUIT:YQ(2)=1  DO
	. ;KEINE MASCHINENBELEGUNG ;no 
	. IF $GET(MASCHX)="" SET YQ(2)=1 QUIT
	. IF '$DATA(^WWWSOR(YUSER,2,MASCHX,TERM1)) SET YQ(2)=1 QUIT
	. ;
	. ;BELEGUNGSDAUER GRÖSSER ARBEITSZEIT ;working hours 
	. SET XDAUER=$GET(^WWWSOR(YUSER,2,MASCHX,TERM1))
	. IF XDAUER'<ARBZEIT DO  QUIT
	. . SET YZ=0
	. . FOR  QUIT:YZ=1  DO
	. . . SET TERM1=TERM1+(1*SORT)  ;1 TAG SPÄTER/FRÜHER
	. . . ;QUIT:$DATA(^TERMIN9(YM,SPRACHE,TERM1))
	. . . ;QUIT:$$^WWWDAY(TERM1)>ARBTAGE
	. . . QUIT:$$^WWWCALDAY(TERM1,,1)'=0   ;KEIN ARBEITSTAG LT. KALENDERVORGABE;FIS;05.11.03;21655
	. . . SET YZ=1
	. . ;
	. . SET PROZEIT=0
	. . DO TERM1(MASCHX)
	. ;
	. ;BELEGUNGSDAUER KLEINER ARBEITSZEIT ;lesser working hours 
	. IF XDAUER<ARBZEIT DO
	. . SET PROZEIT=PROZEIT+XDAUER
	. . SET YQ(2)=1
	
	QUIT
	
TERM2(PLATZX) ;SUCHEN FRÜHSTMÖGLICHER TERMIN FÜR FERTIGUNGSPLATZ ;seek to 
	NEW XDAUER,YZ
	
	SET YQ(3)=0
	FOR  QUIT:YQ(3)=1  DO
	. ;
	. ;KEINE FERTIGUNGSPLATZ-BELEGUNG ;no 
	. IF $GET(PLATZX)="" SET YQ(3)=1 QUIT
	. IF '$DATA(^WWWSOR(YUSER,3,PLATZX,TERM2)) SET YQ(3)=1 QUIT
	. ;
	. ;DAUER BELEGUNG FERTIGUNGSPLATZ ;permanence 
	. SET XDAUER=$GET(^WWWSOR(YUSER,3,PLATZX,TERM2))
	. ;
	. DO  ;MEHRFACHBELEGUNG BEI MEHREREN MITARBEITERN ;FIS, 27.09.02
	. . NEW MITARB
	. . SET MITARB=$LENGTH($PIECE($GET(^INPRONEST(YM,PLATZX,1)),Y,2),";")-1  ;ANZAHL MITARBEITER AUF FERTIGUNGSPLATZ
	. . IF +MITARB>1 SET XDAUER=XDAUER/MITARB
	. ;
	. ;BELEGUNGSDAUER GRÖSSER ARBEITSZEIT ;working hours 
	. IF XDAUER'<ARBZEIT DO  QUIT
	. . SET YZ=0
	. . FOR  QUIT:YZ=1  DO
	. . . SET TERM2=TERM2+(1*SORT)  ;1 TAG SPÄTER/FRÜHER
	. . . ;QUIT:$DATA(^TERMIN9(YM,SPRACHE,TERM2))
	. . . ;QUIT:$$^WWWDAY(TERM2)>ARBTAGE
	. . . QUIT:$$^WWWCALDAY(TERM1,,1)'=0  ;KEIN ARBEITSTAG LT. KALENDERVORGABE;FIS;05.11.03;21655
	. . . SET YZ=1
	. . ;
	. . SET PROZEIT=0
	. . DO TERM2(PLATZX)
	. ;
	. ;BELEGUNGSDAUER KLEINER ARBEITSZEIT ;lesser working hours 
	. IF XDAUER<ARBZEIT DO
	. . SET PROZEIT=PROZEIT+XDAUER
	. . SET YQ(3)=1
	
	QUIT
	
TERM3(MASCHX,TERMX) ;SUCHEN FRÜHSTMÖGLICHER TERMIN FÜR ALTERNATIV-MASCHINE WENN MASCHINE BELEGT ;seek to when machine furry 
	NEW YA
	
	QUIT:$GET(MASCHX)=""
	IF $PIECE($GET(^INMASCH(YM,MASCHX,1)),Y,27)'="" DO
	. SET ALTERN=$TRANSLATE($PIECE($GET(^INMASCH(YM,MASCHX,1)),Y,27),",",";")
	. FOR YA=1:1 QUIT:$PIECE(ALTERN,";",YA,999)=""  DO  ;MEHRERE ALTERNATIVMASCHINEN;FIS;24353;05.11.03
	. . QUIT:$PIECE(ALTERN,";",YA)=""
	. . SET XTERM1=TERM1                           ;SPEICHERN TERMIN WENN ORIGINAL MASCHINE ;Save when machine 
	. . SET TERM1=TERMX                            ;RÜCKHOLEN TERMIN VOR BERECHNUNG MIT ORIGINAL MASCHINE ;pre- by means of machine 
	. . DO TERM1($PIECE(ALTERN,";",YA))
	. . IF SORT=1 IF TERM1'<XTERM1 SET TERM1=XTERM1 QUIT   ;KEIN FRÜHERER TERMIN MIT ALTERNATIVMASCHINE ;no by means of 
	. . IF SORT=-1 IF TERM1'>XTERM1 SET TERM1=XTERM1 QUIT  ;KEIN SPÄTERER TERMIN MIT ALTERNATIVMASCHINE ;no by means of 
	. . SET YQ(4)=$PIECE(ALTERN,";",YA)  ;ALTERNATIVMASCHINE NUTZEN
	
	QUIT
	
SPEI(DAT,DAUER)
	;SPEICHERN DER AUSLASTUNGSZEITEN PRO TAG ;Save the within TAG 
	SET DAT=$GET(DAT)
	QUIT:DAT=""
	SET DAUER=$GET(DAUER)
	QUIT:DAUER=""
	;=================================================
	;MASCHINENAUSLASTUNG
	;-------------------------------------------------
	IF MASCH'="" DO
	. NEW DAUER0
	. SET DAUER0=DAUER
	. ;
	. ;MASCHINE BEREITS IM EINSATZ ;machine yet 
	. IF $DATA(^WWWSOR(YUSER,2,MASCH,DAT)) DO
	. . NEW DAUERALT
	. . SET DAUERALT=$GET(^WWWSOR(YUSER,2,MASCH,DAT))
	. . SET DAUER0=DAUER0+DAUERALT
	. ;
	. ;SPEICHERN MASCHINENAUSLASTUNG PRO TAG ;Save within TAG 
	. SET ^WWWSOR(YUSER,2,MASCH,DAT)=DAUER0  ;BELEGUNGSZEIT MASCHINE ;machine 
	
	;FERTIGUNGSPLATZ-AUSLASTUNG
	;-------------------------------------------------
	IF PLATZ'="" DO
	. NEW DAUER0
	. SET DAUER0=DAUER
	. ;
	. ;FERTIGUNGSPLATZ BEREITS BELEGT ;yet furry 
	. IF $DATA(^WWWSOR(YUSER,3,PLATZ,DAT)) DO
	. . NEW DAUERALT
	. . SET DAUERALT=$GET(^WWWSOR(YUSER,3,PLATZ,DAT))
	. . SET DAUER0=DAUER0+DAUERALT
	. ;
	. ;SPEICHERN FERTIGUNGSPLATZ-AUSLASTUNG PRO TAG ;Save within TAG 
	. SET ^WWWSOR(YUSER,3,PLATZ,DAT)=DAUER0 ;BELEGUNGSZEIT FERTIGUNGSPLATZ
	
	QUIT
	
AUSLASTUNG ;MASCHINEN- UND FERTIGUNGSPLATZ-AUSLASTUNG ERMITTELN ;And find 
	NEW POSX,POS1X,AUFX,AUF1X,TEILX,SUCH
	;AUFTRAG  ;order 
	SET AUFX=""
	FOR  SET AUFX=$ORDER(^INAUF1(YM,AUFX),-1) QUIT:AUFX=""  DO  ;OFFENE AUFTRÄGE
	. SET AUF1X=$GET(^INAUF(YM,AUFX,1))
	. QUIT:$PIECE(AUF1X,Y,2)=2  ;LIEFERANTENBESTELLUNG
	. ;
	. ;AUFTRAGSPOSITION
	. SET POSX=""
	. FOR  SET POSX=$ORDER(^INAUFP(YM,AUFX,POSX)) QUIT:POSX=""  DO
	. . SET POS1X=$GET(^INAUFP(YM,AUFX,POSX,1))  ;AUFTRAGSPOSITION
	. . ;
	. . IF +$PIECE(POS1X,Y,7)'=4 IF +$PIECE(POS1X,Y,7)'=3 QUIT  ;BESTELL ODER LAGERWARE ;Or 
	. . IF '$DATA(^INAUFPXL(YM,AUFX,POSX)) QUIT  ;LEISTUNGEN OHNE TEILE = KEINE PRODUKTION ;LEISTUNGEN without no production 
	. . IF +$PIECE(POS1X,Y,60)=1           QUIT  ;ABGESCHLOSSENE POSITION
	. . IF +$PIECE(POS1X,Y,90)=1           QUIT  ;AUSLIEFERUNGSFÄHIG
	. . IF +$PIECE(POS1X,Y,164)'=0         QUIT  ;FERTIGGESTELLT
	. . IF +$PIECE(POS1X,Y,9)=1            QUIT  ;STORNO POSITION
	. . IF $PIECE(POS1X,Y,45)<0 SET $PIECE(POS1X,Y,45)=$PIECE(POS1X,Y,45)*-1  ;DAUER IMMER POSITIV (DEMONTAGE) ;FIS;22891;15.01.04
	. . DO DAUER
	. . ;
	. . ;UNTERTEILE
	. . SET POS1X=""
	. . SET SUCH="^INAUFPXL("_""""_YM_""""_","_""""_AUFX_""""_","_POSX_")"
	. . FOR  DO  QUIT:SUCH=""
	. . . SET SUCH=$QUERY(@SUCH)
	. . . IF $TRANSLATE($PIECE(SUCH,",",2),"""")'=AUFX SET SUCH="" QUIT  ;21.06.04;FAN;25794; WENN alphanumerischen Auftragsnummern SOLL DATEN AUCH AUFBAUEN.
	. . . ;IF $PIECE(SUCH,",",2)'=AUFX SET SUCH="" QUIT  ;21.06.04;FAN;25794; 
	. . . IF $PIECE(SUCH,",",3)=POSX DO
	. . . . SET TEILX=$PIECE(SUCH,",",4,99)
	. . . . SET TEILX=$TRANSLATE(TEILX,",)","..")
	. . . . SET POS1X=@SUCH
	. . . . ;
	. . . . IF $DATA(@SUCH)=11 IF +$PIECE(POS1X,Y,7)'=3 QUIT  ;KEINE EIGENFERTIGUNG ;no 
	. . . . IF +$PIECE(POS1X,Y,90)=1 QUIT  ;AUSLIEFERUNGSFÄHIG
	. . . . IF +$PIECE(POS1X,Y,164)'=0 QUIT  ;FERTIGGESTELLT
	. . . . IF $PIECE(POS1X,Y,45)<0 SET $PIECE(POS1X,Y,45)=$PIECE(POS1X,Y,45)*-1  ;DAUER IMMER POSITIV (DEMONTAGE) ;FIS;22891;15.01.04
	. . . . DO DAUER
	
	QUIT
	
DAUER ;
	NEW DAUER,MASCH,PLATZ,TAG,LOS,ARBZEIT
	
	SET TAG=$PIECE(POS1X,Y,168)  ;FERTIGUNGSBEGINN
	QUIT:TAG=""  ;KEIN TERMIN ;no 
	;PRÜFUNG OB FERTIGUNG BEREITS BEGONNEN HAT ;quiz whether yet 
	IF $PIECE(POS1X,Y,165)'="" IF $DATA(^INPROSTEMP0(YM,$PIECE(POS1X,Y,165))) DO  ;FERTIGUNGSAUFTRAG
	. NEW STEMP
	. SET STEMP=$ORDER(^INPROSTEMP0(YM,$PIECE(POS1X,Y,165),""),1)
	. SET TAG=$PIECE($GET(^INPROSTEMP0(YM,$PIECE(POS1X,Y,165),STEMP,1)),Y,14)  ;DATUM FERTIGUNGSSTART ;Date 
	
	;FESTLEGUNG DER FERTIGUNGSZEIT ;the 
	SET DAUER=$PIECE(POS1X,Y,45)  ;AUSFÜHRUNGSZEIT ;execution time 
	IF $PIECE(POS1X,Y,166)'="" SET DAUER=$PIECE(POS1X,Y,166)  ;DAUER DER FERTIGUNG ;permanence the 
	;LOSGRÖSSE
	SET LOS=$PIECE(POS1X,Y,72)
	IF +LOS<1 SET LOS=999999999
	;ERMITTELN DER MASCHINENAUSLASTUNG ;find the 
	;-----------------------------------------------------------------------------------
	SET MASCH=$PIECE(POS1X,Y,57)
	IF MASCH'="" DO
	. NEW RUEST,RUHE,DAUERALT
	. SET RUEST=+$PIECE(POS1X,Y,70)*ANZFPL*(1+(($PIECE(POS1X,Y,39)/ANZFPL)-1\LOS))  ;RÜSTZEIT JE LOSGRÖSSE ;setup time once 
	. SET RUHE=+$PIECE($GET(^INMASCH(YM,MASCH,1)),Y,29)  ;RUHEZEIT AUS VORGABE MASCHINEN-STAMMDATEN ;out of default 
	. IF RUHE'=0 SET RUHE=RUHE*ANZFPL*(1+(($PIECE(POS1X,Y,39)/ANZFPL)-1\LOS)) ;RUHEZEIT JE LOSGRÖSSE ;once 
	. SET DAUER=DAUER+RUEST+RUHE
	
	;ERMITTELN DER FERTIGUNGSPLATZ-AUSLASTUNG ;find the 
	;-----------------------------------------------------------------------------------
	SET PLATZ=$PIECE(POS1X,Y,61)  ;FERTIGUNGSPLATZ
	IF PLATZ'="" DO
	. NEW BEWEG,DAUERALT
	. SET BEWEG=+$PIECE($GET(^INPRONEST(YM,PLATZ,1)),Y,4)      ;WARENBEWEGUNGSZEIT AUS VORGABE FERTIGUNGSPLATZ ;out of default 
	. IF BEWEG'=0 SET BEWEG=BEWEG*ANZFPL*(1+(($PIECE(POS1X,Y,39)/ANZFPL-1)\LOS))  ;BEWEGUNGSZEIT JE LOSGRÖSSE ;once 
	. SET DAUER=DAUER+BEWEG
	
	;SPEICHERN DER AUSLASTUNGSZEITEN JE MASCHINE / TAG ;Save the once machine TAG 
	;-------------------------------------------------
	;SET TAG=TAG-1 ;table-mat 
	DO  ;FOR  QUIT:DAUER'>ARBZEIT  DO
	. ;SET TAG=TAG+1 ;table-mat 
	. ;SET DAUER=DAUER-ARBZEIT ;table-mat 
	. ;
	. DO SPEI(TAG,DAUER)
	
	QUIT
]]></Routine>
</Export>