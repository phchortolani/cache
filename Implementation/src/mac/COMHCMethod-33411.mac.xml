<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMHCMethod" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMHCMethod
	#include COMConst
	#define Tree "^CacheTemp(YUSER,""COMHCMethod"")"
 
 
AfterDataFields(pYPARA) 
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Jul-2005	shobby		Only display list of health checks on page 1.
    ;-------------------------------------------------------------------------------
	;
	if YSEITE=1 {
		do Display(pYPARA)
	} elseif YSEITE=2 {
		do AfterDataField^COMSchedule("DAILY","COMHCCALC","CalcSchedule^COMHCCalculate(´Daily´)")
	}
	quit
 
Display(pYPARA)
    ;-------------------------------------------------------------------------------
    ; Display the Health Check methods in a tree structure.
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Jun-2004	shobby	 Hack for display types with prefix MOD. (Intermodule Checks).
    ; 15-Jun-2004	shobby	 Prevent the screen crashing if saved before a method or a checktype has been entered.
    ; 01-Jun-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	;
	new idMethod,objCOMHCMethod,strCheckType,idPrefix1,idPrefix2
	
	set idMethod=""
	for {
		set idMethod=$order(^COMHCMethod(0,idMethod))
		quit:idMethod=""
		set objCOMHCMethod=$get(^COMHCMethod(0,idMethod,1))
		set strCheckType=$$$COMHCMethodCheckType(objCOMHCMethod)
		set idPrefix1=$extract(strCheckType,1,3)
		set idPrefix2=$extract(strCheckType,4,5)
		; Special hacks to make the tree look nice.-------------
		if idPrefix1="MOD" {
			set idPrefix2=" "
		} elseif (idPrefix2="IN")||(idPrefix2="Ta") {
			set idPrefix2=$extract(strCheckType,4,6)
		} elseif idPrefix2="Re" {
			set idPrefix2=$extract(strCheckType,4,13)
		} ;-------------------------------------------------------
		if (idMethod'="")&&(strCheckType'="") {
			set @$$$Tree@(idPrefix1,idPrefix2,strCheckType_" ",idMethod)=objCOMHCMethod
		}
	}
	
	do StartTree^COMTree(pYPARA)
	do AddNode^COMTree("","Health Checks","Health Checks","Health Checks","",,,,,1)
	set idPrefix1=""
	for {
		set idPrefix1=$order(@$$$Tree@(idPrefix1))
		quit:idPrefix1=""
		do AddNode^COMTree("Health Checks",idPrefix1,idPrefix1,idPrefix1,"",,,,,1)
		set idPrefix2=""
		for {
			set idPrefix2=$order(@$$$Tree@(idPrefix1,idPrefix2))
			quit:idPrefix2=""
			if idPrefix2'=" " {
				do AddNode^COMTree(idPrefix1,idPrefix2,idPrefix2,idPrefix2,"",,,,,1)
			}
			set strCheckType=""
			for {
				set strCheckType=$order(@$$$Tree@(idPrefix1,idPrefix2,strCheckType))
				quit:strCheckType=""
				if idPrefix2=" " {
					do AddNode^COMTree(idPrefix1,strCheckType,strCheckType,strCheckType,"",,,,,1)
				} else {
					do AddNode^COMTree(idPrefix2,strCheckType,strCheckType,strCheckType,"",,,,,1)
				}
				set idMethod=""
				for {
					set idMethod=$order(@$$$Tree@(idPrefix1,idPrefix2,strCheckType,idMethod))
					quit:idMethod=""
					set objCOMHCMethod=$get(@$$$Tree@(idPrefix1,idPrefix2,strCheckType,idMethod))
					do AddNode^COMTree(strCheckType,idMethod,idMethod,idMethod,"html.gif",,,,$LB($piece($$$COMHCMethodCode(objCOMHCMethod),"(",1)))
					;do AddNode(pidParent,pidNode,pstrTitle,pstrText,pstrImage,pstrForm="",pstrParam="",pblnHighLight="",pstrExtraData="",pblnLinkStopped="",plngPage="",plngOverRideId="")
				}
			}
		}
		
	}
	do StopTree^COMTree(1)
 
	kill @$$$Tree
	quit
]]></Routine>
</Export>