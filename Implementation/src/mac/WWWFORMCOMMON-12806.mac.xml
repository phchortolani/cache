<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMCOMMON" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[WWWFORMCOMMON
	
#include WWWConst
#include VARConst
#include COMSYS
	
#define FindOption(%1,%2) $find(","_$translate(%1,";",",")_",",","_%2_",")
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORMCOMMON("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	;WWWFORMD
	;	$$NoAccess^WWWFORMCOMMON(YFORM,YDATEI,YBBN,"",   "",   "",    $get(YOPTION),$get(YOPTION1),$get(YPARA1))
	;WWWFORM4
	;	$$NoAccess^WWWFORMCOMMON(YFORM,YDATEI,YBBN,.YTXT,.YLFN,"",    $get(YOPTION),$get(YOPTION1),$get(YPARA1))
	;WWWFORM3
	;	$$NoAccess^WWWFORMCOMMON(YFORM,YDATEI,YBBN,.YTXT,.YLFN,.YTEXT,$get(YOPTION),$get(YOPTION1),$get(YPARA1))
	;-------------------------------------------------------------------------------
	
NoAccess(pYFORM,pYDATEI,pYBBN,&pYTXT="",&pYLFN="",&pstrFieldName="",pYOPTION="",pYOPTION1="",pYPARA1="")
	;-------------------------------------------------------------------------------
	; Notes:
	;	Some Y* parameters are passed by reference as they may be used at other levels
	;	in the call stack after this routine is called.
	;	
	; Params:
	; 	pYFORM:		Form Name
	; 	pYDATEI:	Class Name
	; 	pYBBN:		Field on form
	; 	pYTXT:		Form field design (including customisation)
	; 	pYLFN:		Class field associated with form field
	; 	pstrFieldName:	Name of the field.   **** Note:Only relevant if YHIDDSE=0  ****		
	; 	pYOPTION:	A field can be hidden based on the definition of YOPTION
	; 	pYOPTION1:	A field can be hidden based on the definition of YOPTION1
	; 	pYPARA:		A field can be hidden based on the definition of YPARA
	;
	; Returns:
	; 	blnHIDDSE:	Whether the field should be hidden
	;
	; History:
	; 12-Apr-2011	GRF		SR17711: Allow customisation of Manual field names
	; 11-Feb-2009	GRF		SR16347: Get name from form for manual field
	; 28-Oct-2007	shobby	SRBR014755: Incorrect parameter passed to WWWFELDNAME
	; 03-Jul-2007	GRF		SRBR014615:	$get is not needed for pYOPTION since has
	; 							default value so will always exist.
	; 27-Jul-2007	shobby	SRBR014615:	Return the field name if relevant.  Removed
	; 							YHIDDSE as a parameter.  Corrected default setting
	; 							for Manual fields.
	; 27-Jul-2007	shobby	SRBR014615: Prefixed parameter names with p, 2nd char Y
	; 							is suitable as the 'type' identifier.
	; 27-Jul-2007	shobby	SRBR014586:	Include YOPTION,YOPTION1 and YPARA tests
	; 18-Jul-2007	shobby	SRBR014615: Created
	;-------------------------------------------------------------------------------
	new blnHIDDSE
	
	$$$LogR("NoAccess",pYFORM_"<"_pYBBN)
	
	; These may not be defined and will error if a null value is passed in to this routine as a parameter
	set blnHIDDSE = $$$NO
	set pYTXT     = $$Get^WWW122(pYFORM,pYBBN)
	
	if (pYOPTION="") && ($$$WWW122OnlyShowIfOptionYOPTION(pYTXT)'="") set blnHIDDSE = $$$YES
	if (blnHIDDSE=$$$NO) set blnHIDDSE = $$Hidden(pYOPTION, pYTXT,$$$FldWWW122OnlyShowIfOptionYOPTION)
	if (blnHIDDSE=$$$NO) set blnHIDDSE = $$Hidden(pYOPTION1,pYTXT,$$$FldWWW122OnlyShowIfOptionYOPTION1)
	if (blnHIDDSE=$$$NO) set blnHIDDSE = $$Hidden(pYPARA1,  pYTXT,$$$FldWWW122DisplayItemOnlyWhenMenuPa)
	
	if blnHIDDSE=$$$NO {	
		set pYLFN = $$$WWW122SequenceNumber(pYTXT)
		if pYLFN'="" {
			set blnHIDDSE     = $$$YES ; Uncustomised free fields are hidden but manual shields should still show.
			set pstrFieldName = $$^WWWFELDNAME(pYFORM,"D",pYLFN)
			if $extract(pstrFieldName,1,5)'="_FREE" {
	;			if not hidden by either setting, check authorisation
				if ($$$WWW122InputType(pYTXT)'=0) && ($$$WWW122DataInputType(pYTXT)'=15) {
					set blnHIDDSE = ($$^WWWACCESS($$$WWW122UserAccess(pYTXT),$$$WWW122Module1(pYTXT))'=1)
				}
			}
		} else {    ; Manual field
		;	set pstrFieldName = $$$WWW122ManualCaption(pYTXT)  ; SR17711
			set pstrFieldName = $$^WWWFELDNAME(pYFORM,"M",pYBBN)
		}
	}
	quit blnHIDDSE
	
	
Hidden(pstrValue="",pobjWWW122="",pintField="")
	;-------------------------------------------------------------------------------
	; determines, based on meta data rules for YOPTION, YOPTION1 and YPARA whether a field
	; is hidden
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 03-Aug-2007	GRF		SRBR014615: reuse $piece(pobjWWW122,Y,pintField) variable
	; 26-Jul-2007	shobby	SRBR014615: Created
	;-------------------------------------------------------------------------------
	new blnTest,strData
	
	$$$LogR("Hidden",pstrValue_"<"_pintField)
	
	set blnTest = $$$NO
	if pstrValue'="" {
		set strData = $piece(pobjWWW122,Y,pintField)
		if strData'="" {
			if strData'="'" {
				if '$$$FindOption(strData,pstrValue)                set blnTest = $$$YES
			} else {
				if $$$FindOption($extract(strData,2,999),pstrValue) set blnTest = $$$YES
			}
		}
	}
	quit blnTest
	
	
Coolbar(YVOR="")
	;-------------------------------------------------------------------------------
	; Common method to create Coolbars adjusting the background colour if necessary.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 18-May-2012	shobby	SR17998: id on the COOLBAR
	; 17-Apr-2009	shobby	SR16455: Moved <table> tags in to this routine.
	; 14-Feb-2008	shobby	SRBR014526: Corrected set strText=" STYLE='"
	; 24-Jan-2008	shobby	SRBR014526: Created
	;-------------------------------------------------------------------------------
	new strText,objWWW012
	
	set strText = "<table NOWRAP width=100% cellspacing=0 cellpadding=0"
	if (YVOR="") || ($$$WWW120PicturesAsButtons(YVOR)=$$$YES) {
		set strText   = strText_" class=""coolBar"" id='COOLBAR' " ;SR17998
		set objWWW012 = $get(^WWW012(0,YM,1))
		if $$$WWW012CoolbarBackgroundColor(objWWW012)'="" {
			set strText=strText_" STYLE=""background-color:"_$$$SysEnum("FARBE",$$$WWW012CoolbarBackgroundColor(objWWW012))_""""
		}
	}
	set strText = strText_">"_YCR
	quit strText
	
	
OpenTD(pblnUnselectable=$$$NO)
	;-------------------------------------------------------------------------------
	; 03-May-2010	shobby	SR17253 filter to make more obvious button is disabled.
	;-------------------------------------------------------------------------------
	;quit YCR_"<TD WIDTH=10"_$select(pblnUnselectable:" unselectable style='filter:BlendTrans(Percent=60) gray();'",1:"")_$select($$$WWW120PicturesAsButtons(YVOR):" class=""coolButton""",1:"")_">"
	set disabledButton = pblnUnselectable
	if $$MenuType^WWW013()=14{
		quit YCR_"<TD WIDTH=25"_$select(pblnUnselectable:" unselectable=1 ",1:"")_" style='text-align:center;border:0px;padding:2px 2px 2px 0px;'>"
	}else{
		quit YCR_"<TD WIDTH=10"_$select(pblnUnselectable:" unselectable=1 ",1:"")_$select($$$WWW120PicturesAsButtons(YVOR):" class=""coolButton""",1:"")_">"
	}
	
	
CloseTD()
	quit YCR_"</TD>"_YCR
	
	
Delimiter()
	;-------------------------------------------------------------------------------
	; Creates a toolbar delimiter.  Common code to replace existing repeated code.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 04-Jul-2012	shobby	SR18053: Removed redundant code
	; 25-Aug-2010	GRF		Clarify code
	; 20-Apr-2009	shobby	SR16455: Created
	;-------------------------------------------------------------------------------
	if $$MenuType^WWW013()=14{
		quit ""
	}
	quit "<TD WIDTH=3><IMG SRC="""_YGIF_"delimiter.gif""></TD>"
	
	
StopButton(pstrTitle,pstrImage,pstrName="",pblnEnabled=$$$YES,pblnStatus=$$$NO,pstrStyle="cursor:pointer",pblnCloseA=$$$NO)
	write $$StopButtonEx(pstrTitle,pstrImage,pstrName,pblnEnabled,pblnStatus,pstrStyle,pblnCloseA)
	quit
	
	
StopButtonEx(pstrTitle,pstrImage,pstrName="",pblnEnabled=$$$YES,pblnStatus=$$$NO,pstrStyle="cursor:pointer",pblnCloseA=$$$NO)
	;-------------------------------------------------------------------------------
	; Creates a button.  Common code to replace existing repeated code.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Dec-2013	shobby	SESDF-655: Disabled new buttons should be grey.
	; 04-Jul-2012	shobby	SR18053: Removed redundant code from 16455
	; 12-Jun-2012	shobby	SR18033: Rewrote to obtain HTML instead of write
	; 26-Jul-2011	GRF		Comment out SR16827 block so user "test" doesn't use
	; 20-Aug-2010	GRF		SR17512: increase opacity level from 30 to 50%
	; 02-Aug-2010	shobby	SR17481: $$Gray^WWWFORMCrossBrowserSupport
	; 17-Aug-2009	DWR		SR16827: added testing section to check for possible 
	; 							extension to buttons.
	; 20-Apr-2009	shobby	SR16455: Created
	;-------------------------------------------------------------------------------
	new blnPicButton,strFilter,HTML,strImageMouseOver
	
	set HTML=""
	
	set strFilter = ""

	;SESDF-655 if '$$UseNewButtons() {
		if 'pblnEnabled set strFilter = "; "_$$Gray^WWWFORMCrossBrowserSupportVisual(50) ;SR17481  ; SR17512
	;SESDF-655 }
	
	if pstrImage = "manual.gif" {
		set disabledButton = 0
	}
	if $$$WWW120PicturesAsButtons(YVOR) {
		if '$$UseNewButtons() {
			set icone=""
			if $$MenuType^WWW013()=14{
				set icone = $$traducaoIcone(pstrImage)
			}
			if icone'=""{
				if $extract(icone,1,2)'="fa"&&($length(icone,"-")<2){
					if disabledButton set corBotao="secondary"
					set HTML=HTML_$$$CRLF_"   <div class='btn btn-outline-"_corBotao_" btn-sm "_$select(disabledButton=1:"disabled",1:"")_"' id="""_pstrName_""""
					if pblnStatus set HTML=HTML_$$STATEX^WWWFORMF()
					;;if pstrStyle'="" set HTML=HTML_" style="""_pstrStyle_strFilter_""" "
					set HTML=HTML_" style='padding: 2px 5px 0px 5px;height:23px;vertical-align:middle;text-align:' "
					set HTML=HTML_" title="""_pstrTitle_""" border=0>"
					set HTML=HTML_icone
					set HTML=HTML_"</div>"
					set HTML=HTML_$$$CRLF
				}else{
					if disabledButton set corBotao="secondary"
					set HTML=HTML_$$$CRLF_"   <div class='btn btn-outline-"_corBotao_" btn-sm "_$select(disabledButton=1:"disabled",1:"")_"' id="""_pstrName_""""
					if pblnStatus set HTML=HTML_$$STATEX^WWWFORMF()
					;;if pstrStyle'="" set HTML=HTML_" style="""_pstrStyle_strFilter_""" "
					set HTML=HTML_" style='width:30px' "
					set HTML=HTML_" title="""_pstrTitle_""" border=0>"
					set HTML=HTML_"<i class='"_icone_"'></i>"
					set HTML=HTML_"</div>"
					set HTML=HTML_$$$CRLF
				}
			}else{			
				set HTML=HTML_$$$CRLF_"   <IMG align=absbottom id="""_pstrName_""""
				if pblnStatus set HTML=HTML_$$STATEX^WWWFORMF()
				set HTML=HTML_" SRC="""_YGIF_pstrImage_""""
				if pstrStyle'="" set HTML=HTML_" style="""_pstrStyle_strFilter_""" "
				set HTML=HTML_YHEIGHT_" "_YWIDTH_" TITLE="""_pstrTitle_$s($$MenuType^WWW013()=14:$c(10)_pstrImage,1:"")_""" border=0>"
				;if pblnCloseA set HTML=HTML_$$$CRLF,"</A>"
				set HTML=HTML_$$$CRLF
			}
		} else {
			new strLetter,blnMouseOver
			if $length($replace(pstrImage,"_dis.gif",""))=1 set pstrImage=$replace(pstrImage,"_dis.gif","")
			set strLetter=$piece(pstrImage,".",1)
			if $length(strLetter)=1 set HTML=HTML_"<div>"
			set HTML=HTML_$$$CRLF_"   <IMG id='"_pstrName_"'"
			if pblnStatus set HTML=HTML_$$STATEX^WWWFORMF()
			set HTML=HTML_" SRC="""_$$GetSRC(YGIF,.pstrImage,.blnMouseOver,.strImageMouseOver)_"""" ;SR18053 
		
			if blnMouseOver {
				set HTML=HTML_" _MouseOverImage=1 " 												;SR18053
				set HTML=HTML_" _Image="_pstrImage_" " 												;SR18053
				set HTML=HTML_" _ImageMouseOver="_strImageMouseOver_" " 							;SR18053
			}
		
			if pstrStyle'="" set HTML=HTML_" style='"_pstrStyle_strFilter_"; ' " ;SESDF-655
			set HTML=HTML_YHEIGHT_" "_YWIDTH_" TITLE="""_pstrTitle_""" >"
			set HTML=HTML_$$$CRLF
			if $length(strLetter)=1 {
				set HTML=HTML_"<div"
				if 'pblnEnabled set HTML=HTML_" class='Letter_Disabled'"
				set HTML=HTML_">"_$zconvert(strLetter,"U")_"</div></div>"
			}
		}
	}
	quit HTML

GetSRC(YGIF,&pstrImage,&pblnMouseOver,&pstrImageMouseOver)  ;SR18053
	;-------------------------------------------------------------------------------
	; Replacement of old buttons with new buttons.
	; At some point it will be decided this should be a configuration or whether
	; old buttons are retired from the system.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 04-Jul-2012	shobby		SR18053: Created
	;-------------------------------------------------------------------------------
	new strImage

	set pblnMouseOver=$$$NO
	if $$UseNewButtons() {
		set strImage=pstrImage
		if $length($piece(strImage,".",1))=1 { set strImage="blank.gif"			
		} elseif pstrImage="listg.gif" 		{ set strImage="print.gif"
		} elseif pstrImage="del.gif" 		{ set strImage="delete.gif"
		} elseif pstrImage="action.gif" 	{ set strImage="processar.gif"
		} elseif pstrImage="actionno.gif" 	{ set strImage="processar_off.gif"
		} elseif pstrImage="text.gif" 		{ set strImage="relatorio02.gif"
		} elseif pstrImage="hback.gif" 		{ set strImage="back.gif"
		} elseif pstrImage="hbackd.gif" 	{ set strImage="back_off.gif"
		} elseif pstrImage="rrev.gif" 		{ set strImage="first.gif"
		} elseif pstrImage="rev.gif" 		{ set strImage="previous.gif"
		} elseif pstrImage="for.gif" 		{ set strImage="next.gif"
		} elseif pstrImage="ffor.gif" 		{ set strImage="last.gif"
		} elseif pstrImage="disdel.gif" 	{ set strImage="delete_off.gif"
		} elseif pstrImage="hook.gif" 		{ set strImage="hook.gif"
		} elseif pstrImage="key.gif" 		{ set strImage="Key.gif"
		}
		if ##class(%Library.File).Exists($$$WWW012PhysicalWWWDirectory($get(^WWW012(0,0,1)))_"Buttons/"_strImage) {
			set pstrImage="Buttons/"_strImage
		}
		set strImage=$piece(strImage,".gif",1)_"_mouseover.gif"
		if ##class(%Library.File).Exists($$$WWW012PhysicalWWWDirectory($get(^WWW012(0,0,1)))_"Buttons/"_strImage) {
			set pblnMouseOver=$$$YES
			set pstrImageMouseOver=YGIF_"Buttons/"_strImage
		} else {
			set pstrImageMouseOver=pstrImage
		}
	}
	;set pstrImage=strImage
	set pstrImage=YGIF_pstrImage
	quit pstrImage

UseNewButtons()	;SR18053
	; 23-Aug-2012	shobby	SR18053:  Only do the new buttons in the toolbar
	;
	quit (+$$$WWW013ButtonStyle($get(^WWW013(0,YBED,1))))&&($get(^CacheTempToolbar(YUSER))) ;SR18053
	
	
PreProcessText(pobj,pintField) ;SR16925
	;-------------------------------------------------------------------------------
	; In some cases we don't want to try and translate the input text if there is
	; no corresponding language code that matches exactly the passed in string.
	; In that case we just want to return the original string.
	; A number of fields on forms have the option of entering a language code OR
	; straight text.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 22-Oct-2009	shobby	SR16977: Don't check texts of 255 characters or longer.
	; 15-Oct-2009	shobby	SR16925: Created
	;-------------------------------------------------------------------------------
	new strText
	
	set strText = $piece(pobj,Y,pintField)
	
	if (strText'="") && ($length(strText)<255) {
		if $data(^WWW009(0,SPRACHE,strText)) {
			set strText = $$$Text(strText)
		}
	}
	quit strText
	
traducaoIcone(imagem)
	new (imagem,corBotao)
	set icone = ""
	set corBotao = "primary"
	if $extract(imagem,1,2)="fa"&&($find(imagem,".")<=0) quit imagem
	set objVARPARA = $get(^VARPARA(0,"ICONESDEPARA","PT",imagem,1))
	if objVARPARA'=""{
		set icone = $$$VARPARAText(objVARPARA)
		set cor = $$$VARPARAColorcode(objVARPARA)
		set cor = $$$SysEnum("FARBE",cor)
		if cor="red" set corBotao = "danger"
		if cor="green" set corBotao = "success"
	}
	quit icone
	if imagem="new.gif" set icone="fa fa-file"
	if imagem="open.gif" set icone="fa fa-folder-open"
	if imagem="reset.gif" set icone="fa fa-refresh"
	if imagem="ok.gif" set icone="fa fa-check"
	if imagem="del.gif" set icone="fa fa-trash",corBotao = "danger"
	if imagem="disdel.gif" set icone="fa fa-trash",corBotao = "danger"
	if imagem="save.gif" set icone="fa fa-floppy-o",corBotao = "success"
	if imagem="saved.gif" set icone="fa fa-floppy-o",corBotao = "success"
	if imagem="help.gif" set icone="fa fa-question"
	if imagem="end.gif" set icone="fa fa-window-close"
	if imagem="hback.gif" set icone="fa fa-undo"
	if imagem="hbackd.gif" set icone="fa fa-undo"
	if imagem="search.gif" set icone="fa fa-search"
	if imagem="rrev.gif" set icone="fa fa-fast-backward",corBotao = "success"
	if imagem="rev.gif" set icone="fa fa-backward",corBotao = "success"
	if imagem="for.gif" set icone="fa fa-forward",corBotao = "success"
	if imagem="ffor.gif" set icone="fa fa-fast-forward",corBotao = "success"
	;
	if imagem="industrie.gif" set]]><![CDATA[ icone="fa fa-industry"
	if imagem="e.gif" set icone="fa fa-info-circle"
	if imagem="wv.gif" set icone="fa fa-bell"
	if imagem="time1.gif" set icone="fa fa-clock-o"
	;
	if imagem="language.gif" set icone="fa fa-language"
	if imagem="copy.gif" set icone="fa fa-files-o"
	if imagem="p.gif" set icone="fa fa-money"
	if imagem="p_dis.gif" set icone="fa fa-money"
	;
	if imagem="anschriften.gif" set icone="fa fa-map"
	if imagem="personen.gif" set icone="fa fa-address-book"
	if imagem="Contatos.gif" set icone="fa fa-address-card"
	if imagem="ContatosOff.gif" set icone="fa fa-address-card"
	if imagem="date.gif" set icone="fa fa-table"
	if imagem="briefe.gif" set icone="fa fa-envelope"
	if imagem="dokudruck.gif" set icone="fa fa-file-text-o"
	if imagem="Certidoes.gif" set icone="fa fa-certificate"
	if imagem="CertidoesOff.gif" set icone="fa fa-certificate"
	if imagem="doku.gif" set icone="fa fa-folder"
	if imagem="Representantes.gif" set icone="fa fa-users"
	if imagem="RepresentantesOff.gif" set icone="fa fa-users"
	if imagem="bonus.gif" set icone="fa fa-diamond"
	if imagem="qualificacoes.gif" set icone="fa fa-trophy"
	if imagem="qualificacoes_off.gif" set icone="fa fa-trophy"
	if imagem="konditionen.gif" set icone="fa fa-bullseye"
	if imagem="Ocorrencias.gif" set icone="fa fa-exclamation-triangle"
	if imagem="OcorrenciasOff.gif" set icone="fa fa-exclamation-triangle"
	if imagem="artikel.gif" set icone="fa fa-object-group"
	if imagem="Avaliacoes.gif" set icone="fa fa-book"
	if imagem="AvaliacoesOff.gif" set icone="fa fa-book"
	if imagem="allevertr.gif" set icone="fa fa-file-archive-o"
	if imagem="op-b.gif" set icone="fa fa-usd"
	if imagem="c.gif" set icone="fa fa-list"
	if imagem="c_dis.gif" set icone="fa fa-list"
	if imagem="nachlass.gif" set icone="fa fa-percent"
	if imagem="statistik.gif" set icone="fa fa-line-chart"
	if imagem="h.gif" set icone="fa fa-history"
	if imagem="h_dis.gif" set icone="fa fa-history"
	if imagem="d.gif" set icone="fa fa-file-pdf-o"
	if imagem="d_dis.gif" set icone="fa fa-file-pdf-o"
	;
	if imagem="search1.gif" set icone="fa fa-search"
	if imagem="text.gif" set icone="fa fa-file-text-o"
	;
	if imagem="up.gif" set icone="fa fa-level-up"
	;
	if imagem="bold.gif" set icone="fa fa-bold"
	if imagem="italic.gif" set icone="fa fa-italic"
	if imagem="underline.gif" set icone="fa fa-underline"
	if imagem="center.gif" set icone="fa fa-align-center"
	if imagem="horizon.gif" set icone="fa fa-list"
	if imagem="sort.gif" set icone="fa fa-list-ol"
	;
	if imagem="confirmar.gif" set icone="fa fa-check-circle"
	if imagem="confirmarOff.gif" set icone="fa fa-check-circle"
	if imagem="modelo.gif" set icone="fa fa-universal-access"
	if imagem="modelo_off.gif" set icone="fa fa-universal-access"
	if imagem="s.gif" set icone="fa fa-street-view"
	if imagem="s_dis.gif" set icone="fa fa-street-view"
	if imagem="Cancelar.gif" set icone="fa fa-times"
	if imagem="CancelarOff.gif" set icone="fa fa-times"
	if imagem="r.gif" set icone="fa fa-bookmark"
	if imagem="r_dis.gif" set icone="fa fa-bookmark"
	if imagem="relatorio.gif" set icone="fa fa-print"
	if imagem="relatorio_off.gif" set icone="fa fa-print"
	if imagem="JaImpresso.gif" set icone="fa fa-file-pdf-o"
	if imagem="JaImpressoOff.gif" set icone="fa fa-file-pdf-o"

	quit icone]]></Routine>
</Export>