<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTUNIT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTUNIT
	
#include COMSYS
	
UpdateUnits(idItem,YUPDATE,pidUnit="")
	;-------------------------------------------------------------------------------
	; Update the Unit List (Combo Box) based on the Item Provided
	;
	; Inputs:
	;	pidItem
	;	YUPDATE in the form  "Y"_YFORM_YART_FIELD
	;
	; Returns:
	;
	; History:
	; 22-Oct-2012	SCR		SR18132: Set Unit Option
	; 25-Jun-2012	SCR		SR17993: Pass pack default unit
	; 21-Aug-2009	PPP		SR16842: Created
	;-------------------------------------------------------------------------------
	new idUnit,idUnitSel
	
	set %TXT(1)=""
	if $get(idItem)="" {
		set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~REMOVE"
		set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_0
		quit ""
	}
	
	set YI=0
	set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~REMOVE"
	set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_$$$NULLID_"~"_$$$SPACE_"~"_YI

	set idUnitSel = $$LoadUnitOfMeasureList^INARTUtilities(idItem)
	
	set idUnit = ""
	for YI(1)=1:1 {
		set idUnit = $order(^COMTempList(0,idItem,"UOM",idUnit))
		quit:idUnit=""
		
		set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_idUnit_"~"_idUnit_" - "_$get(^COMTempList(0,idItem,"UOM",idUnit,1))_"~"_YI(1)
	}
	set:$get(pidUnit)'="" idUnitSel=pidUnit ; SR18132

	set %TXT(1) = %TXT(1)_"#"_YUPDATE_"~"_idUnitSel
	
	if $get(YFOCUS)'="" set %TXT(1) = %TXT(1)_"#FUNCTION~document."_YHTMFORM_"."_YUPDATE_".focus();"

	quit idUnitSel
			
]]></Routine>
</Export>