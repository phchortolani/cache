<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWClassTranslation" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWClassTranslation
	
#include %occInclude
#include COMConst
#include WWWConst
#include COMSYS
	
GenerateDataForAClass(pidClass)
	;-------------------------------------------------------------------------------
	; Generate the Proprty Translation list given a Class
	;
	; Params:	pidClass		Class id
	;
	; Returns:	strStatus
	;
	; History:
	; 15-Apr-2009	shobby	SR16468: Code check.
	; 06-Apr-2009	PPP		SR16468: Creation
	;-------------------------------------------------------------------------------
	new idLang,intCnt,intMaxCnt,objClass,objProp
	new strDescription,strField,strPKeys,strStatus
	
	set strStatus = $$$OK
	set objClass = ##class(%Library.ClassDefinition).%OpenId(pidClass)
	
	if objClass '= $$$NULLOREF {
		set intMaxCnt = objClass.Properties.Count()
		
		for intCnt = 1:1:intMaxCnt {
			set objProp = objClass.Properties.GetAt(intCnt)
			set (strDescription,strField) = objProp.Name
		//	set strType = objProp.Type
			
			set idLang = "EN"
			set strPKeys = pidClass_","_strField_","_idLang
			set strStatus = $$$Save("WWWClassTranslation",strPKeys,strDescription,$$$YES)
		}
		do objClass.%Close()
	}
	quit strStatus
	
	
GetText(pidClass, pidProperty, SPRACHE="EN")
	;-------------------------------------------------------------------------------
	; Get the Text associated with a Class:Property from the CAPTION associated
	; with the Class
	;
	; Params:	pidClass		Class Id
	; 			pidProperty		Property Id
	;
	; Returns:	Text string
	;
	; History:
	; 01-Dec-2009	PPP		SR17025: Created new callout $$GetCustomisedText
	; 11-Nov-2009	PPP		SR17025: Description from Customisation if it exists
	; 15-Apr-2009	shobby	SR16468: Open the property with no locking.
	; 07-Apr-2009	SCH		SR16468: Creation
	;-------------------------------------------------------------------------------
	new objProperty,strCode,strText
	
 	//Only for Non @NM classes   //SR17025
 	if '$data(^WWW001(0,pidClass)) {
 		set strText = $$GetCustomisedText(pidClass,pidProperty)
		if strText'="" quit strText				//EARLY EXIT
 	}
 	
	set strCode = ""	
	set objProperty = ##class(%Dictionary.PropertyDefinition).%OpenId(pidClass_"||"_pidProperty,0)
	if objProperty'=$$$NULLOREF {
		set strCode = objProperty.Parameters.GetAt("CAPTION")
	}
	
	if strCode = "" {
		set strText = pidProperty
	} else {
		set strText = $$$Text(strCode)
	}		
	quit strText
	
	
GetTextRelated(pidClass, pidProperty, pidLang="EN")
	;-------------------------------------------------------------------------------
	; Get the Text associated with a Class:Property, Propery is a linked property
	; refering to Related Classes
	;
	; Params:	pidClass		Class Id
	; 			pidProperty		Property Id/with relations
	;
	; Returns:	Text string
	;
	; History:
	; 10-Jun-2011	shobby	SR17788: Rearranged a bit as it could miss translating
	;							the first field.
	; 07-Apr-2009	PPP		SR16468: Creation
	;-------------------------------------------------------------------------------
	new idClass,idField,idFieldClass,intCnt,strText
	
	set strText = ""
	set idClass = pidClass
	
	for intCnt = 1:1:$length(pidProperty,"->") {
		set idField = $piece(pidProperty,"->",intCnt)
		set strText = strText_"->"_$$GetText(idClass,idField,pidLang)	;SR17788
		set idFieldClass = $$GetClass^COMViewObject(idClass,idField)	;SR17788 //alLOC.dStorage
		;SR17788 set idFieldClass = $$GetClass^COMViewObject(idClass,idField)	//alLOC.dStorage
		;SR17788 set strText = strText_"->"_$$GetText(idFieldClass,idField,pidLang)
		set idClass = idFieldClass
	}
	
	quit $piece(strText,"->",2,99)
	
	
Caption(pidClass)
	;-------------------------------------------------------------------------------
	; Call to the Caption1 tag to do the work
	; Note: This has to be a jobbed process otherwise studio won't recognise that the routine
	; has been changed and won't reload it to the user.
	; 
	; Params:	pidClass		Class Id
	;
	; Returns:	1
	;
	; History:
	; 15-Apr-2009	shobby	SR16468: Code Check
	;-------------------------------------------------------------------------------
	job Caption1(pidClass)
	quit 1
	
	
Caption1(pidClass)
	;-------------------------------------------------------------------------------
	; Create the CAPTION parameter for a given 'al' class
	; with a language ID
	; 
	; Params:	pidClass		Class Id
	;
	; Returns:	sc
	;
	; History:
	; 15-Apr-2009	shobby	SR16468: Code Check
	; 09-Apr-2009	shobby	SR16468: Delete the class and recreate.  This forces the studio
	; 								 to recognise a change has been made and message the user
	; 								 to reload.
	; 07-Apr-2009	PPP		SR16468: Creation
	;-------------------------------------------------------------------------------
	new idProperty,objClass,loop,idWWW009
	new objProperty,sc,objClassNew
	new blnSave
	new strText
	
	do ^WWWVAR
	set blnSave = $$$NO
	set sc      = $$$OK
	
	//Only for al Classes
	if $extract(pidClass,1,2)'="al" quit $$$OK
	
	set objClass=##class(%Dictionary.ClassDefinition).%OpenId(pidClass,4)
	
	if objClass'=$$$NULLOREF {
		for loop=1:1:objClass.Properties.Count() {
		;	write !,objClass.Properties.GetAt(loop).Name_":" 
			set objProperty = objClass.Properties.GetAt(loop)
			
			if 'objProperty.Parameters.IsDefined("CAPTION") {
				set strText  = objProperty.Name
				set idWWW009 = $$CreateText^COMLanguage("AL",strText)
				if idWWW009'="" {
					do objProperty.Parameters.SetAt(idWWW009,"CAPTION")
					set blnSave = $$$YES
				}	
			}
		}
		;set blnSave=$$$YES
		if blnSave = $$$YES {
			set sc = objClass.%Save()
			set objClassNew = objClass.%ConstructClone()
			do objClass.%Close()
			set sc = ##class(%Dictionary.ClassDefinition).%DeleteId(pidClass)
		    if $$$ISOK(sc) set sc = objClassNew.NameSet(pidClass)
		    if $$$ISOK(sc) set sc = objClassNew.%Save()
		    if $$$ISOK(sc) set sc = objClassNew.%Close()
	 		kill objClassNew
		}
	}
	quit sc
	
	
GetCustomisedText(pidClass,pidProperty)
	;-------------------------------------------------------------------------------
	; Get the Customised Text for Classes (non @NM)
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 27-Jul-2011	shobby	SR17788: Check that class/property exists.
	; 01-Dec-2009	PPP		SR17025: Created
	;-------------------------------------------------------------------------------
 	new strText

 	set strText = ""
 	if (pidClass'="")&&(pidProperty'="") { ;SR17788
		if $data(^WWW122D(0,pidClass,pidProperty)) {
			set strText = $$$WWW122DFieldDescription($get(^WWW122D(0,pidClass,pidProperty,0,1)))
		}
 	}
	
	quit strText
	
]]></Routine>
</Export>