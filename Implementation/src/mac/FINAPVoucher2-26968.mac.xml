<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPVoucher2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPVoucher2
	
#include FINConst
#include COMSYS
	
InvoiceTotals(pobjGrid,pcurOnCharge,pcurTotal,pcurTax)
    ;-------------------------------------------------------------------------------
    ; Loop through the grid structure and sum various totals.
    ;
    ; Params:
    ;
    ; ByRefs: pobjGrid		:	The grid structure
    ; 		  pcurOnCharge  :	On charge amounts
    ; 		  pcurTotal		:	Invoice total
    ; 		  pcurTax		:	Tax total
    ;
    ; Returns:
    ;
    ; History:
    ; 30-Jan-2006	SteveS	SR13497: Moved from UpdateContainer^FINAPInvCharge
    ;-------------------------------------------------------------------------------
	new idFINAPInv,idFINAPInvLine,objFINAPInvLine,objInvoice
	
	set pcurOnCharge = 0
	set pcurTotal    = 0
	set pcurTax      = 0
		
	// Sum values
	if '$data(pobjGrid) $$$GRIDGetContents(.pobjGrid)
	set objInvoice = $$$GRIDYFELDContainer
		
	set idFINAPInv = $order(pobjGrid(""))
	if (idFINAPInv'="") {
		set idFINAPInvLine = ""
		for {
			set idFINAPInvLine = $order(pobjGrid(idFINAPInv,idFINAPInvLine))
			quit:idFINAPInvLine=""
				
			set objFINAPInvLine = $get(pobjGrid(idFINAPInv,idFINAPInvLine))
			if $$$FINAPInvChargeIsOnCharge(objFINAPInvLine) {
				set pcurOnCharge = $$$FCAdd(pcurOnCharge,$$$FINAPInvChargeAmount(objFINAPInvLine))
			} else {
				set pcurTotal = $$$FCAdd(pcurTotal,$$$FINAPInvChargeAmountTaxEx(objFINAPInvLine))
				set pcurTax   = $$$FCAdd(pcurTax,$$$FINAPInvChargeTax(objFINAPInvLine))
			}
		}
	}
	if '$$$FINAPInvLinesIncludeTax(objInvoice) set pcurTax = $$$FINAPInvTaxTotal(objInvoice)
	if '$$$FINAPInvTaxInvoice(objInvoice)      set pcurTax = 0
	
	quit
	
	
OnBlurDiscountAmount(pYFELD,pYOLDV)
    ;-------------------------------------------------------------------------------
    ; Code to run on blur of the discount amount field
    ;
    ; Params:	pYFELD  :  The YFELD string
    ;			pYOLDV  :  The prev value
    ;			
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 30-Jan-2006	SteveS	SR13497: Created
    ;-------------------------------------------------------------------------------
	if ($$$FINAPInvDiscountAmount(pYFELD)'=pYOLDV) {
		set %TXT(1) = "#Y"_YFORM_"D"_$$$FldFINAPInvDiscountPercent_Y		// TODO: Set or append ?
	}
	quit
	
	
UpdateDiscount(&pYFELD)
    ;-------------------------------------------------------------------------------
    ; Update the Discount fields
    ;
    ; Params:	pYFELD  :  The YFELD string
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 18-Feb-2010	shobby	SR15753: YFELD by reference.
    ; 30-Jan-2006	SteveS	SR13497: Created
    ;-------------------------------------------------------------------------------
	new curDiscTotal
	
	if $$$FINAPInvDiscountPercent(pYFELD)'="" {
		set curDiscTotal = $$GetDiscAmount($$$FINAPInvDiscountPercent(pYFELD))
		set $$$FINAPInvDiscountAmount(pYFELD)=curDiscTotal
	}
	quit
	
	
OnBlurDiscountPercent(&pYFELD)
    ;-------------------------------------------------------------------------------
    ; Code to run on blur of the discount percent field
    ;
    ; Params:	pYFELD  :  The YFELD string
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 18-Feb-2010	shobby	SR15753: YFELD by reference.
    ; 30-Jan-2006	SteveS	SR13497: Created
    ;-------------------------------------------------------------------------------
	do UpdateDiscount(.pYFELD)
	quit
	
		
GetDiscAmount(pfltDiscPC)
    ;-------------------------------------------------------------------------------
    ; For a percentage discount amount, return the corresponding currency discount.
    ;
    ; Params: pfltDiscPC  :  The discount percent amount
    ;
    ; ByRefs:
    ;
    ; Returns:
    ;
    ; History:
    ; 30-Jan-2006	SteveS	SR13497: Created
    ;-------------------------------------------------------------------------------
	new objGrid,curOnCharge,curTotal,curTax,curInvTotal,curNewTotal,curDiscTotal
	
	do InvoiceTotals(.objGrid,.curOnCharge,.curTotal,.curTax)
	
	set curInvTotal  = $$$FCAdd(curTotal,curTax)
	set curNewTotal  = $$$FCMult2(curInvTotal,(1-(pfltDiscPC/100)))
	set curDiscTotal = $$$FCSub(curInvTotal,curNewTotal)
	
	quit curDiscTotal
	
]]></Routine>
</Export>