<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWMAILR2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWMAILR2(SRVR,USER,PASS,DELE,ZUG1,PART,ANZ1) ;WWWMAILR2;SAN;holt eine e-Mail;;:GEAEND.:10.04.2000 ;one   ; Compiled February 9, 2005 13:13:51
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		holt eine e-Mail
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| SAN	holt eine e-Mail
	;|
	;\------------------------------------------------------------------/
	;
	;Kai Sann, SAN 15.3.2000
	;letzte Änderung: FUH 10.4.2000 ;last 
	;;SRVR: IP-Adresse des e-Mail Servers, z.B: 172.16.0.2
	;;USER: e-Mail User, z.B: Sann
	;;PASS: Passwort des e-Mail Kontos
	;;DELE: 1 = abgeholte e-Mail löschen, 0 = abgeholte e-Mail nicht löschen
	;;ZUG1: Zugriff des Arrays in den die e-Mail geschrieben wird, z.B: "LOCAL" oder "^ZUG1BAL(""KNOTEN"")"
	;;PART: 1 = Bodyparts werden in Unterknoten 1..N aufgelistet, 0 = keine Unterknoten
	;;ANZ1: Anzahl e-Mails die geholt werden sollen. Default = 1 ;Number who will 
	NEW (ANZ1,DELE,PART,PASS,SRVR,USER,Y,YBED,YUSER,ZUG1)
	SET ERROR=0
	SET DEBUG=0
	SET ANZ=0        
	IF ZUG1="" SET ZUG1="^WWWMAI(0,"_""""_YBED_""""_")" ;Wenn kein Global mach Dir einen ;not 
	IF $PIECE(YSYST,",")="MSM" DO   ;Kompatibilität zu MSM, ACHTUNG funktioniert nur wenn der AUFRUF SOCKET funktioniert kein Standard MUMPS
	. SET DEV=56
	. OPEN 56
	. USE 56 WRITE /SOCKET(SRVR,110) ;110 Ist der Server Port des POP3 PROTOKOLLS ;who Server 
	. USE 56:(:1:3)
	. QUIT 
	IF $PIECE(YSYST,",")="CACHE" DO
	. SET DEV="|TCP|110"
	. OPEN DEV:(SRVR:110:"C") 
	. USE DEV:(SRVR:110:"C")
	. QUIT 
	IF '$$OK GOTO END
	WRITE "USER "_USER_$CHAR(13,10) WRITE ! IF '$$OK GOTO END ;ACHTUNG: W !  des TCP/IP Protokolls gehe in Bereitschaft für nächste Befehl $C(13,10) funktioniert nicht!!!
	WRITE "PASS "_PASS_$CHAR(13,10) WRITE ! IF '$$OK GOTO END  
	WRITE "STAT",! IF '$$OK GOTO END
	SET ANZ=+$PIECE(IN," ",2)
	IF $GET(ANZ1)="" SET ANZ1=1
	IF ANZ1>ANZ SET ANZ1=ANZ
	SET $PIECE(ERGEBNIS,Y,3)=ANZ
	FOR I=1:1:ANZ1 DO  QUIT:ERROR
	. LOCK +@ZUG1
	. SET MAILNR=$ORDER(@ZUG1@(""),-1)+1
	. SET @ZUG1@(MAILNR,1,1)=""
	. LOCK -@ZUG1
	. ;Wenn Parts getrennt werden sollen dann weiterer Globalknoten ;will should 
	. IF $GET(PART) SET ZUG2=ZUG1 FOR  SET KNO=$HOROLOG_","_$RANDOM(100000),ZUG1="^WWWSOR(""MAIL"","""_KNO_""")" hang 1 QUIT:'$DATA(@ZUG1)
	. SET LFN=1
	. WRITE "RETR "_I,!
	. SET START=0
	. SET STRING2=""
	. IF '$$OK SET ERROR=1 QUIT
	. FOR  SET DUMMY=$$OK QUIT:IN="."  DO
	. . IF START=0 DO KOPF QUIT
	. . SET:$EXTRACT(IN)="." IN=$EXTRACT(IN,2,255)
	. . SET LFN=LFN+1 SET @ZUG1@(MAILNR,LFN,1)=IN
	. . QUIT
	. SET $PIECE(ERGEBNIS,Y,1)=I
	. IF DELE WRITE "DELE "_I,! IF '$$OK SET ERROR=1 QUIT
	. SET $PIECE(ERGEBNIS,Y,2)=I
	. QUIT:$GET(PART)=0
	. QUIT
	
END ; 
	USE DEV WRITE "QUIT",!
	CLOSE DEV 
	SET $PIECE(ERGEBNIS,Y,4)=ERROR
	QUIT ERGEBNIS
	
OK() ;
	NEW (IN,DEV,TCPIP,POP3)
	SET IN=""
	USE DEV
	FOR  READ DUMMY#1:1 QUIT:'$TEST  DO DEBUG(DUMMY) SET IN=IN_DUMMY QUIT:IN[$CHAR(13,10)
	SET IN=$PIECE(IN,$CHAR(13,10))
	QUIT $PIECE(IN," ")="+OK"
	
DEBUG(A) ;
	USE 0 WRITE A USE DEV
	QUIT
	
KOPF ;Hier werden die Kopfzeilen gelesen ;will who 
	NEW I
	SET STRING="From~Subject~Date~MIME-Version~Content-Type~Content-Transfer-Encoding" 
	IF IN="" SET START=1 SET @ZUG1@(MAILNR,1,1)=STRING2 QUIT
	FOR I=1:1:$LENGTH(STRING,Y) DO 
	. SET TEST=$PIECE(STRING,Y,I)
	. IF $PIECE(IN,":")[TEST SET $PIECE(STRING2,Y,I)=$PIECE(IN,": ",2,9)
	. QUIT
	QUIT
	
KOPF2 ;Kopfzeilen der Parts ;who 
	NEW I
	SET STRING="~~~~Content-Type~Content-Transfer-Encoding" 
	IF IN="" SET START=1 SET @ZUG1@(MAILNR,PARTNR,1,1)=STRING2 QUIT
	FOR I=1:1:$LENGTH(STRING,Y) DO 
	. SET TEST=$PIECE(STRING,Y,I)
	. IF $PIECE(IN,":")[TEST SET $PIECE(STRING2,Y,I)=$PIECE(IN,": ",2,9)
	. QUIT
	QUIT
]]></Routine>
</Export>