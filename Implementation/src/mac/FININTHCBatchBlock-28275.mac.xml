<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FININTHCBatchBlock" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FININTHCBatchBlock  ; FINANCE Interface Batch Lines Health Checks.
#include FINHC


Check(Start="",End="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; The Sum of each Interface Block must equal 0
    ;
    ; Returns:
    ;
    ; History:
    ; 08-Jul-2004	shobby	 Can stop the Health Checks with a large number of errors.  Restricted to around 200.
    ; 13-May-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	;
	new strStatus,idBatch,idLine,fltAmount,strBlockMajor,strBlockMinor,objFININTBatchLine
	new intErrorCounter
	
	kill Result
	set strStatus=$$$OK
	if $get(Y)="" d ^WWWVAR
	
	set idBatch=""
	for {
		quit:$get(Result("Errors"))>200
		set idBatch=$order(^FININTBatchLines(YM,1,idBatch))
		quit:idBatch=""
		set strBlockMajor=""
		for {
			set strBlockMajor=$order(^FININTBatchLines(YM,1,idBatch,strBlockMajor))
			quit:strBlockMajor=""
			set strBlockMinor=""
			for {
				set strBlockMinor=$order(^FININTBatchLines(YM,1,idBatch,strBlockMajor,strBlockMinor))
				quit:strBlockMinor=""
				set fltAmount=0
				set idLine=""
				for {
					set idLine=$order(^FININTBatchLines(YM,1,idBatch,strBlockMajor,strBlockMinor,idBatch,idLine))
					quit:idLine=""
					set objFININTBatchLine=$get(^FININTBatchLine(YM,idBatch,idLine,1))
					set fltAmount=$$$FCAdd(fltAmount,$$$FININTBatchLineAmount(objFININTBatchLine))
				}
				if +fltAmount'=0 {
					set Result("Hash",$$$ErrorNo(Result("Errors")))=$$$Text($lb("Fin00342",idBatch,strBlockMajor,strBlockMinor,fltAmount))  ;Total of Interface Block(%1,%2,%3) does not equal zero (%4).
					set strStatus='$$$OK
				}				
			}
		}
	}
	
	
	if strStatus=$$$OK {
		set strStatus=2  ; Because this is a Standalone Check return 2 if Ok
	}
	quit strStatus

]]></Routine>
</Export>