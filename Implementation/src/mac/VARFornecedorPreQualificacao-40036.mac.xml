<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARFornecedorPreQualificacao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARFornecedorPreQualificacao
	#include COMSYS
	#include INConst
	#include VARConst
	quit

OnBeforeButtonLine(pYKEY)
	new idPreQualificacao, objPreQualificacao, idINLIEF, objINLIEF

	set idINLIEF = $$$KEY1(pYKEY)
	set YOPTION = ""
	set GblStatusUltimaVisita = ""
	set GblDataUltimaVisita = ""
	set GblAutorizacaoAnvisa = ""
	set GblGrupoParticipante = ""
	set GblHospitalLider = ""
	set GblApresentacao = ""

	if ($length(idINLIEF) > 0) {
		set idPreQualificacao = $order(^VARFornecedorPreQualificacao(YM,
			idINLIEF, ""), -1)
		if (($length(idPreQualificacao) > 0) &&
			(idPreQualificacao '= $$$KEY2(pYKEY))) {
			set objPreQualificacao = $get(^VARFornecedorPreQualificacao(YM,
				idINLIEF, idPreQualificacao, 1))
			set GblStatusUltimaVisita =
				$$$VARFornecedorPreQualificacaoStatusPreQualificacao(objPreQualificacao)
			set GblDataUltimaVisita =
				$$$VARFornecedorPreQualificacaoDataRealizada(objPreQualificacao)
			set YOPTION = 1

			set GblAutorizacaoAnvisa =
				$$$VARFornecedorPreQualificacaoAutorizacaoAnvisa(objPreQualificacao)
			set GblGrupoParticipante =
				$$$VARFornecedorPreQualificacaoGrupoParticipante(objPreQualificacao)
			set GblHospitalLider =
				$$$VARFornecedorPreQualificacaoHospitalLider(objPreQualificacao)
			set GblApresentacao =
				$$$VARFornecedorPreQualificacaoApresentacao(objPreQualificacao)
		}
	}

	set GblStatusPreQualificacao = "A/Q"

	set objINLIEF = ""
	if ($length(idINLIEF) > 0) set objINLIEF = $get(^INLIEF(YM, idINLIEF, 1))
	set GblSegmento = $$$INLIEFFREE10(objINLIEF)
	quit

OnAfterDataFields(pYKEY)
	new lstHeader, intCount, idINLIEF, idPreQualificacao, strAutorizacaoAnvisa,
	    strGrupoParticipante, strHospitalLider, strApresentacao, datAgendada,
	    datRealizada, strStatusPreQualificacao, datProximaVisita, blnCarta,
	    blnRelatorio, blnChecklist, strKey, strParticipanteDesc,
	    strApresentacaoDesc, strStatusPreQualificacaoDesc, strCartaDesc,
	    strRelatorioDesc, strChecklistDesc

	// Monta cabeçalho da tabela
 	set lstHeader = $listbuild("Seq.", "Autorização&nbsp<br/>ANVISA",
 		"Grupo", "Hospital&nbsp<br/>&nbspLíder", "Apresentação",
 		"Data<br/>&nbsp&nbspAgendada", "Data<br/>&nbsp&nbspRealizada",
 		"Status", "Próxima&nbsp<br/>&nbspVisita",
 		"Carta?", "Relatório?", "Checklist?")

  	if ($$Start^COMTable(lstHeader, "<div style=margin:3px;>"_
  		"Visitas de qualificação realizadas para o fornecedor</div>", , , $$$NO)) {
		set intCount = 0

		if ('$$$NoKey(pYKEY)) {
		 	set rs = ##class(%ResultSet).%New()
		    do rs.Prepare("SELECT   Fornecedor, PreQualificacao, "_
		                  "         AutorizacaoAnvisa, GrupoParticipante, "_
		                  "         HospitalLider, Apresentacao, DataAgendada, "_
		                  "         DataRealizada, StatusPreQualificacao, "_
		                  "         DataProximaVisita, Carta, Relatorio, "_
		                  "         Checklist "_
		    			  "FROM     VARFornecedorPreQualificacao "_
		    			  "WHERE    Fornecedor = ? "_
		    			  "ORDER BY PreQualificacao DESC")
		    do rs.Execute($$$KEY1(pYKEY))

		    while (rs.Next()) {
			    // Check if this succeeded without errors
				if ($SYSTEM.Status.IsOK(rs)) {
		        	// Pegando valores da query
		        	set idINLIEF                 = rs.GetData(1)
		        	set idPreQualificacao        = rs.GetData(2)
		        	set strAutorizacaoAnvisa     = rs.GetData(3)
		        	set strGrupoParticipante     = rs.GetData(4)
		        	set strHospitalLider         = rs.GetData(5)
		        	set strApresentacao          = rs.GetData(6)
		        	set datAgendada              = rs.GetData(7)
		        	set datRealizada             = rs.GetData(8)
		        	set strStatusPreQualificacao = rs.GetData(9)
		        	set datProximaVisita         = rs.GetData(10)
		        	set blnCarta                 = rs.GetData(11)
		        	set blnRelatorio             = rs.GetData(12)
		        	set blnChecklist             = rs.GetData(13)

					set intCount = $increment(intCount)
					set strKey = idINLIEF_","_idPreQualificacao
					set strParticipanteDesc = ""
					if ($length(strGrupoParticipante) > 0) {
						set strParticipanteDesc =
							$$$VARPARAText($get(^VARPARA(YM, "GRUPOPARTICIPANTEFORNECEDOR", SPRACHE, strGrupoParticipante, 1)))
					}
					set strApresentacaoDesc = ""
					if ($length(strApresentacao) > 0) {
						set strApresentacaoDesc =
							$$$VARPARAText($get(^VARPARA(YM, "APRESENTACAOFORNECEDOR", SPRACHE, strApresentacao, 1)))
					}
					set strStatusPreQualificacaoDesc = ""
					if ($length(strStatusPreQualificacao) > 0) {
						set strStatusPreQualificacaoDesc =
							$$$VARPARAText($get(^VARPARA(YM, "STATUSPREQUALIFICFORNECEDOR", SPRACHE, strStatusPreQualificacao, 1)))
					}
					set strCartaDesc = ""
					if ($length(blnCarta) > 0) {
						set strCartaDesc =
							$$$WWW100Text($get(^WWW100(0, "JA/NEIN", SPRACHE, blnCarta, 1)))
					}
					set strRelatorioDesc = ""
					if ($length(blnRelatorio) > 0) {
						set strRelatorioDesc =
							$$$WWW100Text($get(^WWW100(0, "JA/NEIN", SPRACHE, blnRelatorio, 1)))
					}
					set strChecklistDesc = ""
					if ($length(blnChecklist) > 0) {
						set strChecklistDesc =
							$$$WWW100Text($get(^WWW100(0, "JA/NEIN", SPRACHE, blnChecklist, 1)))
					}

	    			do NewLine^COMTable()
	    			do InsertCell^COMTable(intCount,
	    				"VARFornecedorPreQualificacao", strKey)
				    do InsertCell^COMTable(strAutorizacaoAnvisa,
				    	"VARFornecedorPreQualificacao", strKey)
				    do InsertCell^COMTable(strParticipanteDesc,
				    	"VARFornecedorPreQualificacao", strKey)
				    do InsertCell^COMTable(strHospitalLider,
				    	"VARFornecedorPreQualificacao", strKey)
				    do InsertCell^COMTable(strApresentacaoDesc,
				    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
				    do InsertCell^COMTable($$^WWWDATE(datAgendada),
				    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
				    do InsertCell^COMTable($$^WWWDATE(datRealizada),
				    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
				    do InsertCell^COMTable(strStatusPreQualificacaoDesc,
				    	"VARFornecedorPreQualificacao", strKey)
				    do InsertCell^COMTable($$^WWWDATE(datProximaVisita),
				    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
	   			    do InsertCell^COMTable(strCartaDesc,
	   			    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
	   			    do InsertCell^COMTable(strRelatorioDesc,
	   			    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
	   			    do InsertCell^COMTable(strChecklistDesc,
	   			    	"VARFornecedorPreQualificacao", strKey, , "CENTER")
			        do EndLine^COMTable()		        		                	       
				}
		    }
		}

	    if (intCount = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhuma qualificação realizada para "_
				"este fornecedor.", , , , , , 12)
			do EndLine^COMTable()   
	    }

		do NewLine^COMTable("lightsteelblue")
		do InsertCell^COMTable("<strong>Cadastrar nova qualificação"_
			"</strong>", "VARFornecedorPreQualificacao", $$$KEY1(pYKEY), ,
			"RIGHT", , 12)
		do EndLine^COMTable()

	 	do Stop^COMTable()
  	}
	quit

OnAfterAutorizacaoAnvisa()
	write "<strong><font size='-1'>"_
		"<a href='http://www7.anvisa.gov.br/datavisa/Consulta_Autorizacao/consulta_autorizacao_internet.asp' target='_blank'>"_
		"AUTORIZAÇÕES ANVISA</a></font></strong>"
	quit

]]></Routine>
</Export>