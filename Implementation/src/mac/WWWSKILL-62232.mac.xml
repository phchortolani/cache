<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSKILL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSKILL(YDATEI,YKEY,YKILL,YEINMAL)
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LÖSCHEN VON DATEN UND SORTKEYS VON UNTERKNOTEN
	;		KILL DATA AND SORTKEYS INCL. SUP NOTES 
	; 
	; Usage : 
	;  D ^WWWSKILL("DATEINAME",TEILKEY)
	;     or 
	;  D ^WWWSKILL("^DATEI(0,KEY,KEYn,1)")
	;  
	;  
	;  ********
	;  JW NOTE: If the second option is used, the given company is ignored, and the CURRENT COMPANY
	;  			is processed. So if you pass in "^DATEI(2,KEY)", and current company is 5, 
	;  			"^DATEI(5,KEY)" will be processed.
	;  ********
	;
	;
	; Inputs : 
	;   YDATEI    :       DATEINAME
	;   YKEY      :       KNOTEN DIE GELÖSCHT WERDEN SOLLEN ;key to the record being deleted 
	;   YKILL     : '=""  DATENSATZ NICHT LÖSCHEN ; Data record not deleted (just clear indices?)
	;                =""  Delete data record as well (?)
	;   YEINMAL   :  =1   Once Only - Do not resubmit to DCM  FIS;31.01.05;GRF;SR11363;10.01.2005
	;   YUCI      :       Namespace
	;   
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Feb-2014	SCR		SESDF-967: Use COMIndex before the delete
	; 08-Dec-2013	SCR		SESDF-655: Use COMIndex
	; 12-Jul-2005	GRF		Doco; macros
	; 07-Jun-2005	GRF		Will have COM*.OBJ so no need to check
	; 24-MAR-2005	TYBD	undefinded error when using fast sortkey (LC and UC was missing)
	; 18.08.2000	DT
	;-------------------------------------------------------------------------------
	NEW (YM,YDATEI,YKEY,YKILL,YBED,YLOCATION,YEINMAL)
	
	DO ^WWWVORG
	SET LC="ÜÄÖüäöß][\}{|abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$CHAR(128)_$$$DBLQUOTE_$$$SPACE   ;SPEEDUP;TYBD;25.09.2004
	SET UC="UAOUAOSUAOUAOABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; Data Records used
	; YDATA           objWWW001         Data Dictionary
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
	;---------------------------------------
	; FIXME : <GRF> *IF* we have a class that has an Alternative Class Name specified
	;               as well as a translation of the class description, then attempting
	;               to delete the Alternative Class through WWWDLOE will end up processing
	;               the first Primary Class identified with the Alternative Class.
	;               This may be seriously wrong but it appears that the Alternative Class
	;               Name feature is not likely to be used.
	
	IF YDATEI'="" IF $ORDER(^WWW0011s(0,1,$$^WWWUMLAU(YDATEI),""))'="" SET YDATEI=$ORDER(^WWW0011s(0,1,$$^WWWUMLAU(YDATEI),""))  ;tybd;16,2,2005;27239;ALTERNATIVE CLASS
	;---------------------------------------
	
	IF $GET(YUCI)="" SET YUCI=$ZUTIL(5)    ;BEC;25080;09.02.04
	
	;---------------------------------------
	; Extract information from second calling method
	;      D ^WWWSKILL("^DATEI(0,KEY,KEYn,1)")
	; to build YDATEI and YKEY as if the first calling
	; method was used.
	;---------------------------------------
	IF $GET(YKEY)="" IF $FIND($GET(YDATEI),"^") DO  ;AUFBAU DES DATEINAMEN UND DES KEY ;And KEY 
	. NEW YA
	. SET YA=YDATEI
	. SET YDATEI=$PIECE($PIECE(YA,"^",2),"(",1)
	. SET YDATA=""
	. IF YDATEI'="" SET YDATA=$GET(^WWW001(0,YDATEI,1))
	. IF $PIECE(YDATA,Y,8)'=4 SET YKEY=$PIECE($PIECE(YA,"(",2),",1)",1)
	. IF $PIECE(YDATA,Y,8)=4  SET YKEY=$PIECE($PIECE(YA,"(",2),")",1) QUIT
	. SET YKEY=$PIECE(YKEY,",",2,99)
	
	;---------------------------------------
	;  D12		$$$WWW001OtherUCI()
	;  D13		$$$WWW001OtherVolume()
	;  D22		$$$WWW001SaveDataInFile()
	;  D23		$$$WWW001SaveIndexKeyInFile()
	;  
	;  $$^WWWYM returns "0," or "YM," for current YM depending on whether the class is shared
	;---------------------------------------
	IF YDATEI'="" DO
	. NEW YRET
	. DO ^WWWSORTKEY(YDATEI)
	. SET YDATA=$GET(^WWW001(0,YDATEI,1))
	. SET MAXKEY=+$ORDER(^WWW002(0,YDATEI,""),-1)
	. IF MAXKEY=0 SET MAXKEY=1
	. IF MAXKEY'=0 DO
	. . SET YFILE=YDATEI
	. . IF $PIECE(YDATA,Y,22)'="" SET YFILE  = $PIECE(YDATA,Y,22)  ;ANDERE DATEI ;data file 
	. . SET YFILES=YDATEI_"s"
	. . IF $PIECE(YDATA,Y,23)'="" SET YFILES = $PIECE(YDATA,Y,23)  ;ANDERE SORTDATEI
	. . SET YA="^"_YFILE_"("_$$^WWWYM(YDATEI,1)
	. . IF $PIECE(YDATA,Y,12)'="" IF $PIECE(YDATA,Y,13)'="" DO     ;UCI UND VOL 
	. . . SET YA="^["_$$$DBLQUOTE_$PIECE(YDATA,Y,12)_$$$DBLQUOTE_","_$$$DBLQUOTE_$PIECE(YDATA,Y,13)_$$$DBLQUOTE_"]"_$PIECE(YA,"^",2,999)
	. . ;
	. . FOR I=1:1 SET XYKEY=$TRANSLATE($PIECE(YKEY,",",I),$$$DBLQUOTE) QUIT:XYKEY=""  SET YA=YA_$$$DBLQUOTE_XYKEY_$$$DBLQUOTE SET:$PIECE(YKEY,",",I+1)'="" YA=YA_"," IF XYKEY="" SET Q=1
	. . IF $EXTRACT(YA,$LENGTH(YA))="," SET YA=$EXTRACT(YA,1,$LENGTH(YA)-1)
	. . ;SET YA(1)=YA      ;PRUEFEN ;table-mat 
	. . SET YA(1)=YA_","   ;PRUEFEN  TYBD
	. . SET YA=YA_")"
	. . SET YA(9)=YA  ;LÖSCHDATEI
	. . ;TSTART  ;TYBD;26.03.2003;23269;Not needed, because tstart in WWWSETK
	. . ;S YRET(1)="SET YRET=$SORTBEGIN(^"_YFILES_")"   X YRET(1)
	. . ;S YRET(1)="SET YRET=$SORTBEGIN(^"_YFILES_"b)"  X YRET(1)
	. . ;S YRET(1)="SET YRET=$SORTBEGIN(^"_YFILES_"bi)" X YRET(1)
	. . FOR  DO SATZ QUIT:YA=""
	. . ;S YRET(1)="SET YRET=$SORTEND(^"_YFILES_"bi)" X YRET(1)
	. . ;S YRET(1)="SET YRET=$SORTEND(^"_YFILES_"b)"  X YRET(1)
	. . ;S YRET(1)="SET YRET=$SORTEND(^"_YFILES_")"   X YRET(1)
	. . ;TCOMMIT  ;TYBD;26.03.2003;23269;Not needed, because tstart in WWWSETK
	
	QUIT
	
SATZ ;LÖSCHEN ;Delete 
	; 25-Jun-2014	shobby	CORE-385.19: DCM routines may not exist.
	new blnComIndexDone
	DO NEXT
	QUIT:YA=""
	IF '$FIND(YA,"^[") QUIT:YFILE'=$PIECE($PIECE(YA,"^",2),"(",1)
	SET YKEY=$TRANSLATE($PIECE($PIECE($PIECE(YA,"(",2,99),",",2,99),",1)",1),$$$DBLQUOTE)  ;ALLE SCHLUESSEL
	IF $PIECE(YDATA,Y,8)=4 SET YKEY=$TRANSLATE($PIECE($PIECE(YA,"(",2),")",1),$$$DBLQUOTE)
	IF $FIND(YKEY,")")     SET YKEY=$PIECE(YKEY,")",1)
	QUIT:YKEY=""
	QUIT:$FIND(YKEY,")")
	
	set blnComIndexDone=$$RemoveKeys^COMIndex(YDATEI,YKEY) ;SESDF-967

	IF $GET(YKILL)'="" SET %FELD1=$$^WWWSETL(YA)   ;HOLEN DATENSATZ   ;get data record 
	IF $GET(YKILL)=""  SET %FELD1=$$^WWWSETK(YA)   ;LÖSCHEN DATENSATZ ;Delete data record 
	
	; NOTE : Only using variable YFELD here (other routines?) but do use %FELD1
	SET YFELD=%FELD1
	
	;---------------------------------------
	;  D8		$$$WWW001AltSaveProcedure()
	;  D24		$$$WWW001BitMapIndexActive()
	;  D25		$$$WWW001BitmapXREFClass()
	;---------------------------------------
	;SORTKEYS BEARBEITEN
	IF (+$PIECE(YDATA,Y,24)=1)||($PIECE(YDATA,Y,25)'="") SET OK=$$^WWWBITSET(YDATEI,YKEY,0,%FELD1,1,,,,$PIECE(YDATA,Y,25))   ;BITMAP;18.06.2003;TYBD ;bit map 
	;QUIT:YFELD=""  ;KEIN DATENSATZ ;no data record ;TYBD;28.1,2005; IF ONLY PRIMARY KEYS
	
	IF $DATA(YSKEY)&&('blnComIndexDone) DO ;SESDF-967
	. if $$RemoveKeys^COMIndex(YDATEI,YKEY) quit
	. IF $PIECE($GET(^WWW001(0,YDATEI,1)),Y,8)=6 QUIT                ;ABACUS
	. IF $PIECE($GET(^WWW001(0,YDATEI,1)),Y,8)=7 DO ^WWWSKILL7 QUIT  ;ABACUS
	. SET SKEY=""
	. FOR  SET SKEY=$ORDER(YSKEY(SKEY)) QUIT:SKEY=""  DO
	. . NEW YQ,YMAX,YMAX1,YMAX2
	. . KILL YSFELD,YSDATEI
	. . SET YSDATEI="^"_YFILES_"("_$$^WWWYM(YDATEI,1)_SKEY
	. . IF $PIECE(YDATA,Y,12)'="" IF $PIECE(YDATA,Y,13)'="" DO  ;UCI UND VOL ;UCI And 
	. . . SET YSDATEI="^["_$$$DBLQUOTE_$PIECE(YDATA,Y,12)_$$$DBLQUOTE_","_$$$DBLQUOTE_$PIECE(YDATA,Y,13)_$$$DBLQUOTE_"]"_$PIECE(YSDATEI,"^",2,999)
	. . ;
	. . SET YMAX=1
	. . SET YSDATEI(YMAX)=YSDATEI
	. . FOR YI=1:1 SET YLFN=$PIECE(YSKEY(SKEY),",",YI) QUIT:YLFN=""  DO
	. . . IF $EXTRACT(YLFN)="F" SET YSFELD(YI)=$PIECE(%FELD1,Y,+$EXTRACT(YLFN,2,9))
	. . . IF $EXTRACT(YLFN)="K" SET YSFELD(YI)=$PIECE(YKEY,",",+$EXTRACT(YLFN,2,9))
	. . . IF YI=1 DO   ;WENN ERSTER KEY ;when first key 
	. . . . FOR  QUIT:$EXTRACT(YSFELD(YI))'=";"  SET YSFELD(YI)=$EXTRACT(YSFELD(YI),2,200)
	. . . . IF $EXTRACT($REVERSE(YSFELD(YI)))=";" FOR  QUIT:$EXTRACT($REVERSE(YSFELD(YI)))'=";"  SET YSFELD(YI)=$REVERSE($EXTRACT($REVERSE(YSFELD(YI)),2,200))
	. . . . SET YMAX=$LENGTH(YSFELD(YI),";")  ;ANZAHL DER ";" FELDER IM FELD
	. . . . FOR YMAX1=1:1:YMAX SET YSDATEI(YMAX1)=YSDATEI
	. . . ;
	. . . IF YSFELD(YI)="" SET YSFELD(YI)=" "
	. . . FOR YMAX1=1:1:YMAX DO
	. . . . SET YMAX2=YMAX1
	. . . . IF YMAX=1 SET YMAX2=200  ;ANZAHL DER ";" FELDER IN DATENFELD
	. . . . IF YI=1 DO
	. . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU($PIECE(YSFELD(YI),";",YMAX1,YMAX2),1),1,150)     ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE($PIECE(YSFELD(YI),";",YMAX1,YMAX2),LC,UC),1,150)  ;SPEED UP;TYBD;25.09.2004 ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)
	. . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU(YSFELD(YI),1),1,150)
	. . . . . IF YSFELD(YMAX1,YI)="" SET YSFELD(YMAX1,YI)=" "
	. . . . . SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_$$$DBLQUOTE_YSFELD(YMAX1,YI)_$$$DBLQUOTE
	. . . . ;
	. . . . IF YI'=1 DO  ;TYBD;NEU WEGEN FEHLER; WENN MEHRFACH UND UNTERSORT, DANN WURDE NUR DAS ERSTE FELD GENOMMEN
	. . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU(YSFELD(YI),1),1,150)     ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)  ;SPEED UP;TYBD;25.09.2004 ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)
	. . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU(YSFELD(YI),1),1,150)
	. . . . . IF YSFELD(YMAX1,YI)="" SET YSFELD(YMAX1,YI)=" "
	. . . . . SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_$$$DBLQUOTE_YSFELD(YMAX1,YI)_$$$DBLQUOTE
	. . ;
	. . FOR YMAX1=1:1:YMAX DO
	. . . SET YQ=1
	. . . FOR YI=1:1:MAXKEY SET KEY=$PIECE(YKEY,",",YI) SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_$$$DBLQUOTE_KEY_$$$DBLQUOTE IF KEY="" SET YQ=0
	. . . SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_")"
	. . . QUIT:YQ=0
	. . . ;
	. . . ;+++++++++++++++++++++++++++++++++
	. . . KILL @YSDATEI(YMAX1)  ;KILL SORTKEY
	. . . ;+++++++++++++++++++++++++++++++++
	
	; now run function that identifies what, if any, event(s) need to be added to the queue
	IF $GET(YKILL)="" DO
	. NEW OK,YDATEI1
	. QUIT:$GET(YEINMAL)=1    ;Once Only - Do not resubmit to DCM  ;GRF;SR11363;10.01.2005
	. SET YDATEI1=$PIECE($PIECE(YA,"^",2),"(",1)
	. set OK=$$$OK	;CORE-385.19
	. if ##class(%Library.Routine).Exists("COMDCMControlModule") SET OK=$$setRelevantEvents^COMDCMControlModule("INTRAPREND",YDATEI1,"delete",YKEY)  ;INTRAPREND=KENNUNG FÜR @-NET MANAGER ;to  ;CORE-385.19
	. IF '$PIECE(OK,Y,1) if $get(YBED)'="" if $data(^WWW120(0,$translate(YDATEI1,"."))) do
	. . SET ^WWWWV(YM,YBED,$PIECE($horolog,",",1),$translate(YDATEI1,"."),YKEY,1)=$PIECE($horolog,",",1)_Y_$PIECE($horolog,",",2)_Y_YBED_Y_$PIECE(OK,Y,2)
	
	QUIT
	
NEXT ;NACHSTER DATENSATZ ;data record 
	SET YA=$QUERY(@YA)
	IF '$FIND($TRANSLATE(YA,$$$DBLQUOTE),$TRANSLATE(YA(1),$$$DBLQUOTE)) SET YA=""  ;ENDE ;termination 
	QUIT
]]></Routine>
</Export>