<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARContratoVeiculo" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARContratoVeiculo
#include COMSYS
#include COMConst
 
OnBeforeFormat(pstrYFIELDNAME,pstrText,pobjLine)
 	new idField, idKey, idManutencao, idManutencaoLinha, formPai, situacao, keyAlteracao
 	
	$$$GRIDSplitKey(pstrYFIELDNAME,intRow,intCol)
	set idField = $$$GetClassField(YFORM,intCol)
 
	set idKey       = $$ReferenceKey^COMGridEdit31Interface(YFORM,intRow)	
	set idManutencao  = $$$KEY1(idKey)
	set idManutencaoLinha = $$$KEY2(idKey)
	
	new formPai
	set formPai = $get(^CacheTemp(YUSER,"Grid","Container"))
	set keyAlteracao = $get(^VARTempContratoAditivo(YBED))
	
	if formPai = "VARContrato" quit:($$GetStatusContrato^VARContrato(idManutencao) '= 1)
	if formPai = "VARContratoAditivo" quit:($$GetStatusAditivo^VARContratoAditivo($$$KEY1(keyAlteracao),$$$KEY2(keyAlteracao)) '= 1)	 
	
	set situacao = $piece($get(^VARContratoVeiculo(YM,idManutencao,idManutencaoLinha,1)),Y,3)
		
	if (situacao = 1) {
		set YBACKGROUNDCOLOR = "#92d39e"
	}
	elseif (situacao = 2) {
		set YBACKGROUNDCOLOR = "#f8d08b"	;Caso queiram usar vermelho: #e94848
	}
	
	quit
	
VerificaOrigemEquipamento()
	;-------------------------------------------------------------------------------
    ; Identifica a origem do veículo, se foi inserido no próprio contrato ou pelo 
    ; aditivo. Não deve permitir editar um equipamento inserido no contrato (a 
    ; não ser que seja um aditivo de exclusão, o que permite inativar o veículo),
    ; e nem um veículo inserido no aditivo na tela do contrato (caso este esteja
    ; sendo revisado).
    ;
    ; History:
    ; 23-Nov-2009	Gustavo Fiuza	Copied from VerificaOrigemEquipamento^VARContratoManutencao
    ;-------------------------------------------------------------------------------
	new formPai, objEquipamento, Origem
	set formPai = $get(^CacheTemp(YUSER,"Grid","Container"))
 
	set objEquipamento = $get(^VARContratoVeiculo(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1))
	
	set Origem = $piece(objEquipamento,Y,1)
	
	if ((formPai = "VARContrato") && (Origem = 2)) quit $$$YES
	if ((formPai = "VARContratoAditivo") && (Origem = 1)) quit $$$YES
	
	quit $$$NO
	
VerificaSituacao()
	;-------------------------------------------------------------------------------
    ; Verifica se o tipo é de exclusão ou inclusão e seta somente leitura com base
    ; no form atual do usuário.
    ;
    ; History:
    ; 23-Nov-2009	Gustavo Fiuza	Copied from VerificaSituacao^VARContratoManutencao
    ;-------------------------------------------------------------------------------
	if YKEY = "" quit $$$NO
		
	new formPai, objEquipamento, Origem
	set formPai = $get(^CacheTemp(YUSER,"Grid","Container"))	
	if (formPai = "VARContrato") quit $$$YES
	
	set objEquipamento = $get(^VARContratoVeiculo(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1))
	
	set Origem = $piece(objEquipamento,Y,1)
	
	if Origem = "" quit 1
	
	set keyAlteracao = $get(^VARTempContratoAditivo(YBED))	
	set tipoAditivo = $piece($get(^VARContratoAditivo(YM,$$$KEY1(keyAlteracao),$$$KEY2(keyAlteracao),1)),Y,2)
	
	if (formPai = "VARContratoAditivo") {
		if (Origem = 2) quit $$$YES
		if ((tipoAditivo = 1) || (tipoAditivo = 2)) quit $$$YES
	}
	
	quit $$$NO
	
	
OnBeforeDelete(YKEY)
	;-------------------------------------------------------------------------------
    ; Não permite que o usuário delete um veículo caso não tenha sido inserido
    ; na mesma tela e registro em que se encontra.
    ;
    ; History:
    ; 23-Nov-2009	Gustavo Fiuza	Copied from OnBeforeDelete^VARContratoManutencao
    ;-------------------------------------------------------------------------------
	
	new formPai, objEquipamento, Origem, Linha
	set formPai = $get(^CacheTemp(YUSER,"Grid","Container"))
	
	set objEquipamento = $get(^VARContratoVeiculo(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1))
 
	set Origem = $piece(objEquipamento,Y,1)
 
	if ((formPai = "VARContratoAditivo") && (Origem = 1)) {
		set Q = $$$QDontDelete
 		do ReturnError^COMUtilError("Não é possível excluir pois o item foi adicionado no contrato e é preciso manter seus registros históricos. A exclusão é feita alterando a situação do veículo para excluído e clicando em salvar.")
	
	} elseif ((formPai = "VARContrato") && (Origem = 2)) {
		set Q = $$$QDontDelete
 		do ReturnError^COMUtilError("Não é possível excluir pois o item foi adicionado em aditivo e é preciso manter seus registros históricos. Caso queira excluir o equipamento do contrato, cadastre uma alteração de termo de aditivo de exclusão, ou altere o termo de aditivo de inclusão pelo qual foi inserido o veículo.")
	}
 
	quit
]]></Routine>
</Export>