<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRMA" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRMA ;INRMA;FAN;UMBUCHUNG;04.02.05;26899  ; Compiled March 4, 2005 13:41:50
	;
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		UMBUCHUNG
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 06-Sep-2005	GRF		Doco
	; FAN	04.02.05
	;-------------------------------------------------------------------------------
	NEW BET,LAP,RMANR,RMA1
	
	SET RMANR=$GET(YKEY)			QUIT:RMANR=""
	
	SET BET=YLOCATION
	IF BET="" SET BET=$ORDER(^WWW0121(0,YM,""))
	
	;	D2		$$$INVORGBReturnStockLocation()
	SET LAP=$PIECE($GET(^INVORGB(YM,YM,BET,1)),Y,2)
	IF LAP="" DO ^WWWINFO($$^WWWFELDNAME("INVORGB","D",2)_" ?") QUIT
	
	SET RMA1=$GET(^INRMA(YM,RMANR,1))
	IF +$PIECE(RMA1,Y,23)'>0 QUIT    ; OHNE MENGE
	
	IF $PIECE(RMA1,Y,27)="" IF $PIECE(RMA1,Y,29)=1 DO    ;OHNE DATUM, UMBUCHUNG JA
	. DO UMBUCHUNG
	. QUIT
	QUIT
	
UMBUCHUNG
	NEW ART,WED,MENGE,YDATEI,YKEY,YFELD,LIEF,MENGEINHEIT
	
	;	D1		$$$INRMAOrder1()
	;	D2		$$$INRMALineItemNumber()
	;	D3		$$$INRMAReasonForRejection()
	;	D4		$$$INRMATextRejection()
	;	D10		$$$INRMAOrder2()
	;	D11		$$$INRMAPosition1()
	;	D21		$$$INRMAItemNo()
	;	D22		$$$INRMASearchName()
	;	D23		$$$INRMAQty()
	;	D25		$$$INRMAVerursacher()				<
	;	D26		$$$INRMARMANummervonLieferant()		<
	;	D27		$$$INRMAUmbuchungam()				<
	;	D29		$$$INRMAUmbuchungzumRetourLagerp()	<
	;	D30		$$$INRMALieferantbeiBestellung()	<
	;	D31		$$$INRMAMengeneinheit()				<
	
	SET LIEF=$PIECE(RMA1,Y,30)
	SET ART =$PIECE(RMA1,Y,21)
	
	IF ART="" IF $PIECE(RMA1,Y,1)'="" IF $PIECE(RMA1,Y,2)'="" DO
	. SET ART=$PIECE($GET(^INAUFP(YM,$PIECE(RMA1,Y,1),$PIECE(RMA1,Y,2),1)),Y,4)      ;ART
	. IF LIEF="" SET LIEF=$PIECE($GET(^INAUFP(YM,$PIECE(RMA1,Y,1),$PIECE(RMA1,Y,2),1)),Y,12)    ;LIEF
	. IF LIEF="" IF $piece(RMA1,Y,10)'="" IF $piece(RMA1,Y,11)'="" SET LIEF=$PIECE($GET(^INAUFP(YM,$PIECE(RMA1,Y,10),$PIECE(RMA1,Y,11),1)),Y,12)    ;LIEF VON URSPRUNGSAUFTRAG
	. QUIT
	IF ART=""  IF $PIECE(RMA1,Y,22)'="" SET ART=$PIECE(RMA1,Y,22)
	IF ART'="" IF LIEF="" SET LIEF=$ORDER(^INARTK(YM,ART,""))    ;ERST LIEFERANT
	QUIT:ART=""
	
	SET WED         = +$HOROLOG
	SET MENGE       = $PIECE(RMA1,Y,23)
	SET MENGEINHEIT = $PIECE(RMA1,Y,31)
	
	SET $PIECE(YKEY,",",1)=ART
	SET $PIECE(YKEY,",",2)=BET
	SET $PIECE(YKEY,",",3)=LAP
	SET $PIECE(YKEY,",",4)=WED
	
	;		D4			$$$INWEQuantity()
	;		D7			$$$INWESupplier()
	;		D18			$$$INWEQuantityUnit()
	;		D90			$$$INWEDateCreated()
	;		D91			$$$INWECreatedBy()
	;		D200		$$$INWEReklamationsgrund()			<
	;		D201		$$$INWEVerursacher()				<
	;		D202		$$$INWEReklamationstext()			<
	;		D203		$$$INWERMANummervonLieferanten()	<
	;		D204		$$$INWERMANummerfuerKunden()		<
	SET YFELD=""
	SET $PIECE(YFELD,Y,4)   = MENGE
	SET $PIECE(YFELD,Y,18)  = MENGEINHEIT
	SET $PIECE(YFELD,Y,7)   = LIEF
	SET $PIECE(YFELD,Y,90)  = +$HOROLOG
	SET $PIECE(YFELD,Y,91)  = YBED
	SET $PIECE(YFELD,Y,200) = $PIECE(RMA1,Y,3)   ;GRUND
	SET $PIECE(YFELD,Y,201) = $PIECE(RMA1,Y,25)   ;verursachen
	SET $PIECE(YFELD,Y,202) = $PIECE(RMA1,Y,4)   ;text
	SET $PIECE(YFELD,Y,203) = $PIECE(RMA1,Y,26)   ;RMA VON LIEF
	SET $PIECE(YFELD,Y,204) = RMANR   ;RMA
	
	SET YOK=$$^WWWSPEI("INWE",YKEY,YFELD,1)
	
	SET $PIECE(RMA1,Y,27)=+$HOROLOG
	SET YOK=$$^WWWSPEI("INRMA",RMANR,RMA1,1)
	QUIT
]]></Routine>
</Export>