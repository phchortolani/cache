<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INNETTOBRUTTO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INNETTOBRUTTO
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ERRECHNEN NETTO UND BRUTTO AUF ANFORDERUNG
	;		CALCULATE THE GROSS AND NET REQUIREMENT
	; 
	; Called By: INERECH1P1
	;            OnBlur - various fields in forms : INANGPK, INARTK, INAUFPA,
	; 	                                            INAUFPK, INERECH1
	; 
	; ByRef :
	;
	;
	; Returns : Nothing
	;
	;
	; History :
	; 25-Aug-2009	GRF		doco; Removed old commented code blocks
	; 09-Apr-2009	GRF		Boolean Macro
	; 28-Jun-2007	GRF		SR15473: missed upper limit on DP test corrected
	; 13-Mar-2007	SS		No SR, or even required changes.
	; 13-Mar-2007	JW		SR15473: Removed upper limit on DP.
	; 01-Mar-2007	RPW		SR15473: Rewrote in brace syntax.
	; 28-Sep-2006	GRF		Naked Reference; commented dev code removal; remove
	; 							extra NKOMMA test; quits; doco
	; 26-Aug-2005	GRF		Dot level realignment
	; 27-Jul-2005	RPW		SR13097: Changed the QUIT back to just a QUIT.
	; 23-May-2005	shobby	SR12008: Changed override flag to piece 87 to bring into
	; 							line INARTK/INAUFPK/INERECH1/INAUFPA/INANGPK
	; 20.01.2001	DT
	;-------------------------------------------------------------------------------
	new arrFields
	new blnHandled,blnINERECH,blnINERECH1,blnINARTK,blnINAUFPA,blnINAUFPK,blnINANGPK
	new dteToday,fldBasePurchasePrice,fldDiscount1,fldDiscount2,fldDiscount3
	new fldNetPurchasePrice,fldInvoiceAmount,fldBaseUnitPrice,fldPriceForeignCurrency,fldExchangeRate
	new fldExchangeRateDate,fltPurchaseQty,fldPurchaseQuantity
	new fldSurchargeDiscountPercent,fldSurchargeDiscountAmount,fldDealersDiscount
	
	new fltBasePrice,fltConversion,fltExchangeRate,fltInvoiceAmount,fltOriginal,fltRate
	new idConfirmation,intNKOMMA1,objOrder,strKey1,strKey2,strKey3,strPiece,objOrderLine
	new ART,AUF,EXRATE,HEADERDATE,HEADERRATE,INFOMSG,LIEF,MENGE,NKOMMA,NKOMMA1
	new OLDRATE,OVERRIDEFLAG,POS,WHR,YFELD,YI,YINHALT,YKEY

	set dteToday = +$horolog
	
	set YFELD = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1)) ;data record 
	set YKEY  = $get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"P",1)) ;keys
	set %TXT(1)=""
	
	set NKOMMA=+$piece(YFELD,Y,25)
	if NKOMMA<2 set NKOMMA=2        ; 28-Sep-2006
	//if NKOMMA>4 set NKOMMA=2		SR15473 - upper limit not required
	
	set strKey1 = $$$KEY1(YKEY)
	set strKey2 = $$$KEY2(YKEY)
	set strKey3 = $$$KEY3(YKEY)
	
	// INERECH no longer appears to be calling this routine
	set blnINERECH  = (YFORM="INERECH")      // SS  vvv
	set blnINERECH1 = (YFORM="INERECH1")
	set blnINARTK   = (YFORM="INARTK")
	set blnINAUFPA  = (YFORM="INAUFPA")
	set blnINAUFPK  = (YFORM="INAUFPK")
	set blnINANGPK  = (YFORM="INANGPK")      // SS  ^^^
	
	if $$GetFields(YFORM,.arrFields) {
		set fldDiscount1                = arrFields(1)
		set fldDiscount2                = arrFields(2)
		set fldDiscount3                = arrFields(3)
		set fldSurchargeDiscountPercent = arrFields(4)
		set fldSurchargeDiscountAmount  = arrFields(5)
		set fldDealersDiscount          = arrFields(6)
		set fldBasePurchasePrice        = arrFields(7)
		set fldNetPurchasePrice         = arrFields(8)
		set fldPurchaseQuantity         = arrFields(9)
		set fldInvoiceAmount            = arrFields(10)
		set fldBaseUnitPrice            = arrFields(11)
		set fldPriceForeignCurrency     = arrFields(12)
		set fldExchangeRate             = arrFields(13)
		set fldExchangeRateDate         = arrFields(14)
	} else {
		quit                                               ; *** EARLY EXIT ***
	}
	
	if (strKey1 '= "") && (strKey2 '= "") {
		set objOrderLine = $get(^INAUFP(YM,strKey1,strKey2,1))
	}
	
	if blnINERECH1 || blnINERECH {
		set NKOMMA = 2
		if blnINERECH1 {
			set AUF  = strKey1
			set POS  = strKey2
			set LIEF = strKey3
			if (AUF'="") && (POS'="") && (LIEF'="") {
				set ART     = $$$INAUFPItemNumber(objOrderLine)
				set NKOMMA1 = $$$INARTKDecimalPlaces($get(^INARTK(YM,ART,LIEF,1)))
				set idConfirmation=$order(^INAUFPA(YM,AUF,POS,LIEF,""))
				if idConfirmation'="" {
					set intNKOMMA1 = $$$INAUFPAAfterCommaPlace($get(^INAUFPA(YM,AUF,POS,LIEF,idConfirmation,1)))
					if (+intNKOMMA1'=0) {
						set NKOMMA1 = intNKOMMA1    ; 28-Sep-2006
					}
				}
				
				if +NKOMMA1'=0 set NKOMMA=NKOMMA1
	;			set:((NKOMMA>4) || (NKOMMA<2)) NKOMMA=2   ; SR15473  28-Jun-2007
				set:NKOMMA<2 NKOMMA=2
			}
		}
	}
	
	if +$piece(YFELD,Y,fldPriceForeignCurrency)'=0 {
		if blnINARTK                                    set LIEF = strKey2
		if blnINAUFPA || blnINERECH1                    set LIEF = strKey3
		if blnINAUFPK && (strKey1'="") && (strKey2'="") set LIEF = $$$INAUFPSupplier(objOrderLine)
		if blnINANGPK && (strKey1'="") && (strKey2'="") set LIEF = $$$INANGPSupplier($get(^INANGP(YM,strKey1,strKey2,1)))
		
		if $get(LIEF)'="" {
			set WHR=$$$INLIEFCurrency($get(^INLIEF(YM,LIEF,1)))
			if (WHR="") || (WHR=YWHR) {
				for YI=fldExchangeRate,fldExchangeRateDate,fldPriceForeignCurrency set $piece(YFELD,Y,YI)=""  ;KEINE FREMDWÃ„HRUNG
			
			} else {
				set EXRATE = $$FindSupplierConversionRate^INAUFDISC(LIEF)
				if blnINAUFPK {
					set AUF      = strKey1
					set objOrder = $get(^INAUF(YM,AUF,1))
					set OLDRATE      = $$$INAUFPKExchangeRate($get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2)))
					set HEADERRATE   = $$$INAUFExchangeRate(objOrder)
					set OVERRIDEFLAG = $$$INAUFExchangeRateOverride(objOrder)
					set HEADERDATE   = $$$INAUFLastAlteration(objOrder)
					
					set fltExchangeRate = $$$INAUFPKExchangeRate(YFELD)
					
					if (+fltExchangeRate'=0) && (fltExchangeRate'=HEADERRATE) {
						set EXRATE = $$$INAUFPKExchangeRate(YFELD)
						set $$$INAUFPKExchangeRateOverride(YFELD)      = $$$YES
						set $$$INAUFPKExchangeRateEffectivityDa(YFELD) = dteToday
					}
					if (+fltExchangeRate=0) || (fltExchangeRate=HEADERRATE) {
						set $$$INAUFPKExchangeRate(YFELD)              = HEADERRATE  ;SAVE X-RATE
						set $$$INAUFPKExchangeRateOverride(YFELD)      = OVERRIDEFLAG
						set $$$INAUFPKExchangeRateEffectivityDa(YFELD) = HEADERDATE
						set EXRATE=HEADERRATE
					}
					set $$$INAUFPKUnitPrice(YFELD) = $justify($$$INAUFPKCostFC(YFELD)*EXRATE,0,NKOMMA)
				}
				
				if blnINANGPK || blnINAUFPA || blnINERECH1 {              ;MANUEL EXCHANGE RATE
					set OLDRATE = $piece($get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2)),Y,fldExchangeRate)
					if +$piece(YFELD,Y,fldExchangeRate)'=0 {
						set EXRATE=$piece(YFELD,Y,fldExchangeRate)
					} else {
						set $piece(YFELD,Y,fldExchangeRate)=EXRATE        ;SAVE X-RATE
					}
					if $piece(YFELD,Y,fldExchangeRate) '= OLDRATE {
						set $piece(YFELD,Y,fldExchangeRateDate)=dteToday  ;SAVE DATE
					}
					set $piece(YFELD,Y,fldBaseUnitPrice)=$justify($piece(YFELD,Y,fldPriceForeignCurrency)*EXRATE,0,NKOMMA)  ;UNIT PRICE
				}
				if blnINARTK {
					set OLDRATE = $$$INARTKExchangeRate($get(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",2)))
					if +$$$INARTKExchangeRate(YFELD)'=0 {
						set EXRATE=$$$INARTKExchangeRate(YFELD)
					} else {
						set $$$INARTKExchangeRate(YFELD)=EXRATE           ;SAVE X-RATE
					}
					set $$$INARTKBasicPurchasePrice(YFELD)=$justify($$$INARTKBasePriceCurrency(YFELD)*EXRATE,0,NKOMMA)
				}
			}
		}
	}
 
	if 'blnINARTK && (+$piece(YFELD,Y,fldBaseUnitPrice)'=0) {
		if (strKey1'="") && (strKey2'="") {
			set MENGE=$$$INAUFPQuantity(objOrderLine)
			if (blnINERECH1 || blnINAUFPA || blnINAUFPK || blnINANGPK) &&
			   (+$piece(YFELD,Y,fldPurchaseQuantity)'=+MENGE)             {
				
				set MENGE = $piece(YFELD,Y,fldPurchaseQuantity)  ;AB ODER RCHNUNGSEINGANGSMENGE ;Confirm. Or 
			}
			if MENGE="" set MENGE = $$$INANGPQuantity($get(^INANGP(YM,strKey1,strKey2,1)))
			if +MENGE'=0 {
				set $piece(YFELD,Y,fldBasePurchasePrice) = $piece(YFELD,Y,fldBaseUnitPrice)*MENGE/$$^INQTYUNIT(,strKey1,strKey2)
			} else {
				set $piece(YFELD,Y,fldBasePurchasePrice) = $piece(YFELD,Y,fldBaseUnitPrice)
			}
		}
	}
	
	set blnHandled   = $$$NO
	set fltBasePrice = $piece(YFELD,Y,fldBasePurchasePrice)
	
	if +fltBasePrice'=0 {
		set fltOriginal=$$^INNETTO(YFELD)
		set $piece(YFELD,Y,fldNetPurchasePrice)=fltOriginal
		set $piece(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1),Y,fldNetPurchasePrice)=fltOriginal
		set fltRate=$piece(YFELD,Y,fldExchangeRate)
		if +fltRate=0 set fltRate=1
		if blnINARTK {
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"M88~"_$$^WWWZAHL(fltOriginal/fltRate,0,NKOMMA))
		}
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldNetPurchasePrice_"~"_$$^WWWZAHL(fltOriginal,0,2))
		if blnINARTK || blnINAUFPK || blnINANGPK || blnINAUFPA || blnINERECH1 { // ** Combine ? Need to determine if events between prevent combining events
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldPriceForeignCurrency_"~"_$$^WWWZAHL($piece(YFELD,Y,fldPriceForeignCurrency),0,NKOMMA))     ; 28-Sep-2006 split line
		}
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldBasePurchasePrice_"~"_$$^WWWZAHL(fltBasePrice,0,NKOMMA))
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldBaseUnitPrice_"~"_$$^WWWZAHL($piece(YFELD,Y,fldBaseUnitPrice),0,NKOMMA))
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldSurchargeDiscountAmount_"~"_$$^WWWZAHL($piece(YFELD,Y,fldSurchargeDiscountAmount),0,2))
		
		if blnINARTK || blnINAUFPK || blnINANGPK || blnINAUFPA || blnINERECH1 { // ** Combine ?		//SR13074
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldExchangeRate_"~"_$$^WWWTR(0,18,$piece(YFELD,Y,fldExchangeRate)))
		}
		if blnINAUFPK || blnINAUFPA || blnINERECH1 {
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldExchangeRateDate_"~"_$$^WWWDATE($piece(YFELD,Y,fldExchangeRateDate)))
		}
		
		for YI=fldDiscount1,fldDiscount2,fldDiscount3,fldSurchargeDiscountPercent,fldDealersDiscount {
			if $piece(YFELD,Y,YI)>100 set $piece(YFELD,Y,YI)=100
			set strPiece=$piece(YFELD,Y,YI)
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_YI_"~"_$$^WWWZAHL(strPiece,0,$length($piece(strPiece,".",2))))
		}
		
		set blnHandled=$$$YES
		
	} elseif +$piece(YFELD,Y,fldNetPurchasePrice)'=0 {
		set fltOriginal=$$^INBRUTTO(YFELD)   ;RECHEN BRUTTO AUS NETTO FELD ;gr. out of field 
		set $piece(YFELD,Y,fldBasePurchasePrice)=fltOriginal
		set $piece(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1),Y,fldBasePurchasePrice)=fltOriginal
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldNetPurchasePrice_"~"_$$^WWWZAHL($piece(YFELD,Y,fldNetPurchasePrice),0,2))
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldBasePurchasePrice_"~"_$$^WWWZAHL($piece(YFELD,Y,fldBasePurchasePrice),0,NKOMMA))
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldBaseUnitPrice_"~"_$$^WWWZAHL($piece(YFELD,Y,fldBaseUnitPrice),0,NKOMMA))
		set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldSurchargeDiscountAmount_"~"_$$^WWWZAHL($piece(YFELD,Y,fldSurchargeDiscountAmount),0,2))
		
		if blnINARTK || blnINAUFPK || blnINANGPK || blnINAUFPA || blnINERECH1 {
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldExchangeRate_"~"_$$^WWWTR(0,18,$piece(YFELD,Y,fldExchangeRate)))		//13074   ; 28-Sep-2006 split line
		}
		if blnINAUFPK || blnINAUFPA || blnINERECH1 {
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldExchangeRateDate_"~"_$$^WWWDATE($piece(YFELD,Y,fldExchangeRateDate)))
		}
 
		for YI=fldDiscount1,fldDiscount2,fldDiscount3,fldSurchargeDiscountPercent,fldDealersDiscount {
			if $piece(YFELD,Y,YI)>100 set $piece(YFELD,Y,YI)=100
			set strPiece=$piece(YFELD,Y,YI)
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_YI_"~"_$$^WWWZAHL(strPiece,0,$length($piece(strPiece,".",2))))
		}
		
		set blnHandled=$$$YES
	}
	
	if 'blnHandled {
		if blnINAUFPK {
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_$$$FldINAUFPKExchangeRateOverride_"~"_$select($$$INAUFPKExchangeRateOverride(YFELD):"CHECKED",1:"UNCHECKED")) ;shobby SR12008
			
		} elseif blnINERECH1 || blnINERECH || blnINAUFPA {
			set fltInvoiceAmount=$$^INNETTO(YFELD,1)  ;OHNE MWST ;without Tax 
			set $piece(YFELD,Y,fldInvoiceAmount)=fltInvoiceAmount
			set $piece(^WWWDATEN(YM,dteToday,YUSER,YFORM,"D",1),Y,fldInvoiceAmount)=fltInvoiceAmount
			set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_fldInvoiceAmount_"~"_$$^WWWZAHL(fltInvoiceAmount,0,2))
		}
		
		if blnINERECH1 {
			if $$$INERECH1ServiceCostsInvoice(YFELD)="" {
				set YINHALT = ""
				set INFOMSG = ""
				do ^INERECH1D13
				if YINHALT'="" {
					set %TXT(1)=%TXT(1)_$$$Perform_$$$EscapeHyperEventData("Y"_YFORM_"D"_$$$FldINERECH1TransferedUnitPrice_"~"_$$^WWWZAHL(YINHALT,0,2))
				} else {
					if ($piece(YFELD,Y,73)'="") && ($piece(YFELD,Y,72)'=1) {
						set INFOMSG=INFOMSG_$$^WWWTEXT(33929,,1)
					} ; "Attention! Cost Price Is Already In Inventory"
				}
				
				if (strKey1'="") && (strKey2'="") && ((YLFN=fldPurchaseQuantity)||(YLFN=fldBaseUnitPrice)||(YLFN=fldBasePurchasePrice)) {
					set fltConversion=$$$INAUFPKConversionFactorQuantity($get(^INAUFPK(YM,strKey1,strKey2,1)))
					// NEW
					if +fltConversion=0 {
						set fltPurchaseQty = $$$INERECH1InvoiceQuantity(YFELD)
					} else {
						set fltPurchaseQty = $$$INERECH1InvoiceQuantity(YFELD)/fltConversion
					}
					if $justify(fltPurchaseQty,0,2)'=$justify($$$INAUFPQuantity(objOrderLine),0,2) {
						set:INFOMSG'="" INFOMSG=INFOMSG_"'"
						set INFOMSG=INFOMSG_$$^WWWTEXT(33930,,1)
					} ; "Attention! The Quantity Is Wrong."
				}
				if INFOMSG'="" set %TXT(1)=%TXT(1)_$$$Perform_$$$AlertFollowing_$$$EscapeHyperEventData(INFOMSG)
			}
			
		} elseif blnINAUFPA {
			if (strKey1'="") && (strKey2'="") && ((YLFN=fldPurchaseQuantity) || (YLFN=fldBaseUnitPrice) || (YLFN=fldBasePurchasePrice)) {
				set fltConversion=$$$INAUFPKConversionFactorQuantity($get(^INAUFPK(YM,strKey1,strKey2,1)))
				if +fltConversion=0 {
					set fltPurchaseQty = $$$INAUFPAOrderConfirmationQuantity(YFELD)
				} else {
					set fltPurchaseQty = $$$INAUFPAOrderConfirmationQuantity(YFELD)/fltConversion
				}
				if $justify(fltPurchaseQty,0,2)'=$justify($$$INAUFPQuantity(objOrderLine),0,2) {
					set %TXT(1)=%TXT(1)_$$$Perform_$$$AlertFollowing_$$$EscapeHyperEventData($$^WWWTEXT(33958,,1))
				} ; "Attention! Quantity Does Not Match Order Quantity. Please Check Calculation Factor!"
			}
		}
	}
	
	quit
 
GetFields(pidForm,&parrFields)
	;-------------------------------------------------------------------------------
	; Return array of field ids for the current form
	;
	; Params:
	; pidForm - Form Id
	; parrFields - Array of Field Ids
	;
	; Returns: blnFound - $$$YES if successful, otherwise $$$NO
	;
	; History:
	; 20-Mar-2007	RPW		SR15473: Boolean is $$$YES or $$$NO, not $$$OK and $$$NO
	; 							renamed count to intCount. Fixed ""'s as the
	; 							original code is setting pieces that don't exist.
	; 16-Mar-2007	PO		SR15473: Created
	;-------------------------------------------------------------------------------
	new blnFound,intCount
 
	set blnFound = $$$YES
	set intCount = 0
	
	;+++++++++++++++++++++++++++++++++++++++
	;  NOTE :
	;  All field numbers specified below are
	;  the same for each class.  (1:1 equivalence
	;  between form names and classes in these
	;  cases)
	;  Some classes do not use the fields in
	;  question.
	;+++++++++++++++++++++++++++++++++++++++
	
	if pidForm = "INERECH" {
		set parrFields($increment(intCount)) = 2 // vvv This exists to replicate what the old code performed
		set parrFields($increment(intCount)) = 3 // Some of the field ids exist on INERECH but none of them appear to logically relate
		set parrFields($increment(intCount)) = 4
		set parrFields($increment(intCount)) = 5 // INERECH never appears to actually call INNETTOBRUTTO, so this should be able to be dropped
		set parrFields($increment(intCount)) = 6
		set parrFields($increment(intCount)) = 7
		set parrFields($increment(intCount)) = 12
		set parrFields($increment(intCount)) = 13
		set parrFields($increment(intCount)) = 16
		set parrFields($increment(intCount)) = 22
		set parrFields($increment(intCount)) = 47
		set parrFields($increment(intCount)) = 68
		set parrFields($increment(intCount)) = 78
		set parrFields($increment(intCount)) = 79 // ^^^ 
		
	} elseif pidForm = "INERECH1" {
		set parrFields($increment(intCount)) = $$$FldINERECH1Discount1
		set parrFields($increment(intCount)) = $$$FldINERECH1Discount2
		set parrFields($increment(intCount)) = $$$FldINERECH1Discount3
		set parrFields($increment(intCount)) = $$$FldINERECH1AdditionAnticipatedPaymen
		set parrFields($increment(intCount)) = $$$FldINERECH1AdditionAnticipatedPaymen1
		set parrFields($increment(intCount)) = $$$FldINERECH1DiscountPayment
		set parrFields($increment(intCount)) = $$$FldINERECH1BasePurchasePrice
		set parrFields($increment(intCount)) = $$$FldINERECH1NetPurchasePrice
		set parrFields($increment(intCount)) = $$$FldINERECH1InvoiceQuantity
		set parrFields($increment(intCount)) = $$$FldINERECH1InvoiceAmount
		set parrFields($increment(intCount)) = $$$FldINERECH1UnitBasePrice
		set parrFields($increment(intCount)) = $$$FldINERECH1PriceForeignCurrency
		set parrFields($increment(intCount)) = $$$FldINERECH1ExchangeRate
		set parrFields($increment(intCount)) = $$$FldINERECH1ExchangeRateDate
		
	} elseif pidForm = "INARTK" {
		set parrFields($increment(intCount)) = $$$FldINARTKDiscount1
		set parrFields($increment(intCount)) = $$$FldINARTKDiscount2
		set parrFields($increment(intCount)) = $$$FldINARTKDiscount3
		set parrFields($increment(intCount)) = $$$FldINARTKSurchargeAllowance
		set parrFields($increment(intCount)) = $$$FldINARTKSurchargeAllowanceAmount
		set parrFields($increment(intCount)) = $$$FldINARTKDealersDiscount
		set parrFields($increment(intCount)) = $$$FldINARTKBasicPurchasePrice
		set parrFields($increment(intCount)) = $$$FldINARTKNetPurchasePrice
		set parrFields($increment(intCount)) = 16
		set parrFields($increment(intCount)) = $$$FldINARTKInvoiceTotal
		set parrFields($increment(intCount)) = $$$FldINARTKUnitPrice
		set parrFields($increment(intCount)) = $$$FldINARTKBasePriceCurrency
		set parrFields($increment(intCount)) = $$$FldINARTKExchangeRate
		set parrFields($increment(intCount)) = 79
		
	} elseif pidForm = "INAUFPA" {
		set parrFields($increment(intCount)) = $$$FldINAUFPADiscount1
		set parrFields($increment(intCount)) = $$$FldINAUFPADiscount2
		set parrFields($increment(intCount)) = $$$FldINAUFPADiscount3
		set parrFields($increment(intCount)) = $$$FldINAUFPASurchargeDiscount
		set parrFields($increment(intCount)) = $$$FldINAUFPASurchargeDiscountAmount
		set parrFields($increment(intCount)) = $$$FldINAUFPADealersRebate
		set parrFields($increment(intCount)) = $$$FldINAUFPABasePrice
		set parrFields($increment(intCount)) = $$$FldINAUFPANetOriginalPrice
		set parrFields($increment(intCount)) = $$$FldINAUFPAOrderConfirmationQuantity
		set parrFields($increment(intCount)) = $$$FldINAUFPAInvoiceAmount
		set parrFields($increment(intCount)) = $$$FldINAUFPAUnitBasePrice
		set parrFields($increment(intCount)) = $$$FldINAUFPAPriceForeignCurrency
		set parrFields($increment(intCount)) = $$$FldINAUFPAExchangeRate
		set parrFields($increment(intCount)) = $$$FldINAUFPAExchangeRateEffektivityDa
		
	} elseif pidForm = "INAUFPK" {
		set parrFields($increment(intCount)) = $$$FldINAUFPKDiscount1
		set parrFields($increment(intCount)) = $$$FldINAUFPKDiscount2
		set parrFields($increment(intCount)) = $$$FldINAUFPKDiscount3
		set parrFields($increment(intCount)) = $$$FldINAUFPKSurchargeAllowance
		set parrFields($increment(intCount)) = $$$FldINAUFPKSurchargeAllowanceAmount
		set parrFields($increment(intCount)) = $$$FldINAUFPKDealersRebate
		set parrFields($increment(intCount)) = $$$FldINAUFPKBasisOriginalPrice
		set parrFields($increment(intCount)) = $$$FldINAUFPKNetOriginalPrice
		set parrFields($increment(intCount)) = $$$FldINAUFPKPurchaseQuantity
		set parrFields($increment(intCount)) = $$$FldINAUFPKAmountInvoiced
		set parrFields($increment(intCount)) = $$$FldINAUFPKUnitPrice
		set parrFields($increment(intCount)) = $$$FldINAUFPKCostFC
		set parrFields($increment(intCount)) = $$$FldINAUFPKExchangeRate
		set parrFields($increment(intCount)) = $$$FldINAUFPKExchangeRateEffectivityDa
		
	} elseif pidForm = "INANGPK" {
		set parrFields($increment(intCount)) = $$$FldINANGPKDiscount1
		set parrFields($increment(intCount)) = $$$FldINANGPKDiscount2
		set parrFields($increment(intCount)) = $$$FldINANGPKDiscount3
		set parrFields($increment(intCount)) = $$$FldINANGPKSurchargeDiscount
		set parrFields($increment(intCount)) = $$$FldINANGPKDiscountAmount
		set parrFields($increment(intCount)) = $$$FldINANGPKRebate
		set parrFields($increment(intCount)) = $$$FldINANGPKPurchaseBasePrice
		set parrFields($increment(intCount)) = $$$FldINANGPKPurchaseNetPrice
		set parrFields($increment(intCount)) = 16 // Even though this field does not exist prior code was written with the potential to use the undefined field.
		set parrFields($increment(intCount)) = $$$FldINANGPKAmountInvoiced
		set parrFields($increment(intCount)) = $$$FldINANGPKUnitBasePrice
		set parrFields($increment(intCount)) = $$$FldINANGPKPriceForeignCurrency
		set parrFields($increment(intCount)) = $$$FldINANGPKExchangeRate
		set parrFields($increment(intCount)) = 79 // Even though this field does not exist prior code was written with the potential to use the undefined field.
		
	} else {
		// Inappropriate form calling this routine
		set blnFound = $$$NO
	}
 
	quit blnFound
	
	
]]></Routine>
</Export>