<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INTAX" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INTAX
#include INConst
#include COMSYS
 
INDRUCKTAX(YBELEG="",POS1="",YEKK="",YFELDPZ="",YKUNDE="",YLIEFER="",MWPO,TAXLOC)
    ;-------------------------------------------------------------------------------
    ; Get tax rate, tax code and tax loc.
    ;
    ; Params:	YBELEG	- ?? Type ??
    ; 			POS1	- Order Line Record (INAUFP)
    ; 			YEKK	- Supplier Terms Record (INANGPK or INAUFPK)
    ; 			YFELDPZ	- Payment Agreement Record (INAUFPZ)
	;			YKUNDE	- Customer Id
	;			YLIEFER	- Supplier Id
	;			MWPO	- (By Ref) Tax Code - return array structure
	;			TAXLOC	- (By Ref) Tax Location
    ;
    ; Returns:	MWST	- Tax Rate
    ;
    ; History:
    ; 17-Aug-2005	JW		Moved from INDRUCKTAX
    ; 27-Jul-2005	JW		SR12992: Created
    ;-------------------------------------------------------------------------------
	new taxIdentifier,objINKUNDE,objINLIEF,MWST
	
	IF YBELEG=12 && ($PIECE(YFELDPZ,Y,21)'="") {
		SET MWPO=$PIECE(YFELDPZ,Y,21)  ;FIS;11.11.03;22349;STEUERSATZ TEILZAHLUNGSRECHNUNG
		
	} ELSEIF (YBELEG=3)||(YBELEG=10)||(YBELEG=13) && ($$$INAUFPKInputVATPurchase(YEKK)'="") {
		SET MWPO=$$$INAUFPKInputVATPurchase(YEKK)	; Supplier Terms
		
	} ELSE {
		SET MWPO=$$$INAUFPSalesTax(POS1)  ;Order Line Tax
	}
	
	set MWST = $$GetTaxRate(YKUNDE,YLIEFER,.MWPO,.TAXLOC)
	
	/*
	SET TAXLOC=""
	set taxIdentifier=""
	
	IF YKUNDE'="" {
		set objINKUNDE = $GET(^INKUNDE(YM,YKUNDE,1))
		set TAXLOC=$$$INKUNDETaxLocation(objINKUNDE)
		set taxIdentifier = $$$INKUNDEControlIdentifiers(objINKUNDE)
		
	} elseif YLIEFER'="" {
		set objINLIEF = $GET(^INLIEF(YM,YLIEFER,1))
		set TAXLOC=$$$INLIEFTaxLocation(objINLIEF)
		set taxIdentifier = $$$INLIEFTaxIdentifiers(objINLIEF)
	}
	
	IF TAXLOC="" {
		SET TAXLOC=$$CurrentTaxLocation^COMTAXLocation()
	}
	
	set MWPO = $$IsTaxable^COMTAX(MWPO,taxIdentifier,,TAXLOC)
	set MWST = $$CombinedTaxRate^COMTAX(MWPO,TAXLOC,+$HOROLOG,YKUNDE="")  ;COMTAX(TaxCode,TaxLocation,TransactionDate,blnBuy)
	*/
	
	quit MWST
	
 
GetTaxRatePO(pidSupplier="",pidTaxCode,pidTaxLoc,pdteTrans="")
	quit $$GetTaxRate("",pidSupplier,.pidTaxCode,.pidTaxLoc,pdteTrans)
 
GetTaxRateCO(pidCustomer="",pidTaxCode,pidTaxLoc,pdteTrans="")
	quit $$GetTaxRate(pidCustomer,"",.pidTaxCode,.pidTaxLoc,pdteTrans)
	
	
GetTaxRate(pidCustomer="",pidSupplier="",pidTaxCode="",pidTaxLoc,pdteTrans="")
    ;-------------------------------------------------------------------------------
    ; Method Usage
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Aug-2005	JW		SR12141: Created
    ;-------------------------------------------------------------------------------
	new taxIdentifier,taxRate,objINLIEF,objINKUNDE
	
	set pidTaxLoc=""
	set taxIdentifier=""
	
	if pidCustomer'="" {
		set objINKUNDE = $get(^INKUNDE(YM,pidCustomer,1))
		set pidTaxLoc=$$$INKUNDETaxLocation(objINKUNDE)
		set taxIdentifier = $$$INKUNDEControlIdentifiers(objINKUNDE)
		
	} elseif pidSupplier'="" {
		set objINLIEF = $GET(^INLIEF(YM,pidSupplier,1))
		set pidTaxLoc=$$$INLIEFTaxLocation(objINLIEF)
		set taxIdentifier = $$$INLIEFTaxIdentifiers(objINLIEF)
	}
	
	if pidTaxLoc="" {
		set pidTaxLoc=$$CurrentTaxLocation^COMTAXLocation()
	}
	
	set pidTaxCode = $$IsTaxable^COMTAX(pidTaxCode,taxIdentifier,,pidTaxLoc)
	set taxRate = $$CombinedTaxRate^COMTAX(pidTaxCode,pidTaxLoc,pdteTrans,pidCustomer="")  ;COMTAX(TaxCode,TaxLocation,TransactionDate,blnBuy)
		
	quit taxRate
	
	
	
OrderLineTaxRate(pidOrder,pidLine)
    ;-------------------------------------------------------------------------------
    ; Basically the same as INDRUCKTAX, but more structured
    ; 
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
    ; 17-Aug-2005	JW		SR12141: Created
    ;-------------------------------------------------------------------------------
	new taxRate,idTaxCode,objOrder
	
	set taxRate = ""
		
	if ($get(pidOrder)'="") && ($get(pidLine)'="") {
		
		set idTaxCode=""
		
		if $data(^INAUFPK(YM,pidOrder,pidLine,1)) {
			set idTaxCode = $$$INAUFPKInputVATPurchase($get(^INAUFPK(YM,pidOrder,pidLine,1)))
		}
		if idTaxCode="" {
			set idTaxCode = $$$INAUFPSalesTax($get(^INAUFP(YM,pidOrder,pidLine,1)))
		}
		
		set objOrder = $get(^INAUF(YM,pidOrder,1))
		set taxRate = $$GetTaxRate($$$INAUFCustomerNumber(objOrder),$$$INAUFSupplierNumber(objOrder),.idTaxCode) 
	}
	
	quit taxRate
	
	
]]></Routine>
</Export>