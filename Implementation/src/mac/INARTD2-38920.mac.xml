<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTD2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTD2
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PRÃœFEN NACH DOPPELTEN EANNR.
	;		INART Form Field D2 "UPC Code" - Execute On Blur
	;		
	; Inputs : 
	;
	;
	; ByRef :
	;	YFORM
	;	YUSER
	;	YINHALT		Data entered into field
	;
	; Returns :
	;
	;
	; History :
	; 20-Jun-2007	GRF		SRBR014477: Revise with documentation & clearer variables;
	; 						braces; don't generate a next number if we aren't going
	; 						to use it; assume %TXT(1) contains null or valuable details.
	; 01-Jun-2006	GRF		Dot level
	; 28,01,2003	TYBD	Created
	;-------------------------------------------------------------------------------
	new idEDIFACT,idItem,idxUPCCode,strProdCode,YFELD1,YKEY
	
	set YKEY    = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"P",1))
	set YFELD1  = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"D",2))
	set YINHALT = $get(YINHALT)
	
	;+++++++++++++++++++++++++++++++++++++++
	; Generate a new UPCCode by combining the
	; entity number with the next product number
	; and appending a checkdigit
	; 
	;    eeeeeeppppppc   (13 digits)
	; 
	; If the product already had a UPC Code, it is
	; necessary to delete it and then replace it.
	;+++++++++++++++++++++++++++++++++++++++
	
	if YINHALT="+" {
		if $piece(YFELD1,Y,2)'="" {
	;		Redisplay the prior value
	;		"Has the UPC code really changed? Please delete the UPC code and then assign again."
			set YINHALT = $piece(YFELD1,Y,2)
			set %TXT(1) = %TXT(1)_"#Y"_YFORM_"D2~"_YINHALT_"#!"_$$^WWWTEXT(33845,,1)
		
		} else {
	;		Generate and then display the newly generated value
			set idEDIFACT   = $extract($$$INVORGEDIFACTTransmitterNo($get(^INVORG(YM,YM,1))),1,6)
			set strProdCode = $extract(1000000+$$^WWWNEXT("UPC"),2,7)
			set YINHALT     = $extract(1000000000000+(idEDIFACT_strProdCode),2,13)
			set YINHALT     = YINHALT_$$^WWWCRC(YINHALT)
		
			set %TXT(1) = %TXT(1)_"#Y"_YFORM_"D2~"_YINHALT
		}
	}
	
	;+++++++++++++++++++++++++++++++++++++++
	; Check to see if the product code is also used by any other products.
	;+++++++++++++++++++++++++++++++++++++++
	
	if (YINHALT'="") && (YKEY'="") {
		set idxUPCCode = $$$Index(YINHALT)
		set idItem=""
		for  {
			set idItem=$ORDER(^INARTs(YM,3,idxUPCCode,idItem))
			quit:idItem=""
			
			if idItem'=YKEY set %TXT(1) = %TXT(1)_"#!"_$$^WWWTEXT(33739)_" "_idItem   ; "This Is Redundant:"
		}
	}
	
	quit
	
	/*   replaces SRBR014477
	NEW YKEY,YFELD1
	
	SET YKEY   = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1))
	SET YFELD1 = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",2))
	
	IF $GET(YINHALT)="+" DO
	. NEW NUM
	. SET NUM=$$^WWWNEXT("UPC")
	. SET YINHALT=$EXTRACT(1000000000000+($EXTRACT($PIECE($GET(^INVORG(YM,YM,1)),Y,101),1,6)_$EXTRACT(1000000+NUM,2,7)),2,13)
	. SET YINHALT=YINHALT_$$^WWWCRC(YINHALT)
	. ;IF $PIECE(YFELD1,Y,2)'="" SET %TXT(1)="#Y"_YFORM_"D2~"_"#&"_$$^WWWTEXT(33845,,1)_" ("_$P(YFELD1,Y,2)_")"
	. ;SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D2~"_YINHALT_"~~~"_$PIECE(YFELD1,Y,2)
	. ;
	. IF $PIECE(YFELD1,Y,2)'="" SET YINHALT=$PIECE(YFELD1,Y,2) SET %TXT(1)="#Y"_YFORM_"D2~"_YINHALT_"#!"_$$^WWWTEXT(33845,,1) QUIT
	. SET %TXT(1)=%TXT(1)_"#Y"_YFORM_"D2~"_YINHALT
	
	IF $GET(YINHALT)'="" IF $GET(YKEY)'="" DO
	. NEW YI
	. SET YI=""
	. FOR  SET YI=$ORDER(^INARTs(YM,3,$$^WWWUMLAU(YINHALT,1),YI)) QUIT:YI=""  DO
	. . QUIT:YI=YKEY
	. . SET %TXT(1)="!"_$$^WWWTEXT(33739)_" "_YI
	
	QUIT	*/
]]></Routine>
</Export>