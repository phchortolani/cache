<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINARHCAdjustmentsToBatch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINARHCAdjustmentsToBatch
#include FINHC


Check(StartDate="",EndDate="",Location="",Level="",Result)
    ;-------------------------------------------------------------------------------
    ; All AR Posted Adjustments must update a Posted Interface Batch
    ;
    ; Notes:
    ; 	Similar code in FINAPHCAdjustmentsToBatch, check this routine when making changes.
    ;
    ; Returns:
    ;
    ; History:
    ; 26-May-2004	shobby		Changed to work off the third index of FININTBatchLines.
    ; 21-Apr-2004   Shobby   	Created (HCAP7) Copied from FINAPHC.
    ;-------------------------------------------------------------------------------
	;
	new strStatus,idFINARADJ,idNumber,objFINARADJ,idBatch,objFININTBatch,idxFININTBatchLineNumber
	new idLine,objFININTBatchLine,strBlock
	
	kill Result
	set strStatus=$$$OK

	set idFINARADJ=""
	for {
		set idFINARADJ=$order(^FINARADJ(YM,idFINARADJ))
		quit:idFINARADJ=""
		set objFINARADJ=$get(^FINARADJ(YM,idFINARADJ,1))
		if $$$FINARADJPostedDate(objFINARADJ)'="" {
			set idNumber=""
			for {
				set idNumber=$order(^FINARADJLine(YM,idFINARADJ,idNumber))
				quit:idNumber=""
				if $data(^FININTBatchLines(YM,3,"ARADJ",idFINARADJ_" "_idNumber))=0 {
					set Result("Hash",$$$ErrorNo(Result("Errors")))=$$$Text($lb("Fin00337",idFINARADJ,idNumber)) ;Adjustment ´%1,%2´ does not have an Interface Batch.
					set strStatus='$$$OK
				} else {
					set idBatch=$order(^FININTBatchLines(YM,3,"ARADJ",idFINARADJ_" "_idNumber,""))
					set objFININTBatch=$get(^FININTBatch(YM,idBatch,1))
					if $$$FININTBatchPostedDate(objFININTBatch)="" {
						set idLine=""
						for {
							set idLine=$order(^FININTBatchLines(YM,3,"ARADJ",idFINARADJ_" "_idNumber,idBatch,idLine))
							quit:idLine=""
							set objFININTBatchLine=$get(^FININTBatchLine(YM,idBatch,idLine,1))
							set strBlock=$$$FININTBatchLineBlockNumber(objFININTBatchLine)
							set Result("Hash",$$$ErrorNo(Result("Errors")))=$$$Text($lb("Fin00338",idFINARADJ,idNumber,idBatch,strBlock))   ;Adjustment:%1,%2.  Interface Batch(%3,%4) has not been posted.
							set strStatus='$$$OK
						}
					}
				}
			}
		}
	}

	if strStatus=$$$OK {
		set strStatus=2  ; Because this is a Standalone Check return 2 if Ok
	}
	quit strStatus
]]></Routine>
</Export>