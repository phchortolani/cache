<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAutorizacaoProdutoLocal" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARAutorizacaoProdutoLocal
	#include COMSYS
	#include INConst
	#include VARConst
	quit
	
	; Autoriza todos os produtos para o local novo cadastrado
AutorizaLocal(pLocal)
	$$$VAR
	new strStatus, idxProduto
	
	set strStatus = $$$OK
	
	if ($get(pLocal) = "") { quit strStatus }
	
	; Varre todos os produtos
	set idxProduto = ""
	
	for {
		set idxProduto = $order(^INART(YM,idxProduto))
		quit:(idxProduto = "")
		
		set strStatus = $$Autoriza(idxProduto,pLocal)
	}
	
	quit strStatus
	
	; Autoriza todos os locais para o produto novo cadastrado
AutorizaProduto(pProduto)
	$$$VAR
	new strStatus, idxLocal
	
	set strStatus = $$$OK
	
	if ($get(pProduto) = "") { quit strStatus }
	
	; Varre todos os locais
	set idxLocal = ""
	
	for {
		set idxLocal = $order(^WWW0121(YM,YM,idxLocal))
		quit:(idxLocal = "")
		
		set strStatus = $$Autoriza(pProduto,idxLocal)
	}
	
	quit strStatus
	
	
Autoriza(pstrItem,pstrLocal)
	$$$VAR
	
	new strStatus, key, objINART, obj
	
	set strStatus = $$$OK
	
	set $$$KEY1(key)	= pstrLocal
	set $$$KEY2(key)	= pstrItem
	
	set objINART = $get(^INART(YM,$$$KEY2(key),1))
	
	if (objINART = "") {
		set strStatus = "Produto não existe. " _ $$$KEY2(key)
		quit strStatus
	}
	
	if ('$data(^WWW0121(0,0,$$$KEY1(key)))) {
		set strStatus = "Local não existe. " _ $$$KEY1(key)
		quit strStatus
	}
	
	if (strStatus) {
		set obj = $get(^INDRPITEM(YM,$$$KEY1(key),$$$KEY2(key),1))
		
		if (obj '= "") {
			quit strStatus
		}
															
		set $$$INDRPITEMDescription(obj)	= $$$INARTSearchName(objINART)	// Descrição do Produto
		set $$$INDRPITEMItemStatus(obj)		= 1	// Ativo
		set $$$INDRPITEMSaleableItem(obj)	= 1	// Produto Vendável
		set $$$INDRPITEMUnitOfMeasure(obj)	= $$$INARTUnitofMeasure(objINART)	// Unidade de medida
		set $$$INDRPITEMDRPRunID(obj)		= 1	// Número da execução DRP - 1 - Permanent
		
		set strStatus = $$$Save("INDRPITEM", key, obj, 1)
	}
	
	quit strStatus
	
AutorizaTodos()
	; do AutorizaTodos^VARAutorizacaoProdutoLocal()
	new strStatus
	
	set strStatus = $$$OK
	
	$$$VAR
	new strStatus, idxLocal
	
	set strStatus = $$$OK
	
	; Varre todos os locais
	set idxLocal = ""
	
	for {
		set idxLocal = $order(^WWW0121(YM,YM,idxLocal))
		quit:(idxLocal = "")
		
		; Varre todos os produtos
		set idxProduto = ""
		
		for {
			set idxProduto = $order(^INART(YM,idxProduto))
			quit:(idxProduto = "")
			
			set strStatus = $$Autoriza(idxProduto,idxLocal)
		}
	}
	
	quit strStatus
	
]]></Routine>
</Export>