<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPreRequisicao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPreRequisicao

	#include COMSYS
	#include COMConst
	#include VARConst
	#include INConst


OnAfterDataFields(pYKEY,pYFELD)
	
	quit:(pYKEY = "")
	
	if YSEITE = 1 {
		do OnAfterDataFields^VARPreRequisicaoLinha(pYKEY,pYFELD)
		
		do AtualizaStatusLinha(pYKEY)
		do LoadGrid(pYKEY)
		do GridRequisicoesGeradas(pYKEY)
		

	
		set YVOR = $get(^WWW120(0,YFORM,1))
	
		do PrintJS(YVOR)
		do PrintJS^VARPreRequisicaoLinha(YVOR)
	
	}
	
	quit
	
	
LoadGrid(pidPreReq="")
 	
	new YFORM,YKEY,YAUSWAHL,YFELD
	set YFORM="VARPreRequisicaoLinha"
	set YFELD=""
	set YKEY=$get(pidPreReq)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)=1
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)=290
	set $$$COMGridEditParameterGridName(YAUSWAHL)=YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)='($$$WWW120AuthorizationToModifyData(YVOR)=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)="VARPreRequisicao"
	
	$$$GRIDStart(YFORM,YKEY)
	
	quit
	
	
	
GridRequisicoesGeradas(pidReq="")

	if pidReq="" {
		quit
	}
	
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Produto","Unidade","Quant. Aprovada","Quant. Pendente","Local Abastecedor","Nº Requisição (Item)")
			
	if $$Start^COMTable(lstHeader,"Requisições geradas para o pedido de transferência") {
 		
 		set ExisteRegistro = $$$NO
		set idReqLinha = ""
		for {
			set idReqLinha = $order(^VARPreRequisicaoLinha(YM,pidReq,idReqLinha))
			quit:(idReqLinha = "")

			set objPreRequisicaoLinha = $G(^VARPreRequisicaoLinha(YM,pidReq,idReqLinha,1))
			
			;Se o Status da linha for diferente de (2-Ag. atendimento e 3-Atendido) continua
			set StatusLinha = $$$VARPreRequisicaoLinhaStatus(objPreRequisicaoLinha)
			if (StatusLinha'= 2) && (StatusLinha'= 3){
				continue
			}
			
			set ExisteRegistro    = $$$YES
			set Produto           = $$$VARPreRequisicaoLinhaCodProduto(objPreRequisicaoLinha)
			set DescProduto       = $$SQLGetDescricaoProduto^VARSQL(Produto)
			set Unidade           = $piece($get(^WWW101(0,"EINHEIT","PT",$$$VARPreRequisicaoLinhaUnidade(objPreRequisicaoLinha),1)),Y,1)
			set QuantAprovada     = $$$VARPreRequisicaoLinhaQuantAprovada(objPreRequisicaoLinha)
			set LocalAbastecedor  = $$$VARPreRequisicaoLinhaLocalAbastecedor(objPreRequisicaoLinha)
			set DescLocalAbastec  = $$SQLGetLocationName^VARSQL(LocalAbastecedor)
			set NumRequisicao     = $$$VARPreRequisicaoLinhaNumRequisicaoGerada(objPreRequisicaoLinha)
			set ItemRequisicao    = $$$VARPreRequisicaoLinhaItemRequisicaoGerada(objPreRequisicaoLinha)
			
			;Verifica no Item da Requisição a Quantidade Entregue (QtyIssued)
			;e calcula a QtdePendente do Pedido de Transferência
			set objINReqLine = $G(^INReqLine(YM,NumRequisicao,ItemRequisicao,1))
			if (objINReqLine '= ""){
				set QuantEntregue = $$$INReqLineQtyIssued(objINReqLine)
				set QuantPendente = QuantAprovada - QuantEntregue
				if (QuantPendente <= 0) {
					set QuantPendente = 0
				}
			}
			else{
				set QuantPendente = QuantAprovada
			}
				
				
			
			do NewLine^COMTable()
			do InsertCell^COMTable("("_Produto_") "_DescProduto,,,,,,,,,$$$YES)
			do InsertCell^COMTable(Unidade)
			do InsertCell^COMTable( $$^WWWTR(0,12,QuantAprovada),,,,"RIGHT")
			do InsertCell^COMTable( $$^WWWTR(0,12,QuantPendente),,,,"RIGHT")
			do InsertCell^COMTable("("_LocalAbastecedor_") "_DescLocalAbastec)
			;Necessário passar o YPARA = 2 igual é feito no menu com na opção Circuito de disparo de transferência para formulário (TransferTriggerForForm)
			set vYPARA = 2	
			do InsertCell^COMTable(NumRequisicao_" ("_ItemRequisicao_")","INReq",NumRequisicao,vYPARA,"CENTER")
			do EndLine^COMTable() 
		
		}	;end for Modulo
		
		if ExisteRegistro = $$$NO {
			do NewLine^COMTable()
			do InsertCell^COMTable("Não existem requisições geradas para esse Pedido de Transferência",,,,,,6)
			do EndLine^COMTable()
		}			
		
		do Stop^COMTable()
		
	}	
	


	quit
	
	
OnBeforeSave(pYKEY)
	
	new strStatus
	
	set idLocn = $$$VARPreRequisicaoLocalSolicitante(YFELD)
	
	; nao permite salvar pedidos se o destino nao for local de estoque
	set objWWW0121 = $get(^WWW0121(0,YM,idLocn,1))
	
	if '$$$WWW0121StorageLocn(objWWW0121) {
		set Q=$$$QDontSave
		set strStatus = "0 "_"Não é possível requisitar uma transferência pois o local "_idLocn_" não é um local de estoque. Utilize a requisição de consumo."
		$$$DecomposeStatus(strStatus)
		$$$Alert(strStatus)
	} else {
		set strStatus = $$$GRIDSave(pYKEY)
	}
 
	quit strStatus
	
	
CanFirm(pYKEY,pYFELD)	

	new strStatus
	
 	set strStatus = $$$OK
 	
 	if pYKEY="" {
 		quit strStatus 
 	}
 	
	
	if $$$VARPreRequisicaoStatus(pYFELD) > 1 {
	 	set strStatus = "O pedido já foi processado"
 	}
 	
 	if $$$ISOK(strStatus){
		if '$D(^VARPreRequisicaoLinha(YM,pYKEY)){
			set strStatus = "Insira pelo menos uma linha para processar"	
		}
 	}
 	
 	$$$YQHandler(strStatus)
 	
 	quit strStatus


Firm(pYKEY,pYFELD)
	new strStatus
	
	set strStatus = $$$OK
	
 	do CanFirm(pYKEY,pYFELD)
 	
 	if YQ = $$$YQEnable {
		set strStatus = $$Transaction^COMTransaction("FirmTxn^VARPreRequisicao("""_pYKEY_""")",$$$YES)
 
		if $$$ISOK(strStatus) {
			set YSEITE = 1 // FIXME: This does not even fix all cases where page two is displayed.
			do ReloadForm^COMUtilForm()
		}
 	} else {
	 	do ReturnError^COMUtils($$$YQMessage)
 	}
 
	quit strStatus
		
		
	
FirmTxn(pYKEY)
	new strStatus, objPreReq, Linha, objPreReqLinha, ChaveLinha
	
	set strStatus = $$$OK	
	
	set objPreReq = $G(^VARPreRequisicao(YM,pYKEY,1))
	
	if objPreReq = ""{
		set strStatus = "Objeto VARPreRequisicao não localizado"
	}

	if $$$ISOK(strStatus){
		set $$$VARPreRequisicaoStatus(objPreReq)        = 2	;Processado
		set $$$VARPreRequisicaoConfirmadoEm(objPreReq)  = $H
		set $$$VARPreRequisicaoConfirmadoPor(objPreReq) = YBED
		
		;Para cada linha da Pré-Requisição altera o Status para 1=Ag. Aprovação
		set Linha=""
		for {
			set Linha = $order(^VARPreRequisicaoLinha(YM,pYKEY,Linha))
			quit:Linha=""
			quit:'$$$ISOK(strStatus)	
			
			set objPreReqLinha = $G(^VARPreRequisicaoLinha(YM,pYKEY,Linha,1))
			if objPreReqLinha = ""{
				set strStatus = "Objeto VARPreRequisicaoLinha não localizado"
			}
			set $$$VARPreRequisicaoLinhaStatus(objPreReqLinha) = 1	;Ag. Aprovação
			set ChaveLinha = pYKEY_","_Linha
			set strStatus = $$$Save("VARPreRequisicaoLinha", ChaveLinha, objPreReqLinha, 1)
			
		}	
		
		if $$$ISOK(strStatus){
			set strStatus = $$$Save("VARPreRequisicao", pYKEY, objPreReq, 1)
		}
	}

	quit strStatus
	
	
	
OnBeforeButtonLine(pYKEY,&pYFELD)

	if '$$$NoKey(pYKEY) {
	
		if $$$VARPreRequisicaoStatus(pYFELD) > 1 {
			set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
		}
		
	}

	quit
	
	
OnAfterDelete(pYKEY)
	;do OnAfterDelete^VARPreRequisicao(YKEY)
	
	new strStatus

	set strStatus = $$KillChildren^COMUtils("VARPreRequisicaoLinha",pYKEY)
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontDelete
	}
	
	quit
	
OnBlurDataEntrega(pYINHALT)
	quit:(pYINHALT = "")
	
	if ( $zdateh(pYINHALT,4) < $horolog ) {		
		$$$Alert("A data de entrega não pode ser menor que a data atual.")
		set $piece(YFELD,Y,5) = ""
	}

	quit	


AtualizaStatusLinha(pYKEY)
	;do AtualizaStatusLinha^VARPreRequisicao(pYKEY)


	if $$$NoKey(pYKEY) {
		quit
	}
	
	set strStatus = $$$OK	
	
	;Para cada linha da Pré-Requisição que estiver com o status 2-Ag. Atendimento
	;verifica se a linha da Requisição gerada já foi atendida completamente e
	;altera o Status da linha da Pré-Requisição para 3-Atendido.
	;
	set Linha=""
	for {
		set Linha = $order(^VARPreRequisicaoLinha(YM,pYKEY,Linha))
		quit:Linha=""
		quit:'$$$ISOK(strStatus)	
		
		set objPreReqLinha = $G(^VARPreRequisicaoLinha(YM,pYKEY,Linha,1))
		if objPreReqLinha = ""{
			set strStatus = "Objeto VARPreRequisicaoLinha não localizado"
		}
		if $$$VARPreRequisicaoLinhaStatus(objPreReqLinha) '= 2 {
			continue
		}
		
		set NumRequisicao  = $$$VARPreRequisicaoLinhaNumRequisicaoGerada(objPreReqLinha)
		set ItemRequisicao = $$$VARPreRequisicaoLinhaItemRequisicaoGerada(objPreReqLinha)
		set QuantAprovada  = $$$VARPreRequisicaoLinhaQuantAprovada(objPreReqLinha)
		
		set objINReqLine = $G(^INReqLine(YM,NumRequisicao,ItemRequisicao,1))
		if (objINReqLine '= ""){
			set QuantEntregue = $$$INReqLineQtyIssued(objINReqLine)
			set QuantPendente = QuantAprovada - QuantEntregue
			
			;Se a requisição foi totalmente entregue, altera o status do item
			;do pedido de transferência para 3-Atendido e grava a linha.
			if QuantPendente <= 0 { 
				set $$$VARPreRequisicaoLinhaStatus(objPreReqLinha) = 3	;Atendido	
				set ChaveLinha = pYKEY_","_Linha
				set strStatus = $$$Save("VARPreRequisicaoLinha", ChaveLinha, objPreReqLinha, 1)
			}
		}
		
	}	;end for Linha
	

	quit

	
CanCancelarDoc(pYKEY,pYFELD)

 	set YQ = $$$YQEnable
 	 	
	if $$$NoKey(pYKEY) {
		set YQ = 1_"Selecione um registro."
		quit
	}
	
	
	if $$$VARPreRequisicaoStatus(pYFELD) = 1 {
		set YQ = 1_"Esse pedido de transferência ainda não foi processado. Favor verificar."
	}
	else{
		;Só pode cancelar se estiver com status = 2-Ag. Aprovação
		set disableCancelar = $$$YES
		set Linha=""
		for {
			set Linha = $order(^VARPreRequisicaoLinha(YM,pYKEY,Linha))
			quit:Linha=""
			
			set objPreReqLinha = $G(^VARPreRequisicaoLinha(YM,pYKEY,Linha,1))
			set StatusLinha = $$$VARPreRequisicaoLinhaStatus(objPreReqLinha)
			if (StatusLinha = 1){
				set disableCancelar = $$$NO
			}

			
		}	;end for	
		if disableCancelar = $$$YES {
			set YQ = 1_"Não existem produtos no status 1-Ag. Aprovação no pedido de transferência para serem cancelados."
		}
		
		
	}
	
 	 	
	quit
	
	
PrintJS(pobjForm)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARPreRequisicao.mac
    ;-------------------------------------------------------------------------------
	new idClass, strP1Name
 
	set idClass = $$$WWW120ClassUsedInForm(pobjForm)
	set strP1Name = "Y"_idClass_"P1"
	
	write !, "<script language=""javascript"">"
	&js<
	var idPreRequisicao = document.#(YHTMFORM)#.#(strP1Name)#.value;
 
	function iePrompt(message) {
		var settings = "dialogWidth: 420px; dialogHeight: 200px; center: yes; " +
			"edge: raised; scroll: no; status: no";		
		return window.showModalDialog("#(YGIF)#VARPromptSize.html", message,settings);
	}
	
	function cancelarDoc() {
		CallBackNow("CancelarDocOnClickJS^VARPreRequisicao", idPreRequisicao);
	}
	>
	write !, "</script>"
	quit
	
	
CancelarDocOnClickJS(idPreRequisicao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função PrintJS^VARPreRequisicao.mac
    ;-------------------------------------------------------------------------------
	new objPreRequisicao
 
	if ($length($get(idPreRequisicao)) = 0) {
		set objPreRequisicao = ""
	}
	else {
		set objPreRequisicao = $get(^VARPreRequisicao(0,idPreRequisicao,1))
	}
	set YQ = 0
	do CanCancelarDoc(idPreRequisicao, objPreRequisicao)
	if ($extract(YQ, 1, 1) '= $$$YQEnable) {
		&js<
		    alert('#($piece(YQ, "1", 2))#');
		>
		quit
	}
 
	&js<
	    var motivo = iePrompt('Favor inserir o motivo do cancelamento do pedido de transferência:');
		if (motivo) {
			CallBackNow("CancelarDocOnClick^VARPreRequisicao", '#(idPreRequisicao)#', motivo);
		}
	> 
	quit
	
	

CancelarDocOnClick(idPreRequisicao,strMotivoCancelamento)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função CancelarDocOnClickJS^VARPreRequisicao.mac
    ;-------------------------------------------------------------------------------
	new objPreRequisicao,strMotivoCancelamentoFormatado,i,strStatus,YSEITE
	
	set objPreRequisicao = $$GetFormData^COMUtilForm(YFORM)
	quit:($$$VARPreRequisicaoStatus(objPreRequisicao) '= 2)
 
	if ($length(strMotivoCancelamento) = 0) {
		$$$StartScript()
		write "alert('Favor inserir o motivo do cancelamento do pedido de transferência!');"
		$$$EndScript()
	}
	else {
		if ($length(strMotivoCancelamento, """") = 1) {
			set strMotivoCancelamentoFormatado = strMotivoCancelamento
		}
		else {
			set strMotivoCancelamentoFormatado = ""
			for i = 1:1:$length(strMotivoCancelamento, """") {
				set strMotivoCancelamentoFormatado = strMotivoCancelamentoFormatado_
					$piece(strMotivoCancelamento, """", i)_""""""
			}
		}
 
		set strStatus = $$Transaction^COMTransaction("CancelarDocTxn^VARPreRequisicao("""_idPreRequisicao_""","""_strMotivoCancelamentoFormatado_""")", $$$YES)
		if $$$ISOK(strStatus) {
			$$$StartScript()
			write "alert('Pedido de transferência cancelado com sucesso!');"
			$$$EndScript()
 
			set YSEITE = 1
			do GoToForm^COMUtilForm(YFORM, idPreRequisicao, 4, $get(YPARA), $$$NO)
		}
		else {
			$$$StartScript()
			write "alert('Erro cancelar pedido de transferência. => "_$$DecodeError^COMUtilError(strStatus)_"');"
			$$$EndScript()
		}
	}
	quit ""


	
CancelarDocTxn(pidPreRequisicao,pstrMotivoCancelamento)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função CancelarDocOnClick^VARPreRequisicao.mac
    ;-------------------------------------------------------------------------------

	new idPreRequisicaoLinha, flgCancelar, strStatus, objPedidoCompra
 
	set idPreRequisicaoLinha = ""
	for {
		set idPreRequisicaoLinha = $order(^VARPreRequisicaoLinha(YM,pidPreRequisicao,idPreRequisicaoLinha))
		quit:idPreRequisicaoLinha=""
		
		set objPreReqLinha = $G(^VARPreRequisicaoLinha(YM,pidPreRequisicao,idPreRequisicaoLinha,1))
		set StatusLinha = $$$VARPreRequisicaoLinhaStatus(objPreReqLinha)
		
		;Cancela todas as linhas que estiverem com o Staus 1-Ag. Aprovação
		if (StatusLinha = 1){	
			set strStatus = $$CancelarIndividual^VARPreRequisicaoLinha(pidPreRequisicao, idPreRequisicaoLinha, pstrMotivoCancelamento)
			if $$$ISERR(strStatus) {
				set $zerror = $$DecodeError^COMUtilError(strStatus)
				zquit 1 GOTO @$ZTRAP
			}
	 
		}
 
	}	;end for
 

	quit $$$OK	
	
	
CanPrint(pidPreReq)
	
	new strStatus, objPreReq, status	
	set strStatus = $$$OK	
	
	if $$$NoKey(pidPreReq) {
		set strStatus = $listbuild("IN00511")  ;Please select a data record first.
	} else {
		set objPreReq = $get(^VARPreRequisicao(YM,pidPreReq,1))
		set status	  = $$$VARPreRequisicaoStatus(objPreReq)
		
	 if (status '= 2) {
		set strStatus = "Só será possível imprimir o relatório após conclusão do pedido de transferência."	
		}
	}	
	$$$YQHandler(strStatus)

	quit


ImprimeRelatorio(YKEY)
	if ($$GetPreferencia^VARPreferenciasUsuario($$$FldVARPreferenciasUsuarioTelaFiltroRelatorio) = 1){
		do GotoFiltrosRelatorio^VARFiltrosRelatorio("RunReportPreRequisicao",0,1,,YKEY)
	} else {
		do RunReportPreRequisicao^VARJasperRunReport(YKEY)
	}
		
	quit
]]></Routine>
</Export>