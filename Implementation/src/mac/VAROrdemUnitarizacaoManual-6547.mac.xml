<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VAROrdemUnitarizacaoManual" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VAROrdemUnitarizacaoManual
#include COMSYS
#include COMConst
#include INConst
#include VARConst

OnAfterSave
	;-------------------------------------------------------------------------------
    ; Prepara as variáveis para a consulta do usuário.
	;
    ; History:
    ; 05-Out-2011	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------	
	new idLocal, idProduto, strEndereco, fltQuantidade, idMarca, strLote, dteValidade, 
		keyUnitarizacao, objUnitarizacao, strStatus
	
	//Filter
	set idLocal	      = $get(VORG(1))
	set strEndereco   = $get(VORG(2))
	set idProduto     = $get(VORG(3))
	set fltQuantidade = $get(VORG(4))
	set idMarca		  = $get(VORG(5))
	set strLote		  = $get(VORG(6))
	set dteValidade	  = $get(VORG(7))
	
	set strStatus = $$$OK
	
	set keyUnitarizacao = $$^WWWNEXT("VAROrdemUnitarizacao")	
	set objUnitarizacao = ""
	
	set $$$VAROrdemUnitarizacaoLocal1(objUnitarizacao)         = idLocal
	set $$$VAROrdemUnitarizacaoEndArmazenagem(objUnitarizacao) = strEndereco	
	set $$$VAROrdemUnitarizacaoProduto(objUnitarizacao)    = idProduto
	set $$$VAROrdemUnitarizacaoQuantidade(objUnitarizacao) = fltQuantidade
	set $$$VAROrdemUnitarizacaoMarca(objUnitarizacao) 	   = idMarca
	set $$$VAROrdemUnitarizacaoLote(objUnitarizacao) 	   = strLote
	set $$$VAROrdemUnitarizacaoValidade(objUnitarizacao)   = dteValidade
	set $$$VAROrdemUnitarizacaoStatus(objUnitarizacao)     = 0 ;0-Em Aberto
	
	set strStatus = $$$Save("VAROrdemUnitarizacao",keyUnitarizacao,objUnitarizacao,1)	

	if $$$ISOK(strStatus) {
		$$$Alert("Ordem de fracionamento gerada com sucesso.")
	} else {
		$$$Alert("Não foi possível gerar a ordem de fracionamento. Favor verificar.")
	}
	
	do ReloadForm^COMUtilForm("VAROrdemUnitarizacaoManual")
	
	quit
	
OnAfterDataFields
	new mySQL, lstHeader, count, idOrdem, idStatus, idProduto, fltQuantidade, idMarca, strLote,
		dteValidade, param, descStatus, criadoEm, criadoPor
	
	set mySQL = "SELECT CodOrdemUnitarizacao,"
	set mySQL = mySQL_" Status,"	
	set mySQL = mySQL_" Produto,"
	set mySQL = mySQL_" Quantidade,"
	set mySQL = mySQL_" Marca,"	
	set mySQL = mySQL_" Lote,"
	set mySQL = mySQL_" Validade,"
	set mySQL = mySQL_" CriadoEm,"
	set mySQL = mySQL_" CriadoPor"
	set mySQL = mySQL_" FROM VAROrdemUnitarizacao"
	set mySQL = mySQL_" WHERE UPPER(Status) < 4"
	set mySQL = mySQL_" AND UPPER(Local1) = "_YLOCATION
	//set mySQL = mySQL_" ORDER BY CriadoEm desc"

	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("","N°. Ordem","Cód.","Produto","Quantidade","Marca","Lote","Validade","Criado Por","Criado Em","Status")
	
	set count = 0

	set object = ##class(%ResultSet).%New()  ;Create Result Set Object
	
	do object.Prepare(mySQL) ;Prepare Query
	do object.Execute()  	 ;Execute Query 

	if $$Start^COMTable(lstHeader,"Ordens de fracionamento pendentes no local (MANUAIS)") {

		while (object.Next()) {
			set idOrdem       = object.GetData(1)
			set idStatus      = object.GetData(2)
			set idProduto     = object.GetData(3)
			set fltQuantidade = object.GetData(4)
			set idMarca		  = object.GetData(5)
			set strLote		  = object.GetData(6)
			set dteValidade   = object.GetData(7)
			set criadoEm      = object.GetData(8)
			set criadoPor	  = object.GetData(9)
			
			set count = $i(count)

			set param = "STATUSUNITARIZACAO"_Y_idStatus
	
			if (idStatus '= "") {
				set descStatus = $$SQLGetUserParamDescVAR^VARSQL(param)
			} else {
				set descStatus = ""
			}
			
			do NewLine^COMTable()
			do InsertCell^COMTable(count)
			do InsertCell^COMTable(idOrdem,"VAROrdemUnitarizacao",idOrdem)
			do InsertCell^COMTable(idProduto)
			do InsertCell^COMTable($$SQLGetDescricaoProduto^VARSQL(idProduto),,,,,,,,,$$$YES)
			do InsertCell^COMTable($$^WWWTR(0,12,fltQuantidade),,,,"RIGHT")
			do InsertCell^COMTable($$GetBrandName^VARINBrand(idMarca))
			do InsertCell^COMTable(strLote)
			do InsertCell^COMTable($zdate(dteValidade,4))
			do InsertCell^COMTable(criadoPor)			
			do InsertCell^COMTable($zdt(criadoEm,4))
			do InsertCell^COMTable("<font color='blue'>"_descStatus_"</font>")			
			do EndLine^COMTable()
		}
				  	
		do Stop^COMTable()		
	}
	
	do object.Close()

	quit
]]></Routine>
</Export>