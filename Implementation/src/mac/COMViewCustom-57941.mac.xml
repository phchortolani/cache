<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMViewCustom" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMViewCustom
	;-------------------------------------------------------------------------------
	; creation of custom COMViews.
	; 
	; History:
	; 19-Aug-2006	GRF		Doco
	;-------------------------------------------------------------------------------
#include COMConst
#include COMSYS
#include WWWConst
	
	
	;	Callback routine will add the records to INReqLine / INReqKitLine
	; do Initialise^COMViewCustom("INReqTemplate","TemplateSelected^INReqTemplate",$$$YES,$$$NO)

Initialise(pidClass,pstrCallback="",pblnCloseOnSelect=$$$NO,pblnJumpToRecord=$$$NO,pblnViewOnly=$$$NO,pblnColSel=$$$NO,pidFormFld="")
	;-------------------------------------------------------------------------------
	; Initialises a custom COMView.
	; 
	; Params:
	;	pidClass			= Class to search on.
	;	pstrCallback		= code to run if user selects a record.
	;	pblnCloseOnSelect	= close if user selects a record.
	;	pblnJumpToRecord	= jump to the record instead of close. (if in popup,
	;						  jump to main screen)
	;	pblnViewOnly		= No selection or Jump on GridClick (only a View)
	;	pblnColSel			=
	;	pidFormFld			= Overrides the incorrect 'CurrentField' assingment the correct value. Form+'D'+Field Number
	;							Example INIssueRevD1
	; 
	; NOTE: pidClass is actually the FORM !!
	; 
	; History:
	; 25-Sep-2013	SCR		CORE-274: Custom COMVIEW not working correctly.
	; 30-Apr-2009	PPP		SR16499:Added new parameter (pblnColSel) to Initialise
	; 							(Column Selection #6) This will make the COMView a
	; 							Row/Column selection and highlight
	; 22-Oct-2008	shobby	SRBR014974: Reversed YFELD and YFORM in call to hook.
	; 15-Sep-2008	PPP		SR15866: new param pblnViewOnly - no select on GridClick
	; 08-Sep-2008	shobby	SRBR014978: YFELD could be undefined if using a popup
	; 							search from a grid button such as InReqIssueAsReq
	; 01-Sep-2008	shobby	SRBR014974: Call out to a hook (including VAR) for any
	; 							specific code required.
	; 19-Nov-2007	shobby	SRBR014787: Leave a breadcrumb so that COMView can
	;							determine which form to use to display text
	;							correctly for _FREE fields.
	; 19-Jan-2006	Steve S	Macro usage for defaults
	; 30-Dec-2004	Paul K	Created
	;-------------------------------------------------------------------------------
	new strField,strStatus ;BR014974
	
	if pblnCloseOnSelect {
		set strField = "Y"
	
	} elseif pblnJumpToRecord {
		set strField = "J"
	
	} elseif pblnViewOnly {
		set strField = "V"
	
	} else {
		set strField = "S"
	}
 	
 	if pidFormFld="" {
		set strField = strField_pidClass_"D0"
 	} else {
	 	set strField = strField_pidFormFld
 	}
		
	kill ^CacheTempFixedField(YUSER)
	kill ^CacheTempSel(YUSER)		//SR16499
	
	set ^CacheTempFixedField(YUSER,"Class")             = pidClass
	set ^CacheTempFixedField(YUSER,strField,"CallBack") = pstrCallback 
	set ^CacheTempFixedField(YUSER,"CurrentField")      = strField
	;do SetYFORM^COMGridEdit31Head(pidClass)   ;BR014787  ;Shouldn't be necessary
 
	//SR16499
	set ^CacheTempSel(YUSER,"ColSel")         			= $get(pblnColSel)
 
	set strStatus = $$ExecuteHook^WWW001Hook(pidClass,$$$EnumWWWEVENTTYPEOnBeforeCOMViewCustom,"",$get(YFELD),YFORM) ;BR014974
 
	quit
	
	
AddCommand(pstrDescription="",pstrImage="",pstrCode="",pstrConfirmText="")
	;-------------------------------------------------------------------------------
	; Adds a custom button to each row of the result grid in COM View.
	; Params:
	;	pstrDescription		= tooltip of button
	;	pstrImage			= image of button. if none, will be first character of
	;						  description.
	;	pstrCode			= code to receive resultset for user clicking on button.
	;						  Format: "tag^routine"
	;	pstrConfirmText		= optional confirm text to prompt user before action
	;						  taken. if nothing, code just run.
	;
	; History:
	; 12-Apr-2005	Paul K	Created SR:12023
	;-------------------------------------------------------------------------------
	new idCommand,objCommand
	
	if pstrImage="" if pstrDescription'="" set pstrImage=$extract(pstrDescription)_".gif"
	set idCommand=$order(^CacheTempFixedField(YUSER,"Command",""),-1)+1
	set objCommand=""
	set $$$COMViewCommandDescription(objCommand)=pstrDescription
	set $$$COMViewCommandImage(objCommand)=pstrImage
	set $$$COMViewCommandCode(objCommand)=pstrCode
	set $$$COMViewCommandConfirmText(objCommand)=pstrConfirmText
	set ^CacheTempFixedField(YUSER,"Command",idCommand)=objCommand
	quit
	
	
AddFilter(pidField="",pidComparitor=$$$EnumCOMVIEWCOMPARATOREquals,pstrValue="",pblnGroup="",pblnNoconversion=$$$NO)
	;-------------------------------------------------------------------------------
	; Adds Optional Filters to search criteria.
	; 
	; Parameters:
	; pidField			Field number. -primary keys prefixed with "P", datafields with "D"
	;					example - 
	;					  class = INAUFP:
	;						P1            - Order number
	;						D60           - Order Line Completed
	;						P1.D60        - Order Completed
	;						P1.CINAUF1.D2 - Open Orders order type.
	;						                (will ensure there is a INAUF1 for the INAUFP)
	;						D12.D8        - Order Lines supplier's short name
	;					
	;					If nothing, custom sql expected in value property
	;  
	; pidComparitor		Comparitor between field and value.
	;					Defaults to 3 (Equals)
	;					See Application param COMVIEWCOMPARITOR for all values.
	;					Ignored if custom sql.
	;	
	; pstrValue			Value of field required. (Or custom sql)
	; 
	; NOTE : Please note that custom sql may now show correctly in COMView header,
	;        and if linking to other classes, these classes may not be in the
	;        "From" list.
	; 
	; History:
	; 17-Jun-2008	GRF		SR15787: Readability change only
	; 20-Jul-2005	PO		SR12682: Included the argument blnNoconversion
	; 12-Apr-2005	Paul K	Simplified parameters being passed in and added comments above.
	; 28-Dec-2004	Paul K	Created
	;-------------------------------------------------------------------------------
	new idFilter,objFilter,strField,strSearchField
	
	set strField = $get(^CacheTempFixedField(YUSER,"CurrentField"))
	set idFilter = $order(^CacheTempFixedField(YUSER,strField,"Filter",""),-1)+1
	
	set strSearchField=""
	
	if pidField'="" {
		set strSearchField=pidField
	} else {
		set strSearchField="Custom"
	}
	
	if +pidComparitor'=pidComparitor {
		if pidComparitor=">"        {   set pidComparitor = $$$EnumCOMVIEWCOMPARATORGreaterThan
		} elseif pidComparitor="<"  {   set pidComparitor = $$$EnumCOMVIEWCOMPARATORLessThan
		} elseif pidComparitor="="  {   set pidComparitor = $$$EnumCOMVIEWCOMPARATOREquals
		} elseif pidComparitor="'=" {   set pidComparitor = $$$EnumCOMVIEWCOMPARATORNotEquals
		} elseif pidComparitor="in" {   set pidComparitor = $$$EnumCOMVIEWCOMPARATORWithin
		}
	}
	
	set objFilter = ""
	set $$$COMViewFilterField(objFilter)        = strSearchField
	set $$$COMViewFilterValue1(objFilter)       = pstrValue
	set $$$COMViewFilterComparator(objFilter)   = pidComparitor
	set $$$COMViewFilterGroupBy(objFilter)      = pblnGroup
	set $$$COMViewFilterNoconversion(objFilter) = pblnNoconversion
	
	set ^CacheTempFixedField(YUSER,strField,"Filter",idFilter) = objFilter
	
	quit
	
	
Show(pblnBackArrow=$$$NO)
	;-------------------------------------------------------------------------------
	; If using COMViewCustom in custom form (like INAUFORDERRELEASE) 
	; use this to show the comview in the screen.
	;
	; History:
	; 27-Apr-2009	shobby	SR16108: Optionally include a back arrow.
	; 17-Jun-2008	GRF		SR15787: Added missing quit - no functional change
	; 12-Apr-2005	Paul K	SR12023: Created
	;-------------------------------------------------------------------------------
	new idClass,strField
	
	set idClass  = $get(^CacheTempFixedField(YUSER,"Class"))
	set strField = $get(^CacheTempFixedField(YUSER,"CurrentField"))
	do AfterDataFields^COMViewFilter($listbuild(idClass,strField),,,pblnBackArrow)
	quit
	
	
Start(pstrResultInField="",pidSite="",pblnUseMainFrame=$$$NO)
	;-------------------------------------------------------------------------------
	; Creates the popup for the screen.
	; 
	; Params:
	;	pstrResultInField	- A DOM input that the result is to go into.
	;	pidSite				- An optional DCM site to retrieve the data from. 
	;						  *** Not working yet!! ***
	;						  FIXME : Is this comment right? <GRF>  probably misunderstood YSEITE => pidTabNo
	;
	; History:
	; 27-Jul-2007	RPW		SRBR014600: Allow resize of form
	; 09-Feb-2007	RPW		SR15426: Use &amp; instead of just &
	; 23-Jan-2007	JW		SR14235: Add YUSER to window name
	; 21-Dec-2006	JW		SR14235: Added reference to child window
	; 15-Aug-2006	PO		SR14582: Open COMView in Main Frame
	; 15-Dec-2005	JW		SR13195: BEARB call now unecessary.
	; 12-Apr-2005	Paul K	Commented
	;-------------------------------------------------------------------------------
	new strLink,strField,idClass,intWidth,intHeight,intTop,intLeft,objViewUser
	
	set idClass   = $get(^CacheTempFixedField(YUSER,"Class"))
	
	set intWidth  = 570
	set intHeight = 570
	set intTop    = 0
	set intLeft   = 0
		
	if idClass'="" {
		set objViewUser = $get(^COMViewUser(0,idClass,YBED,1))
		if $$$COMViewUserDialogHeight(objViewUser)'="" set intHeight = $$$COMViewUserDialogHeight(objViewUser)
		if $$$COMViewUserDialogWidth(objViewUser)'=""  set intWidth  = $$$COMViewUserDialogWidth(objViewUser)
		if $$$COMViewUserDialogTop(objViewUser)'=""    set intTop    = $$$COMViewUserDialogTop(objViewUser)
		if $$$COMViewUserDialogLeft(objViewUser)'=""   set intLeft   = $$$COMViewUserDialogLeft(objViewUser)
	}
	
	//if intHeight<200 set intHeight = 200 // SRBR014600
	//if intWidth<200  set intWidth  = 200 // SRBR014600
	
	set strField = $get(^CacheTempFixedField(YUSER,"CurrentField"))
	
	set strLink = YAKTION_"EP=WWWFORM"
	set strLink = strLink_"&YFORM=WWWPARA"
	set strLink = strLink_"&YUSER="_YUSER
	set strLink = strLink_"&YBED="_YBED
	set strLink = strLink_"&YTRAKT="_YTRAKT
	set strLink = strLink_"&YUCI="_YUCI
	set strLink = strLink_"&YM="_YM
	set strLink = strLink_"&YLFDAT="_strField
	set strLink = strLink_"&YLFFORM="
	set strLink = strLink_"&YPARA="
	set strLink = strLink_"&YSEC="
	
	write $$$GetSizeEvent(Y,strField) // SRBR014600
	
	if pblnUseMainFrame { // SR14582
		&js<
		document.location.href = '#(strLink)#';
		>
	
	} elseif pstrResultInField="" {
		set strLink = $$Replace^COMUtilStr(strLink,"&","&amp;")
		
		// SRBR014600: Get the new sizes
		&js<
		#("OnWindow"_strField_"Close")# = function() {
			if (#("Window"_strField)#!=null) {#("Window"_strField)#.close();}
		}
			#("Window"_strField)#=subWindow('#(strLink)#','Custom#(YUSER)#','left=#(intLeft-4)#,top=#(intTop-23)#,width='+eval('#(strField)#Width')+',height='+eval('#(strField)#Height')+',resizable=yes,scrollbars=no,status=no');
		window.attachEvent('onunload',#("OnWindow"_strField_"Close")#);
		>
	
	} else {
		set ^CacheTempFixedField(YUSER,"ReturnValue")=1
		// SRBR014600: Get the new sizes

		&js<
			$.post('COM.GridEdit.cls',{method: 'CampoRetorno', YUSER: '#(YUSER)#', yfield: '#($piece(pstrResultInField,".",2))#', pidType: 99},function(){
				var left = (screen.width/2)-(eval('#(strField)#Width')/2);
				var top = (screen.height/2)-(eval('#(strField)#Height')/2);
				//$('#'+yfield).attr('retorno', 1);"
				window.open('#(strLink)#'+ new Date().getSeconds(),'Parameter','toolbar=0,scrollbars=0,resizable=1,width='+eval('#(strField)#Width')+',height='+eval('#(strField)#Height')+',left='+left+',top='+top);
			});
			return;
		>

		/*&js<
		var result = window.showModalDialog('#(strLink)#','Parameter','DialogLeft: #(intLeft-4)#; DialogTop: #(intTop-23)#; DialogWidth: '+eval('#(strField)#Width')+'px; DialogHeight: '+eval('#(strField)#Height')+'px; resizable: yes; scroll: no; help:no; status: no;');
		if (result != null ) {
			#(pstrResultInField)#.value=result;
			#(pstrResultInField)#.focus();
			
		}
		>*/
	}
	quit
	
GetID(pobjResultSet)
	;-------------------------------------------------------------------------------
	; Converts an SQL ID into an @Net ID
	;
	; Returns:String
	;
	; History:
    ; 09-Sep-2008	PP		SR15866: Update COMView to Objects
	; 13-Apr-2006	JW		Fix for shared classes (one less SQL key)
	; 10-Jan-2005	Paul K	Created
	;-------------------------------------------------------------------------------
	new blnObj,idClass,idKey,idRef,intStart,loop
 
	set blnObj = +$get(^CacheTempObj(YUSER,"Object"))		//SR15866
	
	if 'blnObj {
		set intStart = 2
		set idClass  = $get(^CacheTempView(YUSER,"Class"))
		if (idClass'="") && $$$WWW001SharedFile($get(^WWW001(0,idClass,1))) {
			set intStart = 1
		}
		
		set idRef = $get(pobjResultSet.Data("ID"))
		set idKey = ""
		if idRef'="" {
			for loop=intStart:1:$length(idRef,"||") {
				if idKey'="" set idKey = idKey_","
				set idKey = idKey_$piece(idRef,"||",loop)
			}
		}
		
	} else {
		set idKey=$get(pobjResultSet.Data("ID"))
	}
	
	quit idKey
	
	;+++++++++++++++++++++++++++++++++++++++
	;
	;  Methods below are INTERNAL ONLY!!!
	; 
	;+++++++++++++++++++++++++++++++++++++++
	
CallBack(pidKey,pblnSelectAll=$$$NO,pidField="")
	;-------------------------------------------------------------------------------
	; Used when there is a callback with the Custom COMView.
	; 
	; Called By : JS function SelectAll() in COMViewSetupJS1
	;             GridClick^COMViewFilter
	; 
	; History:
    ; 28-Apr-2009	PPP		SR16499: Added the pidField parameter - could be used in
    ; 							the Callback execute
	; 14-Jan-2009	GRF		SR16297: V2 classes are not @netManager classes thus
	; 							need to exclude "YM||" prefix.
	; 17-Jun-2008	GRF		SR15787: Allow CallBack string to include additional
	;							parameters - need to manually specify 'objResult' as
	;							a variable in the parameter list in that case; clear
	;							old change notes.
	; 13-Feb-2007	RPW		SR15426: Reset the YUSER back to the parent if we need to
	; 27-Nov-2006	JW		SR15101: Add comma to YBACK
	; 13-Apr-2006	JW		Fix for shared classes (one less SQL key)
	; 11-May-2005	PO/PK	SR12142: Enabling select all feature
	; 12-Apr-2005	PK		Commented
	;-------------------------------------------------------------------------------
	new idClass,idUser,objResult,strCallBack,strRelation,strSQL
	
	// SR15426: vvv
	set idUser = $get(^CacheTempView(YUSER,YUCI,"YUSER"))
	if idUser="" set idUser = YUSER
	new YUSER
	set YUSER = idUser
	// SR15426: ^^^
	 
	set strRelation = $get(^CacheTempView(YUSER,"Relation"))
	
	if ($extract(strRelation,1)="Y") || ($extract(strRelation,1)="S") {
		set idClass = $get(^CacheTempView(YUSER,"Class"))
		if pblnSelectAll || $$Group^COMViewSQL() {
			set strSQL = $$GenerateSQL^COMViewSQL(idClass,"",pidKey,,,pblnSelectAll)
		} else {
		;	set strSQL="select ID from "_idClass_" where ID='"_      ; SR16297
		;	            $select($$$WWW001SharedFile($get(^WWW001(0,idClass,1))):"",1:"0||")_
		;	            $$Replace^COMUtilStr(pidKey,",","||")_"'"
			set strSQL="select ID from "_idClass_" where ID='"_
			            $select('$data(^WWW001(0,idClass,1)):"",$$$WWW001SharedFile($get(^WWW001(0,idClass,1))):"",1:"0||")_
			            $$Replace^COMUtilStr(pidKey,",","||")_"'"
		}
 
		set strCallBack = $get(^CacheTempView(YUSER,"CallBack"))
		set objResult   = ##class(%Library.ResultSet).%New()
		do objResult.Prepare(strSQL)
		do objResult.Execute()
				 
	;+++++++++++++++++++++++++++++++++++++++
	;  Execute COMView Callback                *** EXECUTE ? ***
	;+++++++++++++++++++++++++++++++++++++++
	; vvv  SR15787
	;	xecute "do "_strCallBack_"(objResult)"
		if strCallBack'["(" {
			set strCallBack = strCallBack_"(objResult)"
		}
		xecute "do "_strCallBack
	; ^^^  SR15787
	//	Use instead ?
	//	do ExecuteCode^COMUtils("do "_strCallBack)
	
	} elseif $extract(strRelation,1)="J" {
		do GridClick^COMViewFilter(pidKey,YFORM_",",1)
	}
	
	quit $$$OK
	
]]></Routine>
</Export>