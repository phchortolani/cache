<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMCodeCheckShow" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMCodeCheckShow ; Code Check Show Edited Code
	; Uses runSourceLogging=1
	
#include WWWConst
#include COMSYS
#include %occInclude


List()
	;-------------------------------------------------------------------------------
	; List Edited Routines from ^%COMRoutineLog
	; 27-Oct-2013	SCR			Save Error Text v
	;-------------------------------------------------------------------------------
	new arrRtn,dtCompiled,dtLastEdited,intErrors,intTags,intUnused,strGlo,strLink
	new strNSpace,strRec,strRtn,strType
	&html<
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
</head>
<body>
<table border="1">
<tr>
<th>Routine</th>
<th>NameSpace</th>
<th>Last Saved</th>
<th>Errors</th>
<th>Unused</th>
<th>Tags</th>
</tr>


	>
	
	set strGlo	= "^%COMRoutineLog(1,99999)"
	for {
		set strGlo	= $query(@strGlo,-1)
		quit:strGlo=""
		quit:$qs(strGlo,1)'=1
		set strRec	= @strGlo
		set strRtn	= $piece(strRec,",",1)
		if $data(arrRtn(strRec)) {
			kill @strGlo  ; Kill Repeated Entries
			continue
		}
		set dtLastEdited	= $zdt($qs(strGlo,2)_","_$qs(strGlo,3),3) 
		set arrRtn(strRec)	= dtLastEdited
		set strType	= $piece(strRtn,".",$length(strRtn,"."))
		continue:strType'="MAC"
		set strRtn	= $piece(strRtn,".",$length(strRtn,".")-1)
		set strNSpace	= $piece(strRec,",",2)
		set dtCompiled	= $get(^[strNSpace]rMAC(strRtn,0))
		if dtCompiled'=$get(^%COMRoutineLog(2,strNSpace,strRtn,"Compiled")) {
			if $znspace = strNSpace {
				set intErrors	= $$Show^COMCodeCheck(strRtn_".INT",0,0)
				set strLink		= "<A HREF=""COM.CodeCheck.cls?Code="_strRtn_".INT"">"_strRtn_"</A>"
			} else {
				set intErrors	= $$Show^COMCodeCheck(strRtn_".INT",0,0,strNSpace)
				set strLink		= "<A HREF=""COM.CodeCheck.cls?Code="_strRtn_".INT&amp;NameSpace="_strNSpace_""">"_strRtn_"</A>"
			}
			set ^%COMRoutineLog(2,strNSpace,strRtn,"Errors")	= intErrors
			set ^%COMRoutineLog(2,strNSpace,strRtn,"Compiled")	= dtCompiled
		} else {
			set intErrors	= $get(^%COMRoutineLog(2,strNSpace,strRtn,"Errors"))
			set strLink		= "<A HREF=""COM.CodeCheck.cls?Code="_strRtn_".INT&amp;NameSpace="_strNSpace_""">"_strRtn_"</A>"
		}
		set intUnused	= $piece(intErrors,",",2)
		set intTags		= $piece(intErrors,",",3)
		set intErrors	= $piece(intErrors,",",1)
		&html<
		<tr>
		<td>#(strLink)#</td>
		<td>#(strNSpace)#</td>
		<td>#(dtLastEdited)#</td>
		<td>#(intErrors)#</td>
		<td>#(intUnused)#</td>
		<td>#(intTags)#</td>
		</tr>
		>
	}
	&html<
	</table>
	</body>
	>
	
	quit
]]></Routine>
</Export>