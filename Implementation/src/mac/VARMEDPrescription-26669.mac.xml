<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARMEDPrescription" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARMEDPrescription
#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst
#include COMSYS

OnAfterDataFieldsCustom(pidPres,pobjPres)
	;-------------------------------------------------------------------------------
	; Code to run when called by the AfterDataField event on the @netManager form.
	;
	; History:
	; 15-Sep-2010	PPP		SR16642: Added the List of Issues COMTable
	; 25-Jun-2009	DWR		SR16642: Created
	;-------------------------------------------------------------------------------
	new idIssue
	
	set YSTOP = $$$YES
	
	set YVOR = $get(^WWW120(0,YFORM,1))
	do PrintJS^VARMEDPrescriptionLine(YVOR)
	do PrintJS^VARMEDPrescription(YVOR)
	
	if '$$$NoKey(pidPres) {
		do LoadGrid^MEDPrescription(pidPres,pobjPres)
		
		if ($order(^INIssues(0,7,$$$Index(pidPres),""))'="") {   // SR16642:Issues/Dispenses
			quit:($$$MEDPrescriptionStatus(pobjPres)=8)
			
			do ShowIssue^MEDPrescriptionShow(pidPres)
		}
	}
	quit

OnBlurPaciente(pYFELD="")
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescription - F2 
	;                                      do OnBlurPaciente^VARMEDPrescription(YFELD)
	; Load COMTempList with Authorized person by Patient ID
	;
	; History:
	; 23-MaR-2011	Karine		Creates: Customization for Prescription
	; 							Created by Natalias's demand
	;--------------------------------------------------------------------------------
	
	new idPaciente, noResponsavel, noPaciente, objAutorizacao, idInternacao, dtAlta, dtAdmission, objAdmission, tpDispensacao
	
	set objAutorizacao = ""
	if pYFELD '= "" {
	
		set %TXT(1) = ""	
		
		kill ^COMTempList(YM,YBED,"ResponsavelPeloPaciente")
	
		set idPaciente = $$$MEDPrescriptionPatientID(pYFELD)
		set tpDispensacao = $$$MEDPrescriptionIssueType(pYFELD)
	
		if idPaciente '= "" {
		  if ((tpDispensacao '= "E") && (tpDispensacao '= "I")) { 		
			set %TXT(1)=%TXT(1)_"#YMEDPrescriptionD31~REMOVE"	 ;MEDAuthorize
			set %TXT(1)=%TXT(1)_"#YMEDPrescriptionD20~REMOVE"	 ;MEDAdmission
			set %TXT(1)=%TXT(1)_"#YMEDPrescriptionD13~REMOVE"   ;Local Disp
			set position = 0
			
			// Relação da Autorização por paciente
			$$$Order4(^MEDAuthorizes,YM,100,idPaciente,idAutorizacao)
				set objAutorizacao=$get(^MEDAuthorize(0,idAutorizacao,1))
				set noResponsavel  = $piece(objAutorizacao,Y,2)
				
				set position = $i(position)
				set %TXT(1) = %TXT(1)_"#YMEDPrescriptionD31"_"~"_idAutorizacao_"~"_noResponsavel_"~"_position
				set ^COMTempList(YM,YBED,"ResponsavelPeloPaciente",idAutorizacao,1) = idPaciente_Y_noResponsavel
			$$$End
			
			set position = 0
		  }	
		}
		if objAutorizacao = "" {
			set %TXT(1)=%TXT(1)_"#YMEDPrescriptionD20~REMOVE"	 ;MEDAdmission
			set %TXT(1)=%TXT(1)_"#YMEDPrescriptionD13~REMOVE"   ;Local Disp
		}
		
	}	
	quit
	
OnBlurDueDate(pYINHALT)
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescription F12
	; 
	; History :
	; 24-Mar-2011	Karine	Created
	;-------------------------------------------------------------------------------
	if ($zdateh(pYINHALT,4) < $horolog) {
		$$$Alert("A data desejada não pode ser menor que a data atual.")
		set %TXT(1)=%TXT(1)_"#Y"_YFORM_"D14~"_""
	}
	quit	
	
	
getRegistroInternacao(pYKEY, pYFELD="")
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescription D20/F5 - ReasonForAdmission - VARRules
	; 
	; History :
	; 05-Jul-2011	Karine	Created
	; @$$getRegistroInternacao^VARMEDPrescription(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	new idPaciente, idInternacao, objAdmission, dtAdmission, dtAlta, vLocalIn, position, setIDinternacao
	
	set setIDinternacao = ""
	
	if '$$$NoKey(pYKEY) {
		
		set idPaciente = $$$MEDPrescriptionPatientID(pYFELD)
		set tpDispensacao = $$$MEDPrescriptionIssueType(pYFELD)
		
		if idPaciente '= ""{
			if (tpDispensacao = "E") || (tpDispensacao = "I") {
				set setIDinternacao = $$getRegInternacaoValida^VARMEDAdmission(idPaciente)
			}		
		}
	}
	
	quit setIDinternacao	
	

	
getRegistroLocalInternacao(pYFELD="")
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescription D20/F5 - ReasonForAdmission - VARRules
	; 
	; History :
	; 05-Jul-2011	Karine	Created
	; @$$getRegistroLocalInternacao^VARMEDPrescription(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	new idPaciente, idInternacao, objAdmission, dtAdmission, dtAlta, vLocalIn, position, setIDinternacao
	
	set setLocalinternacao = ""
	
	
	if pYFELD '= "" {
		
		set idInternacao = $$$MEDPrescriptionReasonForAdmission(pYFELD)
		
		if (idInternacao '= "") {
			set tpDispensacao = $$$MEDPrescriptionIssueType(pYFELD)
		
			if ((tpDispensacao = "E") || (tpDispensacao = "I")) {
		
				set objAdmission=$get(^MEDAdmission(YM,idInternacao,1))
				set setLocalinternacao = $piece(objAdmission,Y,2)
			}	
		}	
	}
	
	quit setLocalinternacao	
	
	
OnBeforeDataAccess(pYKEY,pYFELD,pYFORM)
	;-------------------------------------------------------------------------------
    ; Chamada em VARHooks da CLASSE INPARA : OnBeforeDataAccess^VARMEDPrescription(.YKEY,YFELD,YFORM)
    ; Filtra os dados da INPARA, para o parâmetro : ISSUETYPE
    ;                                               Não apresentar o "W" - Venda
    ;																 "H" - Programas de saúde				
    ; 
    ; History:
    ; 06-Jul-2011	Karine		Created
    ;-------------------------------------------------------------------------------	
	new blnFilter, perfilUsuario, keyParam, keyLang, keyType
	set blnFilter = $$$YES

	set keyType = ""
	if '$$$NoKey(pYKEY) {
		if (pYFORM = "MEDPrescription") || (pYFORM = "INARTPACK")  {
	
			//Não aplicar filtros ao administrador do sistema
			set perfilUsuario = $piece($get(^WWW013(YM,YBED,1)),Y,3)
			quit:(perfilUsuario = 1) $$$YES
	
			set keyParam = $piece(pYKEY,",",1)  ; ISSUETYPE
			set keyLang = $piece(pYKEY,",",2)   ; PT
			set keyType = $piece(pYKEY,",",3)   ; se W e H, não mostrar
		
			quit:(keyParam '= "ISSUETYPE") $$$YES
	
			if (keyType = "W") || (keyType = "H") {
				set blnFilter = $$$NO
				set YKEY = ""
			}
		}
	}	
	quit blnFilter
	
	
CanCreateReq(pYKEY)
	quit:($$$NoKey(pYKEY))
	
	//Movimentação de abastecimento

	new strStatus, objPrescricao, blnValidFarmaceutica, tipoDispensacao, blnValidFarmaceutica,
		status, DispensingLocn, statusAbastecimento
	
	set strStatus = $$$OK
	set objPrescricao = $get(^MEDPrescription(YM,pYKEY,1))

	set YQ = $$$YQEnable
	
	set tipoDispensacao = $$$MEDPrescriptionIssueType(objPrescricao)
	if (tipoDispensacao '= "I") {
		set YQ = $$$YQDisable("A prescrição não é do tipo internado.")
		quit
	}
		
	set blnValidFarmaceutica = $$$MEDPrescriptionFREE2(objPrescricao)	
	if (blnValidFarmaceutica '= $$$YES) {
		set YQ = $$$YQDisable("A prescrição não exige validação farmacêutica.")
		quit
	}
	
	set status = $$$MEDPrescriptionStatus(objPrescricao)
	if (status = 1) {
		set YQ = $$$YQDisable("A prescrição não está confirmada.")
		quit	
	
	} elseif (status > 3) {
		set YQ = $$$YQDisable("A prescrição	não está mais em atendimento.")
		quit
	}
	
	set DispensingLocn = $$$MEDPrescriptionDispensingLocn(objPrescricao)
	if (DispensingLocn = "") {
		set YQ = $$$YQDisable("O local de dispensação precisa estar preenchido.")
		quit
	}

	set statusAbastecimento = $$GetStatusAbastecimento(pYKEY)
	if (statusAbastecimento > 1) {
		set YQ = $$$YQDisable("A dispensação já foi realizada.")
		quit
	} 

	quit

CreateReq(pYKEY)
	quit:($$$NoKey(pYKEY))

	new strStatus, idRequisicao, idUnfirmedReq
	
	set strStatus    = $$$OK
	
	//Verifica se já abriu uma requisição para a prescrição
	set idUnfirmedReq = $$GetUnfirmedReq(pYKEY)
	
	if (idUnfirmedReq) {
		do GoToForm^COMUtilForm("INReq",idUnfirmedReq)
		quit
		
	} else {
		set idRequisicao = $$^WWWNEXT("INReq")
		set strStatus = $$Transaction^COMTransaction("CreateReqFromPrescription^VARMEDPrescription("""_pYKEY_""","""_idRequisicao_""")")
	}
	
	//Redireciona para a tela de requisição
	if $$$ISOK(strStatus) {		
		do GoToForm^COMUtilForm("INReq",idRequisicao)
	} else {
		do ReturnError^COMUtilError(strStatus)
	}

	quit

CreateReqFromPrescription(pYKEY,pidRequisicao)
	quit:($$$NoKey(pYKEY))

	new objPrescricao, strStatus, objRequisicao, idPaciente, idRequisicaoLinha, 
		objRequisicaoLinha, key, idPrescricaoLinha, objPrescricaoLinha
		
	set strStatus = $$$OK
		
	set objPrescricao = $get(^MEDPrescription(YM,pYKEY,1))
	quit:(objPrescricao = "")

	set idPaciente = $$$MEDPrescriptionPatientID(objPrescricao)
	
	set strStatus	 = $$$OK

	set objRequisicao = ""
	
	set $$$INReqStatus(objRequisicao)   = 1 		  //Criada
	set $$$INReqDate1(objRequisicao)    = $piece($horolog,",",1)
	set $$$INReqToLocn(objRequisicao)   = $$$MEDPrescriptionDispensingLocn(objPrescricao)
	set $$$INReqFromLocn(objRequisicao) = YLOCATION
	set $$$INReqDueDate(objRequisicao)  = $piece($horolog,",",1)
	set $$$INReqPriority(objRequisicao) = 2
	set $$$INReqType(objRequisicao)     = 1 		  //Entre Departamentos	
	set $$$INReqFREE8(objRequisicao)	= pYKEY		  //No. da Prescrição, para utilizar como referência
	set $$$INReqNotes(objRequisicao)   = "Movimentação de abastecimento para o núcleo de enfermagem criada para atender a prescrição nº "_YKEY_", paciente "_idPaciente_" - "_$piece($get(^MEDPatient(YM,idPaciente,1)),Y,23)_"."
	
	set strStatus = $$$Save("INReq",pidRequisicao,objRequisicao,$$$YES)
	
	if $$$ISOK(strStatus) {
						   	
   		set idRequisicaoLinha = 0
   		
   		$$$Order3(^MEDPrescriptionLine,YM,pYKEY,idPrescricaoLinha)
   			
   			set idRequisicaoLinha = $increment(idRequisicaoLinha)
   			
   			set objPrescricaoLinha = $get(^MEDPrescriptionLine(YM,pYKEY,idPrescricaoLinha,1))
   			continue:(objPrescricaoLinha = "")
   			
   			set objRequisicaoLinha = ""
   			set $$$INReqLineItem(objRequisicaoLinha)          = $$$MEDPrescriptionLineItem(objPrescricaoLinha)
   			set $$$INReqLineUnit(objRequisicaoLinha)          = $$$MEDPrescriptionLineDispenseUOM(objPrescricaoLinha)
   			set $$$INReqLineStatus(objRequisicaoLinha)        = 1
   			set $$$INReqLineFromStockLocn(objRequisicaoLinha) = YLOCATION   			
   			set $$$INReqLineQtyOrdered(objRequisicaoLinha)    = $$$MEDPrescriptionLineDispenseQuantity(objPrescricaoLinha)
   			set $$$INReqLineQtyRequired(objRequisicaoLinha)   = $$$MEDPrescriptionLineDispenseQuantity(objPrescricaoLinha)
   			set $$$INReqLineUnitPrice(objRequisicaoLinha)     = 0 //Mesmo comportamento quando utilizada a tela
   			
   			set key = pidRequisicao_$$$COMMA_idRequisicaoLinha
   			
   			set strStatus = $$$Save("INReqLine",key,objRequisicaoLinha,$$$YES)
   			
   		$$$End
	}
	
	quit strStatus
	
	
GetStatusAbastecimento(pYKEY)
	quit:($$$NoKey(pYKEY)) 0
	$$$VAR
	
	new objPrescricao, statusAbastecimento, idRequisicao, objRequisicao	
	set objPrescricao = $get(^MEDPrescription(YM,pYKEY,1))
	
	set statusAbastecimento = 0
	
	if ($$$MEDPrescriptionIssueType(objPrescricao) '= "I") {
		set statusAbastecimento = "NA"		
	
	} else {
		
		$$$Order4(^INReqs,YM,200,$$$Index(pYKEY),idRequisicao)
		
			set objRequisicao = $get(^INReq(YM,idRequisicao,1))
			
			if $$$INReqStatus(objRequisicao) = 9 {				
				set statusAbastecimento = 2	
			} else {
				set statusAbastecimento = 1
			}		
		$$$End		
	}
	
	quit statusAbastecimento
	
GetUnfirmedReq(pYKEY)
	quit:($$$NoKey(pYKEY)) 0
	
	new idReq, idRequisicao, objRequisicao
	
	set idReq = ""
	$$$Order4(^INReqs,YM,200,$$$Index(pYKEY),idRequisicao)		
		set objRequisicao = $get(^INReq(YM,idRequisicao,1))
			
		if ($$$INReqStatus(objRequisicao) <= 2) {
			set idReq = idRequisicao
			quit
		}		
	$$$End

	quit idReq
	
PrintJS(pobjForm)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^MEDPrescription.mac
    ; 
    ; Histórico:
    ; 	26-Jan-2012		Criado
    ;-------------------------------------------------------------------------------
	new idClass, strP1Name
 
	set idClass = $$$WWW120ClassUsedInForm(pobjForm)
	set strP1Name = "Y"_idClass_"P1"
	
	write !, "<script language=""javascript"">"
	&js<
	// metodo personalizado para abrir a janela centralizada no firefox
	// quando passar h = 0 a função fará um calculo aproximado da altura da janela pelo tamanho da msg
	function showModalDialogCenter(url, msg, w, h) {
	    var width = screen.availWidth;
	    var height = screen.availHeight;
	    var altura = msg.length;
	    if (h==0){  
	        h = altura / 1.5;
	    }
	    var dialogLeft = ((width - w) / 2);
        var dialogTop = ((height - h) / 2);
		var settings = " center: yes; " +
		"edge: raised; scroll: yes; status: no; ";
		var screenSize = " dialogWidth:"+w+"px; dialogHeight:"+h+"px;";
		var centerScreen = " dialogTop="+dialogTop+"; dialogLeft=" +dialogLeft+ ";";
		var setting =settings+screenSize+centerScreen;
		return window.showModalDialog(url,msg,setting);
	} 	
	var pIdPrescription = document.#(YHTMFORM)#.#(strP1Name)#.value;
 
	function iePrompt(message) {
		var settings = "dialogWidth: 420px; dialogHeight: 200px; center: yes; " +
			"edge: raised; scroll: no; status: no";		
		return showModalDialogCenter("#(YGIF)#VARPromptSize.html", message,420,170);
	}
	function motivoRecusa() {
		CallBackNow("InsertMotiveOnClickJS^VARMEDPrescription", pIdPrescription);
	}
	function VerificarInteracao(){
		CallBackNow("ShowMessageClickJS^VARMEDPrescription", pIdPrescription);
	}
	function mostrarInteracoes(){
		CallBackNow("ShowMessageInteracao^VARMEDDrugInteraction", pIdPrescription);
	}
	function mostraAvisoConfirmacao(){
		var settings = "dialogWidth: 400px; dialogHeight: 120px; center: yes; " +
		"edge: raised; scroll: no; status: no";
		var message = "Você tem certeza que deseja concluir o documento?</br>Todas as interações medicamentosas foram avaliadas?"
		var retorno = showModalDialogCenter("#(YGIF)#VARConfirmMessageShow.html", message,320,0);
		if (retorno){			
			CallBackNow("ConfirmaPrescricao^VARMEDPrescription", pIdPrescription);
		}
	}
	function ShowMsgInteracao(message,height) {		 
		return showModalDialogCenter("#(YGIF)#VARMessageShow.html", message,600,height);
	}
	
	>
	write !, "</script>"
	quit
	
InsertMotiveOnClickJS(pIdPrescription)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função PrintJS^VARMEDPrescription.mac
    ; 
    ; Histórico:
    ; 	26-Jan-2012		Criado
    ;-------------------------------------------------------------------------------
	new objPrescription
 
	if ($length($get(pIdPrescription)) = 0) {
		set objPrescription = ""
	}
	else {
		set objPrescription = $get(^MEDPrescription(0,pIdPrescription,1))
	}
	&js<
	    var motivo = iePrompt('Por favor, insira o motivo para a recusa/cancelamento da prescrição');
		if (motivo) {
			CallBackNow("InsertMotiveOnClick^VARMEDPrescription", '#(pIdPrescription)#', motivo);
		}
	> 
	quit


InsertMotiveOnClick(pIdPrescription,strMotivoRecusa)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função InsertMotiveOnClickJS^VARMEDPrescription.mac
    ; 
    ; Histórico:
    ; 	26-Jan-2012		Criado
    ;-------------------------------------------------------------------------------
	new objPrescription,strMotivoRecusaFormatado,i,strStatus,YSEITE
	
	//set objPrescription = $$GetFormData^COMUtilForm(YFORM)
 	
 	
	if ($length(strMotivoRecusa) = 0) {
		$$$StartScript()
		write "alert('Favor inserir o motivo da recusa.');"
		$$$EndScript()
	}
	else {
		if ($length(strMotivoRecusa, """") = 1) {
			set strMotivoRecusaFormatado = strMotivoRecusa
		}
		else {
			set strMotivoRecusaFormatado = ""
			for i = 1:1:$length(strMotivoRecusa, """") {
				set strMotivoRecusaFormatado = strMotivoRecusaFormatado_
					$piece(strMotivoRecusa, """", i)_""""""
			}
		}
  		set strMotivoRecusaFormatado = "Motivo da recusa/cancelamento da prescrição: "_strMotivoRecusaFormatado
		set strStatus = $$Transaction^COMTransaction("InsertMotive^VARMEDPrescription("""_pIdPrescription_""","""_strMotivoRecusaFormatado_""")", $$$YES)
		if $$$ISOK(strStatus) {
			$$$StartScript()
			write "alert('Prescrição recusada com sucesso!');"
			$$$EndScript() 
			set YSEITE = 3
			//do GoToForm^COMUtilForm(YFORM, pIdPrescription, 4,, $$$NO)
			do RedirectForm^COMUtilForm(YFORM,pIdPrescription,YBACK,YPARA,YSEITE)
		}
		else {
			$$$StartScript()
			write "alert('Erro ao recusar a prescrição. => "_$$DecodeError^COMUtilError(strStatus)_"');"
			$$$EndScript()
		}
	}
	quit ""
	
InsertMotive(pIdPrescription,pStrMotivo)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função InsertMotiveOnClick^VARMEDPrescription.mac
    ; 
    ; Histórico:
    ; 	26-Jan-2012		Criado
    ;-------------------------------------------------------------------------------
	new blnCancelar, strStatus, objPrescription
 
	set objPrescription = $get(^MEDPrescription(YM,pIdPrescription,1))
	if ($length($$$MEDPrescriptionNotes(objPrescription)) > 2){
		set $$$MEDPrescriptionNotes(objPrescription) =$$$MEDPrescriptionNotes(objPrescription)_$CHAR(10,13)_pStrMotivo
	}else{
		set $$$MEDPrescriptionNotes(objPrescription) =$$$MEDPrescriptionNotes(objPrescription)_pStrMotivo
	}
	set $$$MEDPrescriptionStatus(objPrescription) = 8    ; M-Close
	set strStatus = $$$Save("MEDPrescription",pIdPrescription,objPrescription,1)
	if $$$ISERR(strStatus) {
		set $zerror = "Erro ao recusar a prescrição. => "_$$DecodeError^COMUtilError(strStatus)
		zquit 1 GOTO @$ZTRAP
	}
	quit $$$OK
	
ShowMessageClickJS(pIdPrescription)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função PrintJS^VARMEDPrescription.mac
    ; 
    ; Histórico:
    ; 	26-Jan-2012		Criado
    ;-------------------------------------------------------------------------------
	set exist = $$ExisteInteracoes^VARMEDDrugInteraction(pIdPrescription)
	if (exist){ 
		&js<
		    mostraAvisoConfirmacao();
		> 
	}else{
		do ConfirmaPrescricao^VARMEDPrescription(pIdPrescription)
	}
	quit

ConfirmaPrescricao(id)
	;teste
	set sc = $$Firm^MEDPrescription(id)
	set YSEITE = 3
	do RedirectForm^COMUtilForm(YFORM,id,YBACK,YPARA,YSEITE)
	quit
	
CheckRelatorioEnable(pYKEY)
	quit:($$$NoKey(pYKEY))

	new objPrescricao, status
	
	set objPrescricao = $get(^MEDPrescription(YM,pYKEY,1))

	set YQ = $$$YQEnable
	
	set status = $$$MEDPrescriptionStatus(objPrescricao)
	
	if (status = 1) {
		set YQ = $$$YQDisable("A prescrição não está confirmada.")
		quit	
	} 
	
	quit
	
Imprimir(pIdPrescription)
	;do Imprimir^VARMEDPrescription(YKEY)
	
	do RunReportPrescricao^VARJasperRunReport(pIdPrescription)
	
	quit
]]></Routine>
</Export>