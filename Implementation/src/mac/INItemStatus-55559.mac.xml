<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INItemStatus" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INItemStatus
	
#include COMSYS
#include INItemStatus
	
GetColorCode(pintValue)
	;-------------------------------------------------------------------------------
	; 
	; Called By: FARBE1^WWWFORM7
	; 
	; Returns: The user-defined colour code associated with an Item Status code
	; 
	; History:
	; 09-Mar-2011	shobby	SR17680: created
	;-------------------------------------------------------------------------------
	quit $select(pintValue="":"",1:$$$INItemStatusColorCode($get(^INItemStatus(0,pintValue,1))))
	
	
OnBeforeSave(pidStatus,pobjStatus)
	;-------------------------------------------------------------------------------
	; If changing default, update current default
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 18-Oct-2010	GRF		SR17309: Boolean macros
	; 26-May-2010	PPP		SR17309: Created
	;-------------------------------------------------------------------------------
	new idDefault,idGroup,idParent,objDefault,objGroup,strStatus
	
	set strStatus = $$$OK
	
	if $$$INItemStatusDefault1(pobjStatus) {
		// Can only have one default, so if we have just made this one default,
		// remove check from old default
		set idDefault = $$$DefaultStatus
		if (idDefault'="") && (idDefault'=pidStatus) {
			set objDefault = $get(^INItemStatus(0,idDefault,1))
			set $$$INItemStatusDefault1(objDefault) = $$$NO
			set strStatus = $$$Save("INItemStatus",idDefault,objDefault,$$$YES)
		}
	}
	
	if $$$ISERR(strStatus) {
		$$$Error(strStatus)
		set Q = $$$QDontSave
	}
	quit strStatus
	
	
OnBeforeDelete(pidStatus,pobjStatus)
	;-------------------------------------------------------------------------------
	; Can we delete?  ie is it the default, or on a supplier?
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 26-May-2010	PPP		SR17309: Created
	;-------------------------------------------------------------------------------
	new idxStatus,strStatus
	
	set strStatus = $$$OK
	
	if $$$INItemStatusDefault1(pobjStatus) {
		set strStatus = $listbuild("IN00663")  	                ; "The default status cannot be deleted."
		
	} else {
		set idxStatus = $$$Index(pidStatus)
		
	//	if $data(^INARTs(0,2,idxStatus)) || $data(^FINAPSuppliers(0,3,idxStatus)) {
	//		set strStatus = $listbuild("IN00587",pidStatus)     ; "This record (%1) is in use. It cannot be deleted."
	//	}
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontDelete_strStatus
	}
	quit strStatus
	
]]></Routine>
</Export>