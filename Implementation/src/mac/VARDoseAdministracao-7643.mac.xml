<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARDoseAdministracao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include INConst
#include VARConst
#include MEDConst
#include VARDose

VARDoseAdministracao
	quit

OnBeforeButtonLine()
	set YOPTION = $get(YSEITE)
	&html<
		<script type='text/javascript'>
			function dosePrompt(barCode, idDose) { 
				eval('var url ="#(YAKTION)#EP=WWWFORM&YFORM=VARDoseAdministracao&YPARA=#($piece(YPARA,",",1))#,'+barCode+','+idDose+',#($piece(YPARA,",",4))#&YUSER=#(YUSER)#&YBED=#(YBED)#"'); 
				this.location.href = url;
			}
		</script>
		
	>
	quit

OnAfterButtonLine(pYPARA)
	new idMEDPatient, idMEDAdmission, objMEDAdmission, barCode, Item, Lote,
		Prescricao, Linha, Dose, keyDose, Situacao, Dose, Via, Frequencia,
		Inicio, UltimaDose, ProximaDose, Validade, Prescritor, Duracao,
		LinhaPrescricaoStatus, LocalLote, Unidade, InfoPrescricao,
		idVARDoseAdministracaoLinha, idVARDoseAdministracao, objVARDoseAdministracaoLinha

	if (YSEITE = $$$abaConfirmar) {
		set idMEDPatient	= $piece(pYPARA,",",1)
		if ($length(idMEDPatient) = 0) quit

		set idMEDAdmission	= $$GetOpenAdmission^MEDAdmission(idMEDPatient)
		if ($length(idMEDAdmission) = 0) quit
		set objMEDAdmission	= ^MEDAdmission(YM,idMEDAdmission,1)

		set barCode		= $piece(pYPARA,",",2)
		if ($length(barCode) = 0) quit
		set Item		= $piece(barCode,"@",1)
		set Lote		= $piece(barCode,"@",2)

		set keyDose		= $piece(pYPARA,",",3)
		if ($length(keyDose) = 0) quit
		set Prescricao	= $piece(keyDose,"@",1)
		set Linha		= $piece(keyDose,"@",2)
		set Dose		= $piece(keyDose,"@",3)

		set idVARDoseAdministracao = $piece(pYPARA,",",4)
		if ($length(idVARDoseAdministracao)'=0){
			set idVARDoseAdministracaoLinha = $order(^VARDoseAdministracaoLinhas(0,1,Item,$$$Index(Lote),idVARDoseAdministracao,""),-1)
		}
		if ($get(idVARDoseAdministracaoLinha) '= "") {
			set VORG(1) = $$$VARDoseAdministracaoLinhaObservacao(^VARDoseAdministracaoLinha(0,idVARDoseAdministracao,idVARDoseAdministracaoLinha,1))
		}else{
			set VORG(1) = ""
			set VORG(2) = ""
			set VORG(3) = ""
		}

		&sql(
		DECLARE Administracao CURSOR FOR
		SELECT
			  Dose.Situacao
			, Dose.QtdDose||" "||$$SQLGetSiglaUnit^VARSQL(Dose.Unidade)
			, $$GetVia^VARSQL(Dose.Via)
			, $$GetFrequency^VARSQL(Dose.Frequencia)
			, $$GetInicio^VARDose(Dose.Prescricao, Dose.Linha)
			, Dose.UltimaDose
			, Dose.ProximaDose
			, Bundle->UseByDate
			, Dose.Prescritor
			, Dose.Duracao
			, Dose.LinhaPrescricaoStatus
			, Storage->Location AS LocalLote
			, Dose.Unidade
			, Dose.InfoPrescricao
		INTO
			  :Situacao
			, :Dose
			, :Via
			, :Frequencia
			, :Inicio
			, :UltimaDose
			, :ProximaDose
			, :Validade
			, :Prescritor
			, :Duracao
			, :LinhaPrescricaoStatus
			, :LocalLote
			, :Unidade
			, :InfoPrescricao
		FROM Report.VARDose Dose
		INNER JOIN alSOH.dBundleStock Bundle
								ON Bundle.Item = Dose.Item
		WHERE	Bundle->LotNumber = :Lote
				AND Dose.Prescricao = :Prescricao
				AND Dose.Linha = :Linha
				AND Dose.Dose = :Dose
		)
		&sql(OPEN Administracao)
		&sql(FETCH Administracao)
		if (SQLCODE = 0) {
			while (SQLCODE = 0) {
				if ((LinhaPrescricaoStatus '= $$$PrescricaoSuspensa)||(LocalLote '= YLOCATION)) quit
				&sql(FETCH Administracao)
			}
			&sql(CLOSE Administracao)
			if (UltimaDose '= "-"){set UltimaDose = $zdatetime(UltimaDose,4,2)}
			if ($length(Validade) = 0){
				set Validade = "-"
			}
			else {
				set Validade = $zdate(Validade,4,,4)
			}
		}

		&html<	
			<div style="width:575px; font-size:13px; margin-top:20px; font-family:Arial, Helvetica, sans-serif">
				<div style="margin:0px 0px 20px 0px; font-weight:bold; text-align:center;"><h2>#($$SQLGetDescricaoProduto^VARSQL($get(Item)))#</h2></div>
				<div>
					<table style="font-size:12px;" cellspacing="3">
						<tr>
							<td ><strong>Dose:</strong></td>
							<td>#($get(Dose))#</td>
							<td ><strong>Lote:</strong></td>
							<td>#($get(Lote))#</td>
						</tr>
						<tr>
							<td ><strong>Via:</strong></td>
							<td>#($get(Via))#</td>
							<td ><strong>Validade:</strong></td>
							<td>#($get(Validade))#</td>
						</tr>
						<tr>
							<td ><strong>Frequência:</strong></td>
							<td>#($get(Frequencia))#</td>
							<td ><strong>Quantidade:</strong></td>
							<td>1 #($$SQLGetDescUnit^VARSQL($$GetPickUOM^INUOMConversion($get(Item),"I")))#</td>							
						</tr>
						<tr>
							<td ><strong>Início:</strong></td>
							<td>#($get(Inicio))#</td>
							<td ><strong>Duração:</strong></td>
							<td>1 dia</td>
						</tr>
						<tr>
							<td ><strong>Próxima dose:</strong></td>
							<td width="150"><font color="blue"><strong>#($zdatetime($get(ProximaDose),4,2))#</strong></font></td>
							<td><strong>Última dose:</strong></td>
							<td>#($get(UltimaDose))#</td>
						</tr>
						<tr>
							<td ><strong>Prescritor:</strong></td>
							<td colspan="3">#($$GetNomeMedico^VARSQL($get(Prescritor)))#</td>
						</tr>
						<tr>
							<td colspan="4">#($$GetInfo($get(InfoPrescricao)))#&nbsp;</td>
						</tr>																																																				
					</table>
				</div>
			</div>
		>
	}
	elseif (YSEITE = $$$abaOmitir) {
		set idVARDoseAdministracao		= $piece(pYPARA,",",2)
		if ($length(idVARDoseAdministracao) = 0) quit
		set idVARDoseAdministracaoLinha	= $piece(pYPARA,",",3)
		if ($length(idVARDoseAdministracaoLinha) = 0) quit
		
		set objVARDoseAdministracaoLinha = $get(^VARDoseAdministracaoLinha(0,idVARDoseAdministracao,idVARDoseAdministracaoLinha,1))
		set VORG(2) = $$$VARDoseAdministracaoLinhaMotivo(objVARDoseAdministracaoLinha)
		set VORG(3) = $$$VARDoseAdministracaoLinhaObservacao(objVARDoseAdministracaoLinha)
	}



	quit

OnAfterDataField(pYPARA)
	new lstHeader, Item, Lote, Validade, FmtValidade, Quantidade, idVARDoseAdministracao, 
		Observacao, QtdDose, Unidade, Status, objMEDPrescriptionLine, Prescricao, Linha,
		count

	
	if (YSEITE = $$$abaLista) {
		do PrintJS

		set idMEDPatient			= $piece(pYPARA,",",1)
		set idVARDoseAdministracao	= $piece(pYPARA,",",4)

		set keyDose = $piece(pYPARA,",",3)
		if ($length(keyDose) = 0) quit
		set Prescricao = $piece(keyDose,"@",1)
		set Linha = $piece(keyDose,"@",2)
		set Dose = $piece(keyDose,"@",3)

		set lstHeader = ""
	  	set lstHeader = lstHeader_$listbuild("Medicamento","Lote","Validade","Quantidade","Status","Observação")
		do Start^COMTable(lstHeader)
		
		&sql(
		DECLARE DoseAdministracao CURSOR FOR
		SELECT DISTINCT
				  Adm.Item
				, Adm.Lote
				, Adm.Validade
				, Adm.Quantidade
				, Adm.Observacao
				, Dose.QtdDose
				, CASE WHEN (Adm.Status = $$$VARDoseStatusOmitida) THEN "A omitir" ELSE "A confirmar" END
				, Dose.Unidade
				, Adm.Prescricao
				, Adm.Linha1
		INTO
				  :Item
				, :Lote
				, :Validade
				, :Quantidade
				, :Observacao
				, :QtdDose
				, :Status
				, :Unidade
				, :Prescricao
				, :Linha
		FROM VARDoseAdministracaoLinha Adm
		INNER JOIN Report.VARDose Dose
			ON Dose.Item = Adm.Item
			AND Dose.Paciente = :idMEDPatient
			AND Dose.LinhaPrescricaoStatus <> $$$PrescricaoSuspensa
			AND Adm.Prescricao = Dose.Prescricao
			AND Adm.Dose = Dose.Dose
		WHERE
				Administracao = :idVARDoseAdministracao				
		)
		
		&sql(OPEN DoseAdministracao)
		&sql(FETCH DoseAdministracao)
		
		set count = 0
		
		while (SQLCODE = 0){
			set FmtValidade = Validade
			if (FmtValidade '= "") {
				set FmtValidade = $zdate(FmtValidade, 4)
			}
			set objMEDPrescriptionLine = ^MEDPrescriptionLine(0,Prescricao,Linha,1)
			
			set count = $increment(count)
			
			do NewLine^COMTable()
			do InsertCell^COMTable("("_Item_") "_$$SQLGetDescricaoProduto^VARSQL(Item))
			do InsertCell^COMTable(Lote)
			do InsertCell^COMTable(FmtValidade)
			do InsertCell^COMTable(Quantidade_" de "_$$Convert^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),QtdDose,$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$GetPickUOM^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),"I",$$$YES)),,,,"CENTER")
			do InsertCell^COMTable(Status)
			do InsertCell^COMTable(Observacao,,,,,,,,,$$$YES)
			do EndLine^COMTable()
			&sql(FETCH DoseAdministracao)
		}
		&sql(CLOSE DoseAdministracao)
		
		if (count = 0) {
			do NewLine^COMTable("white")
			do InsertCell^COMTable("Nenhum medicamento adicionado para administração ainda.",,,,,,6)
			do EndLine^COMTable()	
		}
		
		do Stop^COMTable()
	}
	elseif (YSEITE = $$$abaOmitir) {
		if ($$CheckBotao(pYPARA)){
			&html<
				<script type='text/javascript'>
					document.getElementById("Y#(YFORM)#M2").disabled=true;
					document.getElementById("Y#(YFORM)#M3").disabled=true;
				</script>
			>
		}
	}
	

	quit

GetInfo(strInfo)
	if ($length(strInfo) = 0) quit strInfo
	set strHTML = "<div>"
	set strHTML = strHTML_"<div style=""float:left; margin:0px 10px 10px 10px;""><IMG SRC='"_YGIF_"botao_dose_info.gif' title='Informações do prescritor' width='30' height='30' ></div>"
	set strHTML = strHTML_"<div style=""margin-left:50px;"">"_strInfo_"</div>"
	set strHTML = strHTML_"<br/></div>"

	quit strHTML

Confirmar()
	new strObservacao, strMotivo, strStatus
	
	set strStatus  = $$$OK
	
	if (YOPTION = $$$abaConfirmar){
		set strObservacao	= $piece($get(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1)),"~",1)
		do Administrar(YPARA, strObservacao)
		do GoToPage($$$abaLista)
		
	}
	elseif (YOPTION = $$$abaOmitir){
		set strMotivo		= $piece($get(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1)),"~",2)
		set strObservacao	= $piece($get(^WWWDATEN(0,$$$KEY1($horolog),YUSER,YFORM,"M",1)),"~",3)
		if ($length(strMotivo) = 0) {
			set strStatus = $$$MakeStatus("%1", "O campo motivo é obrigatório.")
		}
		elseif ($length(strObservacao) = 0) {
			set strStatus = $$$MakeStatus("%1", "O campo observação é obrigatório.")
		}
		if ($$$ISERR(strStatus)) {
			$$$DecomposeStatus(strStatus)
			$$$Alert(strStatus)
			do GoToPage($$$abaOmitir)
		}
		else {
			do OmitirDose(YPARA, strMotivo, strObservacao)
		}
		
	}
	
	quit

Administrar(pYPARA, pstrObservacao)
	new idMEDPatient, objMEDAdmission, barCode, Item, strStatus,
		Lote, keyDose, Prescricao, Linha, Dose, objVARDoseAdministracaoLinha,
		idVARDoseAdministracaoLinha, Validade, Qtd
	
	set strStatus = $$$OK
	
	set idMEDPatient	= $piece(pYPARA,",",1)
	if ($length(idMEDPatient) = 0) quit

	set idMEDAdmission	= $$GetOpenAdmission^MEDAdmission(idMEDPatient)
	if ($length(idMEDAdmission) = 0) quit
	set objMEDAdmission	= ^MEDAdmission(YM,idMEDAdmission,1)

	set barCode		= $piece(pYPARA,",",2)
	if ($length(barCode) = 0) quit
	set Item		= $piece(barCode,"@",1)
	set Lote		= $piece(barCode,"@",2)

	set keyDose		= $piece(pYPARA,",",3)
	if ($length(keyDose) = 0) quit
	set Prescricao	= $piece(keyDose,"@",1)
	set Linha		= $piece(keyDose,"@",2)
	set Dose		= $piece(keyDose,"@",3)
	
	set idVARDoseAdministracao = $piece(pYPARA,",",4)
	if ($length(idVARDoseAdministracao) = 0) set strStatus = $$CriarAdministracao(idMEDPatient,.idVARDoseAdministracao)
	if ($$$ISERR(strStatus)) quit
	
	set idVARDoseAdministracaoLinha = $order(^VARDoseAdministracaoLinhas(0,1,Item,$$$Index(Lote),idVARDoseAdministracao,""),-1)
	if (idVARDoseAdministracaoLinha '= ""){
		set objVARDoseAdministracaoLinha = ^VARDoseAdministracaoLinha(YM,idVARDoseAdministracao,idVARDoseAdministracaoLinha,1)
		set Qtd = $$$VARDoseAdministracaoLinhaQuantidade(objVARDoseAdministracaoLinha)
		set $$$VARDoseAdministracaoLinhaObservacao(objVARDoseAdministracaoLinha)= pstrObservacao
	}
	else {
		set idVARDoseAdministracaoLinha = $$^WWWNEXT1("VARDoseAdministracaoLinha",idVARDoseAdministracao,2)
		do ##class(alSOH.iBundle).GetBundlesForLot(Item,YLOCATION,Lote,,.Validade)
		set $$$VARDoseAdministracaoLinhaPrescricao(objVARDoseAdministracaoLinha)= Prescricao
		set $$$VARDoseAdministracaoLinhaLinha1(objVARDoseAdministracaoLinha)	= Linha
		set $$$VARDoseAdministracaoLinhaDose(objVARDoseAdministracaoLinha)		= Dose
		set $$$VARDoseAdministracaoLinhaItem(objVARDoseAdministracaoLinha)		= Item
		set $$$VARDoseAdministracaoLinhaValidade(objVARDoseAdministracaoLinha)	= Validade
		set $$$VARDoseAdministracaoLinhaLote(objVARDoseAdministracaoLinha)		= Lote
		set $$$VARDoseAdministracaoLinhaObservacao(objVARDoseAdministracaoLinha)= pstrObservacao
		set $$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha)	= $$$VARDoseStatusAguardando
		set Qtd = ""
	}
	set $$$VARDoseAdministracaoLinhaQuantidade(objVARDoseAdministracaoLinha) 	= $increment(Qtd)
	set objMEDPrescriptionLine = ^MEDPrescriptionLine(0,Prescricao,Linha,1)
	if ($$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha) = $$$VARDoseStatusOmitida) {
		set strStatus = $$$MakeStatus("%1", "Essa dose já foi omitida.")
	}elseif ($$Convert^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseQty(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$GetPickUOM^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),"I",$$$YES)) < Qtd) {
		set strStatus = $$$MakeStatus("%1", "Essa dose já foi completamente administrada.")
	}
	if ($$$ISOK(strStatus)) {
		set strStatus = $$$Save("VARDoseAdministracaoLinha",idVARDoseAdministracao_$$$COMMA_idVARDoseAdministracaoLinha,objVARDoseAdministracaoLinha,1)
	}
	else{
		$$$Alert(strStatus)
	}
	
	quit

ConcluirLista()
	new idVARDoseAdministracao, Linha, objVARDose, strStatus,
		Prescricao, PrescricaoLinha, Dose, objINIssue, keyINIssue,
		objINIssueLine, keyINIssueLine, pYPARA
	
	set pYPARA = YPARA
	
	set idMEDPatient	= $piece(pYPARA,",",1)
	if ($length(idMEDPatient) = 0) do ClosePage(pYPARA)

	set idVARDoseAdministracao = $piece(pYPARA,",",4)
	if ($length(idVARDoseAdministracao) = 0) do ClosePage(pYPARA)

	set Linha		= ""
	set strStatus	= $$$OK
	set keyINIssue	= ""

	tstart

	if ($$$ISOK(strStatus)) {
		for {
			set Linha = $order(^VARDoseAdministracaoLinha(0,idVARDoseAdministracao,Linha))
			if (Linha = "") quit
			set objVARDoseAdministracaoLinha	= ^VARDoseAdministracaoLinha(0,idVARDoseAdministracao,Linha,1)
			set Prescricao						= $$$VARDoseAdministracaoLinhaPrescricao(objVARDoseAdministracaoLinha)
			set PrescricaoLinha					= $$$VARDoseAdministracaoLinhaLinha1(objVARDoseAdministracaoLinha)
			set Dose							= $$$VARDoseAdministracaoLinhaDose(objVARDoseAdministracaoLinha)
			set objVARDose						= ^VARDose(0,Prescricao,PrescricaoLinha,Dose,1)
			
			set objMEDPrescriptionLine = ^MEDPrescriptionLine(0,Prescricao,PrescricaoLinha,1)
			if (($$Convert^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseQty(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$GetPickUOM^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),"I",$$$YES)) '= $$$VARDoseAdministracaoLinhaQuantidade(objVARDoseAdministracaoLinha)) 
				&& ($$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha) '= $$$VARDoseStatusOmitida))
				{
					set strStatus = $$$MakeStatus("%1", "A dose de "_$$SQLGetDescricaoProduto^VARSQL($$$VARDoseAdministracaoLinhaItem(objVARDoseAdministracaoLinha))_" não foi completamente administrada.")
					quit
			}else{
				if ($$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha) '= $$$VARDoseStatusOmitida) {
					set $$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha)	= $$$VARDoseStatusAdministrada
				}
				set $$$VARDoseStatus(objVARDose)				= $$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha)
				set $$$VARDoseAdministradoem(objVARDose)		= $horolog
				set $$$VARDoseAdministracao(objVARDose)			= idVARDoseAdministracao
				set $$$VARDoseAdministracaoLinha(objVARDose)	= Linha
				set strStatus = $$$Save("VARDose",Prescricao_$$$COMMA_PrescricaoLinha_$$$COMMA_Dose,objVARDose,1)
				if $$$ISERR(strStatus) quit
			
				set strStatus = $$$Save("VARDoseAdministracaoLinha",idVARDoseAdministracao_$$$COMMA_Linha,objVARDoseAdministracaoLinha,1)
				if $$$ISERR(strStatus) quit
			
				if ($order(^VARDose(0,Prescricao,PrescricaoLinha,Dose)) = "") { //verifica se é a ultima dose dessa prescrição
					set $$$MEDPrescriptionLineStatus(objMEDPrescriptionLine) = $$$AdministracaoConcluida
				}
				else {
					set $$$MEDPrescriptionLineStatus(objMEDPrescriptionLine) = $$$AdministracaoParcial
				}
				set strStatus = $$$Save("MEDPrescriptionLine",Prescricao_$$$COMMA_PrescricaoLinha,objMEDPrescriptionLine,1)
				if $$$ISERR(strStatus) quit
				
				if ($$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha) '= $$$VARDoseStatusOmitida) {
					if (keyINIssue = "") {
						set keyINIssue							= $$$NextKey("INIssue")
						set $$$INIssueStatus(objINIssue)		= 0
						set $$$INIssueFromLocn(objINIssue)		= YLOCATION
						set $$$INIssueCreatedOn(objINIssue)		= $horolog
						set $$$INIssueCreatedBy(objINIssue)		= YBED
						set $$$INIssueType(objINIssue)			= 3 ;3-Dispensação / Entrega FINAL ao paciente
						set $$$INIssueChangedOn(objINIssue)		= ""
						set $$$INIssueChangedBy(objINIssue)		= ""
						set $$$INIssueCustomerID(objINIssue)	= idMEDPatient
						set $$$INIssueCustomerName(objINIssue)	= idMEDPatient						
						set $$$INIssuePatientID(objINIssue)		= idMEDPatient
						set $$$INIssueFREE7(objINIssue)			= $$GetOpenAdmission^MEDAdmission(idMEDPatient)
						set $$$INIssueToLocn(objINIssue)		= "" ;Não preencher o local de admissão como ToLocn, pois já está sendo entregue direto ao paciente
						set $$$INIssueReferrerID(objINIssue)	= $$$MEDPrescriptionProviderNumber(^MEDPrescription(0,Prescricao,1))
						set $$$INIssueDocketNo(objINIssue)		= Prescricao
						set $$$INIssueReference(objINIssue)		= Prescricao
						set $$$INIssuePosted(objINIssue)		= ""
						set strStatus = $$$Save("INIssue",keyINIssue,objINIssue,1)
					}
					if $$$ISERR(strStatus) quit

					set keyINIssueLine	= $$^WWWNEXT1("INIssueLine",keyINIssue,2)
					set $$$INIssueLineItem(objINIssueLine)				= $$$MEDPrescriptionLineItem(objMEDPrescriptionLine)
					set $$$INIssueLineUnit(objINIssueLine)				= $$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine)
					set $$$INIssueLineQtyRequired(objINIssueLine)		= $$CalcInventoryQty^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$CalculateQuantity^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$$VARDoseAdministracaoLinhaQuantidade(objVARDoseAdministracaoLinha)))
					set $$$INIssueLineQtyIssued(objINIssueLine)			= $$CalcInventoryQty^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$CalculateQuantity^INUOMConversion($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$MEDPrescriptionLineDoseUOM(objMEDPrescriptionLine),$$$VARDoseAdministracaoLinhaQuantidade(objVARDoseAdministracaoLinha)))
					set $$$INIssueLineNetworkInventory(objINIssueLine)	= $$GetQty^INARTMENGE($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$NO,,$$$NO,1,$$$NO,$$$NO)
					set $$$INIssueLineLocationInventory(objINIssueLine)	= $$GetQty^INARTMENGE($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$NO,YLOCATION,$$$NO,1,$$$NO,$$$YES)
					set $$$INIssueLineFromStorage(objINIssueLine)		= $$GetEnderecoEstoqueDisponivel^VARSQL(YLOCATION)
					set $$$INIssueLineBundle(objINIssueLine)			= $$GetBundle^VARSQL($$$MEDPrescriptionLineItem(objMEDPrescriptionLine),$$$VARDoseAdministracaoLinhaLote(objVARDoseAdministracaoLinha),YLOCATION,$$$INIssueLineFromStorage(objINIssueLine))
					set $$$INIssueLineReference(objINIssueLine)			= keyINIssue
					set strStatus = $$$Save("INIssueLine",keyINIssue_$$$COMMA_keyINIssueLine,objINIssueLine,1)
					if $$$ISERR(strStatus) quit
				}
			}
		}
		if ($$$ISOK(strStatus)&&(keyINIssue '= "")) {
			set strStatus = $$FirmSaleIssue^INIssueAuto(keyINIssue)
		}
	}
	
	if $$$ISOK(strStatus) {
		if $tlevel>0 tcommit
		do ClosePage(pYPARA)
	}
	else {
		trollback
		$$$Alert(strStatus)
		do GoToPage($$$abaLista)
	}
	
	quit

Omitir()
	new idMEDPatient, objMEDAdmission, barCode, Item, strStatus,
		Lote, keyDose, Prescricao, Linha, Dose, objVARDoseAdministracaoLinha,
		idVARDoseAdministracaoLinha, Validade, pYPARA
		
	set pYPARA = YPARA
	set strStatus = $$$OK

	set idMEDPatient	= $piece(pYPARA,",",1)
	if ($length(idMEDPatient) = 0) quit

	set idMEDAdmission	= $$GetOpenAdmission^MEDAdmission(idMEDPatient)
	if ($length(idMEDAdmission) = 0) quit
	set objMEDAdmission	= ^MEDAdmission(YM,idMEDAdmission,1)

	set barCode		= $piece(pYPARA,",",2)
	if ($length(barCode) = 0) quit
	set Item		= $piece(barCode,"@",1)
	set Lote		= $piece(barCode,"@",2)

	set keyDose		= $piece(pYPARA,",",3)
	if ($length(keyDose) = 0) quit
	set Prescricao	= $piece(keyDose,"@",1)
	set Linha		= $piece(keyDose,"@",2)
	set Dose		= $piece(keyDose,"@",3)

	set idVARDoseAdministracao = $piece(pYPARA,",",4)
	if ($length(idVARDoseAdministracao) = 0) set strStatus = $$CriarAdministracao(idMEDPatient,.idVARDoseAdministracao)
	if ($$$ISERR(strStatus)) quit
	set idVARDoseAdministracaoLinha = $order(^VARDoseAdministracaoLinhas(0,1,Item,$$$Index(Lote),idVARDoseAdministracao,""),-1)
	if (idVARDoseAdministracaoLinha '= ""){
		$$$Alert("Essa dose não pode ser omitida.")
		do GoToPage($$$abaLista)
	}else{
		do GoToPage($$$abaOmitir)
	}
	
	quit

OmitirDose(pYPARA, pMotivo, pObservacao)
	new idMEDPatient, objMEDAdmission, barCode, Item, strStatus,
		Lote, keyDose, Prescricao, Linha, Dose, objVARDoseAdministracaoLinha,
		idVARDoseAdministracaoLinha, Validade
	
	set strStatus = $$$OK
	
	set idMEDPatient	= $piece(pYPARA,",",1)
	if ($length(idMEDPatient) = 0) quit

	set idMEDAdmission	= $$GetOpenAdmission^MEDAdmission(idMEDPatient)
	if ($length(idMEDAdmission) = 0) quit
	set objMEDAdmission	= ^MEDAdmission(YM,idMEDAdmission,1)

	set barCode		= $piece(pYPARA,",",2)
	if ($length(barCode) = 0) quit
	set Item		= $piece(barCode,"@",1)
	set Lote		= $piece(barCode,"@",2)

	set keyDose		= $piece(pYPARA,",",3)
	if ($length(keyDose) = 0) quit
	set Prescricao	= $piece(keyDose,"@",1)
	set Linha		= $piece(keyDose,"@",2)
	set Dose		= $piece(keyDose,"@",3)

	set idVARDoseAdministracao = $piece(pYPARA,",",4)
	set idVARDoseAdministracaoLinha = $$^WWWNEXT1("VARDoseAdministracaoLinha",idVARDoseAdministracao,2)
	do ##class(alSOH.iBundle).GetBundlesForLot(Item,YLOCATION,Lote,,.Validade)
	set $$$VARDoseAdministracaoLinhaPrescricao(objVARDoseAdministracaoLinha)= Prescricao
	set $$$VARDoseAdministracaoLinhaLinha1(objVARDoseAdministracaoLinha)	= Linha
	set $$$VARDoseAdministracaoLinhaDose(objVARDoseAdministracaoLinha)		= Dose
	set $$$VARDoseAdministracaoLinhaItem(objVARDoseAdministracaoLinha)		= Item
	set $$$VARDoseAdministracaoLinhaLote(objVARDoseAdministracaoLinha)		= Lote
	set $$$VARDoseAdministracaoLinhaValidade(objVARDoseAdministracaoLinha)	= Validade
	set $$$VARDoseAdministracaoLinhaQuantidade(objVARDoseAdministracaoLinha)= 1
	set $$$VARDoseAdministracaoLinhaObservacao(objVARDoseAdministracaoLinha)= pObservacao
	set $$$VARDoseAdministracaoLinhaStatus(objVARDoseAdministracaoLinha)	= $$$VARDoseStatusOmitida
	set $$$VARDoseAdministracaoLinhaMotivo(objVARDoseAdministracaoLinha)	= pMotivo
	set strStatus = $$$Save("VARDoseAdministracaoLinha",idVARDoseAdministracao_$$$COMMA_idVARDoseAdministracaoLinha,objVARDoseAdministracaoLinha,1)

	do GoToPage($$$abaLista)

	quit

CheckBotao(pYPARA)
	set Q = $$$NO
	if ($piece(pYPARA,"@",2) = "") set Q = $$$YES
	quit Q

Cancelar()
	if (YOPTION = $$$abaConfirmar){
		do GoToPage($$$abaLista)

	}
	elseif (YOPTION = $$$abaOmitir){
		if ($$CheckBotao(YPARA)){
			do ClosePage(YPARA)
		}
		else{
			do GoToPage($$$abaLista)
		}

	}
	elseif (YOPTION = $$$abaLista){
		do ClosePage(YPARA)
	}
	
	quit

PrintJS()
	$$$StartScript()
	&js<
		document.getElementById('Y#(YFORM)#M4').focus();

		/*
		function addEvent(obj, evType, fn) {
 			if (obj.addEventListener) {
   				obj.addEventListener(evType, fn, false);
   				return true;
 			}
 			else if (obj.attachEvent){
   				var r = obj.attachEvent('on'+evType, fn);
   				return r; 
 			}
 			else {
   				return false;
 			}
		}

		addEvent(window,'load', OnLoadEstoquePrograma);
		*/

	>
	$$$EndScript()

	quit

OnBlurMedicamento()
	do OnBlurMedicamento^VARDose()
	quit

GoToPage(idAba)
	if ($length(idAba)=0) quit
	$$$StartScript()
		write "window.open('"_YAKTION_"EP=WWWFORM&YFORM=VARDoseAdministracao&YPARA="_YPARA_"&YSEITE="_idAba_"&YUSER="_YUSER_"&YUCI="_YUCI_"&YM="_YM_"&YBACK=&YBED="_YBED_"', '_self');"
	$$$EndScript()
	quit

ClosePage(pYPARA)
	new idMEDPatient
	set idMEDPatient = $piece(pYPARA,",",1)
	
	$$$StartScript()
		write " window.parent.reloadPage('"_idMEDPatient_"');",!
		write " window.parent.closeModal();",!
	$$$EndScript()
	quit

CriarAdministracao(pidMEDPatient, &pidVARDoseAdministracao)
	if ($length(pidMEDPatient) = 0) quit $$$NO
	new objVARDoseAdministracao, strStatus
	
	set pidVARDoseAdministracao = $$$NextKey("VARDoseAdministracao")
	set $$$VARDoseAdministracaoPaciente(objVARDoseAdministracao)	= pidMEDPatient
	set $$$VARDoseAdministracaoEnfermeiro(objVARDoseAdministracao)	= YBED
	
	set strStatus = $$$Save("VARDoseAdministracao",pidVARDoseAdministracao,objVARDoseAdministracao,1)
	if $$$ISOK(strStatus) set YPARA = YPARA_pidVARDoseAdministracao
	
	quit strStatus

]]></Routine>
</Export>