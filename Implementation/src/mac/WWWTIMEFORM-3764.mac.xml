<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWTIMEFORM" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWTIMEFORM ;WWWTIMEFORM;FIS;KOPIEREN DER ZEITDATEN IN DIE DATENSTÄNDE;05.11.2001
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		KOPIEREN DER ZEITDATEN IN DIE DATENSTÄNDE
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	05.11.2001
	;|
	;\------------------------------------------------------------------/
	;
	NEW YDATEI,YFORM,YVOR,YM,YDATUM,YI,Y,YMAXKEY,YFELD1,YFELD,YFELD2,YDATEI1,YDATEI0,YKEY,YBED
	SET Y="~"  ;TRENNZEICHEN
	SET YBED="UNKNOWN"
	SET YFORM="" FOR  SET YFORM=$ORDER(^WWW120(0,YFORM)) QUIT:YFORM=""  DO
	. SET YFORM(1)=$GET(^WWW120(0,YFORM,1))
	. QUIT:$PIECE(YFORM(1),Y,106)'=1  ;KEINE ERFASSUNG MÖGLICH ;no logging potential 
	. QUIT:$PIECE(YFORM(1),Y,11)=""  ;KEINE DATEI ;no data file 
	. SET (YFELD,YFELD1,YFELD2)=""
	. ;
	. SET YDATEI=$PIECE(YFORM(1),Y,11)  ;BASISKLASSE
	. SET YDATEI0="^"_YDATEI_"t"
	. IF $DATA(@YDATEI0) DO
	. . SET YDATEI1=YDATEI0
	. . ;
	. . FOR  DO  QUIT:YDATEI1=""
	. . . SET YDATEI1=$QUERY(@YDATEI1)
	. . . SET YM=$PIECE($PIECE(YDATEI1,",",1),"(",2)
	. . . QUIT:YM=""
	. . . SET YMAXKEY=$ORDER(^WWW002(0,YDATEI,""),-1)
	. . . IF +YMAXKEY=0 SET YMAXKEY=1
	. . . ;
	. . . IF $PIECE(YDATEI1,"(",1)'=$PIECE(YDATEI0,"(",1) SET YDATEI1="" QUIT  ;FALSCHE KLASSE ;groovy 
	. . . IF +$PIECE(YDATEI1,",",YMAXKEY+2)>(+$H) SET YDATEI1="" QUIT  ;FALSCHES DATUM ;Date 
	. . . ;
	. . . IF $EXTRACT($PIECE(YDATEI1,",",YMAXKEY+3))'=1 DO  QUIT  ;FALSCHER DATENSATZ ;data record 
	. . . . IF '$DATA(@($PIECE(YDATEI1,",",1,YMAXKEY+2)_",1)")) KILL @YDATEI1
	. . . . QUIT
	. . . ;
	. . . SET YDATUM=$PIECE(YDATEI1,",",YMAXKEY+2)
	. . . SET YDATEI2=$PIECE(YDATEI1,",",1,YMAXKEY+2)_",2)"  ;KEY URSPRÜNGLICHE DATEI VOR ÄNDERUNG ;KEY data file pre- alteration 
	. . . SET YKEY=$PIECE(YDATEI1,",",2,YMAXKEY+1)           ;KEY HAUPTDATEI
	. . . ;
	. . . SET YFELD=$$^WWWSETL("^"_YDATEI_"("_""""_YM_""""_","_YKEY_",1)")  ;HAUPTDATEI
	. . . SET YFELD1=$$^WWWSETL(YDATEI1)  ;ÄNDERUNGSDATEI
	. . . SET YFELD2=$$^WWWSETL(YDATEI2)  ;URSPRUNGS-VERSION VOR ÄNDERUNG ;pre- alteration 
	. . . IF $TRANSLATE(YFELD2,Y)="" SET YFELD2=YFELD
	. . . ;
	. . . FOR YI=1:1  QUIT:$PIECE(YFELD1,Y,YI,999)=""  DO
	. . . . IF $PIECE(YFELD1,Y,YI)'=$PIECE(YFELD2,Y,YI) SET $PIECE(YFELD,Y,YI)=$PIECE(YFELD1,Y,YI)  ;GEÄNDERTES DATENFELD ;data item 
	. . . . QUIT
	. . . ;
	. . . DO ^WWWSPEI(YDATEI,YKEY,YFELD,1)  ;SPEICHERN ÄNDERUNG ;Save alteration 
	. . . KILL @YDATEI1
	. . . KILL @YDATEI2
	. . . QUIT
	. . QUIT
	. QUIT
	QUIT
]]></Routine>
</Export>