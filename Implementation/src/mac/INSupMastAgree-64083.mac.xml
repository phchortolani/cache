<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INSupMastAgree" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INSupMastAgree
	#include INConst
	#include COMSYS
	#include COMConst
	
OnBeforeSave(pYKEY,pYFELD) 
    ;-------------------------------------------------------------------------------
    ; On Before Save of the supplier master agreement header form
	;
	; Params:	pYKEY	: The YKEY value
	; 			pYFELD	: The YFELD value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Dec-2006	Steve S		SR15341: Created
	;-------------------------------------------------------------------------------
	new strStatus,dteOpen,dteClose
	
	set strStatus = $$$OK
	
	set dteOpen 	= $$$INSupMastAgreeOpenDate(pYFELD)
	set dteClose 	= $$$INSupMastAgreeCloseDate(pYFELD)
	
	if (dteOpen'="")&&(dteClose'="")&&(dteOpen>dteClose) {
		set strStatus = $listbuild("IN00614") ;Open Date cannot be after Close Date.
	} else {
		set strStatus=$$$GRIDSave(pYKEY)
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		$$$Error(strStatus)	
	}
	
	quit
	
	
Search()
	;-------------------------------------------------------------------------------
	; Callback for when the Search button is clicked
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Dec-2006	Steve S		SR15341: Created (Copied from REQUISITIONVAR)
	;-------------------------------------------------------------------------------
	do Initialise^COMViewCustom("INART","ItemsSelected^INSupMastAgree")
	do Start^COMViewCustom()
	
	quit $$$OK
 
 
ItemsSelected(pobjResultSet)
	;-------------------------------------------------------------------------------
	; Code to run when an item is selected
	;
	; Params:	pobjResultSet		: The result set
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 28-Dec-2006	Steve S		SR15341: Created (Copied from REQUISITIONVAR)
	;-------------------------------------------------------------------------------
	new idKey,objAgree,objItem
	
	set objAgree = $$$GRIDYFELDContainer
	
	set $$$INSupMastAgreeItemOpenDate(objItem) 	= $$$INSupMastAgreeOpenDate(objAgree)
	set $$$INSupMastAgreeItemCloseDate(objItem) = $$$INSupMastAgreeCloseDate(objAgree)
	set $$$INSupMastAgreeItemTrackBy(objItem)	= $$$EnumINAGREETRACKQuantity
	
	$$$GRIDAddBatchStart
	
	while pobjResultSet.Next() {
		set idKey=$$GetID^COMViewCustom(pobjResultSet)
		$$$GRIDAddLine(objItem,idKey)
	}
 
	$$$GRIDAddBatchStop
	
	quit
]]></Routine>
</Export>