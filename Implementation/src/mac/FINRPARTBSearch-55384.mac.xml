<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINRPARTBSearch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINRPARTBSearch
	#include COMSYS
	#include FINConst 
 	
ShowSelected()
	/*------------------------------------------------------------------------------
	; Show the selected customers
	;
	; Params: 
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	new arrData
 
	if '$data(YMFELD) kill ^CacheTempCustomers(YUSER)
 	
 	if YSEITE=1 {
	 	do UpdateSelected(.arrData)
	 	write "<B>"_$$$Text("Fin00941")_"</B>",! ;Customers Selected
		write "",!
		write "<table style='font-size:14px'>",!
		write "<tr>",!
		write "<td><input readonly style='background-color:lightgrey' size=10 name='CustomersSelectedCounter' value='"_arrData("CustomersSelectedCounter")_"'></td>",!
		write "</tr>",!
		write "<tr>",!
		write "<td><textarea readonly style='background-color:lightgrey' cols=60 rows=30 name='CustomersSelected'></textarea></td>",!
		write "</tr>",!
		write "</table>",!
		
		$$$StartScript()
		;write "<script type='text/javascript'>"
		write "document.WWW2.CustomersSelected.value="""_arrData("CustomersSelected")_""";",!
		;write "</script>"
		$$$EndScript()
	}
	 
	quit
	
	
Search()
	/*------------------------------------------------------------------------------
	; Start the COMView search for customers
	;
	; Params: 
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	do Initialise^COMViewCustom("FINARCustomer","CustomersSelected^FINRPARTBSearch")
	do Start^COMViewCustom()
	
	quit $$$OK
	
	
CustomersSelected(pobjResultSet)
	/*------------------------------------------------------------------------------
	; Routine to run when a customer is selected
	;
	; Params: pobjResultSet		: The COMView result set
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	new idCustomer,arrData
	
	while pobjResultSet.Next() {
		set idCustomer=$$GetID^COMViewCustom(pobjResultSet)
		if idCustomer'="" set ^CacheTempCustomers(YUSER,idCustomer)=""
	}
	
	do UpdateSelected(.arrData)
	
	write "document.WWW2.CustomersSelected.value="""_arrData("CustomersSelected")_""";",!
	write "document.WWW2.CustomersSelectedCounter.value="_arrData("CustomersSelectedCounter")_";",!
	
	quit	
	
UpdateSelected(parrData)
	/*------------------------------------------------------------------------------
	; Update current data selected customers
	;
	; Params:
	;
	; ByRefs: parrData		: The array with the customer data
	;
	; Returns:
	;
	; History:
	; 15-Feb-2006	Steve S		SR13474: Created
	;-----------------------------------------------------------------------------*/
	new idCustomer,intCount,strCustomers,strName,blnLimit,strText
	
	set intCount=0
	set idCustomer=""
	
	set strCustomers=""
	set blnLimit=$$$NO
	
	for {
		set idCustomer=$order(^CacheTempCustomers(YUSER,idCustomer))
		quit:(idCustomer="")
		
		set intCount=intCount+1
		
		if (intCount<=500) {
			set strCustomers=strCustomers_$justify(idCustomer,10)
			set strName=$$$FINARCustomerCustomerName($get(^FINARCustomer(YM,idCustomer,1)))
			if strName="" set strName="---"
			set strCustomers=strCustomers_": "_strName_"\n"
		} else {
			if 'blnLimit {
				set blnLimit=$$$YES
				set strText=$zconvert($$$Text("Fin00967"),"U")  ;Only displaying first 500 entries
				set strCustomers=$justify(strText,5)_"\n"_strCustomers
			}	
		}
	}
	
	set parrData("CustomersSelected")=strCustomers
	set parrData("CustomersSelectedCounter")=intCount
	
	quit
]]></Routine>
</Export>