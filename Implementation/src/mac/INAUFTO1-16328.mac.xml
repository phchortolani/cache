<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFTO1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFTO1
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ALLE POSITIONEN DES AUFTRAGES EINPLANEN
	;       ALL LINES OF THE ORDER TAKING INTO ACCOUNT
	; 
	; Called By :
	;    INAUF for Customer Order
	;        when INVORG D74 (Transfer Date Of Delivering In Tracking Plan) = $$$YES
	;        and  VORG(1)=1  (Item No. or Search Name)    ???
	; 
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 09-Feb-2006	JW		Reverted SR14746 change. INAUFPXL does not conform to @net standard.
	; 21-SEP-2006 	FAN 	SR14746 Sales statistics incorrect
	; 28-Jul-2006	GRF		SR14881: Doco; new SATZ to stop INAUFP record being built in INAUF
	;                       from being over-written.
	; 21-Dec-2005	Steve S	SR13993: Check tour date
	; 06.08.2000	DT		Created
	;-------------------------------------------------------------------------------
	NEW AUF,POS,LITM,UHR,PLANER,YVOR,SATZ,blnCustAddInOrder
	
	;+++++++++++++++++++++++++++++++++++++++
	; FIXME : Ensure this program runs properly if the D74 flag ***IS*** set.
	;         Need to investigate operation of VORG(1) further and identify
	;         the circumstances where it equals 1.                            <GRF>
	;+++++++++++++++++++++++++++++++++++++++
	
	QUIT:$GET(VORG(1))'=1  ;NICHT ALLE AUF DEN GLEICHEN TERMIN ;Not all on resemble date
	
	SET AUF = $PIECE(YKEY,",",1)                             QUIT:AUF=""
	SET POS = $PIECE(YKEY,",",2)                             QUIT:POS=""
	
	SET LITM   = $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,76)    QUIT:LITM=""  ;KEIN TERMIN ;no 
	SET PLANER = $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,77)
	SET UHR    = $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,100)
	SET TOUR   = $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,95)
	
	;SR13993: Check tour date
	if $$IncludeTour^INTOURCHECK(LITM) SET ^INTOUR(YM,LITM,1)=""  ;MÖGLICHES TOURDATUM ; POSSIBLE ROUTE DATE
	SET YFORM="INAUFP"
	SET POS=""
	set blnCustAddInOrder=$$$INAUFCustomerAddressinOrder($get(^INAUF(YM,AUF,1)))  // SR14746
	FOR  SET POS=$ORDER(^INAUFP(YM,AUF,POS)) QUIT:POS=""  DO
	. NEW YVOR
	. SET SATZ=$GET(^INAUFP(YM,AUF,POS,1))
	. IF $PIECE(SATZ,Y,7)=1  IF ($PIECE(SATZ,Y,241)=1) || (blnCustAddInOrder) QUIT   ;FIS;08.08.03;DIREKTLIEFERUNG VON LIEFERANT ;supplier  // SR14746
	. ;
	. IF $PIECE(SATZ,Y,7)'=2 IF '$DATA(^INWEAUF(YM,AUF,POS)) QUIT:$PIECE(SATZ,Y,90)'=1   ;NICHT AUSLIEFERFÄHIG  NICHT EINTOUREN ;Not Not 
	. SET $PIECE(SATZ,Y,77)  = PLANER  ;WER PLANT ;who 
	. SET $PIECE(SATZ,Y,76)  = LITM    ;LIEFERTERMIN ;time of delivery 
	. SET $PIECE(SATZ,Y,100) = UHR     ;WANN WIRD GELIEFERT ;when 
	. SET $PIECE(SATZ,Y,95)  = TOUR    ;WELCHE TOUR ;who ships 
	. SET KEY=AUF_","_POS
	. SET OK=$$^WWWSPEI("INAUFP",KEY,SATZ,1)   ;SPEICHERN DATENSATZ ;Save data record 
	. DO
	. . NEW SATZ
	. . SET SATZ=$GET(^INAUFPXL(YM,AUF,POS))
	. . SET $PIECE(SATZ,Y,77)  = PLANER
	. . SET $PIECE(SATZ,Y,76)  = LITM
	. . SET $PIECE(SATZ,Y,100) = UHR
	. . SET $PIECE(SATZ,Y,95)  = TOUR
	. . SET $PIECE(SATZ,Y,90)  = $$$YES
	. . //SET strStatus=$$$Save("INAUFPXL",AUF_","_POS,SATZ,$$$YES) // SR14746
	. . set ^INAUFPXL(YM,AUF,POS) = SATZ		// JW
 
	
	QUIT
]]></Routine>
</Export>