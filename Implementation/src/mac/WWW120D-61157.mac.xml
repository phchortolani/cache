<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW120D" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW120D
	;-------------------------------------------------------------------------------
	;09-Sep-2008	shobby	SRBR014980 Class/Form changed (Do Not remove comment)
	;-------------------------------------------------------------------------------
	
#include WWWConst
#include COMSYS
	
CustomHelpText(pobjWWW120D)
	;-------------------------------------------------------------------------------
	; If Help Text is a WWW009 reference, return that Text string rather than
	; the Text ID.
	; 
	; 25-May-2011	shobby		SR16925.2: IndividualHelpText->CustomHelpText
	;-------------------------------------------------------------------------------
	quit $$PreProcessText^WWWFORMCOMMON(pobjWWW120D,$$$FldWWW120DCustomHelpText) ;BR014966 ;16925 ;SR16925.2
	
	
CustomTrainingText(pobjWWW120D)
	;-------------------------------------------------------------------------------
	; If Training Text is a WWW009 reference, return that Text string rather than
	; the Text ID.
	; 
	; 25-May-2011	shobby		SR16925.2: IndividualTrainingText->CustomTrainingText
	;-------------------------------------------------------------------------------
	quit $$PreProcessText^WWWFORMCOMMON(pobjWWW120D,$$$FldWWW120DCustomTrainingText) ;BR014966 ;16925 ;SR16925.2

IsNotStandardFormType(YKEY) 
	;-------------------------------------------------------------------------------
	; Determine the form type for 'rules' processing.
	; 
	; 22-Apr-2012	shobby		SR18021: YFORM not newed causing a grid to appear
	;								when editing the form field of a form.
	; 13-Apr-2012	shobby		SR18000: Created
	;-------------------------------------------------------------------------------
	new YFORM 	;SR18021
	set YFORM=$piece(YKEY,",",1)
	if $get(YFORM)="" set YFORM=" "
	quit $$$WWW120FormType($get(^WWW120(0,YFORM,1)))'=1

ExecuteAfterDataFields(YFORM,YKEY,YFELD,&YSTOP)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 24-Aug-2012	shobby		SR18091: Created
	;-------------------------------------------------------------------------------
	new objWWW120D,strExec
	
	set objWWW120D=$get(^WWW120D(0,YFORM,YM,1))
	set strExec=$$$WWW120DExecuteAfterDataFields(objWWW120D)
	if strExec'= "" {
		xecute strExec
		; CAUTION: setting YSTOP=1 will prevent the core ExecuteAfterDataFields
		;          from running.
	}
	;SR18091
	quit
	
		
ReplaceCore(YFORM="")
	;-------------------------------------------------------------------------------
	; Flag to determine if the customisation of ExecuteAfterDataFields runs after
	; the core version or instead of.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 22-Aug-2013	shobby		CORE-182: Created
	;-------------------------------------------------------------------------------
	new objWWW120D,blnBlock
	
	set blnBlock=$$$NO
	if YFORM'="" {
		set objWWW120D=$get(^WWW120D(0,YFORM,YM,1))
		set blnBlock=+$$$WWW120DExecuteAfterDataFieldsRep(objWWW120D)
	}
	quit blnBlock

FixedColumns()
	;-------------------------------------------------------------------------------
	; Determine the fixed columns
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Sep-2013	shobby	CORE-254: Created
	;-------------------------------------------------------------------------------
	new strFixedColumns
	set strFixedColumns=""
	if YFORM'="" {
		set strFixedColumns=$$$WWW120DFixedColumns($get(^WWW120D(0,YFORM,YM,1)))
		if strFixedColumns="" set strFixedColumns=$$$WWW120FixedColumns($get(^WWW120(0,YFORM,1)))
		if strFixedColumns="" set strFixedColumns=$$$WWW012FixedColumns($get(^WWW012(0,YM,1)))
	}
	quit strFixedColumns 

FlatControls(YKEY)
	;-------------------------------------------------------------------------------
	; Determine the Flat Controls
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 10-Dec-2013	shobby	CORE-281: Corrected use of YKEY
	; 08-Dec-2013	shobby	CORE-281: Created
	;-------------------------------------------------------------------------------
	new strFlatControls,YFORM
	
	set strFlatControls=""
	set YFORM=$$$KEY1(YKEY)
	if YFORM'="" {
		set strFlatControls=$$$WWW120FlatControls($get(^WWW120(0,YFORM,1)))
	}
	quit strFlatControls 
]]></Routine>
</Export>