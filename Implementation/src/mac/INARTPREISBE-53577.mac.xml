<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTPREISBE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTPREISBE
#include COMSYS
#include INConst
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		BETRIEBSABHÄNIGE PREISE
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 11-Oct-2006	JW		SR15104: Rewrote, encapsulated functions
	; 02.02.2003	BEC
	;-------------------------------------------------------------------------------
	NEW blnCalcAll,blnCalcCheck,blnPrint
	
	set blnCalcAll		= $GET(VORG(77))	;=1 CALCULATE ALL PRICES
	set blnCalcCheck	= $GET(VORG(88))	;=1 CALCULATE PRICES WITH OPTION (ON THE FLY ONLY) FIELD 7
	set blnPrint 		= '$get(VORG(99))  	;=1 KEIN DRUCK / NO PRINT
	
	set YKEY = $get(YKEY)
	
	if blnCalcAll || blnCalcCheck {
		do Recalc(YKEY,blnCalcCheck)
	}
	if blnPrint {
		do Print(YKEY)
	}
	
	quit

 
OnAfterDataFields(pstrKey)
	;-------------------------------------------------------------------------------
	; Run after data fields of INARTPREISBE form
	;
	; Params:	pstrKey - INARTPREISBE id
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 10-Oct-2006	JW		SR15104: Created
	;-------------------------------------------------------------------------------
	do Print(pstrKey)
	quit
 
 
 
Print(YKEY)
	;-------------------------------------------------------------------------------
	; Print the current location prices
	;
	; Params:	YKEY - INARTPREISBE id (item,location)
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 09-Oct-2006	JW		SR15104: Created
	;-------------------------------------------------------------------------------
	new SWG,WARENGRUPPE,UES
	
	SET SWG=$$$INVORGDefaultItemGroup($GET(^INVORG(YM,YM,1)))
	set WARENGRUPPE = $$WARENGRUPPE($$$KEY1(YKEY))					;Get group
	
	; Header
	SET UES(1)=$$^WWWFELDNAME("INARTPREISBE1","P",3)_Y_$$^WWWFELDNAME("INARTPREISBE1","D",1)_Y_$$^WWWFELDNAME("INARTPREISBE1","D",2)_Y_$$^WWWFELDNAME("INARTPREISBE1","D",10)_Y
	;            PREISKENNZEICHEN                                             BERECHNETER PREIS                  KORRIGIERTER PREIS                      
	SET UES(1)=UES(1)_$$^WWWFELDNAME("INARTPREISBE1","P",4)_Y_$$^WWWFELDNAME("INARTPREISBE1","D",5)_Y_$$^WWWFELDNAME("INARTPREISBE1","D",6)_Y_$$^WWWFELDNAME("INARTPREISBE1","D",11)
	;                                      GÜLTIG AB                                                       GÜLTIG BIS          BEMERKUNG WERBEMAßNEHME ;valuable Confirm. 
	
	DO DRUCK      ;TABELLE ANZEIGEN  ;tabulation display 
	DO LEER^WWWTAB(8,8)
	WRITE "</TABLE>"
	
	quit
 
 
 
WARENGRUPPE(pidItem) ;WARENGRUPPE SUCHEN ;seek 
	;-------------------------------------------------------------------------------
	; Return the item group for the given item
	;
	; Params:	pidItem - INART id
	;
	; ByRefs:
	;
	; Returns:	group id
	;
	; History:
	; 29-Nov-2006	GRF		BR014285: "GRUOP" => "GROUP"
	; 27-Nov-2006	JW		BR014285: Item group change
	; 09-Oct-2006	JW		SR15104: Parameterised, return group.
	;-------------------------------------------------------------------------------
	new WARENGRUPPE,idGroup
	;ACHTUNG EINSPRUNG AUS INSALESPRICE ;estimation out of 
	
	; FIXME : There may not be a group "1"; see variable SWG for default from INVORG D13.
	;         Should never be null in DO block so extra test not required     <GRF>
	
	SET WARENGRUPPE=1
	QUIT:$GET(pidItem)="" WARENGRUPPE
	
	IF pidItem'="" {
		SET WARENGRUPPE=$PIECE($GET(^INART(YM,pidItem,1)),Y,30)
		IF WARENGRUPPE="" SET WARENGRUPPE=1
	}
	
	IF WARENGRUPPE'="" && '$DATA(^INKALKPREIS(YM,WARENGRUPPE)) {
		
		// Get the first parent item group which has prices
		
		SET idGroup=WARENGRUPPE
		FOR {
			set idGroup = $$$INItemGroupParentGroup($get(^INItemGroup(YM,idGroup,1)))		//BR014285
			quit:idGroup=""
			IF $DATA(^INKALKPREIS(YM,idGroup)) {
				SET WARENGRUPPE = idGroup			
				quit
			}
		}
	}
	
	/*  BR014285 - rewritten above
	DO         ;BASISPREIS FESTLEGEN
	. ;BEC;24767;16.02.04;UPPER ITEMGROUP, IF NO PRICERELATIONS ARE AVAILABLE
	. IF WARENGRUPPE'="" DO
	. . QUIT:$DATA(^INKALKPREIS(YM,WARENGRUPPE))
	. . NEW UPGROUP,YI,YOK
	. . SET YOK=""
	. . SET UPGROUP=WARENGRUPPE
	. . FOR YI=1:1:10 QUIT:YOK=1  DO 
	. . . SET UPGROUP=$$^INWAGRUCHECK(UPGROUP)  ;UPPER ITEMGROUP;FIS;16.07.04;26111
	. . . IF UPGROUP'="" DO                                 ;BEC;24767;30.03.04
	. . . . IF $DATA(^INKALKPREIS(YM,UPGROUP)) DO 
	. . . . . SET YOK=1
	. . . . . SET WARENGRUPPE = UPGROUP
	*/
	
	IF WARENGRUPPE="" SET WARENGRUPPE=1
	
	QUIT WARENGRUPPE
	
	
RECHFORMEL(FORMEL,&YPREIS) ;FORMEL AUSRECHNEN ;formula 
	;-------------------------------------------------------------------------------
	; Calculate price using formula
	;
	; Params:	FORMEL	- price calculation formula
	;			YPREIS	- item price
	;
	; ByRefs:	YPREIS
	;
	; Returns:
	;
	; History:
	; 09-Oct-2006	JW		SR15104: Parameterised
	;-------------------------------------------------------------------------------
	NEW WERT,AKTION,YI,FORMEL1,WERT1,RULE,RESULT,YQ,CHECK
	
	SET WERT=0  
	SET YQ=""
	QUIT:FORMEL=""
	
	IF '$FIND(FORMEL,"P") DO      
	. IF $FIND(FORMEL,"%") DO
	. . FOR YI=1:1:2 DO  QUIT:$PIECE(FORMEL,"%",YI,99)=""
	. . . IF YI=1 SET AKTION="%"
	. . . IF YI=2 SET AKTION=""
	. . . SET FORMEL1=$PIECE(FORMEL,"%",YI)
	. . . SET VZ="+" 
	. . . SET WERT1=FORMEL1
	. . . IF $EXTRACT(FORMEL1,1)="-" SET VZ="-" SET WERT1=$EXTRACT(FORMEL1,2,999)
	. . . IF $EXTRACT(FORMEL1,1)="+" SET VZ="+" SET WERT1=$EXTRACT(FORMEL1,2,999)
	. . . IF AKTION=""    SET WERT1=$$^WWWTR(1,8,FORMEL1)
	. . . IF AKTION'="" DO
	. . . . IF AKTION="%" SET WERT1=($TRANSLATE(WERT1,",",".")/100)*YPREIS
	. . . . IF VZ="+"     SET WERT1=YPREIS+WERT1
	. . . . IF VZ="-"     SET WERT1=YPREIS-WERT1
	. . . ;
	. . . SET WERT=WERT+WERT1
	. ;
	. IF '$FIND(FORMEL,"%") IF FORMEL'="" SET WERT=$$^WWWTR(1,8,FORMEL)
	. IF '$FIND(FORMEL,"%") IF FORMEL'="" IF $FIND(FORMEL,"+")    SET WERT=YPREIS+$$^WWWTR(1,8,FORMEL)    ;YPREIS AUS WWWDATEN ;out of 
	. IF '$FIND(FORMEL,"%") IF FORMEL'="" IF $FIND(FORMEL,"-")    SET WERT=YPREIS+$$^WWWTR(1,8,FORMEL)    ;YPREIS AUS WWWDATEN ;out of 
	. IF '$FIND(FORMEL,"%") IF FORMEL'="" IF $EXTRACT(FORMEL)="*" SET WERT=YPREIS*$EXTRACT(FORMEL,2,999)  ;YPREIS AUS WWWDATEN ;out of 
	. IF '$FIND(FORMEL,"%") IF FORMEL'="" IF $EXTRACT(FORMEL)="/" IF +$EXTRACT(FORMEL,2,999)'=0 SET WERT=YPREIS/$EXTRACT(FORMEL,2,999)  ;YPREIS AUS WWWDATEN ;out of 
	. SET YPREIS=WERT
	
	IF $FIND(FORMEL,"P") DO    ;BEC;25408;02.04.04
	. SET RULE=""
	. SET RESULT=""
	. IF +YPREIS=0 IF $FIND(FORMEL,"/P") QUIT
	. IF +YPREIS=0 IF $FIND(FORMEL,"/0") QUIT
	. SET RULE=$$^WWWTRANSLATE(FORMEL,"P",YPREIS)
	. QUIT:RULE=""
	. ;QUIT:$$^WWWOPCHECK(RULE)=1
	. SET RULE="SET RESULT="_RULE
	. XECUTE RULE              ;REGEL ausführen ;regulation implement 
	. SET YPREIS=RESULT
	
	QUIT
	
	
RecalcAll()
	;-------------------------------------------------------------------------------
	; Update location prices (^INARTPREISBE) for all items, locations
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 08-Jul-2009	shobby	SR16720: If only recalculating for one location don't call the Recalc
	; 							method for every location.
	; 09-Oct-2006	JW		SR15104: Created
	;-------------------------------------------------------------------------------
 	new idItem,idLoc,objINVORG
 	
	set objINVORG = $get(^INVORG(YM,YM,1))				// SR14568
	if $$$INVORGOnlyCalculateforStandardL(objINVORG) {  ;16720
 		set idItem = "" for { set idItem = $order(^INART(YM,idItem)) quit:idItem=""
 			do Recalc(idItem_","_$$$INVORGLocationForStandardCalcul(objINVORG))
 		}
	} else {
	 	$$$Order2(^INART,YM,idItem)
	 		$$$Order3(^WWW0121,0,YM,idLoc)
	 			do Recalc(idItem_","_idLoc)
	 		$$$End
	 	$$$End
	} 	
	quit
 
	
	//RECHNEN(YKEY,pblnCalcCheck)	SR15104
Recalc(YKEY,pblnCalcCheck=$$$YES)
	;-------------------------------------------------------------------------------
	; Re-calculate price
	;
	; Params:	YKEY 			- item,location
	; 			pblnCalcCheck	- whether to only update prices if p7 of ^INKALKPREIS is checked.
	;
	; ByRefs:
	;
	; Returns:	status - although not used
	;
	; History:
	; 15-Nov-2006	GRF/JW	SR15104: Reverted if/else ordering
	; 25-Oct-2006	JW		SR15111: Delete irrelevant prices.
	; 11-Oct-2006	JW		SR15104: Changed name, moved code from ^INARTPREISBE.
	; 27-Jul-2006 	BEC		SR14798: If there is no sales Price, price is 0, not manufactionaring costs
	; 10-Jul-2006	JW		SR14568: Doco, variables. Quit if not calculating for this loc
	;-------------------------------------------------------------------------------
	NEW YDATEI,YFELD,BEZIEHUNG1,BPRICE,RELATION,YII,PREISKZ,ZII,DATE1,YOK,WG,EPRICE,strStatus,objItem
	new idItem,idLoc,idPriceBasis,curRelBase,objINVORG,idCalcLoc,YTEMPPRICE,YOK,dteToday,blnStop
	new SWG,WARENGRUPPE,idBasedOn,idPI,dtePrice
	
	set idItem = $$$KEY1(YKEY)
	set idLoc  = $$$KEY2(YKEY)
	
	QUIT:idItem="" $$$OK
	QUIT:idLoc="" $$$OK
	
	set blnStop = $$$NO
	
	set objINVORG = $get(^INVORG(YM,YM,1))				// SR14568
	if $$$INVORGOnlyCalculateforStandardL(objINVORG) {
		set idCalcLoc = $$$INVORGLocationForStandardCalcul(objINVORG)
		if (idCalcLoc'="")&&(idCalcLoc'=idLoc) {		// quit if not calculating for this loc
			set blnStop = $$$YES
		}
	}
	quit:blnStop $$$OK
	
	// SR15104 - moved this code from ^INARTPREISBE
	SET SWG=$$$INVORGDefaultItemGroup($GET(^INVORG(YM,YM,1)))
	set WARENGRUPPE = $$WARENGRUPPE($$$KEY1(YKEY))					;Get group
 
	;NICHT VORHANDE ANLEGEN ;Not put onto 
	//SET WARENGRUPPE=$GET(WARENGRUPPE)		SR15104
	
	SET YFELD=0_Y
	SET YPREIS=""
	SET EPRICE=""
	SET WG=WARENGRUPPE
	IF '$DATA(^INKALKPREIS(YM,WG)) IF SWG'="" SET WG=SWG
	QUIT:WG="" $$$OK
	
	SET strStatus = $$$OK
	
	// SR15111: Check whether price relations exist for each price.
	// If there is no relation, delete the price.
	
	$$$Order4(^INARTPREISBE1,YM,idItem,idLoc,idPI)
		if '$data(^INKALKPREIS(YM,WG,idPI)) {
			$$$Order5(^INARTPREISBE1,YM,idItem,idLoc,idPI,dtePrice)
				set YOK = $$$Kill("INARTPREISBE1",idItem_","_idLoc_","_idPI_","_dtePrice)
			$$$End
		}
	$$$End
	
	// Now for each price relation... create a price if necessary
	; if "Today" entry exists, set DATE   to next price entry    (e.g. "Tomorrow")
	;                          [is this variable used?]
	; otherwise                set YDATEI to previous one        (e.g. "Yesterday")
	;                          If no previous one, create "Today"
	
	SET PREISKZ=""
	FOR  SET PREISKZ=$ORDER(^INKALKPREIS(YM,WG,PREISKZ)) QUIT:PREISKZ=""  DO
	. SET YDATEI=+$HOROLOG 
	. IF $DATA(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI)) SET DATE=YDATEI
	. IF '$DATA(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI)) SET YDATEI=$ORDER(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI),-1) 
	. IF YDATEI="" DO
	. . IF pblnCalcCheck IF '$$$INKALKPREISCalulatePricesAutomatical($GET(^INKALKPREIS(YM,WG,PREISKZ,1))) QUIT 
	. . SET ^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,+$HOROLOG,1)=YFELD 
	
	;ALLE PREISE AUF NULL SETZTEN, BASISPREIS SETZTEN - ALL PRICES SET TO ZERO, BASE PRICE SET
	DO
	. SET PREISKZ=""
	. FOR  SET PREISKZ=$ORDER(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ)) QUIT:PREISKZ=""  DO
	. . IF pblnCalcCheck IF '$$$INKALKPREISCalulatePricesAutomatical($GET(^INKALKPREIS(YM,WG,PREISKZ,1))) QUIT      ;BEC;24823;11.02.04;ON THE FLY PRICES ONLY
	. . SET YDATEI=""
	. . FOR ZII=1:1:20 SET YDATEI=$ORDER(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI),-1) QUIT:YDATEI=""  DO 	// SR15114 FIXME (2) - Why hardcode to look at only 1st 20 prices?
	. . . SET YFELD=$GET(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI,1))
	. . . QUIT:YFELD=""
	. . . SET $PIECE(YFELD,Y,1)=""
	. . . SET YOK=$$^WWWSPEI("INARTPREISBE1",idItem_","_idLoc_","_PREISKZ_","_YDATEI,YFELD,1)    ;BEC;11.06.03;WWWSPEI GEÄNDERT
	
	;Loop  durch alle Preisbeziehungen ;trans- All Pricing 
	
	// SR15104: Code rewritten
	
	set dteToday = +$HOROLOG
	
	FOR YII=1:1:5 {														// SR15114 FIXME (3) - Why are we doing this 5 times?
		$$$Order4(^INARTPREISBE1,YM,idItem,idLoc,PREISKZ)
			SET YDATEI=""
			FOR ZII=1:1:20 {											// SR15114 FIXME (2) - Why hardcode to look at only 1st 20 prices?
				SET YDATEI=$ORDER(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI))
				QUIT:YDATEI=""
				
				SET YFELD=$GET(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI,1))
				continue:YFELD=""
				continue:$$$INARTPREISBE1Base(YFELD)'=""               ;only calculate when base price is null
				
				SET YPREIS=""
				SET EPRICE=""
				
				SET BEZIEHUNG1 = ""
				IF $DATA(^INKALKPREIS(YM,WARENGRUPPE)) {
					SET BEZIEHUNG1=$GET(^INKALKPREIS(YM,WARENGRUPPE,PREISKZ,1))         ;PREIS DER BERECHNET WERDEN SOLL ;prize the will 
				} elseif (SWG'="") {
					SET BEZIEHUNG1=$GET(^INKALKPREIS(YM,SWG,PREISKZ,1))  ;PREIS DER BERECHNET WERDEN SOLL,STANDARTVORGABE
				}
				
				set idBasedOn    = $$$INKALKPREISBasedOn(BEZIEHUNG1)      ; D2
				set idPriceBasis = $$$INKALKPREISPriceBasis(BEZIEHUNG1)   ; D5
				
				IF (idBasedOn="") && (idPriceBasis="") {
					// ***** START BLOCK **** //
					// SR14568 JW: This block of code was brought back from Client site.
					// 
					// SR15114 FIXME (4) - looking at the SalesPrice fields is surely wrong.
					// We aren't taking into account the related price identifier...
					// 
					// SR15114 FIXME (5) - also, should SR14798 have set price to "" or 0 ? If 0, the strStatus line is redundant.
					// 
					SET objItem=$GET(^INART(YM,idItem,1))
					SET YPREIS=$$$INARTSalesPrice(objItem)
					SET:YPREIS="" YPREIS=$$$INARTSalesPrice1(objItem)
					SET:YPREIS="" YPREIS=$$$INARTSalesPrice2(objItem)
					SET:YPREIS="" YPREIS=$$$INARTSalesPrice3(objItem)
 					//SET:'YPREIS YPREIS=$$$INARTProductionCosts(strInart,Y,47)
					SET:YPREIS="" YPREIS=0            ;SR14798;BEC/INTRAPREND;27,07,06;NO SALESPRICE-> 0
					// ****** END BLOCK ***** //
					
					//SET YPREIS=$$$INARTProductionCosts($GET(^INART(YM,idItem,1))) <-- replaced this line
	 				
					IF YPREIS="" SET strStatus=$listbuild(33327)		  ;No Manufacturing Costs (available)
					DO RECHFORMEL($$$INKALKPREISCalculation(BEZIEHUNG1),.YPREIS)  ;FORMEL RECHNEN ;formula have faith in 
					
				} else {  
					if idBasedOn'="" {
						SET DATE=$ORDER(^INARTPREISBE1(YM,idItem,idLoc,idBasedOn,dteToday+1),-1)
						
						if DATE'="" {
							SET RELATION=$GET(^INARTPREISBE1(YM,idItem,idLoc,idBasedOn,DATE,1))
							set curRelBase = $$$INARTPREISBE1Base(RELATION)
							
							IF +curRelBase'=0 {  ;PREIS IN BEZIEHUNG ;prize within bearing 
								IF $$$INARTPREISBE1Entered(RELATION)'="" {
									SET YPREIS=$$$INARTPREISBE1Entered(RELATION)   ;BEC;25195;25.02.04
								} else {
									SET YPREIS=curRelBase ;PREIS IN BEZIEHUNG ;prize within bearing
								}
								DO RECHFORMEL($$$INKALKPREISCalculation(BEZIEHUNG1),.YPREIS)  ;FORMEL RECHNEN ;formula have faith in 
							}
						}
					}
					if idPriceBasis'="" {
						SET YPREIS=$$^INARTKOST(idItem,1,idPriceBasis)
						DO RECHFORMEL($$$INKALKPREISCalculation(BEZIEHUNG1),.YPREIS)  ;FORMEL RECHNEN ;formula have faith in 
					}
				}
				
				;ENTERED PRICE
				IF $$$INARTPREISBE1Readjustment(YFELD)'="" {
					DO RECHFORMEL($$$INARTPREISBE1Readjustment(YFELD),.EPRICE)	;ENTERED PRICE
				}
				
				IF $$$INKALKPREISRoundPrice(BEZIEHUNG1)=1 {
					SET YPREIS=$$^INROUND(YPREIS,WARENGRUPPE,+$$$INKALKPREISCalculatewithStandardItem(BEZIEHUNG1))		;BEC;24.07.03;RUNDUNGSFUNKTION EINGESCHALTET
				}
				
				IF (YPREIS'=0) {
					SET $$$INARTPREISBE1Base(YFELD)    = YPREIS
					SET $$$INARTPREISBE1Entered(YFELD) = EPRICE          ;BEC;25408;02.04.04;ENTERED PRICE
					SET YOK=$$^WWWSPEI("INARTPREISBE1",idItem_","_idLoc_","_PREISKZ_","_YDATEI,YFELD,1)
					
					IF '$DATA(^INARTPREISBE(YM,idItem,idLoc)) {
						SET YFELD=""
						SET $$$INARTPREISBEDateCreated(YFELD)=dteToday
						SET $$$INARTPREISBECreatedBy(YFELD)=YBED
						SET YOK=$$^WWWSPEI("INARTPREISBE",idItem_","_idLoc,YFELD,1)
					}
				}
			}
		$$$End
	}
	
	QUIT strStatus
	
	
DRUCK      ;TABELLE ANZEIGEN  ;tabulation display 
	NEW ZUVOR,PREISKZ,A,YDATEI1,GUELTIG,WG,idItem,idLoc
	
	DO HEAD  ;TABLLENKOPF
	SET YDDSATZ=0
	SET COLOR=""
	
	set idItem = $$$KEY1(YKEY)
	set idLoc = $$$KEY2(YKEY)
	
	QUIT:idItem=""
	QUIT:idLoc=""
	
	NEW YDATEI,AKTION,AKTION1,SPERRE
	
	SET SPERRE=$$$INARTPREISBEItemDisabled($GET(^INARTPREISBE(YM,idItem,idLoc,1)))  ;FARBE FÜR ARTIKELSPERRE  ;tincture to 
	IF +SPERRE'=0 DO
	. SET COLOR = $$$WWW101ColorCode($GET(^WWW101(0,"SPERRE",SPRACHE,SPERRE,1)))
	. IF COLOR'="" SET COLOR=$$$WWW100Text($GET(^WWW100(0,"FARBE",SPRACHE,COLOR,1)))
	. IF COLOR=""  SET COLOR=YRED 
	
	SET WG=WARENGRUPPE
	IF '$DATA(^INKALKPREIS(YM,WARENGRUPPE)) IF SWG'="" SET WG=SWG
	SET PREISKZ=""
	FOR  SET PREISKZ=$ORDER(^INKALKPREIS(YM,WG,PREISKZ)) QUIT:PREISKZ=""  DO
	. SET GUELTIG=""
	. SET YDATEI=""
	. FOR A=1:1:10 SET YDATEI=$ORDER(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI),-1) QUIT:YDATEI=""  DO
	. . SET YDATEI1=$GET(^INARTPREISBE1(YM,idItem,idLoc,PREISKZ,YDATEI,1))
	. . DO ANZ ;ANZEIGEN AKTION ;display 
	
	QUIT
	
HEAD   ;KOPF ;heading 
	WRITE "<FONT SIZE=2><B>"
	WRITE $$^WWWTEXT(32534)  ;Preisinformation  ;Price Request 
	WRITE "</FONT></B><BR>"
	DO START100^WWWTAB  ;STARTEN DER ÜBERSCHRIFT ;launching the superscription 
	WRITE "<THEAD>" 
	DO NL^WWWTAB
	FOR I=1:1 QUIT:$PIECE(UES(1),Y,I,99)=""  DO 
	. WRITE YCR      WRITE "<TH WIDTH=120 " WRITE " BGCOLOR="_YDARKGRAY WRITE ">" WRITE "<FONT SIZE=2 >" 
	. WRITE $PIECE(UES(1),Y,I),"&nbsp;"
	. DO EH^WWWTAB
	
	DO EL^WWWTAB
	WRITE "</THEAD>"
	
	QUIT
	
ANZ ;ANZEIGEN IN DER TAB ;display within the 
	NEW UGUELTIG
	
	SET UGUELTIG=""
	QUIT:YDATEI=""
	
	IF $PIECE(YDATEI1,Y,10)'="" IF $PIECE(YDATEI1,Y,10)         SET UGUELTIG=1   ;WENN GESPERRT ;when DISABLED 
	IF YDATEI'=""               IF YDATEI>$HOROLOG              SET UGUELTIG=1   ;AB GÜLTIGKEIT ;Confirm. 
	IF $PIECE(YDATEI1,Y,5)'=""  IF $PIECE(YDATEI1,Y,5)<$HOROLOG SET UGUELTIG=1   ;BIS GÜLTIGKEIT ;until 
	IF GUELTIG=1 SET UGUELTIG=1                                                  ;WENN SCHON EIN GÜLTIGER GEFUNDEN WURDE, SIND ALLE ANDEREN UNGÜLTIG
	DO NL^WWWTAB
	DO NF^WWWTAB
	QUIT:YDATEI=""
	
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK,WG
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF +SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. IF PREISKZ'="" WRITE PREISKZ_" "
	. SET WG=WARENGRUPPE
	. IF '$DATA(^INKALKPREIS(YM,WARENGRUPPE)) IF SWG'="" SET WG=SWG
	. WRITE $PIECE($GET(^INKALKPREIS(YM,WG,PREISKZ,1)),Y,1)  ;PREISKENNZEICHEN
	. WRITE "</FONT></A>"
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. IF +$PIECE(YDATEI1,Y,2)'=0 WRITE "<STRIKE>"
	. WRITE $$^WWWZAHL($PIECE(YDATEI1,Y,1))   ;BERECHNETER PREIS ;prize 
	. WRITE "</FONT></A>"
	. IF +$PIECE(YDATEI1,Y,2)'=0 WRITE "</STRIKE>"
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">"
	. IF $PIECE(YDATEI1,Y,2)'="" WRITE $$^WWWZAHL($PIECE(YDATEI1,Y,2))   ;KORRIGIERTER PREIS ;prize 
	. WRITE "</FONT></A>"
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. IF $PIECE(YDATEI1,Y,10)'="" WRITE $GET(^WWW100(0,"JA/NEIN",SPRACHE,$PIECE(YDATEI1,Y,10),1))   ;BERECHNETER PREIS ;prize 
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. WRITE $$^WWWDATE(YDATEI)   ;GÜLTIG AB ;valuable Confirm. 
	. WRITE "<FONT SIZE=2>"
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. WRITE $$^WWWDATE($PIECE(YDATEI1,Y,5))   ;GÜLTIG BIS ;valuable until 
	. WRITE "<FONT SIZE=2>"
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""
	. WRITE $$^WWWFELDNAME("INARTPREISBE1","D",6)_": "_YCR
	. WRITE $EXTRACT($TRANSLATE($PIECE(YDATEI1,Y,6),"|"," "),0,500)   ;BEMERKUNG ;remark 
	. IF $EXTRACT($TRANSLATE($PIECE(YDATEI1,Y,6),"|"," "),501)'="" WRITE ".."
	. WRITE """" 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. WRITE $EXTRACT($TRANSLATE($PIECE(YDATEI1,Y,6),"|"," "),0,40)   ;BEMERKUNG ;remark 
	. IF $EXTRACT($TRANSLATE($PIECE(YDATEI1,Y,6),"|"," "),41)'="" WRITE ".."
	. WRITE "<FONT SIZE=2>"
	
	WRITE "&nbsp;"
	DO ZWR^WWWTAB
	DO
	. NEW YKEY1
	. SET YKEY1=YKEY
	. NEW YFORM,YKEY,YBACK
	. SET YFORM="INARTPREISBE1"
	. WRITE "<A"
	. WRITE " TITLE="""_$$^WWWTEXT(374)_""""   ;DATENSATZ AUSWÄHLEN ;data record pick out 
	. WRITE " HREF="""
	. WRITE YAKTION_"EP=WWWFORM&YFORM="_YFORM_"&YKEY="_YKEY1_","_PREISKZ_","_YDATEI
	. SET YBACK="INARTPREISBE,"
	. DO ^WWWCGI
	. WRITE """>"
	. WRITE "<FONT SIZE=2>"
	. IF +SPERRE=0 IF UGUELTIG=1 WRITE "<FONT COLOR="_YRED_">"
	. IF SPERRE'="" IF SPERRE'=0 WRITE "<FONT COLOR="_COLOR_">" 
	. IF $PIECE(YDATEI1,Y,11)'="" WRITE $EXTRACT($PIECE($GET(^INVKVW(YM,$PIECE(YDATEI1,Y,11),1)),Y,1),0,100)  ;WERBEMAßNAHME
	. WRITE "<FONT SIZE=2>"
	
	WRITE "&nbsp;"
	DO EF^WWWTAB
	DO EL^WWWTAB
	IF UGUELTIG'=1 SET GUELTIG=1
	QUIT
	
	
OnBeforeConstruction(pstrKey,&pidGroup)
	;-------------------------------------------------------------------------------
	; Populate the item group field
	;
	; Params:	pstrKey		- INARTPREISBE keys (item,location)
	; 			pidGroup	- group id (YINHALT)
	;
	; ByRefs:	pidGroup
	;
	; Returns:	
	;
	; History:
	; 12-Oct-2006	JW		SR15111: Created - moved from form def
	;-------------------------------------------------------------------------------
	new idItem,idItemGroup
	
	set idItem = $$$KEY1(pstrKey)
	
	if (idItem'="") {
		set idItemGroup = $$$INARTItemGroup($GET(^INART(YM,idItem,1)))
		if idItemGroup'="" {
			set pidGroup = idItemGroup
		}
	}
	
	quit
	
]]></Routine>
</Export>