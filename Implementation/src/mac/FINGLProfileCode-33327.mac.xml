<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINGLProfileCode" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINGLProfileCode
	
#include FINConst
#include COMSYS
	
OnBeforeButtonLine1()
	;set VORG(1)="FINGLProfileCode"
	;set VORG(2)="FINGLProfileCodeTemp"
	;kill ^WWW120(0,"FINGLProfileCodeTemp")
	;do ^WWWCOFA
	;do ^WWWFORM
	;set $piece(^WWW120(0,"FINGLProfileCodeTemp",1),Y,93)="do OnBeforeButtonLine^FINGLProfileCode(YKEY)"
	quit
	
	
OnFormConstruction()
	set YHID=2
	quit
	
	
OnBeforeFormConstructionKey()
    ;-------------------------------------------------------------------------------
    ; Code to suppress the 'Code' primary key from being displayed.
    ; Unfortunately @netManager doesn't seem to support this for primary keys yet.
    ;
    ; Returns:
    ;
    ; History:
    ; 15-Oct-2004	shobby	Created
    ;-------------------------------------------------------------------------------
	set YHID=2
	quit
	
	
OnBeforeFormConstruction(pYKEY,pintField)
    ;-------------------------------------------------------------------------------
    ; Disables the Primary Key and non required parent fields.  (SR10120)
    ;
    ; Returns:
    ;
    ; History:
    ; 07-Sep-2004	Shobby	Created
    ;-------------------------------------------------------------------------------
	new intCount,idFINGLProfileCodeType,idFINGLProfileCode
	
	set intCount=0
	if $get(pYKEY)'="" {
		set idFINGLProfileCode = $$$KEY1(pYKEY)
		set idFINGLProfileCodeType=""
		for {
			set idFINGLProfileCodeType=$order(^FINGLProfileCodeTypes(YM,2,idFINGLProfileCode,idFINGLProfileCodeType))
			quit:idFINGLProfileCodeType=""
			set intCount=intCount+1
		}
		
		if pintField=5 {
			if intCount=0 {
				set YHID=2
			}
		} elseif pintField=6 {
			if intCount<2 {
				set YHID=2
			}
		} elseif pintField=7 {
			if intCount<3 {
				set YHID=2
			}
		}
	}
	
	quit
 
 
OnBeforeDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Checks whether this item can be deleted.
	; 
	; History
	; 09-Mar-2004		shobby		Creation
	; 
	;-------------------------------------------------------------------------------
	;
	quit $$CheckLinks^FINSYS(pYKEY,$lb("FINGLProfile"),"FINGLProfileCode")
 
 
OnAfterSave(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Can change primary key after save
    ;
    ; Returns:
    ;
    ; History:
    ; 12-Jul-2005	Steve S	 Added check for null subscripts
    ; 22-Jun-2004   Shobby   Created
    ;-------------------------------------------------------------------------------
	new idCodeType,idCode,objFINGLProfile,strStatus
	
	if $get(pYKEY)'="" {
		set idCodeType = $$$KEY1(pYKEY)
		set idCode     = $$$KEY2(pYKEY)
		
		if (idCodeType'="") && (idCode'="") {
			set objFINGLProfile=$get(^FINGLProfileCode(0,idCodeType,idCode,1))
			if idCode'=$$$FINGLProfileCodeCodeTemp(objFINGLProfile) {
				if $$$FINGLProfileCodeCodeTemp(objFINGLProfile)="" {
					set $$$FINGLProfileCodeCodeTemp(objFINGLProfile) = idCode
					set strStatus = $$$Save("FINGLProfileCode",pYKEY,objFINGLProfile,$$$YES)
				
				} else {
					set strStatus = $$$Save("FINGLProfileCode",idCodeType_$$$COMMA_$$$FINGLProfileCodeCodeTemp(objFINGLProfile),objFINGLProfile,$$$YES)
					do KILL^FINSYS("FINGLProfileCode",pYKEY)
				}
			}
		}
	}
	
	quit
 
 
OnAfterPrimaryKey(pYKEY)
	quit
 
 
OnBeforeButtonLine(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Dodgy hack to modify the Profile Code screen for dynamic data entry.
    ;
    ; Returns:
    ;
    ; History:
    ; 01-Jul-2004   Shobby   Created (SR10027)
    ;-------------------------------------------------------------------------------
	new idProfileCodeType,idKey,objFINGLProfileCodeType,intParent,intIndex
	
	for intParent=5:1:7 {
		set $piece(^WWW122(0,"FINGLProfileCode",intParent,1),Y,32) = ""
		set $piece(^WWW122(0,"FINGLProfileCode",intParent,1),Y,33) = ""
		set $piece(^WWW122(0,"FINGLProfileCode",intParent,1),Y,34) = ""
		set $piece(^WWW003(0,"FINGLProfileCode",intParent,1),Y,8)  = ""
		set $piece(^WWW003(0,"FINGLProfileCode",intParent,1),Y,9)  = ""
		set $piece(^WWW003(0,"FINGLProfileCode",intParent,1),Y,10) = ""
	}
	
	if $get(pYKEY)'="" {
		set idKey = $$$KEY1(pYKEY)
		set idProfileCodeType = ""
		for {
			set idProfileCodeType = $order(^FINGLProfileCodeType(0,idProfileCodeType))
			quit:idProfileCodeType=""
			
			set objFINGLProfileCodeType=$get(^FINGLProfileCodeType(0,idProfileCodeType,1))
			if $$$FINGLProfileCodeTypeUniqueParentTo(objFINGLProfileCodeType)=idKey {
				set intIndex = +$$$FINGLProfileCodeTypeIndex1(objFINGLProfileCodeType)
				if (intIndex>0) && (intIndex<4) {
					set intParent = 4+intIndex
					set $piece(^WWW122(0,"FINGLProfileCode",intParent,1),Y,32) = "FINGLProfileCode"
					set $piece(^WWW122(0,"FINGLProfileCode",intParent,1),Y,33) = """"_idProfileCodeType_""""
					set $piece(^WWW122(0,"FINGLProfileCode",intParent,1),Y,34) = 1
					set $piece(^WWW003(0,"FINGLProfileCode",intParent,1),Y,8)  = "FINGLProfileCode"
					set $piece(^WWW003(0,"FINGLProfileCode",intParent,1),Y,9)  = """"_idProfileCodeType_""""
					set $piece(^WWW003(0,"FINGLProfileCode",intParent,1),Y,10) = 1
				}
			}
		}
	}
	quit
	
]]></Routine>
</Export>