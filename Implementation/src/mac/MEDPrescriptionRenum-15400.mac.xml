<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="MEDPrescriptionRenum" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
MEDPrescriptionRenum
#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst


ReNumber(pidRx)
	;-------------------------------------------------------------------------------
	; Re-Number Lines 
	; required.
	;
	; Returns:
	;
	; History:
	; 17-May-2012	SCR		SR17993 Created
	;-------------------------------------------------------------------------------

	new strStatus
	
	set strStatus = $$Transaction^COMTransaction("ReNumberTxn^MEDPrescriptionRenum("""_pidRx_""")",$$$YES)
	;set strStatus=$$$OK
	quit strStatus

ReNumberTxn(pidRx)
	;-------------------------------------------------------------------------------
	; Re-Number Lines 
	; required.
	;
	; Returns:
	;
	; History:
	; 27-Nov-2012	SCR		SR18213 Remove Solution number from line number
	; 17-May-2012	SCR		SR17993 Created
	;-------------------------------------------------------------------------------
#dim objRx,idLine,objLine,strStatus as %String
#dim arrLines,arrSol as %ArrayOfDataTypes
#dim intSol,intLine as %Integer
	new objLine,objRx,idLine,arrLines,arrSol,strStatus,intLine,intSol
	
	set strStatus=$$$OK
	if $$$NoKey(pidRx) {
		set strStatus=  "MED00364"  ;No Prescription number
		quit strStatus 
	}
	set objRx=$get(^MEDPrescription(YM,pidRx,1))
	if $$$MEDPrescriptionStatus(objRx)>0 {
		set strStatus= $listbuild("MED01279",$$$MEDPrescriptionStatus(objRx))  ;Can not Renumber with Status %1
	}
	if strStatus=$$$OK {
		kill arrLines
		set idLine=""
		set intLine=0
		for {
			set idLine=$order(^MEDPrescriptionLine(YM,pidRx,idLine))
			quit:idLine=""
			set objLine=$get(^MEDPrescriptionLine(YM,pidRx,idLine,1))
			if $$$MEDPrescriptionLineItem(objLine)="" {
				continue  ; dont save blank lines
			}
			set intSol= $$$MEDPrescriptionLineSolution(objLine)
			if intSol {  ; if solution
				if '$data(arrSol(intSol)) { ; if 1st solution line
					;set intLine=$order(arrLines(""),-1)+1\1
					set intLine=$order(arrLines(""),-1)\100+1*100
					set arrLines(intLine)=objLine
					set arrSol(intSol)=intLine
				} else {
					;set arrSol(intSol)=arrSol(intSol)+.1
					set arrSol(intSol)=arrSol(intSol)+1
					set arrLines(arrSol(intSol))=objLine
				}
			} else { ; if not solution add to next line
				;set intLine=$order(arrLines(""),-1)+1\1
				set intLine=$order(arrLines(""),-1)\100+1*100
				set arrLines(intLine)=objLine
			} 
			
		}
	}
	
	if strStatus=$$$OK {
		set intLine=0 
		for {
			set idLine=$order(^MEDPrescriptionLine(YM,pidRx,idLine))
			quit:idLine=""
			set strStatus=$$$Kill("MEDPrescriptionLine",pidRx_","_idLine)
			quit:strStatus'=$$$OK			
		}
	}
	if strStatus=$$$OK {
		set intLine=""
		for idLine=1:1 {  ;SR18213
		;for {
			set intLine=$order(arrLines(intLine))
			quit:intLine=""
			set objLine=arrLines(intLine)
			set strStatus=$$$Save("MEDPrescriptionLine",pidRx_","_idLine,objLine,1) ; SR18213
			;set strStatus=$$$Save("MEDPrescriptionLine",pidRx_","_intLine,objLine,1)
			quit:strStatus'=$$$OK
		}
	}
	quit strStatus

]]></Routine>
</Export>