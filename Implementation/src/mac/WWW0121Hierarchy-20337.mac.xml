<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW0121Hierarchy" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW0121Hierarchy
#include COMSYS
#include WWWConst
	;-------------------------------------------------------------------------------
	;	Entry Points - Routines associated with traversing the hierarchy
	;	If a value is not defined for the specified location check up the location
	;	hierarchy one step at a time until something is found.
	; 
	; In each case...
	; 
	; Params:
	;	pidCompany		company YM
	;	pidLocn			base location
	;	pidItem			stock item - where applicable (some future expectations)
	;	
	; Returns:
	; 	Contents of desired field at lowest location level found to have a value
	; 
	; History:
	; 25-Oct-2007	GRF		SR15563: Created
	;-------------------------------------------------------------------------------
	
DefaultOnHandStorage(pidCompany,pidLocn,pidItem)
	new strFound
	set strFound = $$DefaultLocnEntry($get(pidItem),$get(pidCompany),$get(pidLocn),6)  ; D6 $$$FldINARTLOCPARDefaultOnHandStorage
	set:strFound="" strFound = $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121OnHandStorage)  ; D40
	quit strFound
	
DefaultReceiptStorage(pidCompany,pidLocn,pidItem)
	new strFound
	set strFound = $$DefaultLocnEntry($get(pidItem),$get(pidCompany),$get(pidLocn),7)  ; D7 $$$FldINARTLOCPARDefaultReceiptStorage
	set:strFound="" strFound = $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121ReceiptStorage)         ; D41
	quit strFound
	
DefaultDespatchStorage(pidCompany,pidLocn,pidItem)
	new strFound
	set strFound = $$DefaultLocnEntry($get(pidItem),$get(pidCompany),$get(pidLocn),6)  ; D8 $$$FldINARTLOCPARDefaultOnHandStorage
	set:strFound="" strFound = $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121DespatchStorage)        ; D42
	quit strFound
 
DefaultSuppReturnStorage(pidCompany,pidLocn,pidItem)   ; pidItem not used at present
	quit $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121SuppReturnStorage)      ; D43
 
DefaultCustReturnStorage(pidCompany,pidLocn,pidItem)   ; pidItem not used at present
	quit $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121CustReturnStorage)      ; D44
 
DefaultMissingStockStorage(pidCompany,pidLocn,pidItem)
	new strFound
	set strFound = $$DefaultLocnEntry($get(pidItem),$get(pidCompany),$get(pidLocn),6)  ; D9 $$$FldINARTLOCPARDefaultOnHandStorage
	set:strFound="" strFound = $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121MissingStockStorage)    ; D45
	quit strFound
 
DefaultDamagedStockStorage(pidCompany,pidLocn,pidItem)
	new strFound
	set strFound = $$DefaultLocnEntry($get(pidItem),$get(pidCompany),$get(pidLocn),6)  ; D10 $$$FldINARTLOCPARDefaultOnHandStorage
	set:strFound="" strFound = $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121DamagedStockStorage)    ; D46
	quit strFound
 
TaxLocation(pidCompany,pidLocn)
	quit $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121TaxLocation)            ; D52
 
PostingCharacteristic1(pidCompany,pidLocn)
	quit $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121PostingCharacteristic1) ; D70	
	
PostingCharacteristic2(pidCompany,pidLocn)
	quit $$DefaultEntry($get(pidCompany),$get(pidLocn),$$$FldWWW0121PostingCharacteristic1) ; D71	
	
 
	;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
	
DefaultEntry(pidCompany,pidLocn,pintFieldNo) private
	;-------------------------------------------------------------------------------
	;	Get the nominated Default Entry - if not defined for the specified
	;	location check up the location hierarchy one step at a time until something
	;	is found.
	;
	; Params:
	;	pidCompany		company YM
	;	pidLocn			base location
	;	pintFieldNo		desired field in class WWW0121
	;	
	; Returns:
	; 	Contents of desired field at lowest location level found to have a value
	;
	; History:
	; 25-Oct-2007	GRF		SR15563: Created
	;-------------------------------------------------------------------------------
	new idNextLocn,loop,objLocation,strFound,strSavePath
	
	quit:(pidCompany="")||(pidLocn="") ""
	quit:+pintFieldNo=0 ""
	
	set strFound    = ""
	set strSavePath = $$GetPath^WWW0121Utils(pidCompany,pidLocn)
	for loop=1:1:$length(strSavePath,$$$COMMA) {
		quit:strFound'=""
		
		set idNextLocn  = $piece(strSavePath,$$$COMMA,loop)
		continue:idNextLocn=""
		
		set objLocation = $get(^WWW0121(0,pidCompany,idNextLocn,1))
		set strFound    = $piece(objLocation,Y,pintFieldNo)
	}
	quit strFound
	
	
DefaultLocnEntry(pidItem,pidCompany,pidLocn,pintFieldNo) private
	;-------------------------------------------------------------------------------
	;	Get the nominated Default Item/Location Entry - if not defined for the specified
	;	location check up the location hierarchy one step at a time until something
	;	is found.
	;
	; Params:
	;	pidItem			stock item
	;	pidCompany		company YM
	;	pidLocn			base location
	;	pintFieldNo		desired field in class INARTLOCPAR
	;
	; Returns:
	; 	Contents of desired field at lowest location level found to have a value
	;
	; History:
	; 25-Oct-2007	GRF		SR15563: Created
	;-------------------------------------------------------------------------------
	new idNextLocn,loop,objItemLocn,strFound,strSavePath
	
	quit:(pidCompany="")||(pidLocn="")||(pidItem="") ""
	quit:+pintFieldNo=0 ""
	
	set strFound    = ""
	set strSavePath = $$GetPath^WWW0121Utils(pidCompany,pidLocn)
	for loop=1:1:$length(strSavePath,$$$COMMA) {
		quit:strFound'=""
		
		set idNextLocn  = $piece(strSavePath,$$$COMMA,loop)
		continue:idNextLocn=""
		
		set objItemLocn = $get(^INARTLOCPAR(pidCompany,pidItem,idNextLocn,1))
		set strFound    = $piece(objItemLocn,Y,pintFieldNo)
	}
	quit strFound
	

	
]]></Routine>
</Export>