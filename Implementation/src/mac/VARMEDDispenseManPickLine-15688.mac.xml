<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARMEDDispenseManPickLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARMEDDispenseManPickLine

#include COMConst
#include INConst
#include COMGridEdit31
#include MEDConst
#include VARConst
#define YCR	"|"
#define FORMFldVARMEDDispenseManPickLineRxItem 	1
Enabled(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By : Form VARMEDDispenseMan : Grid Tab (COMGridEditParameter)
	;
	;
	; History:
	; 30-May-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new blnEnabled
	
	set blnEnabled = $$$YES
	
	if '$$$NoKey(pYKEY) {

		if $$$MEDDispenseStatus(pYFELD) > 3   {   ;"Partially Processed"
			set blnEnabled = $$$NO
		}
	}
	
	quit blnEnabled

LoadLines(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By :  VARMEDDispenseMan.mac
	;
	;
	; History:
	; 28-Aug-2014	SCR		HEVA-1592: Check for 'CEF' (FREE3)
	; 07-Jul-2014	SCR		HEVA-1505: Corrected Unit selection
	; 30-May-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new fltQtyReq,idCount,idItem,idLine,idLocn,idManOrd,idPPickUOM,idPat,idRx,idSrcDisp
	new idSrcLine,idStatusRx,idToLocn,idToStorage,objDispLine,objManOrd,objPickLine
	new objResultSet,objRxLine,objSrcDisp,objSrcLine,sc,strSQL,strStatus
	do ClearDispenseLines(pYKEY)
	if $data(^MEDDispensePickLine(YM,pYKEY)) {
		quit
	}
	set strStatus = $$$OK
	set idPat	= $$$MEDDispensePatient(pYFELD)
	set idCount	= 0
	set strSQL	= ""
	set strSQL	= strSQL _ "SELECT MEDDispenseLine.Dispense as Dispensation,MEDDispenseLine.Line "
	set strSQL	= strSQL _ "FROM MEDDispenseLine  "
	set strSQL	= strSQL _ "WHERE MEDDispenseLine.Company = 0 "
	set strSQL	= strSQL _ "AND MEDDispenseLine.UDO = 1 " 
	set strSQL	= strSQL _ "AND MEDDispenseLine.ValidatedBy IS NOT NULL "
	set strSQL	= strSQL _ "AND MEDDispenseLine.FREE3 IS NOT NULL "
		
	;set strSQL	= strSQL _ "LEFT JOIN MEDDispense   ON MEDDispense.Company = MEDDispenseLine.Company   AND MEDDispense.Dispensation = MEDDispenseLine.Dispense   "
	;set strSQL	= strSQL _ "LEFT JOIN MEDPrescription   ON MEDPrescription.Company = MEDDispense.Company   AND MEDPrescription.PrescriptionNumber = MEDDispense.Prescription  "
	;set strSQL	= strSQL _ "LEFT JOIN MEDPrescriptionLine ON MEDDispense.Company = MEDPrescriptionLine.Company "
	;set strSQL	= strSQL _ "AND MEDDispense.Prescription = MEDPrescriptionLine.PrescriptionNumber "
	;set strSQL	= strSQL _ "AND MEDDispenseLine.Line = MEDPrescriptionLine.Line "	
	;set strSQL	= strSQL _ "LEFT JOIN INART   ON MEDDispenseLine.Company = INART.Company AND MEDDispenseLine.ItemName = INART.ItemNumber   "
	;set strSQL	= strSQL _ "WHERE MEDDispenseLine.Company = 0  "
	;set strSQL	= strSQL _ "AND MEDDispenseLine.UDO = 1  "
	;set strSQL	= strSQL _ "AND MEDDispenseLine.ValidatedBy IS NOT NULL  "
	;set strSQL	= strSQL _ "AND MEDDispenseLine.FREE3 IS NOT NULL  "
	
	;set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 10 "
	;set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 6 "
	;set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 9 "
	;set strSQL	= strSQL _ "AND MEDPrescriptionLine.Status != 8 "
	;set strSQL	= strSQL _ "AND MEDPrescription.PatientID = ? "
	
	set strSQL	= strSQL _ "AND $$SQLGetPatientFromDisp^VARSQL(MEDDispenseLine.Dispense) = ? "
	set objResultSet	= ##class(%Library.ResultSet).%New()
	set sc				= objResultSet.Prepare(strSQL)
	set:sc sc			= objResultSet.Execute(idPat)
	set idLine	= 0
	set idLocn			= $$$MEDDispenseSupplyingLocation(pYFELD)
 	set idToLocn		= $$$MEDDispenseDestinationLocation(pYFELD)
	if sc {
		while objResultSet.Next() {
			quit:$$$ISERR(strStatus)
			;set idManOrd	= objResultSet.Data("ManipulationOrder")
			set idSrcDisp	= objResultSet.Data("Dispensation")
			set idSrcLine	= objResultSet.Data("Line")
			set idManOrd	= ""
			set objManOrd	= ""
			set idToStorage	= ""
			set objSrcDisp	= $get(^MEDDispense(YM,idSrcDisp,1))
			set objSrcLine	= $get(^MEDDispenseLine(YM,idSrcDisp,idSrcLine,1))
			set idRx		= $$$MEDDispensePrescription(objSrcDisp)
			set objRxLine	= $get(^MEDPrescriptionLine(YM,idRx,idSrcLine,1))
			set idStatusRx	= $$$MEDPrescriptionLineStatus(objRxLine)
			continue:idStatusRx=10
			continue:idStatusRx=6
			continue:idStatusRx=9
			continue:idStatusRx=8
			if (idSrcDisp'="")&&(idSrcLine'="") {
				set idManOrd=$order(^VARManipulacaoMagistrals(YM,1,$$$Index(idSrcDisp),$$$Index(idSrcLine),""))
				if idManOrd'="" {
					set objManOrd=$get(^VARManipulacaoMagistral(YM,idManOrd,1))
				}
			}

			set objSrcDisp	= $get(^MEDDispense(YM,idSrcDisp,1))
			set objSrcLine	= $get(^MEDDispenseLine(YM,idSrcDisp,idSrcLine,1))
			set objDispLine	= objSrcLine
			set objPickLine		= ""			
			set idItem		= $$$VARManipulacaoMagistralItem(objManOrd)
			;set fltQtyReq	= $$$VARManipulacaoMagistralQuantity(objManOrd)
			set idPPickUOM	= $$$VARManipulacaoMagistralUnit(objManOrd)
			if (idToLocn'="")&&(idItem'="") {
				set idToStorage = $$GetStorage^INIssueAuto(idItem,idToLocn,,.idToStorage)
			}
			set $$$MEDDispensePickLinePickUOM(objPickLine)		= idPPickUOM
			set $$$MEDDispensePickLineToStorage(objPickLine)	= idToStorage
			set $$$MEDDispenseLineUDODispense(objDispLine)		= idSrcDisp
			set $$$MEDDispenseLineUDODispenseLine(objDispLine)	= idSrcLine
			set $$$MEDDispenseLineWorkOrder(objDispLine)		= idManOrd
			set $$$MEDDispenseLineItemName(objDispLine)			= idItem
			if idItem'="" {
				set objDispLine	= $$UpdateDispenseLine^MEDDispenseCalc(idSrcDisp,idSrcLine,objDispLine)
			}
			set fltQtyReq	= $$$MEDDispenseLineIssueQuantity(objDispLine)
			set $$$MEDDispensePickLineItem(objPickLine)			= idItem
			set $$$MEDDispensePickLineRequestedQty(objPickLine)	= fltQtyReq
			set idLine		= idLine + 1
			set strStatus	=  $$$Save("MEDDispenseLine",pYKEY_","_idLine,objDispLine,$$$YES)
			quit:$$$ISERR(strStatus)
			set strStatus	=  $$$Save("MEDDispensePickLine",pYKEY_","_idLine,objPickLine,$$$YES)
			quit:$$$ISERR(strStatus)
			if idManOrd'="" {
				set $$$VARManipulacaoMagistralManDispense(objManOrd)	= pYKEY
				set $$$VARManipulacaoMagistralManDispenseLine(objManOrd)= idLine
				set strStatus	=  $$$Save("VARManipulacaoMagistral",idManOrd,objManOrd,$$$YES)
			}
			quit:$$$ISERR(strStatus)
			
		}
	}

	quit
ClearDispenseLines(pidDisp)
	;-------------------------------------------------------------------------------
	; Clear Dispense Line if there is no Pick Line
	;
	;
	; History:
	; 12-June-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new arrLine,idLine,strStatus
	set idLine	= ""
	for {
		set idLine	= $order(^MEDDispensePickLine(YM,pidDisp,idLine))
		quit:idLine=""
		set arrLine(idLine\1)=""
	}
	set idLine	= ""
	for {
		set idLine	= $order(^MEDDispenseLine(YM,pidDisp,idLine))
		quit:idLine=""
		if '$data(arrLine(idLine)) {
			set strStatus = $$$Kill("MEDDispenseLine",pidDisp_","_idLine)		
		}
	}
		
	quit
OnBeforeFormat(pYFIELDNAME,pobjLine)  
	;-------------------------------------------------------------------------------
	; set the format
	;	
	; Called By :
	;	Form - GridEdit:VARMEDDispenseManPickLine
	;	Routines -
	;		
	; Inputs : 
	;	pYFIELDNAME	-	Browser Element ID "tdY#row#_#col#"
	;	pobjLine	-	@nM INIssueLine Data record
	;
	; ByRef :
	;
	; Returns : 
	;
	; History :
	; 20-Jan-2015	HGD		HEVA1694: Modified the DoseQty to show "0,56" instead ".56" on Dosage Field
	; 05-Sep-2014	SCR		HEVA1505: Added Item locking
	; 02-Jun-2014	SCR		HEVA1505: Created
	;-------------------------------------------------------------------------------
	new arrLines,blnOK,idBundle,idClassField,idFreq,idItem,idLine,idManDisp,idManLine
	new idRx,idRxItem,intCol,intRow,objBundle,objDisp,objDispLine,objManDisp,objManDispLine
	new objRxLine

	quit:YKEY=""

	; Grid Position
	;---------------------------------------
	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)
	
	set blnOK		= $$GetVars(YKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)

	quit:'blnOK

	if (intCol = 1) {	
		set YTEXT		= "("_idRxItem_") "_$$^INARTNAME(idRxItem)
		set YTOOLTIP	= $$$Text($listbuild("VAR8897",idRx))  ;Prescription
	}
	if (intCol = 2) {
		;set YTEXT	=  $$$MEDPrescriptionLineDoseQty(objRxLine)_" "_$$$AppEnum("EINHEIT", $$$MEDPrescriptionLineDoseUOM(objRxLine))
		set YTEXT	=  $$^WWWTR(0,12,$$$MEDPrescriptionLineDoseQty(objRxLine))_" "_$$$AppEnum("EINHEIT", $$$MEDPrescriptionLineDoseUOM(objRxLine))
	}
	if (intCol = 3) {
		set idFreq	= $$$MEDPrescriptionLineFrequency(objRxLine)
		quit:idFreq=""
		set YTEXT	= $$$MEDFrequencyFrequency($get(^MEDFrequency(YM,idFreq,1)))
	}
	if (intCol = 4) {	
		set idItem	= $$$MEDDispensePickLineItem(pobjLine)
		if idItem'="" {
			set YTEXT	= "("_idItem_") "_$$^INARTNAME(idItem)
		}
		set idDisp	= $$$KEY1(YKEY)	
		set idLine	= $$$KEY2(YKEY)
		if idLine'=(idLine\1) {
			set YENABLED = $$$NO  ; Sub Lines can not have the item changed
			set YTOOLTIP	= $$$Text($listbuild("VAR8900"))  ;Item can not be changed when it is a Sub Line
		} else {
			do GetGridContents^COMGridEdit31G(.arrLines,"VARMEDDispenseManPickLine")
			set idNextLine	= $order(arrLines(idDisp,idLine))
			if idNextLine\1=idLine {
				set YENABLED = $$$NO  ; If the Line has sub lines the item can not be changed
				set YTOOLTIP	= $$$Text($listbuild("VAR8901"))  ;Item can not be changed if it has Sub Lines
			}
		}
	}
	if (intCol = 6) {	
	}
	if (intCol = 7) {	
		set idBundle	= $$$MEDDispensePickLineBundle(pobjLine)
		if idBundle'="" {
			set objBundle	= ##class(alSOH.dBundle).%OpenId(idBundle)
			set YTEXT		= ##class(alSOH.iUSOH).RenderBundle(objBundle,SPRACHE)
		}
	}
	quit

GetVars(YKEY,&pidManDisp,&pidManLine,&pobjManDisp,&pobjManDispLine,&pobjDisp,&pobjDispLine,&pidRx,&pobjRxLine,&pidRxItem)	
	;-------------------------------------------------------------------------------
	; Get Vars
	;	
	;
	; History :
	; 02-Jun-2014	SCR		HEVA1505: Created
	;-------------------------------------------------------------------------------
	new blnOK,pidDisp,pidDispLine,pidRxLine
	set blnOK		= $$$YES
	set pidManDisp	= $translate($$$KEY1(YKEY),"""","")
	set pidManLine	= $$$KEY2(YKEY)
	set:pidManLine="" blnOK	= $$$NO
	if $$$ISOK(blnOK) {
		set pobjManDisp		= $get(^MEDDispense(YM,pidManDisp,1))
		set pobjManDispLine	= $get(^MEDDispenseLine(YM,pidManDisp,pidManLine\1,1))
		set pidDisp			= $$$MEDDispenseLineUDODispense(pobjManDispLine)
		set pidDispLine		= $$$MEDDispenseLineUDODispenseLine(pobjManDispLine)
		set:pidDispLine="" blnOK	= $$$NO
	}
	if $$$ISOK(blnOK) {
		set pobjDisp			= $get(^MEDDispense(YM,pidDisp,1))
		set pobjDispLine		= $get(^MEDDispenseLine(YM,pidDisp,pidDispLine,1))
		set pidRxLine		= pidDispLine
		set pidRx			= $$$MEDDispensePrescription(pobjDisp)
		set:pidRx="" blnOK	= $$$NO
	}
	if $$$ISOK(blnOK) {
		set pobjRxLine		= $get(^MEDPrescriptionLine(YM,pidRx,pidRxLine,1))
		set pidRxItem		= $$$MEDPrescriptionLineItem(pobjRxLine)
	}
	quit blnOK
	
OnBeforeFormatClass(pYFIELDNAME,pobjLine)
	;-------------------------------------------------------------------------------
	; Called from class hook via MEDDispensePickLine.mac
	;	
	;		
	; Inputs : 
	;	pYFIELDNAME	-	Browser Element ID "tdY#row#_#col#"
	;	pYFELD		-	@nM INIssueLine Data record
	;
	; ByRef :
	;
	; Returns : 
	;
	; History :
	;
	; 09-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new idClassField,intCol,intRow,objHeader
	
	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)

	set objHeader  = $$$GRIDYFELDContainer
	
	quit
	
	
	
OnBeforeDisplayLine(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Code to run on before display line for a line in the grid
	;
	; Params: pYKEY,pYFELD	: The YKEY/YFELD values
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 03-Jun-2014	SCR		HEVA-1505: Created 
	;-------------------------------------------------------------------------------
	new blnOK,idManDisp,idManLine,idRx,idRxItem,objDisp,objDispLine,objManDisp
	new objManDispLine,objRxLine
	set blnOK		= $$GetVars(pYKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)
	quit:'blnOK
	; Sets PITEM

	set PITEM	=  idRxItem
	quit 1
	
QtyOK(pYKEY,pfltPickQty)
	;-------------------------------------------------------------------------------
	; Check the Qty
	;
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 06-Jun-2014	SCR		HEVA-1505: Created 
	;-------------------------------------------------------------------------------
	new blnOK,fltDoseQty,fltRxQty,idManDisp,idManLine,idRx,idRxItem,intDoses,objDisp
	new objDispLine,objManDisp,objManDispLine,objRxLine,objTestLine
	
	do GetVars(pYKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)
	set objTestLine	= $$UpdateDispenseLine^MEDDispenseCalc(idManDisp,idManLine,objManDispLine,$$$YES,,.intDoses,.fltDoseQty)
	set fltRxQty	= $$$MEDDispenseLineIssueQuantity(objTestLine)
	if $$MultidoseInjectable^INART(idRxItem) {
		set fltRxQty	= fltRxQty+.999999\1 
	}
	if (pfltPickQty> fltRxQty ) {
		set blnOK	= $$$NO
	} else {
		set blnOK	= $$$YES
	}
	quit blnOK
	
OnBlurItem(pYKEY, pidGridRow, pidItem, &pobjLine)
	;-------------------------------------------------------------------------------
	; On Blur from Item
	;	Update Dyna Table
	;
	;
	; History :
	; 
	; 06-Jun-2014	SCR		HEAV-1505: Created
	;-------------------------------------------------------------------------------
	new blnOK,fltQty,idDispUOM,idItem,idManDisp,idManLine,idRx,idRxItem,idSrcDisp
	new idSrcLine,idUnit,objDisp,objDispLine,objLine,objManDisp,objManDispLine
	new objRxLine,strStatus
	set blnOK		= $$GetVars(pYKEY,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)
	quit:'blnOK
	quit:pidItem=""
	set idItem	= $$$MEDDispenseLineItemName(pobjLine)
	set:idItem="" idItem	= pidItem
	set $$$MEDDispenseLineItemName(objManDispLine)=idItem
	set idUnit		= $$$MEDDispenseLineIssueUOM(objManDispLine)
	set idRx		= $$$MEDDispensePrescription(objDisp)
	if (idUnit'="") {
		if '$data(^INARTPACK(YM,idItem,idUnit)) {
			set idUnit	= ""
			set $$$MEDDispenseLineIssueUOM(objLine)=idUnit
		}
	}
	set idSrcDisp	= $$$MEDDispenseLineUDODispense(objManDispLine)
	set idSrcLine	= $$$MEDDispenseLineUDODispenseLine(objManDispLine)
	set objManDispLine=$$UpdateDispenseLine^MEDDispenseCalc(idSrcDisp,idSrcLine,objManDispLine)
	set idDispUOM	= $$$MEDDispenseLineIssueUOM(objManDispLine)
	set fltQty		= $$$MEDDispenseLineIssueQuantity(objManDispLine)
	set $$$MEDDispensePickLineRequestedQty(pobjLine)= fltQty
	set $$$MEDDispensePickLinePickUOM(pobjLine)		= idDispUOM
	set strStatus	= $$$Save("MEDDispenseLine",idManDisp_","_idManLine,objManDispLine,$$$YES)
	write "DrawDynTable_GridFieldFocus("_pidGridRow_");"
	quit
	
OnBeforeSave(pYKEY,&pYFELD)
	;-------------------------------------------------------------------------------
	; OnBeforeSave of the MEDDispenseLine
	;
	; History :
	; 
	; 06-Jun-2014	SCR		HEAV-1505: Created
	;-------------------------------------------------------------------------------
	set $$$MEDDispensePickLineIssueQty(pYFELD)	= $$$MEDDispensePickLinePickQty(pYFELD)
	quit
	
OnBeforeDelete(pYKEY,pblnDoAll=$$$NO)
	;-------------------------------------------------------------------------------
	; Called By :  WWW001VARHooks form VARMEDDispenseManPickLine on OnBeforeDelete or VARMEDDispenseMan.mac
	;
	;
	; History:
	; 26-Jun-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new blnOK,idDisp,idLine,idOrder,idPick,idScan,objLine,objOrder,strStatus

	set idDisp	= $$$KEY1(pYKEY)
	set idPick	= $$$KEY2(pYKEY)
	set idLine	= idPick\1
	if (idDisp'="") && (idLine'="") {
		set blnOK=$$$YES
		set idScan	= ""
		for {
			set idScan	= $order(^MEDDispensePickLine(YM,idDisp,idScan))
			quit:idScan=""
			continue:idScan\1'=idLine
			continue:idScan=idPick
			set blnOK	= $$$NO
		}
		if blnOK||pblnDoAll {
			set objLine	= $get(^MEDDispenseLine(YM,idDisp,idLine,1))
			set idOrder	= $$$MEDDispenseLineWorkOrder(objLine)
			if idOrder'="" {
				set objOrder	= $get(^VARManipulacaoMagistral(YM,idOrder,1))
				if ($$$VARManipulacaoMagistralManDispense(objOrder)=idDisp)&&
					($$$VARManipulacaoMagistralManDispenseLine(objOrder)=idLine) {
					set $$$VARManipulacaoMagistralManDispense(objOrder)		= ""
					set $$$VARManipulacaoMagistralManDispenseLine(objOrder)	= ""
					set strStatus	= $$$Save("VARManipulacaoMagistral",idOrder,objOrder,1)
				}
			set strStatus = $$$Kill("MEDDispenseLine",idDisp_$$$COMMA_idLine)
			}
		}
	}
	quit $$$OK
	
OnBeforeDisplayCombo(pstrKeys,pobjLine,pintCol,&pSUCH)
	;-------------------------------------------------------------------------------
	; Set 'PITEM' for Alternative Items drop down
	;
	; set pSUCH as the items to be used when you override the list (set via blnOverride)
	;
	; Inputs:
	;	pstrKeys	- YKEY
	;	pobjLine	- YFELD
	;	pintCol     - Edit Grid Column Id
	;	pSUCH       - Search Array
	; 
	; Returns: $$$YES or $$$NO (override the data) 
	;
	; History:
	; 31-Jul-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new blnOK,blnOverride,fltReqDose,idItem,idMan,idManDisp,idManLine,idRx,idRxItem
	new idxItem,objDisp,objDispLine,objMan,objManDisp,objManDispLine,objRxLine
	
	set blnOverride = $$$NO
	; Sets PITEM
	; F11 - Received Unit
	;---------------------------------------
	if pintCol = 4 {   
		set blnOK	= $$GetVars(pstrKeys,.idManDisp,.idManLine,.objManDisp,.objManDispLine,.objDisp,.objDispLine,.idRx,.objRxLine,.idRxItem)
		set PITEM	= idRxItem
		kill pSUCH
		set pSUCH  = "SUCH"
		set blnOverride = $$$YES
		if idRxItem '= "" {
			set idItem	= ""
			for {
				set idItem		= $order(^INARTA(0,idRxItem,idItem))
				quit:idItem=""
				set fltReqDose	= $$$MEDPrescriptionLineDoseQty(objRxLine)
				if $$ItemOK(idRxItem,idItem,fltReqDose) {
					set pSUCH(idItem) = idItem
				}
			}
		}
	}
	quit blnOverride
	
ItemOK(pidRxItem,pidItem,pfltReqDose)
	;-------------------------------------------------------------------------------
	; Check the Alternative Item is OK to use
	;
	;
	; History:
	; 12-Jan-2015	HEVA:1689
	; 14-Aug-2014	SCR		HEVA-1505: Created
	;-------------------------------------------------------------------------------
	new blnMag,blnOK,idMan,idxItem,objAlt,objMan
	set blnOK		= $$$NO
	set blnMag		= $$$NO
	set idMan		= ""
	set idxItem		= $$$Index(pidItem)
	for {
		set idMan	= $order(^VARManipulacaoMagistrals(YM,3,idxItem,idMan))
		quit:idMan=""
		set blnMag	= $$$YES
		set objMan	= $get(^VARManipulacaoMagistral(YM,idMan,1))
		if $$$VARManipulacaoMagistralConcentrationQuantity(objMan) = pfltReqDose {
			set blnOK	= $$$YES
		}
	}
	if ('blnMag) {
		set objAlt	= $get(^INARTA(0,pidRxItem,pidItem,1))
		//if $$$INARTAConversion(objAlt) = 1 { *HEVA-1689 regra desnecessÃ¡ria
			if $$$INARTAType(objAlt) = 3 {
				set blnOK	= $$$YES
			}
		//}
	}
	quit blnOK
	
]]></Routine>
</Export>