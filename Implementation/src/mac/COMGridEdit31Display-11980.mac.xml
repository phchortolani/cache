<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31Display" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31Display
	
#include COMConst
#include COMSYS	
#include WWWConst
	
#define LogR(%1,%2) 	;
#define LogR2(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMGridEdit31Display("_%2_") : "_$zh $$$JournalOn
#;define LogR2(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMGridEdit31Display("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
OnBeforeDelete(YDATEI="",YFORM="",YKEY="",YFELD="")
	;-------------------------------------------------------------------------------
	; Automatically remove any attached grid data
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-May-2011	shobby	SR17245: Wasn't deleting multiple grids correctly.
	; 24-Aug-2010	shobby	SR17508: Corrections when killing child forms.
	; 09-Aug-2010	shobby	SR17415: Only delete grid if it is related to deleted record.
	; 30-Apr-2010	shobby	SR17267: Is this still failing?  (checked YKEY & YFORM)
	; 28-Apr-2010	shobby	SR17267: YVOR may not exist (eg during an upgrade)
	; 							(code change in WWWKILL should ensure it is here GRF)
	; 22-Apr-2010	shobby	SR17267: Don't do this DELETE for grid lines. (eg a grid
	; 							line can't have grid lines)
	; 22-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new strStatus,objCOMGridEditParameter,idClass,objWWW120,idForm ;SR17508 

	$$$LogR("OnBeforeDelete",YFORM_"<"_YKEY)

	set strStatus = $$$OK
	if (YKEY'="")&&(YFORM'="") {
		;SR17245 vvvvv
		set strStatus=$$DeleteGrids(YFORM,YKEY) ;
		;SR17245if $$Relevant(YFORM,.objCOMGridEditParameter) {
		;SR17245	;SR17508 set idClass   = $$$COMGridEditParameterGridName(objCOMGridEditParameter)
		;SR17245	set idForm   = $$$COMGridEditParameterGridName(objCOMGridEditParameter)			;SR17508 
		;SR17245	set objWWW120 = $get(^WWW120(0,idForm,1))										;SR17508 
		;SR17245	set idClass=$$$WWW120ClassUsedInForm(objWWW120)									;SR17508 
		;SR17245	if idClass'="" {																;SR17508 
		;SR17245		set strStatus = $$KillChildren^COMUtils(idClass,YKEY)						;SR17508 
		;SR17245	}																				;SR17508 
		;SR17245}
		if $$$ISOK(strStatus) {
			if YFORM=$get(^CacheTemp(YUSER,"Grid","Container")) {  ;SR17415
				set objWWW120 = $get(^WWW120(0,YFORM,1))
				if objWWW120'="" {
				;	if $$$WWW120FormType(YVOR)'=12 do DELETE^COMGridEdit31R()		;SR17267
					if $$$WWW120FormType(objWWW120)'=12 do DELETE^COMGridEdit31R()
				}
			}
		}
	}
	quit strStatus
	
DeleteGrids(YFORM,YKEY)
	;-------------------------------------------------------------------------------
	; Deletes all grids related to this form in the metadata setup.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-May-2011	shobby		SR17245: Created
	;-------------------------------------------------------------------------------

	new idGrid,objCOMGridEditParameter,idForm,objWWW120
	
	set idGrid="" for { set idGrid=$order(^COMGridEditParameter(0,YFORM,idGrid)) quit:idGrid=""
		quit:$$$ISERR(strStatus)
		set objCOMGridEditParameter = $get(^COMGridEditParameter(0,YFORM,idGrid,1))
		set idForm   = $$$COMGridEditParameterGridName(objCOMGridEditParameter)
		set objWWW120 = $get(^WWW120(0,idForm,1))
		set idClass=$$$WWW120ClassUsedInForm(objWWW120) 
		if idClass'="" { 
			set strStatus = $$KillChildren^COMUtils(idClass,YKEY) 
		} 
	}

	quit strStatus

	
OnAfterDataFields(pYFORM="",pYKEY="",pYFELD="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 26-Sep-2012	shobby	SR18128: Forced YKEY back in to a valid form.
	;							MEDPrescriptionHosp is an example of a form
	;							that may manipulate YKEY during loading.
	; 22-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new objCOMGridEditParameter,YAUSWAHL,YFORM,YKEY,YFELD

	$$$LogR("OnAfterDataFields",pYFORM_"<"_pYKEY)
	
	if $$Relevant(pYFORM,.YAUSWAHL) {
		if ($$$COMGridEditParameterTabs(YAUSWAHL)="") || ((";"_$$$COMGridEditParameterTabs(YAUSWAHL)_";")[(";"_YSEITE_";")) {
			set YKEY  = $piece(pYKEY,",",1,YMAXKEY) ;SR18124
			set YFELD = pYFELD
			set YFORM = $$$COMGridEditParameterGridName(YAUSWAHL)
			set $$$COMGridEditParameterContainer(YAUSWAHL) = pYFORM
			set $$$COMGridEditParameterEnabled(YAUSWAHL)   = $$EnabledTest($$$COMGridEditParameterEnabledTest(YAUSWAHL),pYFELD)
			if $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly set $$$COMGridEditParameterEnabled(YAUSWAHL) = $$$NO
			do ^COMGridEdit31
		}
	}
	quit
	
	
EnabledTest(pstrExpression,YFELD)
	$$$LogR("EnabledTest",pstrExpression_"<")
	
	quit:(pstrExpression="") $$$YES
	quit @pstrExpression
	
	
OnBeforeSave(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 13-May-2011	shobby	SR17245: Some troubles with multiple grids.
	; 22-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	$$$LogR("OnBeforeSave",YKEY)

	set strStatus = $$$OK

	if $data(^COMGridEditParameter(0,YFORM)) {
		set strStatus = $$SAVE^COMGridEdit31Save($get(YKEY))
	}
	quit strStatus


Relevant(pYFORM,&pobjCOMGridEditParameter="")
	;-------------------------------------------------------------------------------
	; If this grid is defined by metadata then we do the processing automatically not 
	; through custom code.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 22-Mar-2010	shobby	SR17245: Created
	;-------------------------------------------------------------------------------
	new blnRelevant,idGrid,blnFound
	
	$$$LogR("Relevant",pYFORM)
	
	set blnRelevant = $$$NO
	set blnFound=$$$NO
	
	if (pYFORM'="") {
		set idGrid="" for { set idGrid=$order(^COMGridEditParameter(0,pYFORM,idGrid)) quit:idGrid=""
			set pobjCOMGridEditParameter = $get(^COMGridEditParameter(0,pYFORM,idGrid,1))
			if (pYFORM="WWW120")||($$$COMGridEditParameterTabs(pobjCOMGridEditParameter)=$get(YSEITE))||($$$COMGridEditParameterTabs(pobjCOMGridEditParameter)="") {
				set blnRelevant=$$$YES
				quit
			}
		}
	}
	quit blnRelevant
	
]]></Routine>
</Export>