<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWSEARBIT2" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWSEARBIT2(YINHALT,YLFDAT)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SAVE SEARCH DEFAULTS
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 20-Oct-2008	GRF		Quits; doco; +
	; 07-Jul-2005	shobby	SR12892: WWW1263 is no longer shared.
	; 01-Jun-2005	RPW		SR12056: Attempt at Performance Increase
	; 10.07.2003	FIS  	Created
	;-------------------------------------------------------------------------------
	NEW YHREF,YI,YFELD,YRETVAL,YREPEAT,YLFDATX,YONCE
	
	SET YRETVAL=""
	IF $GET(YLFDAT)="" QUIT YRETVAL
	IF $GET(YFORM)=""  QUIT YRETVAL
	
	IF YLFDAT="YKILL" DO  QUIT YRETVAL  ;LÖSCHEN SUCHVORGABEN ;Delete 
	. IF $GET(YINHALT)'="KILL" QUIT
	. SET YLFDAT=""
	. FOR  SET YLFDAT=$ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT)) QUIT:YLFDAT=""  DO
	. . IF YLFDAT="YANZAB"  SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,1)=1   QUIT
	. . IF YLFDAT="YMAX"    SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,1)=""  QUIT
	. . IF YLFDAT="YRESULT" SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,1)=""  QUIT
	. . IF YLFDAT="YANZAHL"         QUIT
	. . IF YLFDAT="YSEARCHFULLTEXT" QUIT
	. . IF $EXTRACT(YLFDAT)="X"     QUIT
	. . SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,1)=""
	. . SET YRETVAL=YRETVAL_"#"_YLFDAT_"~"
	
	IF YLFDAT="YANZAHL" DO
	. IF $GET(YINHALT)<1 SET YINHALT=1
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAHL",1)=YINHALT
	. SET $PIECE(^WWW1263(YM,YFORM,YBED,1),Y,1)=YINHALT
	
	IF YLFDAT="YANZAB" DO
	. NEW ANZAB,ANZAHL,YI,ANZABX,ANZMAX
	. SET ANZAB=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAB",1)),Y,1)
	. IF +ANZAB=0 SET ANZAB=1
	. SET ANZAHL=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAHL",1)),Y,1)
	. IF ANZAHL'>0 SET ANZAHL=$PIECE($GET(^WWW1263(YM,YFORM,YBED,1)),Y,1)
	. IF ANZAHL'>0 SET ANZAHL=10
	. SET ANZMAX=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YRESULT",1)),Y,1)
	. IF $GET(YINHALT)'="" DO
	. . IF $EXTRACT(YINHALT,1,3)="SET" SET ANZAB=((($EXTRACT(YINHALT,4,99)-1)*ANZAHL)+1)
	. . IF YINHALT="PLUS"  FOR YI(1)=1:1:5 SET ANZAB=(ANZAB+ANZAHL)
	. . IF YINHALT="MINUS" FOR YI(1)=1:1:5 SET ANZAB=(ANZAB-ANZAHL) IF ANZAB<1 SET ANZAB=1
	. . IF ANZAB>ANZMAX QUIT
	. . SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAB",1),Y,1)=ANZAB
	. . SET ANZABX=ANZAB
	. . IF $EXTRACT(YINHALT,1,3)="SET" SET ANZAB=ANZAB-(5*ANZAHL) IF ANZAB<1 SET ANZAB=1
	. . IF ANZAHL>0 IF (ANZAB\ANZAHL)<990 FOR YI(2)=1:1:10 DO   ;TYBD; ANZAHL>0; WEGEN DIVIDE ERROR
	. . . SET YRETVAL=YRETVAL_"#YLINK"_YI(2)_"~"_((ANZAB\ANZAHL)+YI(2))
	. . ;
	. . IF ANZAHL>0 SET YRETVAL=YRETVAL_"#FUNCTION~setHighLight("_""""_((ANZABX\ANZAHL)+1)_""""_");"
	
	
	;SPEICHERN AUSWAHL ;Save Selection 
	IF YLFDAT'="YANZAHL" IF YLFDAT'="YANZAB" DO
	. IF $EXTRACT(YLFDAT)="X" IF YINHALT=2 DO  ;PRÜFEN AUF ANZAHL 'NICHT'-BEDINGUNGEN (2="~")
	. . SET YONCE=0
	. . SET YLFDAT(1)="X"
	. . FOR  SET YLFDAT(1)=$ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT(1))) QUIT:YLFDAT(1)=""  QUIT:$EXTRACT(YLFDAT(1))'="X"  DO  QUIT:YONCE'=0
	. . . QUIT:YLFDAT(1)=YLFDAT  ;EIGENE AUSWAHL ;Selection 
	. . . IF $PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT(1),1)),Y,1)=2 SET YONCE=YONCE+1  ;NICHT BEDINGUNG ;Not terms 
	. . ;
	. . IF YONCE'=0 SET YINHALT=1 SET YRETVAL=YRETVAL_"#"_YLFDAT_"~"_1_"#!"_$$^WWWTEXT(33707,,1)  ;NICHT kann nur 1x gewählt werden -> NACH 'UND' WANDELN ;Not solely will within 
	. ;
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,1)=$GET(YINHALT)  ;DATEN ZUR PRUEFUNG
	
	;  ;AUSGESCHALTET: AUFRUF DURCH WWWSEARBIT1 ;trans- 
	;IF YLFDAT'="YANZAB" IF $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,2))=$GET(YINHALT) QUIT YRETVAL  ;NO CHANGE
	;DO BITSEARCH()
	;SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,2)=$GET(YINHALT)  ;DATEN ZUR PRUEFUNG
	;SET YRETVAL=YRETVAL_"#FUNCTION~searchResult("_""""_YLFDAT_""""_");"  ;REFESH SUCHANZEIGE
	QUIT YRETVAL
	
	
BITSEARCH(YDATEI,YSUM,YEXPAND,YSORT,YSUMONLY)	;REBUILD SEACHRESULTS
	NEW YLFDAT,YI,YLFDATX,YLFN,YART,YINHALT,YRESULT,YANZ,YAB,YLOGIC,YDATEIX,YMAX,YLNR,YLOGIC1,YFIRST,YDELETE
	
	KILL ^WWWSOR(YUSER,"SEL")
	KILL ^WWWSOR(YUSER,"KEY")
	KILL ^WWWSOR(YUSER,"BIT")
	KILL ^WWWSOR(YUSER,"BITSEAR")
	KILL ^WWWSOR(YUSER,"KEYSEAR")
	
	IF $GET(YSUM)=1 KILL ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YBITSUM")  ;SUMMENFELDER;FIS;21.06.2004;25940
	SET YRESULT=""
	SET YDATEI=$GET(YDATEI)
	SET YDATEIX=""
	
	IF $GET(ANZAHL)="" SET ANZAHL = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAHL",1))
	IF $GET(ANZAB)=""  SET ANZAB  = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAB",1))
	
	;DATENSÄTZE NACH AUSWAHL SUCHEN ;within Selection seek
	SET YLFDAT=""
	FOR  SET YLFDAT=$ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT)) QUIT:YLFDAT=""  DO
	. IF $EXTRACT(YLFDAT)="X"     QUIT
	. IF YLFDAT="YANZAB"          QUIT
	. IF YLFDAT="YANZAHL"         QUIT
	. IF YLFDAT="YMAX"            QUIT
	. IF YLFDAT="YRESULT"         QUIT
	. IF YLFDAT="YSEARCHFULLTEXT" QUIT
	. ;
	. SET YINHALT=$GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",YLFDAT,1))
	. QUIT:$TRANSLATE(YINHALT," ")=""
	. ;
	. SET YLFDATX=YLFDAT
	. SET YLFDATX=$REVERSE($EXTRACT(YLFDATX,2,99))
	. FOR YI=1:1  QUIT:$EXTRACT(YLFDATX,YI,99)=""  QUIT:$EXTRACT(YLFDATX,YI)?1N=0  SET YLFN=$EXTRACT(YLFDATX,1,YI)
	. SET YLFN=$REVERSE($GET(YLFN))
	. QUIT:+YLFN=0
	. ;
	. SET YART=$EXTRACT(YLFDATX,YI)
	. SET YDATEI=$REVERSE($EXTRACT(YLFDATX,YI+1,99))
	. QUIT:YDATEI=""
	. ;
	. SET YDATEIX=YDATEI
	. IF YART="P" SET ^WWWSOR(YUSER,"KEYSEAR",YDATEI,YLFN)=YINHALT  ;SUCHE NACH KEY ;search within KEY 
	. IF YART="D" DO  ;SUCH NACH DATENFELDERN ;within 
	. . SET YLOGIC=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","X"_$EXTRACT(YLFDAT,2,99),1)),Y,1)
	. . IF YLOGIC="" SET YLOGIC="&"  ;UND  ;DFLT. = UND ;And And 
	. . IF YLOGIC=0  SET YLOGIC="!"  ;ODER ;Or 
	. . IF YLOGIC=1  SET YLOGIC="&"  ;UND ;And 
	. . IF YLOGIC=2  SET YLOGIC="~"  ;NICHT ;Not  ;Not privation 
	. . SET ^WWWSOR(YUSER,"BITSEAR",YLOGIC,YDATEI,YLFN)=YINHALT
	
	;=======================================================
	
	;ALLE DATENSÄTZE (KEINE AUSWAHL)
	;-------------------------------
	;IF $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZALL",1))=1 DO  QUIT  ;ALLE LADEN
	IF '$DATA(^WWWSOR(YUSER,"KEYSEAR")) IF '$DATA(^WWWSOR(YUSER,"BITSEAR")) DO  QUIT
	. ;SET YRESULT=$$^WWWBITLOGIC(YDATEI)  ;FIS;26833;03.02.05
	. ;DO ^WWWBITFIND(YUSER,YDATEI,ANZAHL,ANZAB)  ;FIS;26833;03.02.05
	. SET YRESULT=0  ;FIS;26833;03.02.05
	. SET ^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YRESULT",1)=YRESULT
	. IF $GET(YSUM)=1 DO  ;AUFBEREITEN SUMMENFELDER;FIS;21.06.2004;25940
	. . NEW YII
	. . SET YII=""
	. . FOR  SET YII=$ORDER(^WWW001B(0,YDATEI,YII)) QUIT:YII=""  DO
	. . . IF $PIECE($GET(^WWW001B(0,YDATEI,YII,1)),Y,3)=1 DO
	. . . . SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YBITSUM",1),Y,YII)=""
	. ;
	. KILL ^WWWSOR(YUSER,"BIT")  ;KILL
	
	;=======================================================
	
	;NACH VORGABEN SUCHEN ;within seek 
	;--------------------
	SET TEST=0
	NEW YDATEI
	
	SET YFIRST=0
	
	;SUCHEN KEY ;seek KEY 
	SET YDATEI=""
	FOR  SET YDATEI=$ORDER(^WWWSOR(YUSER,"KEYSEAR",YDATEI)) QUIT:YDATEI=""  DO
	. NEW YSKEY,YDATEI1,YSUCH,YEXEC,ID,OK,CHECK
	. SET YMAXKEY=$ORDER(^WWW002(0,YDATEI,""),-1)  ;ANZAHL KEYS ;Number 
	. SET YSUCH=""
	. SET YSKEY=""
	. FOR YLFN=1:1:YMAXKEY SET $PIECE(YSUCH,",",YLFN)=$GET(^WWWSOR(YUSER,"KEYSEAR",YDATEI,YLFN))
	. FOR YLFN=1:1:YMAXKEY QUIT:$PIECE(YSUCH,",",YLFN)=""  SET:YLFN>1 YSKEY=YSKEY_"," SET YSKEY=YSKEY_$PIECE(YSUCH,",",YLFN)
	. QUIT:YSKEY=""
	. SET ID=""
	. SET YDATEI1="^"_YDATEI_"bi("_""""_$$^WWWYM(YDATEI,0)_""""_","_2_","_""""_$TRANSLATE(YSKEY,"""")_""""_")"
	. DO
	. . IF $DATA(@YDATEI1) DO  QUIT  ;VOLLSTÄNDIGER KEY Z.B. "1,2"
	. . . SET EXEC="SET ID=$O(^"_YDATEI_"bi("_""""_$$^WWWYM(YDATEI,0)_""""_","_2_","_""""_$TRANSLATE(YSKEY,"""")_""""_","_""""_""""_"))"
	. . . XECUTE EXEC
	. . . SET OK=$$^WWWBIT("WWWSOR",ID,,,1)
	. . . SET YFIRST=YFIRST+1
	. . . WRITE:TEST=1 " / KEY "_YSKEY_","_ID
	. . ;
	. . FOR  DO  QUIT:YDATEI1=""  ;UNVOLLSTÄNDIGER KEY Z.B. "1,"
	. . . SET YDATEI1=$QUERY(@YDATEI1)
	. . . IF $PIECE(YDATEI1,",",2)'=2 SET YDATEI1="" QUIT
	. . . ;SET CHECK=$TRANSLATE($PIECE(YDATEI1,",",3,$QLENGTH(YDATEI1)),"""")
	. . . SET CHECK=$TRANSLATE($PIECE(YDATEI1,",",3,99),"""")
	. . . IF $EXTRACT(CHECK,1,$LENGTH($TRANSLATE(YSKEY,"""")))'=$TRANSLATE(YSKEY,"""") SET YDATEI1="" QUIT
	. . . SET ID=$PIECE($TRANSLATE($PIECE(YDATEI1,",",4),""""),")",1)
	. . . SET OK=$$^WWWBIT("WWWSOR",ID,,,1)
	. . . SET YFIRST=YFIRST+1
	. . . WRITE:TEST=1 " / KEY "_YSKEY_","_ID
	
	;SUCHEN DATENFELDER ;seek 
	FOR YLOGIC="!","&","~" DO  ;ERST ALLE 'ODER'-MÖGLICHKEITEN, DANN 'UND'-PRÜFUNG UND 'NICHT'-FILTER
	. SET YDATEI=""
	. FOR  SET YDATEI=$ORDER(^WWWSOR(YUSER,"BITSEAR",YLOGIC,YDATEI)) QUIT:YDATEI=""  DO
	. . SET YLFN=""
	. . FOR  SET YLFN=$ORDER(^WWWSOR(YUSER,"BITSEAR",YLOGIC,YDATEI,YLFN)) QUIT:YLFN=""  DO
	. . . SET YINHALT=$GET(^WWWSOR(YUSER,"BITSEAR",YLOGIC,YDATEI,YLFN))
	. . . SET YLFN1=YLFN
	. . . IF $EXTRACT(YLFN1,1,4)=9999 SET YLFN1=9999     ;VOLLTEXTSUCHE
	. . . IF YLFN1=9999 IF $GET(YEXPAND)=1 DO            ;FIS;29.11.04
	. . . . SET YINHALT=$$PREPARE^WWWBITSEARCH(YINHALT)  ;SUCHBEGRIFF GGF. UMSTELLEN;FIS;29.11.04
	. . . . SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YSEARCHFULLTEXT",1),Y,1)=$zconvert(YINHALT,"L")
	. . . . SET YINHALT=$$EXPAND^WWWBITSEARCH(YINHALT)   ;ARTVERWANDTE SUCHBEGRIFFE;FIS;29.11.04
	. . . ;
	. . . SET YLOGIC1=YLOGIC
	. . . SET YFIRST=YFIRST+1
	. . . IF YFIRST=1 IF YLOGIC1'="~" SET YLOGIC1="!"
	. . . IF YFIRST=1 IF YLOGIC1="~"   ;SET YRESULT=$$^WWWBITLOGIC(YDATEI) WRITE:TEST=1 "(~:"_YRESULT_")"  ;ALLE AUSSER = ALLE LADEN FÜR VERGLEICH
	. . . ;
	. . . IF '$FIND(YINHALT," ") IF '$FIND(YINHALT,";") DO  QUIT  ;1 WORT SUCHE
	. . . . WRITE:TEST=1 " 1/ "_YLFN1_" "_YLOGIC1_" "_YINHALT
	. . . . SET YRESULT=$$^WWWBITLOGIC(YDATEI,YLFN1,$$^WWWUMLAU(YINHALT,1,1),YLOGIC1,,)  ;FIS;03.03.05;27426:MIT KOMMA
	. . . . WRITE:TEST=1 " ("_YRESULT_")"
	. . . ;
	. . . SET YDELETE=$$^WWWBITLOGIC(,,,,1,1)   ;KILL TEMP
	. . . ;
	. . . IF $FIND(YINHALT,";") DO  QUIT  ;MEHRFACHAUSWAHL SELECT
	. . . . FOR YI=1:1 QUIT:$PIECE(YINHALT,";",YI)=""  DO
	. . . . . SET YRESULT=$$^WWWBITLOGIC(YDATEI,YLFN1,$$^WWWUMLAU($PIECE(YINHALT,";",YI),1,1),"!",,1)  ;FIS;03.03.05;27426:MIT KOMMA
	. . . . . IF TEST=1 WRITE " 2/ ",YLFN1," ",YLOGIC1," ",$PIECE(YINHALT,";",YI)
	. . . . ;
	. . . . SET YRESULT=$$^WWWBITLOGIC(YDATEI,,,YLOGIC1,,2)
	. . . . SET YDELETE=$$^WWWBITLOGIC(,,,,1,1)   ;KILL TEMP
	. . . . WRITE:TEST=1 " ("_YRESULT_")"
	. . . ;
	. . . IF $FIND(YINHALT," ") DO  ;MEHRFACHAUSWAHL TEXT 
	. . . . IF TEST=1 WRITE " 3/ ",YLFN1," ",YLOGIC1,": "
	. . . . FOR YI=1:1 QUIT:$PIECE(YINHALT," ",YI)=""  DO
	. . . . . NEW YLOGIC2
	. . . . . SET YLOGIC2="!"  ;INNERHALB DES FELDES ;inside 
	. . . . . IF $EXTRACT($PIECE(YINHALT," ",YI))="+" SET YLOGIC2="&" SET $PIECE(YINHALT," ",YI)=$EXTRACT($PIECE(YINHALT," ",YI),2,999)
	. . . . . IF YI=1 SET YLOGIC2="!"  ;1. WORT ;wordy 
	. . . . . IF TEST=1 WRITE " 3n/ ",YLFN1," ",YLOGIC2," ",$PIECE(YINHALT," ",YI)
	. . . . . SET YRESULT=$$^WWWBITLOGIC(YDATEI,YLFN1,$$^WWWUMLAU($PIECE(YINHALT," ",YI),1,1),YLOGIC2,,1)  ;FIS;03.03.05;27426:MIT KOMMA
	. . . . ;
	. . . . SET YRESULT=$$^WWWBITLOGIC(YDATEI,,,YLOGIC1,,2)
	. . . . SET YDELETE=$$^WWWBITLOGIC(,,,,1,1)   ;KILL TEMP
	. . . . WRITE:TEST=1 " ("_YRESULT_")"
	
	QUIT:YDATEIX=""
	
	DO
	. ;MERGE ^WWWSOR(YUSER,"TEMP")=^WWWSOR(YUSER,"BIT")
	. IF $GET(YSUM)=1 DO  ;AUFBEREITEN SUMMENFELDER;FIS;21.06.2004;25940
	. . NEW YII,YRESULT
	. . SET YII=""
	. . FOR  SET YII=$ORDER(^WWW001B(0,YDATEIX,YII)) QUIT:YII=""  DO
	. . . IF $PIECE($GET(^WWW001B(0,YDATEIX,YII,1)),Y,3)=1 DO
	. . . . MERGE ^WWWSOR(YUSER,"BIT")=^WWWSOR(YUSER,"TEMP")
	. . . . SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YBITSUM",1),Y,YII)=+$$^WWWBITLOGIC(YDATEIX,YII,,"&")
	. . . . ;SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YBITSUM",1),Y,YII)=$$^WWWBITCOUNT(YDATEI,YII)
	;. ;
	;. ;MERGE ^WWWSOR(YUSER,"BIT")=^WWWSOR(YUSER,"TEMP")
	;. ;KILL ^WWWSOR(YUSER,"TEMP")
	
	SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YRESULT",1),Y,1)=YRESULT
	IF $GET(YSUMONLY)'=1 DO
	. SET YANZ=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAHL",1)),Y,1)
	. IF +YANZ'>0 SET YANZ=$PIECE($GET(^WWW1263(YM,YFORM,YBED,1)),Y,1)
	. IF +YANZ'>0 SET YANZ=10
	. SET YAB=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAB",1)),Y,1)
	. IF YAB'>0 SET YAB=1
	. IF YAB>YRESULT SET YAB=1 SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V","YANZAB",1),Y,1)=1,ANZAB=1
	. DO ^WWWBITFIND(YUSER,YDATEIX,YANZ,YAB,,$GET(YSORT))
	;. ;I YBED="FIS" ZW ^WWWSOR(YUSER,"SEL")
	
	
	KILL ^WWWSOR(YUSER,"BIT")
	KILL ^WWWSOR(YUSER,"BITSEAR")
	KILL ^WWWSOR(YUSER,"KEYSEAR")
	QUIT
	
]]></Routine>
</Export>