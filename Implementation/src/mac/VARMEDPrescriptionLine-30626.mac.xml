<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARMEDPrescriptionLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARMEDPrescriptionLine
#include COMSYS
#include INConst
#include VARConst
#include MEDConst
#include INSYS
#include %occInclude
#include COMGridEdit31
#include VARLog

#define MEDPrescriptionLineSuspendFld 101
#define MEDPrescriptionLineStatusSuspensa 5
	
SetRouteAdminRules(pobjLine)
	;-------------------------------------------------------------------------------
	; Called by field = D13/F7 VAR Rules
	; Inputs :
	;	YFORM	   
	;
	; History:
	; 21-Jul-2011 	Karine 	Set - Route Of Administration  D13/F7
	;  @$$SetRouteAdminRules^VARMEDPrescriptionLine(YFELD)
	; Commitado
	;-------------------------------------------------------------------------------
	new objItem, getRouteOfAdmin, idItem, vRoute, setRoute
	
	set idItem = ""
	set getRouteOfAdmin = ""
	set vRoute = ""
	
	if pobjLine '= "" {
		set idItem = $$$MEDPrescriptionLineItem(pobjLine)
		if idItem '= "" {
			set objItem = $get(^INART(YM,idItem,1))
			set getRouteOfAdmin = $$$INARTFREE11(objItem)
			
			set vRoute =  $$$MEDPrescriptionLineRouteOfAdministration(pobjLine)
			
			if vRoute '= "" {
				;$$$Alert("A via de Administração não corresponde aquela registrada para esse produto.")
				; O alerta não pode ficar aqui pq é executado 3 vezes.
				set getRouteOfAdmin = vRoute  ;verifica se o campo foi aterado diretament, pois o VARRules é executado também como se fosse um OnBlur	
			}	

		}		
	}
		
	quit getRouteOfAdmin
	
OnBlurCheckRouteAdmin(pYINHALT,pobjLine)
	;-------------------------------------------------------------------------------
	; Called By : Form VARMEDPrescriptionLine F13/D7
	; 
	; History :
	; 01-Jul-2011	Karine	Created
	; do OnBlurCheckRouteAdmin^VARMEDPrescriptionLine(YINHALT,YFELD)
	; Commited
	;-------------------------------------------------------------------------------
	new newRoute, isRegistered

	set isRegistered = ""
	
	if pYINHALT '= "" {
		set idItem = $$$MEDPrescriptionLineItem(pobjLine)
		set objItem = $get(^INART(YM,idItem,1))
		set getRouteOfAdmin = $$$INARTFREE11(objItem)
			
		set newRoute = pYINHALT
			
		if (newRoute '= getRouteOfAdmin)  {
			$$$Alert("A Via de Administração está incorreta segundo o Cadastro de Produtos.")
			; Logo após essa execução o @nm chama também o VARRules acima = SetRouteAdminRules
		} 
	}
	quit	


CheckTotalDoseRules(pobjLine)
	;-------------------------------------------------------------------------------
	; Called by field = D7/F11 - Quantidade Total in VAR Rules
	; Inputs :
	;	YFELD	   
	;	
	; History:
	; 21-Jul-2011 	Karine 	Check if the total quantity is an integer number
	; Call: ($$CheckTotalDoseRules^VARMEDPrescriptionLine(YFELD) = 1)
	; Commitado
	;-------------------------------------------------------------------------------
	new fltFactor,fltDoseQty,fltDays,idFrequency,modCheck, stFloat
	
	set stFloat = $$$NO
	
	set fltDoseQty = ""
	set fltDays = ""
	set idFrequency = "" 
		
	set fltFactor = ""
	if pobjLine'="" {
		set fltDoseQty  = $$$MEDPrescriptionLineDoseQty(pobjLine)
		set fltDays     = $$$MEDPrescriptionLineDuration(pobjLine)
		set idFrequency = $$$MEDPrescriptionLineFrequency(pobjLine)
		if (idFrequency '="") && (fltDays '= "") && (fltDoseQty '= "") {
			set objFrequency = $get(^MEDFrequency(0,idFrequency,1))
			set fltFactor    = $$$MEDFrequencyConversion(objFrequency)
		}
		if fltFactor'="" {
			set fltDoseTotal = fltFactor * fltDoseQty * fltDays
			set modCheck = $piece(fltDoseTotal,".",2)
			if (modCheck '= "") && (modCheck '= 0) {
				set stFloat = $$$YES
			}
		} 
	}
	;if stFloat { ;erro: isso é executado 3 vezes !!!
	;	$$$Alert("Valor inválido para Quantidade Total."_$c(10)_"Verificar frequência e/ou duração selecionada")
	;}
	
 quit stFloat
	
CheckIfInPatient(pYKEY)
	;-------------------------------------------------------------------------------
	; Called by field = D6/F5 - Quantidade Total in VAR Rules
	; Inputs :
	;	YKEY	   
	;	
	; History:
	; 25-Jul-2011 	Karine 	Check if ISSUETYPE =  I (InPatient) - in field MEDPrescriptionIssueType D5/F7
	;						If it is, set MEDPrescriptionLineDurationDays = 1 and ReadOnly
	; Call:   ($$CheckIfInPatient^VARMEDPrescriptionLine(YKEY) = 1)
	; Status: Commited 25-Jul
	;-------------------------------------------------------------------------------

	new idPrescription, objPrescription, isInPatient, stPatient
	
	set stPatient = $$$NO
	
	if '$$$NoKey(pYKEY) {
		set idPrescription = $piece(pYKEY,",",1)
		
		set objPrescription = $get(^MEDPrescription(YM,idPrescription,1))
		
		set isInPatient = $$$MEDPrescriptionIssueType(objPrescription)
		
		if isInPatient = "I" {
			set stPatient = $$$YES	
		}	
	}
	
	quit stPatient
	
	
CheckFrequencyRules(pYINHALT,pobjLine)	
	;-------------------------------------------------------------------------------
	; Called By : Form VARMEDPrescriptionLine F4/D5 VAR Rules
	; 
	; History :
	; 25-Jul-2011	Karine	Created : Para paciente internado a frequencia não poder ser: 
	;                                 48hx48h, 72hx72h, 1 vez por semna nem 1 vezes por semana
	;
	; Call:         ($$CheckFrequencyRules^VARMEDPrescriptionLine(YINHALT,YFELD)=1)
	; IN TEST
	;-------------------------------------------------------------------------------
	new newFrequency, getDuracao
	
	set getDuracao = ""
	set stStatus = $$$NO

	if pYINHALT '= "" {
		set getDuracao = $$$MEDPrescriptionLineDuration(pobjLine)
		
		if getDuracao = 1 {  ;Patiente internado = prescrição apenas para 1 dia
				
			if ((pYINHALT=9) || (pYINHALT = 10) ||  (pYINHALT=11) || (pYINHALT=12)) {
				set stStatus = $$$YES   ;LIMPAR O CAMPO	
			} 
		}
	}
	quit stStatus	
	
	
GetValorPadraoASuspender()
	;-------------------------------------------------------------------------------
    ; Descrição:
    ; Torna o campo habilitado quando o grid estiver desabilitado
    ;
    ; Chamado por:
    ; Campo Valor Padrão do campo (14)-A Cancelar no form MEDPrescriptionLine
    ;------------------------------------------------------------------------------- 
 	do CanSuspender(YKEY)
 	if ('YQ) {
 		set GblCasoEspecial = 1
 		set ^CacheTemp(YUSER, "Grid", "Enabled") = 1
		set ^CacheTempEvent(YUCI, YFORM, "Format") = 1
 	}

 	quit ""


OnBeforeClick(pstrData)
	// Chamado de OnClick^COMGridEdit31S

    if ($get(intCol) = $$$MEDPrescriptionLineSuspendFld) {
		if (intRow > +$get($$$WWWDATEN("ROWCOUNT"))) {
			set $$$WWWDATEN("ROWCOUNT") = intRow
		}

	    set $$$WWWDATEN("Y"_intRow_"_"_intCol, 1) = '$get($$$WWWDATEN("Y"_intRow_"_"_intCol, 1))

		if ($get($$$WWWDATEN("REFERENCEROW", 1, 1)) = intRow) {
			set $$$WWWDATEN("REFERENCEVALUE", "Y"_intRow_"_"_intCol, 1) =
				$$$WWWDATEN("Y"_intRow_"_"_intCol, 1)
		}

	    quit 0
    }

	quit 1
	

OnBeforeFormat(pYFIELDNAME,pYTEXT,pobjLine)
	;-------------------------------------------------------------------------------
	;	
	; Called By : OnBeforeFormat^MEDPrescriptionLine
	;	Form - GridEdit:MEDPrescriptionLine
	;	     - set ^CacheTempEvent(YUCI,"MEDPrescriptionLine","Format") = 1
	;
	; Inputs : 
	;	pYFIELDNAME	-	Browser Element ID "tdY#row#_#col#"
	;	pYTEXT		-	Value currently in element
	;	pYFELD		-	@nM INIssueLine Data record
	;
	; History :
	; 26-Jul-2011				Karine:	Created
	; 07-Feb-2012   HEVA-191	Karine: The dispense quantity can be float if it is an 
	;                                   internal patient 
	;-------------------------------------------------------------------------------
	new objHeader,idPrecriptionNumber,idLineNumber,idItem,fltDoseQty,fltDays
	new idFrequency, fltTotalQuantity, modCheck
	
	if (+$get(GblCasoEspecial)) {
  		if ($length($get(YQ)) = 0) quit

  		if ('YQ) {
	  		set ^CacheTemp(YUSER, "Grid", "Enabled") = 0
 			set $$$WWWDATEN("AUTHORISATION", 1) = 5
 		}
 		
		set ^CacheTempEvent(YUCI, YFORM, "Format") = 0
  		set GblCasoEspecial = 0
  		quit
  	}
	
	set idPrecriptionNumber = $$$KEY1(YKEY)
	set idLineNumber  = $$$KEY2(YKEY)
	
	quit:idLineNumber=""
	; MEDPrecription Header
	;---------------------------------------
	set objHeader  = $$$GRIDYFELDContainer
	set isInPatient = $$$MEDPrescriptionIssueType(objHeader)
	quit:$$$MEDPrescriptionStatus(objHeader)>3
	
	; MEDPrecriptionLine - Grid
	;---------------------------------------
	set idItem           = $$$MEDPrescriptionLineItem(pobjLine)
	set fltDoseQty       = $$$MEDPrescriptionLineDoseQty(pobjLine)
	set fltDays          = $$$MEDPrescriptionLineDuration(pobjLine)
	set idFrequency      = $$$MEDPrescriptionLineFrequency(pobjLine)
	set fltTotalQuantity = $$$MEDPrescriptionLineDoseTotalQuantity(pobjLine)
	
	set modCheck =  $piece(fltTotalQuantity,".",2)
	
	; Grid Position
	;---------------------------------------
	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)
	
	// Frequency
	;---------------------------------------
	if (idClassField = $$$FldMEDPrescriptionLineFrequency) {
		
		if isInPatient '= "I" {    ;Só faz a verificação se paciente não é internado
		                           ; caso seja, o VAR Rules verifica as regras
			if (modCheck '= "") && (modCheck '= 0) {
				if (idFrequency '="") {
					if idFrequency = 9 {
						set YBACKGROUNDCOLOR = "yellow"
						set YTOOLTIP = $$$Text($listbuild("Para frequência selecionada a duração em dias deverá ser múltiplo de 2. "))
					    ; "For this frequency selected the duration in days must be multiple of 2."
					} elseif idFrequency = 10 {
						set YBACKGROUNDCOLOR = "yellow"
						set YTOOLTIP = $$$Text($listbuild("Para frequência selecionada a duração em dias deverá ser múltiplo de 3."))
						; "For this frequency selected the duration in days must be multiple of 3."
					} elseif (idFrequency = 11) || (idFrequency = 12) {
			 			set YBACKGROUNDCOLOR = "yellow"
						set YTOOLTIP = $$$Text($listbuild("Para frequência selecionada a duração em dias deverá ser múltiplo de 7."))
					}
				}
			 }
		}	
	//Dose Total Quantity
	} elseif (idClassField = $$$FldMEDPrescriptionLineDoseTotalQuantity) {
	    set isINPatient =  $$CheckIfInPatient(idPrecriptionNumber)  ; check if it is a integer number only for external Patient
	    if 'isINPatient {
			if (modCheck '= "") && (modCheck '= 0) {
					set YWARNING = $$$YES
					set YTOOLTIP = $$$Text($listbuild("Valor inválido para Quantidade Total. Verificar frequência e/ou duração selecionada."))
			}
	    }	
	}
  	
	quit

OnBeforeSave(pYKEY)
	;-------------------------------------------------------------------------------
	; Called By : Form MEDPrescription  
	; 
	; History :
	; 22-Jul-2011	Karine	Created
	; $$OnBeforeSave^VARMEDPrescriptionLine(YKEY)
	;-------------------------------------------------------------------------------
	
	new idLine, idPrescription, qtdTotal, objLine, idItem, arrGrid, strStatus, idMainLine
	
	set strStatus = $$$OK
		
	if '$$$NoKey(pYKEY) {
		set idPrescription = $piece(pYKEY,",",1)		
			
		$$$GRIDGetContents(.arrGrid,"MEDPrescriptionLine")
		set idLine= ""
		for {
			set idLine = $order(arrGrid(idPrescription,idLine))
			quit:idLine=""
 			quit:$$$ISERR(strStatus)
		
			set objLine = $get(arrGrid(idPrescription,idLine))
			set qtdTotal = $$$MEDPrescriptionLineDoseTotalQuantity(objLine)
			set idItem = $$$MEDPrescriptionLineItem(objLine)
			set modCheck = $piece(qtdTotal,".",2)
			
			if (modCheck '= "") && (modCheck '= 0) {
				;set idGridLine = $get(^WWWDATEN(0,+$horolog,YUSER,"INReceiptLine","V","REFERENCEROW",idLine,1))								
				;set strStatus = $$$MakeStatus("Não é possível salvar."_$c(13)_" Por favor verificar a dosagem do item "_idItem_".")
				set strStatus = $$$MakeStatus("Valor inválido para Quantidade Total."_$c(10)_"Verificar frequência e/ou duração selecionada na linha "_idLine_".")
			}		
		}	
	}	
	quit strStatus

CanSuspender(pYKEY)
	if $$$NoKey(pYKEY) {
		set YQ = 1_"Selecione um registro."
		quit
	}
	
	set YQ = $$$YQEnable

 	set obPrescricao = $get(^MEDPrescription(YM,$$$KEY1(pYKEY),1))
	if ($$$MEDPrescriptionStatus(obPrescricao) = 1) {
		set YQ = 1_"Esta prescrição não contém interações medicamentosas."
	}
	elseif ($$$MEDPrescriptionStatus(obPrescricao) = 8) {
		set YQ = 1_"Essa prescrição já foi encerrada."
	}
	elseif ($$$MEDPrescriptionStatus(obPrescricao) = 9) {
		set YQ = 1_"Essa prescrição já foi completamente atendida."
	}

	if ($length($$$KEY2(pYKEY)) > 0) {
	 	set obLinhaPrescricao = $get(^MEDPrescriptionLine(YM,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
		if ($$$MEDPrescriptionLineFREE4(obLinhaPrescricao) = 5) {
			set YQ = 1_"Essa linha de prescrição já foi suspensa."
		}
		if ($$$MEDPrescriptionLineFREE4(obLinhaPrescricao) = 4) {
			set YQ = 1_"Essa linha de prescrição já foi concluída."
		}
	}
	quit
	
PrintJS(pobjForm)	
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^MEDPrescription.MAC
    ;-------------------------------------------------------------------------------
    new idClass, strP1Name
 
	set idClass = $$$WWW120ClassUsedInForm(pobjForm)
	set strP1Name = "Y"_idClass_"P1"
	
	write !, "<script language=""javascript"">"
	&js<
	var idPrescricao = document.#(YHTMFORM)#.#(strP1Name)#.value;
 
	function iePrompt(message) {
		var settings = "dialogWidth: 380px; dialogHeight: 160px; center: yes; " +
			"edge: raised; scroll: no; status: no";		
		return window.showModalDialog("#(YGIF)#VARPromptSize.html", message,settings);
	}	

	function suspender() {
		CallBackNow("SuspenderOnClickJS^VARMEDPrescriptionLine", idPrescricao);
	}
	>
	write !, "</script>"
	quit


SuspenderOnClickJS(idPrescricao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função PrintJS^VARMEDPrescriptionLine.MAC
    ;-------------------------------------------------------------------------------	
	set YQ = $$$YQEnable
	do CanSuspender(idPrescricao)
	if ($extract(YQ, 1, 1) '= $$$YQEnable) {
		&js<
		    alert('#($piece(YQ, "1", 2))#');
		>
		quit
	}
 
	&js<
	    var motivo = iePrompt('Por favor, insira o motivo da suspensão do(s) medicamento(s):');
		if (motivo) {
			CallBackNow("SuspenderOnClick^VARMEDPrescriptionLine", '#(idPrescricao)#', motivo);
		}
	> 
	quit



SuspenderOnClick(idPrescricao, strMotivoSuspensao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função SuspenderOnClickJS^VARMEDPrescriptionLine.MAC
    ;-------------------------------------------------------------------------------	
	new YFORM, YSEITE, intQtdeLinhas, ListaSuspender, blnAlgoASuspender, flagSuspender, i, strStatus,
		strMotivoSuspensaoFormatado
	
	set YFORM = "MEDPrescriptionLine"
 
	set blnAlgoASuspender = $$$NO
	set intQtdeLinhas = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","ROWCOUNT"))
	set ListaSuspender = ""
	for i = 1:1:intQtdeLinhas {
		set flagSuspender = +$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","Y"_i_"_"_$$$MEDPrescriptionLineSuspendFld_"",1))
		set $piece(ListaSuspender, Y, i) = flagSuspender 
		
		if (flagSuspender = $$$YES) {
			set blnAlgoASuspender = $$$YES
		}
	}
 
	if ('blnAlgoASuspender) {
		$$$StartScript()
		write "alert('É necessário marcar o campo suspender de algum dos medicamentos da prescrição!');"
		$$$EndScript()
	}
	elseif ($length(strMotivoSuspensao) = 0) {
		$$$StartScript()
		write "alert('Entre com o motivo da suspensão dos medicamentos da prescrição!');"
		$$$EndScript()
	}
	else {
		if ($length(strMotivoSuspensao, """") = 1) {
			set strMotivoSuspensaoFormatado = strMotivoSuspensao
		}
		else {
			set strMotivoSuspensaoFormatado = ""
			for i = 1:1:$length(strMotivoSuspensao, """") {
				set strMotivoSuspensaoFormatado = strMotivoSuspensaoFormatado_
					$piece(strMotivoSuspensao, """", i)_""""""
			}
		}
 
		set strStatus = $$Transaction^COMTransaction("SuspenderTxn^VARMEDPrescriptionLine("""_idPrescricao_""","""_ListaSuspender_""","""_strMotivoSuspensaoFormatado_""")", $$$YES)
		if $$$ISOK(strStatus) {
			$$$StartScript()
			write "alert('Medicamento(s) da prescrição suspenso(s) com sucesso!');"
			$$$EndScript()
 
			set YFORM = "MEDPrescription"
			set YSEITE = 1
			do GoToForm^COMUtilForm(YFORM, idPrescricao, 1, $get(YPARA), $$$NO)
		}
		else {
			$$$StartScript()
			write "alert('Erro ao suspender medicamentos da prescrição. => "_$$DecodeError^COMUtilError(strStatus)_"');"
			$$$EndScript()
		}
	}
	quit ""


SuspenderTxn(pidPrescricao, pListaSuspender, pstrMotivoSuspensao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função SuspenderOnClick^VARMEDPrescriptionLine.MAC
    ;-------------------------------------------------------------------------------	
	new idPrescricaoLinha, flgSuspender, strStatus, objPrescricao
	set idPrescricaoLinha = ""
	for {
		set idPrescricaoLinha = $order(^MEDPrescriptionLine(YM,pidPrescricao,idPrescricaoLinha))
		quit:idPrescricaoLinha=""
		
		set flgSuspender = +$piece(pListaSuspender, Y, idPrescricaoLinha)

		if (flgSuspender > 0) {
			set strStatus = $$SuspenderIndividual(pidPrescricao, idPrescricaoLinha, pstrMotivoSuspensao)
			if $$$ISERR(strStatus) {
				set $zerror = $$DecodeError^COMUtilError(strStatus)
				zquit 1 GOTO @$ZTRAP
			}
	 
		}
 
	}
	
	if ($$$ISOK(strStatus)) {
		set strStatus = $$GetTodasLinhasSuspensas(pidPrescricao)
		if $$$ISERR(strStatus) {
			set $zerror = $$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
	}
 
	quit $$$OK


SuspenderIndividual(pidPrescricao, pidPrescricaoLinha, pstrMotivoSuspensao)
	;CancelarIndividual(pidPedidoCompra, pidPedidoCompraLinha, pblnTudo, pblnCancelar, pintQuantidadeACancelar)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função SuspenderTxn^VARMEDPrescriptionLine.mac
    ;-------------------------------------------------------------------------------
	new objPrescricaoLinha, strStatus, flgSuspender, Issue, strStatus, count, IssueLine, nomeUsuario, dia, hora, Doses
	
	set strStatus = $$$OK
	set count = 1
	set Issue = ""
	set Doses = ""
	set Issue = $order(^INIssues(YM,7,$$$Index(pidPrescricao),Issue))
	set Doses = $order(^VARDose(YM,pidPrescricao,pidPrescricaoLinha,Doses))
	set objPrescricaoLinha = $get(^MEDPrescriptionLine(YM,pidPrescricao,pidPrescricaoLinha,1))
	
	if (Issue '=""){
		set produto = $$$MEDPrescriptionLineItem(objPrescricaoLinha)
		set quantidade = $$$MEDPrescriptionLineDoseTotalQuantity(objPrescricaoLinha)
		set blnIssueADeletar = $$$NO
		set idIssueLinha = ""
		set IssueLine = ""
		
		for{
			set IssueLine = $order(^INIssueLine(YM,Issue,IssueLine))
			quit:IssueLine=""
			
			set objIssueLine = $get(^INIssueLine(YM,Issue,IssueLine,1))
			if (produto = $$$INIssueLineItem(objIssueLine))&&(quantidade=$$$INIssueLineQtyRequired(objIssueLine)){
				set blnIssueADeletar = $$$YES
				set idIssueLinha = IssueLine
				quit
			}
		}
		
		if (blnIssueADeletar)&&(idIssueLinha'=""){
			set proximo = $order(^INIssueLine(YM,Issue,idIssueLinha))
			set strStatus = $$$Kill("INIssueLine",Issue_$$$COMMA_idIssueLinha)
		
			if (proximo'="")&&($$$ISOK(strStatus)){
				for{
					set IssueLine = idIssueLinha_"."_count
					set strStatus = $$$Kill("INIssueLine",Issue_$$$COMMA_IssueLine)
					set proximo = $order(^INIssueLine(YM,Issue,IssueLine))
					quit:proximo=""
					set count = $i(count)
				}
			}
		}	
	}
	
	if (($$$ISOK(strStatus))&&(Doses'="")){
		set strStatus =  $$SuspenderDoses^VARDose(pidPrescricao,pidPrescricaoLinha)
	}
	
	if ($$$ISOK(strStatus)){
		if (YBED'="") {
			set nomeUsuario = $$SQLGetUserName^VARSQL(YBED)
			set dia = $$SQLGetDataFormatada^VARSQL($h)
			set hora = $$SQLGetFormatedTime2^VARSQL($h)
			set $$$MEDPrescriptionLineFREE1(objPrescricaoLinha) = pstrMotivoSuspensao_" - Suspenso por "_nomeUsuario_" em "_dia_" às "_hora_"."
		}else{
			set $$$MEDPrescriptionLineFREE1(objPrescricaoLinha) = pstrMotivoSuspensao
		}
		set $$$MEDPrescriptionLineFREE4(objPrescricaoLinha) = 5
		set strStatus = $$$Save("MEDPrescriptionLine",pidPrescricao_$$$COMMA_pidPrescricaoLinha,objPrescricaoLinha,$$$YES)
	}
	
	quit strStatus
	
GetTodasLinhasSuspensas(pidPrescricao)
	new idPrescricaoLinha, blnTodosSuspensos, linhas, strStatus
	set idPrescricaoLinha = ""
	set blnTodosSuspensos = $$$YES
	set strStatus = $$$OK
	set linhas = 0
	
	for {
		set idPrescricaoLinha = $order(^MEDPrescriptionLine(YM,pidPrescricao,idPrescricaoLinha))
		quit:idPrescricaoLinha=""
		
		set objPrescricaoLinha = $get(^MEDPrescriptionLine(YM,idPrescricao,idPrescricaoLinha,1))
		set statusLinha = $$$MEDPrescriptionLineFREE4(objPrescricaoLinha)
		
		if (statusLinha '= $$$MEDPrescriptionLineStatusSuspensa){
			set blnTodosSuspensos = $$$NO
			quit
		}
		
		set linhas = $i(linhas)
	}
	
	if (blnTodosSuspensos)&&(linhas){
		&sql(UPDATE SQLUser.MEDPrescription
       		SET Status = 8
       		WHERE Company = 0
       		AND PrescriptionNumber = :idPrescricao)
   		IF (SQLCODE = 0) {
			$$$VARLOGINFO("Documento "_pidPrescricao_"processado com sucesso por "_YBED)	
   		} ELSE {
    		$$$VARLOGERROR("Erro ao gravar dados do processamento do Documento "_pidPrescricao_" : "_SQLCODE)
    		set strStatus = '$$$OK
    	}
	}
	
	quit strStatus
	
AtualizaStatus(pYKEY,pYFELD)
	quit:YFORM'="MEDPrescription" $$$OK
	
	new obPrescricao,idPrescricao,idPrescricaoLinha,objPrescricaoLinha,strStatus,SQLCODE,blnTodosSuspensos,status,statusLinha
	set obPrescricao = $get(^MEDPrescription(YM,pYKEY,1))
	set idPrescricao = pYKEY
	set idPrescricaoLinha = ""
	set strStatus = $$$OK

	for {
		set idPrescricaoLinha = $order(^MEDPrescriptionLine(YM,idPrescricao,idPrescricaoLinha))
		quit:idPrescricaoLinha=""
		set objPrescricaoLinha = $get(^MEDPrescriptionLine(YM,idPrescricao,idPrescricaoLinha,1))

		set status		= $$$MEDPrescriptionStatus(obPrescricao)
		set statusLinha = $$$MEDPrescriptionLineFREE4(objPrescricaoLinha)
		
		if ($$$ISOK(strStatus)){
			if ((statusLinha = "")&&(status=1)){
				set objPrescriptionLine = $get(^MEDPrescriptionLine(0, idPrescricao, idPrescricaoLinha, 1))
				set $$$MEDPrescriptionLineFREE4(objPrescriptionLine) = 0
				set strStatus = $$$Save("MEDPrescriptionLine", idPrescricao_$$$COMMA_idPrescricaoLinha, objPrescriptionLine, 1)

				/*&sql(UPDATE SQLUser.MEDPrescriptionLine
       				SET FREE4 = 0
       				WHERE Company = 0
       				AND PrescriptionNumber = :idPrescricao
 	      			AND Line = :idPrescricaoLinha)*/

   				IF ($$$ISOK(strStatus)) {
   				//IF (SQLCODE = 0) {
					$$$VARLOGINFO("Documento "_pYKEY_"processado com sucesso por "_YBED)	
   				} ELSE {
    				$$$VARLOGERROR("Erro ao gravar dados do processamento do Documento "_pYKEY_" : "_$$DecodeError^COMUtilError(strStatus))
    				//$$$VARLOGERROR("Erro ao gravar dados do processamento do Documento "_pYKEY_" : "_SQLCODE)
    				set strStatus = '$$$OK
    			}
			}
		}
		
		if ($$$ISOK(strStatus)){
			if ((statusLinha'="")&&(statusLinha'=5)&&(status=2)){
				set objPrescriptionLine = $get(^MEDPrescriptionLine(0, idPrescricao, idPrescricaoLinha, 1))
				set $$$MEDPrescriptionLineFREE4(objPrescriptionLine) = 1
				set strStatus = $$$Save("MEDPrescriptionLine", idPrescricao_$$$COMMA_idPrescricaoLinha, objPrescriptionLine, 1)

				/*&sql(UPDATE SQLUser.MEDPrescriptionLine
       				SET FREE4 = 1
       				WHERE Company = 0
       				AND PrescriptionNumber = :idPrescricao
 	      			AND Line = :idPrescricaoLinha)*/

				
   				IF ($$$ISOK(strStatus)) {
   				//IF (SQLCODE = 0) {
					$$$VARLOGINFO("Documento "_pYKEY_"processado com sucesso por "_YBED)	
   				} ELSE {
    				$$$VARLOGERROR("Erro ao gravar dados do processamento do Documento "_pYKEY_" : "_$$DecodeError^COMUtilError(strStatus))
    				//$$$VARLOGERROR("Erro ao gravar dados do processamento do Documento "_pYKEY_" : "_SQLCODE)
    				set strStatus = '$$$OK
    			}
			}
		}

		if ($$$ISERR(strStatus)){
			set $zerror = $$DecodeError^COMUtilError(strStatus)
			zquit 1 GOTO @$ZTRAP
		}
		
	}

	quit strStatus
]]></Routine>
</Export>