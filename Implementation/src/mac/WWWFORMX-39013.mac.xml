<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMX" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWFORMX
#include WWWConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		Set Form Values from Company Defaults
	;
	; Inputs : 
	;	YFORM		FORMNAME
	;
	; ByRef :
	;	YVOR1		VORGABE AUS MANDANT ;out of Company 
	;	YVOR		VORGABE AUS FORMULAR ;out of form 
	;				(WWW120 record returned to routine WWW120)
	;	YFIXHEADER
	;	YI			FIXME : Should probably new this; ideally pass byRef
	; 				through argument list <GRF>
	;
	; Returns :
	;	YVOR (By Ref) : Form Values - adjusted
	;
	; History :
	; 13-Dec-2007	shobby	Call to DoNotDisplay to determine if additional buttons
	; 							are to be hidden with customisation
	; 28-Mar-2007	RPW		Code Rewrite
	;  8-Aug-2006	JW		SR13594: Cleaned up.
	; 20-Jul-2005	GRF		SR12996 : Doco
	; 11.10.1999	DT
	;-------------------------------------------------------------------------------
	
	;---------------------------------------
	;	YVOR	objForm				WWW120
	;	YVOR1	objCoyDefaults		WWW012
	;---------------------------------------
	
	set YVOR1 = $get(^WWW012(0,YM,1))  ;LAYOUTVORGABEN ; Company layout defaults
	set YVOR  = ""
	IF YFORM'="" {
		set YVOR = $get(^WWW120(0,YFORM,1))  ;FORMULARDATEN
		set $$$WWW120DoNOTDisplayStandardButto(YVOR) = $$DoNotDisplay^WWW120(YFORM)   ;SRBR014601
	}
	
	if $$$WWW120InheritCompanyProperties(YVOR) {
		for YI=3:1:10,63,64,70,77,91 set $piece(YVOR,Y,YI) = $piece(YVOR1,Y,YI)   ;VORGABEN AUS MANDANT IN FORMULAR ;out of Company within form 
		
		set YI = 66    ; $$$WWW012DisplayButtonOnBottomLine		$$$WWW120PositioningOfButtonLine
		if (+$piece(YVOR1,Y,YI)'=0) && ($piece(YVOR,Y,YI)="")  set $piece(YVOR,Y,YI) = $piece(YVOR1,Y,YI)  ;BUTTONLEISTE ; "WWWBUTTONFORMAT"
		if ($piece(YVOR,Y,YI)=2) && ('$data(^WWW124(0,YFORM))) set $piece(YVOR,Y,YI) = 0
		
		
		set YI=13      ; $$$WWW012FormsFramed					$$$WWW120DisplayFrames
		if +$piece(YVOR1,Y,YI)'=$$$NO set $piece(YVOR,Y,YI) = $$$YES
		
		set $piece(YVOR,Y,36) = $piece(YVOR1,Y,17)     ; HorizontalOrientation
		set $piece(YVOR,Y,37) = $piece(YVOR1,Y,18)     ; VerticalOrientation
		
	;	if $piece(YVOR,Y,44)'=2 set $piece(YVOR,Y,44)=$piece(YVOR1,Y,23)  ;WENN KEINE ANDERE VORGABE
		set $$$WWW120PicturesAsButtons(YVOR) = $$$WWW012ForcePicturesAsButtons(YVOR1)     ; D45/D24
	
	} else {
		for YI=3:1:10,63,64,77,91 set $piece(YVOR1,Y,YI) = $piece(YVOR,Y,YI)   ;VORGABEN
	}
	
	if ($$$WWW120InputFormatLengthInPixel(YVOR)="") && ($$$WWW120FormFormatting(YVOR)'=1) set $$$WWW120InputFormatLengthInPixel(YVOR) = 150
	
	if $$$WWW120FixedHeader(YVOR)=$$$YES set YFIXHEADER = $$$YES
	
	quit
	
]]></Routine>
</Export>