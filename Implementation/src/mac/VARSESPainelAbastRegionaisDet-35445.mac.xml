<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESPainelAbastRegionaisDet" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESPainelAbastRegionaisDet
#include COMSYS
#include VARConst
#include VARSESPainelAbastecimento
 
PrintPainelAbastRegionaisDet(pFilterLocal,pfilterTipoPadrao,pfilterSituacao,pfilterOrdenacao,pfilterZerados)
 
	new count, lstHeader, rsSQL, meuSQL, idProduto, idSituacao, tipoSituacao, qtyEstoque, qtyEstoqueLocal,
		CMMLocal, qtyCoberturaEstoque, qtySolicPendentes, keyConsultaEstoque, bkgColor
 
 	if '$data(^VARSESPainelAbastecimentoRun(0,0,1)) {
	 	do PrintMsgEmExecucao^VARSESPainelAbastUtils(1)
	 	quit
 	} 
	
	quit:(pFilterLocal = "")
	quit:(pfilterTipoPadrao = "")
	
	if (pfilterOrdenacao '= "") {
		set ordenarPor = $case(pfilterOrdenacao, 1:"Produto asc", 2:"$$SQLGetDescricaoProduto^VARSQL(Produto) asc",
												3:"CoberturaLocal asc, Produto asc")
	} else {
		set ordenarPor = "Produto asc"
	}
	
	set count = 0
 
	do PrintHeader(pFilterLocal,pfilterTipoPadrao,pfilterSituacao,pfilterZerados,1)
	
	write "<br />" 
	write "<table cellspacing='1' style='margin:right:20px'>"
	write "	<tr style='background-color:#2c6c7f;color:white'>"
	write "		<th>#</th>"
	write " 	<th>Código</th>"
	write "		<th>Produto</th>"
	write " 	<th>Grupo</th>"	
	write "		<th>Estoque Centrais</th>"	
	write "		<th>Estoque Local</th>"
	write "		<th>CMM Local</th>"
	write "		<th>Cobertura Local (em dias)</th>"
	write "		<th>Solicitações Pendentes</th>"
	write "	</tr>"
 
	set meuSQL = "SELECT Produto,"
	set meuSQL = meuSQL_" Situacao,"
	set meuSQL = meuSQL_" TipodePadrao,"
	set meuSQL = meuSQL_" EstoquesCentrais,"
	set meuSQL = meuSQL_" EstoqueLocal,"
	set meuSQL = meuSQL_" CMMLocal,"
	set meuSQL = meuSQL_" QuantSolicPendentes,"
	set meuSQL = meuSQL_" CoberturaLocal"
	set meuSQL = meuSQL_" FROM VARSESPainelAbastRegionais"
	set meuSQL = meuSQL_" WHERE LocalEstoque = '"_pFilterLocal_"'"
	
	if (pfilterTipoPadrao '= "") {
		set meuSQL = meuSQL_" AND TipodePadrao = "_pfilterTipoPadrao
	}
	
	if (pfilterSituacao '= "") {
		set meuSQL = meuSQL_" AND Situacao = "_pfilterSituacao
	}
		
	if (pfilterZerados = 1) {
		set meuSQL = meuSQL_" AND Zerado = 1"
	}
		
	set meuSQL = meuSQL_" ORDER BY "_ordenarPor
	
	set rsSQL = ##class(%ResultSet).%New()  ;Create Result Set Object
	do rsSQL.Prepare(meuSQL) ;Prepare Query
	do rsSQL.Execute()  	 ;Execute Query
 
	while (rsSQL.Next()) {		
		set idProduto         = rsSQL.GetData(1)
		set idSituacao        = rsSQL.GetData(2)
		set tipoPadrao        = rsSQL.GetData(3)
		set qtyEstoqueCentral = rsSQL.GetData(4)
		set qtyEstoqueLocal   = rsSQL.GetData(5)
		set CMMLocal		  = +rsSQL.GetData(6)
		set qtySolicPendentes = +rsSQL.GetData(7)
		set CoberturaLocal	  = +rsSQL.GetData(8)
		
		set count = $increment(count)
 
	  	if (count#2=0) set bkgColor = $$$corCinzaClaro
	  	if (count#2=1) set bkgColor = $$$corCinzaEscuro
		
		//Para poder usar a função WWWTR		
		set:($get(SPRACHE) = "") SPRACHE = "PT"
		set:($get(YBED) = "") YBED = "UNKNOWN"
		//
		
		write "	<tr style='background-color:"_bkgColor_"'>"
		write " <td>"_count_"</td>"
		write " <td><a class='link' href='VARSESPainelAbastProduto.csp?fProd="_idProduto_"&fOrig=2&fLoc="_pFilterLocal_"&fTip="_pfilterTipoPadrao_"&fSit="_pfilterSituacao_"&fOrd="_pfilterOrdenacao_"&fZer="_pfilterZerados_"'>"_idProduto_"</a></td>"
		write " <td><a class='link' href='VARSESPainelAbastProduto.csp?fProd="_idProduto_"&fOrig=2&fLoc="_pFilterLocal_"&fTip="_pfilterTipoPadrao_"&fSit="_pfilterSituacao_"&fOrd="_pfilterOrdenacao_"&fZer="_pfilterZerados_"'>"_$$SQLGetDescricaoProduto^VARSQL(idProduto)_"</a></td>"
		write " <td>"_$$SQLGetGrupoItem^VARSQL(idProduto)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,qtyEstoqueCentral)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,qtyEstoqueLocal)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,CMMLocal)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,CoberturaLocal)_"</td>"
		write " <td style='text-align:right'>"_$$^WWWTR(0,12,qtySolicPendentes)_"</td>"
		write "</tr>"
	}
	
	if (count = 0) {
		write "<tr style='background-color:white'>"
		write "	<td colspan='12'>Nenhum registro encontrado.</td>"
		write "</tr>"
	}
 
	write "</table>"
	do rsSQL.Close()
 
	quit
	
PrintHeader(pidLocal,pidTipoPadrao,pidSituacao,pblnZerados,isCSPPage=0)
	new bkgColor, descTipoProduto, descSituacao	
 
	set descTipoProduto = $$GetDescCompTipoPadraoFromID^VARSESPainelAbastUtils(pidTipoPadrao)
 
	set descSituacao = ""
	set:(pidSituacao '= 6) descSituacao = " com "
	
	if (pblnZerados = $$$YES) {		
		if (pidSituacao = "") {		
			set descSituacao = "Zerados"
		} else {
			set descSituacao = "Zerados "_descSituacao_$piece($get(^VARPARA(0,"SITUACAOABASTECIMENTOREG","PT",pidSituacao,1)),"~",1)
		}
		
	} else {
		set descSituacao = descSituacao_$piece($get(^VARPARA(0,"SITUACAOABASTECIMENTOREG","PT",pidSituacao,1)),"~",1)
	}
	
	set descSituacao = descSituacao_" na unidade "_$$SQLGetLocationName^VARSQL(pidLocal)
	
	set bkgColor = $case(pidSituacao, 6:$$$corCinzaEscuro, 7:$$$corVermelho, 8:$$$corAmarelo, 9:$$$corVerde, 10:$$$corAzul)
 	
 	if (isCSPPage = $$$YES) {
		write "<div style='margin-top:20px;padding-top:10px;padding-bottom:10px;padding-left:15px;padding-right:15px;background-color:"_bkgColor_";'>" 	
 	} else {
		write "<div style='margin-top:20px;padding-top:6px;padding-bottom:6px;padding-left:12px;padding-right:12px;border-style:solid;border-width:2px;border-color:white;background-color:"_bkgColor_";'>"
 	}
 	
	write "<strong>"_descTipoProduto_" "_descSituacao_"</strong>"
	write "</div>"
	
	quit	
]]></Routine>
</Export>