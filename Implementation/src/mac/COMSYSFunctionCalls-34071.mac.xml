<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMSYSFunctionCalls" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMSYSFunctionCalls
	;-------------------------------------------------------------------------------
	;
	; History
	; 10-Dec-2004	GRF		SR11208 : Relocate FINSYSFunctionCalls
	;-------------------------------------------------------------------------------
#include COMSYS
#include WWWConst
	
Start(pstrPrefix="FIN,REP")
	;-------------------------------------------------------------------------------
	;
	; History
    ; 10-Dec-2004	GRF		SR11208 : "idx" easier to searcb for than "i";
    ; 						Boolean => Macro
	; 17-Apr-2004	shobby	Multiple modules.
	; 15-Sep-2003	shobby	Creation
	;-------------------------------------------------------------------------------
	new idx
	
	;kill $$$Storage("Expand")
	do StartTree^COMTree($get(YPARA))
	do AddNode^COMTree("",1,"","Forms","","","","","","","")
	for idx=1:1:$length(pstrPrefix,",") {
		do AddClasses($piece(pstrPrefix,",",idx))
	}
	do StopTree^COMTree($$$YES)
	quit
	
AddClasses(pstrPrefix)
    ;-------------------------------------------------------------------------------
    ;
    ; History:
    ; 25-Feb-2004	shobby	Created
    ;-------------------------------------------------------------------------------
    new idxForm
    
    set idxForm = pstrPrefix
	for {
		set idxForm = $order(^WWW120(0,idxForm))
		quit:idxForm=""
		quit:(($extract(idxForm,1,$length(pstrPrefix))'=pstrPrefix) && (pstrPrefix'=""))
		
		set $$$Storage("Expand",idxForm) = ""
		do AddNode^COMTree(1,idxForm,"",idxForm)
		do AddSubNode(idxForm)
	}
	quit
	
AddSubNode(pidxForm="")
    ;-------------------------------------------------------------------------------
    ; Find calls to methods from @netManager Forms
    ;
    ; Returns:
    ;
    ; History:
    ; 10-Dec-2004	GRF		SR11208 : "idx" easier to search for than "i"
    ; 25-Feb-2004	shobby	Created
    ;-------------------------------------------------------------------------------
	new objWWW120,lstFields,idx,intCount
	
	set intCount=0
	set $$$Storage("Expand","Form - "_pidxForm)=""
	do AddNode^COMTree(pidxForm,"Form - "_pidxForm,"","Form")
	
	set lstFields="93,126,15,14,59,89,16,86,87,17,18"
	
	set objWWW120=$get(^WWW120(0,pidxForm,1))
	for idx=1:1:$length(lstFields,",") {
		set intCount=intCount+$$ShowMethod("Form - "_pidxForm,objWWW120,$piece(lstFields,",",idx))
	}
	do AddFields(pidxForm)
	quit intCount
	
AddFields(pidxForm)
    ;-------------------------------------------------------------------------------
    ;
    ; History:
    ; 10-Dec-2004	GRF		SR11208 : "idx" easier to searcb for than "i"
    ; 25-Feb-2004	shobby	Created
    ;-------------------------------------------------------------------------------
	new idxProperty,strProperty,strFunction,objWWW122,lstFields,idx,intCount,strDescription
	
	set $$$Storage("Expand","Fields - "_pidxForm)=""
	do AddNode^COMTree(pidxForm,"Fields - "_pidxForm,"","Fields")
	set idxProperty=""
	for {
		set idxProperty=$order(^WWW122(0,pidxForm,idxProperty))
		quit:idxProperty=""
		
		set objWWW122=$get(^WWW122(0,pidxForm,idxProperty,1))
		set strProperty=$$$WWW122ManualCaption(objWWW122)
		
		set lstFields="15,25,68,92,99,97"
		
	;	set objWWW122=$get(^WWW122(0,pidxForm,1))
		set intCount=0
		for idx=1:1:$length(lstFields,",") {
			set strFunction=$piece(objWWW122,Y,$piece(lstFields,",",idx))
			if strFunction'="" {
				set strDescription=$$$WWW003PropertyDescription($get(^WWW003(0,"WWW122",$piece(lstFields,",",idx),1)))
				set intCount=intCount+1
				do AddNode^COMTree("Fields - "_pidxForm_":"_strProperty,"Fields - "_pidxForm_":"_strProperty_":"_strDescription,"",strDescription,,,,,strFunction)
			}
		}
		if intCount>0 {
			do AddNode^COMTree("Fields - "_pidxForm,"Fields - "_pidxForm_":"_strProperty,"",strProperty,,,,,)
			set $$$Storage("Expand","Fields - "_pidxForm_":"_strProperty)=""
		}
	}
	quit
	
ShowMethod(pidxForm,pobjWWW120,intField)
    ;-------------------------------------------------------------------------------
    ; If field has text in it then show it
    ;
    ; Returns:
    ;
    ; History:
    ; 25-Feb-2004	shobby	Created
    ;-------------------------------------------------------------------------------
    new strFunction,blnNodeAdded,strFieldDescription
    
    set blnNodeAdded=0
    set strFunction=$piece(pobjWWW120,Y,intField)
    if strFunction'="" {
	    set strFieldDescription=$piece($get(^WWW003(0,"WWW120",intField,1)),"~",1)
	    if '$$IsValid($piece(pidxForm,"- ",2),strFieldDescription,strFunction) {
		    set strFunction="<B>"_strFunction_"</B>"
	    }
	;	set $$$Storage("Expand",strFieldDescription)=""
		do AddNode^COMTree(pidxForm,pidxForm_":"_strFieldDescription,"",strFieldDescription,,,,,strFunction,,,)
		;do AddNode^COMTree(pidxForm,pidxForm_":"_strFieldDescription,"",strFieldDescription,,"WWW122",,,strFunction,,,""""_pidxForm_""","_2)
		set blnNodeAdded=1
    }
	quit blnNodeAdded
	
IsValid(pstrForm,pstrFieldDesc,pstrFunction)
	;-------------------------------------------------------------------------------
	; Checks whether the function call conforms to the naming standards of the Field Description
	;
	; Returns:
	;
	; History:
	; 27-Sep-2007	GRF		Shorten variable name
	; 18-Mar-2005	Shobby	Created
	;-------------------------------------------------------------------------------
	new blnFlag
	
	set blnFlag=0
	set pstrFieldDesc = $translate(pstrFieldDesc," ")
	
	if pstrFieldDesc = "ExecuteAfterDataFields"            set blnFlag = (pstrFunction[("OnAfterDataFields^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteBeforeButtonline"           set blnFlag = (pstrFunction[("OnBeforeButtonLine^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteBeforeSavingDataRecord"     set blnFlag = (pstrFunction[("OnBeforeSave^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteAfterSavingOfDataRecords"   set blnFlag = (pstrFunction[("OnAfterSave^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteAfterPrimaryKey"            set blnFlag = (pstrFunction[("OnAfterPrimaryKey^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteBeforeDeletionDataRecord"   set blnFlag = (pstrFunction[("OnBeforeDelete^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteAfterButtonLine"            set blnFlag = (pstrFunction[("OnAfterButtonLine^"_pstrForm_"("))
	if pstrFieldDesc = "ExecuteAfterDeletionOfDataRecords" set blnFlag = (pstrFunction[("OnAfterDelete^"_pstrForm_"("))
	
	quit blnFlag
	
]]></Routine>
</Export>