<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARCentroCusto" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARCentroCusto
#include COMSYS
#include INConst

OnAfterDataFields(pidCentroCusto)
	new idNode,strDesc,blnHighlight,idParent
	
	do GetNodes(pidCentroCusto)		// Get the nodes to display
	do StartTree^COMTree(YPARA)
	
	set idNode = ""
	for {
		set idNode = $order(^CacheTempCentroCusto(YUCI,YUSER,idNode))
		quit:idNode=""
		
		set idParent     = $get(^CacheTempCentroCusto(YUCI,YUSER,idNode))
		set blnHighlight = (idNode=pidCentroCusto)
		set strDesc      = $$^WWWFORMAT("VARCentroCusto","D",1,idNode)
		
		do AddNode^COMTree(idParent,idNode,$$GetCode^INBItemGroup(idNode),strDesc,,"VARCentroCusto",,blnHighlight,,blnHighlight,,,$$$YES)
	}
	do StopTree^COMTree($$$YES,,$$$YES)
	kill ^CacheTempCentroCusto(YUCI,YUSER)
	
	quit

GetNodes(pidCentroCusto)
	;-------------------------------------------------------------------------------
	; Return a group, its ancestors and direct children - for display.
	; ByRefs:	global array: ^CacheTempCentroCusto(YUCI,YUSER,idCentroCusto)=parent
	;-------------------------------------------------------------------------------
	new idCentroCusto,idParent,objCentroCusto,idxGroup
	
	kill ^CacheTempCentroCusto(YUCI,YUSER)
	
	// Get ancestors
	set idCentroCusto = pidCentroCusto
	while (idCentroCusto'="") {
		set objCentroCusto = $get(^VARCentroCusto(YM,idCentroCusto,1))
		set idParent = $piece($get(^VARCentroCusto(YM,idCentroCusto,Y)),Y,5)
		set ^CacheTempCentroCusto(YUCI,YUSER,idCentroCusto) = idParent
		set idCentroCusto = idParent
	}
	
	// Get children
	set idxGroup = $$$Index(pidCentroCusto)
	$$$Order4(^VARCentroCustos,YM,1,idxGroup,idCentroCusto)
		set ^CacheTempCentroCusto(YUCI,YUSER,idCentroCusto) = pidCentroCusto
	$$$End
	
	quit

OnBeforeDelete(pidKey)
	new idxLocn, strStatus
 	quit:(pidKey = "")
		
	set idxLocn = $$$Index(pidKey)
	
	set strStatus = $$$OK

	if $data(^VARCentroCustos(0,1,idxLocn)) {
		set strStatus = "0 "_"Esse centro de custo Ã© pai de outros centros de custo. Favor excluir os filhos antes de poder deletar o pai."
	}

	if $$$ISERR(strStatus) {
		set Q=$$$QDontDelete	
		$$$DecomposeStatus(strStatus)
		$$$Alert(strStatus)
	}
 
	quit strStatus
]]></Routine>
</Export>