<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARDWPDPAS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARDWPDPAS
#include COMSYS
 
GeraConsolidadoMensalPDPAS(pCompetencia="")
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Gera o consolidado mensal do PDPAS.
    ;
    ; Parâmetro:
    ;	Competência: 201301 (anomes)
	;
	; Chamado por:
	;	[TO DO] VARSESPainelAbastecimento.mac
	;
    ; Histórico:
    ;	07-Ago-2013 Gustavo	Criado
    ;-------------------------------------------------------------------------------
	$$$VAR
	quit:(pCompetencia = "") "Nenhuma competência informada."
 
	new dteInicioCompetencia, dteFinalCompetencia, idProduto, idUnidade, idGrupo, quantTotal,
		valorTotal
	
	//Limpando dados gerados anteriormente
	kill ^VARDWPDPAS(YM,pCompetencia)	
	
	set dteInicioCompetencia = $$GetDataInicialFromCompetencia(pCompetencia)
	set dteFinalCompetencia  = $$GetDataFinalFromCompetencia(pCompetencia)
	
	&sql(DECLARE cursorConsolidadoMensal CURSOR FOR
		  SELECT Proposta.Produto as Codigo, 
		  		 $$getUnidadeSaudeLocal^VARSESWWW0121(Compra.LocaldeCompra),
		  		 $$GetGrupoPDPASFromID^VARSESPainelPDPASUtils(Proposta.Grupo) as Grupo,
		  		 SUM(Proposta.QuantidadeOfertada) as Quantidade, 
		  		 SUM(Proposta.PrecoTotal) as ValorTotal
		    INTO :idProduto, :idUnidade, :idGrupo, :quantTotal, :valorTotal
		    FROM VARSESCOMPRASPropostaLinha Proposta
	  INNER JOIN VARSESCOMPRAS Compra on Compra.NoProcessoCompra = Proposta.NoProcessodeCompra
		   WHERE UPPER(Proposta.Resultado) = 1
		     AND Compra.Modalidade = '10'
		   	 AND $piece(Compra.CriadoEm,',',1) BETWEEN $$GetDataInicialFromCompetencia^VARDWPDPAS(:pCompetencia)
		     	AND $$GetDataFinalFromCompetencia^VARDWPDPAS(:pCompetencia)
		GROUP BY Proposta.Produto, $$getUnidadeSaudeLocal^VARSESWWW0121(Compra.LocaldeCompra)
		ORDER BY Proposta.Produto asc
	)
	
	&sql(open cursorConsolidadoMensal)
   	quit:(SQLCODE)
   	
	for { &sql(fetch cursorConsolidadoMensal)
		quit:(SQLCODE)
 
		set ^VARDWPDPAS(YM,pCompetencia,idProduto,idUnidade,1) = quantTotal_Y_valorTotal_Y_idGrupo_Y_$horolog
	}
	
	&sql(close cursorConsolidadoMensal)
 
	if 'SQLCODE {
		write "Dados gerados com sucesso para a competência "_$extract(pCompetencia,5,6)_"/"_$extract(pCompetencia,1,4)_"." ,!
	}
	else {
		write "Erro: "_SQLCODE
	}
 
	quit $$$OK
	
	
GetDataInicialFromCompetencia(pCompetencia)
	quit:(pCompetencia = "") ""	
	new dia, mes, ano, dteInicial, dteInicialHorolog
	
	set dteInicialHorolog = ""	
	
	set dia = "01"
	set mes = $extract(pCompetencia,5,6)
	set ano = $extract(pCompetencia,1,4)
	
	set dteInicial = dia_"/"_mes_"/"_ano
	set dteInicialHorolog = $zdateh(dteInicial,4)
	
	quit dteInicialHorolog
	
GetDataFinalFromCompetencia(pCompetencia)
	quit:(pCompetencia = "") ""	
	
	new dteInicialHorolog, dteFinalHorolog
	
	set dteInicialHorolog = $$GetDataInicialFromCompetencia(pCompetencia)
	set dteFinalHorolog   = $$GetEndOfMonth^COMUtilDate(dteInicialHorolog)
	
	quit dteFinalHorolog
	
	
getValorMensal(pCompetencia,pidProduto="",pidUnidade="",pidGrupoPDPAS="")
	quit:(pCompetencia = "") 0
	
    new valorMensal, rsValorMensal, meuSQL
    
    set valorMensal = 0
    
    set rsValorMensal = ##class(%ResultSet).%New()  ;Create Result Set Object
    
    set meuSQL = "SELECT SUM(ValorTotal), Produto"
    set meuSQL = meuSQL_" FROM VARDWPDPAS"
	set meuSQL = meuSQL_" WHERE Competencia = '"_pCompetencia_"'"
	
	if (pidProduto '= "") {
		if (pidUnidade '= "") {
			set meuSQL = meuSQL_" AND UnidadeSaude = '"_pidUnidade_"'"
		}
		
		set meuSQL = meuSQL_" AND Produto = '"_pidProduto_"'"
		set meuSQL = meuSQL_" GROUP BY Produto"			
		
	}
	
	elseif ( (pidUnidade '= "") && (pidGrupoPDPAS = "") ) {
		set meuSQL = meuSQL_" AND UnidadeSaude = '"_pidUnidade_"'"
		set meuSQL = meuSQL_" GROUP BY UnidadeSaude"	
	}
	
	elseif ( (pidUnidade = "") && (pidGrupoPDPAS '= "") ) {
		set meuSQL = meuSQL_" AND Grupo = "_pidGrupoPDPAS
		set meuSQL = meuSQL_" GROUP BY Grupo"
	
	} elseif ( (pidUnidade '= "") && (pidGrupoPDPAS '= "") ) {
		set meuSQL = meuSQL_" AND UnidadeSaude = '"_pidUnidade_"'"		
		set meuSQL = meuSQL_" AND Grupo = "_pidGrupoPDPAS
		set meuSQL = meuSQL_" GROUP BY UnidadeSaude"
	}
    
    do rsValorMensal.Prepare(meuSQL)  ;Prepare Query
    do rsValorMensal.Execute()  ;Execute Query    
 
    while (rsValorMensal.Next()) {            
        set valorMensal = rsValorMensal.GetData(1)        
    }
    
    do rsValorMensal.Close()
 
    quit valorMensal
 
getValorAnual(pCompetencia,pidUnidade="",pidGrupoPDPAS="")
	quit:(pCompetencia = "") 0
	
    new valorAnual, rsValorAnual, meuSQL
    
    set valorAnual = 0    
    set rsValorAnual = ##class(%ResultSet).%New()  ;Create Result Set Object
    
    set meuSQL = "SELECT SUM(ValorTotal)"
    set meuSQL = meuSQL_" FROM VARDWPDPAS"
	set meuSQL = meuSQL_" WHERE $$ExtractAnoFromCompetencia^VARSESPainelPDPASUtils(Competencia) = '"_pCompetencia_"'"
	
	if ( (pidUnidade '= "") && (pidGrupoPDPAS = "") ) {
		set meuSQL = meuSQL_" AND UnidadeSaude = '"_pidUnidade_"'"
		set meuSQL = meuSQL_" GROUP BY UnidadeSaude"	
	
	} elseif ( (pidUnidade = "") && (pidGrupoPDPAS '= "") ) {
		set meuSQL = meuSQL_" AND Grupo = "_pidGrupoPDPAS
		set meuSQL = meuSQL_" GROUP BY Grupo"
	
	} elseif ( (pidUnidade '= "") && (pidGrupoPDPAS '= "") ) {
		set meuSQL = meuSQL_" AND UnidadeSaude = '"_pidUnidade_"'"		
		set meuSQL = meuSQL_" AND Grupo = "_pidGrupoPDPAS
		set meuSQL = meuSQL_" GROUP BY UnidadeSaude"
	}   
    
    do rsValorAnual.Prepare(meuSQL)  ;Prepare Query
    do rsValorAnual.Execute()  ;Execute Query    
 
    while (rsValorAnual.Next()) {            
        set valorAnual = rsValorAnual.GetData(1)        
    }
    
    do rsValorAnual.Close()
 
    quit valorAnual
]]></Routine>
</Export>