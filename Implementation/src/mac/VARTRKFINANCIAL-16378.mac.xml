<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARTRKFINANCIAL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARTRKFINANCIAL
#include COMSYS
#include COMConst
#include INConst
#include WWWConst
#include FIN
#include VARConst
 
	quit
	
OnAfterSave
	new strStatus, DataEntregaEmpenhoAoForn
	
	set DataEntregaEmpenhoAoForn = $piece(YFELD,Y,349)
	
	// Atualiza as datas das entregas, baseada na data de entrega do empenho ao fornecedor:
	// Se da DataEntregaEmpenhoAoForn for nula, atualiza das datas das entregas para nulo também.
	set strStatus = $$Transaction^COMTransaction("AtualizaEntregas^VARTRKFINANCIAL("""_YKEY_""","_DataEntregaEmpenhoAoForn_")",$$$YES)
	
	quit
	
AtualizaEntregas(pNumAFM,pDataEntregaEmpenhoAoForn)
	// Andre, 17/03/2008
	// 
	// Sendo definida a "Data de Entrega do Empenho ao Fornecedor", a utiliza
	// para atualizar as datas de entrega de todas as entregas da AFM.
	// 
	
	new strStatus, numEntrega, objEntrega, prazoDeEntrega, DataDeEntrega
	
	set strStatus = 1
	
	if ( $get(pNumAFM) = "" ) {
		set strStatus = "0~Número de AFM não informado."
		quit strStatus
	}
	
	if ( '$data(^INAUF(YM,pNumAFM,1)) ) {
		set strStatus = "0~AFM não cadastrada: "_pNumAFM_"."
		quit strStatus
	}
	
	/*
	if ( +$get(pDataEntregaEmpenhoAoForn) = 0 ) {
		set strStatus = "0~Não foi informada a Data de Entrega do Empenho ao Fornecedor para a AFM """_pNumAFM_"""."
		quit strStatus
	}
	*/
 
	if ( +$get(pDataEntregaEmpenhoAoForn) > +$h ) {
		set strStatus = "0~Data de Entrega do Empenho ao Fornecedor inválida ("_$zdt(pDataEntregaEmpenhoAoForn,4)_") para a AFM """_pNumAFM_"""."_$char(10)_"Esta data deve ser igual ou inferior à data de hoje."
		quit strStatus
	}
 
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARSESAFMEntrega(YM,pNumAFM,numEntrega))
		quit:(numEntrega = "")
		
		set objEntrega = $get(^VARSESAFMEntrega(YM,pNumAFM,numEntrega,1))
		if ( $get(objEntrega) = "" ) {
			set strStatus = "0~Erro ao ler entrega """_numEntrega_""" da AFM """_pNumAFM_"""."
			quit
		}
		
		// Verifica se há prazo de entrega:
		set prazoDeEntrega = $piece(objEntrega,Y,1)
		if ( +prazoDeEntrega <= 0 ) {
			set strStatus = "0~Prazo de entrega inválido ("_+prazoDeEntrega_" dias) para a entrega """_numEntrega_""" da AFM """_pNumAFM_"""."
			quit
		}
		
		// Redefine data de entrega:
		if ( +$get(pDataEntregaEmpenhoAoForn) = 0 ) {
			set DataDeEntrega = ""
		}
		else {
			set DataDeEntrega = +pDataEntregaEmpenhoAoForn + prazoDeEntrega
		}
		set $piece(objEntrega,Y,2) = DataDeEntrega
		
 
		// Atualizada Data de Entrega da NE:
		set $piece(objEntrega,Y,9) = $get(pDataEntregaEmpenhoAoForn)
		
		// Salva:
		set strStatus = $$Save^COMUtils("VARSESAFMEntrega",pNumAFM_YKOMMA_numEntrega,objEntrega,1)
		if ( strStatus '= 1 ) {
			quit
		}
		
	}
	
	quit strStatus
	
GetDataEntregaEmpenhoAoForn(pNumAFM)
 
	new DataEntregaEmpenhoAoForn
	
	set DataEntregaEmpenhoAoForn = ""
 
	if ( $get(pNumAFM) = "" ) {
		quit ""
	}
 
	set objAFM = $get(^INAUF(YM,pNumAFM,1))
	
	if ($get(objAFM) = "") {
		quit ""
	}
 
	set DataEntregaEmpenhoAoForn = $piece(objAFM,Y,349)
	
	quit DataEntregaEmpenhoAoForn
 
	
]]></Routine>
</Export>