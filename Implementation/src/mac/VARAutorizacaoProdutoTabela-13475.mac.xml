<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARAutorizacaoProdutoTabela" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARAutorizacaoProdutoTabela
#include COMSYS
#include COMConst
#include INConst
#include WWWConst
#include FIN
#include VARConst
 
OnAfterSave()
 
	new Produto
	
	set Produto	= $get(VORG(1))
	
	if ($length(Produto) '= 0) {
		if ($get(^VARTempAutorizacaoProdutoTabela(YM,YBED,Produto))) {
			do SalvaAutorizacoes(Produto)
		} else {
			set ^VARTempAutorizacaoProdutoTabela(YM,YBED,Produto) = 1
		}
	} else {
		kill ^VARTempAutorizacaoProdutoTabela(YM,YBED)
	}
	
 	do GoToForm^COMUtilForm("VARAutorizacaoProdutoTabela",1,,,,,Produto)
 	
 	quit
	
SalvaAutorizacoes(pItem)
 
	new idLocal
	
	set idLocal = $order(^VARTempAutorizacaoProdutoTabela(YM,YBED,pItem,""))
	
	while (idLocal '= "") {
    	if ((^VARTempAutorizacaoProdutoTabela(YM,YBED,pItem,idLocal) = 1) && ($data(^VARAutorizacaoProduto(YM,idLocal,pItem)) = 0)) {
			do ^VARIncluirProdLocal(pItem,idLocal)
			do SalvaCodigoAquisicao(pItem,idLocal)					
		} elseif ((^VARTempAutorizacaoProdutoTabela(YM,YBED,pItem,idLocal) = 0) && ($data(^VARAutorizacaoProduto(YM,idLocal,pItem)) > 0)) {				
			set strStatus = $$$Kill("VARAutorizacaoProduto",idLocal_$$$COMMA_pItem)
		}
	
    	set idLocal = $order(^VARTempAutorizacaoProdutoTabela(YM,YBED,pItem,idLocal))
    }
	
	quit
 	
 	
PainelAutorizacoes()
 
	new Produto, Tipo, TipoEstoque, Espacamento
	
	set Produto	= $get(VORG(1))
	
	if ($length(Produto) = 0) {
		kill ^VARTempAutorizacaoProdutoTabela(YM,YBED)
		kill ^VARTempAutorizacaoProdutoTabelaData
		quit
	}
 
	set Tipo = $$$INARTFREE35($GET(^INART(YM,Produto,1)))
		
	set TipoEstoque = $$$WWW0121FREE4($get(^WWW0121(YM,YM,YLOCATION,1)))
	
	do ImprimeCabecalho(Produto,Tipo)
	
 
	if Tipo = TipoEstoque {
		do TabelaAutorizacoes(Produto,Tipo)
	}
	
	
	else { 
		write "<br>&nbsp;<b>Erro:</b> O estoque principal do produto é diferente do tipo de estoque local do usuário."
		write "<br>&nbsp;Estoque principal do produto: "_Tipo_". Tipo de estoque do local do usuário: "_TipoEstoque	
	}
 
	quit
 
 
ImprimeCabecalho(Item,Tipo)
 
 	new DescricaoItem, Unidade, DescricaoUnidade, Grupo, DescricaoGrupo
 
	set DescricaoItem    = $piece($get(^INART(YM,Item,1)),Y,1)
	set Unidade          = $piece($get(^INART(YM,Item,1)),Y,40)
	set DescricaoUnidade = $$SQLGetDescCompletaUnit^VARSQL(Unidade)
	set Grupo 		     = $piece($get(^INART(YM,Item,1)),"~",30)
	set DescricaoGrupo 	 = $$SQLGetDescGrupoItem^VARSQL(Grupo)
	
	write "<hr>" //Desenha 1ª Linha Horizontal
	write "&nbsp;<b>Produto: "_Item_" - "_DescricaoItem_"</b>"
	write "<hr>" //Desenha 2ª Linha Horizontal	
	write "<table style=font-size:12px>" //Detalhes do Item
	write "<tr> <td>Unidade de Estoque:</td><td>"_DescricaoUnidade_"</td></tr>"
	write "<tr> <td>Grupo do Produto:</td><td>"_DescricaoGrupo_"</td></tr>"
	write "<tr> <td>Estoque Principal:</td><td>"_Tipo_"</td></tr>"
	write "</table>"
	
	quit  
 
 
TabelaAutorizacoes(YKEY,Tipo)
 
	new lstHeader, Local, objLocal, StorageLocation, DescricaoLocal, Chave,
		Autorizado, TipoEstoque, LocalAtual, ArrayAbastecidos, LocalFind
 
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Código","Local"," Autorizado?","")
	
	if $$Start^COMTable(lstHeader,,,,$$$NO) {
	
		set LocalAtual = YLOCATION
	
		set Local = ""	
	
		for {				
			set Local = $order(^WWW0121s(YM,3,1,YM,Local))
			quit:(Local = "")
		
			set objLocal = $get(^WWW0121(YM,YM,Local,1))
			set StorageLocation  = $$$WWW0121StorageLocn(objLocal)
			set DescricaoLocal   = $$$WWW0121LocationName(objLocal)
			set TipoEstoque      = $$$WWW0121FREE4(objLocal)					
			
			;5073, e somente ele, está autorizado a receber produtos de Farmácia e Almoxarifado
			if ( (StorageLocation = $$$YES) && (Tipo = TipoEstoque) ) || (Local = 5073) {
		
				set Chave      = Local_","_YKEY
				set Autorizado = $data(^VARAutorizacaoProduto(YM,Local,YKEY))
			
	    		do NewLine^COMTable()  				
	   			do InsertCell^COMTable(Local,"VARAutorizacaoProduto",Chave,"")
	   			do InsertCell^COMTable(DescricaoLocal,"VARAutorizacaoProduto",Chave,"")
				
				if ((Autorizado > 0) || ($get(^VARTempAutorizacaoProdutoTabela(YM,YBED,YKEY,Local)) = 1)) { 
					do InsertCellCheckbox^COMTable(Chave,"ToggleLine^VARAutorizacaoProdutoTabela",1,0)
					set ^VARTempAutorizacaoProdutoTabela(YM,YBED,YKEY,Local) = 1
				
				} elseif Autorizado = 0 {
					do InsertCellCheckbox^COMTable(Chave,"ToggleLine^VARAutorizacaoProdutoTabela",0,0)
					set ^VARTempAutorizacaoProdutoTabela(YM,YBED,YKEY,Local) = 0
				}
 				
 				do IconeIndicativo(Autorizado)
	   			
    			do EndLine^COMTable()
    			
			}		
		}	
 
	   set todosParam = LocalAtual_","_YKEY
 
 	   // Autorizar para todas as regionais
 	   
 	   if ((LocalAtual = 5) || (LocalAtual = 1))  {
 	   
	   		do NewLine^COMTable("lightsteelblue")  				
	   		do InsertCell^COMTable("")
	   		set strAutorizarTodosCallBack = "AutorizarRegionais^VARAutorizacaoProdutoTabela"			
	   		set strAutorizarTodos = "Autorizar para todas as regionais"	   
	   		do InsertCellCallback^COMTable(strAutorizarTodos,strAutorizarTodosCallBack,todosParam)
	   		do InsertCell^COMTable("",,,,,,3)
	   		do EndLine^COMTable()
 	   
	   		do NewLine^COMTable("lightsteelblue")  				
	   		do InsertCell^COMTable("")
	   		set strAutorizarTodosCallBack = "AutorizarUnidadesHospitalares^VARAutorizacaoProdutoTabela"			
	   		set strAutorizarTodos = "Autorizar somente para as unidades hospitalares"	   
	   		do InsertCellCallback^COMTable(strAutorizarTodos,strAutorizarTodosCallBack,todosParam)
	   		do InsertCell^COMTable("",,,,,,3)
	   		do EndLine^COMTable()
	   	
 	   }
	   
	   // Autorizar para todos os centros de saúde
	   
	   if ($length(LocalAtual) <= 4) {
		    do NewLine^COMTable("lightsteelblue")  				
	   		do InsertCell^COMTable("")
	   		set strAutorizarTodosCallBack = "AutorizarCentrosDeSaude^VARAutorizacaoProdutoTabela"				
	   		
	   		if ($length(LocalAtual) = 1) {
	   			set strAutorizarTodos = "Autorizar para todos os centros de saúde"	   
	   		}
	   		else {
	   			set strAutorizarTodos = "Autorizar para todos os centros de saúde da minha regional"	   
	   		}
	   			   		
	   		do InsertCellCallback^COMTable(strAutorizarTodos,strAutorizarTodosCallBack,todosParam)
	   		do InsertCell^COMTable("",,,,,,3)
	   		do EndLine^COMTable()  
		}
	}
	
	quit
 
IconeIndicativo(pblnAutorizado)
 
	if (pblnAutorizado > 0) {
		do InsertCell^COMTable("<img src="_YGIF_"small_check.gif TITLE='Produto já está autorizado para esse local.' border=0 align=""CENTER"">",,,,"CENTER")
		quit
	}
	
	else {
		do InsertCell^COMTable("<img src="_YGIF_"small_x.gif TITLE='Produto não está autorizado para esse local.' border=0 align=""CENTER"">",,,,"CENTER")
		quit
	}
	
	quit
 
BotaoAutorizar(Local,LocalAtual,Autorizado)
 
	new lineParam,strAutorizarCallBack,strBotaoDesautorizarOn
	
	set lineParam = Local_","_YKEY_","_Autorizado				
	set strAutorizarCallBack = "AutorizarDesautorizarProdutos^VARAutorizacaoProdutoTabela"			
	set strBotaoDesautorizarOn = "<img src="_YGIF_"v02.gif TITLE='Clique aqui para autorizar o produto para esse local.' border=0 align=""CENTER"">"						
 
	// Centrais
	if ((LocalAtual = 5) || (LocalAtual = 1)) {
		
		if (Autorizado > 0) {
			do InsertCell^COMTable("<img src="_YGIF_"v02.gif TITLE='Produto já está autorizado para esse local.' border=0 align=""CENTER"">",,,,"CENTER")		
			quit
		}
		
		else {
			do InsertCellCallback^COMTable(strBotaoDesautorizarOn,strAutorizarCallBack,lineParam)					
			quit
		}
	}
	
	// Regionais e seus Filhos
 	elseif (($length(LocalAtual) = 4) && ($extract(Local,1,4) = LocalAtual) && (LocalAtual '= Local)) {
	 	
	 	if (Autorizado > 0) {
			do InsertCell^COMTable("<img src="_YGIF_"v02.gif TITLE='Produto já está autorizado para esse local.' border=0 align=""CENTER"">",,,,"CENTER")		
			quit
		}
		
		else {
			do InsertCellCallback^COMTable(strBotaoDesautorizarOn,strAutorizarCallBack,lineParam)					
			quit
		}
 	}
 
 	// Restante (nem central nem regional com seus filhos)
 	else {
 		do InsertCell^COMTable("<img src="_YGIF_"v02pb.gif TITLE='Você não tem permissões para alterar esse local.' border=0 align=""CENTER"">",,,,"CENTER")
 		quit
	}	
	
	quit
	
 
BotaoDesautorizar(Local,LocalAtual,Autorizado)
 
	new lineParam,strAutorizarCallBack,strBotaoDesautorizarOn
	
	set lineParam = Local_","_YKEY_","_Autorizado				
	set strAutorizarCallBack = "AutorizarDesautorizarProdutos^VARAutorizacaoProdutoTabela"			
	set strBotaoDesautorizarOn = "<img src="_YGIF_"AEPicoCancelOn.gif TITLE='Clique aqui para desautorizar o produto para esse local.' border=0 align=""CENTER"">"						
 
	// Centrais
	if ((LocalAtual = 5) || (LocalAtual = 1)) {
		
		if (Autorizado = 0) {
			do InsertCell^COMTable("<img src="_YGIF_"AEPicoCancelOn.gif TITLE='Produto já está desautorizado para esse local.' border=0 align=""CENTER"">",,,,"CENTER")		
			quit
		}
		else {
			do InsertCellCallback^COMTable(strBotaoDesautorizarOn,strAutorizarCallBack,lineParam)					
			quit
		}
	}
	
	// Regionais com filhos
 	elseif (($length(LocalAtual) = 4) && ($extract(Local,1,4) = LocalAtual) && (LocalAtual '= Local)) {
	 	if (Autorizado = 0) {
			do InsertCell^COMTable("<img src="_YGIF_"AEPicoCancelOn.gif TITLE='Produto já está desautorizado para esse local.' border=0 align=""CENTER"">",,,,"CENTER")		
			quit
		}
		else {
			do InsertCellCallback^COMTable(strBotaoDesautorizarOn,strAutorizarCallBack,lineParam)					
			quit
		}		
 	}
 
 	// Restantes
 	else {
 		do InsertCell^COMTable("<img src="_YGIF_"AEPicoCancelOff.gif TITLE='Você não tem permissões para alterar esse local.' border=0 align=""CENTER"">",,,,"CENTER")
 		quit
	}	
	
	quit
 
 
AutorizarDesautorizarProdutos(Local,YKEY,Status)
 
 	new Produto
 	
 	set Produto = YKEY
 	 	 	
	if Status = 0 {
		
		do ^VARIncluirProdLocal(Produto,Local)
		do SalvaCodigoAquisicao(Produto,Local)					
		do GoToForm^COMUtilForm("VARAutorizacaoProdutoTabela",1,,,,,Produto) 
		
		quit
	}
	
	if Status > 0 {				
	
		kill ^VARAutorizacaoProduto(YM,Local,Produto,1)
		do KILL^VARAutorizacaoProduto
		do GoToForm^COMUtilForm("VARAutorizacaoProdutoTabela",1,,,,,Produto) 		
				
		quit
	}	
	
	quit
 		
	
AutorizarRegionais(LocalAtual,YKEY)
 
 	new Produto, Tipo, TipoEstoque, Local, objLocal, StorageLocation, TipoUnidade
 	
 	set Produto = YKEY
 	set Tipo = $PIECE($GET(^INART(YM,Produto,1)),"~",345)
 	set Local   = ""
 
	for {				
		set Local = $order(^WWW0121s(YM,3,1,YM,Local))
		quit:(Local = "")
		
		set objLocal = $get(^WWW0121(YM,YM,Local,1))
		
		set TipoUnidade = $$$WWW0121FREE22(objLocal)
 
		set TipoEstoque      = $$$WWW0121FREE4(objLocal)		
		
 
		if (((TipoUnidade = 1) || (TipoUnidade = 3)) && (Tipo = TipoEstoque)) {
			
			if ($length(LocalAtual) = 1) {				
				do ToggleLine(Local_","_Produto,"true")
			}	
		}													 	
	}
	
	do GoToForm^COMUtilForm("VARAutorizacaoProdutoTabela",1,,,,,Produto)
		
	quit
	
 
AutorizarCentrosDeSaude(LocalAtual,YKEY)
 
 	new Produto, Tipo, TipoEstoque, Local, objLocal, StorageLocation, TipoUnidade
 	
 	set Produto = YKEY
 	set Tipo = $PIECE($GET(^INART(YM,Produto,1)),"~",345)
 	set Local   = ""
 
	for {				
		set Local = $order(^WWW0121s(YM,3,1,YM,Local))
		quit:(Local = "")
		
		set objLocal = $get(^WWW0121(YM,YM,Local,1))
		
		set TipoUnidade = $piece(objLocal,Y,80)
 
		set TipoEstoque      = $piece(objLocal,Y,61)		
		
		
		if ((TipoUnidade = 2) && (Tipo = TipoEstoque)) {
			
			//se tiver na Central, autoriza para todos os centros de saúde
			if ($length(LocalAtual) = 1) {				
				do ToggleLine(Local_","_Produto,"true")
			}
			
			//se tiver na regional, autoriza apenas os centros de saúde que a regional abastece
			elseif (($length(LocalAtual) = 4) && ($extract(Local,1,4) = LocalAtual)  && (LocalAtual '= Local)) {									
				do ToggleLine(Local_","_Produto,"true")
			}
			
		}													 	
	}
	
	do GoToForm^COMUtilForm("VARAutorizacaoProdutoTabela",1,,,,,Produto)
		
	quit
 		
	
AutorizarUnidadesHospitalares(LocalAtual,YKEY)
 
 	new Produto, Tipo, TipoEstoque, Local, objLocal, StorageLocation, TipoUnidade
 	
 	set Produto = YKEY
 	set Tipo = $PIECE($GET(^INART(YM,Produto,1)),"~",345)
 	set Local   = ""
 
	for {				
		set Local = $order(^WWW0121s(YM,3,1,YM,Local))
		quit:(Local = "")
		
		set objLocal = $get(^WWW0121(YM,YM,Local,1))
		
		set TipoUnidade = $piece(objLocal,Y,80)
		set TipoEstoque      = $piece(objLocal,Y,61)
		
		if ((TipoUnidade = 1) && (Tipo = TipoEstoque)) {
			
			if ($length(LocalAtual) = 1) {				
				do ToggleLine(Local_","_Produto,"true")
			}	
		}													 	
	}
	
	do GoToForm^COMUtilForm("VARAutorizacaoProdutoTabela",1,,,,,Produto)
		
	quit
	
 
SalvaCodigoAquisicao(Produto,Local)
 
	new AlmoxarifadoCentral, FarmaciaCentral, objVARAutorizacaoProduto, CodAquisicao, Chave,
		strStatus
	
	set AlmoxarifadoCentral = $piece($get(^VARParams(YM,YM,1)),Y,1)
	set FarmaciaCentral     = $piece($get(^VARParams(YM,YM,1)),Y,2)
	
	if ((Local = AlmoxarifadoCentral) || (Local = FarmaciaCentral)) {			
		set CodAquisicao = 5 // Comprar			
	}
	
	else {
		set CodAquisicao = 6 // Transferir	
	}
				
	set objVARAutorizacaoProduto = 	$get(^VARAutorizacaoProduto(YM,Local,Produto,1))
		
	set $piece(objVARAutorizacaoProduto,Y,5)  = CodAquisicao
		
	set Chave = Local_$$$COMMA_Produto
	
	set strStatus = $$Save^COMUtils("VARAutorizacaoProduto", Chave, objVARAutorizacaoProduto, 1)
		
	if $$$ISERR(strStatus) { 
		do StartScript^COMUtiljavascript()
		w "alert('Não foi possível salvar os dados. "_strStatus_"'); "
		do EndScript^COMUtiljavascript()
	}
	
	quit
	
ToggleLine(pYKEY="",checked="")
	/*
	checked = "true,cb10103"
	"pYKEY" = "508501.99,90040"
	*/
	
	if ($piece(checked,",",1) = "true") {
    	set ^VARTempAutorizacaoProdutoTabela(YM,YBED,$piece(pYKEY,",",2),$piece(pYKEY,",",1)) = 1
    }
    elseif ($piece(checked,",",1) = "false") {
        set ^VARTempAutorizacaoProdutoTabela(YM,YBED,$piece(pYKEY,",",2),$piece(pYKEY,",",1)) = 0
    }
 
    quit $$$YES
    
OnAfterPrimaryKey(pItem="")
	
	if '$$$NoKey(pItem) {
		write "<script>"
		do UpdateFieldFormat^COMUtils("YVARAutorizacaoProdutoTabelaM1","readOnly","true")
		do UpdateFieldFormat^COMUtils("YVARAutorizacaoProdutoTabelaM1"_".style","backgroundColor",YGRAY)
		write "</script>"
	}
	
	quit
 
]]></Routine>
</Export>