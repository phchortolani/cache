<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="Cache" ts="2001-01-01 00:00:00">
<Routine name="WWWFORMF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[WWWFORMF
	
#include WWWConst
#include COMSYS
#include COMConst 
#include WWWFORM
	
#define Buttons			$$$WWW120PicturesAsButtons(YVOR)
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWFORMF("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		BUTTON STANDARD FORM
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 03-May-2010	GRF		-: Old comments cleanup
	; 02-Mar-2009	GRF		Doco
	; 30-Nov-2005	JW		SR13195: Rewrote a lot of code (see OBSWWWFORMF)
	;							to use braces / macros.
	; 15.07.1999	DT
	;-------------------------------------------------------------------------------
	do HEAD1
	quit
 
	;-------------------------------------------------------------------------------
	; Do NOT Display Standard Button   (WWW120 D94)
	;   1 - New								10 - First Record
	;   2 - Open							11 - Previous Record
	;   3 - Save							12 - Next Record
	;   4 - Internal Message Display		13 - Last Record
	;   5 - Delete							14 - Reminder
	;   6 - Help							15 - Copy
	;   7 - Back							16 - Export to Excel
	;   8 - Cancel							17 - Grid Layout
	;   9 - Search							
	;   
	; if '$find(strDontDisplay,#) do DisplayButton
	;-------------------------------------------------------------------------------
	
	
HEAD1
	;-------------------------------------------------------------------------------
	; Form Type 1 Standard - Display standard buttons
	; 
	; Called By : ^WWWFORMF, WWWFORM1 (for YFOART=1)
	; 
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Dec-2007	shobby  SRBR014601: Move DoNotDisplay call to routine WWWFORMX
    ; 12-Dec-2007	GM		SRBR014601: Call routine that checks characters in Form
    ; 							and Customisation
	; 19-Feb-2007	JW		SR15240: Cleaned up
	;-------------------------------------------------------------------------------
	new strDontDisplay,strStandardSubmit
	$$$LogR("HEAD1",YVOR)
	
	set strDontDisplay    = ","_$$$WWW120DoNOTDisplayStandardButto(YVOR)_","
 	set strStandardSubmit = $$$WWW120StandardSubmit(YVOR)
	
	set YTARGETF = $$$WWW120TargetNameForOutput(YVOR)
	if $$$Buttons write $$Delimiter^WWWFORMCOMMON()
	
	;WENN STANDARD SUBMIT ;when 
	if strStandardSubmit'="" do  quit  ; NUR DIESEN ;only 
	. set $$$Buttons = $$$NO           ; As text rather than as images
	. if '$find(strDontDisplay,",3,") do OK
	. if '$find(strDontDisplay,",5,") if $piece(strStandardSubmit,",",3)'="" do DELETE
	. if '$find(strDontDisplay,",1,") if $piece(strStandardSubmit,",",2)'="" do
	. . if $$$WWW120ClassUsedInForm(YVOR)'="" do NEW quit  ;NEUANLAGE
	. . ;
	. . new YNEU
	. . set YNEU = $$^WWWTEXT($piece(strStandardSubmit,",",2))
	. . if YNEU="" set YNEU="NEW"
	. . write "<TD>"
	. . write "<INPUT TYPE=""RESET"" VALUE="""_$$^WWWUML(YNEU)_""""
	. . if $$$WWW120StylesheetCSSFile(YVOR)'="" write " class=""button"""
	. . write ">"   ;NEU ;recent 
	. . write YCR,"</TD>"
	
	
	
	if '$find(strDontDisplay,",1,") if +$get(YTIMEFORM)=0 do NEW
	if '$find(strDontDisplay,",2,") if +$get(YTIMEFORM)=0 do OPEN
	if '$find(strDontDisplay,",3,") do SAVE
	do TEXT
	if '$find(strDontDisplay,",5,") do DELETE
	if '$find(strDontDisplay,",6,") if +$get(YTIMEFORM)=0 do HELP
	if '$find(strDontDisplay,",7,") do BACK
	if '$find(strDontDisplay,",8,") do END
	if $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumCreateOnly do  quit  ; "Create Only"   ;NUR NEUERFASSUNG ERLAUBT ;only permissive 
	. if '$find(strDontDisplay,",14,") if +$get(YTIMEFORM)=0 do WV
	
	if +$get(YTIMEFORM)=$$$NO {
		if '$find(strDontDisplay,",9,")  do SEARCH
		if '$find(strDontDisplay,",10,") do Direction("rrev", "FIRST","PAGEL","&lt;&lt;",301)
		if '$find(strDontDisplay,",11,") do Direction("rev",  "BACK", "PAGEF","&lt;",    302)
		if '$find(strDontDisplay,",12,") do Direction("for",  "NEXT", "PAGEN","&gt;",    303)
		if '$find(strDontDisplay,",13,") do Direction("ffor", "LAST", "PAGEE","&gt;&gt;",304)
	}
	 
	quit
	
	
HEAD3
	;-------------------------------------------------------------------------------
	; Form Type 3 GRID - STANDARD BUTTONS
	;
	; History
	; 09-Nov-2010	GRF		SR17243: use strDontDisplay to get list once
	;-------------------------------------------------------------------------------
	new strDontDisplay
	
	$$$LogR("HEAD3",YVOR)
	
	set strDontDisplay = ","_$$$WWW120DoNOTDisplayStandardButto(YVOR)_","
	
	if $$$Buttons write $$Delimiter^WWWFORMCOMMON()
	if '$find(strDontDisplay,",1,") if +$get(YTIMEFORM)=0 do NEW
	if '$find(strDontDisplay,",2,") if +$get(YTIMEFORM)=0 do OPEN
	if '$find(strDontDisplay,",3,") {
		if $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly {
			do SAVE  ;DISABLED (READONLY)
		} else {
			do OK1
		}
	}
	
	if '$find(strDontDisplay,",4,") do TEXT
	if '$find(strDontDisplay,",5,") do DELETE
	if '$find(strDontDisplay,",6,") if +$get(YTIMEFORM)=0 do HELP
	if '$find(strDontDisplay,",7,") do BACK
	if '$find(strDontDisplay,",8,") do END
	
	if $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumCreateOnly do  quit  ;NUR NEUERFASSUNG ERLAUBT ;only permissive 
	
	if '$find(strDontDisplay,",9,") do SEARCH
	quit
	
	
OK1	; BUTTON GRID
	new YOK
	
	if $data(^WWW122(0,YFORM)) || $data(^WWW131(0,YFORM)) || (YFOART=7) {
		$$$OpenTD($$$NO)
		if YFOART=8 write "<BR>"
		if '$$$Buttons {
			set YOK = "OK"
			if $piece($$$WWW120StandardSubmit(YVOR),",",1)'="" set YOK = $piece($$$WWW120StandardSubmit(YVOR),",",1)
			write "<INPUT TYPE=""SUBMIT"""
			if $$$WWW120StylesheetCSSFile(YVOR)'="" write " class=""button"""
			write """ onClick=""document.WWW.YOPEN.value=0;""" ; SPEICHERN ;Save 
			write " VALUE="""_YOK_""">" ; OK!
			
		} else {
			write "<A class=link onClick="""  ;SAVE AUF SAVENOW MIT DELAY;TYBD;5.09.2003 ;upon by means of 
			do SaveNow()
			write "return false;"
			write """>"
			do StopButton^WWWFORMCOMMON("OK","save.gif","BUTTON_SAVE")
			write "</A>"   ;NEU ;recent 
		}
		$$$CloseTD
	}
	quit
	
	
OK
	;-------------------------------------------------------------------------------
	; OK BUTTON
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 25-Jul-2006	JW		SR14799: Set YOPEN to 0 for save
	;-------------------------------------------------------------------------------
	new YOK
	
	if $data(^WWW122(0,YFORM)) || $data(^WWW131(0,YFORM)) || (YFOART=7) {
		$$$OpenTD($$$NO)
		if YFOART=8 write "<BR>"
		if '$$$Buttons {
			set YOK="OK"
			if $piece($$$WWW120StandardSubmit(YVOR),",",1)'="" set YOK=$$^WWWTEXT($piece($$$WWW120StandardSubmit(YVOR),",",1))
			write "<INPUT TYPE=""SUBMIT"""
			if $$$WWW120StylesheetCSSFile(YVOR)'="" write " class=""button"""
			write " onClick=""document.WWW.YOPEN.value=0;"""
			write " VALUE="""_YOK_""">"
		
		} else {
			write "<A class=link onClick="""  ;SAVE AUF SAVENOW MIT DELAY 
			write "document.WWW.target='"
			write $$$WWW120TargetNameForOutput(YVOR)  ;TARGET
			write "';"
			write "document.WWW.YBUTTON.value='';"
			write "window.focus();"
			write "SAVENOW();"
			write "return false;"
			write """>"
			do StopButton^WWWFORMCOMMON("OK","ok.gif","BUTTON_SAVE")
			write "</A>"
		}
		$$$CloseTD
	}
	quit
	
	
MENU
	quit
	
	
HBACK
	quit
	
	
NEW
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 30-Sep-2009	shobby	SR16913: Rework to prevent multiple clicks on a button
	;-------------------------------------------------------------------------------
	new YNEU,strWL
 	
	; New data record 
	if $$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumModifyOnly { 
		$$$OpenTD($$$NO)
				
		if '$$$Buttons {
			set YNEU = $$^WWWTEXT(130)
			if $piece($$$WWW120StandardSubmit(YVOR),",",2)'="" set YNEU=$$^WWWTEXT($piece($$$WWW120StandardSubmit(YVOR),",",2))
			write "<INPUT TYPE=""BUTTON"""
			if $$$WWW120StylesheetCSSFile(YVOR)'="" write " class=""button"""
			write " VALUE="""_$$^WWWUML(YNEU)_""""
			
		} else {
			write "<A class=link"
		}
		
		set strWL = YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM
		if $get(YSCREENM)=1 set strWL=strWL_"&amp;YSCREENM=1"
		
		if YFOART=3 {  ;WENN GRID, DANN NICHT ALLES NEU
			set strWL=strWL_"&amp;YNEW=1&amp;YSORT="_$get(YSORT)_"&amp;YORIENT="_$get(YORIENT)
			
			new YXKEY              ; FIXME : Different operation of NEW under brace format - subroutine call instead <GRF>
			set YXKEY=YKEY 
			new YKEY
			
			set YKEY=""
			set YMAXKEY=+$order(^WWW002(0,YDATEI,""),-1)
			if YMAXKEY=0 set YMAXKEY=1
			if $piece(YXKEY,",",YMAXKEY)'="" set YKEY=$piece(YXKEY,",",1,YMAXKEY-1)  ;LETZTEN KEY ABSCHNEIDEN ;KEY shear 
			if YKEY="" if $get(YKEY1)'=""    set YKEY=$piece(YKEY1,",",1,YMAXKEY-1)
			
		} else {	  ;NO GRID
			new YKEY              ; FIXME : Different operation of NEW under brace format - subroutine call instead <GRF>
			set YKEY=""
			if YFOART=1 {
				new YLFN
				for YLFN=1:1 {
					quit:+$$$WWW121FixedInputForHiddenField($get(^WWW121(0,YFORM,YLFN,1)))=0
					if $piece(YFKEY,",",YLFN)'="" set $piece(YKEY,",",YLFN)=$piece(YFKEY,",",YLFN)
				}
			}
		}
		set strWL = strWL_$$WWWCGI2^WWWCGI()
		write $$CreateWindowLocation^WWWBUTTON(strWL)
		write ">"
		
		do StopButton^WWWFORMCOMMON($$^WWWTEXT(130),"new.gif","BUTTON_NEW")
		if $$$Buttons write "</A>"
	}
	quit
	
	
OPEN
	;-------------------------------------------------------------------------------
	; Open data record
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 12-May-2006	JW		SR14508: Temporary exception for INWEINVEHeader
	;-------------------------------------------------------------------------------
	new strImg
	
	if '$$$Buttons {
		if (YBEARB'=4) && ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) {
			write "<TD>"
			write "<INPUT TYPE=""BUTTON"" VALUE="""_$$^WWWTEXT(101)_""" onClick="""		; "Open"
			do SaveNow(1)
			write """>"
			write YCR,"</TD>"
		}
		
	} else {	;wenn nicht gesperrt ;when not Disabled 
		if ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumCreateOnly) {   ;nur ERFASSEN ;solely Edit 
			$$$OpenTD($$$NO)
			write "<A class=link onClick="""
			
			do SaveNow(1)
			write "return false;"
			write """>"
			
			if YFORM="INWEINVEHeader" {
				set strImg = "reset"
				set YSTATUS = $$^WWWTEXT(32810)		; "Refresh"
			} else {
				set strImg  = "open"
				set YSTATUS = $$^WWWTEXT(101)		; "Open"
			}
			do StopButton^WWWFORMCOMMON(YSTATUS,strImg_".gif","BUTTON_OPEN",$$$YES,$$$YES)
			write "</A>"
		}
	}
	quit
	
	
TEXT
	;-------------------------------------------------------------------------------
	;TEXTBAUSTEIN ANZEIGE ;Show 
	;
	; History:
	; 19-Feb-2007	JW		SR15240: Don't allow "pressing" of this 'button'.
	; 11-Oct-2005	GRF		Doco (SR13195 research)
	;-------------------------------------------------------------------------------
	
	;+++++++++++++++++++++++++++++++++++++++
	; BEARBEITUNG System Parameter
	; ----------------------------
	; 1  New
	; 2  Save Changes?
	; 3  Saved
	; 4  Data Item In Use
	; 5  Wrong Data Input
	; 6  Open Or New
	; 7  Wrong Back Jump
	; 8  Read Only!!
	;+++++++++++++++++++++++++++++++++++++++
	
	; Existing key or  No MRU List
	;---------------------------------------
	new YBEA
	
	; FIXME : pass boolean for (strDontDisplay [ 4) to get list once <GRF>
	
	set YBEA=""
	if $$MenuType^WWW013()=14{
		write "<td style='padding:3px;border:0px;text-align:center;width:160px;' unselectable"
	}else{
		write "<td class=""coolButton"" unselectable style='width:160px;'"
	}
	
	if ('$$IsNewRecord^WWWFORMStatus())||(+$$$WWW120NumberofMRURecordListItem(YVOR)=0) {
		; Change "Saved" to "Save Changes?"
		if YBEARB=3 if $$$WWWUSERLastSave(^WWWUSER(0,YUSER,1))=9 set YBEA=$$^WWWTEXT(391) set YBEARB=2  ; "Not Saved!" ; Nicht gespeichert BEI LETZTER SPEICHERUNG 
		
		if $$$Buttons {                            ; AUSGESCHALTETE AKTIONSANZEIGE
			if '$find(","_$piece(YVOR,Y,94)_",",",4,") {
				write " width=138"
			} else {
				write " style=""display:none; visibility:hidden; overflow:hidden; width:0;"""
			}
		}
		write ">",YCR

		write "<INPUT NAME=""YBEARB"""
		
		; "Data Item in Use"
		if YBEARB=4 write YCR_" TITLE="""_$$GETLOCKDETAIL($get(YDATEI),$get(YKEY))_""""
		write YCR
		                                                    ; AUSGESCHALTETE AKTIONSANZEIGE
		if $find(","_$piece(YVOR,Y,94)_",",",4,") {
			write " TYPE=""HIDDEN"""
		} else {
			write " class=""inputCab"" TYPE=""TEXT"" SIZE=""21"" MAXLENGTH=""20"""
		}
		write YCR," onFocus='blur()';"
		write YCR
		write " style='width:150px;"
		if $piece(YVOR,Y,5)'="" write "background-color:"_$piece($get(^WWW100(0,"FARBE",SPRACHE,$piece(YVOR,Y,5),1)),Y,1)
		write "'"
		write YCR,">",YCR
		
	} else {
		; MRU List : "Open" plus list
		;---------------------------------------
		if $$$Buttons write " width=138"
		write ">",YCR
		write "<INPUT NAME=""YBEARB"" TYPE=""HIDDEN"">"
		if +$$$WWW120NumberofMRURecordListItem(YVOR)'=0 do ^WWWFORM91  ;MULTISCHLUESSELVORGABE   ; TODO : Extra text not required
	}
	
	$$$StartScript()
	do UpdateStatus^WWWFORMStatus(YFORM,YBEARB,$$$YES,YBEA)
	$$$EndScript()
	
	$$$CloseTD
	
	quit
	
 
GETLOCKDETAIL(pidClass="",pstrKey="")
	;-------------------------------------------------------------------------------
	; For a class and key return who locked it and when.
	;
	; Returns:String
	;
	; History:
	; 03-May-2010	GRF		SR15961: use dteToday for efficiency
	; 28-Jun-2005	shobby	SR12538: Language Text.  Enabled this existing routine
	; 09-Jun-2005	Paul K	Created
	;-------------------------------------------------------------------------------
	new dteToday,idLock,idSession,tmeLock,idUser,strText
 
	set strText=""
	if (pidClass'="") {
		set idLock="^"_pidClass_"/"_$select($$$WWW001SharedFile($get(^WWW001(0,pidClass,1))):0,1:YM)_"."_$translate(pstrKey,",",".")_".1/"
		set dteToday = +$horolog
		if $data(^WWW006(0,dteToday,idLock,1)) {
			set idSession = $piece($get(^WWW006(0,dteToday,idLock,1)),Y,1)
			set tmeLock   = $piece($get(^WWW006(0,dteToday,idLock,1)),Y,2)
			if idSession'="" {
				set idUser = $piece($get(^WWWUSER(0,idSession,1)),Y,2)
				if idUser'="" {   ; "Locked By"  ;"at"
					set strText = $$$Text("WWW00023")_" : "_$$$WWW013Name($get(^WWW013(0,idUser,1)))_" "_$$$Text("WWW00024")_" : "_$$^WWWTIME(tmeLock)
				}
			}
		}
	}
	quit strText
	
	
WV
	;-------------------------------------------------------------------------------
	;  Reminder
	;-------------------------------------------------------------------------------
	new TAGE,TEXT
	
	if (YKEY'="") && (YBEARB'=4) && (YBEARB'=1) && (YBEARB'=6) {
		$$$OpenTD($$$NO)
		set YSTATUS = $$^WWWTEXT(149)   ; "Reminder"
		if $$$Buttons {
			set TAGE=+$$$WWW120ReminderWithinHowManyDays(YVOR)
			if TAGE=0 set TAGE=1
			set TEXT=$$$WWW120ReminderText(YVOR)
			
			write "<A class=link onClick="""     ; "Reminder On: (You may add text after the date)"
			write "pvalue = prompt('"_$$^WWWTEXT(149,,1)_" "_$$^WWWTEXT(293,,1)_": "_$$^WWWTEXT(325,,1)_"', '"_$$^WWWDATE($horolog+TAGE)_" "_TEXT_"');if (pvalue == null) alert('"_$$^WWWTEXT(324)_" "_$$^WWWTEXT(149)_"'); else {alert('"_$$^WWWTEXT(149)_" "_$$^WWWTEXT(293)_" ' + pvalue); SAVENOW('SAVEWV'+pvalue);}"
			write "return false;"
			write """>"
		}
		do StopButton^WWWFORMCOMMON(YSTATUS,"wv.gif","BUTTON_REMIND",$$$YES,$$$YES)
		if $$$Buttons write "</A>"
		; FIXME : Should there be a CloseTD?
	}
	quit
	
	
BACK
	;-------------------------------------------------------------------------------
	; Back button
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 13-Mar-2014	shobby	CORE-202: Fixed problem with Back button when returning to a COMView Search.
	; 10-Nov-2010	shobby	SR17580: If this form was loaded from a search back button
	;							will return to that search.
	; 12-Mar-2008	shobby	SRBR014897: 'Back' support YFKEYTS not necessary after
	; 							other changes under this SR.
	; 09-Oct-2007	Karine	BR014648: Check if back button is passing correct keys 
	; 19-Feb-2007	JW		SR15240: Disable disabled button.
	;-------------------------------------------------------------------------------
	new YBACK1,YBACK2,YFKEY1,YYALT,YSEITE,YINSEITE

	if $translate(YBACK,",")="" {
		if ($$$WWW012TargetFrameName(YVOR1)'="") {
			if $$$Buttons {               ;KEINE FUNKTION ;no 
				$$$OpenTD($$$YES)
				do StopButton^WWWFORMCOMMON(""]]><![CDATA[,"hbackd.gif","BUTTON_BACK",$$$NO,,0)
			}
		} else {
			$$$OpenTD($$$NO)
			if YMENU'=4 {
				if '$$$Buttons {
					write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(99))_""" onClick=""window.location='"
				} else {
					write "<A class=link onClick="" window.location'"
				}
				write YAKTION_"EP=WWWMENU"   ;ENDE ;termination 
				do ^WWWCGI
				write "'"
				write """>"
				
			} elseIF $$$Buttons {
				write "<A onClick='return doLink(this)' HREF='JavaScript:window.focus();' TARGET='MENUE'>"
			}
			do StopButton^WWWFORMCOMMON($$^WWWTEXT(99),"hback.gif","BUTTON_BACK")
		}
		
	} else {
		set YBACK1 = $piece(YBACK,",",$length(YBACK,",")-1)
		set YBACK2 = $piece(YBACK,",",1,$length(YBACK,",")-2)_","
		set YFKEY1 = $piece(YFKEY,",",1,$length(YFKEY,",")-2)
		;$$$OpenTD($$$NO)
		$$$OpenTD($$$NO)
		
		set YFKEYTS = $translate(YFKEY,"# "_"""","~|")
		
		if '$$$Buttons {
			write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(99))_""" onClick=""window.location='"		; Back
		
		} else {
			;WRITE "<A HREF="""
			write "<A class=link onClick="" window.location='"
		}
		
		new YBACK,YFKEY              ; FIXME : Different operation of NEW under brace format - subroutine call instead <GRF>
		
		if (YBACK1="WWWSEAR")||(YBACK1="COMViewSearch") {  ;SR17580
			write YAKTION_"EP=WWWSEAR&amp;YFORM="_YFORM_"&amp;YLOCKKILL=1"
		
			if $extract(YOPEN,1,4)="SAVE" write "&amp;YOPEN=OLD"
			do ^WWWCGI

			set YYALT=""
			write "'"
			write """>"
			set YYALT = $$^WWWTEXT(99)   ; "Back"
		
		} else {
			write YAKTION_"EP=WWWFORM&amp;YFORM="_YBACK1
		
			if $extract(YOPEN,1,4)="SAVE" write "&amp;YOPEN=OLD"
			write "&amp;YRETURN=1"
			set YBACK = YBACK2
			set YFKEY = YFKEY1
			do ^WWWCGI
		
			write "'"
			write """>"
			set YYALT = ""
			if YBACK1'=""                 set YYALT = $piece($get(^WWW1201(0,YBACK1,SPRACHE,1)),Y,1)
			if (YYALT="") && (YBACK1'="") set YYALT = $piece($get(^WWW120(0,YBACK1,1)),Y,1)
			if YYALT=""                   set YYALT = $$^WWWTEXT(99)   ; "Back"
		}
		do StopButton^WWWFORMCOMMON(YYALT,"hback.gif","BUTTON_BACK")
		$$$CloseTD
	}
	quit
	
	
END ;  Cancel
 
	$$$OpenTD($$$NO)
	if YMENU=4 {
		if '$$$Buttons {
			write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(10))_""" onClick=""window.blur();"
		} else {
			write "<A onClick='return doLink(this)' HREF='JavaScript:window.focus();' TARGET=""MENUE"_$$$GetTopUser(YUSER)
		}
		
	} else {	;Cancel without window 
		if '$$$Buttons {
			write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(10))_""" onClick=""window.location='"
		} else {
			write "<A class=link onClick="" window.location='"
		}
		if $$$WWW012TargetFrameName(YVOR1)="" {
			write YAKTION_"EP=WWWMENU"   ;ENDE ;termination 
			do ^WWWCGI
			
		} else {
			write YGIF_"blank.htm" 
		}
		write "'"
	}
	write """>"
	do StopButton^WWWFORMCOMMON($$^WWWTEXT(10),"end.gif","BUTTON_END")
	if $$$Buttons write "</A>"
	$$$CloseTD
	
	quit
	
	
HELP
	if '$$HideHelpButtons^WWW012() { ;SESDF-715
		if YFORM'="WWW127" {
			do HelpButton($$$YES,YFORM,20)	
		} elseif ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumCreateOnly) && $data(FORMULAR) {
			do HelpButton($$$NO,FORMULAR,99)
		}
	}
	quit
 
 
HelpButton(pblnPopup,pstrForm,pstrText)	private
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 21-Dec-2006	JW		SR14235: Rewrote. Added reference to child window
	;-------------------------------------------------------------------------------
	new strURL
	
	set pstrText = $$^WWWTEXT(pstrText)
	set strURL = YAKTION_"EP=WWWHELP&amp;YFORM="_pstrForm_$$WWWCGI2^WWWCGI(pblnPopup)_"&amp;YSEITE="_YSEITE
	
	$$$OpenTD($$$NO)
	
	if '$$$Buttons {
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML(pstrText)_""""
	} else {
		write "<A class=link"
	}
	
	if pblnPopup {
		write " onClick=""subWindow('"_strURL_"','HELP"_YTARGET_"');"">"
	} else {
		write " onClick=""window.location='"_strURL_"'"">"
	}
	do StopButton^WWWFORMCOMMON(pstrText,"help.gif","BUTTON_HELP",$$$YES,$$$NO,"cursor:help")
	if $$$Buttons write "</A>"
	$$$CloseTD
	quit
	
	
SEARCH
	;-------------------------------------------------------------------------------
	; "Search"
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 22-Oct-2010	shobby	SR17540: Removed HREF=""#"" from the anchor link.
	; 14-Oct-2009	GRF		SR16938: Condense to single write statement
	; 14-Oct-2009	shobby	SR16938: Changed the redirection mechanism.
	;-------------------------------------------------------------------------------
	
	$$$OpenTD($$$NO)
	if '$$$Buttons {    ; "Search"
		write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(148))_""" onClick=""ShowSearch();"">"
	
	} else {
		write "<A onClick=""ShowSearch();"">"  ;SR17540
	}
	
	do StopButton^WWWFORMCOMMON($$^WWWTEXT(148),"search.gif","BUTTON_SEARCH")
	if $$$Buttons write "</A>"
	$$$CloseTD
	quit
 
	
SaveNow(pYOPEN=0)
	write "document.WWW.target='';"
	write "document.WWW.YBUTTON.value='';"
	write "document.WWW.YRICHT1.value='';"
	write "SAVENOW("_pYOPEN_");"
	quit
	
	
SAVE
	;-------------------------------------------------------------------------------
	; Save button
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Feb-2007	JW		SR15240: Disable disabled button.
	;-------------------------------------------------------------------------------
	new strText
	
	if (YBEARB=4) || ($$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly) {	; "IN USE"
		if $$$Buttons {
			$$$OpenTD($$$YES)
			do StopButton^WWWFORMCOMMON("","saved.gif","BUTTONNOSAVE")
		}
		
	} elseif '$$$Buttons {
			write "<TD WIDTH=10>"
			write "<INPUT TYPE=""SUBMIT"" VALUE="""
			write $case($$$WWW120AuthorizationToModifyData(YVOR)<4,$$$YES:$$^WWWUML($$^WWWTEXT(165)),$$$NO:"OK")    ; "Save"
			write """ onClick=""document.WWW.YOPEN.value=0;"">" ; SPEICHERN ;Save 
			write YCR,"</TD>"
			
	} else {	// Save + button
		set strText = $case($$$WWW120AuthorizationToModifyData(YVOR)<4,$$$YES:165,$$$NO:"Com00183")
		
		if $$$WWW120FastSave(YVOR) {
			do FastSave("save","","SAVE",strText)
		
		} else {
			$$$OpenTD($$$NO)
			write "<A class=link onClick="""
			do SaveNow()
			write "return false;"
			write """>"
			do StopButton^WWWFORMCOMMON($$^WWWUML($$^WWWTEXT(strText)),"save.gif","BUTTON_SAVE")
			write "</A>",YCR
			$$$CloseTD
		}
	}
	quit
	
	
Direction(pstrImg,pstrEvent,pstrButton,pstrPic,pstrText) 
	;-------------------------------------------------------------------------------
	; Navigational buttons
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 29-Sep-2009	shobby	SR16913: tweaked to support locking the form during operation
	; 30-Nov-2005	JW		SR13195: Created
	;-------------------------------------------------------------------------------
	if $$$WWW120FastSave(YVOR) {
		do FastSave(pstrImg,pstrEvent,pstrButton,pstrText)
		
	} else {
		$$$OpenTD($$$NO)
		if '$$$Buttons {
			write "<INPUT TYPE=""BUTTON"" VALUE="""_pstrPic_""""
		} else {
			write "<A class=link"
		}
		write $$CreateWindowLocation^WWWBUTTON($$DirectionURL2(pstrEvent))
		write ">"
		do StopButton^WWWFORMCOMMON($$^WWWUML($$^WWWTEXT(pstrText)),pstrImg_".gif","BUTTON_"_pstrButton)
		if $$$Buttons write "</A>"
		$$$CloseTD
	}
	quit
	
	
DirectionURL(pstrEvent)
	;-------------------------------------------------------------------------------
	; Called By : WWWFORMF and WWWFORM8
	; 
	; History:
	; 29-Sep-2009	shobby		SR16913: Redirect to DirectionURL2
	;-------------------------------------------------------------------------------
	write $$DirectionURL2(pstrEvent)
	quit
	
	
DirectionURL2(pstrEvent)
	;-------------------------------------------------------------------------------
	; 
	; History:
	; 29-Sep-2009	shobby		SR16913:
	;-------------------------------------------------------------------------------
	quit YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YRICHT="_pstrEvent_"&amp;YBUTTON="_$$WWWCGI2^WWWCGI()
	
	
FastSave(pstrImg,pstrEvent,pstrButton,pstrText)
	if (YBEARB'=4) && ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly) && $$$Buttons {
		write YCR,"<TD WIDTH=10 class=""coolButton"">"
		
		write YCR,"<IMG SRC="""_YGIF_pstrImg_".gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(pstrText)_""" border=0 "
		write "onClick=""retval=EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX','Y"_YFORM_"','"_pstrEvent_"','9'); BEARB('"_$get(^WWW100(0,"BEARBEITUNG",SPRACHE,3,1))_"');"""
		write " id=""BUTTON_"_pstrButton_""">"
		
		write YCR,"</TD>"
	}
	quit
	
	
AllKeys(pidForm,pstrKey)
	;-------------------------------------------------------------------------------
	; Do we have all the required keys for this form?
	;
	; Params:	pidForm		: The form to check
	; 			pstrKey		: The key we're using
	;
	; ByRefs:
	;
	; Returns:  blnKeys		: Whether we have all the keys or not
	;
	; History:
	; 17-Oct-2006	SteveS	SR15086: Created
	;-------------------------------------------------------------------------------
	new blnKeys,intNumKeys
 
	set blnKeys = $$$YES
	
	set intNumKeys = +$order(^WWW121(0,pidForm,""),-1)
	if (intNumKeys>0) set blnKeys = $$GetKey^COMUtilClass(pstrKey,intNumKeys)
 
	quit blnKeys
	
	
DELETE
	;-------------------------------------------------------------------------------
	; Delete button - available when there is a data record
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Feb-2007	JW		SR15240: Disable disabled button.
	; 14-Dec-2006	JW		BR014285: Allow for error message
	; 17-Oct-2006	SteveS	SR15086: Don't check YKEY for null, use $$AllKeys.
	; 25-Jul-2006	JW		SR14799: Added double quote. Check before overriding with ""
	;-------------------------------------------------------------------------------
	new YDISDEL,Q,YQ,strExec,YKILL,YKEY1,YDEL,idFormRedirect,objWWW120,idFormRedirectKey
	new strTooltip
	
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; YDISDEL  : Disable Delete Flag - can't delete when set to $$$YES
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
	set YDISDEL    = $$$NO
	set strTooltip = $listbuild(45)   ; "Not Deletable"
	
	set strExec = $$$WWW120ExecuteBeforeDeletePossib(YVOR)     ;   *** EXECUTE ***
	if strExec'="" {
		set Q = 0,YQ = 0
		xecute strExec
		if (+YQ=1) || (+Q=1) {
			set YDISDEL = $$$YES                    ;NICHT LÖSCHEN ;Not Delete 
			if $extract(Q,2,999)'="" {              ; Allow for error message
				set strTooltip = $extract(Q,2,999)
			}
		}
	}
	
	set YKEY1 = YKEY
	
	if (YDISDEL=$$$NO) &&
	   (($$$WWW120AuthorizationToModifyData(YVOR)<2) || ($$$WWW120AuthorizationToModifyData(YVOR)=4)) &&
	   $$AllKeys(YFORM,YKEY)      {   ;LÖSCHEN ;Delete 
		
		$$$OpenTD($$$NO)
		if '$$$Buttons {
			set YDEL = $$^WWWTEXT(41)  ; "Delete"
			if $piece($$$WWW120StandardSubmit(YVOR),",",3)'="" set YDEL = $$^WWWTEXT($piece($$$WWW120StandardSubmit(YVOR),",",3))
			
			write "<INPUT TYPE=""BUTTON"""
			if $$$WWW120StylesheetCSSFile(YVOR)'="" write " class=""button"""
			write " VALUE="""_YAM_$$^WWWUML(YDEL)_""""
		} else {
			write "<A class=link"
		}
		
		write " onClick=""if (confirm('"_$$^WWWTEXT(321)_"')) { BEARB('',1); window.location='"
		
		if $get(YTIMEFORM)'=1 {
			write YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"&amp;YRICHT=NEXT"
		} else {
			write YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM_"t&amp;YRICHT=NEXT"
			write "&amp;YTIMEFORM=1"
		}
		
		if YFOART=3 write "&amp;YSORT="_$get(YSORT)_"&amp;YORIENT="_$get(YORIENT)   ;WENN GRID  DANN SORT 
		set YKILL=1
		do ^WWWCGI
		write "'; }"">"
		do StopButton^WWWFORMCOMMON($$^WWWTEXT(41),"del.gif","BUTTON_DELETE",,,)
		$$$CloseTD
	
	} elseif $$$Buttons {
		$$$OpenTD($$$YES)
		do StopButton^WWWFORMCOMMON($$$HTMLText($$$Text(strTooltip)),"disdel.gif","BUTTON_NODELETE",$$$NO,,0)
		$$$CloseTD
	}
	quit
	
	
NEWREC  ;BEARBEITUNGSTABELLE: NEUEN DATENSATZ ANLEGEN
	do   ;UNDO
	. new YKILL,YKEY1
	. $$$OpenTD($$$NO)
	. if '$$$Buttons write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(33946))_""""   ; "Create New Data Record"
	. if $$$Buttons  write "<A HREF=""#"""
	. write " onClick='javascript:pruef(45);document.WWW.YOPEN.value=""""'>"
	. if '$$$Buttons write """>"
	. if $$$Buttons
	. . do StopButton^WWWFORMCOMMON($$^WWWTEXT(33946),"new.gif","BUTTON_NEW")
	. . write "</A>"
	. write                  ; FIXME : missing?  unnecessary?
	. $$$CloseTD
	
	quit
	
	
SAVREC  ;BEARBEITUNGSTABELLE: NEUEN DATENSATZ SPEICHERN
	do
	. new YKILL,YKEY1
	. $$$OpenTD($$$NO)
	. if '$$$Buttons write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(11))_""""   ; "Save Data"
	. if $$$Buttons  write "<A HREF=""#"""
	. write " onClick='if (yaddline ==1) {pruef(45);} else {alert("""_$$^WWWTEXT(34099)_""");}'>"  ; "No New Data Record Has Been Saved" ;KEIN NEUER DATENSATZ ANGELEGT
	. if $$$Buttons write YCR,"<IMG SRC="""_YGIF_"save.gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(11)_""" border=0 id=""BUTTON_SAVE""></A>"  ;SPEICHERN ;Save 
	. $$$CloseTD
	
	quit
	
	
DELREC  ;BEARBEITUNGSTABELLE: DATENSATZ LÖSCHEN
	do 
	. new YKILL,YKEY1
	. $$$OpenTD($$$NO)
	. if '$$$Buttons write "<INPUT TYPE=""BUTTON"" VALUE="""_YAM_$$^WWWUML($$^WWWTEXT(122))_""""  ; "Delete"
	. if $$$Buttons  write "<A HREF=""#"""
	. write " onClick='JavaScript:pruef(46);'>"
	. if $$$Buttons write YCR,"<IMG SRC="""_YGIF_"del.gif"" "_YHEIGHT_" "_YWIDTH_" TITLE="""_$$^WWWTEXT(122)_""" border=0 id=""BUTTON_DELETE""></A>"  ;LÖSCHEN ;Delete 
	. $$$CloseTD
	
	quit
	
RESET ;RESET
	write YCR
	if YFOART'=5 do
	. $$$OpenTD($$$NO)
	. if '$$$Buttons do
	. . write "<INPUT TYPE=""RESET"" VALUE="""_$$^WWWUML($$^WWWTEXT(32810))_""">"   ; "Refresh"   ; AKTUALISIEREN
	. ;
	. if $$$Buttons do
	. . write "<A class=link onClick="" window.location='"
	. . new YKEY
	. . write YAKTION_"EP=WWWFORM&amp;YFORM="_YFORM
	. . set YKEY=""
	. . do ^WWWCGI
	. . write "'"">"
	. . if $find($piece(YVOR,Y,16),"SAVE") do StopButton^WWWFORMCOMMON($$^WWWTEXT(130),"new.gif","BUTTON_NEW")
	. . if '$find($piece(YVOR,Y,16),"SAVE") do StopButton^WWWFORMCOMMON($$^WWWTEXT(32810),"reset.gif","BUTTON_RESET")
	. . write "</A>"
	. $$$CloseTD
	
	quit
	
	
STAT ;STATUS
	write $$STATEX() ;SR18033
	quit

STATEX() ;SR18033
	new HTML
	
	set HTML=""
	set HTML=HTML_" onMouseover='window.status="""_YSTATUS_""";'"
	set HTML=HTML_" onMouseout='window.status=""""'"
	quit HTML
	
MANUAL
	
	d
	. write "<A HREF=""#"" onClick="""
	. write "window.open('"
	. do ##class(COM.fwk.ui.csp.CSPUtil).%New().cspURL("VARManual.csp")
	. write "','Manual','width=800,height=800');"
	. write """>"
	. write YCR
	. set YSTATUS="copy"
	. do StopButton^WWWFORMCOMMON($$$SysEnum("BUTTON",99),"manual.gif","Manual",$$$YES,$$$YES)
	. write "</A>"
	
	write YCR
	quit       
		
	
	
	
COPY
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 13-Oct-2006	SteveS	SRBR014172: Use $$$SysEnum macro
	; 12-Oct-2006	HEBER	SRBR014172: Copy button title translated
	;-------------------------------------------------------------------------------
	if YKEY'="" if $get(YFELD)'="" if YBEARB'=4 if $$$Buttons do
	. new YLFN1
	. quit:$$$WWW120AuthorizationToModifyData(YVOR)>3     ; i.e. quit if = $$$EnumModifyOnly or $$$EnumReadOnly
	. $$$OpenTD($$$NO)
	. write "<A HREF=""#"" onClick="""
	. for YLFN1=1:1 quit:'$data(^WWW121(0,YFORM,YLFN1))  do
	. . write "document."_YHTMFORM_".Y"_YFORM_"P"_YLFN1_".style.background='white';"
	. . if $order(^WWW121(0,YFORM,YLFN1))="" do
	. . . if $piece($get(^WWW002(0,YFORM,YLFN1,1)),Y,3)=9 do  quit
	. . . . ;IF $GET(YHTMFORM)'="WWW2" 
	. . . . write "document."_YHTMFORM_".Y"_YFORM_"P"_YLFN1_".value='+';"
	. . . . write "retval = EventValue('"_YUCI_"','"_YUSER_"','"_YFORM_"','FIX"_YKEY_"','Y"_YFORM_"P"_YLFN1_"','+','2','');"
	. . . ;
	. . . if YFORM'="WWW004" write "document."_YHTMFORM_".Y"_YFORM_"P"_YLFN1_".value='';" quit
	. . ;
	. . write "document."_YHTMFORM_".Y"_YFORM_"P"_YLFN1_".readOnly=false;"
	. ;
	. write "window.alert('"_$$^WWWTEXT(39)_"');"  ; "Copy Data Records"
	. if $$$Buttons write "return false;"
	. write """>"
	. write YCR
	. set YSTATUS="copy"
	. do StopButton^WWWFORMCOMMON($$$SysEnum("BUTTON",15),"copy.gif","BUTTON_COPY",,$$$YES)
	. write "</A>"
	
	write YCR
	quit       
	
	
TM ;ZEITABHÄNGIGE ERFASSUNG ;logging 
	$$$OpenTD($$$NO)
	do
	. new YBACK1
	. set YBACK1 = YBACK
	. new YBACK
	. set YBACK = YBACK1
	. ;
	. if '$$$Buttons write "<INPUT TYPE=""BUTTON"" VALUE=]]><![CDATA["""_YAM_$$^WWWTEXT(389)_""" onCli]]><![CDATA[ck=""window.location='"   ; "Changes By Date"
	. if $$$Buttons  write "<A HREF=""" 
	. write YAKTION_""
	. write "EP=WWWFORM&amp;YFORM="_YFORM_"t"
	. set YBACK=YBACK_YFORM_","
	. do ^WWWCGI
	. if '$$$Buttons write "'"
	. write """>"
	. do StopButton^WWWFORMCOMMON($$^WWWTEXT(389),"time1.gif","BUTTON_TIME")
	. write "</A>"
	  
	$$$CloseTD
	quit]]></Routine>
</Export>