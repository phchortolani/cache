<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31ExtraFields" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include COMConst
#include COMSYSString
	
COMGridEdit31ExtraFields(YINHALT,YLFDAT)
	new strField,strHTML

	if YINHALT="FIX" {
		new YFORM 
		set YFORM    = $piece(YLFDAT,Y,1)
		set strField = $piece(YLFDAT,Y,2)
		do Create(YFORM,YBED,strField)
	} elseif YINHALT="MOVE"       { do Change(YINHALT,YLFDAT)
	} elseif YINHALT="FONT"       { do Change(YINHALT,YLFDAT)
	} elseif YINHALT="SAVE"  	  { do Save(YINHALT,YLFDAT)
	} elseif YINHALT="CUSTOMISE"  { do Save(YINHALT,YLFDAT)
	} elseif YINHALT="CLOSE"      { do Close(YINHALT,YLFDAT)
	}
	quit 1
	
	
InnerHTML(YFORM,pstrField,pintFontSize)
	;-------------------------------------------------------------------------------
	; Build the layout of the screen
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 19-Jan-2011	GRF		-: set strHeadLeftColour and use once; use shorter obj
	;							variable names for clarity
	; 26-May-2010	shobby	SR17336: Don't highlight text behind dragged panel in Firefox.
	; 17-Dec-2009	shobby	SR16969: Created
	;-------------------------------------------------------------------------------
	;  <div id="cgePopupStatus"></div>
	;  <IMG id="cgePopupFontDown">
	;  <IMG id="cgePopupFontUp">
	;  <IMG id="cgePopupClose">
	;  
	;  <TABLE id="cgePopupHeader">Form Name</div>
	;     <TR><TD>Field Name</TD>
	;         <TD><div class='DIVtxt' id='tdY12_34dt'>(CurrentEnum) StrValue</div></TD>
	;         <TD><INPUT type="checkbox" id="cgePopupColVisible_1"></TD>
	;     </TR>
	;     <TR><TD>Field Name</TD>
	;         <TD><div class='DIVtxt' id='tdY12_56dt'>CurrentValue</div></TD>
	;         <TD><INPUT type="checkbox" id="cgePopupColVisible_2"></TD>
	;     </TR>
	;       ...
	;  </TABLE>
	;  <INPUT id="cgePopupButtonCustomise">
	;  <INPUT id="cgePopupButtonSave">
	;  <INPUT id="cgePopupButtonCancel">
	;  <div id="cgePopupStatus"></div>
	;---------------------------------------
	new idClass,idField,intRow,objForm,objWWW122,objObject,strHeadLeftColour,strHTML
	new GCOL,GROW,PARA,REL,RELF,SUCH
	new YCOLOFF,YCOLON,YFELDNAME,YFIELDNAME,YFONT,YFONTSIZE,YHEIGHT
	new YLENGTH,YLFN,YPADDING,YTEXT,YTEXT1,YTXT,YTYP,YVALIGN,YWIDTH
	
	;set GCOL = $piece(pstrField,"_",2)
	;set GROW = $piece($piece(pstrField,"_",1),"Y",2)
	
	set strHTML    = ""
	set YTXT       = ""
	set intRow     = $piece($piece(pstrField,"tdY",2),"_",1)
	set YFIELDNAME = $piece(pstrField,"td",2)
	
	set strHeadLeftColour = $$$SysEnum("FARBE",$$$WWW012ColorCodeForHeaderLeft($get(^WWW012(0,0,1))))
	
	set objForm = $get(^WWW120(0,YFORM,1))
	set idClass = $$$WWW120ClassUsedInForm(objForm)
	;et strHTML = strHTML_"<IMG id=""cgePopupClose"" src="""_YGIF_"close.gif"" align=right top=1 valign=absmiddle position=absolute style=""background-color:"_strHeadLeftColour_";"" >"
	set strHTML = strHTML_"<div id=""cgePopupHeader"" style=""-moz-user-select:none; color:white; background-color:"_strHeadLeftColour_";position:relative;width:100%;height:15px;border:1px solid; "">"  ;SR17336
	set strHTML = strHTML_$$^WWWFORMNAME(YFORM)
	set strHTML = strHTML_"</div>"
	set strHTML = strHTML_"<IMG id=""cgePopupFontDown"" src="""_YGIF_"FontDown.gif"" style=""align:right; left:311px; top:2px; position:absolute; background-color:"_strHeadLeftColour_";"" >"
	set strHTML = strHTML_"<IMG id=""cgePopupFontUp"" src="""_YGIF_"FontUp.gif"" style=""align:right; left:323px; top:2px; position:absolute; background-color:"_strHeadLeftColour_";"" >"
	set strHTML = strHTML_"<IMG id=""cgePopupClose"" src="""_YGIF_"close.gif"" style=""align:right; left:335px; top:2px; position:absolute; background-color:"_strHeadLeftColour_";"" >"

	set strHTML = strHTML_$$LayoutSelector()     ; <SELECT>...</SELECT>
	set strHTML = strHTML_"<TABLE id=""cgePopupTable"" offsetTop=0px style=""font-size:"_pintFontSize_"pt; width:100%;"">"
	
	set idField=""
	for {
		set idField = $order(^WWW122(0,YFORM,idField))
		quit:idField=""
		
		set objWWW122 = $$Get^WWW122(YFORM,idField)
		set strHTML   = strHTML_"<TR style=""height:auto; "">"
		set strHTML   = strHTML_"<TD style=""border:1px solid; margin:0px; width:50%; padding-top:0px; padding-bottom:0px;"">"
		set strHTML   = strHTML_$$$WWW122ManualCaption(objWWW122)
		set strHTML   = strHTML_"</TD>"
		set strHTML   = strHTML_"<TD style=""border:1px solid; width:50%; padding-top:0px; padding-bottom:0px;"">"
		set YTEXT     = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,"V","Y"_intRow_"_"_$$$WWW122SequenceNumber(objWWW122),1))
		
		;do PARA("D",YMAXKEY,.YWIDTH,.YVALIGN,.YHEIGHT,.YFONT,.YFONTSIZE,.YTYP,.YFORM,.YLFN,.YTXT,.YDAT)  ;SET PARAMETER

		set YDAT = ""
		do PARA^COMGridEdit31Body("D",1,"","","",.YFONT,"",.YTYP,.YFORM,.YLFN,.YTXT,.YDAT)  ;SET PARAMETER  ; FIXME : what if no D1?
		
		; FIXME : Doesn't format YTEXT for FP data (DLDPStorage from YFELD in PARA) - does not appear necessary.
		
		set PARA    = ""
		set REL     = ""
		set YTYP    = ""
		set YVALIGN = ""
		set YTEXT1  = YTEXT      ; returns YTEXT in <div> (showing relation value if applicable)
		set YTEXT1  = $replace($replace($$TEXT^COMGridEdit31F(,YFIELDNAME,.YTXT,.PARA,REL,YTEXT,YTYP),YCR,""),"'","""")
	;	set YTEXT1  = "<div class=""DIVtxt"" style="" padding-left:2px; margin:0px;"">"_YTEXT_"</div>"
		set strHTML = strHTML_YTEXT1
		set strHTML = strHTML_"</TD>"
		set strHTML = strHTML_"<TD style=""border:1px solid; padding-top:0px; padding-bottom:0px;"">"
		set strHTML = strHTML_"<INPUT type=""checkbox"" id=""cgePopupColVisible_"_idField_""" style=""height:13px; width:13px;"""
		if $$$COMGridEditLayoutHidden($get(^COMGridEditLayout(0,YBED,YFORM,idField,1))) {
			set strHTML=strHTML_" checked"
		}
	;	set strHTML = strHTML_" onclick=""cgePopupHide"" "
		set strHTML = strHTML_">"
		set strHTML = strHTML_"</TD>"
		set strHTML = strHTML_"</TR>"
	}
	set strHTML = strHTML_"</TABLE>"
	set strHTML = strHTML_$$Buttons()
	set strHTML = strHTML_"<div id=""cgePopupStatus"" style="" color:black; background-color:lightgrey;position:relative;width:100%;height:10px;border:1px solid darkgray; "">"
	set strHTML = strHTML_"</div>"
	quit strHTML	
	
	
Create(YFORM,YBED,strField,pblnForce=$$$NO)
	;-------------------------------------------------------------------------------
	; Start the process
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 17-Dec-2009	shobby	SR16969: Commented
	;-------------------------------------------------------------------------------
	new blnVisible,intHeight,intLeft,intTop,intWidth,objPopup

	if pblnForce do Change("OPEN","")
	set objPopup = $$GetSettings(YBED,YFORM)
	if $$$COMGridEditLayoutPopupVisible(objPopup) {
		do CreateJavascript(YFORM,YBED,strField,objPopup)
	}
	quit $$$OK
	
	
CreateJavascript(YFORM,YBED,strField,pobjPopup)
	;-------------------------------------------------------------------------------
	; Javascript to support this screen
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 17-Jun-2010	shobby	SR17253: Removed a lot of firefox work.  With WWWFORMCrossBrowser support
	; 							firefox can now handle IE specific syntax.
	; 04-Jun-2010	shobby	SR17339: Rework of outerHTML solution from SR17253.
	; 25-May-2010	shobby	SR17327: Avoid losing drag/drop events when loading popup.
	; 18-May-2010	GRF		SR17286: setCapture/releaseCapture
	; 18-May-2010	shobby	SR17253: Correct firefox issue where background colour didn't
	; 							show for the entire popup.
	; 05-May-2010	GRF		SR17286: firefox compatibility
	; 09-Apr-2010	shobby	SR17253: firefox compatibility
	; 17-Dec-2009	shobby	SR16969: Commented
	;-------------------------------------------------------------------------------
	&js<
	var dragobject = null;
	var dragx = 0;
	var dragy = 0;
	var posx = 0;
	var posy = 0;
	var fontsize=#($$$COMGridEditLayoutPopupFontSize(pobjPopup))#;
	var arrcgePopupVisible = new Array();

	function cgePopupClose() {
		cgeDetachEvents();
		document.getElementById('cgePopup').outerHTML='';
		var retval = EventValue(YUCI,YUSER,YFORM,"FIX","COMGridEdit31ExtraFields", "CLOSE" ,"6","");
	}
	function cgePopupButtonClose() {
		cgePopupClose()
	}
	
	function cgePopupFont(val) {
		fontsize=fontsize+val;
		document.getElementById('cgePopupTable').style.fontSize=fontsize+'pt';
	    var retval = EventValue(YUCI,YUSER,YFORM,"FIX","COMGridEdit31ExtraFields", "FONT" ,"6",fontsize);
	}
	
	function cgeDetachEvents() {
		window.status=window.status+'.detach'
		document.getElementById('cgePopupClose').detachEvent('onclick',cgePopupClose);
		document.getElementById('cgePopupFontUp').detachEvent('onclick',function() {cgePopupFont(1) });
		document.getElementById('cgePopupFontDown').detachEvent('onclick',function() {cgePopupFont(-1)});
		document.detachEvent('onmousemove',drag);
		document.detachEvent('onmouseup',dragstop);
		document.getElementById('cgePopupHeader').detachEvent('onmousedown',dragstart);
		document.detachEvent('onclick',cgePopupHide);
		document.getElementById('cgePopupButtonCancel').detachEvent('onclick',cgePopupButtonClose);
		document.getElementById('cgePopupButtonSave').detachEvent('onclick',cgePopupButtonSave);
		document.getElementById('cgePopupButtonCustomise').detachEvent('onclick',cgePopupButtonCustomise); //SR16969
	}
	
	function eventinit(obj,document) {
		window.status=window.status+'.attach'
		obj.innerHTML='#($$InnerHTML^COMGridEdit31ExtraFields(YFORM,strField,$$$COMGridEditLayoutPopupFontSize(pobjPopup)))#';
		document.getElementById('cgePopupClose').attachEvent('onclick',cgePopupClose);
		document.getElementById('cgePopupFontUp').attachEvent('onclick',function() {cgePopupFont(1) });
		document.getElementById('cgePopupFontDown').attachEvent('onclick',function() {cgePopupFont(-1)});
		document.attachEvent('onmousemove',drag);
		document.attachEvent('onmouseup',dragstop);
		document.getElementById('cgePopupHeader').attachEvent('onmousedown',dragstart);
		document.attachEvent('onclick',cgePopupHide);
		document.getElementById('cgePopupButtonCancel').attachEvent('onclick',cgePopupButtonClose);
		document.getElementById('cgePopupButtonSave').attachEvent('onclick',cgePopupButtonSave);
		document.getElementById('cgePopupButtonCustomise').attachEvent('onclick',cgePopupButtonCustomise); //SR16969
	}

	function cgePopupHide() {
		//if (!event) event=window.event;		//SR17253
		if (window.event.srcElement.id.split('_')[0]=='cgePopupColVisible') {
			var col=window.event.srcElement.id.split('_')[1]
			var hidden=!document.getElementById('THfld_'+YFORM+'_'+col).Hidden
			hideShowColumn(col,hidden,YFORM);
			arrcgePopupVisible[col]=hidden;
		//	retval = EventValue(YUCI,YUSER,YFORM,"FIX","COMGridEdit31ExtraFields", "HIDE" ,"6",1);
		}
		 return "";
	}

	function cgePopupButtonSave() {
		var retval = EventValue(YUCI,YUSER,YFORM,"FIX","COMGridEdit31ExtraFields", "SAVE" ,"6",arrcgePopupVisible);
	}
	function cgePopupButtonCustomise() {  //SR16969
		var retval = EventValue(YUCI,YUSER,YFORM,"FIX","COMGridEdit31ExtraFields", "CUSTOMISE" ,"6",arrcgePopupVisible);
	}

	function dragstart() {
		dragobject = document.getElementById('cgePopup');
		dragx = posx - dragobject.offsetLeft;
		dragy = posy - dragobject.offsetTop;
		dragobject.setCapture();
	}
	function dragstop() {
		if (dragobject != null) {
			var retval = EventValue(YUCI,YUSER,YFORM,"FIX","COMGridEdit31ExtraFields", "MOVE" ,"6",dragobject.offsetLeft + "~" + dragobject.offsetTop);
			dragobject.releaseCapture();
		}
		dragobject = null;
	}

	function drag() {
		posx=window.event.clientX;
		posy=window.event.clientY;
		if (dragobject != null) {
			dragobject.style.left = (posx - dragx) + "px";
			dragobject.style.top = (posy - dragy) + "px";
			document.getElementById('cgePopupStatus').innerHTML=window.event.clientX +'+'+dragobject.style.left;    //SR17253  //SR17327
		}
	}

	var obj=document.getElementById('cgePopup');
	if (obj==null) {
		obj=document.createElement('div');
		obj.id='cgePopup';
		obj.className='floating';
		obj.style.position='absolute';
		obj.style.left=#($$$COMGridEditLayoutPopupLeft1(pobjPopup))#;
		obj.style.top=#($$$COMGridEditLayoutPopupTop1(pobjPopup))#;
		obj.style.height='#($$$COMGridEditLayoutPopupHeight(pobjPopup))#';		//SR17253
		obj.style.width=#($$$COMGridEditLayoutPopupWidth(pobjPopup))#;
		obj.style.fontSize='8pt';
		obj.style.border='1px solid orange';
		obj.style.backgroundColor='white';
		obj.style.zIndex=10000000;
		document.WWW2.appendChild(obj);
	}
	eventinit(obj,document);
	>
	quit 1
	
	
GetSettings(YBED,YFORM)
	;-------------------------------------------------------------------------------
	; 25-May-2010	shobby	SRAdhoc Disallow less than 0 (off screen)
	;-------------------------------------------------------------------------------
	new objPopup
	
	set objPopup = $get(^COMGridEditLayoutPopup(0,YBED,YFORM,1))
	if $$$COMGridEditLayoutPopupLeft1(objPopup)   ="" set $$$COMGridEditLayoutPopupLeft1(objPopup)    = 500
	if $$$COMGridEditLayoutPopupTop1(objPopup)    ="" set $$$COMGridEditLayoutPopupTop1(objPopup)     = 100
	if $$$COMGridEditLayoutPopupHeight(objPopup)  ="" set $$$COMGridEditLayoutPopupHeight(objPopup)   = ""   ;SR17253
	if $$$COMGridEditLayoutPopupWidth(objPopup)   ="" set $$$COMGridEditLayoutPopupWidth(objPopup)    = 350
	if $$$COMGridEditLayoutPopupVisible(objPopup) ="" set $$$COMGridEditLayoutPopupVisible(objPopup)  = $$$NO
	if $$$COMGridEditLayoutPopupFontSize(objPopup)="" set $$$COMGridEditLayoutPopupFontSize(objPopup) = 8
	if $$$COMGridEditLayoutPopupTop1(objPopup) <0     set $$$COMGridEditLayoutPopupTop1(objPopup)     = 0
	if $$$COMGridEditLayoutPopupLeft1(objPopup)<0     set $$$COMGridEditLayoutPopupLeft1(objPopup)    = 0
	quit objPopup
	
	
Move(YLFDAT,&pobjPopup)
	set $$$COMGridEditLayoutPopupLeft1(pobjPopup) = $piece(YLFDAT,Y,1)
	set $$$COMGridEditLayoutPopupTop1(pobjPopup)  = $piece(YLFDAT,Y,2)
	quit $$$OK
	
	
Font(YLFDAT,&pobjPopup)
	set $$$COMGridEditLayoutPopupFontSize(pobjPopup) = $piece(YLFDAT,Y,1)
	quit $$$OK
	
	
Change(YINHALT,YLFDAT)
	;-------------------------------------------------------------------------------
	; Redirects calls based on YINHALT
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 17-Dec-2009	shobby	SR16969: Commented
	;-------------------------------------------------------------------------------
	new objPopup,strStatus

	set objPopup=$get(^COMGridEditLayoutPopup(0,YBED,YFORM,1))
	if YINHALT="MOVE"  do Move(YLFDAT,.objPopup)
	if YINHALT="FONT"  do Font(YLFDAT,.objPopup)
	if YINHALT="CLOSE" set $$$COMGridEditLayoutPopupVisible(objPopup) = $$$NO
	if YINHALT="OPEN"  set $$$COMGridEditLayoutPopupVisible(objPopup) = $$$YES
	set strStatus = $$$Save("COMGridEditLayoutPopup",YBED_","_YFORM,objPopup,$$$YES)
	quit $$$OK
	
	
Save(YINHALT,YLFDAT)
	;-------------------------------------------------------------------------------
	; From the save button on the popup panel.  Save the grid layout for the 'Hidden' field
	; Then update the form Properties if in DEVMODE.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 18-Dec-2009	shobby	SR16969: Rewrote to better handle when 'Save' and
	;									'Customise' are both pressed.
	; 22-Oct-2009	shobby	SR16969: Save layout in customisation.
	; 22-Jul-2009	shobby	SR16730: Created
	;-------------------------------------------------------------------------------
	new fltWidth,intPiece,strStatus,intField,objWWW122,objWWW121,idField
	new objLayout,objWWW121D,objWWW122D
	
	set strStatus=$$$OK
	
	for intField=1:1:$length(YLFDAT,",") {
		quit:$$$ISERR(strStatus)
		
		if $data(^WWW122(0,YFORM,intField)) {
			set objLayout = $get(^COMGridEditLayout(YM,YBED,YFORM,intField,1))
			set $$$COMGridEditLayoutHidden(objLayout)=$piece(YLFDAT,",",intField)
			set strStatus = $$$Save("COMGridEditLayout",YBED_","_YFORM_","_intField,objLayout,$$$YES)
		}
	}

	if YINHALT="CUSTOMISE" {
		set idField=""
		for {
			set idField=$order(^COMGridEditLayout(YM,YBED,YFORM,idField))
			quit:idField=""
			quit:$$$ISERR(strStatus)
			
			set objLayout = $get(^COMGridEditLayout(YM,YBED,YFORM,idField,1))
			if $$$COMGridEditLayoutWidth(objLayout)'="" {
				if $extract(idField)="P" {
					set intField=$extract(idField,2,999999)
					set objWWW121D=$get(^WWW121D(0,YFORM,intField,YM,1))
					set $$$WWW121DFixAnswerLengthInPixel(objWWW121D)=$$$COMGridEditLayoutWidth(objLayout)  
					set strStatus = $$$Save("WWW121D",YFORM_","_intField_","_YM,objWWW121D,$$$YES)
				} else {								
					set intField=idField
					set objWWW122D=$get(^WWW122D(0,YFORM,intField,YM,1))
					set $$$WWW122DFixAnswerLengthInPixel(objWWW122D)=$$$COMGridEditLayoutWidth(objLayout)
					set strStatus = $$$Save("WWW122D",YFORM_","_intField_","_YM,objWWW122D,$$$YES)
				}
				if $$$ISOK(strStatus) {
					set $$$COMGridEditLayoutWidth(objLayout)=""
					set strStatus = $$$Save("COMGridEditLayout",YBED_","_YFORM_","_idField,objLayout,$$$YES)
				}
			}
		}
		
	} elseif $$$DEVMODE {
		set idField=""
		for {
			set idField=$order(^WWW122(0,YFORM,idField))
			quit:idField=""
			
			set fltWidth=""
			set objWWW122D = $get(^WWW122D(0,YFORM,idField,YM,1))
			set fltWidth   = $$$WWW122DFixAnswerLengthInPixel(objWWW122D)
			set objLayout  = $get(^COMGridEditLayout(YM,YBED,YFORM,idField,1))
			if $$$COMGridEditLayoutWidth(objLayout)'="" {
				set fltWidth=$$$COMGridEditLayoutWidth(objLayout)
			}
			if fltWidth'="" {
				set objWWW122=$get(^WWW122(0,YFORM,idField,1))
				if $$$WWW122FixAnswerLengthInPixel(objWWW122)'=fltWidth {
					set $$$WWW122FixAnswerLengthInPixel(objWWW122)=fltWidth
					set strStatus = $$$Save("WWW122",YFORM_","_idField,objWWW122,$$$YES)
					if $$$ISOK(strStatus) {
						if $$$WWW122DFixAnswerLengthInPixel(objWWW122D)'="" {
							set $$$WWW122DFixAnswerLengthInPixel(objWWW122D)=""
							set strStatus = $$$Save("WWW122D",YFORM_","_idField_","_YM,objWWW122D,$$$YES)
						}
					}
					if $$$ISOK(strStatus) {
						if $$$COMGridEditLayoutWidth(objLayout)'="" {
							set $$$COMGridEditLayoutWidth(objLayout)=""
							set strStatus = $$$Save("COMGridEditLayout",YBED_","_YFORM_","_idField,objLayout,$$$YES)
						}
					}		
				}
			}
		}

		set idField = ""
		for {
			set idField = $order(^WWW121(0,YFORM,idField))
			quit:idField=""
			
			set fltWidth   = ""
			set objWWW121D = $get(^WWW121D(0,YFORM,idField,YM,1))
			set fltWidth   = $$$WWW121DFixAnswerLengthInPixel(objWWW121D)
			set objLayout  = $get(^COMGridEditLayout(YM,YBED,YFORM,"P"_idField,1))
			if $$$COMGridEditLayoutWidth(objLayout)'="" {
				set fltWidth=$$$COMGridEditLayoutWidth(objLayout)
			}
			if fltWidth'="" {
				set objWWW121=$get(^WWW121(0,YFORM,idField,1))
				if $$$WWW121FixAnswerLengthInPixel(objWWW121)'=fltWidth {
					set $$$WWW121FixAnswerLengthInPixel(objWWW121)=fltWidth
					set strStatus = $$$Save("WWW121",YFORM_","_idField,objWWW121,$$$YES)
					if $$$ISOK(strStatus) {
						if $$$WWW121DFixAnswerLengthInPixel(objWWW121D)'="" {
							set $$$WWW121DFixAnswerLengthInPixel(objWWW121D)=""
							set strStatus = $$$Save("WWW121D",YFORM_","_idField_","_YM,objWWW121D,$$$YES)
						}
					}
					if $$$ISOK(strStatus) {
						if $$$COMGridEditLayoutWidth(objLayout)'="" {
							set $$$COMGridEditLayoutWidth(objLayout)=""
							set strStatus = $$$Save("COMGridEditLayout",YBED_","_YFORM_",P"_idField,objLayout,$$$YES)
						}
					}		
				}
			}
		}
	}
	quit $$$OK
	
	
Close(YINHALT,YLFDAT)
	;-------------------------------------------------------------------------------
	; Save the closed state of the popup screen
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 22-Jul-2009	shobby	SR16730: Created
	;-------------------------------------------------------------------------------
	do Change(YINHALT,YLFDAT)
	quit $$$OK
	
	;*********************************************   Controls   *****************************************
	
LayoutSelector()
	new strHTML
	set strHTML = "<SELECT>"
	set strHTML = strHTML_"<OPTION>Default</OPTION>"
	set strHTML = strHTML_"</SELECT>"
	quit strHTML
	
	
Buttons()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 21-Oct-2009	shobby	SR16969:Language texts, permissions on buttons.
	; ??-???-2009	shobby	SR?????:Created
	;-------------------------------------------------------------------------------
	new strDisabled,strHTML
	
	set strHTML     = ""
	set strDisabled = ""

	if $$^WWWBEDBER(YBED)'=1 set strDisabled = "disabled"
	set strHTML = strHTML_"<INPUT id=""cgePopupButtonCustomise"" type=""button"" "_strDisabled_" value="""_$$$Text("Com00327")_""" style=""align:right; position:absolute; left:110px; width:70px; "">"
	
	set strDisabled = ""
	if '$$$DEVMODE set strDisabled = "disabled"
	set strHTML = strHTML_"<INPUT id=""cgePopupButtonSave"" type=""button"" "_strDisabled_" value="""_$$$Text(165)_""" style=""align:right; position:absolute; left:190px; width:70px; "">"
	set strHTML = strHTML_"<INPUT id=""cgePopupButtonCancel"" type=""button"" value=""Cancel"" style=""align:right; position:relative; left:270px; width:70px;"">"
	
	quit strHTML
	
]]></Routine>
</Export>