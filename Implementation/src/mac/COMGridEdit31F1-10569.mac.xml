<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="COMGridEdit31F1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
COMGridEdit31F1
 
#include WWWConst
#include COMSYS
 
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^COMGridEdit31F1("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	; NOTE: There is a similar procedure at ScreenUpdateStyle^COMGridEdit31S, these may be merged or
	; at least made to use common components at a later stage when the Dynamic Table settles.
	;
DisplayData(pstrData,pidForm="",pidFormField="",pidClass="",pidClassField="")
	;-------------------------------------------------------------------------------
	; Given data format it ready for display
	;
	; Must either provide Form & Form Field or Class & Class Field, form over rides class if both provided
	;
	; Note: This ought to be broken up into converting for display and formatting into HTML
	;
	; Params:
	; pstrData - Data
	; pidForm - Form Id
	; pidFormField - Form Field Id
	; pidClass - Class Id
	; pidClassField - Class Field Id
	;
	; Returns: HTML
	;
	; History:
	; 12-Jan-2015	shobby	HEVA-1665: Rewrite to fix problems of WWW003 and WWW122
	; 06-Jun-2014	SCR		HEVA-1505: Handle no Class and Form & Class no the same
	; 06-Sep-2011	shobby	SR17891: Corrected variable spelling.  Check that relationship is valid.
	; 18-May-2011	GRF		Replace $$$WWWYM
	; 03-May-2011	shobby	SR17714.4: Rearranged so that the CalcRelationalDataField will obey the
	;							 HideRelationClassIDs logic.
	; 27-Apr-2011	shobby	SR17714: A calculated value may be obtained from an OnBeforeDisplay
	;							hook of the referenced class.  Bypassing other calculations.
	; 07-Jan-2011	GRF		SR17579: Use explicit tag for WWWTR calls
	; 05-Jan-2010	GRF		SR15525: Call to $$Convert^WWWTR rather than $$^WWWTR
	; 01-Jul-2009	shobby	override objField from WWW003
	; 29-Nov-2007	GM		SRBR014805: Hide relation class ID of a specific class relation
	; 06-Jul-2007	shobby	SRBR014553: Consider customisation of WWW122
	; 24-Apr-2007	HeberB	SRBR014432: hide IDs if requested
	; 17-Jan-2007	PO/RPW	SR15351: Format data for display ie. call $$^WWWTR
	; 03-Jan-2007	PO		SR15351: Created
	;-------------------------------------------------------------------------------
	new blnHide,idClass,idField,idInputType,idRelClass,loop,objDef,objField,objRecord
	new strDisplay,strRef,strRelational,strRelDisplayList,strRelKeys
	new blnCalcRelationalDataField ;HEVA-1665
	
	$$$LogR("DisplayData",$get(pstrData)_"<"_pidForm_"<"_pidFormField)
	
 	set blnHide = $$HideRelationClassIDs^WWW122HideRelation(pidForm,pidFormField)
	
	; HEVA-1665 vvvvvvvvvvvvvvvvvvv
	if pidForm '= "" {
		set objDef   = $get(^WWW120(0,pidForm,1))
		set objField = $$Get^WWW122(pidForm,pidFormField)
		if pidClass="" {
			set pidClass	= $$$WWW120ClassUsedInForm(objDef)
		}
		
		set idRelClass       			= $$$WWW122RelationClass(objField)
		set strRelKeys       			= $$$WWW122RelationalPrimaryKey(objField)
		set strRelDisplayList 			= $$$WWW122RelationalDataField(objField)
		set idInputType       			= $$$WWW122InputType(objField)
		set blnCalcRelationalDataField	= $$$WWW122CalcRelationalDataField(objField)
	} else {
		set objDef   = $get(^WWW001(0,pidClass,1))
		set objField = $get(^WWW003(0,pidClass,pidClassField,1))
		
		set idRelClass        			= $$$WWW003RelationDatabase(objField)
		set strRelKeys        			= $$$WWW003RelationalPrimaryKeys(objField)
		set strRelDisplayList 			= $$$WWW003RelationalDisplayItems(objField)
		set idInputType       			= $$$WWW003InputType(objField)
		set blnCalcRelationalDataField	= $$$WWW003CalcRelationalDisplayItems(objField)
	}
	
	
	;-----------------------------------------------------------------
	/*
	if pidForm '= "" {
		set objDef   = $get(^WWW120(0,pidForm,1))
		set objField = $$Get^WWW122(pidForm,pidFormField)
		if pidClass="" {
			set pidClass	= $$$WWW120ClassUsedInForm(objDef)
		}
	} else {
		set objDef   = $get(^WWW001(0,pidClass,1))
		set objField = $get(^WWW003(0,pidClass,pidClassField,1))
	}

	set idRelClass = $$$WWW122RelationClass(objField)
	if idRelClass = "" {
		if pidClass = "" {
			set idClass  = $$$WWW120ClassUsedInForm(objDef)
			set idField  = $$$WWW122SequenceNumber(objField)
			set objField = $get(^WWW003(0,idClass,idField,1)) 
		}
		set idRelClass        = $$$WWW003RelationDatabase(objField)
		set strRelKeys        = $$$WWW003RelationalPrimaryKeys(objField)
		set strRelDisplayList = $$$WWW003RelationalDisplayItems(objField)
		set idInputType       = $$$WWW003InputType(objField)
	} else {
		if (pidForm = "INARTPACK") {
			set idRelClass        = $$$WWW122RelationClass(objField)
		}
		set strRelKeys        = $$$WWW122RelationalPrimaryKey(objField)
		set strRelDisplayList = $$$WWW122RelationalDataField(objField)
		set idInputType       = $$$WWW122DataInputType(objField)
	}
	*/
	;-----------------------------------------------------------------
	; HEVA-1665 ^^^^^^^^^^^^^^^^^^^^

	set strRef = ""
	if (idRelClass '= "")&&($translate(pstrData,"""")'="") { ;SR17891
	 	set strRef = "^"_idRelClass_"(0"
	 	if strRelKeys'="" set strRef = strRef_$$$COMMA_strRelKeys
	 	set strRef = strRef_","""_$translate(pstrData,"""")_""",1)"
	 	if strRelDisplayList = "" set strRelDisplayList = $order(^WWW003(0,idRelClass,""))
		if (pidForm = "INARTPACK") {
			set strRelationalDisplayItems = strRelDisplayList
		}
	}

	; SR17597 : FIXME? PassYDECIMALLEN as fourth parameter for FP?
  	if (strRef = "") || ($data(@strRef)#2 '= 1) {
	 	set strDisplay = $$GetLiteral^WWWTR(idInputType,pstrData)
 	} else {
	 	set strRelational = ""
	 	set objRecord = @strRef
	 	;HEVA-1665 if $$$WWW122CalcRelationalDataField(objField)'="" { 												 ;SR17714.4
	 	if blnCalcRelationalDataField'="" { ;HEVA-1665
	 	;	Click on dynamic table populates grid cell.													     ;SR17714.4 
  			set strDisplay = $$GetCalculatedValue(YFORM,YKEY,@strRef,pidFormField,objField,pstrData)         ;SR17714
			set strDisplay = $select(blnHide && (strRelational'=""):"",1:"("_pstrData_")&nbsp;" )_strDisplay ;SR17714.4
 		
 		} else {																							 ;SR17714.4 
		 	for loop=1:1:$length(strRelDisplayList,";") {
	 			;set strRelational = strRelational_$piece(objRecord,Y,$piece($get(strRelationalDisplayItems),";",loop))_$$$COMMA
	 			set strRelational = strRelational_$piece(objRecord,Y,$piece($get(strRelDisplayList),";",loop))_$$$COMMA ;SR17891
		 	} // This means the relational text is not formatted for display
		 	set strRelational = $extract(strRelational,1,$length(strRelational)-1)
		 	if strRelational = "" {
		 		set strDisplay = $$GetLiteral^WWWTR(idInputType,pstrData)
		 	
		 	} elseif strRelational '= "" {
			 	set strDisplay = $select(blnHide && (strRelational'=""):"",1:"("_pstrData_")&nbsp;" )_strRelational
		 	}
 		}																									 ;SR17714.4
 	}
	if strDisplay = "" set strDisplay = "&nbsp;"
 	
	quit strDisplay
	
	
GetCalculatedValue(YFORM,YKEY,YFELD,YBBN,pobjField,pstrData,YLFN="")
	;-------------------------------------------------------------------------------
	; This routine creates the necessary data and Y* globals to allow the GetCalculatedValue
	; to interpret the Calculated value formulas.
	;
	; Inputs:
	;		pobjField:	WWW122 or WWW003
	;
	; Returns:
	;
	; History:
	; 02-Jun-2014	SCR		HEVA-1505: Allow for calculated fields
	; 18-May-2011	GRF		Replace $$$WWWYM
	; 29-Apr-2011	shobby	SR17714.3: Additional variables pstrData and YLFN so 
	;							this will work with dropdown lists in grids.
	; 27-Apr-2011	shobby	SR17714: Created
	;-------------------------------------------------------------------------------
 	new idRelClass,strRelKeys,strRef,strDisplay,strData

 	if YLFN="" set YLFN = $$$GetClassField(YFORM,YBBN)

 	set strDisplay=""

	set idRelClass = $$$WWW122RelationClass(pobjField)
	set strRelKeys = $$$WWW122RelationalPrimaryKey(pobjField)

 	set strRef = "^"_idRelClass_"(0"
	if (strRelKeys'="") {
		set strRef = strRef_","_strRelKeys
	}
	set strRef = strRef_","""_$translate(pstrData,"""")_""",1)"

	if $translate(pstrData,"""")'="" {
		set strDisplay = $$GetCalculatedValue^WWWFOR71($$$WWW122RelationClass(pobjField),$$$WWW122CalcRelationalDataField(pobjField),strRelKeys_","_pstrData,@strRef)
	}

	quit strDisplay
	
]]></Routine>
</Export>