<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReq" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReq
	
#include COMSYS
#include COMConst
#include INConst
#include %occInclude
	 
IsProgramOutOfDateRange(YFELD)			;SR17034
    ;-------------------------------------------------------------------------------
    ; Called By : Core Rules - INReq F24, INReqKit F22
    ;-------------------------------------------------------------------------------
	quit $$IsProgramOutOfDateRange^INPROJECT($$$INReqProgram1(YFELD),$$$INReqDate1(YFELD))
	
	
Editable(pidReq) 
    ;-------------------------------------------------------------------------------
    ; Is form editable (unprocessed) ?
    ;
    ; Params:	pidReq - Req id
    ;
    ; Returns:	status
    ;
    ; History:
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 24-Jan-2008 	HQN		SR15625 Changed class signature dREQ to dUReq
    ; 04-Dec-2007	PPP		SR15597 - Creation
    ;-------------------------------------------------------------------------------
	new objReq,strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pidReq) {
		set objReq = $get(^INReq(0,pidReq,1))
		
	 	if $$$INReqStatus(objReq) > 1 {
		 	set strStatus = $$$MakeStatus("INREQ01")
	 	} ; "Requisition has been Firmed"
	}
	quit strStatus
	
OnBlurDueDate(pYINHALT,pYFELD)
	;-------------------------------------------------------------------------------
	; FIX ME impliment user feed back of YTOOLTIP
	; 
	; 
	; History :
	; 26-Sep-2008	Luke	SR15946 Corrected the Check to %TXT   
	; 24-Sep-2008	Luke	SR15946 Created
	;-------------------------------------------------------------------------------
	
	if pYINHALT '= "" {
		if $$$INReqDueDate(pYFELD) < $horolog {
 			set YTOOLTIP = $$$Text("INREQ23")
		} ; "Due Date can only be today or later"
	}
	quit
	
	
OnAfterDataFields(pYM,pYFORM,pintPage,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
    ; 27-Jan-2010	PPP		SR17145: Added the OpenDemands Table
    ; 01-Jun-2009	PPP		SR16586: Simplify Process (Display Issue for a Requisition)
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 12-Mar-2009	HQN		SR16417: Disable ToLocn if key is valid; $$$NoKey macro
	; 30-Nov-2007	HQN		Created
	;-------------------------------------------------------------------------------
	do RemoveLocksIfRequired^COMLock(pYFORM,pYKEY,$$GetRemovalCode(,pYKEY),$$$YES)
	
	if ((+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,YM,1))))||
		(+$$$WWWClientParamCoreChangesSESPE($get(^WWWClientParam(0,YM,1))))||
		(+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(0,YM,1))))) {
		do PrintJS^VARINReq(YFORM)
	}	
	
	if $$$NoKey(pYKEY) {
		if '$$$INVORGHideOpenDemands($$Get^INVORG()) {
			do OpenDemandsTable^INReqOpenDemands(YLOCATION)
		}
		
	} else {
		do LoadGrid(0,pYFORM,pYKEY,pYFELD)
	}
	
	if ($$$INReqStatus(pYFELD) '= 1) {	  ; not Open		//SR16586
		do ShowIssuedTable^INReqShowIssue(pYKEY)
	}
	quit
	
	
GetRemovalCode(pstrClass="",pidClass="")
 	;-------------------------------------------------------------------------------
	; How to determine whether lock is still required.
	;
	; Params:
	; pstrClass - ** Not used **
	; pidClass - The key of the header record the index is related to.
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
    ; 27-Jan-2010	PPP		SR17145: Created
	;-------------------------------------------------------------------------------
	//quit $$$LOCKCODE_"'$data(^INReqLines0,3,$$Index^COMUtils(strClassId),"""_pidClass_"""))"
	quit $$$LOCKCALL_"CanRemoveLock^INReq(strClassId,"""_pidClass_""")"
	
	
CanRemoveLock(pstrClassId,pidClass)
	;-------------------------------------------------------------------------------
	; Determines whether a given lock can be removed
	;
	; Params:
	; pstrClassId  : The id of the class to be removed
	; pidClass     : The id of the lock owner record
	;
	; ByRefs:
	;
	; Returns:
	; Boolean
	;
	; History:
    ; 27-Jan-2010	PPP		SR17145: Created
	;-------------------------------------------------------------------------------
	new blnRemove,idReq,idStatus
	/*
	if pstrClassName="INReqLine" {
		set blnRemove='$data(^INReqToSupOrderLines(0,1,$$$Index(pstrClassId),pidClass))
	} else {		
		set blnRemove='$data(^INReqToSupOrderLines(0,2,$$$Index($$$KEY1(pstrClassId)),$$$Index($$$KEY2(pstrClassId)),pidClass))
	}
	*/
	
	set blnRemove = $$$NO
	set idReq = $order(^INReqLines(0,3,$$$Index(pstrClassId),"")) 
	
	if idReq'="" {	
		set idStatus = $$$INReqStatus($get(^INReq(0,idReq,1)))
		if (idStatus>0) && (idStatus<7) {
			set blnRemove = $$$YES
		}
	}
	quit blnRemove
	
IsOpen(pidReq="")
	;-------------------------------------------------------------------------------
	;
	; History :
	; 06-Dec-2013	shobby	 Created 
	;-------------------------------------------------------------------------------
	new objINReq,blnResult
	
	set blnResult=$$$NO
	if pidReq'="" {
		set objINReq=$get(^INReq(YM,pidReq,1))
		set blnResult=($$$INReqStatus(objINReq)=1) ;Open		
	}
	quit blnResult

	
OnBeforeDelete(pYM,pYFORM,pstrKey,pYFELD,pYFORMLine="INReqLine")
	;-------------------------------------------------------------------------------
	;			
	; Called By : OnBeforeDelete^INReqCancel, OnBeforeDelete^INReqKit, OnBeforeDelete^INReqReject
	;             Form INReq : Execute On Before Delete
	;                          (other forms call the OnBeforeDelete tags shown above)
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 06-Dec-2013	shobby	SESPE-571: Check record is still 'Open'
	; 22-Apr-2010	shobby	SR17267: Line Form could be INReqKitLine, INReqCancel,
	;							etc. so add pYFORMLine to parameters
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 18-Oct-2007	LB		SR15600: Created 
	;-------------------------------------------------------------------------------
 	new strStatus
 	
 	set Q = $$$QSave
	
 	//set strStatus = $$ExecuteRule(pYFORM,0,pstrKey,"Delete") 
 	set strStatus = $$$OK
 	if '$$IsOpen(pstrKey) set strStatus=$$$MakeStatus("IN01805")
 	
 	if $$$ISOK(strStatus) {
		;***************************************
	 	tstart
	 	if ($$DeleteAll^INReqLine(pYFORMLine) && $$$GRIDSave(pstrKey)) {		;SR17267
	 		$$$GRIDDelete
	 		tcommit
	 	} else {
		 	trollback
		;***************************************
		 	set strStatus = $$$NO      ; FIXME : <GRF> Error message?
	 	}
 	}
	
	//Remove any COMLock records if present
	do RemoveLocksIfRequired^COMLock(pYFORM,pstrKey)
	
  	if $$$ISERR(strStatus) {
	 	;do ReturnError^COMUtilError(strStatus)
	 	$$$Alert(strStatus)
	 	set Q = $$$QDontSave
 	}
 	quit strStatus
	
	
OnBeforeSave(pYM,pYFORM,pstrKey,pYFELD,pYFORMLine="INReqLine")
	;-------------------------------------------------------------------------------
	; this gets called before the screen write plus its enclosed in a hyper 
	; event or javascript call!
	;			
	; Called By : OnBeforeSave^INReqKit  (from Form INReqKit)
	;             Form INReq : Execute On Before Save
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History:
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 24-Sep-2008	Luke	SR15946 Added DueDate check and high light
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 18-Oct-2007	Luke	SR15600: Created 
	;-------------------------------------------------------------------------------
	new objReq,objStatus,strStatus
 	
	set Q         = $$$QSave
	set strStatus = $$$OK
	
	if ($$$INReqFromLocn(pYFELD)'="") && ( $$$INReqToLocn(pYFELD)'="") {
		set $$$INReqType(pYFELD) = $$GetReqType(pYFELD)
	}
	
	if '$$$NoKey(pstrKey) {
		if ($$$INReqDueDate(pYFELD)'="") && ($$$INReqDueDate(pYFELD)<$horolog)  {
			set strStatus = $$$MakeStatus("INREQ23")            ; "Due Date can only be today or later"
			set Q         = $$$QDontSave
		}
		//	FIXME : Are the following 
		if (YOPTION = 2) || ($$$INReqCancelled(pYFELD) = $$$YES) {
			if $$$INReqReasonCancellation(pYFELD) = "" {
				set Q = $$$QDontSave
				set strStatus = $$$MakeStatus("WWW00105",$$$WWW003CaptionInForms($get(^WWW003(0,"INReq",$$$FldINReqReasonCancellation,1))))
			}                                                   ; "Reason Cancelled : Mandatory Field"
		}
		
		if (YOPTION = 3) || ($$$INReqRejected(pYFELD) = $$$YES) {
			if $$$INReqReasonRejection(pYFELD) = "" {
				set Q = $$$QDontSave
				set strStatus = $$$MakeStatus("WWW00105",$$$WWW003CaptionInForms($get(^WWW003(0,"INReq",$$$FldINReqReasonRejection,1))))
			}                                                   ; "Reason Rejected : Mandatory Field"
		}
	}
	
 	if Q=$$$QSave {
 		set objReq = ##class(alREQ.dUReq).%OpenId("0||"_pstrKey)
		
	 	if objReq'= $$$NULLOREF {
		 	if objReq.RequisitionAllowed() {
				set objStatus = ##class(alSYS.Status.dUStatus).%OpenId("0||INReq||1")
				if (objReq.NewRecord()) { 
					set objReq.Status = objStatus 
				}
			 	set strStatus = $$SAVE^COMGridEdit31Save(pstrKey,pYFORMLine)     	// "INReqLine"		;SR17267
		 	
		 	} else {
			 	set strStatus = $$$MakeStatus("INREQ13")   ; "Requisition must be with the same main site"
		 	}
			do objReq.%Close()
		}
 	}
 	
	if $$$ISERR(strStatus) {
	 	do ReturnError^COMUtilError(strStatus)
	 	set Q = $$$QDontSave
 	}
 	quit
	
	
OnAfterSave(pidReq,pobjReq)
	;-------------------------------------------------------------------------------
	; Create Req lines if location selected in "Open" Demands table then firm Req
	;
	; Params:
	; pidReq - Req Id
	; pobjReq - Req Record
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 08-Jun-2010	PPP		SR17351: Moved call to ClearBlanks^INReq from 
	;							OnAfterSave to FirmRequisition
	; 11-Feb-2010	PPP		SR17122: Added the ClearBlank Lines routine
    ; 27-Jan-2010	PPP		SR17145: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	if $$$INReqStatus(pobjReq) = 1 {		// Open
	//	set strStatus = $$Transaction^COMTransaction("ClearBlanks^INReq("""_pidReq_""")")  //SR17351
		
		if $$$ISOK(strStatus) {
			//This is created by LinesToCreate^INReqOpenDemands	
			if $data(^CacheTempReqLinesToCreate(YUSER)) {
				set strStatus = $$Transaction^COMTransaction("CreateLinesReq^INReqOpenDemands("""_pidReq_""")")
			
				if $$$ISOK(strStatus) {
					set strStatus = $$Transaction^COMTransaction("FirmTxn^INReq("""_pidReq_""")")
				}
				
				if $$$ISOK(strStatus) {
					//set %("%KEY","HYPEREVENT") = $$$YES
					//
					//	This will cause GoToForm^COMUtilForm to load for in main frame using js,
					//	if omitted transfer form will appear at the bottom of the page.
					//do PrintPick^INTRNPick(pidTFR)
				}
				kill ^CacheTempReqLinesToCreate(YUSER)
			}
		}
	}
	quit
	
	
ClearBlanks(pidReq)
	;-------------------------------------------------------------------------------
	; Clear/Delete All Lines where the Qty is Blank or Zero
	;
    ; 
	; Returns:Status
	;
	; History:
	; 29-Jun-2010	SCR		SR17394: Remove Zero Qty Lines also
	; 11-Feb-2010	PPP		SR17122: Created
	;-------------------------------------------------------------------------------
	new idLine,objReqLine,strStatus
	
	set strStatus = $$$OK
	
	set idLine = ""
	for {
		quit:$$$ISERR(strStatus)
		
		set idLine = $order(^INReqLine(0,pidReq,idLine))
		quit:idLine=""
		
		set objReqLine = $get(^INReqLine(0,pidReq,idLine,1))
		
		if +$$$INReqLineQtyOrdered(objReqLine)=0 {  ; Remove Blank or Zero lines
			set strStatus = $$$Kill("INReqLine",pidReq_$$$COMMA_idLine)
		}
	}
	quit strStatus
	
	
FirmRequisition(pYM=0,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Post adjustment
	;
    ; Called By: Form INDispenseToPatient : Button 1 - Execute OnClick
    ;            Form INReq               : Button 1 - Execute OnClick
    ; 
	; Returns:Status
	;
	; History:
	; 08-Jun-2010	PPP		SR17351: Moved call to ClearBlanks^INReq from 
	;							OnAfterSave to FirmRequisition
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 15-May-2009	GRF		SR16199: sc/strStatus use clarified
	; 11-May-2009	PPP		SR16199: Updated as a Single Interface for both
	; 							Department Issue and POS/Sale Issue
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
 	if $$$INReqStatus(pYFELD) = 2 {
	 	set strStatus = $$$MakeStatus("INREQ01")           ; "Requisition has been Firmed"
 	}

	if $$$ISOK(strStatus) {	
		set strStatus = $$Transaction^COMTransaction("ClearBlanks^INReq("""_pYKEY_""")",$$$YES)  //SR17351
		if $$$ISOK(strStatus) {	
			set strStatus = $$Transaction^COMTransaction("FirmTxn^INReq("""_pYKEY_""")",$$$YES)
		}
	}
	
  	if $$$ISOK(strStatus) {

		if (+$$$WWWClientParamCoreChangesALL($get(^WWWClientParam(0,YM,1)))) {
			set $piece(^INReq(YM,pYKEY,1),Y,50) = $horolog  ;Free12 - Confirmado Em
			set $piece(^INReq(YM,pYKEY,1),Y,52) = YBED  	;Free14 - Confirmado Por
		}
	  	
	 	do ^WWWFORM
 	} else {
	 	do ReturnError^COMUtilError(strStatus)
 	}
 	$$$YQHandler(strStatus)
	quit
	
UpdateStatus(pidReq)
	;-------------------------------------------------------------------------------
	; Update Req Status based on lines
	;
	; History:
	; 16-Dec-2013	SCR		SESDF-655: Add Auto Close
	; 06-Dec-2013	SCR		SESDF-655: Created
	;-------------------------------------------------------------------------------
	new blnComplete,blnSent,idLine,idReqStatus,intIssued,intRecieved,intRequired
	new objLine,objReq,strStatus
	
	set strStatus	= $$$OK
	quit:pidReq="" strStatus
	set objReq		= $get(^INReq(YM,pidReq,1))
	set idReqStatus	= $$$INReqStatus(objReq)
	set blnSent		= $$$YES  ; Assume Yes
	set blnComplete	= $$$YES
	set idLine		= ""
	for {
		set idLine	= $order(^INReqLine(YM,pidReq,idLine))
		quit:idLine=""
		set objLine	= $get(^INReqLine(YM,pidReq,idLine,1))
		set intRequired	= $$$INReqLineQtyRequired(objLine) -  $$$INReqLineQtyRejected(objLine)
		set intIssued	= $$$INReqLineQtyIssued(objLine)
		set intRecieved	= $$$INReqLineQtyRequired(objLine)
		if intRequired>intIssued {
			set blnSent	= $$$NO
		}
		if intRequired'<intRecieved {
			set blnComplete	= $$$NO
		}
		
	}
	if blnSent {
		if idReqStatus<5 {
			set idReqStatus	= 5
		}
	} else {
		if idReqStatus>5 {
			set idReqStatus = 3
		}
	}
	if blnComplete {
		if idReqStatus<6 {
			set idReqStatus	= 9
		}
	}
	if idReqStatus'=$$$INReqStatus(objReq) {
		set $$$INReqStatus(objReq)	= idReqStatus
		set strStatus	= $$$Save("INReq",pidReq,objReq,1)
	}
	quit strStatus
	
	
FirmTxn(pidReq)
	;-------------------------------------------------------------------------------
	;		Forces a change of state to FIRMED
	; 
	; Called By :
	; 	Form - INReq
	; 	Routines -
	; 		Button from INReq form
	;		
	; Inputs : 
	;	companyId 	- 	Primary Key of Company
	;	reqId 		- 	%Id of INReq instance
	;
	; ByRef :
	;
	; Returns :
	;
	; History :	
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	;-------------------------------------------------------------------------------
	new objReq,sc,strStatus
	
	set (strStatus,sc) = $$$OK
	//$$ExecuteRule(pYFORM,0,pYKEY,"canFirm") 
 	//set strStatus = $$ExecuteStatus(pYFORM,0,pYKEY,"Firm") 
	
	set objReq = ##class(alREQ.dUReq).%OpenId("0||"_pidReq)
	
	if objReq'= $$$NULLOREF {
	 	if objReq.RequisitionAllowed() {
			if (objReq.ReqLinesCount() > "0") { 
				set sc = objReq.FirmRequisition() 
				if $$$ISOK(sc) {
			  		set objReq.Status = ##class(alSYS.Status.dUStatus).%OpenId("0||INReq||2")
					set sc = objReq.%Save()
				}
				
			 } else { 
				 set strStatus = $$$MakeStatus("INREQ07")  ; "Requisition has no lines to issue"
			 }
			
	 	} else {
		 	set strStatus = $$$MakeStatus("INREQ13")       ; "Requisition must be with the same main site"
	 	}
		do objReq.%Close()
	}
	
	if $$$ISOK(strStatus) {
 		set:$$$ISERR(sc) strStatus = $$ISStatusToDLStatus^COMUtilError(sc,$$$NO)
	}
 	quit strStatus
	
	/* REDUNDANT - Replaced by BELOW
ManuallyClose(pYM=0,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;			

	new objReq,sc,strStatus 
	
	set (strStatus,sc) = $$$OK
 	//set strStatus = $$ExecuteRule(pYFORM,0,pYKEY,"manClose") 
	
	set objReq = ##class(alREQ.dUReq).%OpenId("0||"_pYKEY)
	

	
 	$$$YQHandler(strStatus)
 	quit 
 	*/
 	
ManuallyClose(pYM=0,pYFORM,pYKEY,pYFELD,pVerbose=0)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
 	; 03-Jun-2011	PPP		SR17776: Updated to include Transaction boundary
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 30-Nov-2007	HQN		Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$Transaction^COMTransaction("ManuallyCloseTxn^INReq("""_pYKEY_""")",$$$YES)
	
	if pVerbose=0 { //If it's coming from an integration, do not change the form
		if $$$ISOK(strStatus) {
			if ((+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,YM,1))))||
				(+$$$WWWClientParamCoreChangesSESPE($get(^WWWClientParam(0,YM,1))))||
				(+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(0,YM,1))))) {
				do GoToForm^COMUtilForm(pYFORM,pYKEY)
			} else {
				do ^WWWFORM
			}
		} else {
			do ReturnError^COMUtilError(strStatus)
		}
	} else {
		q strStatus
	}
	
 	$$$YQHandler(strStatus)
 	quit 

ManuallyCloseTxn(pidReq)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : pidReq
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 03-Jun-2011	PPP	SR17776: Created
	;-------------------------------------------------------------------------------
	new objReq,sc,strStatus 
	
	set (strStatus,sc) = $$$OK
	
	set objReq = ##class(alREQ.dUReq).%OpenId("0||"_pidReq)
	
	if objReq'= $$$NULLOREF {
		set sc = objReq.ManuallyClose()	
		do objReq.%Close()
	}
	
 	if $$$ISERR(sc) set strStatus = $$ISStatusToDLStatus^COMUtilError(sc,$$$NO)
		
 	quit strStatus
 	
Print(pYKEY, &pYFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 30-Nov-2007	HQN		Created
	;-------------------------------------------------------------------------------
	new objReq,sc,strStatus
	
	set (strStatus,sc) = $$$OK
 	//set strStatus = $$ExecuteStatus(YFORM,YM,YKEY,"Print") 
	
	set objReq = ##class(alREQ.dUReq).%OpenId("0||"_pYKEY)
	
	if objReq'= $$$NULLOREF {
		set sc = objReq.Print()	
		do objReq.%Close()
	}
	
 	if $$$ISERR(sc) set strStatus = $$ISStatusToDLStatus^COMUtilError(sc,$$$NO)
	
 	if $$$ISOK(strStatus) {
	 	do ^WWWFORM
 	} else {
	 	do ReturnError^COMUtilError(strStatus)
 	}
 	
 	$$$YQHandler(strStatus)
 	quit
 	
 	
LoadGrid(pYM,pYFORM,pstrKey,pYFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 14-Aug-2013	shobby	CORE-233.1: Some problems resizing in HEVA
	; 13-Aug-2009	GRF		SR16544: explicit Hide switches
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 29-Jan-2009	HQN		SR16296: show columns when lines marked as rejected
	; 							or cancelled
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 30-Nov-2007	HQN		Created
	;-------------------------------------------------------------------------------
	new YAUSWAHL,YFORM,blnCancelled,blnRejected,idReqLine,objReqLine
	
	if (pstrKey '= "") {
		; FIXME : does editgrid do line locking?
		kill ^CacheTempLocked(YUSER)
		do ClearOld^WWWMultiLock(YUSER)
		set YFORM = pYFORM_"Line"
		
		set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
		set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
		set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
		set $$$COMGridEditParameterContainer(YAUSWAHL)     = pYFORM
		if (+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,YM,1)))){
			set $$$COMGridEditParameterEnabled(YAUSWAHL)   = (($$$INReqStatus(pYFELD) = 1) && '($$$WWW120AuthorizationToModifyData(YVOR)=5)) ; open
		} else {
			set $$$COMGridEditParameterEnabled(YAUSWAHL)   = ($$$INReqStatus(pYFELD) = 1) ; open
		}
		
	//	set strStatus = $$ExecuteRule(YFORM,0,pstrKey,"LoadGrid") 
		do Start^COMGridEdit31(YFORM,pstrKey)
		
		; Hide/Show correct Cancel/Reject columns
		if YOPTION = 1 { 		// Editing, cancel reject meaningless
			$$$StartScript()
			$$$GRIDHideShowColumns("12;13;14;15;16;18;19;20",$$$YES)  ; Hide form columns
			$$$EndScript()
		
		} else {
			if YLOCATION = $$$INReqFromLocn(pYFELD) {
				$$$StartScript()
				$$$GRIDHideShowColumns("13;15;19",$$$YES)  ; Hide : F13 Rejected, F15 Reason Rejection, F19 Rejection Date
				$$$EndScript()
			
			} elseif YLOCATION = $$$INReqToLocn(pYFELD) {
				$$$StartScript()
				$$$GRIDHideShowColumns("12;14;18",$$$YES)  ; Hide : F12 Cancelled, F14 Reason Cancellation, F18 Cancellation Date
				$$$EndScript()
			}
		}
		
		; 29-Jan-2009 vvvv
		set blnRejected  = $$$NO
		set blnCancelled = $$$NO
		set idReqLine = ""
		for {
			set idReqLine = $order(^INReqLine(0,pstrKey,idReqLine))
			quit:idReqLine=""
			
			set objReqLine = $get(^INReqLine(0,pstrKey,idReqLine,1))
			if $$$INReqLineRejected(objReqLine)=$$$YES  set blnRejected  = $$$YES
			if $$$INReqLineCancelled(objReqLine)=$$$YES set blnCancelled = $$$YES
		}
		if blnRejected {
			$$$StartScript()
			$$$GRIDHideShowColumns("15;16;19",$$$NO)  ; Show form columns
			$$$EndScript()
		}
		if blnCancelled {
			$$$StartScript()
			$$$GRIDHideShowColumns("14;18;20",$$$NO)  ; Show form columns
			$$$EndScript()
		}
		$$$StartScript()			;CORE-233.1
		write "moveFocus('T');"		;CORE-233.1
		$$$EndScript()				;CORE-233.1
	}
	quit
	
	
IsDeletable(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By : Button - Delete
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 18-Dec-2007	PPP		Only Status 1 - can be deleted (not after Firm (2))
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 30-Nov-2007	HQN		Created
	;-------------------------------------------------------------------------------
	new blnStatus
	
	set blnStatus = ($$$INReqStatus(pYFELD) = 1) ; open
	
 	$$$YQHandler(blnStatus)
 	quit blnStatus
	
	
IsFirmable(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Test if buttons should be disabled
	; 
	; Called by INReq : Button "Firm"
	;           and ???
	; 
	; History: 
	; 18-Aug-2009	PPP		SR16838: Check that the Status is > 1 
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 15-Sep-2008	HQN		SR15903: Using optional setting to strip object error
	; 							code
	; 01-Aug-2008	Luke	SR15814: Corrected use of status and naming 
	; 24-Jan-2008 	HQN		SR15625 Changed class signature dREQ to dUReq
	; 11-Dec-2007	HQN 	Code cleanup, added new
	; 22-Nov-2007	GRF		SR15614: Proper case for tag
	; 06-Dec-2007	HQN		Additional checks in place, needs to be converted to 
	; 							Script engine
	;-------------------------------------------------------------------------------
	new idItem,idReq,idStatus,idToLocn,objReq,objReqLine,reqLine,sc,strStatus
	
 	//set strStatus = $$ExecuteRule(pYFORM,0,pYKEY,"canFirm") 
 	set strStatus = $$$OK
	
 	if $$$INReqStatus(pYFELD) > 1 {    ; Firmed
	 	set strStatus = $$$MakeStatus("INREQ01")     ; "Requisition has been Firmed"
 	}
	;06-Apr-2011 Karine: CoreChange-Verifica se existe saldo em estoque para aprovar a requisi????o
	;                    Isso s?? ocorre se o par??metro de usu??rio que libera requisi????o com estoque zerado estiver = 1
	if $$$ISOK(strStatus) {
		set strStatus = $$CheckIfHasSOHForApproval^VARTCIReq(pYKEY)
	}
	;^^^^	
	
 	if $$$ISOK(strStatus) {
	 	set objReq = ##class(alREQ.dUReq).%OpenId("0||"_pYKEY)
	 	if objReq'= $$$NULLOREF {
		 	set sc = objReq.IsFirmable()
	 		set:$$$ISERR(sc) strStatus = $$ISStatusToDLStatus^COMUtilError(sc,$$$NO)
			do objReq.%Close()
 		}
 	}
 	
 	if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(0,YM,1)))) {
		if ($$getControlarAutorizacaoProdutos^VARParametroCliente(YM)){
			if ((pYFORM = "INReq")&&(pYKEY '= "")){
				if $$$ISOK(strStatus){
				
					set idReq  = pYKEY
					set objReq = $get(^INReq(YM,idReq,1))
		
					set idStatus = $$$INReqStatus(objReq)
					set idToLocn = $$$INReqToLocn(objReq)
					set reqLine  = ""
		
					for{
						set reqLine = $order(^INReqLine(pYM,idReq,reqLine))
						quit:(reqLine="")
			
						set objReqLine = $get(^INReqLine(pYM,idReq,reqLine,1))
						set idItem = $piece(objReqLine,"~",1)
			
						if ((idToLocn'="")&&(idItem'="")){
							if ($$GetDRPRecord^INDRPITEM(idToLocn,idItem) = "") {
								set strStatus = "Produto "_idItem_" n??o autorizado para o local "_idToLocn_"."
							}
						}
					}
			 	}
			}
		}
	}
 	
 	$$$YQHandler(strStatus)
 	quit strStatus
	
	
CanLoadTemplate(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By : Button - Use Template
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 04-Nov-2013	SCR		SESDF-655: Get Record based on YKEY, so this can work from the edit grid
	; 18-Dec-2007	PPP		Created
	;-------------------------------------------------------------------------------
	new blnStatus,idReq,objReq
	
	set blnStatus	= $$$NO
	set idReq		= $$$KEY1(pYKEY)
	if idReq'="" {
		set objReq		= $get(^INReq(pYM,idReq,1))
		set blnStatus 	= ($$$INReqStatus(objReq)=1)   ; open
	}
 	$$$YQHandler(blnStatus)
 	quit blnStatus
	
	
HasLines(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Test if buttons should be disabled
	; 
	; Called by INReq : Button 3 "Save As Template"
	; 
	; History: 
	; 22-Nov-2007	GRF		SR15614: Created
	;-------------------------------------------------------------------------------
	new blnStatus
	
	if (pYKEY'="") && $data(^INReqLine(0,pYKEY)) {
		set blnStatus = $$$YES
 	} else {
		set blnStatus = $$$NO
 	}
	
 	$$$YQHandler(blnStatus)
 	quit blnStatus
	
	
IsManuallyCloseable(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Test if buttons should be disabled
	; 
	; Called by INReq : Button 2 "Firm Requisition"
	; 
	; History: 
    ; 29-May-2009	PPP		SR16586: Simplify Process (remove unnecessary calls to
    ; 							Script Engine/State Engine)
	; 28-May-2009	DWR		SR16582: replaced DEV00000 entry with language text
	; 28-Jan-2009	HQN		SR16296: Replicate OpenStatus handling
	; 06-Dec-2007	HQN		(Test script Req-001[22])
	; 							Added status check for already manually closed
	;-------------------------------------------------------------------------------
	new blnCloseable
	set blnCloseable = $$$NO
 	if ($$IsFirmable^INReq(0,pYFORM,pYKEY,pYFELD)) {
	 	set blnCloseable = $$$NO
 	}
 	if '$$$NoKey(pYKEY) {
	 	if $$$INReqStatus(pYFELD)=1 {    ; open
			set blnCloseable = $$$MakeStatus("IN01046")    ; "Not yet firmed"
		 	
	 	} elseif ($$$INReqStatus(pYFELD)'=7) && ($$$INReqStatus(pYFELD)'=8) && ($$$INReqStatus(pYFELD)'=9) {
			set blnCloseable =  $$$YES    ; not Manual-Close nor Auto-Close
		}
	}
	
 	$$$YQHandler(blnCloseable)
	quit blnCloseable
	
	
GetHomeLocation(pidUser)
	;-------------------------------------------------------------------------------
	; Users have a single Home Location and a set of Allowed Locations
	; This function returns the former.
	; 
	; Called By : [Removed from Form INReqReject F3 "To Location"]
	;             Form INReq F3
	;             Form INReqCancel F3
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 30-Nov-2007	HQN		Created
	;-------------------------------------------------------------------------------
	new objUser
	
	set objUser = $get(^WWW013(0,pidUser,1))   ;User Defaults
	quit $$$WWW013HomeLocation(objUser)
	
	
OnDataAccess(pidReq,pidForm)
	;-------------------------------------------------------------------------------
	; Whether a Req can be used at this time.
	;
	; Params:	pidReq
	; 			Form
	;
	; Returns: blnDataAccess
	; 	$$$NO		Req is Not Accessable
	; 	$$$YES		Req is Accessable
	;
	; History:
	; 04-Jun-2009	PPP		SR16544: No checks if the form is INIssueReleaseLine
	; 18-Dec-2007	PPP		SR15600: Updated the Status Checks
	; 11-Dec-2007	PPP		SR15600: Created
	;-------------------------------------------------------------------------------
	new blnDataAccess,objReq
	
	set blnDataAccess = $$$YES
	
 	//if Issue Release no checks required
 	if pidForm = "INIssueReleaseLine" quit blnDataAccess              //EARLY EXIT 	
	
	if '$$$NoKey(pidReq) {
		set blnDataAccess = $$$NO
		set objReq        = $get(^INReq(0,pidReq,1))
		
		if (+$$$WWWClientParamCoreChangesTCI($get(^WWWClientParam(0,YM,1)))) {
			if ($$$INReqStatus(objReq) > 2)               &&
		  	($$$INReqStatus(objReq) < 8)                  {
			
				set blnDataAccess = $$$YES
			}
		}	
	}
	quit blnDataAccess
	
	
OnBeforeFormConstruction(pYPARA,&pYSATZ="")
	;-------------------------------------------------------------------------------
	; Params:
	; 	pYSATZ		WWW122 record from Data Field
	; 
	; ByRef:
	; 	YKOPF		Form Title
	; 	YKEY		Primary Key for INReq
	; 	YBBN		Form Field Id
	; 	YPFLICHT	(compulsory?  Boolean?)
	; 	
	; History:
	; 08-Dec-2009	shobby	SR17034: Display 'Program' dropdown based on setting in INVORG.
	; 10-Jun-2009	GRF		Repair macros (property names changed in class)
	; 13-Jan-2009	HQN		Set ReqType to read only when record is already saved
	; 08-Jan-2009	HQN		Alter Form Title depending on value of pYPARA
	;-------------------------------------------------------------------------------
	;if pYPARA = $$$AppParameter
	
	if $get(YBBN) = $$$GetFormField("INReq",$$$FldINReqType) {
		if '$$$NoKey(YKEY) set YHID = 2
	
	} elseif $get(YBBN) = $$$GetFormField("INReq",$$$FldINReqFromLocn) {
		if (YOPTION '= 1)  set YHID = 2
	
	} elseif $get(YBBN) = $$$GetFormField("INReq",$$$FldINReqDueDate) {
		if (YOPTION '= 1)  set YHID = 2
	
	} elseif $get(YBBN) = $$$GetFormField("INReq",$$$FldINReqPriority) {
		if (YOPTION '= 1)   set YHID = 2
	
	} elseif $get(YBBN) = $$$GetFormField("INReq",$$$FldINReqReasonCancellation) {
		set YPFLICHT = 1               ; Used within WWWFORM7 by ref
	
	} elseif $get(YBBN) = $$$GetFormField("INReq",$$$FldINReqReasonRejection) {
		set YPFLICHT = 1               ; Used within WWWFORM7 by ref
	}
	
	set YOPTION1 = '$$DisableProgramFunctionality^INVORG()	;SR17034
	
	quit
	
	
OnBlur(pFormFieldId,&pYFELD)
	;-------------------------------------------------------------------------------
	; Depending on the Locations, set the ReqType to the correct one
	; 
	; ByRef:
	; 	YM		Company ID
	; 	YFORM	Form ID (Should be INReq)
	; 
	; History:
	; 28-Feb-2014	shobby	SESDF-1159.2: Remove on blur code for FromLocn.  Done as rules now.
	; 23-Apr-2013	SCR		HEVA-811: Correct Req Type
	; 28-Mar-2013	SCR		HEVA-811: Use 'Transfer or Issue' Calc
	; 19-Mar-2013	SCR		HEVA-811: If enabled the Destination Location type dictates the Requisition Type. 
	;									If the Location is a consumption an Issue is created else a Transfer is created 
	; 08-Dec-2009	shobby	SR17034: When 'Program' changes update the grid and the
	; 							Dynamic table.
	; 10-Jun-2009	GRF		Repair macros (property names changed in class)
	; 13-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new intReqType
	 
	if pFormFieldId=$$$FldINReqType {
		if $$$INReqType(pYFELD) = 4 {                      ; SurgicalKit
			do GoToForm^COMUtilForm("INReqKit","",1,"")
		}
		
	} elseif pFormFieldId=$$$FldINReqProgram1 {
		if '$$$NoKey(YKEY) {
			do Update^COMGridEdit31Interface("INReqLine",YKEY,pYFELD)                            ; SR17034
			do ShowItems^INReqTable(1,"INReq"_$char(31)_"INReqLine"_$char(31)_"1"_$char(31)_"1") ; SR17034
		}  ; [idForm*idGrid*idKey*intRow]
	}
	quit
	
GetReqType(pYFELD)
	;-------------------------------------------------------------------------------
	; Get Req Type
	; 
	; ByRef:
	; 	YFORM	Form ID (Should be INReq)
	; 
	; History:
	; 24-Apr-2013	SCR		HEVA-811: Create]]><![CDATA[d
	;-------------------------------------------------------------------------------
	new idCalc,idFrom,intReqType,lstTran,objDest,objVORG
	
	set intReqType 	= $$$INReqType(pYFELD) ; Default to existing
	set objVORG 	= $get(^INVORG(YM,YM,1))
	set idCalc	= +$$$INVORGTransferorIssueCalc(objVORG)
	if ((idCalc =1) && ($$$INReqToLocn(pYFELD)'="")) {
		set objDest = $get(^WWW0121(0,YM,$$$INReqToLocn(pYFELD),1))

		if $$$WWW0121StorageLocn(objDest) {
			set intReqType = 2
		} else {
			set intReqType = 1
		}
	} elseif idCalc=0 {
		if $$MainSite^WWW0121Utils(0,$$$INReqFromLocn(pYFELD)) = $$MainSite^WWW0121Utils(0,$$$INReqToLocn(pYFELD)) {
			set intReqType = 1
		} else {
			set intReqType = 2
		}
	} elseif idCalc=2 {
		set intReqType=2
	} elseif idCalc=3 {	
		set intReqType=1			
	} elseif idCalc=4 {
		if $$$INReqToLocn(pYFELD)'="" {
			set objDest = $get(^WWW0121(0,YM,$$$INReqToLocn(pYFELD),1))
			set lstTran	= $$$WWW0121TransferLocations(objDest)
			set idFrom	= $$$INReqFromLocn(pYFELD)
			if (";"_lstTran_";")[(";"_idFrom_";") {
				set intReqType=2
			} else {
				set intReqType=1
			}
		} else {
			set intReqType=1 ; Default for Location
		}
		
	}
	
	quit intReqType

	
IsRejectable(pYKEY)
	;-------------------------------------------------------------------------------
	; Returns whether this Req is in a Rejectable state
	; 
	; CalledBy:
	; 	INReq @nm Form	[Reject] Button
	; 	
	; ByRef:
	; 	YQ	Hidden flag
	; 	YLOCATION Location ID
	; 	
	; History:
	; 18-Dec-2012	SCR		SESDF-655: Improved logic (blnRejectable in sync with YQ)
	; 17-Dec-2012	shobby	SESDF-655: Can only be rejectable if status 2 or 3
	; 28-May-2009	DWR		SR16582: Created Language Texts for DEV00000 entries.
	; 14-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new blnRejectable,objReq
	
	set blnRejectable = $$$NO
	set YQ = 2
	
	quit:$$$NoKey(pYKEY) blnRejectable
	
	set objReq = $get(^INReq(0,pYKEY,1))
	if objReq '= $$$NULLOREF {
		set blnRejectable = $$$YES
		if $$$INReqStatus(objReq) = 1 {
			$$$YQHandler($$$MakeStatus("IN01046"))         ; "Not yet firmed"        
		
		} elseif $get(YLOCATION) '= $$$INReqFromLocn(objReq) {
			$$$YQHandler($$$MakeStatus("IN01047"))         ; "Only the sending location may perform the processing"
		
		} else {
			; Check Status
			if ($$$INReqStatus(objReq) = 8) || ($$$INReqStatus(objReq) = 9) {
				$$$YQHandler($$$MakeStatus("alREQ0002"))   ; "Already Closed"
			
			} elseif ($$$INReqStatus(objReq) = 6) || ($$$INReqStatus(objReq) = 7) {
				$$$YQHandler($$$MakeStatus("INREQ06"))     ; "Requisition line is not Outstanding"
			
			} elseif ($$$INReqStatus(objReq) = 4) || ($$$INReqStatus(objReq) = 5) {																				;SESDF-655	
				$$$YQHandler($$$MakeStatus("IN01048",$$GetDescription^WWWStatus("INReq",$$$INReqStatus(objReq))))     ; "Cannot Reject Record with Status = %1" ;SESDF-655

			//} elseif ($$$INReqStatus(objReq) = 3) {
			//	$$$YQHandler($$$MakeStatus("IN01048"))     ; "Cannot Reject Record with Status = Active"
			
			} else {
				if $$$INReqCancelled(objReq) '= "" { 
					$$$YQHandler($$$MakeStatus("IN01043")) ; "Already Cancelled"
				
				} elseif $$$INReqRejected(objReq) '= "" {
					$$$YQHandler($$$MakeStatus("IN01044")) ; "Already Rejected"
				
					;SESDF-655} else {
					;SESDF-655	set YQ = 0
					;SESDF-655	set blnRejectable = $$$OK
				}
			}
		}
		
	} else {
		$$$YQHandler($$$MakeStatus("IN01045",pYKEY))       ; "Unable to load Req: %1"
	}
	set:YQ'=2 blnRejectable = $$$NO
	quit blnRejectable
	
	
OnBeforeButtonLine(pYM,pYFORM,pintPage,pYKEY,&pYFELD)
    ;-------------------------------------------------------------------------------
    ; Set form to readonly if not editable
    ;
    ; Params:	pidReq		- Req id
    ; 			pobjReq		- Req object
    ;
    ; ByRef: 
    ; 	YVOR		WWW122 record
    ; 	YPARA
    ;
    ; History:
    ; 19-Jan-2009	HQN		SR16296: Moved from OnAfterPrimaryKey
    ; 							Added YPARA options to alter form for
    ; 							rejecting/cancelling
    ; 04-Dec-2007	PPP		SR15597 - Creation
    ;-------------------------------------------------------------------------------
	set YOPTION = 1
	
	if '$$$NoKey(pYKEY) {
		if (YPARA = "REJECT") || (YPARA = "CANCEL") || (YPARA = "REJECTLINES") {
			if YPARA = "REJECT" {
				set YOPTION = 3
				if '$$IsRejectable(pYKEY) {
					set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
				} else {
					set $$$INReqRejected(pYFELD) = $$$YES
				}
			
			} elseif YPARA = "CANCEL" {
				set YOPTION = 2
				if '$$IsCancellable(pYKEY) {
					set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
				} else {
					set $$$INReqCancelled(pYFELD) = $$$YES
				}
			
			} elseif YPARA = "REJECTLINES" {
				set YOPTION = 5
			}
			set YPARA = ""
		
		} else {
			if '$$Editable(pYKEY) {
				set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
			}
			if $$$INReqRejected(pYFELD) '= "" {
				set YOPTION = 3 	// "REJECT"
			
			} elseif $$$INReqCancelled(pYFELD) '= "" {
				set YOPTION = 2 	// "CANCEL"
			}
		}
	}
	quit
	
	
IsCancellable(pYKEY)
	;-------------------------------------------------------------------------------
	; Returns whether this Req is in a Cancellable state
	; 
	; CalledBy:
	; 	INReq @nm Form	[Cancel] Button
	; 	
	; ByRef:
	; 	YQ	Hidden flag
	; 	YLOCATION Location ID
	; 	
	; History:
	; 28-May-2009	DWR		SR16582: Created Language Texts for DEV00000 entries.
	; 19-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new blnCancellable,objReq
	
	set blnCancellable = $$$NO
	set YQ = 2
	
	quit:$$$NoKey(pYKEY) blnCancellable
	
	set objReq = $get(^INReq(0,pYKEY,1))
	
	if objReq '= $$$NULLOREF {
		if $get(YLOCATION) '= $$$INReqToLocn(objReq) {
			$$$YQHandler($$$MakeStatus("IN01041"))         ; "Only the receiving location may perform the processing"
		
		} else {
			; Check Status
			if ($$$INReqStatus(objReq) = 1) {
				set YQ = 1
			
			} elseif ($$$INReqStatus(objReq) = 8) || ($$$INReqStatus(objReq) = 9) {
				$$$YQHandler($$$MakeStatus("alREQ0002"))   ; "Already Closed"
			
			} elseif ($$$INReqStatus(objReq) = 6) || ($$$INReqStatus(objReq) = 7) {
				$$$YQHandler($$$MakeStatus("INREQ06"))     ; "Requisition line is not Outstanding"
			
			} else {
				if $$$INReqCancelled(objReq) '= "" {
					$$$YQHandler($$$MakeStatus("IN01043")) ; "Already Cancelled"
				
				} elseif $$$INReqRejected(objReq) '= "" {
					$$$YQHandler($$$MakeStatus("IN01044")) ; "Already Rejected"
				
				} else {
					set YQ = 0
					set blnCancellable = $$$OK
				}
			}
		}
		
	} else {
		$$$YQHandler($$$MakeStatus("IN01045",pYKEY))       ; "Unable to load Req: %1"
	}
	
	quit blnCancellable
	
	
HyperEvent(pYINHALT,pYVAR)
	$$$Alert($get(^WWWDATEN(0,+$horolog,YUSER,"INReq","P",1)))
	quit $$$OK
	
	
	;*******************************************************************************
	;  vvv OBSOLETE CODE - DO NOT USE vvv
	;*******************************************************************************
	
Cancel(pYM=0,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;*** DEPRECATED, Not used apart from bogus button on form INReq <DWR> ***
	;-------------------------------------------------------------------------------
	quit $$$OK     ; DEPRECATED
	
	
ExecuteRule(pidForm,YM,YKEY="",pstrMethod)
	; *************************** NOT USED, DEPRECATED **************************
	;-------------------------------------------------------------------------------
	quit $$$OK      ; DEPRECATED
		
	
ExecuteStatus(pidForm,pYM,YKEY="",pstrMethod)
	; *************************** NOT USED, DEPRECATED **************************
	;-------------------------------------------------------------------------------
	quit $$$OK     ; DEPRECATED
	
	
Reject(pYM=0,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;*** DEPRECATED, Not used apart from bogus button on form INReq <DWR> ***
	;-------------------------------------------------------------------------------
	quit $$$OK     ; DEPRECATED
	
OnAfterPrimaryKey(pidReq,pobjReq,&pobjForm)
	if (+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(0,YM,1)))){
		do OnAfterPrimaryKey^VARReq(pidReq,pobjReq,.pobjForm)
	}

	quit
	
DefaultSupplingLoc(pidToLoc)
	;-------------------------------------------------------------------------------
	; Default Suppling Location
	; 
	; Called By: Forms InReq 
	; 
	;
	; Returns : Default Suppling Location  From WWW0121
	;
	; History :
	; 21-Nov-2013	SCR		SESDF-655:Created
	;-------------------------------------------------------------------------------
	new idSupLoc,objToLoc
	
	set idSupLoc	= ""
	if pidToLoc'="" {
		set objToLoc	= $get(^WWW0121(0,YM,pidToLoc,1))
		set idSupLoc	= $$$WWW0121DefaultSupplingLocation(objToLoc)
	}
	quit idSupLoc
]]></Routine>
</Export>