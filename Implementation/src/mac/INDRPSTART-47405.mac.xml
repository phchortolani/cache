<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INDRPSTART" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INDRPSTART ;INDRPSTART;FIS;STARTEN DRP-PROZESSE;29.10.2001
	#include COMSYS
	;
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		STARTEN DRP-PROZESSE
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 08-Dec-2005	GRF		SR13984 : Doco
	; 02-Sep-2005	JW		SR12966: INDRPMESPROT/INDRPRUNPROT are not shared 
	; 27-Jun-2005	JW		SR12620: Removed timeout
	; 29.10.2001	FIS		Created
	;-----------------------------------------------------------------------------*/
	
AUTO ;AUTO-START AUS WWWTAG HERAUS ;out of away 
	;START AUS HINTERGRUNDJOB: NUR YM IST GESETZT ! ;take-off out of only staid 
	;  13-Jul-2011	shobby		SR17806:    Record which namespace DRP was started in.
	SET $ZTRAP="^WWWERROR"
	DO LOW^%PRIO
	DO
	. NEW YM
	. DO ^WWWVAR
	
	IF '$DATA(YM) QUIT
	
	/*--------------------------------------
	;	D80		$$$INVORGDRPMRPDataExchange()
	;	^WWW101(0,"DRPAUSTAUSCH")	1		XML Background Communication
	;-------------------------------------*/
	IF $PIECE($GET(^INVORG(YM,YM,1)),Y,80)=1 JOB ^WWWXMLSEND()::1  ;STARTEN JOB FÜR DATENKOMUNIKATION ;launching to 
	
	;STARTEN NEUER PROZESS ;launching 
	LOCK +^INDRPRUNJOB(YM):0 IF '$TEST  QUIT  ;NICHT MÖGLICH (LÄUFT BEREITS) ;Not potential 
	LOCK -^INDRPRUNJOB(YM)
	;JOB ^INDRPSTART1(YM)::1 IF '$TEST QUIT  ;STARTEN HINTERGRUNDJOB ;launching 
	JOB ^INDRPSTART1(YM) ; SR12620
	
	SET ^INDRPRUNJOB(YM,+$HOROLOG,$PIECE($HOROLOG,",",2),1)="1"_Y_"WWWTAG"_Y_+$HOROLOG_Y_$PIECE($HOROLOG,",",2)_Y_$zu(5)  ;PROTOKOLL GESTARTET ;protocol  ;SR17806
	
	;LÖSCHEN UNBEARBEITETER AUFTRÄGE VOM VORTAG;TYBD;15,09,2003;24273
	IF '$DATA(^INDRPMESPROT(YM,+$HOROLOG)) IF '$DATA(^INDRPRUNPROT(YM,+$HOROLOG)) DO   ;HEUTE NOCH NICHT GELAUFEN ;today yet Not 
	. DO ^INDRPAUFKILL()
	
	;ULM, PROTOKOLLIERUNG HINTERGRUNDJOBS, 11.03.03       start/STOP                      LOCKDATEI        LOCKSTATUS      DRP-PROZESS
	SET ^INBACKJOBS1(YM,"INDRPSTART1",+$HOROLOG,$PIECE($HOROLOG,",",2),1)="1"_Y_YBED_Y_"WWW120(0,"_""""_"INDRPRUNJOB"_""""_")"_Y_"1"_Y_$$^WWWTEXT(33330)  ;DRP - PROTOKOLL GESTARTET, AUSWERTUNGSDATEI HINTERGRUNDJOBS
	QUIT
	
RESTART ;RESTART DES TAGES; WAEHREND DES LAUFS
	;   DO RESTART^INDRPSTART  ;START NEU DES TAGES;DANACH STARTET DER AUFBAU DER ORDERS NEU;TYBD;15,09,2003
	DO ^WWWVAR
	DO ^INDRPAUFKILL()
	KILL ^INDRPITEMRUN(YM)
	KILL ^INDRPITEMRUNs(YM)
	SET RUNID="" FOR  SET RUNID=$ORDER(^INDRPRUNID(YM,RUNID)) QUIT:RUNID=""  DO
	. SET $PIECE(^INDRPRUNID(YM,RUNID,1),Y,5)=""
	. SET $PIECE(^INDRPRUNID(YM,RUNID,1),Y,4)=""
	
	QUIT
	
START
	/*------------------------------------------------------------------------------
	;  STARTEN: EXECUTE AUS FORMULAR
	;  Execute On Save from form INDRPSTART
	;
	;  VORG(1): 1=START 2=STOP
	;  
	;  History
	;  13-Jul-2011	shobby		SR17806:    Record which namespace DRP was started in.
	;  01-Oct-2007	shobby		SRBR014383: Don't do the WWWINFO message.  DRP will start/stop much quicker now.
	;  										Just redraw the screen (after a small delay) should have time to have
	;  										changed the status.
	;-----------------------------------------------------------------------------*/
	SET VORG(1)=$GET(VORG(1))
	QUIT:VORG(1)=""
	
	IF '$DATA(^INDRPRUNJOB(YM)) SET ^INDRPRUNJOB(YM)=""		; WHY? <GRF>
	
	/*++++++++++++++++++++++++++++++++++++++
	;  START
	;+++++++++++++++++++++++++++++++++++++*/
	IF VORG(1)=1 DO
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,80)=1 JOB ^WWWXMLSEND()::1  ;STARTEN JOB FÜR DATENKOMUNIKATION ;launching to 
	. ;
	. LOCK +^INDRPRUNJOB(YM):0 IF '$T DO ^WWWINFO($$^WWWTEXT(144)) QUIT  ;NICHT MÖGLICH (LÄUFT BEREITS) ;"Not possible" 
	. LOCK -^INDRPRUNJOB(YM)
	. JOB ^INDRPSTART1(YM)::1  IF '$T DO ^WWWINFO($$^WWWTEXT(144)) QUIT  ;STARTEN HINTERGRUNDJOB ;"Not possible" on Launching as background process 
	. SET ^INDRPRUNJOB(YM,+$H,$PIECE($H,",",2),1)="1"_Y_YBED_Y_+$H_Y_$PIECE($H,",",2)_Y_$zu(5) ;PROTOKOLL GESTARTET ;protocol ;SR17806
	. ;DRP - PROTOKOLL GESTARTET, AUSWERTUNGSDATEI HINTERGRUNDJOBS
	. ;ULM, PROTOKOLLIERUNG HINTERGRUNDJOBS, 11.03.03       start/STOP                      LOCKDATEI        LOCKSTATUS      DRP-PROZESS "DRP Process"
	. SET ^INBACKJOBS1(YM,"INDRPSTART1",+$H,$PIECE($H,",",2),1)="1"_Y_YBED_Y_"WWW120(0,"_""""_"INDRPRUNJOB"_""""_")"_Y_"1"_Y_$$^WWWTEXT(33330)
	. ;
	. hang .5  ;BR014383
	. ;DO ^WWWINFO($$^WWWTEXT(32746))  ;GESTARTET  ; "DRP Process Started"  ;BR014383
	
	/*++++++++++++++++++++++++++++++++++++++
	;  STOP
	;+++++++++++++++++++++++++++++++++++++*/
	IF VORG(1)=2 DO
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,80)=1 SET ^WWWXML(YM)=1  ;STOPPEN JOB FÜR DATENKOMUNIKATION ;to 
	. ;
	. SET ^INDRPRUNJOB(YM,+$H,$PIECE($H,",",2),1)="0"_Y_YBED_Y_+$H_Y_$PIECE($H,",",2)_Y_$zu(5)  ;PROTOKOLL GESTOPPT ;protocol ;SR17806
	. ;DRP - PROTOKOLL GESTARTET, AUSWERTUNGSDATEI HINTERGRUNDJOBS
	. ;ULM, PROTOKOLLIERUNG HINTERGRUNDJOBS, 11.03.03       start/STOP                      LOCKDATEI        LOCKSTATUS      DRP-PROZESS
	. SET ^INBACKJOBS1(YM,"INDRPSTART1",+$H,$PIECE($H,",",2),1)="2"_Y_YBED_Y_"WWW120(0,"_""""_"INDRPRUNJOB"_""""_")"_Y_"1"_Y_$$^WWWTEXT(33330)
	. ;
	. hang .5 ;BR014383
	. ;DO ^WWWINFO($$^WWWTEXT(32748))  ;GESTOPPT  ; "DRP Process Will Be Stopped (This will take a few minutes)"   ;BR014383
	do RefreshForm^COMUtilForm()   ;BR014383
	QUIT
	
STATUS(R)
	/*------------------------------------------------------------------------------
	;   ANZEIGEN JOB-STATUS ;display process status
	;
	;  R= MIT ODER OHNE RAHMEN ;by means of Or without framework 
	;  		0=EINSPRUNG AUS DRPRUNID  ;MIT LINK, OHNE RAHMEN
	;  		1=EINSPRUNG AUS FORMULAR  ;OHNE LINK, MIT RAHMEN
	;  		2=EINSPRUNG AUS DRPRUN    ;OHNE LINK, OHNE RAHMEN
	;-----------------------------------------------------------------------------*/
	SET R=$GET(R)
	IF R=1 DO FS^WWWTAB($$^WWWTEXT(32782))  ;PROZESS-INFO   ; "Process Info"
	IF R=0 DO
	. NEW YFORM,YKEY
	. WRITE "<A HREF="_""""_YAKTION_"EP=WWWFORM&YFORM=INDRPSTART&YBACK=INDRPRUNID,"
	. DO ^WWWCGI
	. WRITE """"
	. WRITE "TITLE="_""""_$$^WWWTEXT(374)_""""           ; "Select Data Record"
	. WRITE ">"
	
	LOCK +^INDRPRUNJOB(YM):0 IF '$TEST DO  QUIT  ;IN BEARBEITUNG ;within adaptation 
	. NEW LASTRUN,TEXT,LASTDAY
	. SET TEXT="32746"  ;PROZESS GESTARTET
	. SET LASTDAY=$ORDER(^INDRPRUNJOB(YM,""),-1)          ;SUCHEN LETZTER TAG ;seek last TAG 
	. QUIT:LASTDAY=""
	. SET LASTRUN=$ORDER(^INDRPRUNJOB(YM,LASTDAY,""),-1)  ;SUCHEN LETZTER RUN ;seek last 
	. IF LASTRUN'="" DO
	. . IF $PIECE($GET(^INDRPRUNJOB(YM,LASTDAY,LASTRUN,1)),Y,1)'=1 DO
	. . . IF R=2 SET TEXT="32747" QUIT  ;GESTOPPT      ; "DRP Process Stopped"
	. . . SET TEXT="32748"  ;PROZESS WIRD GESTOPPT     ; "DRP Process Will Be Stopped (This will take a few minutes)"
	. . QUIT
	. IF R=1 WRITE "&nbsp;&nbsp;<B>"
	. IF R=0 WRITE "<B>"
	. IF R=2 IF TEXT="32747" WRITE "<FONT COLOR="_YRED_">"
	. WRITE $$^WWWTEXT(TEXT)
	. IF R=2 IF TEXT="32747" WRITE "</FONT>"
	. IF R=0 WRITE "</B></A>"
	. IF R=1 WRITE "&nbsp;&nbsp</B>"
	
	LOCK -^INDRPRUNJOB(YM)
	
	IF R=1 WRITE "&nbsp;&nbsp;<B>"
	IF R=0 WRITE "<B>"
	IF R=2 WRITE "<FONT COLOR="_YRED_">"
	WRITE $$^WWWTEXT(32747)  ;PROZESS GESTOPPT  ; "DRP Process Stopped"
	IF R=2 WRITE "</FONT>"
	IF R=0 WRITE "</B></A>"
	IF R=1 WRITE "&nbsp;&nbsp</B>"
	IF R=1 DO FSE^WWWTAB
	
	QUIT

Schedule()
    ;-------------------------------------------------------------------------------
    ; Run DRP via the scheduler
    ;
    ; Params: nil
    ;
    ; Returns: message
    ;
    ; History:
    ; 12-Mar-2010	SCR		SR17232: Created
    ;-------------------------------------------------------------------------------
	do AUTO
	
	quit $$$Text("IN01209")  ;DRP Started

]]></Routine>
</Export>