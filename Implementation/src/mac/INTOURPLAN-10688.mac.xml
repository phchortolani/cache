<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INTOURPLAN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INTOURPLAN(START) ;INTOURPLAN;FIS;ANZEIGEN TOUREN;08.04.2003
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		ANZEIGEN TOUREN
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	08.04.2003
	;|
	;\------------------------------------------------------------------/
	;
	NEW TAG,YDDSATZ,TOUREN,TOUR,YI,TOUR1,YII,KEY
	SET KEY=""
	IF +$G(START)=0 SET START=+$H
	IF YFORM="INAUFTO"!(YFORM="INAUFTOM2") DO
	. NEW CUST,ADR1
	. SET KEY=$G(^WWWDATEN(YM,+$H,YUSER,YFORM,"P",1))  ;MÖGLICHE TOUREN FÜR DIESEN AUFTRAG AUS VERSAND-PLANUNG ;to order out of 
	. QUIT:KEY=""
	. WRITE "<B>"
	. WRITE $$^WWWTEXT(32047)_": "  ;AUFTRAG ;mandate  ;order 
	. WRITE $P(KEY,",",1)_"-"_$P(KEY,",",2)
	. WRITE "&nbsp;"
	. SET CUST=$P($G(^INAUF(YM,$P(KEY,",",1),1)),Y,1)
	. IF CUST'="" DO
	. . SET ADR1=$P($G(^INKUNDE(YM,CUST,1)),Y,1,24)
	. . IF $D(^INAUFA(YM,$P(KEY,",",1),6,1)) SET ADR1=^(1) SET $P(ADR1,Y,18)=$P($G(^INKUNDE(YM,CUST,1)),Y,18)
	. . IF $D(^INAUFPLA(YM,$P(KEY,",",1),$P(KEY,",",1),1)) SET ADR1=^(1) SET $P(ADR1,Y,18)=$P($G(^INKUNDE(YM,CUST,1)),Y,18)
	. . WRITE " ("_$P(ADR1,Y,8)_", "_$P(ADR1,Y,16)_") "
	. . WRITE $$^WWWFELDNAME("INKUNDE","D",12)_": "_$P(ADR1,Y,12)
	. . IF $P(ADR1,Y,18)'="" WRITE " "_$$^WWWFELDNAME("INKUNDE","D",18)_": "_$P($G(^INPARA(YM,"REGION",SPRACHE,$P(ADR1,Y,18),1)),Y,1)
	. . QUIT
	. WRITE "</B><Hr>",YCR
	. QUIT
	;BUTTONS
	;-------------------------
	DO START^WWWBUTTON(90)
	;VOR ;pre- 
	DO
	. NEW YI,YTXT,YFUNCT,YFUNCT1
	. SET YI=YAKTION_"EP=WWWMANU&YFORM="_YFORM_"&YEXEC=DO|^INTOURPLAN("_(START-7)_")"
	. NEW YFORM
	. DO VAR^WWWCGI
	. DO BUTTON^WWWBUTTON("-"_$$^WWWTEXT(33383),YI,"-"_$$^WWWTEXT(33383))
	. QUIT
	DO ZW^WWWBUTTON
	;ZURÜCK ;retro-  ;back 
	DO
	. NEW YI,YTXT,YFUNCT,YFUNCT1
	. SET YI=YAKTION_"EP=WWWMANU&YFORM="_YFORM_"&YEXEC=DO|^INTOURPLAN("_(START+7)_")"
	. NEW YFORM
	. DO VAR^WWWCGI
	. DO BUTTON^WWWBUTTON("+"_$$^WWWTEXT(33383),YI,"+"_$$^WWWTEXT(33383))
	. QUIT
	DO END^WWWBUTTON
	;-------------------------
	DO START1000^WWWTAB
	DO NL^WWWTAB
	FOR TAG=START:1:START+6 DO
	. DO NF^WWWTAB
	. ;
	. DO START100^WWWTAB
	. DO NL^WWWTAB
	. DO NH^WWWTAB
	. WRITE $P($$^WWWTEXT(30022),",",$$^WWWDAY(TAG))_","  ;WOCHENTAG ;weekday 
	. WRITE "&nbsp;"
	. WRITE $$^WWWDATE(TAG)
	. WRITE "&nbsp;"
	. WRITE YCR
	. DO EH^WWWTAB
	. DO EL^WWWTAB
	. DO STOP^WWWTAB
	. ;
	. DO EF^WWWTAB
	. QUIT
	DO EL^WWWTAB
	DO NL^WWWTAB
	FOR TAG=START:1:START+6 DO
	. DO NF^WWWTAB
	. ;
	. DO START100^WWWTAB
	. SET TOUREN=$$^INTOURCHECK($P(KEY,",",1),$P(KEY,",",2),,TAG,1)  ;ALLE TOUREN AN DIESEM TAG ;upon TAG 
	. IF TOUREN'="" DO
	. . NEW YDDSATZ
	. . SET YDDSATZ=0
	. . ;
	. . ;
	. . FOR YI=1:1  QUIT:$P(TOUREN,Y,YI,999)=""  SET TOUR=$P(TOUREN,Y,YI) DO
	. . . QUIT:TOUR=""
	. . . SET TOUR1=$G(^INSPEDTOUR(YM,$P(TOUR,",",1),$P(TOUR,",",2),1))
	. . . DO NL^WWWTAB
	. . . DO NF^WWWTAB
	. . . ;
	. . . IF KEY'="" IF YFORM="INAUFTO"!(YFORM="INAUFTOM2") DO
	. . . . NEW YKEY,YFORM
	. . . . WRITE YCR,"<A onClick='return doLink(this)' HREF=""JavaScript:opener.window.location='"
	. . . . WRITE YAKTION_"EP=WWWMANU&YFORM=INAUFTO&YEXEC=DO|SAVE^INTOURPLAN(\'"_KEY_"\',\'"_TOUR_"\',"_TAG_")"
	. . . . DO ^WWWCGI
	. . . . WRITE "'; self.close();"
	. . . . WRITE """"
	. . . . ;WRITE " TARGET="_YTARGET
	. . . . WRITE ">"
	. . . . QUIT
	. . . ;
	. . . WRITE "<b>"_$TR(TOUR,",","/")_"</b>"
	. . . IF $P(TOUR1,Y,2)'="" DO  ;KENNZEICHEN ;characteristic 
	. . . . WRITE "<br>",YCR
	. . . . WRITE $P(TOUR1,Y,2)
	. . . . QUIT
	. . . IF $P(TOUR1,Y,1)'="" DO  ;FAHRER ;driver 
	. . . . WRITE "<br>",YCR
	. . . . WRITE $P($G(^WWW013(0,$P(TOUR1,Y,1),1)),Y,1)
	. . . . QUIT
	. . . IF $P(TOUR1,Y,3)'="" DO  ;VOL
	. . . . WRITE "<br>",YCR
	. . . . WRITE $$^WWWSUP($$^WWWFELDNAME("INSPEDTOUR","D",3))_" : "_$$^WWWZAHL($P(TOUR1,Y,3),0,$L($P($P(TOUR1,Y,3),".",2)))
	. . . . QUIT
	. . . IF $P(TOUR1,Y,4)'="" DO  ;KG
	. . . . WRITE "<br>",YCR
	. . . . WRITE $$^WWWSUP($$^WWWFELDNAME("INSPEDTOUR","D",4))_" : "_$$^WWWZAHL($P(TOUR1,Y,4),0,$L($P($P(TOUR1,Y,4),".",2)))
	. . . . QUIT
	. . . IF $P(TOUR1,Y,6)'="" DO  ;GEBIET ;zone 
	. . . . WRITE "<br>",YCR
	. . . . WRITE $$^WWWFELDNAME("INSPEDTOUR","D",6)_":<br>"
	. . . . FOR YII=1:1  QUIT:$P($P(TOUR1,Y,6),";",YII,999)=""  DO
	. . . . . QUIT:$P($P(TOUR1,Y,6),";",YII)=""
	. . . . . WRITE $P($G(^INPARA(YM,"REGION",SPRACHE,$P($P(TOUR1,Y,6),";",YII),1)),Y,1)
	. . . . . IF $P($P(TOUR1,Y,6),";",YII+1)'="" WRITE "<br>",YCR
	. . . . . QUIT
	. . . . QUIT
	. . . IF $P(TOUR1,Y,8)'="" DO  ;PLZ ;ZIP 
	. . . . WRITE "<br>",YCR
	. . . . WRITE $$^WWWFELDNAME("INSPEDTOUR","D",8)_":<br>"
	. . . . FOR YII=1:1  QUIT:$P($P(TOUR1,Y,8),";",YII,999)=""  DO
	. . . . . QUIT:$P($P(TOUR1,Y,8),";",YII)=""
	. . . . . WRITE $P($G(^INPARA(YM,"PLZBEREICHE",SPRACHE,$P($P(TOUR1,Y,8),";",YII),1)),Y,1)
	. . . . . IF $P($P(TOUR1,Y,8),";",YII+1)'="" WRITE "<br>"
	. . . . . QUIT
	. . . . QUIT
	. . . IF KEY'="" IF YFORM="INAUFTO" WRITE "</A>"
	. . . DO EF^WWWTAB
	. . . DO EL^WWWTAB
	. . . QUIT
	. . QUIT
	. ;
	. ;
	. IF TOUREN="" DO
	. . NEW YDDSATZ
	. . SET YDDSATZ=0
	. . DO NL^WWWTAB
	. . DO NF^WWWTAB
	. . WRITE "&nbsp;"
	. . DO EF^WWWTAB
	. . DO EL^WWWTAB
	. . QUIT
	. ;
	. WRITE YCR
	. DO STOP^WWWTAB
	. ;
	. DO EF^WWWTAB
	. QUIT
	DO EL^WWWTAB
	DO STOP^WWWTAB
	QUIT
	
SAVE(KEY,TOUR,TAG) ;SPEICHERN TOUR IN AUFTRAG ;Save voyage within order 
	NEW AUF,POS,SPED,TO
	SET AUF=$P($G(KEY),",",1)
	SET POS=$P($G(KEY),",",2)
	SET SPED=$P($G(TOUR),",",1)
	SET TO=$P($G(TOUR),",",2)
	SET TAG=$G(TAG)
	IF AUF'="" IF POS'="" IF SPED'="" IF TO'="" IF +TAG'=0 DO
	. NEW YFORM,YVOR
	. SET POS1=$G(^INAUFP(YM,AUF,POS,1))
	. SET $P(POS1,Y,76)=TAG
	. SET $P(POS1,Y,95)=SPED
	. SET $P(POS1,Y,317)=TO
	. DO ^WWWSPEI("INAUFP",AUF_","_POS,POS1,1)
	. QUIT
	SET %("VAR","YFKEY")=""
	;SET %("VAR","YKEY")=AUF_","_POS
	;SET %("VAR","YFORM")="INAUFTO"
	DO ^WWWFORM
	QUIT
]]></Routine>
</Export>