<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INItemPolicy" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INItemPolicy	
	
#include INConst
#include %occInclude
#include COMSYS
#include COMConst

OnBeforeFormConstruction()		;SR17034
	set YOPTION1 = '$$DisableProgramFunctionality^INVORG()
	quit
	
OnBeforeSave(pstrKey, pYFELD)
	;-------------------------------------------------------------------------------
	; this gets called before the screen write plus its enclosed in a hyper 
	; event or javascript call!
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History:
	; 10-Aug-2011	shobby	SR17864: Don't error if the record currently being saved
	;								 is the 'Default Policy' record.
	; 15-Jun-2009 	PPP 	SR16510: Created
	;-------------------------------------------------------------------------------
	new idDefaultPolicy,strStatus
 	
	set Q         = $$$QSave
	set strStatus = $$$OK
	
	if '$$$NoKey(pstrKey) {
		if $$$INItemPolicyDefaultPolicy(pYFELD) = $$$YES {
			set idDefaultPolicy = $$GetDefaultPolicy()

			if (idDefaultPolicy '= "")&&(idDefaultPolicy'=pstrKey) {  ;SR17864
				set strStatus = $$$MakeStatus("IN01059",idDefaultPolicy)
			}  ; "Item Policy %1' already flagged as Default Policy"
		}
	}
	
	if $$$ISERR(strStatus) {
	 	do ReturnError^COMUtilError(strStatus)
	 	set Q = $$$QDontSave
 	}
	quit
	
	
GetDefaultPolicy()
	;-------------------------------------------------------------------------------
	; Get the default Policy definition			
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :idPolicy
	;
	; History:
	; 15-Jun-2009 	PPP 	SR16510: Created
	;-------------------------------------------------------------------------------
	new idPolicy,objPolicy

	set idPolicy = ""
	for {
		set idPolicy = $order(^INItemPolicy(YM,idPolicy))
		quit:idPolicy=""
	
		set objPolicy = $get(^INItemPolicy(YM,idPolicy,1))
		quit:$$$INItemPolicyDefaultPolicy(objPolicy)
	}
	quit idPolicy


OnAfterPrimaryKey(pYKEY) 
    ;-------------------------------------------------------------------------------
    ; Set form to readonly if not editable
    ;
    ; Params:
    ;
    ; Returns:
    ;
    ; History:
	; 19-Jun-2009 	PPP 	SR16510: Created
    ;-------------------------------------------------------------------------------
	if '$$$NoKey(pYKEY) {
		if '$$Editable(pYKEY) {
			set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
		}
	}
	
	quit
	
	
CanDelete(pstrKey)
	;-------------------------------------------------------------------------------
	; If the Policy is used by an Item, do not allow the delete.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2009 	PPP 	SR16510: Created
	;-------------------------------------------------------------------------------
 	quit:$$$NoKey(pstrKey)

	if '$$Editable(pstrKey) {
		set Q = $$$QDontDelete
 	}
	
	quit
	
	
Editable(pstrKey)
	;-------------------------------------------------------------------------------
	; If the Policy is used by an Item, do not allow the Edits
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2009 	PPP 	SR16510: Created
	;-------------------------------------------------------------------------------
	new idxKey

	set idxKey = $$$Index(pstrKey)

	quit '$data(^INARTs(YM,18,idxKey))
	
]]></Routine>
</Export>