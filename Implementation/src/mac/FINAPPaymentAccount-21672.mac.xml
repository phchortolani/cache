<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="FINAPPaymentAccount" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
FINAPPaymentAccount
	; 11-Jul-2003	SCR				Bug Fixes from Code Checking
	; 08-Jul-2003	SCR				Bug Fixes from Code Checking
	#Include FIN
	#Include FINConst
ShowAccounts(pidPay,pidPayLine)
	;-------------------------------------------------------------------------------
	;
	; History
	;  5-May-2005	JW			SR12309: Corrected summations
	; 22-Dec-2004	JW			SR11191: Use new intermediate class/form
	; 18-Jun-2004	shobby		Foreign Currency
	; 08-Jul-2003	shobby		Variables
	; 
	;--------------------------------------------------------------------------------
	;
	new prepayment,idExpenseGroup,curAccountsTotal,objPayAccount,objSYSAccountGroup
	new curAmount,curTotalInvoices,objFINAPPaymentGroup,objLine
	
	set prepayment = $$$FINAPPaymentLinePrepayment($get(^FINAPPaymentLine(YM,pidPay,pidPayLine,1)))
	set curTotalInvoices=$$$FCSub($$GetPaying^FINAPPaymentInvoice(pidPay_","_pidPayLine),$$GetDiscountTotal^FINAPPaymentInvoice(pidPay,pidPayLine))
	do Start^COMTable($lb($$$StrFINAPPaymentAccountAccountGroup,$$$StrFINAPPaymentAccountTotal))
	set idExpenseGroup=""
	set curAccountsTotal=0
	for {
	  	;SR11191
		set idExpenseGroup=$order(^FINAPPaymentGroup(YM,pidPay,pidPayLine,idExpenseGroup))
		quit:idExpenseGroup=""
		
		set objFINAPPaymentGroup=$get(^FINAPPaymentGroup(YM,pidPay,pidPayLine,idExpenseGroup,1))
		set curAmount=$$$FINAPPaymentGroupTotal(objFINAPPaymentGroup)
		
		set objSYSAccountGroup=$get(^FINSYSAccountGroup(YM,idExpenseGroup,1))
		do NewLine^COMTable()
	  	;SR11191
		do InsertCell^COMTable($$$FINSYSAccountGroupDescription(objSYSAccountGroup),"FINAPPaymentGroup",pidPay_","_pidPayLine_","_idExpenseGroup)
		do InsertCurrencyCell^COMTable(curAmount,$$$NO,"FINAPPaymentGroup",pidPay_","_pidPayLine_","_idExpenseGroup)
	  
		do EndLine^COMTable()
		set curAccountsTotal=$$$FCAdd(curAccountsTotal,curAmount)
	}
	
	do NewLine^COMTable(YDARKGRAY)
	do InsertCell^COMTable($$^WWWTEXT("Fin00074"))  ;Invoice Total
	do InsertCurrencyCell^COMTable($$$FCSub(curTotalInvoices,curAccountsTotal),$$$NO)
	do EndLine^COMTable()
 
	do NewLine^COMTable(YDARKGRAY)
	do InsertCell^COMTable($$$Text("Fin00761"))  ;Prepayment
	do InsertCurrencyCell^COMTable(prepayment,$$$NO)
	
	do EndLine^COMTable()
	
	do NewLine^COMTable(YDARKGRAY)
	do InsertCell^COMTable($$^WWWTEXT("Fin00071"))  ;Total
	do InsertCurrencyCell^COMTable($$$FCAdd(prepayment,curTotalInvoices),$$$NO)
	do EndLine^COMTable()
	
	do Stop^COMTable()
	quit
	;Manual query used by the Payment line screen to show relevant account details for the payment line.
	quit
 
GetTotalAccount(pidPay,pidPayLine)
    ;-------------------------------------------------------------------------------
    ; Get Total for Accounts
    ;
    ; Returns:
    ;
    ; History:
    ; 01-Jun-2004   SCR			FC Upgrade
    ;-------------------------------------------------------------------------------
	new idExpenseGroup,curAmount,idPayAccount,objPayAccount
	
	set idExpenseGroup=""
	set curAmount=0
	for {
		quit:(pidPay="")||(pidPayLine="") ; In case parms are zero length
		set idExpenseGroup=$order(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup))
		quit:idExpenseGroup=""
		set idPayAccount=""
		for {
			set idPayAccount=$order(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup,idPayAccount))
			quit:idPayAccount=""
			set objPayAccount=$get(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup,idPayAccount,1))
			set curAmount=$$$FCAdd(curAmount,$$$FINAPPaymentAccountTotal(objPayAccount))
			}
		}
	quit curAmount
 
	;returns the total amount for an expense group for a payment line
 
BuildMessage(pidPay,pidPayLine,pPaymentMessage)
	new idExpenseGroup,idPayAccount,objPayAccount,curAmount
	set idExpenseGroup=""
	
	for {
		set idExpenseGroup=$order(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup))
		quit:idExpenseGroup=""
		set idPayAccount=""
		for {
			set idPayAccount=$order(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup,idPayAccount))
			quit:idPayAccount=""
			set objPayAccount=$get(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup,idPayAccount,1))
			set curAmount=$$$FINAPPaymentAccountTotal(objPayAccount)
			if +curAmount'=0 {
				set @pPaymentMessage@("Payments",pidPayLine,"Account",idPayAccount)=curAmount
				set @pPaymentMessage@("Payments",pidPayLine,"Account",idPayAccount,"Reference")=$$$FINAPPaymentAccountReference(objPayAccount)
			}
		}
	}
	quit
 
CreateAccounts(pidPay,pidPayLine)
    ;-------------------------------------------------------------------------------
    ; History:
    ; 22-Dec-2004	JW		SR11191: Added intermediate global for Grid
    ;-------------------------------------------------------------------------------
 
	new idExpenseGroup,lstAccounts,lngNumAccounts,idAccount,lngAccountLoop
	set idExpenseGroup=""
	for {
		set idExpenseGroup=$order(^FINSYSAccountGroup(YM,idExpenseGroup))
		quit:idExpenseGroup=""
		
		set lstAccounts=$$GetAccounts^FINSYSAccountGroup(idExpenseGroup)
		set lngNumAccounts=$listlength(lstAccounts)
		
		if (lngNumAccounts>0) { ;SR11191
			if '$data(^FINAPPaymentGroup(YM,pidPay,pidPayLine,idExpenseGroup,1)) {
				do Save^FINSYS("FINAPPaymentGroup",pidPay_","_pidPayLine_","_idExpenseGroup,0,1)
			}
		}
		
		for lngAccountLoop=1:1:lngNumAccounts {
			set idAccount=$listget(lstAccounts,lngAccountLoop)
			if '$data(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpenseGroup,idAccount,1)) {
				do Save^FINSYS("FINAPPaymentAccount",pidPay_","_pidPayLine_","_idExpenseGroup_","_idAccount,0,1)
			}
		}
	}
	quit
 
KillAccounts(pidPay,pidPayLine,pblnZero)
    ;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:	status
	;
    ; History:
    ; 19-Oct-2006	JW		SR15134: Look at kill status. Added return value.
    ; 22-Dec-2004	JW		SR11191: Added intermediate global for Grid
    ;-------------------------------------------------------------------------------
	new idExpense,idAccount,objAccount,objGroup,strStatus
	set strStatus = $$$OK
	set idExpense=""
	for {
		set idExpense=$order(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpense))
		quit:idExpense=""
		quit:$$$ISERR(strStatus)
		
		set objGroup=$get(^FINAPPaymentGroup(YM,pidPay,pidPayLine,idExpense,1)) ;SR11191
		if (+$$$FINAPPaymentGroupTotal(objGroup)=0)||(pblnZero) {
			set strStatus = $$$Kill("FINAPPaymentGroup",pidPay_","_pidPayLine_","_idExpense)		//SR15134
			
			set idAccount=""
			for {
				set idAccount=$order(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpense,idAccount))
				quit:idAccount=""
				quit:$$$ISERR(strStatus)
				
				set objAccount=$get(^FINAPPaymentAccount(YM,pidPay,pidPayLine,idExpense,idAccount,1))
				if (+$$$FINAPPaymentAccountTotal(objAccount)=0)||(pblnZero) {
					set strStatus = $$$Kill("FINAPPaymentAccount",pidPay_","_pidPayLine_","_idExpense_","_idAccount)	//SR15134
				}
			}
		}
	}
	quit strStatus
	
	
OnBlur()	// 24-May-2005 JW - No longer in use
    ;-------------------------------------------------------------------------------
    ; On blur event for total field
    ;
    ; Returns:
    ;
    ; History:
    ; 22-Dec-2004	JW		SR11191 Created
    ;-------------------------------------------------------------------------------
 
	//do UpdateTotal^FINAPPaymentGroup()
	
	quit
 
 
UpdateContainer(pidField="",pobjGrid,pstrHeadYFELD="",pstrHeadYMFELD="")
    ;-------------------------------------------------------------------------------
    ; Run OnAfterDataFields, OnBlur and OnDelete
    ; 
    ; Returns:
    ; 
    ; History:
    ; 30-Sep-2005	JW			SR11573: Param is now class field not form col
    ; 24-May-2005	JW		SR12415: Created
    ;-------------------------------------------------------------------------------
	new idBank,idLine,idGroup,idAccount,objLine,total
	
	if (pidField="")||($$$FldFINAPPaymentAccountTotal=pidField) {
	
		set total=0
		set idBank=$order(pobjGrid(""))
		if idBank'="" {
			set idLine = $order(pobjGrid(idBank,""))
			if idLine'="" {
				set idGroup = $order(pobjGrid(idBank,idLine,""))
				if idGroup'="" {
					set idAccount=""
					for {
						set idAccount=$order(pobjGrid(idBank,idLine,idGroup,idAccount))
						quit:idAccount=""
						
						set objLine=$get(pobjGrid(idBank,idLine,idGroup,idAccount))
						set total=total+$$$FINAPPaymentAccountTotal(objLine)
					}
				}
			}
		}
		
		set $$$FINAPPaymentGroupTotal(pstrHeadYFELD) = total
	}
	quit
 
 
 
DisableButton(YKEY)
    ;-------------------------------------------------------------------------------
    ; Disable add accounts button
    ;
    ; Returns:
    ;
    ; History:
    ; 24-May-2005	JW		Created SR12415
    ;-------------------------------------------------------------------------------
	new isPay
	
	set isPay = $piece($get(YKEY),",",1)
	
	if isPay="" || '$$Editable^FINSYSReview("FINAPPayment",$$$FINAPPaymentStatus($get(^FINAPPayment(YM,isPay,1)))) {
		
		set Q=$$$QDontSave
	}
	
	quit
]]></Routine>
</Export>