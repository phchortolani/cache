<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINDispenseToPatient" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINDispenseToPatient
#include COMSYS
#include INConst
#include VARConst
#include MEDConst

OnBeforeFormConstruction()
	if YPARA = "" set YPARA = YAUSWAHL	
	
	kill ^CacheTempAlertaPaciente
			
	if (YOPTION = "") && (YPARA '= "") {
		set YOPTION = YPARA	
		kill ^VARTempPatientYOPTION(YBED)
		set ^VARTempPatientYOPTION(YBED) = YPARA
	
	} elseif (YOPTION = "") && (YPARA = "") {
		set YOPTION = $get(^VARTempPatientYOPTION(YBED))
		set YPARA = $get(^VARTempPatientYOPTION(YBED))
	}
	
	quit
	
OnBlurProduto(pYM,pYFORM,pYKEY,&pYFELD,pGridRow,pGridCol,pYINHALT)	
	;do OnBlurProduto^VARINDispenseToPatient(YM,YFORM,YKEY,.YFELD,GROW,GCOL,.YINHALT)
	;
	;Petik 08/06/2010
	;Rotina criada para validar se o produto pode ser usado por determinado paciente
	;Só será executada se o local for 18.1 - Farmácia de Ações Judiciais
	;Caso contrário, só chama a rotina core normalmente
	
	;Esta rotina está sendo chamada no evento OnBlur do campo Item do form INDispenseToPatientLine
	
	;seta o YPARA do INDispenseToPatientLine igual a do INDispenseToPatient
	set YPARA = $piece(^WWWDATEN(YM,$$$KEY1($horolog),YUSER,"INDispenseToPatient","D",1),"~",24)
	
	if ((YLOCATION = $$getFarmaciaAcoesJudiciais^VARParametroCliente(0))&&(pYINHALT '= ""))&&(YPARA = 3){
		;Verifica se tem estoque para o produto no endereço do paciente
		set Produto  = pYINHALT
		set objDispensacao = $$$GRIDYFELDContainer
		set Paciente = $$$INIssueCustomerName(objDispensacao)
		
		set objResultSet = ##class(%Library.ResultSet).%New("alSOH.dBundleStock:GetViaLocation")
		//if objResultSet.Execute(objItem.%Id(),objLocn.%Id()) {
		if objResultSet.Execute(Produto,YLOCATION) {
			
			set idLine = 0
			set Achou = $$$NO
			;Trecho de código copiado da rotina BuildDynTableRows^INDispenseToPatientTable
			;++++++++++
			while objResultSet.Next() {
				set objBundleStock = ##class(alSOH.dBundleStock).%OpenId(objResultSet.Data("ID"))

				set dteVal=objBundleStock.Bundle.UseByDate
				if dteVal'=""	{
					continue:(dteVal < $horolog)	//SR16540
				}
				continue:($get(arrBundleStock(objBundleStock.%Id())) >= objBundleStock.QtyOnHand)
				set fltQtyAvailable = objBundleStock.QtyOnHand-$$GetQtyPlanned^INDispenseToPatientTable(objBundleStock.Item.%Id(),objBundleStock.Storage.Location.%Id(),objBundleStock.Storage.%Id(),objBundleStock.Bundle.%Id())
				continue:(fltQtyAvailable<=0)
				
				;Se achar o produto no Endereço igual ao código do cliente
				;altera variável Achou para $$$YES
				if (objBundleStock.Storage.Code = Paciente){
					set Achou = $$$YES
					quit	;quit do while
				}

			}	;end while
			
			;++++++++++++ Fim do trecho copiado
			
			if (Achou = $$$NO) {
				set strMessage = "O produto "_Produto_" não está disponível para esse paciente!"
				&js<
					alert("<%=strMessage%>");
				>
				;Limpa o campo Produto
				set $piece(pYFELD,Y,8)  = ""
				quit
					
			}
			
			
		}	;end if
		
	}
	
	;Se o YLOCATION não for 18.1 ou se tiver passado pela rotina acima,
	;chama a rotina core normalmente
	
	do OnBlur^INDispenseToPatientLine(pYM,pYFORM,pYKEY,.pYFELD,pGridRow,pGridCol,pYINHALT)
	
	quit

OnBlurPaciente(pYFELD="")
	quit:(pYFELD = "")
	
	new objAdm,idAdm,idPaciente,idAuta,idFIA,idBAA,idUnidade,found
	
	if ($$Lower^COMUtilStr($extract($piece(pYFELD,Y,23),1,5)) = "90i01"){
		set $piece(pYFELD,Y,23) = $extract($piece(pYFELD,Y,23),6,*)
		set $piece(YFELD,Y,23)  = $piece(pYFELD,Y,23)
		if ($piece(pYFELD,Y,23) = ""){
			set $piece(pYFELD,Y,23) = ""
		}
	}

	set idPaciente = $piece(pYFELD,Y,23)
	quit:(idPaciente="")
	set found=0
	set idAdm = $order(^MEDAdmissions(YM,100,idPaciente,""),-1)
		
	while ((idAdm'="") && (found=0)) {

		set objAdm 		= $get(^MEDAdmission(0,idAdm,1))
		set idAlta 		= $$$MEDAdmissionDateDischarged(objAdm)
		set idUnidade 	= $$$MEDAdmissionLocation(objAdm)
		set idFIA 		= $$$MEDAdmissionFREE4(objAdm)
		set idBAA 		= $$$MEDAdmissionFREE5(objAdm)

		if (idAlta = "") {
			set found=1
			set $piece(YFELD,Y,14) = idUnidade
			set $piece(YFELD,Y,15) = idFIA
			set $piece(YFELD,Y,16) = idBAA
			set $piece(YFELD,Y,17) = idAdm
		}
		set idAdm = $order(^MEDAdmissions(YM,100,idPaciente,idAdm),-1)
	}

	if '$Data(^CacheTempAlertaPaciente(YUSER)){
		set ^CacheTempAlertaPaciente(YUSER) = 0
	}

	if ($$CheckVincularPacAdmAtivas^VARINDispenseToPatient()){
	
		if ('found) && (($piece(YFELD,Y,17) '= "") ||($piece(YFELD,Y,23) '= ""))  {
			if (^CacheTempAlertaPaciente(YUSER)'=$piece(YFELD,Y,17)){
				$$$Alert("Por favor, selecione um paciente que possua uma admissão aberta.")
			}
			set ^CacheTempAlertaPaciente(YUSER) = $piece(YFELD,Y,17)		
			set $piece(YFELD,Y,17) = ""
			set $piece(YFELD,Y,23) = ""
			set $piece(YFELD,Y,14) = ""
			set $piece(YFELD,Y,15) = ""
			set $piece(YFELD,Y,16) = ""
			quit
		}
	
		new idPaciente, noProcessoJudicial, noAutorizacao, objAutorizacao, position
	
		kill ^COMTempList(YM,YBED,"PacienteProcessoJudicial")
		kill ^COMTempList(YM,YBED,"RetiradaAutorizadaPaciente")

		set idPaciente = $piece(pYFELD,Y,23)
		quit:(idPaciente = "")

		set %TXT(1) = ""	
		set %TXT(1)=%TXT(1)_"#YINDispenseToPatientD11~REMOVE"
		set %TXT(1)=%TXT(1)_"#YINDispenseToPatientD12~REMOVE"	
	
		set position = 0
		//09-Nov-11  Karine: alterado para funcionar a relação de classe
		;$$$Order3(^VARProcessoJudicial,YM,idPaciente,noProcessoJudicial)
		$$$Order4(^VARProcessoJudicials,YM,1,idPaciente,noProcessoJudicial)
	
			set position = $i(position)
	
			set %TXT(1) = %TXT(1)_"#YINDispenseToPatientD11"_"~"_noProcessoJudicial_"~"_noProcessoJudicial_"~"_position
			set ^COMTempList(YM,YBED,"PacienteProcessoJudicial",noProcessoJudicial,1) = noProcessoJudicial
		$$$End

		set position = 0
	
		$$$Order3(^VARPacienteAutorizacao,YM,idPaciente,noAutorizacao)		
			set objAutorizacao = $get(^VARPacienteAutorizacao(YM,idPaciente,noAutorizacao,1))		
			set nomeResponsavel = $piece(objAutorizacao,Y,1)
		
			set position = $i(position)
	
			set %TXT(1) = %TXT(1)_"#YINDispenseToPatientD12"_"~"_noAutorizacao_"~"_nomeResponsavel_"~"_position
			set ^COMTempList(YM,YBED,"RetiradaAutorizadaPaciente",noAutorizacao,1) = nomeResponsavel
		$$$End
	}
	
	else {
		
		if (idAdm = "") {
			set idAdm = $order(^MEDAdmissions(YM,100,idPaciente,idAdm),-1)
			
			while ((idAdm'="") && (found=0)) {
			
				set objAdm 		= $get(^MEDAdmission(0,idAdm,1))
				set idAlta 		= $$$MEDAdmissionDateDischarged(objAdm)
				set idUnidade 	= $$$MEDAdmissionLocation(objAdm)
				set idFIA 		= $$$MEDAdmissionFREE4(objAdm)
				set idBAA 		= $$$MEDAdmissionFREE5(objAdm)
				
				if ($$$MEDAdmissionDateAdmitted(objAdm) >= (+$horolog-3)){
					set found=1
					set $piece(YFELD,Y,14) = idUnidade
					set $piece(YFELD,Y,15) = idFIA
					set $piece(YFELD,Y,16) = idBAA
					set $piece(YFELD,Y,17) = idAdm					
				}
				set idAdm = $order(^MEDAdmissions(YM,100,idPaciente,idAdm),-1)
			}
		}
		
		if ('found) && (($piece(YFELD,Y,17) '= "") ||($piece(YFELD,Y,23) '= ""))  {
			if (^CacheTempAlertaPaciente(YUSER)'=$piece(YFELD,Y,17)){
				$$$Alert("Por favor, selecione um paciente que possua uma admissão aberta ou que foi aberta nas últimas 72 horas.")
			}
			set ^CacheTempAlertaPaciente(YUSER) = $piece(YFELD,Y,17)		
			set $piece(YFELD,Y,17) = ""
			set $piece(YFELD,Y,23) = ""
			set $piece(YFELD,Y,14) = ""
			set $piece(YFELD,Y,15) = ""
			set $piece(YFELD,Y,16) = ""
			quit
		}
	
		new idPaciente, noProcessoJudicial, noAutorizacao, objAutorizacao, position
	
		kill ^COMTempList(YM,YBED,"PacienteProcessoJudicial")
		kill ^COMTempList(YM,YBED,"RetiradaAutorizadaPaciente")

		set idPaciente = $piece(pYFELD,Y,23)
		quit:(idPaciente = "")

		set %TXT(1) = ""	
		set %TXT(1)=%TXT(1)_"#YINDispenseToPatientD11~REMOVE"
		set %TXT(1)=%TXT(1)_"#YINDispenseToPatientD12~REMOVE"	
	
		set position = 0
		//09-Nov-11  Karine: alterado para funcionar a relação de classe
		;$$$Order3(^VARProcessoJudicial,YM,idPaciente,noProcessoJudicial)
		$$$Order4(^VARProcessoJudicials,YM,1,idPaciente,noProcessoJudicial)
	
			set position = $i(position)
	
			set %TXT(1) = %TXT(1)_"#YINDispenseToPatientD11"_"~"_noProcessoJudicial_"~"_noProcessoJudicial_"~"_position
			set ^COMTempList(YM,YBED,"PacienteProcessoJudicial",noProcessoJudicial,1) = noProcessoJudicial
		$$$End

		set position = 0
	
		$$$Order3(^VARPacienteAutorizacao,YM,idPaciente,noAutorizacao)		
			set objAutorizacao = $get(^VARPacienteAutorizacao(YM,idPaciente,noAutorizacao,1))		
			set nomeResponsavel = $piece(objAutorizacao,Y,1)
		
			set position = $i(position)
	
			set %TXT(1) = %TXT(1)_"#YINDispenseToPatientD12"_"~"_noAutorizacao_"~"_nomeResponsavel_"~"_position
			set ^COMTempList(YM,YBED,"RetiradaAutorizadaPaciente",noAutorizacao,1) = nomeResponsavel
		$$$End
		
	}
 
	quit
	
PrintDevolucoes(pYKEY)
	quit:(pYKEY = "")
	quit:( '$data(^INIssueRevs(YM,2,9,$$$Index(pYKEY))) )

	write "<br /><br />"
			
   	new lstHeader, count, idIssueRev, objIssueRev, dteIssueRev, idIssueRevLine, objINIssueRevLine, 
   		idItem, fltQty, reason
   	
	set lstHeader=""
	set lstHeader=lstHeader_$listbuild("Seq.","No. Devolução","Data","Produto","Quant. Devolvida","Motivo")

  	if $$Start^COMTable(lstHeader,"&nbsp;Devoluções feitas para esta dispensação",,,$$$NO) {

		$$$Order5(^INIssueRevs,YM,2,9,$$$Index(pYKEY),idIssueRev)
			set objIssueRev = $get(^INIssueRev(YM,idIssueRev,1))
			set dteIssueRev = $$$INIssueRevPosted(objIssueRev)
			
			$$$Order3(^INIssueRevLine,YM,idIssueRev,idIssueRevLine)
				
				set objINIssueRevLine = $get(^INIssueRevLine(YM,idIssueRev,idIssueRevLine,1))
				
				set idItem = $$$INIssueRevLineItemNo(objINIssueRevLine)
				set fltQty = $$$INIssueRevLineQuantityReturned(objINIssueRevLine)
				set reason = $$$INIssueRevLineReason(objINIssueRevLine)
				
				set count = $i(count)
			
				do NewLine^COMTable()
				do InsertCell^COMTable(count)
				do InsertCell^COMTable(idIssueRev,"INIssueRev",idIssueRev)
				do InsertCell^COMTable($zdate(dteIssueRev,4),"INIssueRev",idIssueRev)
				do InsertCell^COMTable(idItem_" - "_$$SQLGetDescricaoProduto^VARSQL(idItem),"INIssueRev",idIssueRev,,,,,,,$$$YES)
				do InsertCell^COMTable($$^WWWTR(0,12,fltQty),"INIssueRev",idIssueRev,,"RIGHT")
				do InsertCell^COMTable(reason,"INIssueRev",idIssueRev,,,,,,,$$$YES)
				do EndLine^COMTable()   
			
			$$$End

		$$$End

	  }

	quit
	
	
ImprimeRelatorio(YKEY)
	if ($$GetPreferencia^VARPreferenciasUsuario($$$FldVARPreferenciasUsuarioTelaFiltroRelatorio) = 1){
		do GotoFiltrosRelatorio^VARFiltrosRelatorio("RunReportDispPaciente",0,1,,YKEY)
	} else {
		do RunReportDispPaciente^VARJasperRunReport(YKEY)
	}
		
	quit
	
CanImprimirEtiqueta(pYKEY)
	quit:pYKEY="" $$$YQDisable("Selecione um registro.")
	
	new objDispense,idPaciente
	
	set YQ = $$$YQEnable
	
	set objDispense = $get(^INIssue(0,pYKEY,1))
	set idPaciente 	= $$$INIssueCustomerName(objDispense)

	set:idPaciente="" YQ = $$$YQDisable("Não há paciente para essa dispensação.")
	
	quit 
	
ImprimirEtiqueta(pYKEY)
	new objDispense,idPaciente,idSaida
	
	set objDispense = $get(^INIssue(0,pYKEY,1))
	set idPaciente 	= $$$INIssueCustomerName(objDispense)
	set idSaida     = pYKEY

	if ('$data(^MEDPatient(YM,idPaciente,1))) {
		$$$Alert("Informe um registro válido de paciente.")
		do ReloadForm^COMUtilForm
	}else{
		do RunReportPacienteEtqDisp^VARJasperRunReport(idPaciente,idSaida)
	}
	
	quit

ImprimeRelatorioDispensacao(YKEY)
	new objDispense,idSaida
	
	set objDispense = $get(^MEDDispense(0,YKEY,1))
	set idSaida		= $$$MEDDispenseIssue(objDispense)
	
	if (idSaida = "") {
		$$$Alert("É necessário processar a dispensação.")
		do ReloadForm^COMUtilForm
	}else{
		set ^VARTempMEDDispenseSeparacao(YM,YBED) = $$$NO
		do RunReportDispPaciente^VARJasperRunReport(idSaida)
	}
	
	quit

ImprimirEtiquetaDispensacao(pYKEY)
	new objDispense,idPaciente,idSaida,prescricao,objPrescription
	
	set objDispense = $get(^MEDDispense(0,pYKEY,1))
	set prescricao 	= $$$MEDDispensePrescription(objDispense)
	set idSaida		= $$$MEDDispenseIssue(objDispense)
	
	
	if prescricao'=""{
		set objPrescription = $get(^MEDPrescription(0,prescricao,1))
		set idPaciente 	= $$$MEDPrescriptionPatientID(objPrescription)
	}

	if ('$data(^MEDPatient(YM,idPaciente,1))) {
		$$$Alert("Informe um registro válido de paciente.")
		do ReloadForm^COMUtilForm
	}else{
		set ^VARTempMEDDispenseSeparacao(YM,YBED) = $$$NO
		do RunReportPacienteEtqDisp^VARJasperRunReport(idPaciente,idSaida)
	}
	
	quit
	
CheckVincularPacAdmAtivas()
	$$$VAR
	set status = $$$NO
	set objParametroCliente = $get(^VARParametroCliente(YM,YM,1))
	if ($find($$$VARParametroClienteVincularPacAdmAtiva(objParametroCliente),YLOCATION)){
		set status = $$$YES
	}
	quit status
	
Postable(pidIssue)
	quit:$$$NoKey(pidIssue) '$$$OK
	set strStatus = $$$OK
	set objIssue = $get(^INIssue(0,pidIssue,1))
	set objAdm = $get(^MEDAdmission(0, $$$INIssueFREE7(objIssue),1))
	quit:($$$MEDAdmissionDateDischarged(objAdm) = "") strStatus
	if ($$CheckVincularPacAdmAtivas^VARINDispenseToPatient()){
		if ($$$MEDAdmissionDateDischarged(objAdm) '= "") {
			set strStatus = $$$MakeStatus("Por favor, selecione um paciente que possua uma admissão aberta.")
		}
	}
	else {
		if (($$$MEDAdmissionDateDischarged(objAdm) '= "") && ($$$MEDAdmissionDateAdmitted(objAdm) < (+$horolog-3))){
			set strStatus = $$$MakeStatus("Por favor, selecione um paciente que possua uma admissão aberta ou que foi aberta nas últimas 72 horas.")				
		}	
	}
	quit strStatus
	
OnBlurAdmissao(pYFELD="")
	quit:(pYFELD = "")

	if '($$CheckVincularPacAdmAtivas^VARINDispenseToPatient()){
		new objAdm,idAdm,idAlta,idFIA,idBAA,idUnidade
	
		set idAdm = $piece(pYFELD,Y,17)
		quit:(idAdm = "")
		set objAdm 		= $get(^MEDAdmission(0,idAdm,1))
		set idAlta 		= $$$MEDAdmissionDateDischarged(objAdm)
		set idUnidade 	= $$$MEDAdmissionLocation(objAdm)
		set idFIA 		= $$$MEDAdmissionFREE4(objAdm)
		set idBAA 		= $$$MEDAdmissionFREE5(objAdm)
	
		set $piece(YFELD,Y,14) = idUnidade
		set $piece(YFELD,Y,15) = idFIA
		set $piece(YFELD,Y,16) = idBAA
		set $piece(YFELD,Y,17) = idAdm					
	}
	else {
		new objAdm,idAdm,idAlta,idFIA,idBAA,idUnidade
	
		set idAdm = $piece(pYFELD,Y,17)
		quit:(idAdm = "")
		set objAdm 		= $get(^MEDAdmission(0,idAdm,1))
		set idAlta 		= $$$MEDAdmissionDateDischarged(objAdm)
		set idUnidade 	= $$$MEDAdmissionLocation(objAdm)
		set idFIA 		= $$$MEDAdmissionFREE4(objAdm)
		set idBAA 		= $$$MEDAdmissionFREE5(objAdm)
	
		if (idAlta = ""){
			set $piece(YFELD,Y,14) = idUnidade
			set $piece(YFELD,Y,15) = idFIA
			set $piece(YFELD,Y,16) = idBAA
			set $piece(YFELD,Y,17) = idAdm			
		}
		else {
			$$$Alert("Por favor, selecione uma admissão aberta.")	
			set $piece(YFELD,Y,14) = ""
			set $piece(YFELD,Y,15) = ""
			set $piece(YFELD,Y,16) = ""
			set $piece(YFELD,Y,17) = ""
		}
	}
	quit
]]></Routine>
</Export>