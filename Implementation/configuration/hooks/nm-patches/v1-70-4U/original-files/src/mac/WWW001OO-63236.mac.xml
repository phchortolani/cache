<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW001OO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW001OO
#include WWWConst
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		WWW001OO
	;		@net-Klassen zu Cache-Klassen mit Hilfe des %Dictionary-Packets konvertieren
	;
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 24-Jul-2007	Frank	BR014485: Comment out the usage of MAXVAL property.
	; 							If we set the MAXVAL of a integer datafield to be
	; 							10, the only allowed values are 0,1,2,3,...,10.
	; 							It has nothing to do with the size of the field. 
	; 11-Jul-2006	RPW		SR11964: If we fail to delete the object, quit with the
	; 							error.
	; 08-Feb-2006	Frank F	SR14278: Keeping the current class import statements in
	; 							the new created class.
	; 28-Oct-2005	Frank F	SR13353: Keeping the current class methods in the new
	; 							created class.
	; 07-Jul-2005	Steve S	SR12854: Added Calc fields for currencies
	; 22-Jun-2005	SCR		SR12755: Added Calculated fields
	; 05-Apr-2005	Paul K	Added comment for change in SR#11714. Counter fields
	; 							can be strings
	; 12.07.2004	OJR
	; 17.04.2004	ARI
	;-------------------------------------------------------------------------------
	
COMPILE(pidClass,NOFORM,&sc)
	;-------------------------------------------------------------------------------
	;	Kompilierung aus Formular WWW001 heraus
	; Inputs:
	; 	pidClass	; Class ID
	;	NOFORM=1	; 1 - don't display messages through browser
	;				; 0 - called from a form - display messages
	;	sc			; By Ref (Status)
	;
	; History:
	; 29-Jul-2010	SCR		SR17471: Include SubClasses in the compile
	; 21-Jul-2009	PPP		SR16757: Added new By Ref Parameter 'sc' - returns status
	; 04-Mar-2009	GRF		SR16410: Stricter enforcement of "Do Not Compile This Class"
	; 23-Feb-2009	shobby	SRAdhoc: Error message is corrupted by characters 13 & 10
	; 05-Jan-2006	SC		SR14136: Don't add strPackName prefix to pidClass when
	; 							pidClass contains a ".", ie "a.b" 
	;-------------------------------------------------------------------------------
	; Actions Performed
	; 
	; 1. CREATE
	;   a) Pre-processing validation
	;     - CheckForDoubles
	;     - WWWSORTKEY
	;     - FixPropNames
	;   b) Preserve Existing (Non-calculation) Methods in User.Class
	;   c) Delete User.Class
	;   d) Create new User.Class from WWW001 data
	;     - Add Properties from WWW002 and WWW003
	;     - Add Currency Calculations as Properties
	;     - Add Calculated Fields as Properties
	;   e) Add Index entrie to User.Class
	;   f) Build SQL storage mapping
	;    - Include @netManager Indices as Maps
	;   g) Restore Preserved Methods
	;   
	; 2. $SYSTEM.OBJ.Compile("User.Class","-d+b",.arrError)
	; 
	;-------------------------------------------------------------------------------
	new arrError,blnDontCompile,objANMClass,strClassName,strPackName
	
	set sc = $$$OK
	
	quit:$get(pidClass)=""
	
	set strPackName = "User"  ;will be Parameter ^WWW012(0,YM,1)...
	
	; If WWW001 D21 "No Compile (mapped class)" is ticked, a User.xxxxx class will not be generated from the @nM class.
	; Only where class contains a "." in the name and where NOFORM isn't $$$YES.
	
	set objANMClass = $get(^WWW001(0,pidClass,1))
	set blnDontCompile = $$$WWW001NoCompilemappedclass(objANMClass)
	if blnDontCompile {
		if $get(NOFORM) '= $$$YES {
			do SCRPTAL^WWWTOOLS("Class is mapped. Not compiled.")
			do ^WWWFORM
		}	
		quit                                         ; *** EARLY EXIT ***
	}
	
	;---------------------------------------
	
	set sc = $$CREATE(pidClass)
	set strClassName = $select(pidClass[".":"",1:strPackName_".")_pidClass  //SR14136
 
	;if $$$ISOK(sc) set sc = $SYSTEM.OBJ.Compile(strClassName,"-d",.arrError)  
	if $$$ISOK(sc) set sc = $SYSTEM.OBJ.Compile(strClassName,"-d+b",.arrError)  ; SR17471
	
	; Display messages if called from a form
	if $get(NOFORM) '= $$$YES {
		if $$$ISERR(sc) {
		;	FIXME : If CREATE returns $$$NO then this will display "ERROR #00: (no error description)"
		;	        This can happen if we try to compile a class with WWW001 but no WWW002 or WWW003. <GRF>
			do $SYSTEM.Status.DecomposeStatus(sc,.arrError)
		;	do SCRPTAL^WWWTOOLS($translate($$^WWWUMLAU(ERROR(1)),"'"))      ;SRAdhoc    ; FIXME : Why is this different from below? <GRF>
			do SCRPTAL^WWWTOOLS($translate(arrError(1),$$$CRLF))      
		} else {
			;set sc = $SYSTEM.OBJ.Compile(strClassName,"-d",.arrError) 
			set sc = $SYSTEM.OBJ.Compile(strClassName,"-d+b",.arrError) ; SR17471 
			
			if sc {
				do SCRPTAL^WWWTOOLS($$^WWWTEXT("WWW00061")) ; "Compile Done."
			
			} else {   ; Convert each actual double quote to 3 character representation and all $$$CRLF to "new line" instruction
				do SCRPTAL^WWWTOOLS($$SUBSTR^WWWTOOLS($$SUBSTR^WWWTOOLS($$^WWWUMLAU(arrError(1)),"""","#34"),$$$CRLF,"\n"))
			}
		}
		do ^WWWFORM
	}
	quit
	
	
CREATEALL ;
	do ^WWWBACK
	new A,OK,START
	set START = $PIECE($HOROLOG,",",2)
	set A = ""
	for  set A = $ORDER(^WWW001(0,A)) QUIT:A=""  DO
	. IF '$DATA(^WWW002(0,A)),'$DATA(^WWW003(0,A)) QUIT
	. set OK = $$CREATE^WWW001OO(A)
	. IF 'OK DO $SYSTEM.OBJ.DisplayError(OK) 
	. WRITE " ",A,!,YCR,"<BR> "
	
	;WRITE !,$PIECE($HOROLOG,",",2)-START
	quit
	
CREATEPART ;
	do ^WWWBACK
	IF $GET(SPRACHE)="" set SPRACHE="EN"   ;TYBD;17.05.2004;
	set VORG(1)=$TRANSLATE($GET(VORG(1)),",",";")
	
	;IF VORG(1)="" IF VORG(3)=1 DO CREATEALL Q  ;COMPILE ALL
	
	set VORGX = "" 
	FOR  set VORGX = $ORDER(^WWW001(0,VORGX)) QUIT:VORGX=""  DO
	. IF VORG(1)'="" QUIT:'$FIND(";"_VORG(1)_";",";"_VORGX_";")  ;NICHT AUSGEWÄHLT
	. IF '$DATA(^WWW002(0,VORGX)) QUIT
	. QUIT:VORGX="WWW000"                                        ;ROUTINE
	. IF '$DATA(^WWW002(0,VORGX)),'$DATA(^WWW003(0,VORGX)) QUIT
	. set OK = $$CREATE^WWW001OO(VORGX)
	. WRITE "<BR>",YCR,VORGX," "
	. IF 'OK DO $SYSTEM.OBJ.DisplayError(OK) QUIT
	. WRITE "OK"
	
	quit
	
	
CREATE(pidClass)
	;-------------------------------------------------------------------------------
	; History :
	; 30-Jul-2010	SCR		SR17471: Set MAXLEN to 300 for Free fields 
	; 28-Jul-2010	SCR		SR17471: Add %OnBeforeSave method for @NM Audit
	; 10-Jun-2010	PPP		SR17369: Added SIZE ie 32000 to the memo field (%String) - as per SR16701
	; 28-Oct-2009	GRF		-: Missing parentheses around IF clause
	; 17-Jul-2009	shobby	SR16701: Changed Memo fields to %String from %Stream
	; 06-Mar-2009	GRF		SR16410: Stricter enforcement of "Do Not Compile This
	; 							Class"; class & boolean macros; reuse class objects;
	; 							get text once as strCompany; separate some processes
	; 19-Dec-2008	shobby	SR16248: Put back the code that preserves existing methods
	; 							with the exception of any calculated methods which will
	; 							be regenerated anyway.
	; 02-Oct-2008	shobby	BR014985: Make classes inherit from XML.Adaptor
	; 30-Jul-2007	GRF		SR15571: STORAGE not STOARAGE
	; 13-Jul-2007	RPW		SR15571: Use WWW.DiscTimestamp so that object access and
	; 							global access for timestamps are the same
	; 15-May-2007	RPW/GRF	SR15516: Do not worry about customisation on compilation,
	; 							it's not nice.  Naked References
	; 29-Mar-2007	RPW		SR15488: If we are 2007, use SetAt for parameters not
	; 							Data, looks based on Caché4 Syntax not 5.
	;-------------------------------------------------------------------------------
	if $get(pidClass)=""                                          quit $$$NO
	if '$data(^WWW001(0,pidClass))                                quit $$$NO
	if '$data(^WWW002(0,pidClass)) && '$data(^WWW003(0,pidClass)) quit $$$NO
	
	new arrClassMethods,ATNETTYPE,blnIs2007,blnSharedFile,CACHECLASS,CAPTION,CLASS,CLASSX,DATA
	new FELDNR,GLOBAL,GLOBREF,IDFIELD,idReserved,INDEX,INDEXNR,idxMethod,loop
	new METHNAME,METHOD,objANMClass,objCacheClass,objClassMethods,objClassMethod
	new PACKNAME,PARAMETERS,PROP,PROPNAME
	new sc,SIZE,SPRACHE,SQLDATA,SQLMAP,SQLSUBSCRIPT,STORAGE
	new strCaption,strCompany,strDelimiter,strImport,strInclude,strMethodName,strPropList
	new strSaveCode,strSaveCodeRet
	new WWWOO1,WWW002,WWW003,YSKEY
	
	set strSaveCode    = ""  ; SR17471
	set strSaveCodeRet = ""  ; SR17471
	
	set SPRACHE    = "EN"           ; guarantees consistent language in object classes
	set strCompany = $$^WWWTEXT("400;Mandant")      ; "Company"
	
	set objANMClass = $get(^WWW001(0,pidClass,1))
	quit:$$$WWW001NoCompilemappedclass(objANMClass)=$$$YES $$$YES  ; *** DO NOT COMPILE THESE CLASSES ***
	
	set blnIs2007 = $SYSTEM.Version.GetMajor()>"2006"
	
	DO
	. new OBJECT
	. if '$data(^WWW002(0,pidClass)) quit
	. quit:pidClass="WWW000"                                    ; ROUTINE
	. quit:$$$WWW001NoCompilemappedclass(objANMClass)=$$$YES
	. ;
	. set NOCOMPILE = 0                                         ; COMPILE OK
	. ;do NEU  ;NEUE ÜBERTRAGUNG ;recent translation 
	. ;set DELIM  =  $piece(objANMClass,Y,11)
	. ;if +DELIM=0 set DELIM=126  ;TRENNZEICHNEN
	. ;
	. ; D8		$$$WWW001AltSaveProcedure()
	. ;		0 : @netManager
	. ;		3 : No Client No.
	. ;		4 : Cache Object
	. ;		5 : Routine			=> quit
	. set OBJECT = +$piece(objANMClass,Y,8)               ; 
	. if (OBJECT'=0) && (OBJECT'=4) && (OBJECT'=3) quit  ;NICHT AUSWERTEN WENN ROUTINE ODER ABA ODER SONSTIGES ;Not when routine Or Or 
	. ;
	. ;SET SORT=""    ; Not used
	. do CheckForDoubles(pidClass)
	. do ^WWWSORTKEY(pidClass)  ; sets YSKEY() array ; FIXME : Not used before called again below <GRF>
	. do FixPropNames(pidClass,strCompany)
	
	
	; Package Name
	;---------------------------------------
	set PACKNAME = "User"  ; Package Name    ; will be Parameter ^WWW012(0,YM,1)...
	set CLASSX   = pidClass
	if $piece($GET(^WWW0011(0,pidClass,SPRACHE,1)),Y,2)'="" set CLASSX = $piece(^WWW0011(0,pidClass,SPRACHE,1),Y,2)
	if pidClass'["." {
		set CACHECLASS = PACKNAME_"."_CLASSX
	} else {
		set CACHECLASS = pidClass
	}
	
	if ##class(%Dictionary.ClassDefinition).%ExistsId(CACHECLASS) {
		new strName,strClass
		; Saving all the variables in an array to add them later to a new created class.
		set objCacheClass   = ##class(%Dictionary.ClassDefinition).%OpenId(CACHECLASS)
		; FIXME : May get objCacheClass=$$$NULLOREF if class is locked for editing (?) <GRF>
		set objClassMethods = objCacheClass.Methods
		set strInclude      = objCacheClass.IncludeCode
		set strImport       = objCacheClass.Import
		
		for idxMethod=1:1:objClassMethods.Count() {           ; FIXME : int not idx
			set objClassMethod = objClassMethods.GetAt(idxMethod)
			set strName        = objClassMethods.GetAt(idxMethod).Name
			if $extract(strName,$length(strName)-2,$length(strName))="Get" {
				set strName = $extract(strName,1,$length(strName)-3)
			}
			set strClass = CACHECLASS                         ; FIXME : Move before idxMethod for loop ?
			if $piece(strClass,".",1)="User" {
				set strClass = $piece(strClass,".",2,9999)
			}                                                 ; FIXME : ^^^ 
			if '$data(^WWW003Calcs(0,1,$$$Index(strName),strClass)) && 
			    (strName'="%OnBeforeSave")                            {   ; SR17471
				; Don't preserve calculated methods.
				set arrClassMethods(objClassMethod.Name, "Implementation") = objClassMethod.Implementation.Read()
				set arrClassMethods(objClassMethod.Name, "FormalSpec")     = objClassMethod.FormalSpec
				set arrClassMethods(objClassMethod.Name, "ClassMethod")    = objClassMethod.ClassMethod
				set arrClassMethods(objClassMethod.Name, "ReturnType")     = objClassMethod.ReturnType
			}
		}
		set sc = ##class(%Dictionary.ClassDefinition).%DeleteId(CACHECLASS)
		
		if $$$ISERR(sc) quit sc                      ; *** EARLY EXIT ***
	}
	;SET WWW001=$GET(^WWW001(0,pidClass,1))
	;quit:$piece(WWW001,"~",21) 1                    ; D21   $$$WWW001NoCompilemappedclass()
	
	; General Class Definition
	;---------------------------------------
	set CLASS = ##class(%Dictionary.ClassDefinition).%New()
	set CLASS.Description = $$$WWW001ClassDescription(objANMClass)
	if $data(^WWW0011(0,pidClass,SPRACHE,1)) {
		set CLASS.Description = CLASS.Description_"/"_$piece($get(^WWW0011(0,pidClass,SPRACHE,1)),Y,1)
	}
	set CLASS.Description=CLASS.Description_":"
	if '$$ISEMPTY^WWWMEMO($$$WWW001Description(objANMClass)) { 
		set CLASS.Description = CLASS.Description_"<br>"_$$TOHTML^WWWMEMO($$$WWW001Description(objANMClass))
	}
	
	set CLASS.Super = "%Library.Persistent,%XML.Adaptor,%Library.Populate"  
	if $$$WWW001InheritsFromSuperClasses(objANMClass)'="" set CLASS.Super = $$$WWW001InheritsFromSuperClasses(objANMClass) ; D20
	if CLASS.Super="Library.Persistent"                   set CLASS.Super = "%Library.Persistent"
	if '$find(CLASS.Super,"%XML.Adaptor")                 set CLASS.Super = CLASS.Super_",%XML.Adaptor"
	
	set CLASS.ClassType = "persistent"
	set CLASS.Name      = CACHECLASS
	
	; Company Property  ; DEPRECATED - always "0" now
	;---------------------------------------
	set blnSharedFile = '$$$WWW001SharedFile(objANMClass)     ; D3  [Company = YM or 0]
	
	if blnSharedFile DO
	. set PROP = ##class(%Dictionary.PropertyDefinition).%New()
	. do CLASS.Properties.Insert(PROP)
	. set PROP.Name     = strCompany             ;"Company"
	. set PROP.Type     = "%String"
	. set PROP.Required = $$$YES
	. if blnIs2007 do      // SR15488
	. . do PROP.Parameters.SetAt(30,"MAXLEN")
	. . do PROP.Parameters.SetAt("attribute","XMLPROJECTION")
	. if 'blnIs2007 do     // SR15488                              ; FIXME : Can we remove this now?
	. . set PROP.Parameters.Data("MAXLEN")        = 30
	. . set PROP.Parameters.Data("XMLPROJECTION") = "attribute"
	
	set strSaveCode = $char(9)_"set strClass="""_pidClass_""""_$char(13,10)
	
	; Properties : Primary Keys and Data Fields
	;---------------------------------------
	FOR GLOBAL="WWW002","WWW003" DO
	. set GLOBREF = "^"_GLOBAL_"(0,pidClass)"
	. set FELDNR  = ""
	. FOR  set FELDNR = $order(@GLOBREF@(FELDNR)) quit:FELDNR=""  DO
	. . set SIZE = ""                                           ; SR16701
	. . set DATA    = @GLOBREF@(FELDNR,1)
	. . set IDFIELD = (GLOBAL="WWW002")
	. . set PROP    = ##class(%Dictionary.PropertyDefinition).%New()
	. . DO CLASS.Properties.Insert(PROP)
	. . set PROP.Name = $$$WWW003PropertyName(DATA)
	. . ;SR17471 vvvv
	. . if IDFIELD=1 set strSaveCode	= strSaveCode_$char(9)_"set $piece(idKey,"","","_FELDNR_")=i%"_PROP.Name_$char(13,10)
	. . if IDFIELD=0 set strSaveCode	= strSaveCode_$char(9)_"set $piece(objRec,""~"","_FELDNR_")=i%"_PROP.Name_$char(13,10)
	. . if IDFIELD=0 set strSaveCodeRet	= strSaveCodeRet_$char(9)_"set i%"_PROP.Name_"=$piece(objRec,""~"","_FELDNR_")"_$char(13,10)
	. . ;SR17471 ^^^^
	. . set ATNETTYPE = $$$WWW003InputType(DATA)
	. . if ATNETTYPE'=16 set PROP.Type = $$TypeDesc(ATNETTYPE)
	. . if ATNETTYPE=3   set SIZE      = 32000
	. . if 'IDFIELD if ATNETTYPE=16 if $piece($GET(^WWW003(0,pidClass,FELDNR,1)),Y,26)'="" DO  quit
	. . . set PROP.Type = $$$WWW003AlternateInputType(^WWW003(0,pidClass,FELDNR,1))  ; D26
	. . . if $$$WWW003AlternateInputType(^WWW003(0,pidClass,FELDNR,1))'["." do
	. . . . set PROP.Type = PACKNAME_"."_$$$WWW003AlternateInputType(^WWW003(0,pidClass,FELDNR,1)) ; D26
	. . ;
	. . ;ARI 240504 brauchen wir nicht I PROP.Type="%String",$F($$UPCASE^ECVSIMP($$$WWW003xPROPERTYNAME(DATA)),"NAME") S PROP.Type="%Name"
	. . set PARAMETERS=""
	. . ;ARI 021203 bei PrimaryKey-Feldern muss man die Collation auf EXACT
	. . ;setzen, sonst geht Kompilierung ab Cache 5.0.4 schief.
	. . if IDFIELD && (PROP.Type="%String") || (PROP.Type="%Name") do
	. . . if blnIs2007  do PROP.Parameters.SetAt("EXACT","COLLATION")
	. . . if 'blnIs2007 set PROP.Parameters.Data("COLLATION")="EXACT"
	. . ;
	. . ; Added Type 3 - Memo SR17369 - Add SIZE to Memo field
	. . if $CASE(ATNETTYPE,3:1,5:1,6:1,10:1,:0) DO
	. . . if SIZE="" set SIZE = $$$WWW003InputLength(DATA)
	. . . if $extract($$$WWW003PropertyDescription(DATA),1,5)="_FREE" set SIZE=300 // SR17471
	. . . if blnIs2007&&SIZE  do PROP.Parameters.SetAt(SIZE,"MAXLEN")   // SR17471
	. . . if 'blnIs2007&&SIZE set PROP.Parameters.Data("MAXLEN")=SIZE   // SR17471
	. . ;
	. . set CAPTION    = $$$WWW003PropertyDescription(DATA)
	. . set strCaption = $GET(@("^"_GLOBAL_"1")@(0,pidClass,FELDNR,SPRACHE,1))
	. . if strCaption'="" set CAPTION = strCaption
	. . if blnIs2007  do PROP.Parameters.SetAt(CAPTION,"CAPTION")
	. . if 'blnIs2007 set PROP.Parameters.Data("CAPTION") = CAPTION
	. . if IDFIELD do
	. . . if blnIs2007  do PROP.Parameters.SetAt("attribute","XMLPROJECTION")
	. . . if 'blnIs2007 set PROP.Parameters.Data("XMLPROJECTION") = "attribute"
	. . if IDFIELD || $$$WWW003MandatoryField(DATA) set PROP.Required=$$$YES
	. . set PROP.Description = $$$WWW003PropertyDescription(DATA)
	. . if 'IDFIELD && ($$$WWW003CachCollection(DATA)=$$$YES) DO
	. . . set PROP.Collection       = "list"
	. . . set PROP.SqlListDelimiter = """;"""
	. . . set PROP.SqlListType      = "delimited"
	. . ;
	. . ;  FIXME : Braces inside dot level syntax; NOFORM cf YNOFORM <GRF>
	. . if $get(YNOFORM)=1 set sc = CLASS.%Save() if 'sc { write !,GLOBAL_"::"_pidClass_":"_PROP.Name_" "_FELDNR do DisplayError^%apiOBJ(sc) quit}
	
	; Add "Calculated, SqlComputeCode" properties (and Methods?) for Calculated Fields
	;---------------------------------------
	do CalcCurrencyFields^WWW001CalcFields(pidClass)        ; Calc Fields for Currencies
	do CalcFields^WWW001CalcFields(pidClass,.CLASS)         ; Calculated Fields
	
	; Get Primary Key Properties and apply to Index definition
	;---------------------------------------
	set strPropList = ""
	if blnSharedFile do INSERT^WWWPLIST(.strPropList,strCompany)  ; "Company"
	set FELDNR = ""
	for  {
		set FELDNR = $order(^WWW002(0,pidClass,FELDNR))
		quit:FELDNR=""
		
		set WWW002 = $get(^WWW002(0,pidClass,FELDNR,1))
		do INSERT^WWWPLIST(.strPropList,$$$WWW002PropertyName(WWW002),$$$NO,$$$COMMA)    ; D25	()
	}
	
	set INDEX = ##class(%Dictionary.IndexDefinition).%New()
	do CLASS.Indices.Insert(INDEX)
	set INDEX.Properties      = strPropList
	set INDEX.Name            = "IDKEY"
	set INDEX.PrimaryKey      = 1
	set INDEX.IdKey           = 1
	set INDEX.Unique          = 1
	
	
	; Identify @nManager tilde-delimited structures for SQL as "map1"
	;---------------------------------------
	set CLASS.StorageStrategy = "StorageAtNet"
	
	set STORAGE               = ##class(%Dictionary.StorageDefinition).%New()
	do CLASS.Storages.Insert(STORAGE)
	set STORAGE.Type      = "%CacheSQLStorage"
	set STORAGE.Name      = "StorageAtNet"
	
	set SQLMAP            = ##class(%Dictionary.StorageSQLMapDefinition).%New()
	do STORAGE.SQLMaps.Insert(SQLMAP)
	set SQLMAP.Name       = "map1"
	set SQLMAP.Type       = "data"
	set SQLMAP.Global     = "^"_pidClass
	set SQLMAP.Structure  = "delimited"
	
	set SQLSUBSCRIPT      = ##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	do SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	set SQLSUBSCRIPT.Name = 1
	if blnSharedFile {
		set SQLSUBSCRIPT.Expression = "{"_strCompany_"}"
	} else {
		set SQLSUBSCRIPT.Expression = 0
	}
	
	; Add Primary Keys to map
	;---------------------------------------
	set FELDNR = ""
	for  set FELDNR = $order(^WWW002(0,pidClass,FELDNR)) quit:FELDNR=""  do
	. set SQLSUBSCRIPT = ##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	. do SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	. set SQLSUBSCRIPT.Name       = FELDNR+1
	. set SQLSUBSCRIPT.Expression = "{"_$$$WWW002PropertyName($get(^WWW002(0,pidClass,FELDNR,1)))_"}"  ; D25
	
	set SQLSUBSCRIPT = ##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	do SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	set SQLSUBSCRIPT.Name       = $order(^WWW002(0,pidClass,""),-1)+2
	set SQLSUBSCRIPT.Expression = 1
	
	set strDelimiter = $$$WWW001AltDelimiterASCII(objANMClass)    ; D11
	if +strDelimiter=0 {
		set strDelimiter = Y
	} else {
		set strDelimiter = $char(strDelimiter)
	}
	
	; Add Data Fields to map
	;---------------------------------------
	set FELDNR = ""
	for  set FELDNR = $order(^WWW003(0,pidClass,FELDNR)) quit:FELDNR=""  DO
	. set SQLDATA = ##class(%Dictionary.StorageSQLMapDataDefinition).%New()
	. do SQLMAP.Data.Insert(SQLDATA)
	. set SQLDATA.Name      = $$$WWW003PropertyName($get(^WWW003(0,pidClass,FELDNR,1)))  ; D25
	. set SQLDATA.Delimiter = """"_strDelimiter_""""
	. set SQLDATA.Piece     = FELDNR
	
	; Build list of @nM Indices into YSKEY and transfer
	; them to separate maps "SortIndex#" for SQL
	;---------------------------------------
	do ^WWWSORTKEY(pidClass)  ; sets YSKEY() array e.g. YSKEY(6) = "K1,F2,K3" where index 6 is based on three fields P1, D2 & P3
	
	set INDEXNR = ""
	FOR  set INDEXNR = $order(YSKEY(INDEXNR)) quit:INDEXNR=""  DO
	. set SQLMAP = ##class(%Dictionary.StorageSQLMapDefinition).%New()
	. DO STORAGE.SQLMaps.Insert(SQLMAP)
	. set SQLMAP.Name      = "SortIndex"_INDEXNR
	. set SQLMAP.Type      = "index"
	. set SQLMAP.Global    = "^"_pidClass_"s"
	. set SQLMAP.Structure = "delimited"
	. set SQLSUBSCRIPT     = ##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	. DO SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	. set SQLSUBSCRIPT.Name = 1
	. if blnSharedFile  set SQLSUBSCRIPT.Expression = "{"_strCompany_"}"    ; "Company"
	. if 'blnSharedFile set SQLSUBSCRIPT.Expression = 0
	. set SQLSUBSCRIPT = ##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	. DO SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	. set SQLSUBSCRIPT.Name       = 2
	. set SQLSUBSCRIPT.Expression = INDEXNR
	. FOR loop=1:1:$LENGTH(YSKEY(INDEXNR),",") DO
	. . set FELDNR  = $piece(YSKEY(INDEXNR),",",loop)
	. . set IDFIELD = ($EXTRACT(FELDNR)="K")
	. . set FELDNR  = $EXTRACT(FELDNR,2,$LENGTH(FELDNR))
	. . quit:FELDNR=""
	. . ;
	. . if 'IDFIELD if $$$WWW003PropertyName($GET(^WWW003(0,pidClass,FELDNR,1)))="" quit     ; D25
	. . if IDFIELD  if $$$WWW002PropertyName($GET(^WWW002(0,pidClass,FELDNR,1)))="" quit     ; D25
	. . ;
	. . set SQLSUBSCRIPT=##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	. . DO SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	. . set SQLSUBSCRIPT.Name = loop+2
	. . if CACHECLASS["zzObjectTest" do
	. . . if IDFIELD  set SQLSUBSCRIPT.Expression = "$$^WWWUMLAU({"_$$$WWW002PropertyName($get(^WWW002(0,pidClass,FELDNR,1)))_"},1)"  ; D25
	. . . if 'IDFIELD set SQLSUBSCRIPT.Expression = "$$^WWWUMLAU({"_$$$WWW003PropertyName($get(^WWW003(0,pidClass,FELDNR,1)))_"},1)"  ; D25
	. . if CACHECLASS'["zzObjectTest" do
	. . . if IDFIELD  set SQLSUBSCRIPT.Expression = "$$UPPER({"_$$$WWW002PropertyName($get(^WWW002(0,pidClass,FELDNR,1)))_"})"  ; D25
	. . . if 'IDFIELD set SQLSUBSCRIPT.Expression = "$$UPPER({"_$$$WWW003PropertyName($get(^WWW003(0,pidClass,FELDNR,1)))_"})"  ; D25
	. ;
	. set FELDNR=""
	. FOR  set FELDNR=$order(^WWW002(0,pidClass,FELDNR)) quit:FELDNR=""  DO
	. . set SQLSUBSCRIPT=##class(%Dictionary.StorageSQLMapSubDefinition).%New()
	. . DO SQLMAP.Subscripts.Insert(SQLSUBSCRIPT)
	. . set SQLSUBSCRIPT.Name       = FELDNR+$LENGTH(YSKEY(INDEXNR),",")+2
	. . set SQLSUBSCRIPT.Expression = "{"_$$$WWW002PropertyName($get(^WWW002(0,pidClass,FELDNR,1)))_"}"  ; D25
	
	; (Deprecated?) Calculated Fields (WWWCLASSCALC is not populated - WWW003Calc instead?)
	;---------------------------------------
	set PROPNAME = ""
	FOR  set PROPNAME = $order(^WWWCLASSCALC(0,pidClass,PROPNAME)) quit:PROPNAME=""  DO
	. set DATA = $GET(^WWWCLASSCALC(0,pidClass,PROPNAME,1))
	. set PROP = ##class(%Dictionary.PropertyDefinition).%New()
	. DO CLASS.Properties.Insert(PROP)
	. set PROP.Name = PROPNAME
	. set PROP.Type = "%String"
	. if $piece(DATA,Y,2)'="" set PROP.Type = $piece(DATA,Y,2)
	. if '$$ISEMPTY^WWWMEMO($piece(DATA,Y,1)) set PROP.Description=$$TOHTML^WWWMEMO($piece(DATA,Y,1))
	. set PROP.Calculated=1
	. if '$$ISEMPTY^WWWMEMO($piece(DATA,Y,3)) DO
	. . set PROP.SqlComputed    = 1
	. . set $piece(DATA,Y,3)    = $$STRIPEND^WWWMEMO($piece(DATA,Y,3))
	. . set PROP.SqlComputeCode = $$TOSTREAM^WWWMEMO($piece(DATA,Y,3))
	
	; (Deprecated?) @nM Methods (WWWCLASSMETH is not populated)
	;---------------------------------------
	set METHNAME = ""
	FOR  set METHNAME = $order(^WWWCLASSMETH(0,pidClass,METHNAME)) quit:METHNAME=""  DO
	. set DATA   = $GET(^WWWCLASSMETH(0,pidClass,METHNAME,1))
	. set METHOD = ##class(%Dictionary.MethodDefinition).%New()
	. DO CLASS.Methods.Insert(METHOD)
	. set METHOD.Name        = METHNAME
	. set METHOD.ClassMethod =+$piece(DATA,Y,3)
	. set METHOD.FormalSpec  = $piece(DATA,Y,2)
	. set METHOD.ReturnType  = $piece(DATA,Y,4)
	. DO METHOD.Implementation.Write($$TOSTREAM^WWWMEMO($piece(DATA,Y,1)))
	
	; Restore existing methods, etc.
	;---------------------------------------
	; Adding to the new created class all the existing methods, and the include code as well.
	if $data(strInclude) set CLASS.IncludeCode = strInclude
	if $data(strImport)  set CLASS.Import      = strImport
	
	if $data(arrClassMethods) {
		set strMethodName = $order(arrClassMethods(""))
		while strMethodName '= "" {
			set objClassMethod = ##class(%Dictionary.MethodDefinition).%New()
		
			set objClassMethod.Name              = strMethodName
			do objClassMethod.Implementation.Write(arrClassMethods(strMethodName, "Implementation")) 
			set objClassMethod.FormalSpec        = arrClassMethods(strMethodName, "FormalSpec")
			set objClassMethod.ClassMethod       = arrClassMethods(strMethodName, "ClassMethod")
			set objClassMethod.ReturnType        = arrClassMethods(strMethodName, "ReturnType")
			
			do CLASS.Methods.Insert(objClassMethod)
			
			set strMethodName = $order(arrClassMethods(strMethodName))
		}
	}
	; SR17471: Insert %OnBeforeSave Class
	set strSaveCode = strSaveCode_$char(9)_"set sc=$$OnBeforeSave^COMObject(insert,strClass,idKey,.objRec)"_$char(13,10)
	set strSaveCode = strSaveCode_strSaveCodeRet
	set strSaveCode = strSaveCode_$char(9)_"Quit sc"_$char(13,10)
	set objClassMethod = ##class(%Dictionary.MethodDefinition).%New()
	set objClassMethod.Name				= "%OnBeforeSave"
	do objClassMethod.Implementation.Write(strSaveCode) 
	set objClassMethod.FormalSpec		= "insert:%Boolean"
	set objClassMethod.ClassMethod		= 0
	set objClassMethod.ReturnType		= "%Status"
	;SR17471.1 ???? set objClassMethod.Private        	= 0
	set objClassMethod.Private        	= 1
	set objClassMethod.ServerOnly      	= 1
	
	do CLASS.Methods.Insert(objClassMethod)
	
	set sc = CLASS.%Save()
	quit sc
	
	
CheckForDoubles(pidClass)
	;-------------------------------------------------------------------------------
	; Property Name Validation - Strips any problem Property Names to be rebuilt later
	; 1. Is it a reserved SQL word?
	; 2. Has it already been used?
	; 3. Does it start with a numeral? 0-9
	; 
	; As each is read, add to WWWSOR and clear any further occurrences
	; File as case-sensitive and case-insensitive
	; 
	; History :
	; 06-Mar-2009	GRF		SR16410: Separate functionality
	;-------------------------------------------------------------------------------
	new idFldNo,idxPropName,objClassData,strPropName
	
	; FIXME : Index 3.2 on this piece - Reindex? <GRF>
	; FIXME : Doesn't check if WWW003 names are duplicates of WWW002 names. <GRF>
	
	kill ^WWWSOR(YUSER)
	set idFldNo = ""
	for  {
		set idFldNo = $order(^WWW003(0,pidClass,idFldNo))
		quit:idFldNo=""
		
		set objClassData = $get(^WWW003(0,pidClass,idFldNo,1))
		set strPropName  = $$$WWW003PropertyName(objClassData)
		quit:strPropName=""
		
		set idxPropName = $$^WWWUMLAU(strPropName,1)
		if $data(^WWWRESs(0,1,idxPropName)) {
			set $$$WWW003PropertyName(^WWW003(0,pidClass,idFldNo,1)) = ""  
		
		} elseif $data(^WWWSOR(YUSER,strPropName)) {
			set $$$WWW003PropertyName(^WWW003(0,pidClass,idFldNo,1)) = ""
		
		} elseif ($extract(strPropName)=0) || (+$extract(strPropName)'=0) {
			set $$$WWW003PropertyName(^WWW003(0,pidClass,idFldNo,1)) = ""
		
		} else {
			set ^WWWSOR(YUSER,strPropName) = ""
			set ^WWWSOR(YUSER,idxPropName) = ""
		}
	}
	quit
	
	
FixPropNames(pidClass,pstrCompany)
	;-------------------------------------------------------------------------------
	; Reconstruct property names from descriptions - any duplicates get incrementing suffix
	; 
	; 1. Add reserved words to list (incl "Company")
	; 2.
	; 
	; Returns: 
	; 
	; History :
	; 06-Mar-2009	GRF		SR16410: Separate functionality
	;-------------------------------------------------------------------------------
	new idFldNo,idReserved,idxPropName,intSuffix,loop
	new objClassData,objClassKey,objDataLang,objKeyLang
	new strBase,strNewName,strPropName,strXlate
	
	;if Z=5 {
	;	set LW5="ÜÄÖüäöß][\}{|~ ,()@#$%^&*_=+<>?/"_$CHAR(128)_""""
	;	                       ^^^^   ^ ^^^ ^   ^      ^
	;	set UP5="UAOUAOSUAOUAOS"
	
	set strXlate = " .-,'`´/+*%&();:!_"
	;               ^  ^   ^^^^^^^   ^   ; Repeated Translation + $CHAR(128)
	for loop=128:1:255 set strXlate = strXlate_$char(loop)
	
	kill ^WWWSOR(YUSER)
	set ^WWWSOR(YUSER,$$^WWWUMLAU(pstrCompany,1)) = ""   ; "Company" is not permitted [as "COMPANY" for comparison]
	
	; List of Reserved SQL Words
	;-------------------------------------
	set idReserved = ""
	for  {
		set idReserved = $order(^WWWRES(0,idReserved))
		quit:idReserved=""
		
		set idxPropName = $$^WWWUMLAU($get(^WWWRES(0,idReserved,1)),1)
		quit:idxPropName=""
		set ^WWWSOR(YUSER,idxPropName) = ""
	}
	
	set idFldNo=""
	for  {
		set idFldNo = $order(^WWW002(0,pidClass,idFldNo))
		quit:idFldNo=""
		
		set objClassKey = $get(^WWW002(0,pidClass,idFldNo,1))
		set strPropName  = $$$WWW002PropertyName(objClassKey)
		quit:$find(strPropName,".")
		
		if $translate(strPropName," ")'="" {     ; FIXME : Prop Name can't have spaces anyway <GRF>
			set strNewName = strPropName
		} else {
			set strNewName = $$$WWW002PropertyDescription(objClassKey)
			set objKeyLang = $get(^WWW0021(0,pidClass,idFldNo,SPRACHE,1))
			if objKeyLang'="" {
				set strNewName = $translate($extract($$^WWWUMLAU($piece(objKeyLang,Y,1),5),1,25),strXlate)
			}
		}
		set strNewName = $translate($extract($$^WWWUMLAU(strNewName,5),1,25),strXlate)
		if strNewName'="" {
			set strBase = strNewName
			for intSuffix=1:1 {     ; "XXX" => "XXX1", "XXX2", etc.
				if $data(^WWWSOR(YUSER,$$^WWWUMLAU(strNewName,1))) set strNewName = strBase_intSuffix
			
				if '$data(^WWWSOR(YUSER,$$^WWWUMLAU(strNewName,1))) {  ; new index required  - strNewName may have had suffix added
					set ^WWWSOR(YUSER,$$^WWWUMLAU(strNewName,1)) = ""
					set ^WWWSOR(YUSER,222,idFldNo)               = strNewName
					quit
				}
			}
		}
		if strNewName'="" && ($translate(strPropName," ")="") {
			set $$$WWW002PropertyName(^WWW002(0,pidClass,idFldNo,1)) = strNewName  ; D25  ; FIXME : Index 3.2 on this piece - Reindex? <GRF>
		}
	}
	
	
	set idFldNo=""
	for  {
		set idFldNo = $order(^WWW003(0,pidClass,idFldNo))
		quit:idFldNo=""
		
		set objClassData = $get(^WWW003(0,pidClass,idFldNo,1))
		set strPropName  = $$$WWW003PropertyName(objClassData)
		quit:$find(strPropName,".")
		
		if $translate(strPropName," ")'="" {     ; FIXME : Prop Name can't have spaces anyway <GRF>
			set strNewName = strPropName
		} else {
			set strNewName = $$$WWW003PropertyDescription(objClassData)
			set objDataLang = $get(^WWW0031(0,pidClass,idFldNo,SPRACHE,1))
			if objDataLang'="" {
				set strNewName = $translate($extract($$^WWWUMLAU($piece(objDataLang,Y,1),5),1,25),strXlate)
			}
		}
		set strNewName = $translate($extract($$^WWWUMLAU(strNewName,5),1,25),strXlate)
		if strNewName'="" {
			set strBase = strNewName
			for intSuffix=1:1 {     ; "XXX" => "XXX1", "XXX2", etc.
				if $data(^WWWSOR(YUSER,$$^WWWUMLAU(strNewName,1))) set strNewName = strBase_intSuffix
			
				if '$data(^WWWSOR(YUSER,$$^WWWUMLAU(strNewName,1))) {  ; new index required  - strNewName may have had suffix added
					set ^WWWSOR(YUSER,$$^WWWUMLAU(strNewName,1)) = ""
					set ^WWWSOR(YUSER,333,idFldNo)               = strNewName
					quit
				}
			}
		}
		if strNewName'="" && ($translate(strPropName," ")="") {
			set $$$WWW003PropertyName(^WWW003(0,pidClass,idFldNo,1)) = strNewName  ; D25  ; FIXME : Index 3.2 on this piece - Reindex? <GRF>
		}
	}
	quit
	
	
TypeDesc(penumType)
	;-------------------------------------------------------------------------------
	; Convert Input Types from enum to object type 
	; 
	; History :
	; 06-Mar-2009	GRF		SR16410: Separate functionality
	;-------------------------------------------------------------------------------
	new strResult
	
	set strResult = $case(penumType,
	                       1:"WWW.DiscDate",
	                       2:"%Boolean",
	                       3:"%String",
	                       4:"%Numeric",
	                       5:"%String",
	                       6:"%String",
	                       7:"%Time",
	                       8:"%Currency",
	                       9:"%String",
	                      10:"%String",
	                      11:"%String",
	                      12:"%Float",
	                      13:"%String",
	                      14:"WWW.DiscTimestamp",
	                      15:"%List",
	                        :"%String")
	quit strResult
	;---------------------------------------
	; previously
	; 1:"%Date"
	; 3:"%Stream"        ;SR16701
	; 14:"%TimeStamp"
	
	; not ]]><![CDATA[covered - default to %String
	; 16:	Embedded  - special handling when called
	; 17:	Date (YYYYMMDD)
	; 18:	Exchange Rate
	;---------------------------------------
	


]]></Routine>
</Export>