<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWWKILL" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWWKILL(YDATEI,YKEY,YEINMAL)
#include COMSYS
#include WWWConst
	
#define LogR(%1,%2)	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2)	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWWKILL("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LOESCHEN SATZ
	;	D ^WWWKILL(YDATEI,YKEY) ACHTUNG YFORM BEACHTEN GGF NEW
	;	D SUBKILL^WWWKILL = wenn bei execute nach löschen die sub Formdaten
	;	                    ebenfalls gelöscht werden sollen
	;
	; Inputs : 
	;	YDATEI		YDATEI
	;	YKEY		SCHLUESSEL KEY1,KEY2,KEYn
	;	YEINMAL=1	UNTERWERFEN SIE NICHT ZU DCM WIEDER
	;				Once Only - Do not resubmit to DCM  ;GRF;SR11363;10.01.2005
	;
	; NOTE : Performs Transaction processing TSTART/TCOMMIT - Take care if
	;        called from within another such block.
	;        Deletion failures are currently being logged under the following conditions:
	;        	(1) We entered WWWKILL while inside a transaction
	;        	(2) ^SysSetup(15134) is not 'true'
	;        To turn off the logging: set ^SysSetup(15134) = 1
	;        
	;
	; ByRef : 		see new (...)
	;
	; Returns : 	status
	;
	; History :
	; 06-Jul-2010	SCR		SR17415: Dont do 'OnBeforeDelete^COMGridEdit31Display' for classed the containing with COMView. 
	;							This causes a problem with deleting favourites 
	; 28-Apr-2010	GRF		SR17189.1: Must have YVOR even if no form so later
	; 							processing okay
	; 17-Feb-2010	GRF		SR17189: Executables may perform other tasks such as
	; 							deleting associated data rather than just
	; 							determining whether deletion is possible.  Ownership
	; 							validation should be performed BEFORE any of these
	; 							tasks are performed.  Move YVOR/YFELD defn up.
	; 19-Jun-2009	GRF		Doco; logging
    ; 07-Oct-2008	FIS		SR15947: Transaction Wrapper for "Execute After Delete" implemented
    ; 								 Transaction Wrapper for "Execute Before Delete" implemented
	; 22-Feb-2008	GRF		SR15622: Version 1 => Version 2 conversion for selected classes
	; 02-Jan-2007	GRF		SR15134: Removed obsolete code per note
	; 14-Dec-2006	JW		BR014285: Allow for error message from OnBeforeDelete
	; 14-Dec-2006	Steve S	SR15316 Run class hooks
	; 23-Oct-2006	JW		SR15134: Made transaction smaller. Rewrote in brace syntax.
	; 						Look at DCM/kill statuses. Logging.
	; 05-Oct-2006	JW		SR15098: Fixed transaction. Don't always DCM.
	; 						Reverted incorrect changes of SR14414. Doco.
	; 24-Aug-2006	GRF		Doco; quits
	; 07-Jul-2006	RPW		SR12522: Delete any Quick Search if required
	; 26-Apr-2006	SC		Return strStatus for VARHooks error messages.
	; 13-Apr-2006	JW		Removed + from user id's in lock check
	; 10-Apr-2006	SC		SR14414: Check VARHooks for OnBeforeDelete.
	; 22-Dec-2005	RPW		SR13899: New schluessel
	; 30-Mar-2005	JW		Copied LC and UC definitions from WWWSKILL - undefined error.
	; 16-Feb-2005	DT		27239;ALTERNATIVE CLASS
	; 08.08.1997	DT		(C) BY DITMAR TYBUSSEK
	;-------------------------------------------------------------------------------
	NEW (SPRACHE,LANGUAGE,YM,YBED,YUSER,YFORM,YDATEI,YKEY,YFELD,YFELD1,YLOCATION,YTIMEFORM,YOPEN,YYFELD,YYKEY,YUCI,YANZ,YHYPER,YEINMAL,%request,%session,%KEY,%,%ZCS,%CGIEVAR)   ;TYBD;22.07.2003; FEHLENDE VARIABLEN FÜR FORM UND CSP...;23959; + YHYPER;FIS;27.05.04;25460
	
	$$$LogR("",$get(YDATEI)_"<"_$get(YKEY)_"<")
	
	set strStatus=$$$OK
	
	do ^WWWVORG
	if ($data(LC)#2=0) set LC="ÜÄÖüäöß][\}{|abcdefghijklmnopqrstuvwxyz,()@#$%^&*_=+<>?/"_$CHAR(128)_""" "
	if ($data(UC)#2=0) set UC="UAOUAOSUAOUAOABCDEFGHIJKLMNOPQRSTUVWXYZ                   "
	
	if YDATEI'="" {
		set idxYDATEI = $$^WWWUMLAU(YDATEI,1)
		if $ORDER(^WWW0011s(0,1,idxYDATEI,""))'="" set YDATEI = $ORDER(^WWW0011s(0,1,idxYDATEI,""))  ;ALTERNATIVE CLASS
	}
	if $GET(YUCI)="" set YUCI = $ZUTIL(5)
	
	;--------------------------------------------------------------------------------------------------------
	; SCHLUESSEL = "^DATEI(YM,KEY,KEYn,1)"
	
	if YDATEI'="" {
		set blnOk = $$BuildGlobal(YDATEI,YKEY,.SCHLUESSEL,.YDATA,.MAXYKEY)
		if blnOk {
			set blnLog = ('$get(^SysSetup(15134)) && ($tlevel>0))		//  Logging
			
			;------- TSTART ------------------------
			set blnTUnit = $$$NO  ;SR15947
	;vvvvvvv  ;SR15947 Transaction Units rather manual tstart/tcommit
			if $$$NEVER {  // INACTIVE -> ACTIVATE AFTER THE BUILD !!!   ($$$WWWTransactionActive1($get(^WWWTransaction(0,YFORM,"ExecuteAfterDelete",1)))=$$$YES) {  ;SR15947
				set blnTUnit   = $$$YES
				set strTZtrap  = $ztrap
				set $ztrap     = "TransactionUnitError^COMTransaction"                   //change error handler
				set strTStatus = $$StartUnit^COMTransaction(YFORM,"ExecuteAfterDelete")  //start transaction
			} else {
				tstart  //dflt. transaction control
			}
	;^^^^^^^^^^
			;tstart
			;---------------------------------------
			set Q       = $$$QDelete
			set strForm = $get(YFORM)
			set YVOR = ""                                            ; SR17189.1
			if strForm'="" set YVOR = $get(^WWW120(0,YFORM,1))       ; SR17189 vvv
			set YFELD = $$^WWWSETL(SCHLUESSEL)
			
			;---------------------------------------  SR17189 moved here
			; Check user access to current location
			;---------------------------------------
			if (Q=$$$QDelete) && (strForm'="") {	;
	$$$LogRx("Ownership Check:"_strForm)
				if $$$WWW120DataItemOfLocation(YVOR)'="" {
					if $$^WWWLOCATION($get(YLOCATION),YFORM,YFELD)'=$$$YES {
						set Q = $$$QDontDelete   ; No authorisation
					}
				}
			}                                                        ; SR17189 ^^^
			
			;---------------------------------------
			;  On Before Delete                            *** EXECUTE Kill 1 ***
			;---------------------------------------
	;		set YFELD = $$^WWWSETL(SCHLUESSEL)             ; SR17189 vvv move up & add Q test
	;		if strForm'="" {
	;			set YVOR = $GET(^WWW120(0,YFORM,1))
			if (Q=$$$QDelete) && (strForm'="") {           ; SR17189 ^^^
				set strExec = $$$WWW120ExecuteBeforeDeletionData(YVOR)
				if ((strExec'="") || ($data(^WWWTransactionLine(0,YFORM,"ExecuteBeforeDelete")))) && ($GET(YTIMEFORM)'=1) {  ;SR15947
					
	$$$LogRx("On Before Delete")
	$$$LogRx("Execute:"_$$$WWW120ExecuteBeforeDeletionData(YVOR))
	
					set strStatus=$$$OK			;SR17245
					XECUTE $$$WWW120ExecuteBeforeDeletionData(YVOR)  ;EXECUTE VOR LÖSCHEN WENN NICHT LÖSCHEN, DANN Q=1
					if $$$ISOK(strStatus) {		;SR17245
						set strStatus = $$TransactionUnit^COMTransaction(YFORM,"ExecuteBeforeDelete",blnTUnit,.strTError,.Q)  ;SR15947
					} else {
						set Q=$$$QDontDelete	;SR17245
					}
					// BR014285: Allow for error message
					if $extract(Q,2,999)'="" set strStatus = $extract(Q,2,999)   ; FIXME : Is this a text string rather than a strStatus value? <GRF>
					set Q = +Q
				}
			}
			
			;---------------------------------------
			;  On Before Delete Hook                       *** EXECUTE Kill 2 ***
			;---------------------------------------
			if Q=$$$QDelete {
				//SR15316 Commented: Handled in WWW001Hook
				//set strStatus = $$ExecuteVARHook^WWW001VARHooks(YDATEI,$$$EnumWWWEVENTTYPEOnBeforeDelete,YKEY,YFELD,strForm)	// SR14414
				//if $$$ISOK(strStatus) { //SR15316
	$$$LogRx("OnBeforeDeleteHook")
				set strStatus = $$ExecuteHook^WWW001Hook(YDATEI,$$$EnumWWWEVENTTYPEOnBeforeDelete,YKEY,YFELD,strForm)	// SR14414
				//}
				if $$$ISERR(strStatus) set Q = $$$QDontDelete
			}
			
			; SR17189 : Previous EXECs may perform secondary operations - should validate ownership first - Moved up
	;		if (Q=$$$QDelete) && (strForm'="") {	;Check user access to current location
	;			if $$$WWW120DataItemOfLocation(YVOR)'="" {
	;				if $$^WWWLOCATION($GET(YLOCATION),YFORM,YFELD)'=$$$YES {
	;					set Q = $$$QDontDelete   ;No authorisation
	;				}
	;			}
	;		}
			
			;if Q=$$$QDelete {   ;SR17245
			if (Q=$$$QDelete) && (YDATEI'["COMView") {   ;SR17415, do not do this for COMView classes SR17245
				set strStatus = $$OnBeforeDelete^COMGridEdit31Display(YDATEI,strForm,YKEY,YFELD)
				if $$$ISERR(strStatus) set Q = $$$QDontDelete	;SR17245
			}
			
			
			
			;-------------------------------
			; Version 1 => Version 2
			;-------------------------------
			if Q=$$$QDelete {                                   ;  *** EXECUTE Kill 3 ***
	$$$LogRx("Conversion")
				set strStatus = $$ConvertOnDelete^COMConversion(YDATEI,YKEY)
				if $$$ISERR(strStatus) set Q = $$$QDontDelete
			}
			
			if Q=$$$QDontDelete {	// Do not delete !!		SR15098 - cleaned up
	$$$LogRx("Do Not Delete")
				if $get(YFORM)=""            set YFORM  = ""
				if $GET(YKEY)'=""            set YYKEY  = $get(YKEY)
				if $GET(YFELD)'=""           set YYFELD = $get(YFELD)
				if (YFELD="") && (YFORM'="") set YYFELD = $get(^WWWUSE(0,YUSER,YFORM,"D",1))
				set YFELD = YYFELD
				set YKEY  = YYKEY
				set YOPEN = "OLD"
			
			} else {
	$$$LogRx("Actually Delete")
				;------ DELETE data record  -------------
				set %FELD1 = $$^WWWSETK(SCHLUESSEL) 
				;----------------------------------------
				
				;BITMAP;18.06.2003;TYBD ;bit map 
				if $$$WWW001BitMapIndexActive(YDATA) || ($$$WWW001BitmapXREFClass(YDATA)'="") {
					set OK = $$^WWWBITSET(YDATEI,YKEY,0,%FELD1,1,,,,$piece(YDATA,Y,25))   ;BITMAP;18.06.2003;TYBD ;bit map 
				}
				set YFELD = %FELD1
				if $get(YTIMEFORM)'=1 {  ;NUR ZEITABHÄNGIGE ERFASSUNG ;only logging 
					if ($get(YFORM)'="") && ($piece($get(^WWW120(0,YFORM,1)),Y,29)=1) {  ;PROTOKOLLDATEI
						set YUHR      = $piece($horolog,",",2)
						set strKeyPRO = $translate(YKEY,"/",",")
						while ($data(^WWWPRO(YM,YDATEI,+$horolog,YUHR,YBED,strKeyPRO,0,1))) {
							set YUHR=YUHR+.1
						}
						set ^WWWPRO(YM,YDATEI,+$horolog,YUHR,YBED,strKeyPRO,0,1) = %FELD1
						set ^WWWPRO(YM,YDATEI,+$horolog,YUHR,YBED,strKeyPRO,1,1) = ""
					}
				}
				do Indexes()		//SR15134
				
 
				if $GET(YTIMEFORM)'=1 {
					if strForm'="" {
						DO ^WWWFORM5
			;---------------------------------------
			;  On After Delete                            *** EXECUTE Kill 4 ***
			;---------------------------------------
						set strExec = $$$WWW120ExecuteAfterDataDeleted(YVOR)
						if strExec'="" {
	$$$LogRx("OnAfterDelete")
							xecute strExec
						}
						if ($data(^WWWTransactionLine(0,YFORM,"ExecuteAfterDelete"))) {  ;SR15947
							set strStatus = $$TransactionUnit^COMTransaction(YFORM,"ExecuteAfterDelete",blnTUnit,.strTError)
						}
						if (strExec="") || $$$WWW120DeleteSubformsRegardlessO(YVOR) {
							if $$$WWW120DeleteAlsoSubforms(YVOR) {
	$$$LogRx("SUBKILL")
								DO SUBKILL
							}
							if $$$WWW120DeleteAlsoTheFollowingSub(YVOR)'="" {
	$$$LogRx("SUBKILL1")
								DO SUBKILL1
							}
						}
					}
				}
			}
			
			;--------- DCM ------------------------------ SR15098: Do not always try DCM - check Q (if kill occurred) first.
			
			if (Q=$$$QDelete) && ($GET(YEINMAL)'=$$$YES) {
				set strStatus = $$DeleteIndex^COMQuickSearch(YDATEI,YKEY) // SR12522	SR15134
				
				if $$$ISOK(strStatus) {
					; Now run function that identifies what, if any, event(s) need to be added to the queue
					
					set intTlevel = $tlevel				//SR15134 vvvvvvvv Check transaction level
					set strStatusDCM = $$setRelevantEvents^COMDCMControlModule("INTRAPREND",YDATEI,"delete",YKEY)  ;INTRAPREND=KENNUNG FÜR @-NET MANAGER ;to 
					if (intTlevel '= $tlevel) {			//SR15134 ^^^^^^^^
						set strStatus = $listbuild("Com00261",$piece(strStatusDCM,Y,2))  ;Transaction Level not maintained by DCM. Contact Support. Status: %1
						
					} elseif '$piece(strStatusDCM,Y,1) && ($GET(YBED)'="") && ($GET(YFORM)'="") {	// Log only
						set ^WWWWV(YM,YBED,+$horolog,YFORM,YKEY,1) = +$horolog_Y_$piece($horolog,",",2)_Y_YBED_Y_$piece(strStatusDCM,Y,2)
					}
				}
				if $$$ISERR(strStatus) {
					set Q = $$$QDontDelete
				}
			}	
			
			;--- COMMIT / ROLLBACK -------------------- SR15098 Do not always tcommit. Check tlevel and Q (if kill occurred).
			
			if (Q=$$$QDelete) {
				;vvvvvvv  ;SR15947 Transaction Units rather manual tstart/tcommit
				if (blnTUnit =$$$YES) {
					if $$$ISOK(strTStatus) {
						set strTStatus=$$EndUnit^COMTransaction(YFORM,"ExecuteAfterDelete",strTStatus,$get(strTError))
						if '$$$ISOK(strTStatus) set strStatus = $$$NO  ;strTStatus  // set error if rolling back due to strTError
					}
					set $ztrap=strTZtrap
				} else {
					if $tlevel>0 tcommit  //dflt. tcommit
					//Either deletes the object or exports its parent
					do ##class(SourceControl.Exporter).DeleteArtifactByNameKey(YDATEI,YKEY)
				}
				;^^^^^^^^^^
				;if $tlevel>0 tcommit
			} else {
				if blnLog do StackDump^COMDebug("WWWKILL","Class: "_YDATEI_", Key: "_YKEY _",Status: "_strStatus)		//SR15134 - logging
				;vvvvvvv  ;SR15947 Transaction Units rather manual tstart/tcommit
				if (blnTUnit =$$$YES) {
					if $$$ISOK(strTStatus) { set strTStatus=$$EndUnit^COMTransaction(YFORM,"ExecuteAfterDelete",strTStatus,$listbuild("WWW00044",YDATEI,YKEY)) } 	 ;System deletion failed. Class: %1, Key: %2
					set $ztrap=strTZtrap
				} else {
					trollback  //dflt. trollback
				}
				;^^^^^^^^^^
				;trollback
				if $$$ISOK(strStatus) {		// We are rolling back but don't have an error status, so return default error.
					set strStatus = $listbuild("WWW00044",YDATEI,YKEY) 	 ;System deletion failed. Class: %1, Key: %2
				}
			}
			;---------------------------------------
		}
	}
	quit strStatus
	
	
BuildGlobal(pidClass,pstrKey,&pstrGlobal,&pobjClass,&pintKeys)
	;-------------------------------------------------------------------------------
	; Build the global string
	;
	; Params:	pidClass	- WWW001 id
	; 			pstrKey		- key of the class
	;
	; ByRefs:	pstrGlobal	- the constructed string
	; 			pobjClass	- WWW001 data
	; 			pintKeys	- number of keys in the class
	;
	; Returns:	boolean - whether global could be formed properly
	;
	; History:
	; 23-Feb-2007	JW		SR15452: Quote the company
	; 23-Oct-2006	JW		SR15134: Created
	;-------------------------------------------------------------------------------
	new intKey,blnOk,idKey
	
	set blnOk = $$$YES
	
	set pobjClass = $get(^WWW001(0,pidClass,1))
	set pintKeys = +$order(^WWW002(0,pidClass,""),-1)
	if pintKeys=0 set pintKeys=1
	
	set pstrGlobal="^"_pidClass
 
	if $get(YTIMEFORM)=1 {		// time-dependent change
		$$$Append(pstrGlobal,"t")
		set pintKeys = pintKeys+1
	}
	$$$Append(pstrGlobal,"("_$$$QUOTE($$^WWWYM(pidClass)))	//SR15452
	
	for intKey = 1:1:pintKeys {
		set idKey = $$$DEQUOTE($piece(pstrKey,",",intKey))
		if idKey="" {
			set blnOk = $$$NO
		} else {
			$$$Append(pstrGlobal,","""_idKey_"""")
		}
	}
	if blnOk {
		if $$$WWW001AltSaveProcedure(pobjClass)'=4 {
			$$$Append(pstrGlobal,",1")
		}
		$$$Append(pstrGlobal,")")
	}
	quit blnOk
	
	
Indexes()
	;-------------------------------------------------------------------------------
	; Re-index after the deletion		;SORTKEYS BEARBEITEN
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 23-Oct-2006	JW		SR15134: Created. Encapsulated from main routine
	;-------------------------------------------------------------------------------
	if $$$WWW001AltSaveProcedure($GET(YDATA))'=4 do 	;NICHT BEI CACHÉ ;next to 
	. DO ^WWWSORTKEY(YDATEI)  ;SKEY BESTIMMEN ;ordain 
	. IF $DATA(YSKEY) DO
	. . SET YYYM(0)=$$^WWWYM(YDATEI,0)
	. . SET YYYM(1)=$$^WWWYM(YDATEI,1)
	. . IF $PIECE($GET(^WWW001(0,YDATEI,1)),Y,8)=7 DO ^WWWKILL7 QUIT  ;SQLSTORAGE
	. . IF $PIECE($GET(^WWW001(0,YDATEI,1)),Y,8)=6              QUIT  ;ABACUS
	. . SET SKEY=""
	. . FOR  SET SKEY=$ORDER(YSKEY(SKEY)) QUIT:SKEY=""  DO
	. . . NEW YQ,YMAX,YMAX1,YMAX2
	. . . NEW YFILES
	. . . SET YFILES=YDATEI_"s"
	. . . IF $PIECE(YDATA,Y,23)'="" SET YFILES=$PIECE(YDATA,Y,23)  ;ANDERE DATEI ;data file 
	. . . KILL YSFELD,YSDATEI
	. . . SET YSDATEI="^"_YFILES_"("_YYYM(1)_SKEY
	. . . IF $PIECE(YDATA,Y,12)'="" IF $PIECE(YDATA,Y,13)'="" DO  ;UCI UND VOL ;UCI And 
	. . . . SET YSDATEI="^["_""""_$PIECE(YDATA,Y,12)_""""_","_""""_$PIECE(YDATA,Y,13)_""""_"]"_$PIECE(YSDATEI,"^",2,999)
	. . . ;
	. . . SET YMAX=1
	. . . SET YSDATEI(YMAX)=YSDATEI
	. . . FOR YI=1:1 SET YLFN=$PIECE(YSKEY(SKEY),",",YI) QUIT:YLFN=""  DO
	. . . . IF $EXTRACT(YLFN)="F" SET YSFELD(YI)=$PIECE(%FELD1,Y,+$EXTRACT(YLFN,2,9))
	. . . . IF $EXTRACT(YLFN)="K" SET YSFELD(YI)=$PIECE(YKEY,",",+$EXTRACT(YLFN,2,9))
	. . . . IF YI=1 DO   ;WENN ERSTER KEY ;when premier KEY 
	. . . . . FOR  QUIT:$EXTRACT(YSFELD(YI))'=";"  SET YSFELD(YI)=$EXTRACT(YSFELD(YI),2,200)
	. . . . . IF $EXTRACT($REVERSE(YSFELD(YI)))=";" FOR  QUIT:$EXTRACT($REVERSE(YSFELD(YI)))'=";"  SET YSFELD(YI)=$REVERSE($EXTRACT($REVERSE(YSFELD(YI)),2,200))
	. . . . . SET YMAX=$LENGTH(YSFELD(YI),";")  ;ANZAHL DER ";" FELDER IM FELD
	. . . . . FOR YMAX1=1:1:YMAX SET YSDATEI(YMAX1)=YSDATEI
	. . . . ;
	. . . . IF YSFELD(YI)="" SET YSFELD(YI)=" "
	. . . . FOR YMAX1=1:1:YMAX DO
	. . . . . SET YMAX2=YMAX1
	. . . . . IF YMAX=1 SET YMAX2=200  ;ANZAHL DER ";" FELDER IN DATENFELD
	. . . . . IF YI=1 DO
	. . . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU($PIECE(YSFELD(YI),";",YMAX1,YMAX2),1),1,150)     ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE($PIECE(YSFELD(YI),";",YMAX1,YMAX2),LC,UC),1,150)  ;SPEED UP;TYBD;25.09.2004 ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)
	. . . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU(YSFELD(YI),1),1,150)
	. . . . . . IF YSFELD(YMAX1,YI)="" SET YSFELD(YMAX1,YI)=" "
	. . . . . . SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_""""_YSFELD(YMAX1,YI)_""""
	. . . . . ;
	. . . . . IF YI'=1 DO  ;TYBD;NEU WEGEN FEHLER; WENN MEHRFACH UND UNTERSORT, DANN WURDE NUR DAS ERSTE FELD GENOMMEN
	. . . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU(YSFELD(YI),1),1,150)     ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . . IF $EXTRACT(YLFN)="F" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)  ;SPEED UP;TYBD;25.09.2004 ;KEINE UMLAUTE;TYBD;6,10,2004;26526
	. . . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU=""  SET YSFELD(YMAX1,YI)=$EXTRACT($TRANSLATE(YSFELD(YI),LC,UC),1,150)
	. . . . . . IF $EXTRACT(YLFN)="K" IF YUMLAU'="" SET YSFELD(YMAX1,YI)=$EXTRACT($$^WWWUMLAU(YSFELD(YI),1),1,150)
	. . . . . . IF YSFELD(YMAX1,YI)="" SET YSFELD(YMAX1,YI)=" "
	. . . . . . SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_""""_YSFELD(YMAX1,YI)_""""
	. . . ;
	. . . FOR YMAX1=1:1:YMAX DO
	. . . . SET YQ=1
	. . . . ;FOR YI=1:1:MAXYKEY SET KEY=$PIECE(YKEY,",",YI) SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_""""_KEY_"""" IF KEY="" SET YQ=0
	. . . . FOR YI=1:1:MAXYKEY SET KEY=$TRANSLATE($PIECE(YKEY,",",YI),"""") SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_","_""""_KEY_"""" IF KEY="" SET YQ=0  ;FIS;06.08.04;26208; ALPHAN. KEY (IN ANFÜHRUNGSZEICHEN)
	. . . . SET YSDATEI(YMAX1)=YSDATEI(YMAX1)_")"
	. . . . QUIT:YQ=0
	. . . . KILL @YSDATEI(YMAX1)
	
	quit
	
	
SUBKILL
	;-------------------------------------------------------------------------------
	; Delete subforms from buttons
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 23-Oct-2006	JW		SR15134: Newed variables
	;-------------------------------------------------------------------------------
	new YLFN,YXXFORM,YXXDATEI
	
	set YLFN = ""
	for {
		set YLFN = $order(^WWW124(0,YFORM,SPRACHE,YLFN))
		quit:YLFN=""

		set YXXFORM = $$$WWW124NewFormOnClick($get(^WWW124(0,YFORM,SPRACHE,YLFN,1)))
		if YXXFORM'="" {
			if $$$WWW121FixedInputForHiddenField($get(^WWW121(0,YXXFORM,1,1)))=$$$YES {
				set YXXDATEI=""
				for YI=1:1:MAXYKEY {
					set KEY = $piece(YKEY,$$$COMMA,YI)
					set:KEY'="" YXXDATEI = YXXDATEI_","_""""_KEY_""""
				}
				set YXXDATEI=$extract(YXXDATEI,2,999)
				do ^WWWSKILL($$$WWW120ClassUsedInForm($get(^WWW120(0,YXXFORM,1))),YXXDATEI)
			}
		}
	}
	
	QUIT
	
SUBKILL1
	;-------------------------------------------------------------------------------
	; Delete subforms that are specified on form def
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-Jun-2009	GRF		field macro
	; 23-Oct-2006	JW		SR15134: Newed variables
	;-------------------------------------------------------------------------------
	new YLFN,YXXFORM,YXXDATEI,YF,YI
	
	for YF=1:1 {
		set YXXFORM = $piece($translate($$$WWW120DeleteAlsoTheFollowingSub(YVOR),",",";"),";",YF)
		quit:YXXFORM=""
		
		if $$$WWW121FixedInputForHiddenField($get(^WWW121(0,YXXFORM,1,1)))=$$$YES {
			set YXXDATEI=""
			for YI=1:1:MAXYKEY {
				set KEY = $piece(YKEY,$$$COMMA,YI)
				if KEY'="" {
					set:YXXDATEI'="" YXXDATEI = YXXDATEI_","
					set YXXDATEI = YXXDATEI_""""_KEY_""""
				}
			}
			do ^WWWSKILL($$$WWW120ClassUsedInForm($get(^WWW120(0,YXXFORM,1))),YXXDATEI)
		}
	}
	
	QUIT
	
]]></Routine>
</Export>