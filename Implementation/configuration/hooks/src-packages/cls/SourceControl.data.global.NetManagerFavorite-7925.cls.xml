<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="9" zv="CACHE" ts="2001-01-01 00:00:00">
<Class name="SourceControl.data.global.NetManagerFavorite">
<ClassType/>
<Import>SourceControl.control,SourceControl.data</Import>
<ProcedureBlock>1</ProcedureBlock>
<Super>GlobalBase</Super>

<Method name="ExportAll">
<ClassMethod>1</ClassMethod>
<ReturnType>%Integer</ReturnType>
<Implementation><![CDATA[
	set strStatus = $$$OK

	set nErrors = 0
	set nExported = 0

	// Get all the forms
	set id = ""
	for {
		set id = $order(^COMView(0, id))
		quit:(id = "")

		set strStatus = ..Export(id, 2)
		if ($$$ISERR(strStatus)) {
			set nErrors = nErrors + 1
		}
		else {
			set nExported = nExported + 1
		}
	}

	write !, "Number of exported files: "_nExported
	write !, "Number of errors during export: "_nErrors

	quit nErrors
]]></Implementation>
</Method>

<Method name="Export">
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String,printMessage:%Boolean=1</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set sc = $$$OK

	if ($get(id) = "") {
		quit ##class(Errors).MakeAndPrintErrorStatus("Empty form "_
			"name!", printMessage)
	}

	if ('$data(^COMView(0, id))) {
		quit ##class(Errors).MakeAndPrintErrorStatus("Net Manager "_
			"form definition not found: "_id_". Skipping "_
			"it.", printMessage)
	}

	// Empty ignored list
	set ignoredList = ""

	set fileName = ##class(DirectoryFile).ConcatPath(
		##class(Config).GetExternalNMFavoritesDir(),
		##class(FileControl).GetSafeFilename(id)_"."_
		##class(FileType).%GetParameter("TXTFAVORITES"))

	set subs = $listbuild("0,"_##class(Strings).QuoteString(id))
	set sc = ..ExportGlobalNode(fileName, id, ..GetGlobals(), subs,
		ignoredList, printMessage=2, .ignored, 0)

	if ($$$ISOK(sc) && ('ignored)) {
		if (printMessage) {
			write !, "Favorite '"_id_"' successfully "_
				"exported as '"_fileName_"'" 
		}

		// Update the timestamp, so that the source control can use
		// it to prevent the same file from being loaded again.
		do ##class(FileControl).UpdateTimeStamp(fileName)
	}

	if ($$$ISOK(sc)) {
		set fileName = ##class(DirectoryFile).ConcatPath(
			##class(Config).GetExternalNMFavoritesDir(),
			##class(FileControl).GetSafeFilename(id)_"."_
			##class(FileType).%GetParameter("TXTCUSTOMFAVORITES"))

		set sc = ..ExportGlobalNode(fileName, id, ..GetCustomGlobals(),
			subs, ignoredList, printMessage=2, .ignored, 1)

		if ($$$ISOK(sc) && ('ignored)) {
			if (printMessage) {
				write !, "Favorite '"_id_"' successfully exported as "_
					"'"_fileName_"'" 
			}

			// Update the timestamp, so that the source control can use
			// it to prevent the same file from being loaded again.
			do ##class(FileControl).UpdateTimeStamp(fileName)
		}
	}

	quit sc
]]></Implementation>
</Method>

<Method name="IsToIgnore">
<ClassMethod>1</ClassMethod>
<FormalSpec>globalName:%String,node:%String,custom:%Boolean</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	set id = ""
	set subId = ""
	if ((globalName = "^COMView") ||
		(globalName = "^COMViewColumn") ||
		(globalName = "^COMViewFilter")) {
		set id = $qsubscript(node, 2)
		set subId = $qsubscript(node, 3)
	}
	elseif (globalName = "^COMViewLang")||(globalName = "^COMViewD") {
		set id = $qsubscript(node, 2)
		set subId = $qsubscript(node, 4)
	}

	set ignore = ..IsDeleted(id, subId)

	if ('ignore) {
		set ignore = ..IsPrivate(id, subId)
	}

	if ('ignore)&&(globalName'="^COMViewD") {
		set favCustom = ..IsCustom(id, subId)

		if ((('custom) && favCustom) ||
			(custom && ('favCustom))) {
			set ignore = 1
		}
	}

	quit ignore
]]></Implementation>
</Method>

<Method name="IsDeleted">
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String,subId:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	if (($length(id) '= 0) && ($length(subId) '= 0) &&
		('$data(^COMView(0, id, subId, 1)))) {
		quit 1
	}

	quit 0
]]></Implementation>
</Method>

<Method name="IsPrivate">
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String,subId:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	#define COMViewUser1(%obj) $piece(%obj,"~",3)

	if (($length(id) = 0) || ($length(subId) = 0) ||
		('$data(^COMView(0, id, subId, 1)))) {
		quit 0
	}

	set favorite = $get(^COMView(0, id, subId, 1))

	quit ($length($$$COMViewUser1(favorite)) > 0)
]]></Implementation>
</Method>

<Method name="IsCustom">
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String,subId:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	#define COMViewDistribute(%obj) $piece(%obj,"~",5)

	if (($length(id) = 0) || ($length(subId) = 0) ||
		('$data(^COMView(0, id, subId, 1)))) {
		quit 0
	}

	set favorite = $get(^COMView(0, id, subId, 1))

	quit (+$$$COMViewDistribute(favorite) = $$$NO)
]]></Implementation>
</Method>

<Method name="GetGlobals">
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	// Lista de favoritos
	quit $listbuild("^COMView", "^COMViewColumn", "^COMViewFilter",
		"^COMViewLang")
]]></Implementation>
</Method>

<Method name="GetCustomGlobals">
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	quit ..GetGlobals()_$lb("^COMViewD")
]]></Implementation>
</Method>
</Class>
</Export>