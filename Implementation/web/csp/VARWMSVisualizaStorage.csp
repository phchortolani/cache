<script language="cache" runat="server">
;-------------------------------------------------------------------------------
; Descrição:
;	O conteúdo que é exibido nesta página é inserido dinamicamente (via Ajax) 
;	dentro dos DIV's dos endereços de armazenagem. Este conteúdo é então tratado
;	e utilizado na função Javascript  'targetDiv' da página VARWMSVisualizaRua.csp.
;
;	OBSERVAÇÃO IMPORTANTE: o conteúdo gerado por este arquivo csp é submetido recebido
;   como resposta a uma chamada Ajax e é separado em duas partes pelo delimitador "||".
;	Esta separação é feita da seguinte maneira:
;			
;					 END. ARMAZENAGEM || CONTEÚDO HTML
;
;	Verifique a função 'targetDiv' citada para um melhor entendimento.	
;
; Histórico:
; 	02-Fev-2012  Gustavo	Criado
;-------------------------------------------------------------------------------	

new pidLocal, pidEndereco, pModo, idItem, Local, Storage, fltQuant, lote, dteValidade, idModulo, idRua, idPredio,
	idAndar, idApto, objItem, descItem, unidade, categAlertaItem, descCategAlerta, bkgColor, nomeUsuario,
	dteLocked, StockStorageSQL, meuSQL, idPalete

set pidLocal	= $get(%request.Data("local",1))
set pidEndereco = $get(%request.Data("endereco",1))
set pModo       = $get(%request.Data("modo",1))
set pIsStorageBlocked = $get(%request.Data("bloqueio",1))
set pUserLockedObject = $get(%request.Data("situacao",1))

//ATENÇÃO: NÃO REMOVA A LINHA ABAIXO POIS ELA É IMPRESCINDÍVEL PARA O CORRETO FUNCIONAMENTO
//         DA FUNÇÃO 'targetDiv' DA PÁGINA VARWMSVisualizaRua.csp.
write pidEndereco_"||"

//Variáveis ambiente que serão necessárias. Basta isso, não precisa ficar chamando ^WWWVAR.
set YM = "0"
set Y  = "~"
set YBED 	 = "GUSTAVO"
set SPRACHE  = "PT"
set YDECIMAL = ","

set StockStorageSQL = ##class(%ResultSet).%New()  ;Create Result Set Object    
    
set meuSQL = "SELECT alSOH.dBundleStock.Item, "
set meuSQL = meuSQL_" alSOH.dBundleStock.Storage->Location,  "
set meuSQL = meuSQL_" alSOH.dBundleStock.Storage->Code,  "
set meuSQL = meuSQL_" +alSOH.dBundleStock.QtyOnHand,  "
set meuSQL = meuSQL_" alSOH.dBundleStock.Bundle->LotNumber,  "
set meuSQL = meuSQL_" alSOH.dBundleStock.Bundle->UseByDate  "
set meuSQL = meuSQL_"  FROM alSOH.dBundleStock "
set meuSQL = meuSQL_" WHERE alSOH.dBundleStock.Storage->Location = '"_pidLocal_"'"

if (pModo = "Palete") {	
	set meuSQL = meuSQL_" AND alSOH.dBundleStock.Storage->Physical->Code = '"_pidEndereco_"'"		
} else {
	set meuSQL = meuSQL_" AND alSOH.dBundleStock.Storage->Code = '"_pidEndereco_"'"
}

set meuSQL = meuSQL_" ORDER BY $$SQLGetEnderecoParaOrdenar^VARSQL(alSOH.dBundleStock.Storage->Code), alSOH.dBundleStock.Item"
    
do StockStorageSQL.Prepare(meuSQL)  ;Prepare Query
do StockStorageSQL.Execute()  ;Execute Query    		  
   		
set idModulo = $piece(pidEndereco,"-",1)
set idRua    = $piece(pidEndereco,"-",2)
set idPredio = $piece(pidEndereco,"-",3)
set idAndar  = $piece(pidEndereco,"-",4)
set idApto   = $piece(pidEndereco,"-",5)
   		
&html<
	<table>
   		<tr>
   			<td class='mark storageDiv' colspan='7' align='left' style='background-color:lightsteelblue;'>
   				<strong>Endereço de origem - Módulo: #(idModulo)#&nbsp;
   				Rua: #(idRua)# Prédio: #(idPredio)# Andar: #(idAndar)# Apartamento: #(idApto)# (#(pidEndereco)#)</strong>
   			</td>
   		</tr>
	>
	
   	if (pModo = "Palete") {
	   	
	   	//Melhoria de performance - utilizando cache, substituindo-o pela consulta SQL caso encontre algo
	   	set idPalete = $piece($get(^VARTempWMSCacheEndereco(YBED,pidEndereco)),Y,2)	   	
	   	if (idPalete = "") {
	   		set idPalete = $$getPalletInStorage^VARWMSUtils(pidLocal,pidEndereco)
	   	}
	   	
		&html< 
		<tr>
			<td class='mark storageDiv' colspan='7' align='left' style='background-color:#b0deb3;'>
				<strong>Palete: #(idPalete)#</strong>
			</td>
		</tr>
		>	   	
   	}
   	
	//Objeto bloqueado por outro usuário
	if (pUserLockedObject '= "") {
			
		set nomeUsuario = $extract($$SQLGetUserName^VARSQL(pUserLockedObject),1,25)
		set dteLocked = $piece($get(^VARTempWMSTransaction(YM,pUserLockedObject,pidLocal,pidEndereco)),Y,2)
		
		if dteLocked '= "" set dteLocked = $zdatetime(dteLocked,4)
		
		&html< 
		<tr>
			<td class='mark storageDiv' colspan='7' align='left' style='background-color:#ffb166;'>
				Endereço sendo manuseado por <strong>#(nomeUsuario)#</strong> desde <strong>#(dteLocked)#</strong>
			</td>
		</tr>
		>			
	}
	
	//Endereço bloqueado		
	if (pIsStorageBlocked = $$$YES) {
		
		set descMotivoBloq = $$getMotivoEndBloqueado^VARWMSUtils(pidLocal,pidEndereco)
		
		&html< 
			<tr>
				<td class='mark storageDiv' colspan='7' align='left' style='background-color:#ffb166;'>
					Endereço <strong>bloqueado</strong>. Motivo: <strong>#(descMotivoBloq)#</strong>
				</td>
			</tr>
		>			
	}
	
   	&html<
   		<tr style='font-weight:bold;background-color:#E0E0E0;'>
			<td class='mark storageDiv' align='left'>&nbsp;</td>
			<td class='mark storageDiv' align='left'>Código</td>
			<td class='mark storageDiv' align='left'>Produto</td>
			<td class='mark storageDiv' align='center'>Qtde</td>
			<td class='mark storageDiv' align='center'>UN</td>
			<td class='mark storageDiv' align='center'>Lote</td>
			<td class='mark storageDiv' align='center'>Validade</td>
		</tr>
	>
	
	While (StockStorageSQL.Next()) {
		
		set idItem		= StockStorageSQL.GetData(1)
		set Local       = StockStorageSQL.GetData(2)
		set Storage     = StockStorageSQL.GetData(3)
		set fltQuant    = StockStorageSQL.GetData(4)
		set lote        = StockStorageSQL.GetData(5)
		set dteValidade = StockStorageSQL.GetData(6)

	 	set objItem  = $get(^INART(YM,idItem,1)) 	
		set descItem = $extract($piece(objItem,Y,1),1,35)
		set fltQuant = $$^WWWTR(YM,12,fltQuant)
		set unidade  = $$SQLGetUnitSigla^VARSQL(idItem)
		
		set categAlertaItem = $piece(objItem,Y,315)			
		if dteValidade '= "" set dteValidade = $zdate(dteValidade,4)
		
		&html< <tr style="background-color:#FFFFFF;"> >
		
		if (categAlertaItem '= "") {
			set descCategAlerta = $piece($get(^INPARA(YM,"VARCATEGALERTA","PT",categAlertaItem,1)),Y,1)							
			set bkgColor = $$getBkgColorAlerta^VARWMSUtils(categAlertaItem,idItem,pidEndereco)
			
			&html< <td class='mark storageDiv' align='left' style='background-color:#(bkgColor)#'><strong>#(descCategAlerta)#</strong></td> >
		
		} else {
			&html< <td class='mark storageDiv'>&nbsp;</td> >
		}	
		
		//Caso o usuário esteja consultando o produto, destaca ele
		if ($piece($get(^VARTempWMSSearchItem(YM,YBED)),Y,1) = idItem) {
			
			set bkgColor = $$getBkgColorAlerta^VARWMSUtils(99)				
							
			&html<
				<td class='mark storageDiv' style='background-color:#(bkgColor)#' align='left'>#(idItem)#</td>
			>
		
		} else {
			&html<
				<td class='mark storageDiv' align='left'>#(idItem)#</td>
			>				
		} 			

		&html<
			<td class='mark storageDiv' align='left'>#(descItem)#</td>
			<td class='mark storageDiv' align='left'>#(fltQuant)#</td>
			<td class='mark storageDiv' align='center'>#(unidade)#</td>
			<td class='mark storageDiv' align='center'>#(lote)#</td>
			<td class='mark storageDiv' align='center'>#(dteValidade)#</td>
		</tr>
		>
	}
	
&html< </table> >

do StockStorageSQL.Close()

</script>