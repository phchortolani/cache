<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINART" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINART
#include COMSYS
#include INConst
#include WWWConst
#define Patrimonio 3
#define FarmaciaCentral 5
#define AlmoxarifadoCentral 1
 
OnAfterDataFieldsCustom(pidItem)
	new strListaFornecedores, count, idFornecedor, objFornecedor, nomeFantasia, cnpjcpf,
		telefone, fax
	
	;Preservar a execução do OnAfterDataFields do formulário, e incluir o da customização	
	set YSTOP = $$$NO
	
	quit:($$$NoKey(pidItem))
	
	if (($get(YSEITE)) = 51) {  		// Aba Fornecedores
		do PrintFornecedores(pidItem)  
	
	} elseif (($get(YSEITE) = 53)) { 	// Aba Histórico	
		do PrintHistoricoItem(pidItem)
	}
	
	quit
 
OnBeforeSaveHookVAR(YKEY,YFELD)
	new strStatus, idLocal, newStatus, oldStatus, fltSOH, prefixoProduto, naturezaFornec, estoquePrincip,
		categPadrao, newNaturezaFornecimento, oldNaturezaFornecimento
	
	set strStatus = $$$OK	
	set idLocal   = $get(YLOCATION)
	
	if (YFORM = "INART") {
	
		if (YKEY '= "") {
			set newStatus = $$$INARTHoldReason(YFELD)
 
			set objItem   = $get(^INART(YM,YKEY,1))
			set oldStatus = $$$INARTHoldReason(objItem)
 
			if ( (newStatus '= oldStatus) && (newStatus = 4) ) {
				set fltSOH = ##class(alSOH.iSOH).GetQtyAvailable(YKEY)
				if (fltSOH > 0) {
					set strStatus = "Não é possível inativar este produto pois ele contém quantidade em estoque na rede."
				}
			}
 
			if $$$ISOK(strStatus) {
 
				set newNaturezaFornecimento = $$$INARTFREE1(YFELD)
				set oldNaturezaFornecimento = $$$INARTFREE1(objItem)
			
				if ( (newNaturezaFornecimento '= oldNaturezaFornecimento) && ($$IsInUse^INART(YKEY) = $$$YES) ) {
					set strStatus = "Não é possível alterar a natureza de fornecimento deste produto pois ele está em uso."
				}
			}
 
			if $$$ISOK(strStatus) {
 			
				set prefixoProduto = $extract(YKEY,1)
				set naturezaFornec = $$$INARTFREE1(YFELD)
				set estoquePrincip = $$$INARTFREE35(YFELD)
				set categPadrao	   = $$$INARTFREE29(YFELD)
		
				if (prefixoProduto = "P") {
					//ATENÇÃO = Se o item for de patrimônio tem que ter obrigatóriamente o piece 29=1 para que não haja entrada em estoque.				
					if (naturezaFornec '= $$$Patrimonio) { 
						set strStatus = "Produtos com o prefixo 'P' tem que ter a natureza de fornecimento igual a patrimônio. Favor verificar."
				
					} elseif (idLocal = $$$FarmaciaCentral) {
						set strStatus = "Somente o Almoxarifado Central pode cadastrar ou alterar produtos com o prefixo 'P' de Patrimônio. Favor verificar o seu local."
				
					} elseif (estoquePrincip '= "ALMOXARIFADO") {
						set strStatus = "Produtos com o prefixo 'P' tem que ter o estoque principal igual a ALMOXARIFADO Favor verificar."
					}
			
				} elseif (prefixoProduto '= "P") {
					if ( (naturezaFornec = $$$Patrimonio) && (estoquePrincip '= "ALMOXARIFADO") ) {
						set strStatus = "Produtos com natureza de fornecimento patrimônio devem ter o estoque principal igual a ALMOXARIFADO. Favor verificar."
					}
				}
			
				if $$$ISOK(strStatus) {			
					if ( (idLocal = $$$FarmaciaCentral) && (naturezaFornec = $$$Patrimonio) ) {
						set strStatus = "Somente o Almoxarifado Central pode cadastrar ou alterar produtos com natureza de fornecimento igual a Patrimônio. Favor verificar."
					}
			
					if (estoquePrincip = "ALMOXARIFADO") && ((categPadrao = 0) || (categPadrao = 1)) {
						set strStatus = "Se o Estoque Principal é Almoxarifado, a categoria de item padrão deve ser Material de Almoxarifado."
					}
					if (estoquePrincip = "FARMACIA") && (categPadrao = 2) {
						set strStatus = "Se o Estoque Principal é Farmácia, a categoria de item padrão não pode ser Material de Almoxarifado."
					}						
				}
			}
		}
	}
	
	;vvvvvv SESDF-495 vvvvvv
	/* Essa validação foi removida pois esta causando sérios problemas na produção. 
	Posteriormente pode ser implementada, mas recomenda-se utilizar outro método, como deixar o form somente leitura.
	
	if $$$ISOK(strStatus) {
		if ($$IsEditable^VARAcessoGrupoProduto(YFELD) = $$$YES){
			set strStatus = "Não é possível salvar o registro pois o grupo do item não é permitido para este perfil de usuário."
		}
	}
	*/
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
	}
 
	quit strStatus
	
PrintFornecedores(pidItem)
	quit:(pidItem = "")
	
	new strListaFornecedores, count, lstHeader, idFornecedor, objFornecedor, nomeFantasia, cnpjcpf,
		email, telefone, fax
	
	set strListaFornecedores = $$GetFornecedores(pidItem)
	set count = 0
		
	//Monta cabeçalho da tabela
 	set lstHeader=""
  	set lstHeader=lstHeader_$listbuild("Seq.","Cód.","Fornecedor","CNPJ/CPF","Email","Telefone 1","Fax")
				 			    			    							 	
  	if $$Start^COMTable(lstHeader,"<div style=margin:3px;>Fornecedores identificados para o produto</div>",,,$$$NO) {
		
		for cnt = 1:1:$length(strListaFornecedores,";") {      		
    		set idFornecedor = $piece(strListaFornecedores,";",cnt)
     			
    		continue:(idFornecedor = "")
    		continue:(idFornecedor = 0)  ;Não mostrar o fornecedor 0-Própria Empresa
    		set count = $i(count)
      			
    		set objFornecedor = $get(^INLIEF(YM,idFornecedor,1))
    		set nomeFantasia  = $piece(objFornecedor,Y,8)
    		set cnpjcpf       = $piece(objFornecedor,Y,201)
      		set email         = $piece(objFornecedor,Y,24)
      		set telefone      = $piece(objFornecedor,Y,208)
      		set fax       	  = $piece(objFornecedor,Y,22)
      			
      		do NewLine^COMTable()
      		do InsertCell^COMTable(count,"INLIEF",idFornecedor)
      		do InsertCell^COMTable(idFornecedor,"INLIEF",idFornecedor)
      		do InsertCell^COMTable(nomeFantasia,"INLIEF",idFornecedor)
      		do InsertCell^COMTable(cnpjcpf,"INLIEF",idFornecedor)
      		do InsertCell^COMTable(email,"INLIEF",idFornecedor)
      		do InsertCell^COMTable(telefone,"INLIEF",idFornecedor)
      		do InsertCell^COMTable(fax,"INLIEF",idFornecedor)
      		do EndLine^COMTable()		
		}
		
		if (count = 0) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Nenhum fornecedor identificado para o produto.",,,,,,7)
			do EndLine^COMTable()			
		}
		
		if ((YKEY = "") || (YKEY = "+")) {
			do NewLine^COMTable()
			do InsertCell^COMTable("Favor selecionar um produto.",,,,,,7)
			do EndLine^COMTable()
		}
		
		do Stop^COMTable()
  	}
 
	quit
 
 
GetFornecedores(pListItems)
	quit:(pListItems = "") ""
	
	new strListaFornecedores, cnt, idItem	
	set strListaFornecedores = ""
 
	for cnt = 1:1:$length(pListItems,";") {
		
		set idItem = $piece(pListItems,";",cnt)
		
		continue:(idItem = "")
		continue:('$data(^INART(YM,idItem)))
 
		// 1. Busca a lista com as empresas que fornecem determinado produto, conforme relacionamento definido na INART
		new idFornecedor, objFornecedor, strGroups, strItems, strItemsFind, itemFind, idGroup, groupFind, strGroupsFind,
			idFornecedorFind
 
		$$$Order2(^INLIEF,YM,idFornecedor)		
			set objFornecedor = $get(^INLIEF(YM,idFornecedor,1))			
			
			//Verifica se o fornecedor já foi selecionado 
			set idFornecedorFind = ";"_idFornecedor_";"
			continue:($find(strListaFornecedores,idFornecedorFind))			
			
			set strItems  = $piece(objFornecedor,Y,229)
			set strGroups = $piece(objFornecedor,Y,246)
					
			if (strItems '= "") {
				set strItemsFind = ";"_strItems_";"			
				set itemFind = ";"_idItem_";"
		
				if $find(strItemsFind,itemFind) {
				
					if (strListaFornecedores = "") {
						set strListaFornecedores = strListaFornecedores_";"_idFornecedor_";"
					} else {
						set strListaFornecedores = strListaFornecedores_idFornecedor_";"
					}
				
					continue ;se já encontrou o produto individualmente, não há porque verificar se o grupo está lá também
				}
			}
		
			if (strGroups '= "") {
				set strGroupsFind = ";"_strGroups_";"			
			
				set idGroup = $piece($get(^INART(YM,idItem,1)),Y,30)
				set groupFind = ";"_idGroup_";"
		
				if $find(strGroupsFind,groupFind) {
					if (strListaFornecedores = "") {
						set strListaFornecedores = strListaFornecedores_";"_idFornecedor_";"
					} else {
						set strListaFornecedores = strListaFornecedores_idFornecedor_";"
					}
				}
			}		
	
		$$$End
 
		// 2. Busca a lista com as empresas que já forneceram determinado produto conforme relacionamento na INAUF 
		new idCompra, idFornecedorCompra, idFornecedorCompraFind, objCompra
		$$$Order4(^INAUFPs,YM,8,idItem,idCompra)
	
			set objCompra = $get(^INAUF(YM,idCompra,1))
			set idFornecedorCompra = $piece(objCompra,Y,12)
			continue:(idFornecedorCompra = "")
		
			set idFornecedorCompraFind = ";"_idFornecedorCompra_";"
		
			if '$find(strListaFornecedores,idFornecedorCompraFind) {
				if (strListaFornecedores = "") {
					set strListaFornecedores = strListaFornecedores_";"_idFornecedorCompra_";"
				} else {
					set strListaFornecedores = strListaFornecedores_idFornecedorCompra_";"
				}
			}
		
		$$$End
 
 
		// 3. Busca a lista com as empresas que possuem de preço registrado no catálogo do banco de preços 
		new idLinha, idFornecedorCatalogo, idFornecedorCatalogoFind, objBancoPrecoLinha
		$$$Order3(^VARBancoPrecoLinha,YM,idItem,idLinha)
	
			set objBancoPrecoLinha = $get(^VARBancoPrecoLinha(YM,idItem,idLinha,1))
			set idFornecedorCatalogo = $piece(objBancoPrecoLinha,Y,1)
			continue:(idFornecedorCatalogo = "")
		
			set idFornecedorCatalogoFind = ";"_idFornecedorCatalogo_";"
		
			if '$find(strListaFornecedores,idFornecedorCatalogoFind) {
				if (strListaFornecedores = "") {
					set strListaFornecedores = strListaFornecedores_";"_idFornecedorCatalogo_";"
				} else {
					set strListaFornecedores = strListaFornecedores_idFornecedorCatalogo_";"
				}
			}
		
		$$$End
	
		// 4. Busca a lista das empresas que foram participantes em estimativa de preços
		new idFornecedor, idParticipante, idParticipanteFind
		$$$Order3(^VARBancoPrecoEstimativaForns,YM,1,idParticipante)
			continue:(idParticipante = "")
		
			set idParticipanteFind = ";"_idParticipante_";"
		
			if '$find(strListaFornecedores,idParticipanteFind) {
				if (strListaFornecedores = "") {
					set strListaFornecedores = strListaFornecedores_";"_idParticipante_";"
				} else {
					set strListaFornecedores = strListaFornecedores_idParticipante_";"
				}
			}
		$$$End
	
	
		// 5. Busca a lista das empresas que possuem cotação registrada (preço) em estimativas de preços
		new idFornecedor, idFornecedorEstimativaFind
		$$$Order3(^VARBancoPrecoEstimativaProps,YM,1,idFornecedor)
			continue:(idFornecedor = "")
		
			set idFornecedorEstimativaFind = ";"_idFornecedor_";"
		
			if '$find(strListaFornecedores,idFornecedorEstimativaFind) {
				if (strListaFornecedores = "") {
					set strListaFornecedores = strListaFornecedores_";"_idFornecedor_";"
				} else {
					set strListaFornecedores = strListaFornecedores_idFornecedor_";"
				}
			}
		$$$End
	
	}
 
	quit strListaFornecedores
 
GerarChaveINART(pYKEY, pYFELD)
	;do GerarChaveINART^VARINART(YKEY, YFELD)
 
	if (pYKEY = 0) || (pYKEY = "") || (pYKEY = "+") {
	
		new codSubGrupo, codGrupo, novoCodigo, proxCodigo, proxCodigoStr
		
		set strStatus = $$$OK
		
		set codSubGrupo = $piece(pYFELD,Y,30)
		set codGrupo    = $piece(codSubGrupo,".",1)
 
		set proxCodigo  = $increment(^VARINARTContador(codGrupo), 1)
		
		;Preenche com zeros a esquerda
		set proxCodigoStr = $$FillChar^VARSQL(proxCodigo,4,"0")
		
		set novoCodigo = codGrupo_proxCodigoStr
 
		if $D(^INART(YM,novoCodigo)){
			set strStatus = "O produto "_novoCodigo_" não pôde ser cadastrado."
		}
		else{
			;Insere os dados na Classe(Classe,Chave Primária,Valores,Always Save)
			set strStatus = $$$Save("INART",novoCodigo,pYFELD,1)
		}
 
		if strStatus'=$$$OK {
			set Q = $$$QDontSave
			do ^WWWINFO(strStatus)		
		}
		else{
 
			$$$StartScript()
			write "BEARB('Salvo',1); "
			$$$EndScript()
 
			
			;Altera a aba para 50-Principal, que é customizada
			;Pois estava abrindo na aba 1-Geral
			set YSEITE = 50
			do RedirectForm^COMUtilForm("INART",novoCodigo,YBACK,YPARA,YSEITE)
			//do GoToForm^COMUtilForm("INART", novoCodigo, 1)
			
			set Q = $$$QDontSave
			
		}
 
	}
 
 
	quit	
	
	
PopularContador()
	;do PopularContador^VARINART()
 
	if $G(YM) = ""{
		do ^WWWVAR
	}
	
	
	;Limpa global
	kill ^VARINARTContador
	
	;Para cada produto 
	set codItem = ""
	for {
		set codItem = $order(^INART(0,codItem))
		quit:(codItem = "")
 
		set objITEM = $get(^INART(0,codItem,1))
		if objITEM = ""{
			W "Dados não encontrados: "_codItem ,!
			continue
		}
		
		set codSubGrupoCad = $piece(objITEM,Y,30)
		if codSubGrupoCad = ""{
			W "Sub-Grupo em branco: "_codItem ,!
			continue
		}
		
		set codGrupoCad = $piece(codSubGrupoCad,".",1)
		if codGrupoCad = ""{
			W "Grupo em branco: "_codItem ,!
			continue
		}
			
		
		set tamGrupo   = $length(codItem)-4
		set codGrupo   = $extract(codItem,1,tamGrupo)
		set codProduto = $extract(codItem,tamGrupo+1,$length(codItem))
		
		if (codGrupoCad'=codGrupo) {
			W "Grupo cadastrado diferente do código do produto: "_codItem_" Grupo Cadastrado: "_codGrupoCad ,!
			continue	
		}
		;W codItem_": "_codGrupo_"-"_codProduto_"#"
		
		;Remove zeros a esquerda e grava no contador
		set ^VARINARTContador(codGrupo) = $zstrip($fnumber(codProduto,"T"),"<>W")
		
	}
	
	W "Contadores gerados:" ,!
	set codContador = ""
	for {
		set codContador = $order(^VARINARTContador(codContador))
		quit:(codContador = "")
		
		W "Grupo: "_codContador_" = "_$G(^VARINARTContador(codContador)) ,!
 
	}	
	
 
 
	quit
	
	
CheckItemPolicyHasProgram(pidItem)
	;Gustavo - 01/Set/2011 - Verifica se a política do produto contempla programa ou não
	quit:(pidItem = "") ""
	
	new objItem, idItemPolicy, objPolicy, ProgramPolicy, blnItemPolicyHasProgram
	
	set objItem = $get(^INART(YM,pidItem,1))
	set idItemPolicy = $$$INARTItemPolicy(objItem)
	
	if (idItemPolicy '= "") {		
		set objPolicy = $get(^INItemPolicy(YM,idItemPolicy,1))
		set ProgramPolicy = $$$INItemPolicyProgram1(objPolicy)
		
		if ( (ProgramPolicy = 1) || (ProgramPolicy = 3) ) { //1=Sim ou 3=Opcional
			set blnItemPolicyHasProgram = $$$YES
		} else {
			set blnItemPolicyHasProgram = $$$NO
		}
	}
	
	quit blnItemPolicyHasProgram
	
	
CheckItemPolicyHasBrand(pidItem)
	;Gustavo - 01/Set/2011 - Verifica se a política do produto contempla marca ou não
	quit:(pidItem = "") ""
	
	new objItem, idItemPolicy, objPolicy, ProgramPolicy, blnItemPolicyHasBrand
	
	set objItem = $get(^INART(YM,pidItem,1))
	set idItemPolicy = $$$INARTItemPolicy(objItem)
	
	if (idItemPolicy '= "") {		
		set objPolicy = $get(^INItemPolicy(YM,idItemPolicy,1))
		set ProgramPolicy = $$$INItemPolicyBrand(objPolicy)
		
		if ( (ProgramPolicy = 1) || (ProgramPolicy = 3) ) { //1=Sim ou 3=Opcional
			set blnItemPolicyHasBrand = $$$YES
		} else {
			set blnItemPolicyHasBrand = $$$NO
		}
	}
	
	quit blnItemPolicyHasBrand
	
	
PrintHistoricoItem(pidItem)
   ;-------------------------------------------------------------------------------
    ; Exibe tabela com o histórico de alterações do item.
    ;
    ; Histórico:
    ; 16-Out-2011	RGB					Alterações para pegar a ultima hora e usuários que alteraram.
    ;									Por causa de outro salvamento realizado pela ^INART, ignoro alterações feitos pelo mesmo usuário em um espaço de 2 segundos.
    ; 14-Set-2011	Gustavo Fiuza		Criado
    ;-------------------------------------------------------------------------------
	quit:(pidItem = "")
 
	new count, lstHeader, date, hour, idUsuario, objWWWPROAntes, objWWWPRODepois, acao, formatPre, formatPos,
		ADescResumida, DDescResumida, ADescCompleta, DDescCompleta, AUnidade, DUnidade, DGrupo, AGrupo, 
		APadrao, DPadrao, ACurvaABC, DCurvaABC, AClassXYZ, DClassXYZ, AStatus, DStatus, lastTimeChange,lastUserChange,lastDateChange		
 
	set formatPre = "<font color=red>"
	set formatPos = "</font>"
	set lastDateChange=""
	set lastTimeChange=""
	set lastUserChange=""
    set count = 0
    set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("Seq.","Ação","Data","Hora","Usuário","Descrição Resumida","Descrição Completa","UN","Grupo","Status","Padrão","ABC","XYZ")
 
    if $$Start^COMTable(lstHeader,"&nbsp;Histórico de Alterações do Produto",,,$$$NO) {
	    
	    $$$Order3(^WWWPRO,YM,"INART",date)
	    	$$$Order4(^WWWPRO,YM,"INART",date,hour)
	    		$$$Order5(^WWWPRO,YM,"INART",date,hour,idUsuario)
	    			$$$Order7(^WWWPRO,YM,"INART",date,hour,idUsuario,pidItem,idEstado)
	    				
	    				//Rodando somente no estado 1 (melhor performance)
	    				continue:(idEstado = 0)
	    				continue:((idUsuario=lastUserChange) && (lastDateChange=date) && (hour-lastTimeChange<2))
	    				set objWWWPROAntes  = $get(^WWWPRO(YM,"INART",date,hour,idUsuario,YKEY,0,1)) //Antes
	    				set objWWWPRODepois = $get(^WWWPRO(YM,"INART",date,hour,idUsuario,YKEY,1,1)) //Depois
	    			
	    				if ( (objWWWPROAntes = "") && (objWWWPRODepois '= "") ) {
		    				set acao = "Criado"
	    			
	    				} elseif ( (objWWWPROAntes '= "") && (objWWWPRODepois = "") ) {
		    				set acao = "Excluído"
	    			
	    				} else {
		    				set acao = "Alterado"
	    				}
	    			
	    				set ADescResumida    = $piece(objWWWPROAntes,Y,1)
	    				set ADescCompleta    = $piece(objWWWPROAntes,Y,10)
	    				set AGrupo	         = $piece(objWWWPROAntes,Y,30)
	    				set AUnidade         = $piece(objWWWPROAntes,Y,40)
	    				set AStatus			 = $piece(objWWWPROAntes,Y,66)
						set ACurvaABC		 = $piece(objWWWPROAntes,Y,166)
						set AClassXYZ		 = $piece(objWWWPROAntes,Y,277)	    				
	    				set APadrao			 = $piece(objWWWPROAntes,Y,280)
 
	    				set DDescResumida    = $piece(objWWWPRODepois,Y,1)
	    				set DDescCompleta    = $piece(objWWWPRODepois,Y,10)
	    				set DGrupo	  		 = $piece(objWWWPRODepois,Y,30)
	    				set DUnidade   		 = $piece(objWWWPRODepois,Y,40)
	    				set DStatus			 = $piece(objWWWPRODepois,Y,66)
						set DCurvaABC		 = $piece(objWWWPRODepois,Y,166)
						set DClassXYZ		 = $piece(objWWWPRODepois,Y,277)
	    				set DPadrao			 = $piece(objWWWPRODepois,Y,280)
						
						//Formatting data...
	    				if ( (ADescResumida '= DDescResumida) && (acao '= "Criado") ) {
	    					set DDescResumida = formatPre_DDescResumida_formatPos
	    				}
	    				
	    				if ( (ADescCompleta '= DDescCompleta) && (acao '= "Criado") ) {
	    					set DDescCompleta = formatPre_DDescCompleta_formatPos
	    				}
 
	    				if ( (AGrupo '= DGrupo) && (acao '= "Criado") ) {
	    					set DGrupo = formatPre_DGrupo_formatPos
	    				}
 
	    				if ( (AUnidade '= DUnidade) && (acao '= "Criado") ) {
		    				set DUnidade = formatPre_$$SQLGetSiglaUnit^VARSQL(DUnidade)_formatPos
	    				} else {
		    				set DUnidade = $$SQLGetSiglaUnit^VARSQL(DUnidade)
	    				}   								 				
	    				
	    				if ( (AStatus '= DStatus) && (acao '= "Criado") ) {
		    				set DStatus = formatPre_$piece($get(^INItemStatus(YM,DStatus,1)),Y,1)_formatPos
	    				} else {
		    				set DStatus = $piece($get(^INItemStatus(YM,DStatus,1)),Y,1)
	    				}
	    				
	    				if ( (ACurvaABC '= DCurvaABC) && (acao '= "Criado") ) {
	    					set DCurvaABC = formatPre_DCurvaABC_formatPos
	    				}
	    				
	    				if ( (AClassXYZ '= DClassXYZ) && (acao '= "Criado") ) {
	    					set DClassXYZ = formatPre_DClassXYZ_formatPos
	    				}	    					    				
	    				
	    				if ( (APadrao '= DPadrao) && (acao '= "Criado") ) {
		    				set DPadrao = formatPre_$$SQLGetYesOrNo^VARSQL(DPadrao)_formatPos
	    				} else {
		    				set DPadrao = $$SQLGetYesOrNo^VARSQL(DPadrao)
	    				}
					
						set count = $i(count)
						
						do NewLine^COMTable()
						do InsertCell^COMTable(count)
						do InsertCell^COMTable(acao)
						do InsertCell^COMTable($zdate(date,4))
						do InsertCell^COMTable($ztime(hour))
						do InsertCell^COMTable(idUsuario)
						do InsertCell^COMTable(DDescResumida,,,,,,,,,$$$YES)
						do InsertCell^COMTable(DDescCompleta,,,,,,,,,$$$YES)
						do InsertCell^COMTable(DUnidade)
						do InsertCell^COMTable(DGrupo)
						do InsertCell^COMTable(DStatus,,,,,,,,,$$$YES)
						do InsertCell^COMTable(DPadrao)
						do InsertCell^COMTable(DCurvaABC)
						do InsertCell^COMTable(DClassXYZ)
						do EndLine^COMTable()
						set lastDateChange=date
						set lastTimeChange=hour
	    				set lastUserChange=idUsuario
					$$$End	
				$$$End
			$$$End
		$$$End
    }
 
	quit
 
OnAfterSaveHook(YKEY,YFELD)
	set strStatus = $$$OK
 
	job LoadINARTLOCPAR^VARINART(YKEY)
 
    if ($$$INARTAverageCost(YFELD) '= $$$INARTAverageCost(YFELDOLD)) {
	    new objINARTT, idINARTT1, idINARTT2
	    set idINARTT1 = ""
	    for {
		    set idINARTT1 = $order(^INARTTs(0,1,YKEY,idINARTT1))
		    if (idINARTT1 = "") quit
		    set idINARTT2 = ""
		    for {
			    set idINARTT2 = $order(^INARTT(0,idINARTT1,idINARTT2))
			    if (idINARTT2 = "") quit
			    set objINARTT = ^INARTT(0,idINARTT1,idINARTT2,1)
			    if ($$$INARTTItemNumber1(objINARTT) = YKEY) {
				    set $$$INARTTValue1(objINARTT) = $$$INARTAverageCost(YFELD) * $$$INARTTQuantity(objINARTT)
				    set strStatus = $$$Save("INARTT",idINARTT1_$$$COMMA_idINARTT2,objINARTT,$$$YES)
			    }
		    }
	    }
    }
 
	if ($$$ISOK(strStatus)) {
		set strStatus = $$VarHookOnAfterSave^VARTRKINART(YKEY)
	}
 
	if ($$$ISOK(strStatus)) {
		set strStatus = $$AutorizarProdutoNaCentral(YKEY)
	}
 
	quit strStatus
 
AutorizarProdutoNaCentral(pYKEY)
	quit:( $$$NoKey(pYKEY) ) "Nenhum produto definido."
	
	new strStatus, objProduto, estoquePrincipal, statusProduto, idLocalAAutorizar, blnJaAutorizado,
		keyAutorizacao, objAutorizacao
		
	set strStatus = $$$OK
 
	set objProduto = $get(^INART(YM,pYKEY,1))
	
	set estoquePrincipal = $$$INARTFREE35(objProduto)
	set statusProduto    = $$$INARTHoldReason(objProduto)
 
	if (statusProduto = 0) { // A autorização é realizada somente para produtos com status 0-Ativo.
 		
 		if (estoquePrincipal = "ALMOXARIFADO")  {
	 		set idLocalAAutorizar = 1
 		} elseif (estoquePrincipal = "FARMACIA") {
	 		set idLocalAAutorizar = 5
 		}
 		
		if (idLocalAAutorizar = "") {
			quit 1
		}
 
		set blnJaAutorizado = $data(^VARAutorizacaoProduto(YM,idLocalAAutorizar,pYKEY,1))
 
		if (blnJaAutorizado = 0) {	
			set keyAutorizacao = idLocalAAutorizar_$$$COMMA_pYKEY
			set objAutorizacao = YBED_Y_$horolog
			
			set strStatus = $$$Save("VARAutorizacaoProduto", keyAutorizacao, objAutorizacao, $$$YES)
		}
	}
 
	quit strStatus
	
LoadINARTLOCPAR(Product)
	$$$VAR
	set Location=""
	set Location=$order(^WWW0121(YM,YM,Location))
	while Location'="" {
		set objLocation=""
		set objLocation=$get(^WWW0121(YM,YM,Location,1))
		// Consumo automático para o local
		if $$$WWW0121FREE8(objLocation)=1 {
			set objPar=""
			set objPar=$get(^INARTLOCPAR(YM,Product,Location,1))
			set $piece(objPar,Y,1)=1
			set key=Product_","_Location
			set strStatus=$$Save^COMUtils("INARTLOCPAR",key,objPar,1)
			if strStatus'=1 {
				//set errorstring="Erro ao salvar INARTLOCPAR "_key_" . Erro->"_strStatus
				//$$$Alert(errorstring)
			}
		}
		set Location=$order(^WWW0121(YM,YM,Location))
	}
	quit
	
OnBeforeDataAccess(pYKEY,pYLOCATION,pYFORM)
	;-------------------------------------------------------------------------------
	; Filters what COMView displays for chosing.
	;
	; Called by : OnBeforeDataAccess VARHook for INART
	; Params:
	;
	; Returns:
	; $$$YES - if the item can be displayed for the user
	; $$$NO  - if the item cannot be displayed (it will be hidden)
	;
	; History:
	; 21-Nov-2013	THIAGO	Created
	;-------------------------------------------------------------------------------
	new strStatus,idItem,idLocation,idForm,objItem,objLocn,itemStockType,locnStockType,reqNum,objReq
	
	set strStatus = $$$NO
	quit:(($get(pYKEY)="")||($get(pYLOCATION)="")||($get(pYFORM)="")) $$$YES
	
	set idItem 		= $get(pYKEY)
	set idLocation 	= $get(pYLOCATION)
	set idForm		= $get(pYFORM)
	
	quit:(
		(idForm="INART") 						|| 
		(idForm="INARTLOCPAR")					||
		(idForm="INWEINVELINE")					||
		(idForm="VARAnaliseConsumo")			||
		(idForm="VARAutorizacaoProdutoTabela")	||
		(idForm="VARConsultaGerencialProduto")	||
		(idForm="VARStock")						||
		(idForm="VARStockHistory")				||		
		(idForm="VARHistoricoCompra")			||
		(idForm="VARHistoricoConsumo")			||
		(idForm="VARHistoricoConsumoDetalhe")	||
		(idForm="VARInventarioMultDigLinha")	||
		(idForm="VARSESConsumoConsolidado")		||
		(idForm="VARSESRelatorioUltimosPrecos") ||
		(idForm="VARNadaConsta") ||
		(idForm="VARSESCOMPRASPDPASLinha") ||  ;A validação do produto/local no PDPAS é realizada internamente na VARSESCOMPRASPDPASLinha.mac. O YLOCATION não é um local de estoque.
		(idForm="")) $$$YES
	
	set objItem = $get(^INART(YM,idItem,1))
	set objLocn = $get(^WWW0121(YM,YM,idLocation,1))
	quit:((objItem="")||(objLocn="")) $$$YES
	
	set itemStockType = $$$INARTFREE35(objItem)
	set locnStockType = $$$WWW0121FREE4(objLocn)
	
	if ((itemStockType = "ALMOXARIFADO") && (locnStockType = "ALMOXARIFADO")){
		set strStatus = $$$YES
	} elseif ((itemStockType = "FARMACIA") && (locnStockType = "FARMACIA")){
		set strStatus = $$$YES
	
	/* Para a Farmácia Judicial, está autorizado pedir produtos de almoxarifado.
	   Isto claro, se o produto de almoxarifado estiver autorizado, para a farmácia judicial, o que será validado mais abaixo. */
	} elseif ((itemStockType = "ALMOXARIFADO") && (locnStockType = "FARMACIA") && (idLocation = 5073)){
		set strStatus = $$$YES
		
	} elseif (locnStockType = "CENTRODECUSTO"){
		if ($$$WWW120ClassUsedInForm($get(^WWW120(0,idForm,1))) = "INReqLine"){
			set reqNum = $get(^WWWDATEN(YM,+$horolog,YUSER,idForm,"V","REFERENCEKEY"))
			
			if (reqNum '= ""){
				set objReq = $get(^INReq(YM,reqNum,1))
				
				if ($$$INReqFromLocn(objReq) '= ""){
					set idLocation = $$$INReqFromLocn(objReq)
				}
			}
			
		}
		elseif ($$$WWW120ClassUsedInForm($get(^WWW120(0,idForm,1))) = "INDispenseToPatientLine"){
			set issueNum = $get(^WWWDATEN(YM,+$horolog,YUSER,idForm,"V","REFERENCEKEY"))
			
			if (issueNum '= ""){
				set objIssue = $get(^INIssue(YM,issueNum,1))
				
				if ($$$INIssueFromLocn(objIssue) '= ""){
					set idLocation = $$$INIssueFromLocn(objIssue)
				}
			}
			
		}
		
		set strStatus = $$$YES
	}
	
	if ($$$ISOK(strStatus)){
		if ('$data(^VARAutorizacaoProduto(YM,idLocation,idItem,1))){
			set strStatus = $$$NO
		}
	}
	
	quit strStatus
]]></Routine>
</Export>