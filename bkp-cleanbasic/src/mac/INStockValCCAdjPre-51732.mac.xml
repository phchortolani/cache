<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INStockValCCAdjPre" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INStockValCCAdjPre
#include COMSYS
#include INConst
	
AfterDataFields(pYKEY,pYFELD) 
	;-------------------------------------------------------------------------------
	; Get and show info
	; 
	; Called by: form INStockValCCAdjPre
	; 
	; Params:
	;
	; Returns:
	;
	; History:
	; 29-Oct-2009	GRF		SR16871: Copied from INStockValAdjustPre
	; 03-Nov-2005	JW		SR12920: Created
	;-------------------------------------------------------------------------------
	new idCostCentre,idItem
	
	set idItem       = $$$KEY1(pYKEY)
	set idCostCentre = $$$KEY2(pYKEY)
	
	if idItem'="" {
		do Adjust^INStockValCCAdj(pYKEY,$$$YES,$$$YES)
		do ShowPrePost(idItem,idCostCentre,pYFELD)
	}
	quit
	
	
ShowPrePost(pidItem,pidCostCentre,pYFELD) private
	;-------------------------------------------------------------------------------
	; Show pre post info
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 29-Oct-2009	GRF		SR16871: Copied from INStockValAdjustPre
	; 06-Nov-2005	JW		SR12920: Created
	;-------------------------------------------------------------------------------
	new arrKeys,Query
	
	set Query("Name") ="INStockVal"
	set Query("Class")="INStockVal"
	set Query("Title")=" "
	
	set Query("Col",1)="P~2~L"                         ; Location
	set Query("Col",2)="P~3~L"                         ; Stock Location
	
	set Query("Col",3)="D~1"                           ; Quantity
	set Query("Col",4)="D~2~~~~~~"_$$$Text("IN00376")  ; "Current Value"
	set Query("Col",5)="D~4~~~~~~"_$$$Text("IN00375")  ; "New Value"
	set Query("Col",6)="D~5~~~~~~"_$$$Text("IN00374")  ; "Adjustment"
	
	set Query("Get") ="set id=id(1)"
	set Query("Loop")="PrePostQueryLoop^INStockValCCAdjPre"
	
	if $$$INStockValAdjustSource(pYFELD)'="" {
		set Query("Highlight") = "set blnHighlight = ($piece(id,Y,3)=""***"")"
		
		write "<br><b>"_$$$Text("IN00380")
			; "Note: The adjustment of the highlighted line includes the receipt reversal."
	}
	
	set arrKeys(1) = pidItem
	set arrKeys(2) = pidCostCentre
	do Query^COMQuery(.Query,.arrKeys)
	
	quit
	
	
PrePostQueryLoop(&Query,&Storage,&parrKeys,pblnDisplay=$$$YES) 
	;-------------------------------------------------------------------------------
	; COMQuery data loop for pre post
	; 
	; where ^CacheTempINStock(YUSER,"Adjust",idItem,idLocn,idStore) = 
	;                                            objStockVal_Y_curNewVal_Y_curAdjVal
	; 
	; Params:
	;
	; Returns:
	;
	; History:
	; 29-Oct-2009	GRF		SR16871: Copied from INStockValAdjustPre; only show
	; 							locations that will be affected by the change
	; 04-Nov-2005	JW		SR12920: Created
	;-------------------------------------------------------------------------------
	new id,idCostCentre,idItem,intCount,objStockVal,loop
	
	set idItem       = parrKeys(1)
	set idCostCentre = parrKeys(2)
	
	set intCount=0
	
	set id(2) = ""                     ; Location
	for {
		set id(2) = $order(^CacheTempINStock(YUSER,"Adjust",idItem,id(2)))
		quit:id(2)=""
		continue:$$GetCostCentre^INCostCentre(id(2))'=idCostCentre
		
		set id(3) = ""                 ; Storage
		for {
			set id(3) = $order(^CacheTempINStock(YUSER,"Adjust",idItem,id(2),id(3)))
			quit:id(3)=""
			
			set objStockVal = $get(^CacheTempINStock(YUSER,"Adjust",idItem,id(2),id(3)))
			
			for loop=4:1:5 {		// Two dummy pieces for extra data
				set $piece(objStockVal,Y,loop) = $$FormatCurrency^COMTable($piece(objStockVal,Y,loop))
			}
			set id(1) = objStockVal
			
			merge @Storage@($increment(intCount)) = id
		}
	}
	quit intCount
	
]]></Routine>
</Export>