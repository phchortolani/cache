<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INIssueLocPick" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INIssueLocPick
 
	; Copied from INDispenseToPatientPick
	
#include INConst
#include COMSYS
#include %occInclude
	
#def1arg TempStore(%1) ^CacheTemp(YUCI,YUSER,%1)
	
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; Called By : [P] Button on Form INIssueLoc
	
	;---------------------------------------------------------------------------+ List
	;                      Dispense To Patient Picklist                         |   Title (2A)
	;                Issued By Location    LOCN_ID (LOCATION_NAME)              |
	;-----------------------+------------------------+--------------------------+
	;   For: TO_LOCATION    |                        |                          |   Title (2B)
	;   Reference: CUST_ID  |   Status: ENUM_STATUS  | Issue Number: ISSUE_NO   |
	;--------+---------+----+-+-------------+--------+-+------+--------+--------+
	; Number | Sending | Item |    Item     | Quantity | Unit | Use By |  Lot   |   Header
	;        | Storage |      | Description |          |      |  Date  | Number |
	;--------+---------+------+-------------+----------+------+--------+--------+
	;        |         |      |             |          |      |        |        |   ANZ
	;        |         |      |             |          |      |        |        |
	;        |         |      |             |          |      |        |        |
	;--------+---------+------+-------------+----------+------+--------+--------+
	;  (text after Pick List)                                                   |   ZUSATZTEXT
	;---------------------------------------------------------------------------+
	
	; May be multiple pages each with Title through to ANZ
	;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
CanPrintPick(pidIssue)
	;-------------------------------------------------------------------------------
	;
	; Called By:
	;	PrintPick^INIssueLocPick
	;	Form INIssueLoc : Button 5 - Execute To Check For Disabled
	;	
	; Params:
	;	pidIssue - Key of header record
	;
	; ByRefs: None
	;
	; Returns: strStatus, error message if unable to print
	;
	; History:
	;-------------------------------------------------------------------------------
	new objIssue,strStatus
	
	set strStatus = $$$OK
	
	if '$$$NoKey(pidIssue) {
		if $$$ISOK(strStatus) {
			if '$data(^INIssueLine(YM,pidIssue)) {
				set strStatus = $$$MakeStatus("IN00427")  ;No stock has been selected to be transferred.
			}
			set objIssue = $get(^INIssue(YM,pidIssue,1))
			if $$$ISOK(strStatus) {
				if $$IsLockedSiteLocation^INLocking($$$INIssueFromLocn(objIssue)) {
					set strStatus = $$ReasonLocked^INLocking($$$INIssueFromLocn(objIssue))
				}
			}
		}
 
	} else {
		set strStatus = $$$MakeStatus(32072)  ; "Please select a data record first."
	}
		
	$$$YQHandler(strStatus)
	
	quit strStatus
	
PrintPick(pidIssue)
	;-------------------------------------------------------------------------------
	; Print the Pick List for the Transfer
	;
	; Called By:
	;	PrintPick^INIssueLocPick
	; 
	; Params: pidIssue
	;
	; Returns: Status
	;
	;
	; History:
	; 14-May-2009	PPP		SR16199: Created
	;-------------------------------------------------------------------------------
	new ANZM,strStatus,YDDSATZ
	
	kill ^WWWSOR(YUSER)
	
	set strStatus=$$$OK
	set YBACK = ""	
	set ANZM=0
	set YNOFOOT=1
		
	do List(pidIssue)
		
	if ANZM=0 do ^WWWINFO($$^WWWTEXT(46),1,,1)    ; "No Data Available" 		
	kill ^WWWSOR(YUSER)
		
	quit strStatus
	
	
List(pidIssue) ;
	;-------------------------------------------------------------------------------
	; Print the Pick List for the Transfer
	;
	; Params: pidIssue
	;
	; Returns: None
	;
	;
	; History:
	; 20-Feb-2009	GRF		SR16303: Move Title & <TABLE> from Header to match
	; 							</TABLE>; pass parameters to Header and ANZ
	; 03-Feb-2009	DavidR  SR16303: copied and modified from INTRNPickList
	; 15-Dec-2008   DavidR  SR16302: Added two more columns for lot number and expiry
	; 							date. Also modified the naming of the columns to be
	; 							reliant on the form, to allow for any customisation.
	; 12-Dec-2007	PPP		SR15597: Created
	;-------------------------------------------------------------------------------
	new ANZSEIT,idForm,intLn,LFN,objVORGLANG,TEXT,UES,VORG1,YFELD
	
	set YNOFOOT=1
	do ^WWWBACK
	
	set VORG1       = $get(^INVORG(YM,YM,1))
	set objVORGLANG = $get(^INVORGLANG(YM,YM,SPRACHE,1))
	set TEXT = $$$INVORGLANGTextAfterPickList(objVORGLANG)
	
	set ANZSEIT=$PIECE(VORG1,Y,82)    ; Number within side 
	if ANZSEIT="" SET ANZSEIT=30
	
	set idForm = "INIssueLine"
	set UES=$$^WWWTEXT(207)                                                 ; "Number"
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineFromStorage)  	; 2     "Sending Storage"
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineItem)         	; 1     "Item"
	set UES=UES_Y_$$^WWWTEXT(32026)                                         ; "Item Description"
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineQtyIssued)	   	; 3     "Quantity"
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineUnit)         	; 4     "Unit"
	set UES=UES_Y_$$^WWWTEXT("AL00016")_"/"_$$^WWWTEXT("AL00017") 			; "Lot Number / Use By Date"
	set UES=UES_Y_$$^WWWFELDNAME(idForm,"D",$$$FldINIssueLineToStorage)    	; 3     "To Storage"
	
	set ANZM=0
	do Title(pidIssue)
	
	do START100^WWWTAB  ;STARTEN DER ÜBERSCHRIFT ;launching the superscription 
	do Header(UES)		
	set YDDSATZ=0
	
	set intLn = 0
	for {
		set intLn = $order(^INIssueLine(YM,pidIssue,intLn))
		quit:intLn=""
		
		set LFN=""
		set YFELD=$get(^INIssueLine(YM,pidIssue,intLn,1))
		
		if (ANZM'=0) && (ANZM#ANZSEIT=0) {
			write "</TD></TR>"
			write "</TABLE>",YCR
			
			do FF^WWWW()        ; FORMFEED
			
			do Title(pidIssue)
			do START100^WWWTAB  ;STARTEN DER ÜBERSCHRIFT ;launching the superscription 
			do Header(UES)
		}
		do ANZ(pidIssue,intLn)
	}
	
	do LEER^WWWTAB(15,8) ;SR16302 added columns from 6 to 8
	write "</TABLE>",YCR
	do ZUSATZTEXT(TEXT)
		
	quit
	
	
ANZ(pidIssue,pintLn)
	;-------------------------------------------------------------------------------
	; Print the Pick List for the Transfer
	;
	; Params: pintLn - line number
	;
	; Returns: None
	;
	;
	; History:
	; 06-Apr-2011	shobby	SR17714.6: Formatting for 'To Storage'
	; 04-Apr-2011	shobby	SR17714.4: Redirected previous change to INLP.
	; 28-Apr-2011	shobby	SR17714.2: Use CodeWithTSU formatting.
	; 07-Mar-2011	PPP		SR17677: Removed the 15 character limit for Desc
	; 14-May-2009	PPP		SR16199:Updated the fields displayed
	; 03-Feb-2009	DavidR  SR16303: copied and modified from INTRNPick
	; 15-Dec-2007   DavidR  SR16302: Modified to poulate extra fields added (7 and 8)
	;                       	also modified call to use macro to make it readable.
	; 12-Dec-2007	PPP		SR15597: Created
	;-------------------------------------------------------------------------------
	new idItem,idUnit,objItem,strBundleValues,strItemDescription
	
	set idItem = $$$INIssueLineItem(YFELD)
	
	set ANZM = ANZM+1
	write YCR
	do NL^WWWTAB
	
	; 1. Line Number
	;---------------------------------------
	do NF^WWWTAB
	write YCR,pintLn,"&nbsp;"
	
	; 2. Storage
	;---------------------------------------
	do ZWL^WWWTAB
	;SR17714.2	write $$$INIssueLineFromStorage(YFELD),"&nbsp;"
	write $$CodeWithTSU2^INLP($$$INIssueFromLocn($get(^INIssue(0,pidIssue,1))),$$$INIssueLineFromStorage(YFELD))	;SR17714.4
	
	; 3. Item
	;---------------------------------------
	do ZWL^WWWTAB
	write idItem,"&nbsp;"
	
	; 4. Item Description
	;---------------------------------------
	do ZWL^WWWTAB
	set objItem = $get(^INART(YM,idItem,1))
	set strItemDescription = $$$INARTSearchName(objItem)
	//SR17677
	//if $length(strItemDescription) > 15 {
	//	set strItemDescription = $extract(strItemDescription,1,15)_"..."
	//}
	write strItemDescription,"&nbsp;"
	
	; 5. Quantity
	;---------------------------------------
	do ZWR^WWWTAB
	write $$$INIssueLineQtyIssued(YFELD),"&nbsp;"
	
	; 6. Unit
	;---------------------------------------
	do ZWL^WWWTAB
	set idUnit = $$$INIssueLineUnit(YFELD)
	write idUnit_" ("_$$$AppEnum("EINHEIT",idUnit)_")","&nbsp;"
	
	; 7. Bundle Values
	;---------------------------------------
	do ZWL^WWWTAB
	
	set strBundleValues=$$GetBundleDesc^INIssueLocLine(pidIssue, pintLn)
	
	write strBundleValues,"&nbsp;"
	
	; 8. To Storage
	;--------------------------------------- ;  Added
	do ZWL^WWWTAB
	;17714.6 write $$$INIssueLineToStorage(YFELD),"&nbsp;"	
	write $$CodeWithTSU2^INLP($$$INIssueToLocn($get(^INIssue(0,pidIssue,1))),$$$INIssueLineToStorage(YFELD))	;SR17714.6
	write YCR
	do EL^WWWTAB
	
	quit
	
	
ZUSATZTEXT(TEXT)   ;ZUSATZTEXT
 	new YI
 	
	IF ANZM'=0 IF TEXT'="" DO
	. WRITE "<BR>"
	. IF '$FIND(TEXT,"<") DO  QUIT
	. . FOR YI=1:1 QUIT:$PIECE(TEXT,"|",YI,9999)=""  WRITE $PIECE(TEXT,"|",YI),"<BR>",YCR  ;NUR TEXT ;only Text 
	. ;
	. FOR YI=1:1 QUIT:$PIECE(TEXT,"|",YI,9999)=""  WRITE $PIECE(TEXT,"|",YI),YCR  ;HTML
	
	WRITE YCR
	DO FF^WWWW()
	WRITE YCR
	QUIT
	
	
Title(pidIssue)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 14-May-2009	PPP		SR16199:Updated the fields displayed
	; 03-Feb-2009	DavidR  SR16303: copied and modified from INTRNPick
	; 12-Dec-2007	PPP		SR15597: Created
	;-------------------------------------------------------------------------------
	new dtePosted,dteTransfer,idFromLocn,idRef,idToLocn,intStatus
	new objIssue,objRecord,strDescription
	
	set objIssue   = $get(^INIssue(YM,pidIssue,1))
	set idFromLocn = $$$INIssueFromLocn(objIssue)
	
	if $$$INIssueType(objIssue) = 3 { 
 		set idToLocn   = $$$INIssueCustomerID(objIssue)
		if idToLocn'="" {
			set idToLocn = idToLocn_" ("_$$$INKUNDEName1($get(^INKUNDE(YM,idToLocn,1)))_")"	
		}
 
	} else {
 		set idToLocn   = $$$INIssueToLocn(objIssue)
		if idToLocn'="" {
			set idToLocn = idToLocn_" ("_$$$WWW0121LocationName($get(^WWW0121(0,YM,idToLocn,1)))_")"	
		}
	}
 
	set idRef      	= $$$INIssueCustomerName(objIssue)
	set intStatus  	= $$$INIssueStatus(objIssue)
	set dtePosted 	= $$$INIssuePosted(objIssue)
	
	; TABLE 1
	;---------------------------------------
	DO START1000^WWWTAB
	WRITE YCR,"<TR><TD><NOBR>"
	
	; TABLE 2A
	;---------------------------------------
	WRITE YCR,YCR,"<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	WRITE YCR,"<TR>"
	
	WRITE YCR,"<TD ALIGN=CENTER COLSPAN=2>"
	WRITE "<FONT SIZE=3><B>"
	WRITE $$^WWWFORMNAME("INIssueLoc")_" "_$$^WWWTEXT(32503)     ; "Picklist"
	WRITE "</B></FONT>"
	WRITE "<BR>",YCR
	WRITE "<FONT SIZE=2>"      ; "Issued By  "  "Location"
	WRITE $$^WWWTEXT(33404)_" "_$$^WWWTEXT(388)_": "_idFromLocn_" ("_$$$WWW0121LocationName($get(^WWW0121(0,YM,idFromLocn,1)))_")"
	WRITE "</FONT>"
	WRITE "</TD>"
	
	WRITE "</TR>"
	WRITE "</TABLE>",YCR
	; TABLE 2A END
	
	; TABLE 2B
	;---------------------------------------
	WRITE "<TABLE BORDER=0 CELLSPACING=1 WIDTH=100%>"
	WRITE "<TR><TD WIDTH=33%>"
	WRITE "<FONT SIZE=2><B>"
	WRITE $$^WWWTEXT(32807)_": "  ; "For" : To location
	WRITE idToLocn
	WRITE "<BR>"
	WRITE $$^WWWTEXT(32247)_": "  ; "Reference" 
	WRITE idRef
	WRITE "</B></FONT>"
	WRITE "</TD>",YCR
	
	WRITE "<TD ALIGN=CENTER VALIGN=BOTTOM WIDTH=33%>"
	WRITE "<FONT SIZE=2><B>"
	WRITE " "
	WRITE "<BR>"
	WRITE $$^WWWTEXT(32781)_": "_intStatus  ; "Status"
	WRITE "</B></FONT>"
	WRITE "</TD>",YCR
	
	WRITE "<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>"
	WRITE "<FONT SIZE=2><B>"
	if pidIssue '= "" write $$^WWWFELDNAME("INIssueLoc","P",1)_": "_pidIssue
	WRITE "</B></FONT>"
	WRITE "</TD></TR>"
 
 
	
	WRITE "</TD></TR>",YCR
	
	
 	WRITE "<TR>"
	WRITE "<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>"
	WRITE "</TD>"
	WRITE "<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>"
	WRITE "</TD>"
	WRITE "<TD ALIGN=RIGHT VALIGN=BOTTOM WIDTH=33%>"
	WRITE "<FONT SIZE=2><B>"
	write $$^WWWFELDNAME("INIssueLoc","D",$$$FldINIssuePosted)_":"_$select(dtePosted'="":$zdt(dtePosted,3),1:"")
	WRITE "</TD>"
 	WRITE "</TR>"
	
	
	WRITE "</TABLE>",YCR
	; TABLE 2B END
 
	DO STOP^WWWTAB
 
 
	; TABLE 1 END
	
	quit
	
	
Header(UES)
	;-------------------------------------------------------------------------------
	; History:
	; 20-Feb-2009	GRF		SR16303: Move Title & <TABLE> into List to match </TABLE>
	; 03-Feb-2009	DavidR  SR16303: copied and modified from INTRNPick
	; 15-Sep-2008	GRF		replace "I" with "loop"
	;-------------------------------------------------------------------------------
	new loop
	
	write "<THEAD>" 
	do NL^WWWTAB
	for loop=1:1 {
		quit:$piece(UES,Y,loop,99)=""
		
		do NHWZ^WWWTAB
		write $piece(UES,Y,loop),"&nbsp;"
		do EH^WWWTAB
	}
	do EL^WWWTAB
	write "</THEAD>"
	quit
	
	
]]></Routine>
</Export>