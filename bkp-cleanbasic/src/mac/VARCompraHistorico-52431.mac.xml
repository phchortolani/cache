<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARCompraHistorico" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARCompraHistorico
#include COMSYS
#include INConst
#include VARConst
 
OnAfterPrimaryKey()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo OnAfterPrimaryKEY do form VARCompraHistorico
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	set VORG(1)=$piece(YKEY,YKOMMA,1)
	quit
 
GetInformacaoAfetada()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	DefaultVariableInput do Campo (7)-Informação Afetada do form VARCompraHistorico
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit $case($$$VARCompraHistoricoItem(YFELD), "":"", 0:"Documento",:"Produto")
 
GetCodigo()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	DefaultVariableInput do Campo (8)-Código do form VARCompraHistorico
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new strCodigo, objCompraLinha, objProduto
	
	set strCodigo = ""
	if ($$$VARCompraHistoricoItem(YFELD) = 0) {
		set strCodigo = "AFM"
	}
	
	elseif ($length($$$VARCompraHistoricoItem(YFELD)) > 0) && ($$$VARCompraHistoricoItem(YFELD) '= 0) && ($length(VORG(1)) > 0) {
		set objCompraLinha = $get(^INAUFP(0, VORG(1),$$$VARCompraHistoricoItem(YFELD), 1))
		set objProduto = $get(^INART(0, $$$INAUFPItemNumber(objCompraLinha), 1))
		set strCodigo = "("_$$$INAUFPItemNumber(objCompraLinha)_")&nbsp;"_$$$INARTSearchName(objProduto)
	}
 
	quit strCodigo 
 
RegistrarAlteracao(pidCompra, pintAcao, pintItem, pintQuantidade, pstrMotivo)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterSaveHook^VARCompra.mac.
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit $$RegistrarAlteracaoIdentificada($horolog, $get(YBED), pidCompra, pintAcao,pintItem, pintQuantidade, pstrMotivo)
 
RegistrarAlteracaoIdentificada(pdatAlt, pstrUsuario, pidCompra, pintAcao, pintItem,pintQuantidade, pstrMotivo)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função RegistrarAlteracao^VARCompraHistorico.mac.
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new objCompraHistorico, idHistAlt, strStatus, strLog
 
	kill objCompraHistorico
	set $$$VARCompraHistoricoData(objCompraHistorico) 	 	= pdatAlt
	set $$$VARCompraHistoricoUsuario(objCompraHistorico) 	= pstrUsuario
	set $$$VARCompraHistoricoAcao(objCompraHistorico) 	 	= pintAcao
	set $$$VARCompraHistoricoItem(objCompraHistorico) 	 	= pintItem
	set $$$VARCompraHistoricoQuantidade(objCompraHistorico) = pintQuantidade
	set $$$VARCompraHistoricoMotivo(objCompraHistorico) 	= pstrMotivo
 
	set idHistAlt = $$^WWWNEXT1("VARCompraHistorico", pidCompra, 2)
	set strStatus = $$$Save("VARCompraHistorico",pidCompra_YKOMMA_idHistAlt,objCompraHistorico,1)
 
	set strLog = "Data/hora='"_$zdatetime($horolog, 3, 1)_"', "_
		"usuário="_$get(YBED)_", "_
		"ação="_$$GetDescricaoAcao(pintAcao)_", "_
		"item="_pintItem_", "_
		"quantidade="_pintQuantidade_", "_
		"status="_strStatus_", "_
		"motivo='"_pstrMotivo_"'"
 
	quit strStatus
 
GetDescricaoAcao(pintAcao)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função RegistrarAlteracaoIdentificada^VARCompraHistorico.mac.
    ; 
    ; Histórico:
    ; 	30-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	if ('$data(^VARPARA(YM, "TIPOHISTORICO", SPRACHE, pintAcao, 1))) {
		quit "Desconhecida"
	}
	
	quit $$$VARPARAText(^VARPARA(YM, "TIPOHISTORICO", SPRACHE, pintAcao, 1))

SQLGetMotivoArquivamento(pidCompra,idLinha=0)
	quit:(pidCompra="") ""
	set strMotivo = ""
	set sequencia = $order(^VARCompraHistorico(0,pidCompra,""))
	while($length(sequencia) > 0) {
		set objAFMHistAlter = $get(^VARCompraHistorico(0,pidCompra,sequencia,1))

		set acao 	= $$$VARCompraHistoricoAcao(objAFMHistAlter)
		set item 	= $$$VARCompraHistoricoItem(objAFMHistAlter)
		set motivo 	= $$$VARCompraHistoricoMotivo(objAFMHistAlter)
		
		if ((acao = 3) && (item = idLinha)){
			set strMotivo = motivo
			quit
		}
		set sequencia = $order(^VARCompraHistorico(0,pidCompra,sequencia))
	}
	
	quit strMotivo
]]></Routine>
</Export>