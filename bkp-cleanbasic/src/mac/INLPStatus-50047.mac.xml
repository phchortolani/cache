<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INLPStatus" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INLPStatus
	
#include COMSYS
#include INConst
	
OnBeforeSave(pidStatus,pobjStatus)
	;-------------------------------------------------------------------------------
	; If changing default, update current default
	; 
	; Called By: Form INLPStatus
	; 
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 15-Jun-2011	GRF		SR17716: Validate DefaultOnReceipt switch
	; 03-Jun-2011	GRF		SR17716: Copied from INItemStatus
	;-------------------------------------------------------------------------------
	new idDefault,idGroup,idParent,objDefault,objGroup,strStatus
	
	set strStatus = $$$OK
	
	if $$$INLPStatusIsDefaultStatus(pobjStatus) {
		; Can only have one default, so if we have just made this one default,
		; remove check from old default
		set idDefault = $order(^INLPStatuss(0,1,$$$YES,""))
		
		if (idDefault'="") && (idDefault'=pidStatus) {
			set objDefault = $get(^INLPStatus(0,idDefault,1))
			set $$$INLPStatusIsDefaultStatus(objDefault) = $$$NO
			set strStatus = $$$Save("INLPStatus",idDefault,objDefault,$$$YES)
		}
	}
	
	if $$$ISOK(strStatus) && $$$INLPStatusDefaultOnReceipt(pobjStatus) {
		; Can only have one status as default for "Set on Goods Receipt",
		; so if we have just made this one default, remove check from old default
		set idDefault = $order(^INLPStatuss(0,2,$$$YES,""))
		if (idDefault'="") && (idDefault'=pidStatus) {
			set objDefault = $get(^INLPStatus(0,idDefault,1))
			set $$$INLPStatusDefaultOnReceipt(objDefault) = $$$NO
			set strStatus = $$$Save("INLPStatus",idDefault,objDefault,$$$YES)
		}
	}
	
	if $$$ISERR(strStatus) {
		$$$Error(strStatus)
		set Q = $$$QDontSave
	}
	quit strStatus
	
	
OnBeforeDelete(pidStatus,pobjStatus)
	;-------------------------------------------------------------------------------
	; Can we delete?  i.e. is it the default, or in use?
	;
	; Called By: Form INLPStatus
	; 
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 03-Jun-2011	GRF		SR17716: Copied from INItemStatus
	;-------------------------------------------------------------------------------
	new idxStatus,strStatus
	
	set strStatus = $$$OK
	
	if $$$INLPStatusIsDefaultStatus(pobjStatus) {
		set strStatus = $listbuild("IN00663")  	 ; "The default status cannot be deleted."
		
	} else {
		set idxStatus = $$$Index(pidStatus)
		if $data(^INLPs(0,6,idxStatus)) {
			set strStatus = $listbuild("IN00587",pidStatus)
		} ; "This record (%1) is in use. It cannot be deleted."
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontDelete_strStatus
	}
	quit strStatus
	
	
GetDefaultReceiptStatus()
	;-------------------------------------------------------------------------------
	; It is possible to alter the status code for a Temporary Storage Unit on
	; receipt of goods from a supplier.  Only one "Set Default on Receipt" code may
	; be set and this would normally be to automatically close the TSU on receipt.
	; If no status code is returned the current status code should not be amended.
	;
	; Called By: 
	; 
	; Returns:	The default status code
	;
	; History:
	; 15-Jun-2011	GRF		SR17716: Created
	;-------------------------------------------------------------------------------
	new enumStatusCode
	
	set enumStatusCode = $order(^INLPStatuss(0,2,$$$YES,""))
	quit enumStatusCode
	
	
IsTSUOpen(penumStatusCode="")
	;-------------------------------------------------------------------------------
	; For Temporary Storage Units - Is "Closed" switch set to NO for Status Code
	; 
	; Called By: 
	; 
	; Returns:	boolean - defaults to "OPEN" if no status code
	;
	; History:
	; 15-Jun-2011	GRF		SR17716: Created
	;-------------------------------------------------------------------------------
	new blnIsOpen,objLPStatus
	
	if penumStatusCode="" {
		set penumStatusCode = $order(^INLPStatuss(0,1,$$$YES,""))   ; Default Status
	}
	
	if penumStatusCode'="" {
		set objLPStatus = $get(^INLPStatus(0,penumStatusCode,1))
		set blnIsOpen   = '$$$INLPStatusIsTSUClosedForReceipts(objLPStatus)
	
	} else {
		set blnIsOpen = $$$YES
	}
	quit blnIsOpen
	
	
IsTSUClosed(penumStatusCode="")
	;-------------------------------------------------------------------------------
	; For Temporary Storage Units - Is "Closed" switch set to YES for Status Code
	; 
	; Called By: 
	; 
	; Returns:	boolean - defaults to "OPEN" if no status code
	;
	; History:
	; 15-Jun-2011	GRF		SR17716: Created
	;-------------------------------------------------------------------------------
	quit '$$IsTSUOpen(penumStatusCode)

CheckClosed(YFORM="",YKEY="",YFELD="",YKEYCONTAINER="")
	;-------------------------------------------------------------------------------
	; Excludes 'Closed' storages from the drop-down list.
	;
	; Inputs:
	; 
	; ByRef: YLFN - class field number
	;
	; Returns:
	;
	; History:
	; 04-Jun-2014	shobby	CORE-385.11: Moved from WWW0121C
	; 15-Jun-2011	GRF		SR17716: TSU Closed now based on Status
	; 12-May-2011	GRF		-: Split key once
	; 04-Nov-2010	shobby	SR17598: Include the current saved value even if 'Closed'
	; 02-Nov-2010	shobby	SR17598: Created
	;-------------------------------------------------------------------------------
	new blnResult,idLocn,idStore,objINLP
	
	set idLocn    = $$$KEY1(YKEY)
	set idStore   = $$$KEY2(YKEY)
	set blnResult = $$$YES
	
	if (idLocn'="") && (idStore'="") && ($get(YLFN)'="") {
		set objINLP   = $get(^INLP(0,idLocn,idStore,1))
	;	set blnResult = '$$$INLPClosed(objINLP)                      ; SR17716
		set blnResult = $$IsTSUOpen^INLPStatus($$$INLPStatus(objINLP))
		if 'blnResult set blnResult = ($piece(YFELD,Y,YLFN)=idStore) ; Include the current saved value even if 'Closed'
	}
	quit blnResult
	
]]></Routine>
</Export>