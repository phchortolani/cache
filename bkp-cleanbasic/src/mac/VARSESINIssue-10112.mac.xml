<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARSESINIssue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARSESINIssue
	#include COMConst
	#include INConst
	#include WWWConst
	#include FIN
 	#include COMSYS
	#include COMGridEdit31
	#include VARSESConst
 
History(pidItem,pidLoc,pidStkLoc,pfltQty,pcurCost,pidReason)
	;-------------------------------------------------------------------------------
	; Update History for this adjustment
	;
	; Params:	pidItem,pidLoc,pidStkLoc - SOH keys
	; 			pfltQty		- qty adjusted
	; 			pcurCost	- cost update
	; 			pidReason	- 
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 17-Aug-2006	JW		SR14826: Created
	;-------------------------------------------------------------------------------
	new strText,idDelStkLoc
	
	set strText = $$$Text($listbuild("IN00520",pfltQty,pidLoc,pidStkLoc,pidReason))	;%1 Adjusted From %2/%3 Reason Code %4
	
	set idDelStkLoc = $$$INVORGDeleteStockLocation($get(^INVORG(YM,YM,1)))
	if idDelStkLoc="" set idDelStkLoc = "X"
	
	do ^INARTHIST(pidItem,strText,pidLoc,,,-pfltQty,,-pcurCost,idDelStkLoc,,pidStkLoc,,,pidLoc,,"ADJ",,,pidReason)
	
	quit
	
ProcessOnClick(pidAdjustment)
 
	do ProcessOnClick^VARSESCONSUMOPACIENTE(pidAdjustment)
 
	quit
 
 
ValidaBotaoProcessar(YKEY)
	;-------------------------------------------------------------------------------
    ; History:
    ; 21-May-2008	GustavoF	Alert user which lines does not have stock anymore from
    ; 							the time he created the line to the time he tries to
    ; 							click on process.
    ; 
    ; 15-Sep-2007	GustavoF	Não deixa o usuário processar o registro se não for
    ; 							um registro do seu local.
    ;-------------------------------------------------------------------------------
 
	new LocalUsuario, LocalDonoDoRegistro, idINIssueLine, objINIssueLine, CodProduto,
		Quantidade, EstoqueAtual, strMensagem, blnDisable
	
	set LocalUsuario = YLOCATION
	set LocalDonoDoRegistro = $PIECE(YFELD,Y,3)

	do IsFirmable^INIssue(YM,YFORM,YKEY,YFELD)
			
	if LocalUsuario '= LocalDonoDoRegistro { 
		set YQ = $$$YQDisable("O Local que criou o registro é diferente do local do usuário atual.")
		quit
	}
	
	//Verifica se os produtos ainda têm estoque
	set idINIssueLine = ""
	set blnDisable  = 0
	set strMensagem = ""
	
	for {
		set idINIssueLine = $order(^INIssueLine(YM,YKEY,idINIssueLine))
	   	quit:(idINIssueLine = "")
	        			
	    set objINIssueLine = $get(^INIssueLine(YM,YKEY,idINIssueLine,1))
	    
	    set CodProduto   = $$$INIssueLineItem(objINIssueLine)
	    set Quantidade   = $$$INIssueLineQtyIssued(objINIssueLine)
	    
	    set EstoqueAtual = $$getSOHLocal^VARSESINIssueLine(CodProduto,LocalDonoDoRegistro)
		
		if Quantidade > EstoqueAtual {
			set strMensagem = strMensagem_$char(10)_"Seq. "_idINIssueLine_". Produto "_CodProduto_" -> Quantidade = "_$$^WWWTR(0,12,Quantidade)_" e Quant. Estoque = "_$$^WWWTR(0,12,EstoqueAtual)_""
			set blnDisable = $$$YES				
		}
	}
	
	if (blnDisable = $$$YES) {
		//set YQ = 1_"Um ou mais produtos não têm estoque suficiente para a quantidade a ser consumida. Favor corrigir."_$char(10)_strMensagem
		set YQ = 1_$$$Text("MED00199")_$char(10)_strMensagem
		quit
	}
 
	quit
	
	
CanViewReport()
 	//Karine, 09/06/2008
 	//Desabilita o botão de relatório caso a saída não tenha sido processada.
 	
 	if YKEY '= "" {	 	
 		if $$$INIssueStatus(YFELD) '= 1 {
			//set YQ = 1_"A Saída precisar ter sido processada para visualizar o relatório."
			set YQ = 1_$$$Text("MED00200")
		}  	
		else { 
			set YQ = $$$YQEnable
		} 	
 	} 
	quit
]]></Routine>
</Export>