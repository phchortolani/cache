<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINIssue" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINIssue
#include COMSYS
#include COMConst
#include INConst
#include VARConst

CanViewReport(pidIssue)

	new objIssue
	
	set strStatus = $$$OK
	
	if $$$NoKey(pidIssue) {
		set strStatus = $$$MakeStatus(32072)  ; "Please Select A Data Record First."
 
	} else {
		set objIssue = $get(^INIssue(YM,pidIssue,1))
			if ($$$INIssueStatus(objIssue) < 9) {
				set strStatus = "Somente poderão ser impressas as dispensações processadas."
				}
	}
	
	$$$YQHandler(strStatus)
	
	quit
		
ImprimeRelatorio(YKEY)
	if ($$GetPreferencia^VARPreferenciasUsuario($$$FldVARPreferenciasUsuarioTelaFiltroRelatorio) = 1){
		do GotoFiltrosRelatorio^VARFiltrosRelatorio("RunReportComprovanteEntrega",0,1,,YKEY)
	} else {
		do RunReportComprovanteEntrega^VARJasperRunReport(YKEY)
	}
		
	quit
	
GetSetorFromSolic(pYFELD)
	new idSolic,objSolic,idSetor
	
	set idSolic = $$$INIssueReference(pYFELD)
	
	quit:idSolic="" ""
	
	set objSolic = $get(^INReq(YM,idSolic,1))
	set idSetor = $$$INReqFREE7(objSolic)
	
	quit idSetor

GetDataFromSolic(pYFELD)
	new idSolic,objSolic,dueDate
	
	set idSolic = $$$INIssueReference(pYFELD)
	
	quit:idSolic="" ""
	
	set objSolic = $get(^INReq(YM,idSolic,1))
	set dueDate = $$$INReqDueDate(objSolic)
	
	quit dueDate
	
IsFirmable(pYM,pYFORM,pstrKey,pYFELD)
	
	do IsFirmable^INIssue(pYM,pYFORM,pstrKey,pYFELD)
	
	if ($get(YQ)=0) && (pstrKey '= ""){
		set dataDesejada = $$$INIssueFREE14($get(^INIssue(YM,pstrKey,1)))
		
		if (dataDesejada = ""){
			set YQ = 1_"Primeiro salve o registro."	
		}	
	}
	
	quit

Postable(pYKEY)
	do Postable^INDispenseToPatient(YKEY)
	
	if ($get(YQ)=0) && (pYKEY '= ""){
		set issueLine = ""
		
		for{
			set issueLine = $order(^INIssueLine(YM,pYKEY,issueLine))
			quit:issueLine=""
			
			set objIssueLine =  $get(^INIssueLine(YM,pYKEY,issueLine,1))
			set qtyIssued = $$$INIssueLineQtyIssued(objIssueLine)
			
			if (YFORM '= "VARSESPatientConsumptionMC"){
				if (+qtyIssued = 0){
					set YQ = 1_"Informe uma quantidade válida e maior que zero na linha "_issueLine_"."
					quit
				}
			}else{
				if ($length($$$INIssueLineQtyPicked(objIssueLine))=0) {
					set YQ = 1_"Informe uma quantidade na linha "_issueLine_"."
					quit
				}
			}	
		}
	}
	
	quit
	
CompareQty(pYFELD)
	quit:(pYFELD="") $$$NO
	
	set blnOverQty = $$$NO
	
	set qtySaida = $$$INIssueRevLineQtyConsumed(pYFELD)
	set qtyDev	 = $$$INIssueRevLineUnitQuantityReturned(pYFELD)
	set qtyJaDev = $$$INIssueRevLineQtyAlreadyRetrn(pYFELD)
	
	if (qtyDev > (qtySaida-qtyJaDev)){
		set blnOverQty = $$$YES
	}
	
	quit blnOverQty
	
IsListPrinted(pidIssue,pobjIssue)
	quit:(pidIssue="") $$$NO
	
	new blnIsPrinted,objIssue,dtPrinted,strUsrPrint
	
	set blnIsPrinted = $$$NO
	set objIssue	 = $get(^INIssue(0,pidIssue,1))
	
	if (objIssue '= "") {
		set dtPrinted 	= $$$INIssuePickPrintedTimestamp(objIssue)
		set strUsrPrint = $$$INIssuePickPrintedUser(objIssue)
		if ((dtPrinted '="") && (strUsrPrint '="")) {
			set blnIsPrinted = $$$YES
		}
	}
	
	quit blnIsPrinted
]]></Routine>
</Export>