<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPNEU" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPNEU	;INAUFPNEU;FIS;ADD ORDER LINE ITEM;28.12.2004  ; Compiled March 15, 2005 11:13:18
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ADD ORDER LINE ITEM
	; 
	; Called By: Form INAUFPNEU - Execute After Save
	; 
	; Inputs : 
	;	YAUSWAHL
	;	VORG(1)		idOrder
	;	VORG(2)		idSupplier
	;	VORG(3)		idItem
	;	VORG(4)		Quantity
	;	VORG(5)		Base Price (Each*Qty)
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 28-Sep-2006	GRF		Doco; clarify
	; 28.12.2004	FIS
	;-------------------------------------------------------------------------------
	NEW YPOSITION,AUFART,AUF,ART,MENGE
	
	;+++++++++++++++++++++++++++++++++++++++
	;	YPOSITION		
	;	INERECH1		objINERECH1
	;+++++++++++++++++++++++++++++++++++++++
	
	SET VORG(1)=$GET(VORG(1))      IF VORG(1)="" DO ^WWWINFO($$^WWWTEXT(32292)) QUIT   ; "Check Please!"   ;BITTE PRÜFEN
	SET VORG(2)=$GET(VORG(2))      IF VORG(2)="" DO ^WWWINFO($$^WWWTEXT(32292)) QUIT
	SET VORG(3)=$GET(VORG(3))      IF VORG(3)="" DO ^WWWINFO($$^WWWTEXT(32292)) QUIT
	SET VORG(4)=$GET(VORG(4))      IF +VORG(4)=0 DO ^WWWINFO($$^WWWTEXT(32292)) QUIT
	IF '$DATA(^INAUF(YM,VORG(1)))                DO ^WWWINFO($$^WWWTEXT(32292)) QUIT
	IF '$DATA(^INLIEF(YM,VORG(2)))               DO ^WWWINFO($$^WWWTEXT(32292)) QUIT
	IF '$DATA(^INART(YM,VORG(3)))                DO ^WWWINFO($$^WWWTEXT(32292)) QUIT
	
	SET $PIECE(YAUSWAHL,",",2)=$PIECE(^INUSER(YM,YBED,1),Y,4)
	SET $PIECE(YAUSWAHL,",",3)=$PIECE(^INUSER(YM,YBED,1),Y,18)
	SET $PIECE(YAUSWAHL,",",4)=$PIECE(^INUSER(YM,YBED,1),Y,56)	
	
	SET AUF    = VORG(1)
	SET AUFART = $PIECE($GET(^INAUF(YM,AUF,1)),Y,2)
	SET ART    = VORG(3)
	SET MENGE  = VORG(4)
	SET YPOSITION=""
	SET $PIECE(YPOSITION,Y,4)=ART                   ;ARTIKEL ;item 
	SET $PIECE(YPOSITION,Y,5)=MENGE                 ;MENGE   ;quantity 
	IF VORG(2)'="" {
		SET $PIECE(YPOSITION,Y,6)=MENGE             ;MENGE   ;quantity 
		SET $PIECE(YPOSITION,Y,7)=1                 ;BESTELLUNG ;sales order
	}
	IF ART'="" IF $PIECE($GET(YAUSWAHL),",",2)'="" SET $PIECE(YPOSITION,Y,10)=$$^WWWTEXT(32515)_" "_$$^WWWTEXT(33441)_" "_$PIECE($GET(YAUSWAHL),",",2) ;AUS POSITION)
	
	IF $PIECE($GET(YAUSWAHL),",",2)'="" {
		SET $PIECE(YPOSITION,Y,384)=$PIECE($GET(^INAUFP(YM,AUF,$PIECE($GET(YAUSWAHL),",",2),1)),Y,384)
		SET $PIECE(YPOSITION,Y,385)=$PIECE($GET(^INAUFP(YM,AUF,$PIECE($GET(YAUSWAHL),",",2),1)),Y,385)
	}
	
	SET $PIECE(YPOSITION,Y,12)=VORG(2)                  ;LIEFERANT   ;supplier 
	SET $PIECE(YPOSITION,Y,47)=$GET(VORG(5))            ;PREIS 
	SET $PIECE(YPOSITION,Y,118)=$JUSTIFY($GET(VORG(5))/MENGE*$$^INQTYUNIT(ART),0,2)  ;PREIS 
	SET $PIECE(YPOSITION,Y,255)=1                       ;VK=EK
	SET $PIECE(YPOSITION,Y,85)="SYSTEM"                 ;BESTELLUNG
	SET $PIECE(YPOSITION,Y,83)=+$HOROLOG                ;DRUCKDATUM
	SET $PIECE(YPOSITION,Y,84)=$PIECE($HOROLOG,",",2)   ;UHRZEIT
	SET $PIECE(YPOSITION,Y,90)=1                        ;AUSLIEFERUNGSFÄHIG
	SET $PIECE(YPOSITION,Y,91)="AUTO"                   ;LIEFERSCHEIN
	SET $PIECE(YPOSITION,Y,92)=+$HOROLOG                ;DRUCKDATUM
	SET $PIECE(YPOSITION,Y,93)=$PIECE($HOROLOG,",",2)   ;UHRZEIT
	SET $PIECE(YPOSITION,Y,94)="SYSTEM"
	DO POSITION^INAUFANLAGE              ; can update classes : ^INAUF1, ^INAUFPK, ^INAUF, ^INAUFP, ^INAUFPSP, ^INAUFPXL +?
	
	IF $GET(POS)'="" DO ^INAUFPKNEU(AUF,POS,0)  ;FIS;15.03.05;27144;KONDITIONEN ANLAGEN
	
	IF $GET(POS)'="" IF $PIECE($GET(YAUSWAHL),",",2)'="" DO
	. NEW INERECH1,INERECH,YFELD,YKEY,YFORM,YVOR,YI
	. IF +$GET(MENGE)=0 SET MENGE=1
	. QUIT:$PIECE($GET(YAUSWAHL),",",3)=""
	. QUIT:$PIECE($GET(YAUSWAHL),",",4)=""
	. SET INERECH1=$GET(^INERECH1(YM,AUF,$PIECE(YAUSWAHL,",",2),$PIECE(YAUSWAHL,",",3),$PIECE(YAUSWAHL,",",4),1))
	. SET YFELD=""
	. FOR YI=1,24,25,37,38,39 SET $PIECE(YFELD,Y,YI)=$PIECE(INERECH1,Y,YI)
	. SET $PIECE(YFELD,Y,47)=$JUSTIFY($GET(VORG(5))/MENGE*$$^INQTYUNIT(ART),0,2)   ; Unit Base Price
	. SET $PIECE(YFELD,Y,16)=MENGE                         ; Invoice Quantity
	. SET $PIECE(YFELD,Y,12)=$GET(VORG(5))                 ; Base Purchase Price
	. SET $PIECE(YFELD,Y,13)=$GET(VORG(5))                 ; Net Purchase Price
	. SET $PIECE(YFELD,Y,22)=$GET(VORG(5))                 ; Invoice Amount
	. SET YKEY=AUF_","_POS_","_$PIECE(YAUSWAHL,",",3)_","_$PIECE(YAUSWAHL,",",4)
	. SET OK=$$^WWWSPEI("INERECH1",YKEY,YFELD,1)
	
	SET %("VAR","YKEY")=AUF
	SET %("VAR","YFORM")="INAUF"
	SET %("VAR","YBACK")=""
	DO ^WWWFORM
	QUIT
	
POSITION(YKEY)
	;-------------------------------------------------------------------------------
	; Called By: Form INERECH1 - Button 5 (Add Additional Puchase Line Item) - Execute OnClick
	;-------------------------------------------------------------------------------
	SET %("VAR","YAUSWAHL")=$GET(YKEY)
	SET %("VAR","YFORM")="INAUFPNEU"
	;SET %("VAR","YBACK")="INERECH1,"
	SET %("VAR","YBACK")=""
	DO ^WWWFORM
	SET $PIECE(^INUSER(YM,YBED,1),Y,3) =$PIECE(YKEY,",",1)
	SET $PIECE(^INUSER(YM,YBED,1),Y,4) =$PIECE(YKEY,",",2)
	SET $PIECE(^INUSER(YM,YBED,1),Y,18)=$PIECE(YKEY,",",3)
	SET $PIECE(^INUSER(YM,YBED,1),Y,56)=$PIECE(YKEY,",",4)
	QUIT
]]></Routine>
</Export>