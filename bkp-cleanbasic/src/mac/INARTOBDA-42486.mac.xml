<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTOBDA" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTOBDA

#include COMSYS
#include INConst
	
OnBeforeDataAccess(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Only display UOM's that are in INARTPACK.
	;
	; Inputs:
	; 	YKEY		Key of relationship : e.g. "EINHEIT,EN,1" or "KLASSIFIZIERUNG,EN,A"
	; 	YFELD		objINART
	; 
	; ByRef:
	;	YKEYCONTAINER	idINART
	;	YBBN			Form Field No			May not match $$$FldINART...
	;	YLFN			Class Field No (P & D)	$$$FldINART...
	; 
	; Returns:
	;
	; History:
	; 03-Feb-2010	GRF		SR17119: Also test idItem is not null.
	; 13-Jan-2010	shobby	SR17119: Don't allow selection of 'Obsolete' Item Units.
	; 17-Dec-2009	shobby	SR17034: Created a proper strStatus message.  At this time
	; 							it won't actually be displayed anywhere, but may be at some
	; 							future time.
	; 14-Sep-2009	shobby	SR16890: Created
	;-------------------------------------------------------------------------------
	new idItem,idUnit,objINARTPACK,strStatus
	
	set strStatus = $$$OK
	if '$$$NoKey(YKEYCONTAINER) && ($$$KEY1(YKEY)="EINHEIT") {
		set idItem = YKEYCONTAINER
		set idUnit = $$$KEY3(YKEY)
		if ($get(YBBN)=$$$FldINARTBaseUnit) {
			set strStatus = $$IsValid^INARTPACK(idItem,idUnit)
		
		} elseif ($get(YBBN)=$$$FldINARTUnitofMeasure) {
			if (idItem'="") && (idUnit'="") {   ; Allows selection of unit not yet in INARTPACK
				set objINARTPACK = $get(^INARTPACK(YM,idItem,idUnit,1))
				if $$$INARTPACKIsObsolete(objINARTPACK) {
					set strStatus = $$$NO   ; FIXME : Message or does OBDA really only deal with boolean result?
				}
			}
		}
	}
	quit strStatus
]]></Routine>
</Export>