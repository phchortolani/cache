<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFInvReprint" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFInvReprint
 
#include COMSYS
#include INConst
#include WWWConst
OnBeforeFormConstruction()
	;-------------------------------------------------------------------------------
	; Workaround: Override ReadOnly flag YAENBER
	; 
	; History:
	; 12-Nov-2008 HQN	SR16129: Created
	;-------------------------------------------------------------------------------
	set $$$WWW122UserAccessForChange(YVOR) = $$$WWW122UserAccessForChange($get(^WWW122(YM,YFORM,1,1)))
	quit
	
	;-------------------------------------------------------------------------------
	; Build COMTempList with invoices that can be re-reprinted.
	;
	; Params: pidOrder
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 19-May-2006	PO		SR14537: Ensure the form is always editable.
	; 24-Feb-2006	PO		SR14293: Created
	;-------------------------------------------------------------------------------
AvailableInvoices(pidOrder)
	new idKey,idLine,objLine,strFoundInvoices,strInvoiceNumber,strStatus,tmeTimeStamp
	kill YSORTBY // Unlock the form if it is locked
 
	if $get(pidOrder) = "" {
		set pidOrder = $get(^CacheTempINAUFInvReprint(YUSER,"OrderNumber"))
	} else {
		set ^CacheTempINAUFInvReprint(YUSER,"OrderNumber") = pidOrder
	}
	
	set strFoundInvoices = ""
	
	set idLine = ""
	for {
		set idLine = $order(^INAUFP(YM,pidOrder,idLine))
		quit:idLine=""
		set objLine = $get(^INAUFP(YM,pidOrder,idLine,1))
		set strInvoiceNumber = $$$INAUFPInvoiceNumber(objLine)
		
		if (";"_strFoundInvoices_";") '[ (";"_strInvoiceNumber_";") {
			set strFoundInvoices = $case(strFoundInvoices,"":strInvoiceNumber,:strFoundInvoices_";"_strInvoiceNumber)
			set tmeTimeStamp = $$$INAUFPBillPrintedDate(objLine)_","_$$$INAUFPBillPrintedTime(objLine)
			set ^COMTempList(YM,YUSER,"InvoicesToReprint",strInvoiceNumber,1) = $$^WWWTR(0,14,tmeTimeStamp)
		}
	}
	
	quit
 
	;-------------------------------------------------------------------------------
	; Print a document if required details provided
	;
	; Params: pidDocumentType
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 24-Feb-2006	PO		SR14293: Created
	;-------------------------------------------------------------------------------
RePrint(pidDocumentType) ;,strIdentifier
	new intCountInvoices,intEnd,strIdentifier
 
	set strIdentifier = $get(^WWWDATEN(YM,+$h,YUSER,YFORM,"M",1))
 
	if strIdentifier = "" {
		write "<p>"_$$$Text("IN00396")_"</p>" ; You need to select an Invoice To Re-Print.
		; Stop print dialog box from being displayed.
		;write "<script type='text/javascript'><!--",!
		$$$StartScript()
		write "document.body.onload = '';",!
		;write "//-->",!
		;write "</script>"
		$$$EndScript()
	} else {
		set intEnd = $length(strIdentifier,";")-1
		for intCountInvoices = 1:1:intEnd {
			set ^CacheTempINAUFInvReprint(YUSER,"InvoiceNumber") = $piece(strIdentifier,";",intCountInvoices)
			set YBELEG = pidDocumentType
			do ^INRECH1PRINT
			if intCountInvoices < intEnd write "<p style='page-break-after:always;'></p>",!
		}
		kill ^CacheTempINAUFInvReprint(YUSER,"InvoiceNumber")
	}
 
	quit
]]></Routine>
</Export>