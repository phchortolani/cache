<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPD5" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPD5	;INAUFPD5;FIS;MENGENÄNDERUNGEN PRÜFEN;25.08.2003  ; Compiled February 10, 2005 08:58:39
	;
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		MENGENÄNDERUNGEN PRÜFEN
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 18-Nov-2005	GRF		SR13570 : doco
	; 25.08.2003	FIS
	;-------------------------------------------------------------------------------
	NEW ALT,FELD1,KEY,NEU,WH,TXT
	
	SET ALT	   = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",2))
	SET NEU	   = $GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1))
	SET TXT	   = ""
	SET TXT(1) = ""
	SET KEY	   = $PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"P",1)),Y,1)
	
	/*+++++++++++++++++++++++++++++++++++++++
	; 33567		Attention! Sales Order Printed
	; 33568		Attention! Work Order Printed
	; 33569		Attention! Stock already assigned. Must be corrected manually if necessary.
	; 33871		This Receipt Has Already Been Entered (INWEAUF). Cancel!
	; 33953		Attention! Prices Can Be Wrong. Prices are Up-To-Date after processing Daily Files.
	; 34140		Single Sales Price Will Not be Re-calculated. Please Check.
	+++++++++++++++++++++++++++++++++++++++*/
	
	IF $PIECE(NEU,Y,5)'=$PIECE(ALT,Y,5) DO
	. SET WH=+$PIECE(NEU,Y,7)
	. IF WH=1        IF $PIECE(NEU,Y,83)'=""  SET TXT=33567      ;ACHTUNG! BESTELLUNG BEREITS GEDRUCKT ;sales order yet 
	. IF $PIECE(KEY,",",1)'="" IF $PIECE(KEY,",",2)'="" IF $DATA(^INWEAUF(YM,$PIECE(KEY,",",1),$PIECE(KEY,",",2))) SET TXT=33871   ;26548;TYBD;WE ZUGEORTNET;7,12,2004;sales order RECEIVED yet 
	. IF WH=2!(WH=5) IF $PIECE(ALT,Y,5)'<0    SET TXT=33569      ;ACHTUNG! LAGERBESTAND BEREITS ZUGEORDNET, MUSS GGF. MANUELL KORRIGIERT WERDEN
	. IF WH=3        IF $PIECE(NEU,Y,165)'="" SET TXT=33568      ;ACHTUNG, FERTIGUNGSANWEISUNG BEREITS GEDRUCKT
	. IF WH=3        IF $PIECE(NEU,Y,165)=""  SET TXT=33953      ;Achtung, Preise sind ggf. erst nach Abarbeiten Tagesdatei aktuell ;BEC;26231;17.08.04
	. ;IF $PIECE(ALT,Y,5)<0 I $PIECE(NEU,Y,5)>0 S TXT=???
	. SET TXT(1)=34140                                           ;FIS;27323;10.02.05;einzel-vk wird nicht neu kalkuliert
	
	IF TXT'=33953 IF TXT'="" DO
	. SET %TXT(1)="#Y"_YFORM_"D5~"_$PIECE(ALT,Y,5)_"#!"_$$^WWWTEXT(TXT,,1)  ;TYBD;7,12,2004;WIEDER REFRESH
	
	IF TXT=33953!(TXT(1)'="") DO
	. SET %TXT(1)="!"
	. IF TXT'=""               SET %TXT(1)=%TXT(1)_$$^WWWTEXT(TXT,,1)     ;TYBD;7,12,2004;WIEDER REFRESH
	. IF TXT'="" IF TXT(1)'="" SET %TXT(1)=%TXT(1)_"'"
	. IF TXT(1)'=""            SET %TXT(1)=%TXT(1)_$$^WWWTEXT(TXT(1),,1)  ;FIS;27323;10.02.05
	
	;BEC;26231;26231;16.08.04; NEURECHNEN HESTELLKOSTEN, WENN SICH DIE MENGE ÄNDERT
	QUIT:$PIECE(KEY,",",1)=""
	QUIT:$PIECE(KEY,",",2)=""
	
	SET FELD1=$GET(^INAUFP(YM,$PIECE(KEY,",",1),$PIECE(KEY,",",2),1))
	
	IF $PIECE(FELD1,Y,7)'=1 IF $PIECE(FELD1,Y,5)'=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1)),Y,5) DO     ;BEC;06.08.04;ÄNDERN HERSTELLKOSTEN DURCH ÄNDERUNG MENGE ;alter trans- alteration quantity 
	. NEW MENGEALT,HERSTKOST
	. SET MENGEALT  = $PIECE(FELD1,Y,5)
	. SET HERSTKOST = $PIECE(FELD1,Y,47)
	. IF +MENGEALT'=0 SET HERSTKOST = (HERSTKOST/MENGEALT)*$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1)),Y,5)
	. SET HERSTKOST = $JUSTIFY(HERSTKOST,0,2)
	. SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1),Y,47) = HERSTKOST     ;GEÄNDERTE HERSTELLKOSTEN
	
	QUIT
]]></Routine>
</Export>