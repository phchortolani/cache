<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPK
#include INConst
#include COMSYS
	; SR17138 change to (class/form) don't remove comment.
	
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		VORGABE DER KONDITIONEN
	;
	; Inputs : 
	;
	;
	; ByRef :
	;	YKEY		idOrder,idLine[,idSupplier]
	;	YFKEY		
	;	YFELD		
	;	YFORM		form name	"INAUFPK"
	;	YDATEI		class name	"INAUFPA", "INERECH1"
	;
	; Returns :
	;
	;
	; History :
	; 28-Sep-2006	GRF		Doco; objINAUF and macros in GESRAB
	; 04-Sep-2006	GRF		Naked References; doco; quits; long lines split
    ; 03-Jun-2005	shobby	SR12008: Called INAUFDISC ("INAUF" is header class)
	; 09-Oct-2000	DT
	;-------------------------------------------------------------------------------
	NEW AUF,KOND,POS,YI
	
	SET AUF=$PIECE(YKEY,",",1)         QUIT:AUF=""
	SET POS=$PIECE(YKEY,",",2)         QUIT:POS=""
	
	;+++++++++++++++++++++++++++++++++++++++
	;	KOND	objINAUFPK
	;	KOND	objINAUFPA
	;+++++++++++++++++++++++++++++++++++++++
	
	SET KOND=$GET(^INAUFPK(YM,AUF,POS,1))  ;AUS POSITION ;out of 
	IF $PIECE(YKEY,",",3)="" DO            ;LIEFERANT   ;supplier 
	. SET $PIECE(YKEY,",",3)  = $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,12)
	. SET $PIECE(YFKEY,",",3) = $PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,12)
	
	IF YFORM="INAUFPK" DO
	. IF $PIECE(YFELD,Y,16)="" 
	. . SET $PIECE(KOND,Y,16)=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,5)  ;MENGE   ;quantity 
	. . IF +$PIECE(KOND,Y,41)'=0 SET $PIECE(KOND,Y,16)=$PIECE(KOND,Y,16)*$PIECE(YFELD,Y,41)
	. do Recalculate2^INAUFDISC(.YFELD,"INAUF",AUF,POS)  ;SR12008
	
	QUIT:$TRANSLATE(YFELD,Y)'=""  ;SCHON DATEN ERFASST ;yet 
	IF $PIECE(YFELD,Y,16)="" DO
	. IF YFORM'="INAUFPK" IF +$PIECE($GET(^INAUFPK(YM,AUF,POS,1)),Y,41)'=0 SET $PIECE(KOND,Y,16)=$PIECE(^INAUFPK(YM,AUF,POS,1),Y,16) QUIT     ; 04-Sep-2006
	. SET $PIECE(KOND,Y,16)=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,5)    ;MENGE   ;quantity 
	
	IF +$PIECE(KOND,Y,16)'=0 IF +$PIECE(KOND,Y,47)=0 SET $PIECE(KOND,Y,47)=$JUSTIFY($PIECE(KOND,Y,12)/$PIECE(KOND,Y,16)*$$^INQTYUNIT(,AUF,POS),0,2)  ;EINZEIPREIS = GES/MENGE
	;IF +$PIECE(KOND,Y,16)'=0 SET $PIECE(KOND,Y,47)=$JUSTIFY($PIECE(KOND,Y,13)/$PIECE(KOND,Y,16),0,2)  ;EINZEIPREIS = GES/MENGE
	
	DO  ;SUCHEN AUFTRAGSBESTÄTIGUNG ;seek order confirmation 
	. NEW LIEFAB,ABNUM
	. SET LIEFAB=$ORDER(^INAUFPA(YM,AUF,POS,""),-1)        ;LETZTER LIEFERANT ;last supplier 
	. QUIT:LIEFAB=""
	. SET ABNUM=$ORDER(^INAUFPA(YM,AUF,POS,LIEFAB,""),-1)  ;LETZTE AB ;last Confirm. 
	. QUIT:ABNUM=""
	. IF $GET(YDATEI)="INAUFPA" DO  ;AUS VORHERIGER AB ;out of Confirm. 
	. . SET KOND=$GET(^INAUFPA(YM,AUF,POS,LIEFAB,ABNUM,1))  ;AUS AB ;out of Confirm. 
	
	/*vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	;IF $GET(YDATEI)="INAUFPA" DO  ;BEI AB
	. NEW LIEFAB,ABNUM,POSX
	. SET POSX=$ORDER(^INAUFPA(YM,AUF,POS),-1)              ;LETZTE POSITION   ;last line
	. QUIT:POSX=""
	. SET LIEFAB=$ORDER(^INAUFPA(YM,AUF,POSX,""),-1)        ;LETZTER LIEFERANT ;last supplier 
	. QUIT:LIEFAB=""
	. SET ABNUM=$ORDER(^INAUFPA(YM,AUF,POSX,LIEFAB,""),-1)  ;LETZTE AB         ;last Confirm. 
	. QUIT:ABNUM=""
	. SET $PIECE(YKEY,",",4)=ABNUM
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END */
	
	FOR YI=2:1:7,9,10,12,13,15,16,18,21,47,25 {         ; 04-Sep-2006 split line to clarify
		SET $PIECE(YFELD,Y,YI)=$PIECE(KOND,Y,YI)
		SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1),Y,YI)=$PIECE(KOND,Y,YI)  ;KOPIEREN DATEN AUS POSITION ;out of 
	}
	
	IF YDATEI="INAUFPA" DO 
	. NEW LIETM
	. SET LIETM=$PIECE($GET(^INAUFP(YM,AUF,POS,1)),Y,19)         ;LIEFERTERMIN POSITION ;time of delivery 
	. IF LIETM="" SET LIETM=$PIECE($GET(^INAUF(YM,AUF,1)),Y,19)  ;AUS AUFRAG ;out of 
	. SET $PIECE(YFELD,Y,23)=LIETM                               ;KOPIEREN DATEN AUS POSITION ;out of 
	. FOR YI=68,78,79 DO                                         ; 04-Sep-2006 split line to clarify
	. . SET $PIECE(YFELD,Y,YI)=$PIECE(KOND,Y,YI)
	. . SET $PIECE(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"D",1),Y,YI)=$PIECE(KOND,Y,YI)     ;ÜBERTRAGEN FREMSDWÄHRUNG;26316;FIS;25.08.04
	
	IF (YDATEI="INERECH1") || (YDATEI="INAUFPA") SET YI=22 SET $PIECE(YFELD,Y,YI)=$$^INNETTO(YFELD,1)   ;KOPIEREN DATEN AUS POSITION ;out of 
	QUIT
	
GESRAB ; GESAMTRABATT ANZEIGEN ;display Discount
	NEW AUF,KOND,objINAUF,POS,YI
	
	SET AUF=$PIECE(YKEY,",",1)          QUIT:AUF=""
	SET POS=$PIECE(YKEY,",",2)          QUIT:POS=""
	
	SET objINAUF = $GET(^INAUF(YM,AUF,1))                           ;AUFTRAG   ;order 
	IF +$$$INAUFSurchargeAmount(objINAUF)=0 IF +$$$INAUFSurcharge(objINAUF)=0 QUIT    ; D251/252   No Discount  ;KEIN GESAMTRABATT
	
	WRITE "&nbsp;"_$$^WWWTEXT(33061)_": "             ; "Discount On Total: "
	IF +$$$INAUFSurchargeAmount(objINAUF)'=0 WRITE $$^WWWZAHL($$$INAUFSurchargeAmount(objINAUF),0,2)_"&nbsp;&nbsp;&nbsp;"
	IF +$$$INAUFSurcharge(objINAUF)'=0       WRITE $$^WWWZAHL($$$INAUFSurcharge(objINAUF),0,2)_"% "
	quit


OnAfterSave(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Update INARTK from INAUFPK
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 24-Feb-2010	shobby		SR17206: Populate the Unit Price field in INARTK
	; 10-Feb-2010	shobby		SR17138.3 Conditional with INVORG setup.
	; 09-Feb-2010	shobby		SR17138.2 Call out to DoSave.
	; 05-Feb-2010	shobby		SR17138: Created
	;-------------------------------------------------------------------------------
	new objINARTK,idItem,idSupplier,objINAUFP,strStatus
	
	set strStatus=$$$OK
	if '$$$NoKey(YKEY) {
		if $$$INVORGUpdateItemConditionsPerSu($get(^INVORG(YM,YM,1)))=1 {		;When Editing order Line.
			set objINAUFP=$get(^INAUFP(YM,$$$KEY1(YKEY),$$$KEY2(YKEY),1))
			set idItem=$$$INAUFPItemNumber(objINAUFP)
			set idSupplier=$$$INAUFPSupplier(objINAUFP)
			if (idItem'="")&&(idSupplier'="") {
				set objINARTK=$get(^INARTK(YM,idItem,idSupplier,1))
				if objINARTK'="" {
					if $$$INAUFPKDeliveryQuantityFullLot(YFELD)     =$$$INARTKDeliveryQuantityUnit(objINARTK) {
						set $$$INARTKBasicPurchasePrice(objINARTK)  =$$$INAUFPKUnitPrice(YFELD)
						set $$$INARTKUnitPrice(objINARTK)			=$$$INAUFPKUnitPrice(YFELD)					;SR17206
						set strStatus=$$DoSave("INARTK",idItem_","_idSupplier,objINARTK)
					}
				}
			}
		}
	}
	quit strStatus


DoSave(YFORM,YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Before saving it is necessary to recalculate a few values.  Unfortunately the routine
	; that does that is tied to the free globals so we have to fudge a bit.
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 09-Feb-2010	shobby		SR17138.2: Created
	;-------------------------------------------------------------------------------
	;
	set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"D",1)= YFELD
	set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"P",1)= YKEY
	do ^INNETTOBRUTTO
	set YFELD =^WWWDATEN(YM,+$horolog,YUSER,YFORM,"D",1)
	kill YFORM  ;Save will fail if this is not killed.
	quit $$Save^COMUtils("INARTK",YKEY,YFELD,$$$YES)
]]></Routine>
</Export>