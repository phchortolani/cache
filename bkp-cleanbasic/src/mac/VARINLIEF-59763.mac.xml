<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINLIEF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINLIEF
#include COMSYS
#include INConst
#include COMConst
#include WWWConst
#include COMSYSDate
#define SecondsInDay 86400
#define MonthsInYear 12

OnAfterDataFieldsCustom(pYKEY,pYSEITE)

	//Preserve the ExecuteAfterDataFields form call (WWW120)
	set YSTOP = $$$NO

	if (YSEITE = 52) { //Contatos
		do OnAfterDataFields^VARINPARTN(pYKEY)
	}
	elseif (YSEITE = 53) {   //CRC
		 do OnAfterDataFields^VARCertidoesFornecedor(pYKEY)
	}
	elseif (YSEITE = 54) {   //Representantes
		do OnAfterDataFields^VARRepresentantesFornecedor(pYKEY)
	}
	elseif (YSEITE = 57) {   //Pré-Qualificações
		do OnAfterDataFields^VARFornecedorPreQualificacao(pYKEY)
	}
	elseif (YSEITE = 55) {   //Ocorrências
		do OnAfterDataFields^VARFornecedorOcorrencia(pYKEY)
	}
	elseif (YSEITE = 56) {   //Avaliações
		do OnAfterDataFields^VARFornecedorAvaliacao(pYKEY)
	}
	
	quit

OnBeforeSaveHook(pidSupplier, pobjSupplier)
	;-------------------------------------------------------------------------------
	; Check if CPF/CNPJ code is unique, valid and not blank
	; **** Note: This is optional code that is called based on an OnBeforeSave VAR hook
	;            being created on the INLIEF class.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns: strStatus
	;
	; History:
	; 06-Sep-2011	carlos	: Copied from INLIEF and adapted
	;-------------------------------------------------------------------------------
	if $get(Y)="" do ^WWWVAR
		
	new blnCompanyNumberIsUsed,idSupplier,idxCompanyNumber
	new strCompanyNumber,strCompanyNumberClean,strFiscal,strStatus
	
	quit:(pidSupplier="") $$$NO
	
	set strStatus             = $$$OK
	set strCompanyNumber      = $$$INLIEFCompanyNumber(pobjSupplier)
	set strCompanyNumberClean = ""
	set strFiscal = $$$INLIEFFREE5(pobjSupplier)
	
	if strCompanyNumber="" {
		if strFiscal=1 {
			set strStatus = $$$MakeStatus("Com00294")	     ; "Fill out the Person Number(CPF)"
		
		} elseif strFiscal=2 {
			set strStatus = $$$MakeStatus("Com00295")	     ; "Fill out the Company Number(CNPJ)"
		
		} else {
			/* Do not care */
		}
	} else {
		if strFiscal="" {
			set strStatus = $$$MakeStatus("Com00296")      ; "Fill out the "Natureza Fiscal""
		
		} else {
			; if number provided with no symbols add them to be saved and 
			; checked whether has already being used
			; 3N1"."3N1"."3N1"-"2N		2N1"."3N1"."3N1"/"4N1"-"2N
			if '$find(strCompanyNumber,".") {
				set strCompanyNumberClean = strCompanyNumber
				if (strFiscal=2) {	// CNPJ
					set strCompanyNumber =  $extract(strCompanyNumber,1,2)_"."_
											$extract(strCompanyNumber,3,5)_"."_
											$extract(strCompanyNumber,6,8)_"/"_
											$extract(strCompanyNumber,9,12)_"-"_
											$extract(strCompanyNumber,13,14)
				} else {			// CPF
					set strCompanyNumber =  $extract(strCompanyNumber,1,3)_"."_
											$extract(strCompanyNumber,4,6)_"."_
											$extract(strCompanyNumber,7,9)_"-"_
											$extract(strCompanyNumber,10,11)
				}
				set $$$INLIEFCompanyNumber(pobjSupplier) =  strCompanyNumber
			} else {
				set strCompanyNumberClean = $translate(strCompanyNumber,".-/")
			}
 
			set idxCompanyNumber = $$$Index(strCompanyNumber)
			if $data(^INLIEFs(YM,6,idxCompanyNumber)) {			
				set blnCompanyNumberIsUsed = $$$NO	
				$$$Order4(^INLIEFs,YM,6,idxCompanyNumber,idSupplier)
					if (idSupplier'=pidSupplier) {
						set blnCompanyNumberIsUsed = $$$YES	
						quit
					}
				$$$End
				if (blnCompanyNumberIsUsed) {
					set strStatus = $$$MakeStatus("Com00293")
				} ; "This Person Number(CPF) or Company Number(CNPJ) has already been used"
			}		
		}
	}
	
	if $$$ISOK(strStatus) {
		new idRec, blAchou
		if ($$$INLIEFFREE23($get(^INLIEF(YM,pidSupplier,1))) = 1){
			set blAchou = $$$NO
			set idRec = ""
			for {
				set idRec = $order(^INREC(0,idRec))
				quit:(idRec = "")
				quit:(blAchou = $$$OK)
				continue:($$$INRECTypeofReceiving($get(^INREC(0,idRec,1))) '= 4)
				continue:($$$INRECSupplier($get(^INREC(0,idRec,1))) '= pidSupplier)
				set blAchou = $$$OK
			}	
			if (blAchou){
				set strStatus = "Não é possível desmarcar a opção ""Incorporação/Produção"" pois já foi feito um recebimento deste tipo para este fornecedor."
			}
		}
	}
	
	if $$$ISOK(strStatus) {		
		if (strFiscal=2) {
			set strStatus = $$ValidateCNPJ^COMValidation(strCompanyNumberClean)
		
		} elseif (strFiscal=1) {
			set strStatus = $$ValidateCPF^COMValidation(strCompanyNumberClean)
		}
	}
	quit strStatus
	
	 
OnAfterDataField(YKEY, YFELD, pintField)
	;-------------------------------------------------------------------------------
	; Returns Date, Invoice value, Invoice Number, Month value, Year value from INREC for each Supplier
	; 
	; Params: pintField - the field number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-Nov-2006	GM		SRBR014327: Created
	;------------------------------------------------------------------------------- 
	if $get(Y)="" do ^WWWVAR
	
	new strResult, objINREC, objINAUF
	
	set idINREC=""
	set intNRec=""
	set strLast=0
	set strDate=""
	set idLast=""
	set intValue=""
	set intCurrencyValue=0
	set intMonthValue=0
	set intYearValue=0	
	set strSupplier=""
	set intOrder=""
 
		
	for {
 		set intValue=intMonthValue
 		set idINREC=$order(^INREC(YM,idINREC),-1)
 		quit:(idINREC="")
 		set objINREC = $get(^INREC(YM,idINREC,1))
 		set intOrder = $$$INRECFREE10(objINREC)
 		set intNRec = $$$INRECStatus(objINREC)
 		set intValue = $$$INRECAmount(objINREC)
 		
 		
 	 	if intOrder'="" {
			set objINAUF = $get(^INAUF(YM,intOrder,1)) 
		 	set strSupplier=$$$INAUFSupplierNumber(objINAUF)
	 		if strSupplier=$get(YKEY) {
	 			if (intNRec=1){						 			
		 			set strDate = $$$INRECDeliveryNoteDate(objINREC)
		 			set intCurrencyValue = intValue + intCurrencyValue
		 				 			 			 		
	 				if strDate>strLast {
	  					set strLast = strDate 
	  					set idLast = idINREC
	  						
	   				}
	   				set strStartMonth=$$$DateFirstDayOfMonth(strLast)
					set strEndMonth=$$$DateLastDayOfMonth(strLast)
					
					if (strDate'<strStartMonth)&&(strDate'>strEndMonth) {
						set intMonthValue = intValue + intMonthValue
					
	   				}
	   				set strStartYear=$$$DateFirstDayOfYear(strLast)
					set strEndYear=$$$DateLastDayOfYear(strLast)
					
					if (strDate'<strStartYear)&&(strDate'>strEndYear) {
						set intYearValue = intValue + intYearValue
					
	   				}
	   				
	   			}		
	   		}	
		}	
	}
	
	set strResult=0
	
	if (idLast'="") && (intNRec=1) {
		set objINREC=$get(^INREC(YM,idLast,1))
		
		set strResult = $piece(objINREC,Y,pintField)
	}
	if 1 {	
		if pintField = 13 {			
			if strResult'=0 {
				set strResult = $ZDATE(strLast,4)
			} else {
				set strResult=""
			}			
			write "<TABLE BORDER=0 STYLE='FONT-SIZE: 13px' CELLSPACING=0><TR><TD WIDTH=150 ALIGN=RIGHT>Data(Última compra):</TD><TD ALIGN=LEFT>&nbsp;"_strResult_"</TD></TR></TABLE>"
 			
		}
		if pintField = 15 {
			write "<TABLE BORDER=0 STYLE='FONT-SIZE: 13px' CELLSPACING=0><TR><TD WIDTH=150 ALIGN=RIGHT>Valor(Última compra):</TD><TD ALIGN=LEFT>&nbsp;R$ "_strResult_"</TD></TR></TABLE>"	
		}	
		if pintField = 14 {
			write "<TABLE BORDER=0 STYLE='FONT-SIZE: 13px' CELLSPACING=0><TR><TD WIDTH=150 ALIGN=RIGHT>N.F.(Última compra):</TD><TD ALIGN=LEFT>&nbsp;"_strResult_"</TD></TR></TABLE>"
		}	
		if pintField = 400 {					
			set strResult = intMonthValue
			write "<TABLE BORDER=0 STYLE='FONT-SIZE: 13px' CELLSPACING=0><TR><TD WIDTH=150 ALIGN=RIGHT>Valor Mensal:</TD><TD ALIGN=LEFT>&nbsp;R$ "_strResult_"</TD></TR></TABLE>"
		}	
 		if pintField = 410 {
			set strResult = intYearValue
			write "<TABLE BORDER=0 STYLE='FONT-SIZE: 13px' CELLSPACING=0><TR><TD WIDTH=150 ALIGN=RIGHT>Valor Anual:</TD><TD ALIGN=LEFT>&nbsp;R$ "_strResult_"</TD></TR></TABLE>"
			
		}	
	}
	
	quit 1

getNomeFornecedor(pCodFornecedor)
	if pCodFornecedor = "" quit ""
	quit $piece($get(^INLIEF(YM,pCodFornecedor,1)),Y,4)
	
getCNPJ(pidFornecedor)
	quit:(pidFornecedor = "") ""
	
	set objFornecedor = $get(^INLIEF(YM,pidFornecedor,1))
	quit:(objFornecedor = "") ""
	
	quit $piece(objFornecedor,Y,201)  ;CompanyNumber
	
getEmail(pCodFornecedor)
	if pCodFornecedor = "" quit ""
	quit $piece($get(^INLIEF(YM,pCodFornecedor,1)),Y,24)
 
getFax(pCodFornecedor)
	if pCodFornecedor = "" quit ""
	quit $piece($get(^INLIEF(YM,pCodFornecedor,1)),Y,22)
 
getStatusFornecedor(pCodFornecedor)
	if pCodFornecedor = "" quit ""
	quit $piece($get(^INLIEF(YM,pCodFornecedor,1)),Y,258)
 
getUltimaCompraRegular(pidItem,pCodFornecedor)
	if pidItem = "" quit ""
	if pCodFornecedor = "" quit "" 
	
	new Quantidade, PrecoUnitario, ValorTotal, Fornecedor, Data, NumAFM, NumItemAFM,
		strUltimaCompraRegular
		
	set strUltimaCompraRegular = ""
	
	set Quantidade 	  = ""
	set PrecoUnitario = ""
	set ValorTotal 	  = ""
	set Fornecedor    = ""
	set Data 		  = ""
	set NumAFM 		  = ""
	set NumItemAFM 	  = ""
	
	&sql(SELECT TOP 1
			INAUFP.Quantity AS Quantidade,
			INAUFPK.UnitPrice AS PrecoUnitario,
			INAUFP.Cost AS ValorTotal,
			INAUF.SupplierNumber||' - '||INLIEF.ShortName AS Fornecedor,
			INAUF.OrderDate AS Data,
			INAUF.OrderNumber AS NumAFM,
			INAUFP.LineItem AS NumItemAFM
		INTO :Quantidade, :PrecoUnitario, :ValorTotal, :Fornecedor, :Data, :NumAFM, :NumItemAFM
		FROM INAUFP, INAUF, INAUFPK, INLIEF 
		WHERE  INAUF.OrderNumber = INAUFP.OrderNo
		  AND  INAUFP.OrderNo = INAUFPK.OrderNumber
		  AND  INAUFP.LineItem = INAUFPK.LineItem
		  AND  INAUF.SupplierNumber = INLIEF.SupplierNumber
		  AND ItemNumber = :pidItem
          AND INAUF.SupplierNumber = :pCodFornecedor
		ORDER BY Data DESC	
	)
	
	set strUltimaCompraRegular = Quantidade_Y_PrecoUnitario_Y_ValorTotal_Y_Fornecedor_Y_Data_Y_NumAFM_Y_NumItemAFM	
 
	quit strUltimaCompraRegular
	
 
getUltimaCompraPDPAS(pidItem,pCodFornecedor)
	if pidItem = "" quit ""
	if pCodFornecedor = "" quit "" 
	
	new Quantidade, PrecoUnitario, ValorTotal, Fornecedor, Data, NumCompra, NumItemCompra,
		strUltimaCompraPDPAS
		
	set strUltimaCompraPDPAS = ""
	
	set Quantidade    = ""
	set PrecoUnitario = ""
	set ValorTotal 	  = ""
	set Fornecedor    = ""
	set Data          = ""
	set NumCompra     = ""
	set NumItemCompra = ""
	
	&sql(SELECT TOP 1
		QuantidadeOfertada, PrecoUnitario, PrecoTotal, Fornecedor, ModificadoEm, NoProcessodeCompra, Item
		INTO :Quantidade, :PrecoUnitario, :ValorTotal, :Fornecedor, :Data, :NumCompra, :NumItemCompra
		FROM  VARSESCOMPRASPropostaLinha
		WHERE $UPPER(Resultado) = 1
		  AND Produto = :pidItem
          AND Fornecedor = :pCodFornecedor
		ORDER BY CriadoEm DESC
	)
	
	set strUltimaCompraPDPAS = Quantidade_Y_PrecoUnitario_Y_ValorTotal_Y_Fornecedor_Y_Data_Y_NumCompra_Y_NumItemCompra	
	
	quit strUltimaCompraPDPAS
	
OnBeforeDataAccess(YFORM="",YKEY="")
	new strStatus, objINLIEF
	
	set strStatus = $$$OK
	set objINLIEF = $get(^INLIEF(YM,YKEY,1))
	
	if ($get(YFORM) = "VAREntrada"){
		if ($$$INLIEFFREE23(objINLIEF) '= 1) {
			set strStatus = $$$NO
		}
	} else {
		if ($$$INLIEFFREE23(objINLIEF) = 1) {
			set strStatus = $$$NO
		}		
	}

	quit strStatus	
]]></Routine>
</Export>