<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW012" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW012
	
#include COMSYS
#include WWWConst
	
WelcomeMessage(SPRACHE)
	;-------------------------------------------------------------------------------
	; Gets the welcome message
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 24-Nov-2010	shobby		SR17247: Created
	;-------------------------------------------------------------------------------
	new objWWW012,strMessage
	
	set objWWW012 = $get(^WWW012(0,0,1))
	if $$$WWW012WelcomeMessage(objWWW012)'="" {
		set strMessage = $$$Text($listbuild($$$WWW012WelcomeMessage(objWWW012),$$$WWW012CompanyName(objWWW012)))
		
	} else {
		set strMessage = $$$Text($listbuild("413",$$$WWW012CompanyName(objWWW012)),,SPRACHE)
	} ; "Welcome to %1"
	quit strMessage
	
FullScreenHeaderImage()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 05-Sep-2012	shobby		SR18104: Created
	;-------------------------------------------------------------------------------
	new objWWW012,strImage
	
	set objWWW012=$get(^WWW012(0,YM,1))
	set strImage=$$$WWW012FullScreenHeaderImage(objWWW012)
	if strImage="" set strImage="COMtopleft.gif"
	quit strImage


LoginImage()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 06-Sep-2012	shobby		SR18106: Created
	;-------------------------------------------------------------------------------
	new objWWW012,strImage
	
	set objWWW012=$get(^WWW012(0,YM,1))
	set strImage=$$$WWW012LoginImage(objWWW012)
	if strImage="" set strImage="elem4.gif"
	quit strImage

	
GetMaxSelectRecords()
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 05-Dec-2006	JW		SR???: Created
	;-------------------------------------------------------------------------------
	new intMax
	
	set intMax = $$$WWW012MaximumNumberOfRecordsOnS($get(^WWW012(0,0,1)))
	
	if (intMax<0) || (intMax="") || (intMax>100) {
		set intMax = 100
	}
	quit intMax
	
	
ModificationLogRetention(pYM) ;BR014891
	quit $$$WWW012ModificationLogRetention($get(^WWW012(0,pYM,1)))
	
	
FontFace(pYM)
	;-------------------------------------------------------------------------------
	; Get the FontFace setting
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 01-Sep-2008	shobby	SRBR014976: Created
	;-------------------------------------------------------------------------------
	new intFont
	
	if $get(pYM)="" set pYM = $get(YM,0)
	set intFont = $$$WWW012FontFace($get(^WWW012(0,pYM,1)))
	quit $$$SysEnum("SCHRIFTART",intFont)
	
	
OnBeforeSave(&YFELD)
	;-------------------------------------------------------------------------------
	; Process before Save - files are rebuilt if not found - character set should
	; not be changing once set as a rule so little impact on live system.
	;
	; Params: YFELD (ByRef) onscreen copy of objWWW012 (?)
	;
	; Returns:
	;
	; History:
	; 22-Aug-2011	shobby	SR17873: Moved block of code to COMUpgradeHandler
	; 22-Sep-2009	shobby	SR16894: Remove javascript files when there has been a
	; 							change in character set.
	; 10-Jan-2008	FIS		SR15619: set "framed form" in WWW012 always to YES
	;-------------------------------------------------------------------------------
	new idLanguage,strDirectory,strStatus
	
	set strStatus = $$$OK
	if $$$WWW012FormsFramed(YFELD)'=$$$YES {
		set $$$WWW012FormsFramed(YFELD) = $$$YES
	}
	
	if $$$WWW012CharacterSet(YFELD)'=$$$WWW012CharacterSet($get(^WWW012(0,0,1))) {
		set strStatus=$$DeleteJSFiles()		;SR17873 ;ALPHAUP-177
	}
	if $$$ISERR(strStatus) {
		$$$Alert(strStatus)
		set Q = $$$QDontSave
	}
	quit strStatus

DeleteJSFiles()
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
    ; 10-Sep-2014	shobby		ALPHAUP-177: Copied from COMUpgradeHandler
	; 22-Aug-2011	shobby		SR17873: Created
	;-------------------------------------------------------------------------------
	new strStatus,strDirectory
	
	set strStatus = $$$OK
	
	set strDirectory = ##Class(%File).NormalizeDirectory($$$WWW012PhysicalWWWDirectory($get(^WWW012(0,0,1))))
	set strStatus    = $$Delete(strDirectory_"COMView0.css")
	if $$$ISOK(strStatus) set strStatus = $$Delete(strDirectory_"griden1.js")
	if $$$ISOK(strStatus) set strStatus = $$Delete(strDirectory_"GridEdit.css")
	set idLanguage = ""
	for {
		set idLanguage = $order(^WWW100(0,"SPRACHE",idLanguage))
		quit:idLanguage=""
		
		if $$$ISOK(strStatus) set strStatus = $$Delete(strDirectory_"COMView"_idLanguage_"_0.js")
	}
	quit strStatus

Delete(pstrFile)
	;-------------------------------------------------------------------------------
    ; Delete a file using %Library.File instead of $zf(-1)
    ;
    ; Returns: boolean success or error message
    ;
    ; History:
    ; 10-Sep-2014	shobby	ALPHAUP-177: Copied from COMDistOut6
    ; 15-Oct-2007	GRF		Standard Error Prefixes
    ; 23-Jun-2005	RPW		Created SR12022,12280
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	
	if ##class(%Library.File).Exists(pstrFile) {
		set strStatus = ##class(%Library.File).Delete(pstrFile)
	}
	
	if strStatus = $$$NO {
		set strStatus = $listbuild("Com00303",pstrFile)   ; "File '%1' does not exist. Contact Support"
	}
	
	quit strStatus	
	
Get(pYM) ; 19-Jan-1011 - no calls found to this tag - use $get(^WWW012(0,0,1)) directly
	;-------------------------------------------------------------------------------
	; History:
	; 19-Jan-2011	GRF		-: remove unneeded $data and set to null
	; 22-Dec-2008	HQN		SR16255: Created
	;-------------------------------------------------------------------------------
	new objWWW012
	
	set objWWW012 = $get(^WWW012(0,pYM,1))
	quit objWWW012
	
	
SimulateCommainPrimaryKey(pYM=0)
	;-------------------------------------------------------------------------------
	; Simulate Comma *in* Primary Key  ; FIXME : Rename for clarification
	; 
	; History:
	; 10-Feb-2009	shobby	SR16126: Created
	;-------------------------------------------------------------------------------
	quit +$$$WWW012SimulateCommainPrimaryKey($get(^WWW012(0,pYM,1)))
	
	
CoolbarBackgroundColor(pYM=0)
	;-------------------------------------------------------------------------------
	; History:
	; 20-Apr-2009	shobby	SR16455: Created
	;-------------------------------------------------------------------------------
	new intColor
	
	set intColor = $$$WWW012CoolbarBackgroundColor($get(^WWW012(0,pYM,1)))
	if intColor=""  set intColor = $$$WWW012BackgroundColor($get(^WWW012(0,pYM,1)))
	if intColor'="" set intColor = $$$SysEnum("FARBE",intColor)
	quit intColor

HideRelationClassIDs() ;SR17840
	quit +$$$WWW012HideRelationClassIDs($get(^WWW012(0,YM,1)))

FixedColumns() ;CORE-254
	quit +$$$WWW012FixedColumns($get(^WWW012(0,YM,1)))
	
HideHelpButtons()
	quit +$$$WWW012HideHelpButtons($get(^WWW012(0,YM,1))) ;SESDF-715

CharacterSet()
	quit $$$SysEnum("CHARSET",+$$$WWW012CharacterSet($get(^WWW012(0,YM,1)))) ;SESDF-822


]]></Routine>
</Export>