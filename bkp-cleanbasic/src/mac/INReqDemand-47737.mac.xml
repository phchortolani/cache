<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReqDemand" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReqDemand
	
#include INConst
#include COMSYS
#include COMView
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Show the form as a COMView with Accept/Reject buttons  
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 25-May-2007	RPW		SR15513: Created
	;-------------------------------------------------------------------------------
	do Initialise^COMViewCustom("INDRPDEMAND",,1,1)
	do AddFilter^COMViewCustom("P1","=",YLOCATION)
	do AddFilter^COMViewCustom("D"_$$$FldINDRPDEMANDBlocked,"=",$$$YES)
	do AddFilter^COMViewCustom("D"_$$$FldINDRPDEMANDRequisitionNumber,"'=")
	do AddFilter^COMViewCustom("D"_$$$FldINDRPDEMANDDemandClosed,"'=",$$$YES)
	
	do AddCommand^COMViewCustom($$^WWWTEXT("IN00686"),"ok.gif",       "Release^INReqDemand", $$^WWWTEXT("IN00686"))  ; "Accept Demand"
	do AddCommand^COMViewCustom($$^WWWTEXT("IN00687"),"AEPReject.gif","UnSource^INReqDemand",$$^WWWTEXT("IN00687"))  ; "Reject Demand"
 
	do Show^COMViewCustom()
	quit
	
	
Release(pobjResult)
	;-------------------------------------------------------------------------------
	; Allow the releasing of a demand
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 25-May-2007	RPW		SR15513: Created
	;-------------------------------------------------------------------------------
	new idDemand
	
	while pobjResult.Next() {
		set idDemand=$$GetID^COMViewCustom(pobjResult)
		if idDemand'="" do ReleaseDemands(idDemand)	
	}
	quit $$$OK
	
	
UnSource(pobjResult)
	;-------------------------------------------------------------------------------
	; Prepare to unsource the requisition and close the demand
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 25-May-2007	RPW		SR15513: Created
	;-------------------------------------------------------------------------------
	new idDemand,strScript
	
	while pobjResult.Next() {
		set idDemand=$$GetID^COMViewCustom(pobjResult)
		if (idDemand'="") {
			set strScript="var strMessage=prompt('"_$$$Text("IN00688")_"',''); CallBackNow('CloseDemand^INReqDemand','"_idDemand_",'+strMessage);"
			
			$$$StartScript()
			write strScript
			$$$EndScript()
			;do CloseDemand(idDemand)
		}
	}
	quit $$$OK
	
	
CloseDemand(pidDemandAndMessage)
	;-------------------------------------------------------------------------------
	; Close the demand and unsource the creating requisition
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 24-May-2007	RPW		SR15513: Created
	;-------------------------------------------------------------------------------
	new idRequisition,objDemand,objRequisition,strStatus
	
	set objDemand     = $get(^INDRPDEMAND(0,$$$KEY1(pidDemandAndMessage),$$$KEY2(pidDemandAndMessage),1))
	set idRequisition = $$$INDRPDEMANDRequisitionNumber(objDemand)
	
	do AddEntry^COMChangeHistory(0,YFORM,YBED,"0,"_idRequisition,"Demand Rejected:"_$piece(pidDemandAndMessage,$$$COMMA,3,99))
	
	set strStatus = $$SetStatus^INReqCommon(YFORM,idRequisition,$$$EnumINREQSTATUSSourced,$$$EnumINREQSTATUSFirmed)
	
	set objRequisition = $get(^INRequisition(0,0,idRequisition,1))
	set $$$INRequisitionStatus(objRequisition)   = $$$EnumINREQSTATUSFirmed
	set $$$INRequisitionSource(objRequisition)   = $$$NO
	set $$$INRequisitionDemand(objRequisition)   = ""
	set $$$INRequisitionRejected(objRequisition) = $$$YES
	
	if $$$ISOK(strStatus) {
		set strStatus = $$$Save("INRequisition","0,"_idRequisition,objRequisition,$$$YES)
		if $$$ISOK(strStatus) {
			set $$$INDRPDEMANDDemandClosed(objDemand) = $$$YES
			set strStatus = $$$Save("INDRPDEMAND",$piece(pidDemandAndMessage,$$$COMMA,1,2),objDemand,$$$YES)
		}
	}
	
	// The below sucks, but there is nothing I can do about it.
	;kill ^CacheTempSQL($$$SQLID) // SR15041
	;do DisplayGrid^COMViewFilter()
	
	write "document.location.reload(true);"
	quit
	
	
ReleaseDemands(pidDemand)
	;-------------------------------------------------------------------------------
	; Find the demands and mark them as unblocked and ready for delivery so they can
	; go on a transfer
	;
	; Inputs:
	;
	; ByRef:
	;
	; Returns:
	;
	; History:
	; 24-May-2007	RPW		SR15513: Created
	;-------------------------------------------------------------------------------
	new idRequisition,lstData,objDemand,objRequisition,strStatus
		
	set strStatus = $$$OK
	
	set objDemand     = $get(^INDRPDEMAND(0,$$$KEY1(pidDemand),$$$KEY2(pidDemand),1))
	set idRequisition = $$$INDRPDEMANDRequisitionNumber(objDemand)
	
	set objRequisition=$get(^INRequisition(0,0,idRequisition,1))
	
	do AddEntry^COMChangeHistory(0,YFORM,YBED,"0,"_idRequisition,$$^WWWTEXT("IN00691"))  ; "Demand Accepted"
	
	set strStatus = $$UpdateRequisition^INReqRelease(idRequisition,YFORM,.lstData) // Put back in
	
	if $$$ISOK(strStatus) {
		set $$$INDRPDEMANDBlocked(objDemand)          = $$$NO
		set $$$INDRPDEMANDReadyForDelivery(objDemand) = $$$YES
	
		set strStatus = $$$Save("INDRPDEMAND",pidDemand,objDemand,$$$YES)
		set VORG(3)   = $$$INRequisitionDueDate(objRequisition)
		
		do ^INDRPSAVED(pidDemand,objDemand)
	}
	quit
	
]]></Routine>
</Export>