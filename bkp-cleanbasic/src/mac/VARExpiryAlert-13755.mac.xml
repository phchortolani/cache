<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARExpiryAlert" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARExpiryAlert
#include COMSYS

OnBeforeFormConstruction	
	if $get(^VARTempExpiryAlert(YM,YBED,1)) = "" {
		set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,3) = YLOCATION
		set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,4) = 30 //default, 30 days
		set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,9) = 1 //default, contábil
	}
	
	quit

OnAfterSave
	new Item, Group, Location, intDaysToExpire, intDaysExpired, program
	
	//Filter
	set Item  	 = $get(VORG(1))
	set Group    = $get(VORG(2))
	set Location = $get(VORG(3))
	set program  = $get(VORG(6))
	set Format   = $get(VORG(9))

	set intDaysToExpire = $get(VORG(4))
	set intDaysExpired  = $get(VORG(5))

	set intFromPeriodExpiry = $get(VORG(7))
	set intToPeriodExpiry   = $get(VORG(8))
	set ContaContabil		= $get(VORG(10))

	// If the user has not set any filtering
	if ((intDaysToExpire = "") && (intDaysExpired = "") &&
		(intFromPeriodExpiry = "") && (intToPeriodExpiry = "")) {
		set intDaysToExpire = 30
	}

	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,1)  = Item
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,2)  = Group
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,3)  = Location
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,4)  = intDaysToExpire
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,5)  = intDaysExpired
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,6)  = program
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,7)  = intFromPeriodExpiry
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,8)  = intToPeriodExpiry
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,9)  = Format
	set $piece(^VARTempExpiryAlert(YM,YBED,1),Y,10) = ContaContabil

	do VARLogAfterSave^VARUtil(YBED,YFORM)
	do ReloadForm^COMUtilForm("VARExpiryAlert")
	
	quit
	
OnAfterDataFields
	new flterItem, flterGroup, flterLocation, flterDays, flterDaysToExpire, flterDaysExpired,
		dteStart, dteEnd, object, mySQL,flProgram, flFormat, flContaContabil, showPhysical, dmm,coberturaEstoque

	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	set flterItem         = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,1)
	set flterGroup        = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,2)
	set flterLocation     = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,3)	
	set flterDaysToExpire = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,4)
	set flterDaysExpired  = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,5)
	set flProgram         = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,6)	
	set flFormat          = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,9)	
	set flContaContabil   = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,10)	
	if flFormat=3 {
		set flFormat=1
		set showPhysical=1
	}else {
		set showPhysical=0
	}
	
	if flterDaysExpired = "" {
		set dteStart = $piece($horolog,",",1)
	
	} else {
		set dteStart = $piece($horolog,",",1) - flterDaysExpired
	}

	if (flterDaysToExpire = "") {
		if (flterDaysExpired = "") {
			set flterDaysToExpire = 30
		} else {
			set flterDaysToExpire = 0
		}
	}
	
	set dteEnd = $piece($horolog,",",1) + flterDaysToExpire

	set flterFromPeriodExpiry = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,7)
	set flterToPeriodExpiry   = $piece($get(^VARTempExpiryAlert(YM,YBED,1)),Y,8)

	if ((flterFromPeriodExpiry '= "") || (flterToPeriodExpiry '= "")) {
		if (flterFromPeriodExpiry '= "") {
			set dteStart = flterFromPeriodExpiry - 1
		}
		else {
			set dteStart = ""
		}
		set dteEnd = flterToPeriodExpiry

		set flterDaysToExpire = ""
	}
	
	set object = ##class(%ResultSet).%New()  ;Create Result Set Object

	set mySQL = "SELECT alSOH.dBundleStock.Item,"
	set mySQL = mySQL_" alSOH.dBundleStock.Storage->Location,"
	set mySQL = mySQL_" alSOH.dBundleStock.Storage->Code,"
	
	if (flFormat = 2){ // físico
		set mySQL = mySQL_" +alSOH.dBundleStock.QtyOnHand,"
	}
	elseif (flFormat = 1){ // contábil
		set mySQL = mySQL_" SUM(+alSOH.dBundleStock.QtyOnHand),"
	}
	
	set mySQL = mySQL_" alSOH.dBundleStock.Bundle->LotNumber,"
	
	;if (flFormat = 1){ // Contabil
	;	set mySQL = mySQL_" MIN(alSOH.dBundleStock.Bundle->UseByDate),"
	;}
	;else{
		set mySQL = mySQL_" alSOH.dBundleStock.Bundle->UseByDate,"
	;}
	
	set mySQL = mySQL_" alSOH.dBundleStock.Bundle->Program,"
	set mySQL = mySQL_" alSOH.dBundleStock.Bundle->Brand,"
	set mySQL = mySQL_" ROUND($$SQLGetINARTKOST^VARSQL(alSOH.dBundleStock.Item),2) as CustoMedio,"
	set mySQL = mySQL_" $$SQLGetContaContabil^VARSQL(alSOH.dBundleStock.Item) as CodContaContabil"
	set mySQL = mySQL_" FROM alSOH.dBundleStock"
	set mySQL = mySQL_" WHERE alSOH.dBundleStock.Bundle->UseByDate is not null"

	if flterItem '= "" {
		set mySQL = mySQL_" AND alSOH.dBundleStock.Item = "_flterItem
	}
	
	if flterLocation '= "" {
		set mySQL = mySQL_" AND alSOH.dBundleStock.Storage->Location = """_flterLocation_""""
	}

	if (dteStart '= "") {
		set mySQL = mySQL_"    AND alSOH.dBundleStock.Bundle->UseByDate >= "_dteStart
	}
	if (dteEnd '= "") {
		set mySQL = mySQL_"    AND alSOH.dBundleStock.Bundle->UseByDate < "_dteEnd
	}
	
	if flProgram '= "" {
		set mySQL = mySQL_"    AND alSOH.dBundleStock.Bundle->Program = "_flProgram
	}
	
	if (flContaContabil '= ""){
		set mySQL = mySQL_"    AND $$SQLGetContaContabil^VARSQL(alSOH.dBundleStock.Item) = """_flContaContabil_""""
	}
	
	if (flFormat = 1){ // contábil
		if showPhysical=1 {
			set mySQL = mySQL_" GROUP BY alSOH.dBundleStock.Item,alSOH.dBundleStock.Bundle->LotNumber,alSOH.dBundleStock.Bundle->UseByDate HAVING alSOH.dBundleStock.Bundle->UseByDate = MIN(alSOH.dBundleStock.Bundle->UseByDate)"
		}else {
			set mySQL = mySQL_" GROUP BY alSOH.dBundleStock.Item HAVING alSOH.dBundleStock.Bundle->UseByDate = MIN(alSOH.dBundleStock.Bundle->UseByDate)"
		}
	}
    
    set mySQL = mySQL_" ORDER BY alSOH.dBundleStock.Bundle->UseByDate"
    
	do object.Prepare(mySQL) ;Prepare Query
	do object.Execute()  	  ;Execute Query  
    
	//Tabela
   	new lstHeader, count, Item, Location, Storage, Quantity, Lot, ExpiryDate, idPrograma, nomePrograma,
   		objItem, ItemDesc, UnitCode, UnitDesc, ItemGroup, intNoDaysAVencer, strImg, key, CustoMedio, ValorTotal,
   		idMarca, nomeMarca, descStorage, objStorage, blnDUC
 	
 	set lstHeader = ""
 	
 	;Petik 05/08/2010
 	;Se o parâmetro de cliente Mostrar Valor no Alerta de Validade for igual a SIM
 	;mostra duas colunas a mais (Custo Médio e Valor Total)
 	if $$getMostrarValorAlertaValidade^VARParametroCliente(YM) {
	  	if (flFormat = 2){ // Formato Físico
		  	set lstHeader = lstHeader_$listbuild(,"Código",
		  										  "Produto",
		  										  "Local",
		  										  "Lote",
		  										  "Validade",
		  										  //"Programa",
		  										  "Marca",
		  										  "Quantidade",
		  										  "Unidade",
		  										  "Endereço",
		  										  "Prazo&nbsp;<br />&nbsp;&nbsp;Restante",
		  										  "Custo Médio",
		  										  "Valor Total")
	  	}
	  	elseif (flFormat = 1) { // Formato Contábil
	  		if showPhysical=1 {
	  			set lstHeader = lstHeader_$listbuild(,"Código",
		  											  "Produto",
		  											  "Local",
		  											  "Lote",
		  										 	  "Validade",
		  											  //"Programa",
		  											  "Unidade",
		  											  "Quantidade",
		  											  "Prazo&nbsp;<br />&nbsp;&nbsp;Restante")
	  		}else {
		  		set lstHeader = lstHeader_$listbuild(,"Código",
		  											  "Produto",
		  											  "Local",
		  											  //"Programa",
		  											  "Unidade",
		  											  "Quantidade",
		  											  "Prazo&nbsp;<br />&nbsp;&nbsp;Restante",
		  											  "DMM",
		  										  	  "Cobertura de <br />&nbsp;&nbsp;Estoque")
	  		}
	  	}
 	}
 	else{
	  	set lstHeader = lstHeader_$listbuild(,"Código",
	  										  "Produto",
	  										  "Local",
	  										  "Lote",
	  										  "Validade",
	  										  //"Programa",
	  										  "Marca",	  										  
	  										  "Quantidade",
	  										  "Unidade",
	  										  "Endereço",
	  										  "Prazo&nbsp;<br />&nbsp;&nbsp;Restante")
 	}
 		 	
  	set count = 0
  	
  	if $$Start^COMTable(lstHeader) {
				   
		while (object.Next()) {

			set Item 	   = object.GetData(1)
			set Location   = object.GetData(2)
			set Storage    = object.GetData(3)
			set Quantity   = object.GetData(4)
			set Lot	       = object.GetData(5)
			set ExpiryDate = object.GetData(6)
			set idPrograma = object.GetData(7)
			set idMarca    = object.GetData(8)
			set CustoMedio = object.GetData(9)
			set CustoMedio = $NORMALIZE(CustoMedio,2)
			set ValorTotal = Quantity * CustoMedio
			
			//Item Details
			set objItem   = $get(^INART(YM,Item,1))
			set ItemDesc  = $piece(objItem,Y,1)
			set UnitCode  = $piece(objItem,Y,40)
			set UnitDesc  = $piece($piece($get(^WWW101(YM,"EINHEIT",SPRACHE,UnitCode,1)),Y,1)," - ",1)
			set ItemGroup = $piece(objItem,Y,30)
			
			//Storage Details
			set objStorage = $get(^INLP(YM,Location,Storage,1))
			set blnDUC     = $piece(objStorage,Y,21)
					
			set descStorage = Storage
			
			//DMM e Cobertura Estoque(dias)	
			if (flterLocation = "") { ; da rede	
				set dmm				 = $$GetDM^VARReposicao(Item)
				set coberturaEstoque = $$GetCoberturaEstoque^VARReposicao(Item)		
			} else {
				set dmm 			 = $$GetDM^VARReposicao(Item,Location)
				set coberturaEstoque = $$GetCoberturaEstoque^VARReposicao(Item,Location)
			}
				
			if (blnDUC = $$$YES) { //Se o endereco é um DUC
				set physicalStorage = $piece(objStorage,Y,22)
				set descStorage = physicalStorage_" ["_Storage_"]"
			}
			
			//Programa e Marca Details
			set nomePrograma = $$GetProgramName^VARSQL(idPrograma)
			set nomeMarca    = $$GetBrandName^VARSQL(idMarca)
			
			//Filtering
			if flterGroup '= "" continue:(ItemGroup '= flterGroup)

			set count = $i(count)
			
			set intNoDaysAVencer = 0
			set intNoDaysAVencer = ExpiryDate - $piece($horolog,",",1)
			
			set strImg = $$GetImg(intNoDaysAVencer)
			
			if intNoDaysAVencer >= 0 {
				do NewLine^COMTable()
			} else {
				do NewLine^COMTable("navajowhite")
			}
			
			set key = Item_","_Location			
			
		    do InsertCell^COMTable(strImg)	    	
	    	do InsertCellCallback^COMTable(Item,"GoToVARStock^VARExpiryAlert",key)
	    	//do InsertCellCallback^COMTable(ItemDesc,"GoToVARStock^VARExpiryAlert",key)
	    	do InsertCell^COMTable(ItemDesc,,,,,,,,,$$$YES)
	    	do InsertCell^COMTable($$SQLGetLocationName^VARSQL(Location),,,,,,,,,$$$YES)
	    	
	    	if (flFormat = 2) { // Formato físico
	    		do InsertCell^COMTable(Lot)
	    		do InsertCell^COMTable($zdate(ExpiryDate,4))
	    		//do InsertCell^COMTable(nomePrograma)
	    		do InsertCell^COMTable(nomeMarca)
		    	do InsertCell^COMTable($$^WWWTR(0,12,Quantity),,,,"RIGHT")
	    		do InsertCell^COMTable(UnitDesc,,,,"CENTER")
	    		do InsertCell^COMTable(descStorage)
	    	}
	    
	    	if (flFormat = 1){
		    	if showPhysical=1 {
			    	do InsertCell^COMTable(Lot)
	    			do InsertCell^COMTable($zdate(ExpiryDate,4))
		    	}
		    	do InsertCell^COMTable(UnitDesc,,,,"CENTER")
		    	do InsertCell^COMTable($$^WWWTR(0,12,Quantity),,,,"RIGHT")
	    	}
	    	
	    	if intNoDaysAVencer >= 0 {
	    		do InsertCell^COMTable($$^WWWTR(0,12,intNoDaysAVencer)_" dia(s)",,,,"RIGHT")
	    	} else {
	    		do InsertCell^COMTable("<font color=red><strong>Vencido há "_$$^WWWTR(0,12,intNoDaysAVencer*-1)_" dia(s)</strong></font>",,,,"RIGHT")		    	
	    	}
	    	
	    	if ( (flFormat = 1) && (showPhysical '= 1) ) {
		    	do InsertCell^COMTable($$^WWWTR(0,12,dmm),,,,"RIGHT")
		    	do InsertCell^COMTable($$^WWWTR(0,12,coberturaEstoque),,,,"RIGHT")
	    	}

		 	;Petik 05/08/2010
		 	;Se o parâmetro de cliente Mostrar Valor no Alerta de Validade for igual a SIM
		 	;mostra duas colunas a mais (Custo Médio e Valor Total)
		 	if (flFormat = 2) { // Formato físico
			 	if $$getMostrarValorAlertaValidade^VARParametroCliente(YM) {
			 	
					;Se o Custo Médio possuir mais de dois decimais mostra todos,
					;caso contrário formata para duas casas decimais
					if ($length($piece(CustoMedio,".",2)) > 2) {
						do InsertCell^COMTable($$^WWWTR(0,12,CustoMedio),,,,"RIGHT")
					}
					else{
						do InsertCell^COMTable($$^WWWTR(0,8,CustoMedio,2),,,,"RIGHT")
					}
				
					do InsertCell^COMTable($$^WWWTR(0,8,ValorTotal,2),,,,"RIGHT")
				
			 	}
		 	}
	    	do EndLine^COMTable()	
		}
		
		if count = 0 {
			do NewLine^COMTable()
		    do InsertCell^COMTable("")
		    if $$getMostrarValorAlertaValidade^VARParametroCliente(YM) {
		    	do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,12)
		    } else {
			    do InsertCell^COMTable("Nenhum registro encontrado.",,,,,,9)
		    }
	    	do EndLine^COMTable()		
		}
		
		do Stop^COMTable()
		do object.Close()		
  	}
  	
	quit
	
GetImg(pintNoDaysAVencer)
	new alertHint, img, strImg

	set alertHint = "Atenção: resta(m) "_intNoDaysAVencer_" dia(s) para este produto vencer."

	if (pintNoDaysAVencer < 0) {
		set strImg = ""
	
	} elseif ((pintNoDaysAVencer >= 0) && (pintNoDaysAVencer <= 30)) {
		set img = "alertRed.gif"
		set strImg = "<IMG SRC="_YGIF_img_" TITLE='"_alertHint_"' border=0 align=""center"" >"
	
	} elseif ((pintNoDaysAVencer > 30) && (pintNoDaysAVencer <= 60)) {
		set img = "alertYellow.gif"
		set strImg = "<IMG SRC="_YGIF_img_" TITLE='"_alertHint_"' border=0 align=""center"" >"
	
	} elseif ((pintNoDaysAVencer > 60) && (pintNoDaysAVencer <= 90)) {
		set img = "alertBlue.gif"
		set strImg = "<IMG SRC="_YGIF_img_" TITLE='"_alertHint_"' border=0 align=""center"" >"
	
	} else {
		set strImg = ""
	}
	
	quit strImg

OnFromExpiryBlur(YFELD)
	if (YINHALT '= "") {
		set %TXT(1) = "#Y"_YFORM_"M4~#Y"_YFORM_"M5~"
	}
	quit

OnToExpiryBlur()
	;do OnFromExpiryBlur("")
	quit

GoToVARStock(pidItem,pidLocal)
	new externalKey
	set externalKey = "External"_$$$COMMA_pidItem_$$$COMMA_pidLocal
		
 	do GoToForm^COMUtilForm("VARStock",externalKey,,,$$$YES)

	quit
	
Print()

	do RunReportAlertaValidade^VARJasperRunReport()
	
	quit
]]></Routine>
</Export>