<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARBRDiretorios" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARBRDiretorios
 
 
 ;============================================================
 ;
 ; RETORNA O DIRETÓRIO FÍSICO CADASTRADO PARA O MÓDULO
 ; CONCATENANDO COM O DIRETÓRIO PAI, SE EXISTIR
 ;
 ;============================================================
GetDiretorioFisico(pEmpresa, pModulo)
	;$$GetDiretorioFisico^VARBRDiretorios(Empresa,Modulo)

	new DirResult
	
	set DirResult = $$GetDiretorio(pEmpresa,pModulo,1)	;1=Físico 

	quit DirResult


 ;============================================================
 ;
 ; RETORNA O DIRETÓRIO VIRTUAL (WEB) CADASTRADO PARA O MÓDULO
 ; CONCATENANDO COM O DIRETÓRIO PAI, SE EXISTIR
 ;
 ;============================================================
GetDiretorioVirtual(pEmpresa, pModulo)
	;$$GetDiretorioVirtual^VARBRDiretorios(Empresa,Modulo)

	new DirResult
	
	set DirResult = $$GetDiretorio(pEmpresa,pModulo,2)	;2=Virtual 

	quit DirResult

	
	
 ;============================================================
 ;
 ; RETORNA O DIRETÓRIO CADASTRADO PARA O MÓDULO
 ; CONCATENANDO COM O DIRETÓRIO PAI, SE EXISTIR
 ; 
 ; o parâmetro pTIPO define se deve retornar o 
 ; diretório Físico ou o Virtual (Web)
 ;
 ;============================================================
GetDiretorio(pEmpresa, pModulo, pTipo=1)
	;$$GetDiretorio^VARBRDiretorios(Empresa,Modulo,pTipo)
	;
	;O método foi feito para apenas um nível,
	;se futuramente for necessário vários níveis PAI
	;esse método precisa ser corrigido.
		
	new DirResult,Dir,idPai,DirPai
	new objDir,objDirPai
	
	set DirResult = "" 
	
	if (pEmpresa="") || (pModulo="") {
		quit DirResult
	}
	
	if pTipo=2 {
		set pTipo=3		;piece 3
	}
	
	;Isso é necessário pois esse método está
	;sendo usado também em arquivos .csp
	;vvvvv
	if $G(Y) = "" {
		set Y = "~"
	}
	
	set objDir = $G(^VARBRDiretorios(pEmpresa, pModulo, 1))
	if objDir '= "" {
		set Dir   = $piece(objDir,Y,pTipo)
		set idPai = $piece(objDir,Y,4)
		if idPai = "" {
			set DirResult = Dir
		}
		else{
			set objDirPai = $G(^VARBRDiretorios(pEmpresa, idPai, 1)) 
			if objDirPai '= "" {
				set DirPai    = $piece(objDirPai,Y,pTipo)  
				if ($extract(DirPai, $length(DirPai)) '= "\") {
					set DirPai = DirPai _ "\"
				}				
				set DirResult = DirPai_Dir
			}

		}
			
	}

	quit DirResult	
]]></Routine>
</Export>