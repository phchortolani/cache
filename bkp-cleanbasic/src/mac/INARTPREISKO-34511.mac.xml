<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTPREISKO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTPREISKO
	
#include COMSYS
#include INConst
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^INARTPREISKO("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		KONTROLL-STATISTIK ARTIKELPREISE
	;
	; Inputs : 
	;	VORG(1)		: Item No
	;	VORG(2)		: 
	;   YUSER		:
	; 
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 13-Jul-2005	GRF		SR12863 : doco
	; 08.05.2002	FIS
	;-------------------------------------------------------------------------------
	SET VORG(1)=$GET(VORG(1))
	IF VORG(1)=""                 DO ^WWWINFO($$^WWWTEXT(32310)) QUIT                             ; "No Selection Made"
	IF '$DATA(^INART(YM,VORG(1))) DO ^WWWINFO($$^WWWTEXT(32292)_" ("_$$^WWWTEXT(32024)_")") QUIT  ; "Check Please! (Item)"
	
	SET VORG(2)=+$GET(VORG(2))
	KILL ^WWWSOR(YUSER)
	DO SORT
	DO DRUCK
	KILL ^WWWSOR(YUSER)
	QUIT
	
	
MANU(ART) ;EINSPRUNG AUS FORMULAR ;out of form 
	;-------------------------------------------------------------------------------
	; Show Costs - Called from INART Button - 'Item Price Statistics'
	; 
	; History :
	; 10-Nov-2009	GRF		SR16871: INItemCost replaces INARTKOST
	; 28-Oct-2009	GRF		SR16871: INAvgCost with cost centre replaces INARTAVG
	; 14-Apr-2009	PPP		SR16482: Average Cost History to be accessed from Item 
	; 							Master
	;-------------------------------------------------------------------------------
	new idCostCentre,VORG
	
	$$$LogR("MANU",ART)
	
	set VORG(1) = $get(ART)
	do ^INARTPREISKO
 
	; Show Average Cost Calculations
	;---------------------------------------
	;if ($get(ART)'="") && ($data(^INARTAVG(YM,ART))) { //SR16482   ; SR16871 vvv
	;	do ShowAverages^INStockValAdjust(ART)
	;}
	
	set idCostCentre = $$GetCostCentre^INCostCentre($get(YLOCATION))
	if ($get(ART)'="") && ($data(^INAvgCost(0,ART,idCostCentre))) {
		do ShowAverages^INStockValCCAdj(ART,idCostCentre)
	}
	
	quit
	
	
SORT   ;SUCHEN ARTIKELTEILE LT. ARTIKEL-STAMMDATEN ;seek 
	NEW TEIL,YFELD,YEBENE,YDATEI
	
	$$$LogR("SORT","")
	
	; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; Data Records :
	;	YFELD			objINART			Item Masterfile
	; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
	SET YFELD=$GET(^INART(YM,VORG(1),1))
	QUIT:YFELD=""
	
	SET YEBENE=0  ;TEILE-EBENE
	SET $PIECE(YFELD,Y,39)=1  ;MENGE ;quantity 
	SET ^WWWSOR(YUSER,1,YEBENE,0,VORG(1))=YFELD  ;DATENSATZ ;data record 
	SET ^WWWSOR(YUSER,2,VORG(1))=1  ;ARTIKELMENGE
	
	;ARTIKELTEILE
	IF $DATA(^INARTXL(YM,VORG(1))) DO
	. SET YDATEI="^INARTXL("_""""_YM_""""_","_""""_VORG(1)_""""_")"
	. FOR  DO  QUIT:YDATEI=""
	. . SET YDATEI=$QUERY(@YDATEI)
	. . ;IF $PIECE(YDATEI,",",2)'=VORG(1) SET YDATEI="" QUIT   ;02.07.2004;FAN;alphanumerische Aufträge ;25888  ;25794
	. . IF $TR($PIECE(YDATEI,",",2),"""")'=VORG(1) SET YDATEI="" QUIT   ;02.07.2004;FAN;alphanumerische Aufträge ;25888  ;25794
	. . SET YFELD=@YDATEI
	. . QUIT:$PIECE(YFELD,Y,4)=""  ;ARTIKEL ;item 
	. . SET TEIL=$PIECE(YDATEI,",",3,99)
	. . SET TEIL=$TRANSLATE(TEIL,",)","..")
	. . ;SET YEBENE=$LENGTH(TEIL,".")-1
	. . SET YEBENE=0
	. . ;
	. . ;ERMITTELN GESAMTMENGE ;find 
	. . IF $PIECE(YFELD,Y,26)'=2 DO
	. . . NEW YII,MENGE
	. . . SET MENGE=$PIECE(YFELD,Y,5)
	. . . IF +MENGE=0 SET MENGE=1
	. . . FOR YII=2:1:$LENGTH(TEIL,".")-1  DO
	. . . . SET TEIL1="^INARTXL("_""""_YM_""""_","_""""_VORG(1)_""""_","_$TRANSLATE($PIECE(TEIL,".",1,$LENGTH(TEIL,".")-YII),".",",")_")"
	. . . . QUIT:'$DATA(@TEIL1)
	. . . . SET MENGE=MENGE*$PIECE(@TEIL1,Y,5)
	. . . ;
	. . . SET $PIECE(YFELD,Y,5)=MENGE
	. . ;
	. . SET $PIECE(YFELD,Y,39)=$PIECE(YFELD,Y,5)
	. . SET ^WWWSOR(YUSER,1,YEBENE,TEIL,$PIECE(YFELD,Y,4))=YFELD  ;EBENE,TEILEKEY,ARTIKEL
	. . SET ^WWWSOR(YUSER,2,$PIECE(YFELD,Y,4))=$GET(^WWWSOR(YUSER,2,$PIECE(YFELD,Y,4)))+(+$PIECE(YFELD,Y,39))  ;MENGE ;quantity 
	
	QUIT
	
	;--------------------------------------------------------------------------------
	;                       Item Master Data (ItemId - ItemDesc)                      KOPF
	;--------------------------------------------------------------------------------
	;Item|Qty|Price1|Price2|Price3|Price4|Price5|Price6|Price7|Price8|Price9|Price10|
	;--------------------------------------------------------------------------------
	;    |   |      |      |      |      |      |      |      |      |      |       | DRUCK (**)
	;--------------------------------------------------------------------------------
	; Price 1 = Cost Of Ordered Items                         "HERSTELLKOSTEN"        DRUCK (***)
	; Price 2 = Manufacturing Costs 
	; Price 3 = Lowest Cost 
	; Price 4 = Highest Cost
	; Price 5 = Highest Manufacturing Costs Incl. Stock 
	; Price 7 = First In, First Out
	; Price 8 = Last In, First Out
	; Price 9 = Average Cost 
	; Price 10 = Fixed Costs
	; 
	; Average Cost History                                                      ShowAverages^INStockValAdjust
	;---------------------------------------------------------------------------
	;Items per page: [  ] 1  2  3  4
	;---------------------------------------------------------------------------
	;Date|Time|Quantity|Incoming |   Current  | Incoming |    New     | Source |
	;    |    |On Hand |Goods Qty|Average Cost|Goods Cost|Average Cost|        |
	;---------------------------------------------------------------------------
	;---------------------------------------------------------------------------
	;---------------------------------------------------------------------------
	
DRUCK   ;DRUCKEN PREISE ;print 
	NEW ART,YEBENE,TEIL,YFELD,YI,KOSTENART,YDDSATZ,GESAMT
	
	$$$LogR("DRUCK","")
	$$$LogRm(^WWWSOR(YUSER))
	
	;ERMITTELN KOSTENARTEN ;find types of costs 
	;+++++++++++++++++++++++++++++++++++++++
	;          "HERSTELLKOSTEN"
	; e.g.
	;	Price 1 = Cost Of Ordered Items 
	;	Price 2 = Manufacturing Costs 
	;	Price 3 = Lowest Cost 
	;	Price 4 = Highest Cost
	;	Price 5 = Highest Manufacturing Costs Incl. Stock 
	;	Price 7 = First In, First Out
	;	Price 8 = Last In, First Out
	;	Price 9 = Average Cost 
	;	Price 10 = Fixed Costs
	;+++++++++++++++++++++++++++++++++++++++
	
	SET YI=""
	FOR  SET YI=$ORDER(^WWW101(0,"HERSTELLKOSTEN",SPRACHE,YI)) QUIT:YI=""  DO
	. SET KOSTENART(YI)=$PIECE($GET(^WWW101(0,"HERSTELLKOSTEN",SPRACHE,YI,1)),Y,1)  ;BEZEICHNUNG ;notation 
	
	SET TEILE=0
	IF $ORDER(^WWWSOR(YUSER,1,0,0))'="" SET TEILE=1  ;MIT UNTERTEILEN ;by means of 
	DO ^WWWUP(0)
	DO ^WWWBACK
	
	DO KOPF
	
	;---------------------------------------
	; Price information (**) Main Item and components
	;---------------------------------------
	
	SET YDDSATZ=0
	SET YEBENE=""
	FOR  SET YEBENE=$ORDER(^WWWSOR(YUSER,1,YEBENE)) QUIT:YEBENE=""  DO
	. SET TEIL=""
	. FOR  SET TEIL=$ORDER(^WWWSOR(YUSER,1,YEBENE,TEIL)) QUIT:TEIL=""  DO
	. . SET ART=""
	. . FOR  SET ART=$ORDER(^WWWSOR(YUSER,1,YEBENE,TEIL,ART)) QUIT:ART=""  DO
	. . . SET YFELD=$GET(^WWWSOR(YUSER,1,YEBENE,TEIL,ART))
	. . . ;
	. . . DO NL^WWWTAB
	. . . IF TEILE=1 DO
	. . . . DO NF^WWWTAB
	. . . . WRITE "<FONT SIZE=2>"
	. . . . IF TEIL=0  WRITE "-"       ;HAUPTTEIL
	. . . . IF TEIL'=0 WRITE TEIL      ;TEILESTRUKTUR
	. . . . WRITE "&nbsp;",YCR
	. . . . DO EF^WWWTAB
	. . . ;
	. . . DO NF^WWWTAB
	. . . WRITE "<FONT SIZE=2>"
	. . . IF TEIL=0 WRITE "<B>"
	. . . WRITE ART                                  ;ARTIKEL     ;item 
	. . . WRITE "&nbsp;"
	. . . WRITE $EXTRACT($PIECE(YFELD,Y,1),1,25)     ;BEZEICHNUNG ;description 
	. . . WRITE "&nbsp;",YCR
	. . . DO EF^WWWTAB
	. . . ;
	. . . DO NFR^WWWTAB
	. . . WRITE "<FONT SIZE=2>"
	. . . IF TEIL=0 WRITE "<B>"
	. . . SET GESAMT=$PIECE($GET(^WWWSOR(YUSER,2,ART)),Y,1)
	. . . IF TEIL'=0 IF +GESAMT'=0 WRITE " ("_$$^WWWZAHL(GESAMT,0,$LENGTH($PIECE(GESAMT,".",2)))_") "                     ;GESAMTMENGE
	. . . ;
	. . . IF +$PIECE(YFELD,Y,39)'=0 WRITE $$^WWWZAHL($PIECE(YFELD,Y,39),0,$LENGTH($PIECE($PIECE(YFELD,Y,39),".",2)))_" "  ;MENGE ;quantity 
	. . . IF $PIECE(YFELD,Y,40)'="" WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(YFELD,Y,40),1)),Y,1)             ;LEISTUNG ;performance 
	. . . WRITE "&nbsp;",YCR
	. . . DO EF^WWWTAB
	. . . ;
	. . . SET YI=""
	. . . FOR  SET YI=$ORDER(KOSTENART(YI)) QUIT:YI=""  DO
	. . . . DO NFR^WWWTAB
	. . . . WRITE "<FONT SIZE=2>"
	. . . . IF YI=$PIECE($GET(^INVORG(YM,YM,1)),Y,15) WRITE "<B>"
	. . . .;WRITE $$^WWWZAHL($$^INARTKOST(ART,$PIECE(YFELD,Y,39),YI))  ;PREIS JE KOSTENART ;price once   ; SR16871
	. . . . write $$^WWWZAHL($$GetCost^INItemCost(ART,$get(YLOCATION),YI))
	. . . . WRITE "&nbsp;",YCR
	. . . . DO EF^WWWTAB
	. . . ;
	. . . DO EL^WWWTAB
	
	DO STOP^WWWTAB
	
	;---------------------------------------
	; Price legend (***)
	;---------------------------------------
	
	DO
	. NEW YDDSATZ
	. WRITE YCR
	. DO START0^WWWTAB
	. WRITE YCR
	. DO NL^WWWTAB
	. DO NF^WWWTAB
	. SET YI=""
	. FOR  SET YI=$ORDER(KOSTENART(YI)) QUIT:YI=""  DO
	. . WRITE "<FONT SIZE=2>"
	. . WRITE $$^WWWTEXT(32993)_" "_YI_" = "_$GET(KOSTENART(YI))  ; "Price"
	. . WRITE "<BR>",YCR
	. ;
	. DO EF^WWWTAB
	. DO EL^WWWTAB
	. WRITE YCR
	. DO STOP^WWWTAB
	. WRITE YCR
	
	DO ^WWWUP(1)
	QUIT
	
KOPF   ;TABELLENÜBERSCHRIFT
	WRITE "<CENTER>"
	DO START00^WWWTAB
	DO NL^WWWTAB
	DO NHWO^WWWTAB
	WRITE "<FONT SIZE=3>"
	WRITE "<B>"
	WRITE "<NOBR>"
	WRITE YKOPF
	WRITE " ("_VORG(1)_" - "_$PIECE($GET(^INART(YM,VORG(1),1)),Y,1)_")"    ; (ItemID - Item Description)
	WRITE "</NOBR>"
	WRITE "</B>"
	DO EH^WWWTAB
	DO EL^WWWTAB
	DO STOP^WWWTAB
	WRITE "</CENTER>"
	
	;---------------------------------------
	
	WRITE "<BR>"
	DO START100^WWWTAB  ;STARTEN DER ÜBERSCHRIFT (TABELLENANFANG) ;launching the superscription 
	WRITE "<THEAD>"
	DO NL^WWWTAB
	DO NH^WWWTAB
	IF TEILE=1 DO
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWTEXT(32085)     ;TEILESTRUKTUR		; "Level Structure  "
	. WRITE "&nbsp;",YCR
	. DO EH^WWWTAB
	. DO NHW^WWWTAB
	
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(32024)       ;ARTIKEL 				; "Item"
	WRITE "&nbsp;",YCR
	DO EH^WWWTAB
	
	DO NHW^WWWTAB
	WRITE "<FONT SIZE=2>"
	WRITE $$^WWWTEXT(31407)       ;MENGE 				; "Quantity"
	WRITE "&nbsp;",YCR
	DO EH^WWWTAB
	
	SET YI=""
	FOR  SET YI=$ORDER(KOSTENART(YI)) QUIT:YI=""  DO
	. DO NH^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWTEXT(32993)_" "_YI  ;PREIS			; "Price"
	. WRITE "&nbsp;"
	. WRITE YCR
	. DO EH^WWWTAB
	
	DO EL^WWWTAB
	WRITE "</THEAD>"
	QUIT
]]></Routine>
</Export>