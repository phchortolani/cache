<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINWEINVP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINWEINVP
#include COMSYS
#include INConst
	quit

OnBeforeSaveCustom()
	new idDoEndereco, idAteEndereco
	
	set idDoEndereco = $$$INWEINVPFromStockLocation($$$GetFormData(YFORM,"D"))
	if ((idDoEndereco '= "") && ('$data(^INLP(YM, YLOCATION, idDoEndereco,1)))) {
		quit $$$MakeStatus("%1","Campo ""Do endereço"" inválido.")
	}
	set idAteEndereco = $$$INWEINVPToStockLocation($$$GetFormData(YFORM,"D"))
	if ((idAteEndereco '= "") && ('$data(^INLP(YM, YLOCATION, idAteEndereco,1)))) {
		quit $$$MakeStatus("%1","Campo ""Até o endereço"" inválido.")
	}
	
	quit $$$OK

IsCentral(pidLocal)
	if ((pidLocal = 1)
		|| (pidLocal = 5)
		|| (pidLocal = 5070)) {
		quit $$$YES
	}
	quit $$$NO

CheckStorage(pidStorage, pidLocal)
	quit:(($length($get(pidStorage)) = 0) || ($length($get(pidLocal)) = 0)) $$$NO
	if ('($data(^INLP(YM, pidLocal, pidStorage, 1))) || (pidStorage = "EmTransito")) {
		$$$Alert("Endereço inválido.")
		quit $$$YES
	}
	quit $$$NO

OnBeforeEditAcessHook()
	if ($$SQLGetTipoEstoqueLocal^VARSQL(YLOCATION) = "CENTRODECUSTO") {
		$$$Alert("O local "_YLOCATION_" é um Centro de Custo e não possui permissão para cadastrar inventário.")
		quit $$$NO
	}
	quit $$$YES

CanCreateList(pidStocktake)
	quit:$$$NoKey(pidStocktake)
	new strStatus
	do CanCreateList^INWEINVP1(pidStocktake)
	set strStatus = $$$OK
	
	if ('$$$ISOK(YQ)) {
		set objStocktake = $get(^INWEINVP(YM,pidStocktake,1))
		
		if $$$ISOK(strStatus) {
			if $$IsLockedSiteLocation^INLocking($$$INWEINVPSiteLocation(objStocktake)) {        ; BR014730
				set strStatus = $$ReasonLocked^INLocking($$$INWEINVPSiteLocation(objStocktake)) ;15570
			}			
		}
	
		if $$$ISOK(strStatus) {
		
			set idSite = $$$INWEINVPSiteLocation(objStocktake)
			set blCheckTransfers = $$$NO
			//Transferências em aberto sem estarem revertidas
			set idTransfer = $order(^INTFRs(YM,3,idSite,2,""))
			while ($length(idTransfer) > 0) {
				//Check if this transfer was reversed
				set objTransfer = ^INTFR(0,idTransfer,1)
				if ('$data(^INTFRRev(YM,idTransfer))) {
					set blCheckTransfers = $$$YES
					quit
				}
				elseif ($$CustomReversalStatus^VARINTFR(idTransfer) '= 1) { // verifica se foi totalmente estornada
					set YQ = $$$YQEnable
					do CanCreateReversal^INTFRButtons(idTransfer)
					if (YQ) {
						set blCheckTransfers = $$$YES
						quit
					}
				}
				set idTransfer = $order(^INTFRs(YM,3,idSite,2,idTransfer))
			}
		
			if (blCheckTransfers) {
				set strStatus = $$$MakeStatus("%1", "Não é possível realizar a ação pois existem aceites pendentes para esse local. Favor clicar em Aceite de Transferência e processar todos os aceites pendentes para poder então gerar a fotografia de estoque de inventário.")
			}
		
			if $$$ISOK(strStatus) {
				//Transferências enviadas, mas não totalmente aceitas
				set idTransfer = $order(^INTFRs(YM,3,idSite,3,""))
				while ($length(idTransfer) > 0) {
					if ('$$isTotalmenteAceita^VARINTFR(idTransfer)) {
						set blCheckTransfers = $$$YES
						quit
					}
					set idTransfer = $order(^INTFRs(YM,3,idSite,3,idTransfer))
				}
		
				if (blCheckTransfers) {
					set strStatus = $$$MakeStatus("%1", "Não é possível realizar a ação pois existem aceites pendentes para esse local. Favor clicar em Aceite de Transferência e processar todos os aceites pendentes para poder então gerar a fotografia de estoque de inventário.")
				}
			}
	
		}
	
		$$$YQHandler(strStatus)
	}
	quit
]]></Routine>
</Export>