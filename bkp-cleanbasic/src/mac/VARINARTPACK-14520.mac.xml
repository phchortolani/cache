<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINARTPACK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINARTPACK
	#include COMSYS
	#include INConst
	#include WWWConst
	#import VAR.infra.integracao
	#import VAR.infra.util
	quit

OnAfterSaveHook(pYKEY)
	if (+$$IsInventoryUnit^INARTPACK(pYKEY) = 1) quit $$$OK
	if (+$$IsFlagNaoIntegrarProduto(pYKEY) = 1) quit $$$OK

 	do ##class(GerenciaFilaPendencias).RegistraEvento(
		##class(GerenciaFilaPendencias).%GetParameter("ENTIDADEUNIDADEMEDIDA"),
		$$$KEY1(pYKEY),
		$$$KEY2(pYKEY),
		##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
		##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
		##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
		##class(GerenciaFilaPendencias).%GetParameter("ACAOSALVAR"))
	quit $$$OK


OnBeforeDeleteHook(pYKEY,pYFELD)
	if (+$$IsInventoryUnit^INARTPACK(pYKEY) = 1) quit $$$OK
	if (+$$IsFlagNaoIntegrarProduto(pYKEY) = 1) quit $$$OK
	if (+$$OnBeforeDeleteHook^INARTPACK(pYKEY,pYFELD) '= 1) quit $$$OK

 	do ##class(GerenciaFilaPendencias).RegistraEvento(
		##class(GerenciaFilaPendencias).%GetParameter("ENTIDADEUNIDADEMEDIDA"),
		$$$KEY1(pYKEY),
		$$$KEY2(pYKEY),
		##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
		##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
		##class(GerenciaFilaPendencias).%GetParameter("CHAVEVAZIA"),
		##class(GerenciaFilaPendencias).%GetParameter("ACAOEXCLUIR"))
	quit $$$OK
	
	
CheckReferedUnits(pidItem,pidUnit,&parrReferedUnits,&pintTotalInv=1,pidUnitRef="")

	new idOuterUnit,objPack
	
	set idOuterUnit = ""
	
	if (pidUnitRef="") {
		for {
			set idOuterUnit = $order(^INARTPACK(0,pidItem,idOuterUnit))
			quit:idOuterUnit=""

			set objPack = $get(^INARTPACK(0,pidItem,idOuterUnit,1))
			if (($$$INARTPACKUnit(objPack) = pidUnit) && 
					('$$IsInventoryUnit^INARTPACK(pidItem_","_idOuterUnit))) {
				set parrReferedUnits(pidItem,idOuterUnit) = $$$INARTPACKQuantity(objPack)
				set pintTotalInv = pintTotalInv * $$$INARTPACKQuantity(objPack)
				do CheckReferedUnits(pidItem,idOuterUnit,.parrReferedUnits,.pintTotalInv)
			}
		}
	}
	else {
		set idOuterUnit = pidUnitRef
		for {
			quit:idOuterUnit=pidUnit
			set objPack = $get(^INARTPACK(0,pidItem,idOuterUnit,1))
			if (objPack '= "") {
				set parrReferedUnits(pidItem,idOuterUnit) = $$$INARTPACKQuantity(objPack)
				set pintTotalInv = pintTotalInv * $$$INARTPACKQuantity(objPack)
				set idOuterUnit = $$$INARTPACKUnit(objPack)
			}
			else {
				quit
			}
		}
	}
	
	quit
	
IsFlagNaoIntegrarProduto(pYKEY)
	quit:($$$KEY1(pYKEY)="") $$$YES
	
	set blnNaoIntegrar = $$$NO
	
	if ( (##class(VAR.infra.services.ServiceRunner).IsProductionComputer()) && (##class(Util).GetNamespace() = "HEVA") ){
		$$$VAR
		
		set blnNaoIntegrar = $$$YES
	
		set codProduto = $$$KEY1(pYKEY)
		set objProduto = $get(^INART(YM,codProduto,1))
	
		if ($$$INARTFREE45(objProduto) = $$$YES){
			set blnNaoIntegrar = $$$NO
		}
	}
	
	quit blnNaoIntegrar
]]></Routine>
</Export>