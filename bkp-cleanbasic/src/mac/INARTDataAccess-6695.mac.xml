<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTDataAccess" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTDataAccess
	
#include COMSYS
#include INConst
#include INItemStatus
	
 	/*26-May-2010	PPP		SR17309: Updated with Item Status (Supersedes previous version)
IsUsable1(pidItem,pidForm)
	;-------------------------------------------------------------------------------
	; Determine whether item allowed to be used
	;
	; Params:
	; pidItem - Idtem Id 
	; pidForm - Form Id
	;
	; ByRefs: None
	;
	; Returns: strStatus - $$$OK if allowed to use, otherwise error message
	;
	; History:
	; 18-Jul-2006	PO		SR14847: Changed from returning $$$NO to error message
	; 28-Apr-2006	PO		SR14427: Created
	;-------------------------------------------------------------------------------
	new objItem,strStatus
 
	set strStatus = $$$OK
 
	if $case(pidForm,"INTFRLine":$$$YES,:$$$NO) {
		set objItem = $get(^INART(YM,pidItem,1))
		if $$$INARTItemWithoutInventoryMaint(objItem) {
			set strStatus = $listbuild("IN00445")
		} ; "Non stock items can not be transferred."
	}
	quit strStatus
	*/


IsUsable(pidItem,pidForm)
	;-------------------------------------------------------------------------------
	; Whether an Item can be used at this time.
	;
	; Params:	pidItem, pidForm
	; pidItem - Idtem Id 
	; pidForm - Form Id
	;
	; ByRefs: None
	;
	; Returns: strStatus - $$$OK if allowed to use, otherwise error message
	;
	; History:
	; 21-Oct-2013	shobby	HEVA-1148.23: Include VARManipulacaoMagistral.
	; 20-Sep-2011	SCR		SR17895: Revomed Exception for Stocktake
	; 12-Aug-2010	PPP		SR17296: Added Exceptions for 'Stocktake'
	; 12-Jul-2010	PPP		SR17309-1: Added Return To Supplier
	; 26-May-2010	PPP		SR17309: Updated with Item Status (Superseeds previous version)
	; 18-Jul-2006	PO		SR14847: Changed from returning $$$NO to error message
	; 28-Apr-2006	PO		SR14427: Created
	;-------------------------------------------------------------------------------
	new idForm,idStatus,intField,objItem,strPre,strStatus
	
	set strStatus = $$$OK
	
	set idForm = $zcvt(pidForm,"u")
	
	if (+$$$WWWClientParamCoreChangesSESDF($get(^WWWClientParam(YM,YM,1)))) {
		if ((pidForm = "VARInventarioMultDigLinha") || (pidForm = "INWEINVELINE")) {
			quit $$$OK
		}
	}
	
	if ($get(pidItem)'="") && (($extract(pidForm,1,2) = "IN")||($extract(pidForm,1,3)="VAR")) {
		set idStatus = $$$INARTHoldReason($get(^INART(0,pidItem,1)))
		// Only if Item Status exists and Item Status Data exists
		if (idStatus '= "") && $data(^INItemStatus(0,idStatus)) {
			set strPre = $extract(pidForm,3,5)

			set intField = $select(	
							idForm [ "INAUF"		: $$$FldINItemStatusPurchaseOrders,		// Purchase Orders
							idForm [ "INPUR"		: $$$FldINItemStatusPurchaseOrders,		// Purchase Orders - New
							idForm [ "INREC"		: $$$FldINItemStatusReceiveGoods,		// PO Receipts
							idForm [ "INMOREC"		: $$$FldINItemStatusReceiveGoods,		// Manufacturing/Production Receipts
							idForm [ "INISS"		: $$$FldINItemStatusIssueGoods,			// Requisition Issues
							idForm [ "INDISPENSE"	: $$$FldINItemStatusIssueGoods,			// Dispense Issues
							idForm [ "INMOISS"		: $$$FldINItemStatusIssueGoods,			// Manufacturing/Production Issue
							idForm [ "VARMANIPULACAOMAGISTRAL"		: $$$FldINItemStatusIssueGoods,			// Manufacturing/Production Issue
							idForm [ "INREQ"		: $$$FldINItemStatusRequisitions,		// Requisitions
							idForm [ "INKIT"		: $$$FldINItemStatusRequisitions,		// Kit Requistions
							idForm [ "INTFR"		: $$$FldINItemStatusTransfers,			// Transfers
							idForm [ "INTRN"		: $$$FldINItemStatusTransfers,			// Transfers
							idForm [ "INADJ"		: $$$FldINItemStatusAdjustments,		// Adjustments
							idForm [ "INRETURNTO"	: $$$FldINItemStatusReturnToSupplier,	// Return To Supplier (SR17309-1)
							1						: $$$FldINItemStatusOther)				// Stocktake, ???
			set strStatus = $$$HasPermission(pidItem,idStatus,intField)
		} ; "IN01230"     "Item %1 does not have permission to: %2~"
	}
	
	if $$$ISOK(strStatus) {
		if (pidForm ["INTFRLine") {
			set objItem = $get(^INART(YM,pidItem,1))
			if $$$INARTItemWithoutInventoryMaint(objItem) {
				set strStatus = $listbuild("IN00445")
			} ; "Non stock items can not be transferred."
		}
	}
	
	//Exceptions
	if $$$ISERR(strStatus) {
		// Stocktake - Create, Perform, Delete
		; SR17895 vvvvv
		;if idForm [ "INWEINV" {
		;	set strStatus = $$$OK
		;}
		; SR17895^^^^^
	}
	quit strStatus
	
]]></Routine>
</Export>