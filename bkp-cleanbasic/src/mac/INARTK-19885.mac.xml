<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTK
#include COMSYS
#include INConst
	do Start
	quit

#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^INARTK("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
Start	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		PRÜFEN LIEFERANTEN KONDITIONEN UND EK-PREISE
	;		EINSPRUNG AUS INARTK(ARTIKELKONDITIONEN)
	;			UND INAUFPA (AB) UND INAUFPK UND INERECH1
	;
	;		EXAMINE SUPPLIERS CONDITIONS AND INPUT CLUTCH PRICES
	;		RE-ENTRY POINT OUT INARTK(ARTIKELKONDITIONEN)
	;			AND INAUFPA (OFF) AND INAUFPK AND INERECH1
	; 
	; Called by Routines :
	;	INSHIPINFOCOSTS
	;	FINAPInvPostCost
	;	
	; Inputs : 
	;	YFELD		Record based on class ......?
	;
	;	YDATEI		YKEY = idKey1,idKey2,idKey3,idKey4
	;  --------------------------------------------------
	;  "INARTK"			Item No, Supplier No
	;  "INAUFPK"		OrderNo, OrderLine						(INAUF, INAUFP)=> Supplier
	;  "INAUFPA"		OrderNo, OrderLine, Supplier, OrderConf
	;  "INANGPK"		QuoteNo, QuoteItem						(INANG, INANGP)=> Supplier
	;  "INERECH1"		OrderNo, OrderLine, Supplier, InvoiceNo
	;	
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 27-Jul-2009	GRF		SR16602: Direct setting of INAUFP bypasses DCM event
	; 							"INAUFPSave" which updates FINAPINVMatchCache
	; 03-Apr-2009	GRF		Key Macros
	; 12-Oct-2006	GRF		Doco
	; 20-Sep-2006	GRF		swap order of IF tests to check class first; use boolean
	; 						macro; FIX ME
	; 25-Aug-2006	GRF		SR12027 : Renamed per coding standards; Doco
	; 13-Jun-2006	GRF		Doco
	; 26-Aug-2005	GRF		Typo correction on piece number
	; 12-Jul-2005	GRF		SR12027 : Doco
	; 23-Jun-2005	GRF		SR12027 : Clarify; !=>||
	; 27.03.2000	DT		Created
	;-------------------------------------------------------------------------------
	new ANZ,idKey1,idKey2,idKey3,idKey4,loop,objOrder,objOrderLine,strStatus
	
	$$$LogR("Start",$get(YKEY)_"<"_$get(YDATEI)_"<"_$get(YFELD))

	QUIT:YFELD=""
	
	set idKey1 = $$$KEY1(YKEY)
	set idKey2 = $$$KEY2(YKEY)
	set idKey3 = $$$KEY3(YKEY)
	set idKey4 = $$$KEY4(YKEY)
	
	; If the key includes supplier information but that supplier doesn't exist,
	; clear the record.
	
	; FIXME : What about INARTK - only goes to idKey2 rather than idKey3 ? Okay(?) if only killing transient records <GRF>
	
	IF idKey2'="" IF idKey1'="" IF idKey3'="" IF '$DATA(^INLIEF(YM,idKey3,1)) DO  QUIT      
	. DO ^WWWSKILL(YDATEI,YKEY)   ; only INAUFPA and INERECH1 have idSupplier as P3
	
	; --------------------------------------
	
	; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	; Data Records :
	;	YART1			objINART			Item Masterfile
	;	KOND1			objINLIEFK			Item Group Conditions (Supplier, Group)
	;	YFELD			objINAUFP			Order Line  (localised use)
	;	YFELD			objINAUFPIMPACT		Order Line Additional Costs
	;	YFELD1			objINAUFP			Order Line  (localised use)
	;	POSX1			objINAUFP			Order Line  (localised use)
	;	SATZ			objINARTK			Item Supplier Conditions
	; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
	;EINZELPREIS FÜR AB/RECHN ;PRICE PER UNIT FOR STARTING FROM/COUNT
	IF YDATEI="INARTK" IF idKey2'="" IF idKey1'="" DO
	. IF +$PIECE(YFELD,Y,12)'=0 DO
	. . set $$$INARTKUnitPrice(^INARTK(YM,idKey1,idKey2,1))=$PIECE(YFELD,Y,12)
	. . set $PIECE(YFELD,Y,47)=$PIECE(YFELD,Y,12)
	. ;
	. IF $PIECE(YFELD,Y,18)=4 DO
	. . NEW YART1,ME
	. . set YART1 = $GET(^INART(YM,idKey1,1))
	. . set ME    = $$$INARTUnitofMeasure(YART1)
	. . ; TODO : Check Units match "EN" list @ AdRad <GRF>   Why just these units?  Why any units?
	. . IF ME'=5 IF ME'=6 IF ME'=12 IF ME'=18 QUIT			;m, m2, m3, mm
	. . QUIT:+$$$INARTWeightUM(YART1)=0
	. . QUIT:+$$$INARTKConversionFactor(YFELD)'=0
	. . set $$$INARTKConversionFactor(^INARTK(YM,idKey1,idKey2,1)) = $$$INARTWeightUM(YART1)
	. . set $$$INARTKConversionFactor(YFELD)                       = $$$INARTWeightUM(YART1)
	
	
	SET ANZ=0
	; FIXME : Appears should have a for loop to be able to check multiple suppliers.
	;         Quite possibly copied from elsewhere & no attempt to clean up code.
	;         ANZ not used elsewhere in this routine.                              <GRF>
	
	DO  ;LIERANTENNUMMER IN ARTIKEL MIT EIGENLEISTUNG FÜR VERLÄNGERTE WERKBANK,ULM,10.01.03
	. IF ANZ>1 DO ^WWWINFO(33271)                ; "Only One Supplier per External Production allowed"   ; Not called - ANZ always 0
	. IF YDATEI="INARTK" IF $PIECE(YFELD,Y,74)=$$$YES IF idKey1'="" IF idKey2'="" DO            ; 20-Sep-2006
	. . if $$$INARTItemType($get(^INART(YM,idKey1,1)))=3 set $$$INARTSupplierOnOrder(^INART(YM,idKey1,1))=idKey2  ; Type = "Make"
	. SET ANZ=ANZ+1
	
	;ZAHLUNGSKONDITION ; payment condition
	if idKey2'="" if idKey1'="" do  
	. QUIT:$PIECE(YFELD,Y,80)'=""  ;KEINE SWERVICEKOSTEN RECHNUNG BEARBEITEN;FIS;06.01.05
	. DO                           ;COPY 
	. . NEW LIEF,KOND,ART,YI
	. . SET KOND=""
	. . IF YDATEI="INARTK"             SET LIEF=idKey2                                          ;LIEFERANT AUS ARTIKEANLAGE        ;supplier out of 
	. . IF YDATEI="INAUFPK"            SET LIEF=$PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,12)  ;LIEFERANT IN AUFTRAGSPOSITION     ;supplier within 
	. . IF YDATEI="INAUFPK" IF LIEF="" SET LIEF=$PIECE($GET(^INAUF(YM,idKey1,1)),Y,12)          ;LIEFERANT AUS AUFTRAG             ;supplier out of mandate 
	. . IF YDATEI="INANGPK"            SET LIEF=$PIECE($GET(^INANGP(YM,idKey1,idKey2,1)),Y,12)  ;LIEFERANT IN AUFTRAGSPOSITION     ;supplier within 
	. . IF YDATEI="INANGPK" IF LIEF="" SET LIEF=$PIECE($GET(^INANG(YM,idKey1,1)),Y,12)          ;LIEFERANT AUS AUFTRAG             ;supplier out of mandate 
	. . IF YDATEI="INAUFPA"            SET LIEF=idKey3                                          ;LIEFERANT AUS AUFTRAGSBEARBEITUNG ;supplier out of sales order processing 
	. . IF YDATEI="INERECH1"           SET LIEF=idKey3                                          ;LIEFERANT AUS RECHNUNGSERFASSUNG  ;supplier out of 
	. . QUIT:LIEF=""
	. . ;
	. . SET KOND = $PIECE($GET(^INLIEF(YM,LIEF,1)),Y,56)
	. . IF KOND'="" IF $PIECE(YFELD,Y,54)=1 DO
	. . . SET $PIECE(YFELD,Y,7)  = $PIECE($GET(^INKOND(YM,KOND,1)),Y,3)   ;SKONTO
	. . . SET $PIECE(YFELD,Y,9)  = $PIECE($GET(^INKOND(YM,KOND,1)),Y,2)   ;TAGE
	. . . SET $PIECE(YFELD,Y,10) = $PIECE($GET(^INKOND(YM,KOND,1)),Y,4)   ;NETTO
	. . ;
	. . ;+++++++++++++++++++++++++++++++++++
	. . ; Copy Supplier specific details to other record
	. . ;+++++++++++++++++++++++++++++++++++
	. . ;
	. . SET ART=""
	. . IF YDATEI="INARTK" SET ART=idKey1  ;ARTIKEL AUS ARTIKEANLAGE ;item out of 
	. . ;
	. . IF YDATEI="INAUFPK" DO
	. . . SET ART=$PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,4)               ;ARTIKEL IN AUFTRAGSPOSITION ;item within 
	. . . IF $PIECE(YFELD,Y,2)="" IF $PIECE(YFELD,Y,7)="" IF ART'="" DO
	. . . . FOR YI=2,3,4,5,6,7,9,10 SET $PIECE(YFELD,Y,YI)=$PIECE($GET(^INARTK(YM,ART,LIEF,1)),Y,YI)  ;AUS ARTIKELKONDITON ;out of 
	. . ;
	. . IF YDATEI="INANGPK" DO
	. . . SET ART=$PIECE($GET(^INANGP(YM,idKey1,idKey2,1)),Y,4)               ;ARTIKEL IN ANGEBOTGSPOSITION ;item within 
	. . . IF $PIECE(YFELD,Y,2)="" IF $PIECE(YFELD,Y,7)="" IF ART'="" DO
	. . . . FOR YI=2,3,4,5,6,7,9,10 SET $PIECE(YFELD,Y,YI)=$PIECE($GET(^INARTK(YM,ART,LIEF,1)),Y,YI)  ;AUS ARTIKELKONDITON ;out of 
	. . ;
	. . IF YDATEI="INAUFPA" DO
	. . . SET ART=$PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,4)               ;LIEFERANT AUS AUFTRAGSBEARBEITUNG ;supplier out of sales order processing 
	. . . IF $PIECE(YFELD,Y,2)="" IF $PIECE(YFELD,Y,7)="" DO
	. . . . FOR YI=2,3,4,5,6,7,9,10 SET $PIECE(YFELD,Y,YI)=$PIECE($GET(^INAUFPK(YM,idKey1,idKey2,1)),Y,YI)  ;AUS ARTIKELKONDITON ;out of 
	. . ;
	. . IF YDATEI="INERECH1" DO
	. . . SET ART=$PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,4)               ;LIEFERANT AUS RECHNUNGSERFASSUNG ;supplier out of 
	. . . IF $PIECE(YFELD,Y,2)="" IF $PIECE(YFELD,Y,7)="" FOR YI=2,3,4,5,6,7,9,10 SET $PIECE(YFELD,Y,YI)=$PIECE($GET(^INAUFPK(YM,idKey1,idKey2,1)),Y,YI)  ;AUS ARTIKELKONDITON ;out of 
	. . ;
	. . NEW WG,YI
	. . SET WG=""
	. . IF ART'="" SET WG=$PIECE($GET(^INART(YM,ART,1)),Y,30)
	. . ;
	. . IF WG'="" IF $DATA(^INLIEFK(YM,LIEF,WG,1)) IF $PIECE(YFELD,Y,54)=1 DO   ;KONDITION AUS LIEFERANT ;out of supplier 
	. . . SET KOND1=$GET(^INLIEFK(YM,LIEF,WG,1))
	. . . FOR YI=2,3,4,5,6,51,52,53 SET $PIECE(YFELD,Y,YI)=$PIECE(KOND1,Y,YI)   ;RABATTE
	. . . SET KOND=$PIECE(KOND1,Y,7)
	. . . QUIT:KOND=""
	. . . SET $PIECE(YFELD,Y,7)  = $PIECE($GET(^INKOND(YM,KOND,1)),Y,3)   ;SKONTO
	. . . SET $PIECE(YFELD,Y,9)  = $PIECE($GET(^INKOND(YM,KOND,1)),Y,2)   ;TAGE
	. . . SET $PIECE(YFELD,Y,10) = $PIECE($GET(^INKOND(YM,KOND,1)),Y,4)   ;NETTO
	. ;
	. SET $PIECE(YFELD,Y,54)=""                  ;LÖSCHEN COPY ;Delete 
	. SET YOK=$$^WWWSPEI(YDATEI,YKEY,YFELD,1)
	
	IF +$PIECE(YFELD,Y,12)'=0 DO
	. SET $PIECE(YFELD,Y,13)=$$^INNETTO(YFELD)   ;RECHEN NETTO AUS BRUTTO FELD ;out of gr. field 
	. SET YOK=$$^WWWSPEI(YDATEI,YKEY,YFELD,1)
	
	; -------------------------------------- NOTE : trailing QUIT <<<<<<<<<<<<<<<<<     *** EARLY EXIT ***
	
	IF +$PIECE(YFELD,Y,12)=0 IF +$PIECE(YFELD,Y,13)'=0 DO  QUIT
	. SET $PIECE(YFELD,Y,12)=$$^INBRUTTO(YFELD)   ;RECHEN BRUTTO AUS NETTO FELD ;gr. out of field 
	. SET YOK=$$^WWWSPEI(YDATEI,YKEY,YFELD,1)
	; --------------------------------------
	
	;---------------------------------------
	; CALCULATE LINE PRICES WITH OFF AND CALCULATION    ;ERRECHNEN POSITIONSPREISE BEI AB UND RECHNUNG
	; 
	; NOTE : Temporary changes to $$$INAUFPNoRecalculationOfCostAllo[wed] ***MUST***
	;        be saved for checking by INAUFKOST.
	;        Probably better to pass switch to override setting.
	; NOTE : strStatus not checked
	;---------------------------------------
	; SR16602 vvvvv
	if ($extract(YDATEI,1,5)="INAUF") || (YDATEI="INERECH1") {
		if (idKey1'="") && (idKey2'="") {
			set objOrderLine = $get(^INAUFP(YM,idKey1,idKey2,1))
			
			if $$$INAUFPPreliminaryManufacturingC(objOrderLine)="" set $$$INAUFPPreliminaryManufacturingC(objOrderLine) = $$$INAUFPCost(objOrderLine)
			set $$$INAUFPNoRecalculationOfCostAllo(^INAUFP(YM,idKey1,idKey2,1)) = $$$NO  ;FREIGEBEN ÄNDERUNG ;allow alteration 
			set $$$INAUFPCost(objOrderLine)  = $$^INAUFKOST(idKey1,idKey2)
			set $$$INAUFPNoRecalculationOfCostAllo(^INAUFP(YM,idKey1,idKey2,1)) = $$$YES ;SPERREN ÄNDERUNG   ;block alteration 
			
			;	VK = EK SETZTEN (SPEZIELLE ANFORDERUNG VON UPA / WHOLESALE AMERICA) ;Sale Planned Cost 
			if $$$INAUFPSalesPriceisWholesalePric(objOrderLine)=$$$YES {
		; FIXME : SR15107 : Use D109 Received quantity if non-null? <GRF>    <DEFERRED FOR PURCHASE ORDER REVIEW>
				set $$$INAUFPUnitSalesPrice(objOrderLine) = $justify($$$INAUFPQuantity(objOrderLine)/$$$INAUFPCost(objOrderLine)*$$^INQTYUNIT($$$INAUFPItemNumber(objOrderLine),idKey1,idKey2),0,2)
				for loop=121,122,128,212,214,211 set $piece(objOrderLine,Y,loop)=""
				do EHWrapper^INBRUTTONETTO(YFORM,.objOrderLine)
			}
			set strStatus = $$$Save("INAUFP",idKey1_","_idKey2,objOrderLine,1)
			
			set objOrder = $get(^INAUF(YM,idKey1,1))
			set $$$INAUFStandardCondition(objOrder) = ""        ; Terms of Payment
			set strStatus = $$$Save("INAUF",idKey1,objOrder)
		}
	}
	
	/*
	IF ($EXTRACT(YDATEI,1,5)="INAUF") || (YDATEI="INERECH1") DO   ;ERRECHNEN POSITIONSPREISE BEI AB UND RECHNUNG ;CALCULATE POSITION PRICES WITH OFF AND CALCULATION 
	. QUIT:idKey1=""
	. QUIT:idKey2=""
	.;IF $PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,250)="" SET $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,250)=$PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,74)  ;SAVE ORIGIN ITEM COST ;FIS;19.11.04;26593
	. IF $PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,250)="" SET $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,250)=$PIECE($GET(^INAUFP(YM,idKey1,idKey2,1)),Y,47)  ;Typo correction;GRF;26-Aug-2005
	. SET $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,254) = $$$NO                           ;FREIGEBEN ÄNDERUNG ;allow alteration 
	. SET $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,47)  = $$^INAUFKOST(idKey1,idKey2)
	. SET $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,254) = $$$YES                          ;SPERREN ÄNDERUNG   ;block alteration 
	. ;
	. ;VK = EK SETZTEN (SPEZIELLE ANFORDERUNG VON UPA / WHOLESALE AMERICA) ;Sale Planned Cost 
	.;IF $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,255)=1!($PIECE($GET(^INVORG(YM,YM,1)),Y,202)=1) DO  ;VK=EK GENERELL !;FIS;28.12.04
	. IF $PIECE(^INAUFP(YM,idKey1,idKey2,1),Y,255) = $$$YES DO  ;VK=EK
	. . NEW YFELD,YOK,YII
	. . SET YFELD = $GET(^INAUFP(YM,idKey1,idKey2,1))
	. . ;
	. . ; FIXME : SR15107 : Use D109 Received quantity if non-null? <GRF>    <DEFERRED FOR PURCHASE ORDER REVIEW>
	. . ; 
	. . ; D118	$$$INAUFPUnitSalesPrice()
	. . SET $PIECE(YFELD,Y,118)=$JUSTIFY($PIECE(YFELD,Y,47)/$PIECE(YFELD,Y,5)*$$^INQTYUNIT(,idKey1,idKey2),0,2)
	. . FOR YII=121,122,128,212,214,211 SET $PIECE(YFELD,Y,YII)=""  ;tybd;19.2.2005
	. . DO EH^INBRUTTONETTO                  ; Updates YFELD ByRef
	. . SET YOK=$$^WWWSPEI("INAUFP",idKey1_","_idKey2,YFELD,1)
	. . ;
	. . SET $PIECE(^INAUF(YM,idKey1,1),Y,56)=""  ;KEINE SKONTO, DA NETTO BETRAG ERRECHNET IN KOSTEN;TYBD;2,1,2005
	*/
	; SR16602 ^^^^^
	
	;+++++++++++++++++++++++++++++++++++++++
	;	D202	$$$INVORGSaleCost
	;	D213	$$$INVORGApplyInvDiscountsToOrder()
	;	D215	$$$INVORGCalcAdditionalCostsInLine()
	;+++++++++++++++++++++++++++++++++++++++
	
	IF YDATEI="INARTK" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,202) = $$$YES DO     ;WENN EK=VK DANN AUTOMATISCH NEU VK ÜBERTRAGEN
	. IF +$PIECE(YFELD,Y,13)'=0 IF idKey1'="" SET $PIECE(^INART(YM,idKey1,1),Y,88)=$PIECE(YFELD,Y,13)
	
	
	
	;VK=EK
	IF YDATEI="INERECH1" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,202) = $$$YES DO   ;TYBD;2,1,2005;BEI RECHNUNG WENN EK=VK
	 .QUIT:idKey1=""
	. QUIT:idKey2=""
	. ;
	. NEW YFELD1,YOK,YII
	. SET YFELD1=$GET(^INAUFP(YM,idKey1,idKey2,1))
	. QUIT:$PIECE(YFELD1,Y,97)'=""  ;RECHNUNG BEREITS GEDRUCKT
	. ;IF +$PIECE(YFELD,Y,16)'=0 SET $PIECE(YFELD1,Y,118)=$JUSTIFY($PIECE(YFELD,Y,12)/($PIECE(YFELD,Y,16)*$$^INQTYUNIT(,idKey1,idKey2)),0,2)
	. IF +$PIECE(YFELD,Y,16)'=0 SET $PIECE(YFELD1,Y,118)=$JUSTIFY($PIECE(YFELD,Y,12)/($PIECE(YFELD,Y,16)/$$^INQTYUNIT(,idKey1,idKey2)),0,$$^WWWDECIMALLEN("INAUFP",118))  ;FIS;18.02.05;27373;TYBD;23,2,2005 NK AUS FELD 118
	. FOR YII=121,122,128,212,213,214,211,125,129,210 SET $PIECE(YFELD,Y,YII)=""
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,213) '= $$$YES DO  ;NUR NETTO-EK ÜBERTRAGEN;FIS;29.02.05;27351
	. . IF +$PIECE(YFELD,Y,16)'=0 SET $PIECE(YFELD1,Y,118)=$JUSTIFY($PIECE(YFELD,Y,22)/($PIECE(YFELD,Y,16)/$$^INQTYUNIT(,idKey1,idKey2)),0,$$^WWWDECIMALLEN("INAUFP",118))
	. . IF +$PIECE(YFELD,Y,16)=0  SET $PIECE(YFELD1,Y,118)=$JUSTIFY($PIECE(YFELD,Y,22)/$$^INQTYUNIT(,idKey1,idKey2),0,$$^WWWDECIMALLEN("INAUFP",118))
	. ;
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,213) = $$$YES DO   ;RABATTE ÜBERTRAGEN;FIS;29.02.05;27351
	. . IF $PIECE(YFELD,Y,2)'="" SET $PIECE(YFELD1,Y,122)=$PIECE(YFELD,Y,2)  ;RABATT1     = POSITIONSRABATT ;FIS;16.02.05;27351
	. . IF $PIECE(YFELD,Y,3)'="" SET $PIECE(YFELD1,Y,128)=$PIECE(YFELD,Y,3)  ;RABATT2     = ZUSATZRABATT    ;FIS;16.02.05;27351
	. . IF $PIECE(YFELD,Y,4)'="" SET $PIECE(YFELD1,Y,212)=$PIECE(YFELD,Y,4)  ;RABATT3     = ZUSATZRABATT    ;TYBD;16.02.05;27377
	. . IF $PIECE(YFELD,Y,5)'="" SET $PIECE(YFELD1,Y,214)=$PIECE(YFELD,Y,5)  ;ZU/ABSCHLAG = ZUSATZRABATT    ;TYBD;16.02.05;27377
	. . IF $PIECE(YFELD,Y,6)'="" SET $PIECE(YFELD1,Y,211)=$PIECE(YFELD,Y,6)  ;ZU/ABSCHLAG = SPEZIALZUSCHLAG ;FIS;16.02.05;27351
	. ;
	. DO
	. . NEW YFELD
	. . SET YFELD=YFELD1
	. . DO EH^INBRUTTONETTO                  ; Updates YFELD ByRef
	. . SET YFELD1=YFELD
	. ;
	. SET YOK=$$^WWWSPEI("INAUFP",idKey1_","_idKey2,YFELD1,1)
	. ;
	. ;NEURECHNEN ZUSATZKOSTEN IN POSITIONEN;FIS;08.03.05;27220
	. IF $PIECE($GET(^INVORG(YM,YM,1)),Y,215) = $$$YES DO
	. . NEW YKEYX
	. . SET YKEYX=YKEY
	. . NEW YKEY,YFELD,LFN
	. . SET LFN = ""
	. . FOR  SET LFN = $ORDER(^INAUFPIMPACT(YM,$PIECE(YKEYX,",",1),$PIECE(YKEYX,",",2),LFN)) QUIT:LFN=""  DO
	. . . SET YFELD = $GET(^INAUFPIMPACT(YM,$PIECE(YKEYX,",",1),$PIECE(YKEYX,",",2),LFN,1))
	. . . SET YKEY  = $PIECE(YKEYX,",",1)_","_$PIECE(YKEYX,",",2)_","_LFN
	. . . DO ^INAUFPIMPACT
	
	
	
	;ÜBERTRAGEN KONDITIONEN VON AUFTRAGS-AB NACH ARTIKELSTAMM ;transport terms of payment within 
	IF (YDATEI="INAUFPA") || (YDATEI="INERECH1") IF $PIECE(YFELD,Y,60)=1 DO
	. NEW ART,LIEF,SATZ,YI,YOK
	. SET ART=$$$INAUFPItemNumber($GET(^INAUFP(YM,idKey1,idKey2,1)))
	. QUIT:ART=""
	. SET LIEF=idKey3  ;LIEFERANT AUS AUFTRAGSBEARBEITUNG ;supplier out of sales order processing 
	. QUIT:LIEF=""
	. QUIT:'$DATA(^INARTK(YM,ART,LIEF,1))  ;NICHT ANGELEGT ;Not present
	. ;
	. SET SATZ=^INARTK(YM,ART,LIEF,1) 
	. if +$PIECE(YFELD,Y,16)'=0 do
	. . SET $$$INARTKBasicPurchasePrice(SATZ)       = $JUSTIFY($PIECE(YFELD,Y,12)/$PIECE(YFELD,Y,16),0,2)  ;BRUTTO EK ;gr. Planned Cost 
	. . SET $$$INARTKNetPurchasePrice(SATZ)         = $JUSTIFY($PIECE(YFELD,Y,13)/$PIECE(YFELD,Y,16),0,2)  ;NETTO EK  ;Planned Cost 
	. . SET $$$INARTKSurchargeAllowanceAmount(SATZ) = $JUSTIFY($PIECE(YFELD,Y,6) /$PIECE(YFELD,Y,16),0,2)  ;ZUSCHLAG BETRAG ;surcharge Sum 
	. FOR YI=2,3,4,5,7,9,10,25,51,52,53  SET $PIECE(SATZ,Y,YI)=$PIECE(YFELD,Y,YI)
	. SET $$$INARTKPriceMarker(SATZ)=1  ;FLAG: PREIS AUS LETZTER RECHNUNG;FIS;28.12.04
	. DO
	. . NEW YFORM,YVOR
	. . SET YOK = $$^WWWSPEI("INARTK",ART_","_LIEF,SATZ,1)
	. ;
	. IF YDATEI="INAUFPA"  SET $PIECE(^INAUFPA(YM,idKey1,idKey2,idKey3,idKey4,1),Y,60) =""
	. IF YDATEI="INERECH1" SET $PIECE(^INERECH1(YM,idKey1,idKey2,idKey3,idKey4,1),Y,60)=""
	;. DO ^INARTNEU(ART)  ;ARTIKEL NEU RECHNEN
	
	
	
	;Bei Bestellsystem (Internet) Eingangsrechnung=Wareneingang/AUSGANG;FIS;28.12.04
	IF YDATEI="INERECH1" IF idKey3'="" IF $PIECE($GET(^INLIEF(YM,idKey3,1)),Y,244)=1 DO
	. NEW POSX1,POSX
	. SET POSX1=$GET(^INAUFP(YM,idKey1,idKey2,1))
	. QUIT:$PIECE(POSX1,Y,91)'=""                    ; SCHON OK 
	. SET $PIECE(POSX1,Y,90)=1                       ; AUSLIEFERFÄHIG
	. SET $PIECE(POSX1,Y,85)="SYSTEM"                ; BESTELLUNG
	. SET $PIECE(POSX1,Y,83)=+$HOROLOG               ; DRUCKDATUM
	. SET $PIECE(POSX1,Y,84)=$PIECE($HOROLOG,",",2)  ; UHRZEIT
	. SET $PIECE(POSX1,Y,91)="AUTO"                  ; LIEFERSCHEIN
	. SET $PIECE(POSX1,Y,92)=+$HOROLOG               ; DRUCKDATUM
	. SET $PIECE(POSX1,Y,93)=$PIECE($HOROLOG,",",2)  ; UHRZEIT
	. SET $PIECE(POSX1,Y,94)="SYSTEM"  ;
	. NEW YFORM,YVOR,YOK
	. SET YOK=$$^WWWSPEI("INAUFP",idKey1_","_idKey2,POSX1,1)
	. ;
	. IF $DATA(^INAUFPs(YM,14,$$^WWWUMLAU(idKey1,1),$$^WWWUMLAU("#"_idKey2,1))) DO  ;ZUSATZBESTELLPOSITION EBENFALLS AUF GELIEFERT SETZEN;FIS;02.03.05;27043
	. . SET POSX=$ORDER(^INAUFPs(YM,14,$$^WWWUMLAU(idKey1,1),$$^WWWUMLAU("#"_idKey2,1),idKey1,""))
	. . QUIT:POSX=""
	. . SET POSX1=$GET(^INAUFP(YM,idKey1,POSX,1))
	. . QUIT:$PIECE(POSX1,Y,91)'=""                    ;SCHON OK 
	. . SET $PIECE(POSX1,Y,90)=1                       ;AUSLIEFERFÄHIG
	. . SET $PIECE(POSX1,Y,85)="SYSTEM"                ;BESTELLUNG
	. . SET $PIECE(POSX1,Y,83)=+$HOROLOG               ;DRUCKDATUM
	. . SET $PIECE(POSX1,Y,84)=$PIECE($HOROLOG,",",2)  ;UHRZEIT
	. . SET $PIECE(POSX1,Y,91)="AUTO"                  ;LIEFERSCHEIN
	. . SET $PIECE(POSX1,Y,92)=+$HOROLOG               ;DRUCKDATUM
	. . SET $PIECE(POSX1,Y,93)=$PIECE($HOROLOG,",",2)  ;UHRZEIT
	. . SET $PIECE(POSX1,Y,94)="SYSTEM"  ;
	. . NEW YFORM,YVOR,YOK
	. . SET YOK=$$^WWWSPEI("INAUFP",idKey1_","_POSX,POSX1,1)
	
	
	
	IF YDATEI="INARTK" DO 
	. DO ^INARTNEU(idKey1,1)  ;ARTIKEL NEU RECHNEN ;item recent have faith in 
	;. IF $PIECE(^INVORG(YM,YM,1),Y,121)=""  ;NICHT UPTODATE
	
	QUIT
	
	
OnAfterPrimaryKey(YKEY)
	;-------------------------------------------------------------------------------
	; called as : s LIEFERANT=$$OnAfterPrimaryKey^INARTK(YKEY)
	; 
	; Returns : Supplier
	; 
	; History :
	; 25-Aug-2006	GRF		SR12027 : Form adjusted to Coding Standards
	;-------------------------------------------------------------------------------
	new LIEFERANT
	
	do DisplayRate^INARTKCountryCustom(YKEY)
	do ^INARTTRACK
	set LIEFERANT=" "
	if $piece(YKEY,",",2)'="" set LIEFERANT=$piece(YKEY,",",2)  ;ARTIKELINFO LIEFERANT
	quit LIEFERANT
	
	
OnAfterSave(YKEY)
	;-------------------------------------------------------------------------------
	; called as : d OnAfterSave^INARTK(YKEY)
	; 
	; History :
	; 25-Aug-2006	GRF		SR12027 : Form adjusted to Coding Standards; direct call
	;-------------------------------------------------------------------------------
	;d ^INARTK d BESTELLMENGE^INTAGES($g(YKEY))    ; original entry
	do Start^INARTK
	do BESTELLMENGE^INTAGES5(YKEY)     ; CALCULATE CONSUMPTION
	quit
	
	
IsUsable(pstrKey,pidForm)
	;-------------------------------------------------------------------------------
	; Check whether these terms are usable in this instance.
	; At the moment just checks the supplier.
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 26-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idSupplier
	
	set idSupplier = $$$KEY2(pstrKey)
	set strStatus  = $$IsUsable^INLIEF(idSupplier,pidForm)	// Check supplier status
	
	quit strStatus
 
 
DisplayConversionFactor()
	;-------------------------------------------------------------------------------
	; Displays the inverse of the 'Conversion Factor' field in a dialog box where it
	; can be edited.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 14-Mar-2007	shobby	SRBR014399: Call to the dialog box is now a standard macro.
	; 14-Mar-2007	shobby	SRBR014399: Replaced reference to WWWDATEN with $$$GetFormData
	; 09-Mar-2007	shobby	SRBR014399: Created
	;-------------------------------------------------------------------------------
	new fltInverse,objINARTK,fltConversionFactor
	
	set objINARTK = $$$GetFormData(YFORM,"D")
	set fltConversionFactor = $$$INARTKConversionFactor(objINARTK)
	
	if +fltConversionFactor'=0 {
		set fltInverse = (1/fltConversionFactor)
	} else {
		set fltInverse = 0
	}
	
	$$$ShowPrompt($$$Text(32510),"UpdateConversionFactor^INARTK",fltInverse)
	
	quit
	
	
UpdateConversionFactor(pfltConversionFactor="")
	;-------------------------------------------------------------------------------
	; Takes the value entered in the dialog box above and calculates the inverse, then
	; updates the 'Conversion Factor' field of INART. 
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 12-Apr-2007	shobby	SRBR014399: Corrected failure when updating the field
	; 							that initially had a 0 value.
	; 14-Mar-2007	shobby	SRBR014399: If returned result is the same (within a small
	; 							margin of error) Don't change the original number.
	; 							Resolves some rounding errors.
	; 09-Mar-2007	shobby	SRBR014399: Created
	;-------------------------------------------------------------------------------
	;
	new objINARTK
	if +pfltConversionFactor'=0 {
		set objINARTK = $$$GetFormData(YFORM,"D")
		if (+$$$INARTKConversionFactor(objINARTK)=0)    ||
		   ($extract(+(1/$$$INARTKConversionFactor(objINARTK)),1,15)'=$extract(+pfltConversionFactor,1,15)) {
			
			do PopulateDataField^COMUtils("INARTK",$$$FldINARTKConversionFactor,(1/pfltConversionFactor))
		}
	}
	quit 
  
  
DefaultUnit(pidItem="",pidSupplier="")
	;-------------------------------------------------------------------------------
	;
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 03-Feb-2010	shobby	SR17138: Created
	;-------------------------------------------------------------------------------
	new idDefaultUnit,objINARTK,objINARTPACK,objINART
	
	set idDefaultUnit = ""
	if pidSupplier'="" {
		set objINARTK = $get(^INARTK(YM,pidItem,pidSupplier,1))
		if $$$INARTKDeliveryQuantityUnit(objINARTK)'="" {
			set objINARTPACK = $get(^INARTPACK(YM,pidItem,$$$INARTKDeliveryQuantityUnit(objINARTK),1))
			if '$$$INARTPACKIsObsolete(objINARTPACK) {
				set idDefaultUnit = $$$INARTKDeliveryQuantityUnit(objINARTK)
			}
		}
	}
	if idDefaultUnit="" {
		set objINART      = $get(^INART(YM,pidItem,1))
		set idDefaultUnit = $$$INARTUnitofMeasure(objINART)
	}
  	quit idDefaultUnit
  	
]]></Routine>
</Export>