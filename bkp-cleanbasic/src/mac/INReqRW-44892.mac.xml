<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReqRW" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReqRW
	
#include %occInclude
#include COMSYS
#include INConst
#include COMConst
#include COMView
#include COMDebug
	
OnAfterDataFields(pstrCompany,pstrForm,pYPARA)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 
	; 26-MAR-2009	KALYAN	Created
	;-------------------------------------------------------------------------------
	do LoadGrid(pstrCompany,pstrForm,pYPARA)
	quit
	
	
LoadGrid(pstrCompany=0,pstrForm,pYPARA,pblnLocked=$$$NO)
	;-------------------------------------------------------------------------------
	;			
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 14-Dec-2009	PPP		SR17077:Status has to 3 - Sourced 
	; 16-Jun-2009	shobby	SR16697:Allow the supplier filter to show lines without suppliers.
	; 26-MAR-2009 	KALYAN	Created
	;-------------------------------------------------------------------------------
	new YFORM,YKEY,YAUSWAHL,YFELD
 
	do ClearOld^WWWMultiLock(YUSER)
 	
	$$$ClearExternalFilter("INReqRW") ; YFORM is used as a global within COMGridEdit31
	set YFORM = "INReqRWLine"
	
	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionStatus,0,$$$EnumCOMVIEWCOMPARATORNotEquals,$$$ANNO)
	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionStatus,1,$$$EnumCOMVIEWCOMPARATORNotEquals,$$$ANNO)
	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionStatus,2,$$$EnumCOMVIEWCOMPARATORNotEquals,$$$ANNO)
	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionStatus,4,$$$EnumCOMVIEWCOMPARATORNotEquals,$$$ANNO)
 	;if (pYPARA'="") {
  	;	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionSupplier,$Piece(pYPARA,",",1)_",",$$$EnumCOMVIEWCOMPARATOREquals,$$$YES)
	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionSupplier,$piece(pYPARA,",",1),$$$EnumCOMVIEWCOMPARATOREquals,$$$YES)
 	;}
 	;else{
	;	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionSupplier," ",$$$EnumCOMVIEWCOMPARATORLike,$$$YES) 	
 	;}
	
	do AddExternalFilter^COMViewFilter("INReqRW","D"_$$$FldINRequisitionStockProgram,$piece(pYPARA,",",2),$$$EnumCOMVIEWCOMPARATOREquals,$$$ANNO)
	set YFELD	= ""
	set YKEY	= YM		// No header
    
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)          = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL)       = 400
	set $$$COMGridEditParameterGridName(YAUSWAHL)            = "INReqRWLine"
	set $$$COMGridEditParameterEnabled(YAUSWAHL)             = $$$YES
	set $$$COMGridEditParameterContainer(YAUSWAHL)           = "INReqRW"
	set $$$COMGridEditParameterDontUpdateContainer(YAUSWAHL) = $$$NO
	set $$$COMGridEditParameterCOMViewGrid(YAUSWAHL)         = $$$YES
 
	do Start^COMGridEdit31(YFORM,YKEY)
	set YFORM = "INReqRW"                 ; COMViewFilter uses YFORM byref
	
	
	set ^CacheTemp(YUCI,YUSER,"idSup")=$piece(pYPARA,",",1)
	do LoadCOMViewGrid^COMViewFilter("INReqRW","INReqRWLine",1)
	
	quit $$$YES
	

OnBeforeSave()
	;-------------------------------------------------------------------------------
	; Before we save the record, save the grid.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 14-Nov-2006	SteveS	SR15316: Created (Copied from REQUISITIONVAR)
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus=$$$GRIDSave(YM)
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		do ReturnError^COMUtilError(strStatus)	
	}
	
	quit
	
	
CreatePO(pYKEY,pYPARA)
	;-------------------------------------------------------------------------------
	; Method Usage :
	; To Create Purchase Order from the selected INRequisition Lines
	;
	; Params:
	;
	; Returns:
	;
	; History:
	;
	; 24-Apr-2009	SRAPOL	Added Updation of INPOID in INRequsition
	; 18-Apr-2009	SRAPOL	Added Updation of INPOLine in INRequsition
	; 31-Mar-2009 	KALYAN	Created
	;-------------------------------------------------------------------------------
	new idINPO,idINReq,idReq,idINReqLine,idINRequisition,idINReqLineIndex
	new objINPO,objINReqLine,objINReq,objINRequisition,objReq
	new strStatus,strStatus1,strStatus2,strStatus3,idxSupplier
	new intarrCount,arrMessageLines,arrStatus,arrINReqLine,intArrCount
	new tmpStkProgram,blnStkProgram
	
	set intarrCount   = 1
	set tmpStkProgram = $piece(pYPARA,",",2)
	set blnStkProgram = $$$YES
	set idxSupplier   = $$$Index($piece(pYPARA,",",1))
	
	set idReq = ""
	for {
		set idReq = $order(^INRequisitions(YM,20,1,1,YM,idReq))
		quit:idReq=""
		set objReq = $get(^INRequisition(YM,pYKEY,idReq,1))
		quit:objReq=""
		
		if ($$$INRequisitionSupplier(objReq)="") {
			set $$$INRequisitionSupplier(objReq)=idxSupplier
		}
		if (($$$INRequisitionUnitPrice(objReq)'="") && ($$$INRequisitionLocation(objReq)'="")) {	
		;	if (tmpStkProgram="") {
		;		set tmpStkProgram = $$$INRequisitionStockProgram(objReq)
		;	} else {
		;		if (tmpStkProgram'=$$$INRequisitionStockProgram(objReq)) {
		;			set blnStkProgram = $$$NO
		;			set intarrCount   = 1						
		;			quit
		;		}
		;	}  
			set $piece(objINPO,Y,2)  = 1
			set $piece(objINPO,Y,3)  = $$$INRequisitionItem(objReq)
			set $piece(objINPO,Y,4)  = " "
			set $piece(objINPO,Y,5)  = $$$INRequisitionQuantity(objReq)
			set $piece(objINPO,Y,6)  = $$$INRequisitionUnit(objReq)
			set $piece(objINPO,Y,9)  = $$$INRequisitionUnitPrice(objReq)
			set $piece(objINPO,Y,14) = $$$INRequisitionSupplier(objReq)
			set $piece(objINPO,Y,15) = $$$INRequisitionSupplierName(objReq)
			set $piece(objINPO,Y,16) = $zdate(+$horolog)
			set $piece(objINPO,Y,19) = $$$INRequisitionLocation(objReq)
			set $piece(objINPO,Y,21) = $$$INRequisitionPaymentTerms(objReq)
			set $piece(objINPO,Y,25) = $$$INRequisitionLocation(objReq)
			set $piece(objINPO,Y,26) = $$$WWW0121LocationName($get(^WWW0121(0,pYKEY,$$$INRequisitionLocation(objReq),1)))
			set $piece(objINPO,Y,27) = $zdate(+$horolog)
			set $piece(objINPO,Y,28) = $$$INRequisitionTransportationMethod(objReq)
			set $piece(objINPO,Y,30) = ""
			set $piece(objINPO,Y,32) = $$$INRequisitionStockProgram(objReq)
			
			set arrMessageLines(intarrCount)=objINPO
			set arrINReqLine(intarrCount) = $$$INRequisitionINReqLine(objReq)
			set intarrCount=intarrCount+1
			
		} else {
			kill arrINReqLine
			kill arrMessageLines
			set intarrCount=1
		}
	}
	
	if (intarrCount>1) {
		set idINPO = $$POCreateFromLines^INPO(.arrMessageLines,.strStatus,.arrStatus)
		if (idINPO'="") {
			for {
				set idReq = $order(^INRequisition(YM,pYKEY,idReq))
				quit:idReq=""
				set objReq = $get(^INRequisition(YM,pYKEY,idReq,1))
				quit:objReq=""
				
				if (($$$INRequisitionStatus(objReq)=1)  &&
				    ($$$INRequisitionRelease(objReq)=1) &&
				    ($$$INRequisitionINReqLine(objReq)'="")) {
						
					set objINReqLine = $get(^INReqLine(YM,$piece($$$INRequisitionINReqLine(objReq),",",1),$piece($$$INRequisitionINReqLine(objReq),",",2),1))
					set $$$INRequisitionStatus(objReq)  =4
					set $$$INReqLineStatus(objINReqLine)=9
					set $$$INReqLinePurchaseOrder(objINReqLine)=idINPO
					set strStatus1 = $$$Save("INRequisition",YM_","_idReq,objReq,1)
					set strStatus2 = $$$Save("INReqLine",$$$INRequisitionINReqLine(objReq),objINReqLine,1)
					set idINReq = $Piece($$$INRequisitionINReqLine(objReq),",",1)
					set objINReq=$get(^INReq(YM,idINReq,1))
					set $$$INReqStatus(objINReq)=9
					set strStatus3 = $$$Save("INReq",idINReq,objINReq,1)
				}
		
			}
			set intArrCount= ""
			set idINRequisition = ""
			for {
				set intArrCount = $order(arrINReqLine(intArrCount))
				quit:intArrCount=""
				set idINReqLine      = $get(arrINReqLine(intArrCount))
				set idINReqLineIndex = $piece(idINReqLine,",",1)_" "_$piece(idINReqLine,",",2)
				set idINRequisition  = $order(^INRequisitions(YM,17,idINReqLineIndex,YM,idINRequisition))
				quit:idINRequisition=""
				set objINRequisition                           = $get(^INRequisition(YM,YM,idINRequisition,1))
				set $$$INRequisitionINPOID(objINRequisition)   = idINPO
				set $$$INRequisitionINPOLine(objINRequisition) = $piece($get(arrMessageLines(intArrCount)),Y,29)_","_$piece($get(arrMessageLines(intArrCount)),Y,30)	
				set strStatus1 = $$$Save("INRequisition",YM_","_idINRequisition,objINRequisition,1)
			}
			do GoToForm^COMUtilForm("INPO",idINPO)
			quit		
		}
		
	} else {
		if (blnStkProgram=$$$NO) {
			set strStatus=$$SetRelease(pYKEY)
			write "<B> Stock Program Should Be Same For All The Lines </B>"
			do GoToForm^COMUtilForm("INReqRW",YM)
		} else {
			set strStatus=$$SetRelease(pYKEY)
			write "<B> You Cannot Create PO as Required Feilds are Blank </B>"
			do GoToForm^COMUtilForm("INPOReqSummary",YUSER)
		}
	}
	
	quit
CheckDisable(pYPARA)
	quit
	if ($piece(pYPARA,",",1)'="") {
		set YQ=0
	} else {
		set YQ="1 Supplier Cannot Be Blank. Click On S Button to Save Supplier"
	}
	
	
CheckSupplier(pYPARA)	
	if ($piece(pYPARA,",",1)="") {
		set YQ=0
	} else {
		set YQ="1 Already Saved Click On C Button to Create PO"
	}
 
SaveRequisition(pYKEY)
 	new idReq,objReq,blnSupp,strStatus
 	
	set idReq="" 
	set blnSupp=$$$YES
	set strStatus=$$$GRIDSave(pYKEY)
	for {
		set idReq=$Order(^INRequisition(YM,pYKEY,idReq))
		quit:idReq=""
		set objReq=$get(^INRequisition(YM,pYKEY,idReq,1))
		quit:objReq=""
		
		if (strStatus=$$$YES) {
			if (($$$INRequisitionStatus(objReq)=1) && ($$$INRequisitionSupplier(objReq)="")) {
				set blnSupp=$$$NO
			}
		}
	}
	if (blnSupp=$$$NO) {
		//$$$Alert("Select Supplier")
		//do ReturnError^COMUtilError("Select Supplier")
		write "<B><H2> Select Supplier and Save<H2></B>"
		do GoToForm^COMUtilForm("INReqRW",YM)
	} else {
		do GoToForm^COMUtilForm("INPOReqSummary",YUSER)
	}
	
	
SetRelease(pYKEY)
	new idReq,objReq,strStatus
	
	set strStatus=$$$OK
	set idReq=""
	for {
		set idReq=$Order(^INRequisitions(YM,20,1,1,YM,idReq))
		quit:idReq=""
		set objReq=$get(^INRequisition(YM,pYKEY,idReq,1))
		quit:objReq=""
		
		set $$$INRequisitionRelease(objReq)=0
		set strStatus=$$$Save("INRequisition",YM_","_idReq,objReq,1)
	}	
	
	quit strStatus		
	
]]></Routine>
</Export>