<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INTFRReqLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INTFRReqLine
	
#include INConst
#include COMSYS
#include COMGridEdit31
	
OnBeforeFormat(pstrYFIELDNAME,pstrText,pobjLine)
	;-------------------------------------------------------------------------------
	; Params:
	; 	pstrYFIELDNAME - Field name
	; 	pstrText - Not used
	; 	pobjLine - Transfer Order Line record
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 24-Mar-2009	GRF		SR16428: Call common code for CellColours; idClassFld
	; 							rather than idField
	; 20-Feb-2009	HQN		SR16296: Use IsRejectable instead of relying on 
	; 							Reference only
	; 27-Jan-2009	HQN		SR16296: Added ReasonRejection handling
	; 23-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	do OnBeforeFormat^INTFRLine(pstrYFIELDNAME,pstrText,pobjLine)
	quit
	
	
OnBeforeFormConstruction(pYBBN,pYFELD)  ; DEPRECATED
	;-------------------------------------------------------------------------------
	; Params:
	; 	pYBBN - Form Field Id
	; 	pYFELD - INTFRLine Record
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 16-Feb-2010	shobby	SR15753: Remove redundant code.
	; 20-Feb-2009	HQN		SR16296: Use IsRejectable to disable instead of relying
	; 							on Reference only
	; 27-Jan-2009	HQN		SR16296: Added ReasonRejection handling
	; 23-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	quit
	
	
OnBeforeSave(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Params:
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 20-Feb-2009	GRF		SR16296: Language Text; Other tests rendered obsolete by
	; 							disabling "Reject Remainder" switch under certain
	; 							conditions.
	; 23-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new fltQtyOrdered
	
	do OnBeforeSave^INTFRLine(pYM,pYFORM,pYKEY,pYFELD)
	
	if '$data(YRETVAL) {
		if ($$$INTFRLineRejectRemainder(pYFELD) = $$$YES) {
			if $$$INTFRLineReasonRejection(pYFELD) = "" {
				set YRETVAL = $$$MakeStatus("IN00896")     ; "Requires a reason for rejection."
			}
		}
	}
	quit
	
	
OnBeforeDelete(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Params:
	;  If the Transfer is firmed, revert the corresponding ReqLine's Rejection flags
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 29-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new idReq,idReqLine,objTFR,objUReq,sc,strStatus
	
	set strStatus = $$$OK
	set objTFR = $get(^INTFR(YM,$$$KEY1(pYKEY),1))
	if ($$$INTFRLineRejectRemainder(pYFELD) = $$$YES) && ($$$INTFRStatus(objTFR) = 1){
		; Revert remainder on line ; FIXME: Multiple rejections cannot be handled, ie: reject at ReqLine then Reject at TFRLine needs multiple reasons
		set idReq     = $$$KEY1($$$INTFRLineReference(pYFELD))
		set idReqLine = $$$KEY2($$$INTFRLineReference(pYFELD))
		set objUReq   = ##class(alREQ.dUReqLine).%OpenId("0||"_idReq_"||"_idReqLine,,.sc)
		if $$$ISOK(sc) {
			set objUReq.ReasonRejection = ""
			set objUReq.QtyRejected     = 0
			set objUReq.Rejected        = $$$NO
			set sc = objUReq.%Save()
		}
		if $$$ISERR(sc) set strStatus = $$ISStatusToDLStatus^COMUtilError(sc)
	}
	$$$YQHandler(strStatus)
	if $$$ISERR(strStatus) set Q = $$$QDontSave
	
	quit strStatus
	
	
OnBlur(pYM,pYFORM,pYKEY,&pYFELD,pGridRow,pGridCol,pYINHALT)
	;-------------------------------------------------------------------------------
	; Params:
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 03-Nov-2010	PPP		SRAdhoc: SR15753 - removed more; reinstated some code
	; 16-Feb-2010	shobby	SR15753: Cleanup Redundant code.
	; 24-Mar-2009	GRF		SR16428: Call common code for CellColours; idClassFld
	; 							rather than idField; use Default Colour to toggle
	; 							*NOT* Required Colour; restore YGRIDWHITE set in
	; 							CellColours
	; 20-Feb-2009	HQN		SR16296: Don't allow Rejections if unable to
	; 27-Jan-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new idClassFld
	
	set idClassFld = $$$GetClassField(pYFORM,pGridCol)
	
	if idClassFld = $$$FldINTFRLineQuantity {
		do OnBlur^INTFRLine(pGridCol,pGridRow,.pYFELD)
	}

	quit
	
	
IsRejectable(pYFELD)
	;-------------------------------------------------------------------------------
	; Params: pYFELD TransferLine record
	;
	; ByRefs: None
	;
	; Returns: Boolean
	;
	; History:
	; 16-Sep-2011	GRF		SR17823: QtyOrdered is in different unit to other Qty
	; 05-Jul-2011	PPP		SR17818: Calc using incorrect variable (pYFELD instead of objReqLine)
	; 20-Feb-2009	HQN		SR16296: Created
	;-------------------------------------------------------------------------------
	new blnRejectable,idReqLine,objReqLine
	
	set blnRejectable = $$$YES
	if $$$INTFRLineReference(pYFELD) = "" {
		set blnRejectable = $$$NO
	
	} else {
		set idReqLine = $$$INTFRLineReference(pYFELD)
		set objReqLine = $get(^INReqLine(0,$$$KEY1(idReqLine),$$$KEY2(idReqLine),1))
		if objReqLine = "" {
			set blnRejectable = $$$NO
		
	;	} elseif ($$$INTFRLineQuantity(pYFELD) >= ($$$INReqLineQtyOrdered(objReqLine) - $$$INReqLineQtyRejected(objReqLine) - $$$INReqLineQtyCancelled(pYFELD) - $$$INReqLineQtyIssued(pYFELD))) { //SR17818
	;	} elseif ($$$INTFRLineQuantity(pYFELD) >= ($$$INReqLineQtyOrdered(objReqLine) - $$$INReqLineQtyRejected(objReqLine) - $$$INReqLineQtyCancelled(objReqLine) - $$$INReqLineQtyIssued(objReqLine))) { ; SR17823
		} elseif ($$$INTFRLineQuantity(pYFELD) >= ($$$INReqLineQtyRequired(objReqLine) - $$$INReqLineQtyRejected(objReqLine) - $$$INReqLineQtyCancelled(objReqLine) - $$$INReqLineQtyIssued(objReqLine))) {
			set blnRejectable = $$$NO
		}
	}
	quit blnRejectable
	
]]></Routine>
</Export>