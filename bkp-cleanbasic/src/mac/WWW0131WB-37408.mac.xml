<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW0131WB" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW0131WB
	
#include COMSYS
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^WWW0131WB("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		WECHSELN BETRIEB
	;	VORLAGE VON ^WWW0131
	;
	; Called By : Form WWW0131 (Execute After Save)
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 02-Jul-2012	shobby	SR17998: Use GotoForm with MegaMenu (CORE-81)
	; 11-Feb-2011	GRF		SR17659: use strAllowedLocns
	; 09-Jan-2007	shobby	SRBR014851: Call to CheckLocationDefaultInput
	; 25-Oct-2007	FIS		SRBR014734: clear key information for re-load when changing the location
	; 08.07.2004	FAN		25930
	;-------------------------------------------------------------------------------
	
	$$$LogR("","")
	$$$LogRx("WB1:"_$get(YPARA)_"<")
	
	if $get(VORG(2))="" set VORG(2)=YLOCATION
	do Change("",VORG(2)) 
	quit
	
CheckLocationDefaultInput(pidForm)
	;-------------------------------------------------------------------------------
	; This routine checks whether any of the primary keys on a form refer to WWW0121
	; and only allows clearing of WWW126 if there is.  Without this check pressing the
	; back button on the 'Change Site Location' form will clear out the record on the
	; previous form.  Now it will only do it if the Site Location is one of the keys
	; on the record to be displayed.
	; 
	; History:
	; 08-Jan-2008	shobby		BR014851: Created
	;-------------------------------------------------------------------------------
	new blnResult,idField,objWWW121,idClass,objWWW120,objWWW002
	
	set blnResult = $$$NO
	
	set objWWW120 = $get(^WWW120(0,pidForm,1))
	set idClass   = $$$WWW120ClassUsedInForm(objWWW120)
	
	if idClass'="" {
		set idField = ""
		for {
			set idField = $order(^WWW121(0,pidForm,idField))
			quit:idField=""
			
			set objWWW121 = $get(^WWW121(0,pidForm,idField,1))
			set objWWW002 = $get(^WWW002(0,idClass,idField,1))
			if ($$$WWW121RelationClass(objWWW121)="WWW0121") || ($$$WWW002RelationClass(objWWW002)="WWW0121") {
				if $$$WWW121DefaultVariableInput(objWWW121)'="" {
					set blnResult = $$$YES
					quit
				}
			}
		}
	}
	quit blnResult
	
Change(pidLang="",pidLoc="",pstrYBACK="")
	;-------------------------------------------------------------------------------
	; Change the Users Language or Location
	; 
	; History:
	; 11-Feb-2014	SCR		SESDF-936: Load YPARA & YAUSWAHL
	; 01-Jan-2014	SCR		SESDF-655: Created
	;-------------------------------------------------------------------------------
	new arrParam,intBack,objChange,objSession,objUser,strAllowedLocns,strStatus
	new strTmpForm

	set:$get(^CacheTempWWWSave(YUSER,"YPARA"))'="" YPARA		= $get(^CacheTempWWWSave(YUSER,"YPARA"))
	set:$get(^CacheTempWWWSave(YUSER,"YAUSWAHL"))'="" YAUSWAHL	= $get(^CacheTempWWWSave(YUSER,"YAUSWAHL"))
	set:pstrYBACK'="" YBACK	= pstrYBACK
	set objUser		= $get(^WWW013(0,YBED,1))
	set objSession	= $get(^WWWUSER(0,YUSER,1))
	if (pidLang'="")&&($data(^WWW100(0,"SPRACHE",SPRACHE,pidLang))) {
		set objUser		= $get(^WWW013(0,YBED,1))
		set $$$WWW013Language1(objUser)		= pidLang
		set $$$WWWUSERLanguage1(objSession)	= pidLang 
		set SPRACHE		= pidLang
		set LANGUAGE	= pidLang
	}
	if (pidLoc'="")&&($data(^WWW0121(0,YM,pidLoc,1))) {
		set strAllowedLocns = $$$WWW013AllowedLocations(objUser)
		if +$$^WWWBEDBER(YBED)'=1 && (strAllowedLocns'="" ) && ( '$find(";"_strAllowedLocns_";",";"_pidLoc_";")) {
			do ^WWWINFO($$^WWWTEXT(12)) quit  ;BETRIEB NICHT ERLAUBT
		} else {
			set $$$WWW013HomeLocation(objUser) = pidLoc
			set %(YQUERY,"YLOCATION") = pidLoc
			set %("VAR","YLOCATION")  = pidLoc
			set %KEY("YLOCATION")     = pidLoc
			set YLOCATION             = pidLoc
			set $$$WWWUSERLastLocation(objSession) = pidLoc
	
			set objChange	= $get(^WWW013Loc(YM,YBED,pidLoc,1))
			set $$$WWW013LocNoofhits(objChange)	= $$$WWW013LocNoofhits(objChange) + 1
			if $$$WWW013LocFirstSelected(objChange)="" {
				set $$$WWW013LocFirstSelected(objChange)	= $h
			}
			set $$$WWW013LocLastSelected(objChange)	= $h
			set strStatus	= $$$Save("WWW013Loc",YBED_","_pidLoc,objChange,1)
			set strTmpForm	= ""
			for {
				set strTmpForm	= $order(^WWW126(0,strTmpForm))
				quit:strTmpForm=""
		
				if $$CheckLocationDefaultInput(strTmpForm) {
					if $data(^WWW126(0,strTmpForm,YUSER)) kill ^WWW126(0,strTmpForm,YUSER)
				}
			}
		}
	}
	;^^^^^^^^^ 
	
	if objUser'=$get(^WWW013(0,YBED,1)) {
		set strStatus	= $$$Save("WWW013",YBED,objUser,1)
	}
	if objSession'=$get(^WWWUSER(0,YUSER,1)) {
		set ^WWWUSER(0,YUSER,1)	= objSession
	}
		
	for intBack=$length(YBACK,","):-1:1 {
		if $piece(YBACK,",",intBack)'="" {
			if $piece(YBACK,",",intBack)'=YFORM {
				set YFORM	= $piece(YBACK,",",intBack)
				set YBACK	= ""
				quit
			}
		}
	}
	set arrParam("YTRAKT")	= YTRAKT
	set arrParam("YBACK")	= ""
	set arrParam("YOPTION")	= 1
	set arrParam("YRETURN")	= 1
	do GoToForm^COMUtilForm(YFORM,YKEY,,$get(YPARA),,,$get(YAUSWAHL),,.arrParam)
	quit ""
	
OnAfterDataFields()
	;-------------------------------------------------------------------------------
	; Show Recent Location Changes
	; 
	; History:
	; 11-Feb-2014	SCR		SESDF-936: Save YPARA & YAUSWAHL
	; 02-Jan-2014	SCR		SESDF-655: Created
	;-------------------------------------------------------------------------------
	new arrQuery
	set arrQuery("Class") = "WWW013Loc"
	set arrQuery("Loop")  = "MostUsedLocLoop^WWW0131WB"
	set arrQuery("Get")   = "set id=$$MostUsedLocData^WWW0131WB(id(1),id(2))"
	set arrQuery("Title") = $$$Text( "WWW00196" ) ;Most Used Locations
	;set arrQuery("Link")  = "@ExecuteOnly@do MostUsedLocLink^WWW0131WB(id(2))~P~2" 
	
	set arrQuery("Col",1) = "P~2~L~~~~~~"	;Location
	set arrQuery("Col",2) = "D~1~C~~~~~~1"  ;Number of Hits
	set arrQuery("Col",3) = "D~2~C~~~~~~1"  ;First Used
	set arrQuery("Col",4) = "D~3~C~~~~~~1"  ;Last Used
	set arrQuery("Col",1,"DoInstead") = "MostUsedLocLink^WWW0131WB"
	set arrQuery("Col",2,"DoInstead") = "MostUsedLocLink^WWW0131WB"
	set arrQuery("Col",3,"DoInstead") = "MostUsedLocLink^WWW0131WB"
	set arrQuery("Col",4,"DoInstead") = "MostUsedLocLink^WWW0131WB"
	
	do Query^COMQuery(.arrQuery,YBED)
	kill ^CacheTempWWWSave(YUSER)
	set:$get(YPARA)'="" ^CacheTempWWWSave(YUSER,"YPARA")		= $Get(YPARA)
	set:$get(YAUSWAHL)'="" ^CacheTempWWWSave(YUSER,"YAUSWAHL")	= $Get(YAUSWAHL)
	quit

MostUsedLocLoop(arrQuery,&parrStorage,pidUser)
	;-------------------------------------------------------------------------------
	; Loop for the COMQuery above - show Location
	; 
	; History:
	; 02-Jan-2014	SCR		SESDF-655: Created
	;-------------------------------------------------------------------------------
	new idLoc,intCount,intLocCount,idxUser
	kill ^mtemp("Query",$job)
	//set idIssue = parrQuery("Parameters","pidIssue")
	
	set intCount = 0
	set intLocCount	= ""
	set idxUser	=	$$$CacheIndex(pidUser)
	if pidUser'="" {
		for {
			set intLocCount = $order(^WWW013Locs(YM,1,intLocCount),-1)
			quit:intLocCount=""
			set idLoc	= ""
			for {
				set idLoc	= $order(^WWW013Locs(YM,1,intLocCount,pidUser,idLoc))
				quit:idLoc=""
				set intCount=intCount+1
				set @parrStorage@(intCount,1) = pidUser
				set @parrStorage@(intCount,2) = idLoc
			}
		}
	}
	quit intCount

MostUsedLocData(pidUser,pidLoc)
	;-------------------------------------------------------------------------------
	; Get Location Data
	; 
	; History:
	; 01-Jan-2014	SCR		SESDF-655: Created
	;-------------------------------------------------------------------------------
	new objChange
	set objChange	= $get(^WWW013Loc(YM,pidUser,pidLoc,1))
	set $$$WWW013LocFirstSelected(objChange)	= $$^WWWTR(0,14,$$$WWW013LocFirstSelected(objChange))
	set $$$WWW013LocLastSelected(objChange)		= $$^WWWTR(0,14,$$$WWW013LocLastSelected(objChange))
	quit objChange
	
MostUsedLocLink(pidLoc,pstrData)
	;-------------------------------------------------------------------------------
	; Change Location Link
	; 
	; History:
	; 02-Jan-2014	SCR		SESDF655: Created
	;-------------------------------------------------------------------------------
	new strArgs,strOutput
	
	new strData	
	
	; Assumes id																		
	set strData=id(2)
	
	set strArgs = "'Change^WWW0131WB','','"_strData_"','"_YBACK_"'"

	set strOutput = "<a href=""#"" onclick=""javascript:CallBack("_strArgs_");"">"
	set strOutput = strOutput_pstrData
	set strOutput = strOutput_"</a>"
	
	quit strOutput

	
		

	
]]></Routine>
</Export>