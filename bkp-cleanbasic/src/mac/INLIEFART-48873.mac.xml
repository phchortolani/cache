<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INLIEFART" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INLIEFART
	#include COMSYS
	#include INConst
	
SupplierItemSelected(pobjResultSet,pidForm="INARTK")
	;-------------------------------------------------------------------------------
	; Routine to run when an item is selected
	;
	; Params: pobjResultSet		: The row set
	;
	; Returns:
	;
	; History:
	; 10-Nov-2006	JW			SR15113: Added form parameter
	; 13-Oct-2006	Steve S		SR15113: Created
	;-------------------------------------------------------------------------------
	new idKey,idItem,idSupplier
 
	if pobjResultSet.Next() {
		set idKey		= $$GetID^COMViewCustom(pobjResultSet)
		set idItem 		= $$$KEY1(idKey)
		set idSupplier 	= $$$KEY2(idKey)
		
		if (idItem'="")&&(idSupplier'="") {
			//do GoToForm^COMUtilForm("INARTK",idKey,,,$$$YES,"INLIEFItems")	
			do GoToForm^COMUtilForm(pidForm,idKey,,,$$$YES,"INLIEFItems")
		}
	}
 
	quit
	
SupplierItems(pidSupplier)
	;-------------------------------------------------------------------------------
	; Routine to run when the Supplier Items button is clicked
	;
	; Params: pidSupplier		: The supplier number
	;
	; Returns:
	;
	; History:
	; 10-Nov-2006	JW			SR15113: Added discount command
	; 13-Oct-2006	Steve S		SR15113: Created
	;-------------------------------------------------------------------------------
	if $$$NoKey(pidSupplier) {
		$$$Alert("IN00328")  ;Supplier Required	
	} else {
		do Initialise^COMViewCustom("INARTK","SupplierItemSelected^INLIEFART")
		do AddFilter^COMViewCustom("P2","=",pidSupplier)
		do AddCommand^COMViewCustom($$$Text(33993),"nachlass.gif","GoToDiscounts^INLIEFART")    ;Discount Scale
		do Start^COMViewCustom()
	}
	
	quit $$$OK
 
 
GoToDiscounts(pobjResultSet)
	;-------------------------------------------------------------------------------
	; Routine to run when the discount button is clicked.
	;
	; Params:	pobjResultSet		: The row set
	;
	; ByRefs:
	;
	; Returns:	boolean - no refresh
	;
	; History:
	; 10-Nov-2006	JW		SR15113: Created
	;-------------------------------------------------------------------------------
	do SupplierItemSelected(pobjResultSet,"INARTKR")
	
	quit $$$NO
]]></Routine>
</Export>