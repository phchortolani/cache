<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINTFR" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINTFR
#include COMSYS
#include INConst
#include VARConst
#include VARLog

#define StatusFullyReversed 1
#define StatusPartlyReversed 2

GetObsRequisicao(pYFELD="")
	new strObs,idRequisition,objRequisition
	set strObs=""
	if $get(pYFELD)="" q strObs
	set idRequisition=$$$INTFRRequisitionNumber(pYFELD)
	if idRequisition'="" {
		set objRequisition=$get(^INReq(YM,idRequisition,1))
		if objRequisition'="" set strObs=$replace($$$INReqFREE11(objRequisition),"""","'")
	}
	quit strObs
	
GetTransport(pYFELD="",pTypeTransport="")
	new idDispatch,idSendLocn,idRecLocn
	set idDispatch=""
	if $get(pTypeTransport)="" q idDispatch
	if $get(pYFELD)="" q idDispatch
	
	set idSendLocn=$$$INTFRSendingLocation(pYFELD)
	set idRecLocn=$$$INTFRReceivingLocation(pYFELD)
	
	if idSendLocn="" q idDispatch
	if idRecLocn="" q idDispatch
	
	if pTypeTransport=1 {
		set idDispatch = $order(^INDRPUMLKWs(0,1,idSendLocn,""))
	}
	if pTypeTransport=2 {
		set idDispatch = $order(^INDRPUMLKWs(0,1,idRecLocn,""))
	}

	quit idDispatch

OnAfterPrimaryKey(pYKEY)
	;if $$$ISOK($$isTransferenciaEstornada(pYKEY)) {
		if ($$CustomReversalStatus(pYKEY) = 1){
			w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:6px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
			w "	<font color=red size=2>"
			w "		Atenção: essa transferência foi totalmente estornada</strong>."
			w "	</font>"
			w "</div>"	
		}
		if ($$CustomReversalStatus(pYKEY) = 2){
			w "<div style=border-color:999999;border-width:1px;border-style=solid;background-color:#E0E0E0;margin-top:6px;margin-bottom:5px;padding-top:4px;padding-right:12px;padding-bottom:4px;padding-left:12px>"
			w "	<font color=red size=2>"
			w "		Atenção: essa transferência foi parcialmente estornada</strong>."
			w "	</font>"
			w "</div>"	
		}
	;}
	
	do UpdateReversalStatus(pYKEY)
	
	quit
	
CanViewReport(pKEY)
	new objINTFR
	
	if $$$NoKey(pKEY) {
		set YQ = $$$YQDisable("Selecione um registro.")
	}else{
		set objINTFR = $get(^INTFR(0,pKEY,1))
		
		if (objINTFR = "") {
			set YQ = $$$YQDisable("Selecione um registro.")
		} else {
			if $$$INTFRStatus(objINTFR) > 1 {
				set YQ = $$$YQEnable
			}else{
				set YQ = $$$YQDisable("A transferência precisa ter sido enviada para que possa gerar o relatório.")
			}
		}
	}
	quit

isTransferenciaEstornada(pidTransfer)
	quit:(pidTransfer = "") $$$NO
	$$$VAR
	
	new objTransfer
	set objTransfer = $get(^INTFR(YM,pidTransfer,1))
	quit:($$$INTFRStatus(objTransfer) '= 2) $$$NO ;enviada
	
	new idTransferLine, baseQuantity, revQuantity, blnFullyReversed
	
	set blnFullyReversed = $$$YES
	
	$$$Order3(^INTFRPickLine,YM,pidTransfer,idTransferLine)

		set objTransferPickLine	= $get(^INTFRPickLine(YM,pidTransfer,idTransferLine,1))
		
		set baseQuantity = +$$$INTFRPickLinePickedQuantity(objTransferPickLine)
		set revQuantity  = +$$$INTFRPickLineReversalQuantity(objTransferPickLine)
		
		if '((revQuantity = baseQuantity)&(revQuantity'=0)) {
			set blnFullyReversed = $$$NO
		}

	$$$End

	quit blnFullyReversed

isTotalmenteAceita(pidINFTR)
	quit:(pidINFTR = "") $$$NO
	new objINTFR, idINTFRLine, objINTFRLine, QuantRecebida, QuantTransferida, QuantEstornada, strStatus
	set objINTFR = $get(^INTFR(YM,pidINFTR,1))
	quit:(($$$INTFRStatus(objINTFR) '= 2)&&($$$INTFRStatus(objINTFR) '= 3)) $$$NO ;enviada
	set strStatus = $$$NO

	set idINTFRLine = ""
	for { 
		set idINTFRLine = $order(^INTFRLine(YM,pidINFTR,idINTFRLine))
		quit:idINTFRLine=""
		set objINTFRLine = ^INTFRLine(YM,pidINFTR,idINTFRLine,1)
		set QuantRecebida = +$$$INTFRLineReceivedQuantity(objINTFRLine)
		set QuantEstornada = +$$$INTFRLineReversalQuantity(objINTFRLine)
		set QuantTransferida = $$PickedQuantity^INRPTransfer(YM,pidINFTR,idINTFRLine)
		if (QuantRecebida+QuantEstornada = QuantTransferida){
			set strStatus = $$$YES
		} else {
			set strStatus = $$$NO
			quit
		}
	}
	
	quit strStatus


ImprimeRelatorio(YKEY)
	if ($$GetPreferencia^VARPreferenciasUsuario($$$FldVARPreferenciasUsuarioTelaFiltroRelatorio) = 1){
		do GotoFiltrosRelatorio^VARFiltrosRelatorio("RunReportNotaTransferencia",0,1,,YKEY)
	} else {
		do RunReportNotaTransferencia^VARJasperRunReport(YKEY)
	}
		
	quit
	
ImprimeRelatorioContabil(YKEY)
	if ($$GetPreferencia^VARPreferenciasUsuario($$$FldVARPreferenciasUsuarioTelaFiltroRelatorio) = 1){
		do GotoFiltrosRelatorio^VARFiltrosRelatorio("RunReportNotaTransferenciaContabil",0,1,,YKEY)
	} else {
		do RunReportNotaTransferenciaContabil^VARJasperRunReport(YKEY)
	}
		
	quit
	
AlertSegundaSeparacao(pidTFR)
	;Karine
		if $data(^INTFRPickLine(0,pidTFR)) { $$$Alert("A lista de separação já foi impressa !")}
		
	quit
	
UpdateReversalStatus(pidTransfer)
	quit:(pidTransfer = "")
	
	new objTransfer,strStatus,newStatus,lastStatus
	
	set strStatus   = $$$OK
	set objTransfer = $get(^INTFR(YM,pidTransfer,1))
	set lastStatus  = $$$INTFRFREE15(objTransfer)
	set newStatus   = $$CustomReversalStatus(pidTransfer)
	
	if (lastStatus '= newStatus) {
		set $$$INTFRFREE15(objTransfer) = newStatus //Salvando status atual do estorno.
		set strStatus = $$$Save("INTFR",pidTransfer,objTransfer,$$$YES)
		do RefreshForm^COMUtilForm()
	}	
	if $$$ISERR(strStatus) {
		$$$StartScript()
		write "alert('Erro ao atualizar o status do estorno da transferencia');"
		$$$EndScript()
		$$$VARLOGERROR(strStatus)
	} 
	
	quit
	
CustomReversalStatus(pidTransfer) 	
 	quit:(pidTransfer = "") ""
	
	$$$VAR 

	new objTransfer
	set objTransfer = $get(^INTFR(YM,pidTransfer,1))
	quit:($$$INTFRStatus(objTransfer) '= 2) "" ;enviada
	
	new idTransferLine, baseQuantity, revQuantity, stReversed, objTransferPickLine, cntFullReverse,
	    cntLines
	
	set cntFullReverse = 0
	set cntLines 	   = 0
	set stReversed	   = ""
	
	$$$Order3(^INTFRPickLine,YM,pidTransfer,idTransferLine)
		set cntLines = $increment(cntLines)
		set objTransferPickLine	= $get(^INTFRPickLine(YM,pidTransfer,idTransferLine,1))
		
		set baseQuantity = +$$$INTFRPickLinePickedQuantity(objTransferPickLine)
		set revQuantity  = +$$$INTFRPickLineReversalQuantity(objTransferPickLine)		
		
		if (revQuantity > 0) {
			if (revQuantity '= baseQuantity) {
				set stReversed = $$$StatusPartlyReversed
				quit
			} else {
				set cntFullReverse = $increment(cntFullReverse)			
			}
		}		
	$$$End
	
	if (cntFullReverse > 0) {
		if (cntLines = cntFullReverse) {
			set stReversed = $$$StatusFullyReversed
		} else {
			set stReversed = $$$StatusPartlyReversed
		}
	}
	
	quit stReversed		
	
OnAfterSaveHook(pYKEY,pYFELD)
	new idTFR,linha,idStorage,idLocation,objINLP,endereco,EnderecoFisico,strStatus,SQLCODE

	set idTFR 	   = $$$KEY1(pYKEY)
	set linha 	   = $$$KEY2(pYKEY)
	set idStorage  = $piece(pYFELD,Y,2)
	set idLocation = ^CacheTempINTFRSendLoc(YUSER)
	
	quit:((idTFR="")||(linha="")||(idLocation="")||(idLocation="")) $$$OK
	
	set strStatus 	    = $$$OK
	set objINLP 	    = $get(^INLP(YM,idLocation,idStorage,1))
	set PhysicalStorage = $$$INLPPhysical(objINLP)
	
	if (PhysicalStorage'=""){
		set EnderecoFisico = $$SQLGetEnderecoParaOrdenar^VARSQL(PhysicalStorage)
	} elseif (PhysicalStorage=""){
		set EnderecoFisico = $$SQLGetEnderecoParaOrdenar^VARSQL($$$INLPStorage1(objINLP))
	}
	
	if (EnderecoFisico = ""){
		quit strStatus
	} elseif(EnderecoFisico '= ""){
		&sql(UPDATE SQLUser.INTFRPickLine
       		SET FREE1 = :EnderecoFisico
       		WHERE Company = 0
       		AND TransferNumber = :idTFR
       		AND TransferPickLineNumber = :linha)
   		IF (SQLCODE = 0) {
			$$$VARLOGINFO("Endereço físico "_EnderecoFisico_" gravado com sucesso por "_YBED)	
   		} ELSE {
    		$$$VARLOGERROR("Erro ao gravar Endereço físico "_EnderecoFisico_" : "_SQLCODE)
    		set strStatus = '$$$OK	 
    	}
	}
	
	quit strStatus
	
]]></Routine>
</Export>