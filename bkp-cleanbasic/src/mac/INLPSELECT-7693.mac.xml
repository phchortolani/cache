<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INLPSELECT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INLPSELECT
	
#include COMSYS
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN LAGERPLÄTZE NACH EINGABE DER BETRIEBSNUMMER
	;
	; Inputs : 
	;Suchen und anzeigen lagerplätze eines betriebes ;Search and Display 
	;	YINHALT		BETRIEB
	;	YLFN		DATEN/PRIMÄRFELDNUMMER
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 24.06.2004	FIS		25984 AUSGESCHALTET
	; 17.07.2001	FIS
	; 02.03.2001	DT
	;-------------------------------------------------------------------------------
	quit:$get(YLFN)=""
	
	new YLFNX,YI
	
	set YFELD=$get(^WWWDATEN(0,+$horolog,YUSER,YFORM,YART,1)) ;DATENSATZ ;data record 
	set %TXT(1)=""
	if YINHALT="" do  quit
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YART_(YLFN+1)_"~REMOVE"  ;LÖSCHEN VOR NEUAUFBAU
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YART_(YLFN+1)_"~"_""_"~"_" "_"~"_0
	
	do
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YART_(YLFN+1)_"~REMOVE"  ;LÖSCHEN VOR NEUAUFBAU
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YART_(YLFN+1)_"~"_""_"~"_" "_"~"_0
	. set YLFNX   = ""
	. for YI=1:1:60 set YLFNX = $order(^INLP(0,YINHALT,YLFNX)) quit:YLFNX=""  do
	. . set %TXT(1) = %TXT(1)_"#Y"_YFORM_YART_(YLFN+1)_"~"_YLFNX_"~"_YLFNX_" - "_$piece($get(^INLP(0,YINHALT,YLFNX,1)),Y,10)_"~"_YI
	
	quit
	
	
SEL(YPLUS,YZIEL)
	;-------------------------------------------------------------------------------
	;WENN DATENFELD LAGERPLATZ NICHT DER BETRIEBSAUSWAHL FOLGT
	;YINHALT=BETRIEB
	;YPLUS=ZAHL, UM DIE HÖHER GEZÄHLT WERDEN MUSS
	;(Z.B.: BETRIEB=D1 / LAGER=D5  =>  YPLUS=4)
	;YZIEL=FELDART (M,D OD. P)
	;-------------------------------------------------------------------------------
	quit:$get(YVAR)'=1  ;NUR 1.SEITE ;only 
	quit:$get(YLFN)="" 
	
	new YLFNX,YI
	
	set YPLUS = $translate(YPLUS,";",",")
	if +YPLUS=0 set YPLUS = 1
	if $get(YZIEL)="" set YZIEL = YART
	set YFELD   = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,YART,1)) ;DATENSATZ ;data record 
	set %TXT(1) = ""
	if YINHALT="" do  quit
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YZIEL_(YLFN+YPLUS)_"~REMOVE"  ;FIS;25984;24.06.04;LÖSCHEN VOR NEUAUFBAU
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YZIEL_(YLFN+YPLUS)_"~"_""_"~"_" "_"~"_0
	
	do  ;IF $DATA(^INLP(0,YINHALT)) DO
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YZIEL_(YLFN+YPLUS)_"~REMOVE"  ;FIS;25984;24.06.04;LÖSCHEN VOR NEUAUFBAU
	. set %TXT(1) = %TXT(1)_"#Y"_YFORM_YZIEL_(YLFN+YPLUS)_"~"_""_"~"_" "_"~"_0
	. set YLFNX=""
	. for YI=1:1:60 set YLFNX = $order(^INLP(0,YINHALT,YLFNX)) quit:YLFNX=""  do
	. . set %TXT(1) = %TXT(1)_"#Y"_YFORM_YZIEL_(YLFN+YPLUS)_"~"_YLFNX_"~"_YLFNX_" - "_$piece($get(^INLP(0,YINHALT,YLFNX,1)),Y,10)_"~"_YI
	
	quit
	
MANU
	;-------------------------------------------------------------------------------
	;MANUELLER AUFRUF (BUTTON) ZUM LADEN DER LAGERPLÄTZE ;charge the 
	;-------------------------------------------------------------------------------
	if $get(YHID)'=2 do  ;ERSTELLEN DES BUTTONS
	. write YCR
	. write "<A HREF="_""""_"#"_""""
	. write " onClick=' if (document."_YHTMFORM_".Y"_YFORM_YART_YLFN_".value != "_""""_""""_") {"
	. write " retval = EventValue("_""""_YUCI_""""_","_""""_YUSER_""""_","_""""_YFORM_""""_","_""""_"FIX"_""""_","_""""_"INLPSELECTMANU"_""""_",document."_YHTMFORM_".Y"_YFORM_YART_YLFN_".value,"_""""_"6"_""""_","_""""_YART_"-"_YLFN_""""_");"
	. write " document."_YHTMFORM_".Y"_YFORM_YART_(YLFN+1)_".focus();"
	. write " } else {"
	. write " alert("_""""_$$^WWWTEXT(33874)_""""_")"  ; "Please Select The Location"
	. write " }'>"
	. write YCR,"<IMG SRC="_""""_YGIF_"platz.gif"_""""_" ALIGN=ABSBOTTOM TITLE="_""""_$$^WWWTEXT(33873)_""""_" border=0>"
	. write "</A>"     ; "Load Storage For This Location"
	
	quit
	
	
	;********************** SUCHFUNKTION  **********************************
	
SUCH(NR1,NR2)
	;-------------------------------------------------------------------------------
	;SUCHFUNKTION
	;NR1: DATEN FELDNUMMER VON BETRIBE 
	;NR2: DATEN FELDNUMMER VON LAP 
	;-------------------------------------------------------------------------------
	quit:+NR1=0
	quit:+NR2=0
	
	do  ;ERSTELLEN DES BUTTONS
	. write YCR,"<A HREF="_""""
	. set URL=YAKTION_"EP=WWWMANU&YEXEC=D|SUCH1^INLPSELECT('"_NR1_"','"_NR2_"')&YUSER="_YUSER_"&YBED="_YBED_"&YTRAKT="_YTRAKT_"&YUCI="_$get(YUCI)_"&YM="_YM_"&YKEY="_YKEY_"&YART="_YART_"&YFORM="_YFORM
	. set OPT="HEIGHT=400,WIDTH=250,SCROLLBARS=YES,RESIZEABLE=YES"
	. write "javascript:var parameter=window.open('"_URL_"','LAGEPLATZ','"_OPT_"');"  
	. write """"
	. write ">"
	. write YCR,"<IMG SRC="_""""_YGIF_"search1.gif"_""""_" ALIGN=ABSBOTTOM TITLE="_""""_$$^WWWTEXT(148)_""""_" border=0>"   ; "Search"
	. write "</A>"
	
	quit
	
	
SUCH1(NR1,NR2)
	;-------------------------------------------------------------------------------
	;ANZEIGEN DER LAGEPLATZ DURCH DIE LUPE
	;NR1: DATEN FELDNUMMER VON BETRIBE 
	;NR2: DATEN FELDNUMMER VON LAP
	;
	; History:
	; 16.01.2002	FAN
	;-------------------------------------------------------------------------------
	new BET,LAP,LAP1
	
	do ^WWWVAR
	set YNOFOOT = 1
	do
	. new YFORM,YKEY,YTRAKT
	. do ^WWWSTART($$^WWWTEXT(32054),,)    ; "Storage"
	
	quit:$get(YFORM)=""                                                                   ; EARLY EXIT
	
	set YART  = $get(%(YQUERY,"YART"))
	set YFELD = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,YART,1)) ;DATENSATZ ;data record 
	if YART="P" {
		set BET = $piece(YFELD,",",NR1)     ;......."P",1)=XX,XX,XX...
	} else {
		set BET = $piece(YFELD,Y,NR1)       ;......."M",1) ODER ....."D",1)=XX~XX~XX...
	}
	if BET="" write $$^WWWTEXT(32310) quit                      ; "No Selection Made"     ; EARLY EXIT
	
	if $piece($get(^WWW120(0,YFORM,1)),Y,123)=1 set YHTMFORM = "WWW2"
	write YCR,"<FONT FACE=ARIAL SIZE=2><B>"
	write YCR,$$^WWWTEXT(388)_": "                              ; "Location"
	write YCR,"("_BET_") "_$piece($get(^WWW0121(0,0,BET,1)),Y,1)
	write YCR,"</B></FONT>"
	write "<HR>"
	write YCR,"<FONT FACE=ARIAL SIZE=2>"
	write "<NOBR>"
	set ANZ = 0
	set LAP = ""
	for  set LAP = $order(^INLP(0,BET,LAP)) quit:LAP=""  do
	. write "&nbsp;"
	. write "<A onClick='return doLink(this)' HREF=""JavaScript:opener.document."_YHTMFORM_".Y"_YFORM_YART_NR2_".value='"_LAP_"';opener.document."_YHTMFORM_".Y"_YFORM_YART_NR2_".focus();document.close();close();"_""""
	. write ">"
	. write LAP
	. set LAP1=$get(^INLP(0,BET,LAP,1))
	. write "  "_$piece(LAP1,Y,10)
	. write "</A>"
	. write "<BR>"
	. write YCR
	. if ANZ=0 set ANZ = 1
	
	if ANZ=0 write $$^WWWTEXT(119)  ; "No Data Record Found" 
	write "&nbsp;"
	write "</FONT>"
	do ^WWWSTOP
	quit
	
LEER(NR2)
	;-------------------------------------------------------------------------------
	;BETRIEB ÄNDERN=LAGPLATZ=""
	;YINHALT=BETRIEB
	;NR2: DATEN FELDNUMMER VON LAP
	;-------------------------------------------------------------------------------
	new YFELD,LAP
	
	quit:$get(YART)=""
	
	set LAP     = ""
	set %TXT(1) = ""
	set YFELD   = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,YART,1))
	if YART="P" {
		set LAP = $piece(YFELD,",",NR2)     ;......."P",1)=XX,XX,XX...
	} else {
		set LAP = $piece(YFELD,Y,NR2)       ;......."M",1) ODER ....."D",1)=XX~XX~XX...
	}
	quit:LAP=""
	quit:$get(YINHALT)=""
	quit:$data(^INLP(0,YINHALT,LAP))  ;LAGERPLATZ VORHANDEN ;stock location on hand 
	
	set %TXT(1) = ""
	set %TXT(1) = %TXT(1)_"#Y"_YFORM_YART_NR2_"~"_""
	quit
	
SELBLEER(NR1)
	;-------------------------------------------------------------------------------
	;LAGPLATZ NICHT IN DIESEM BETRIEB ;Not within location 
	;YINHALT=LAP
	;NR1: FELD NR. BETRIEB ;field location 
	; 
	; History
	; 04-Mar-2014	shobby	CORE-198: YFELD should not be changed here.
	;-------------------------------------------------------------------------------
	new YFELD ;CORE-198
	quit:+NR1=0
	quit:YINHALT=""
	quit:$get(YART)=""
	
	set %TXT(1) = ""
	set BET     = ""
	set YFELD   = $get(^WWWDATEN(0,+$horolog,YUSER,YFORM,YART,1)) ;DATENSATZ ;data record 
	if YART="P" {
		set BET = $piece(YFELD,",",NR1)     ;......."P",1)=XX,XX,XX...
	} else {
		set BET = $piece(YFELD,Y,NR1)       ;......."M",1) ODER ....."D",1)=XX~XX~XX...
	}
	if BET="" {
		set %TXT(1) = %TXT(1)_"§"_$$^WWWTEXT(46)          ; "No Data Available"   KEINE DATEN VORHANDEN
	} else {
		if '$data(^INLP(0,BET,YINHALT,1)) {
			set %TXT(1) = %TXT(1)_"§"_$$^WWWTEXT(46)      ; "No Data Available"   KEINE DATEN VORHANDEN
		}
	}
	quit
	
]]></Routine>
</Export>