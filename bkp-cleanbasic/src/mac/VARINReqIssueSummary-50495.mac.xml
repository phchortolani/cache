<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINReqIssueSummary" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
#include COMSYS
#include COMConst
#include INConst
#include %occInclude
#include WWWConst

VARINReqIssueSummary
	quit

CanPick(pidIssue,pYFELD)
	;-------------------------------------------------------------------------------
	; Description : Determines if Button should be enabled
	;			
	; Called By : INIssue FORMSPEC - Button(2) Pick
	;
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 25-Mar-2013	SCR		HEVA-811: Check for null pidIssue
	; 06-Mar-2013	SCR		HEVA-811: Check BOH.
	; 15-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new strStatus,idLn,objLine,objIssue,idFromLoc,fltSumQty,fltBOHQty,idItem,objVORG
	new objReqLine,idReq,idReqLine,blnLess,blnMore
	
	set strStatus = $$$OK
	
	; HEVA-811 vvvv
	if pidIssue="" {
			$$$YQHandler(strStatus)
			quit
	}
	; HEVA-811 ^^^^

	if ($$$INIssueStatus(pYFELD) > 1) {
			set strStatus = $$$MakeStatus("IN01333")    ; "Invalid Operation for Current Status."			
	}
	
	; HEVA-811 vvvv
	if strStatus = $$$OK {
		set objIssue 	= $get(^INIssue(0,pidIssue,1))
		set idFromLoc	= $$$INIssueFromLocn(objIssue)
		set objVORG		= $get(^INVORG(YM,YM,1))
		set blnMore		= $$$INVORGIssueMorethantheRequested(objVORG)
		set blnLess		= $$$INVORGIssueLessthantheRequested(objVORG)
		set idLn 		= ""
		for {
			
			quit:$$$ISERR(strStatus)
			
			set idLn = $order(^INIssueSummary(0,pidIssue,idLn))
			quit:idLn=""
			
			set objLine 	= $get(^INIssueSummary(0,pidIssue,idLn,1))
			set idItem		= $$$INIssueSummaryItem(objLine)
			set fltSumQty	= $$$INIssueSummaryUnitQuantity(objLine)
			set fltBOHQty 	= ##class(alSOH.iSOH).GetQtyOnHand(idItem,idFromLoc,"",$$$YES) ;dont include blocked items
			set idReq		= $$$KEY1($$$INIssueSummaryReferenceLine(objLine))
			set idReqLine	= $$$KEY2($$$INIssueSummaryReferenceLine(objLine))
			set objReqLine	= $get(^INReqLine(YM,idReq,idReqLine,1))
			set fltReqQty	= $$$INReqLineQtyOrdered(objReqLine) - $$$INReqLineQtyIssued(objReqLine)
			if (fltSumQty > fltReqQty) &&'blnMore {
				set strStatus = $listbuild("IN01406",fltSumQty,fltReqQty,idLn)  ;Quantity (%1) can not be greater thanl Requested Quantity (%2) Line (%3)
			}
			if (fltSumQty < fltReqQty) &&'blnLess {
				set strStatus =  $listbuild("IN01407",fltSumQty,fltReqQty,idLn)  ;Pick Quantity (%1) can not be less than Requested Quantity (%2) Line (%3)
			}
			if (fltSumQty > fltBOHQty) {  
				set strStatus = $listbuild("IN01403",fltSumQty,fltBOHQty,idLn)  ;Quantity (%1) can not be greater than BOH (%2) Line (%3) 
			}
			
			if (idItem '= ""){
				set objINART = $get(^INART(0,idItem,1))
				if (objINART '= "") {
					if $$$INARTHoldReason(objINART) {
						set strStatus = $listbuild("IN01230",idItem,$$$FldINItemStatusIssueGoods)
					}
				}
			}

		}
		
		; MOBILE-52 vvvv
		if ($$$ISOK(strStatus)){
			if $$$INIssueFREE11(objIssue) = 1 {
				set strStatus = $$$MakeStatus("INIssue001")
			}
		}
		; MOBILE-52 ^^^^		
		
	}
	; HEVA-811 ^^^^
	
	$$$YQHandler(strStatus)
	
	quit
]]></Routine>
</Export>