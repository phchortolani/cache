<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INTFRInterface" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INTFRInterface
#include COMSYS
#include INConst
#include WWWConst
 
GetLineForm(pidForm="",pidDefault="")
	;-------------------------------------------------------------------------------
	; Try and find out the name of the line form in a header line arrangement.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 28-Jan-2008	shobby		SRBR014754: Created
	;-------------------------------------------------------------------------------
	;
	new idLineForm
	
	set idLineForm=pidDefault
	if pidForm'="" {
		if $data(^WWW120(0,pidForm_"Linha",1)) {
			set idLineForm=pidForm_"Linha"
		} else {
			set idLineForm=pidForm_"Line"
		}
	}
	quit idLineForm
 
GetSupplyKey(pidSendingLocn,pidDemand,pstrCreator)
	;-------------------------------------------------------------------------------
	; Get the key for the supply using the key to the demand
	;
	; Params:
	; pidSendingLocn - Sending Stock Location
	; pidDemand - Demand Id
	; pstrCreator - OPTIONAL By Reference
	;
	; ByRefs:
	; pstrCreator - if supply was created due to a demand will be "D", otherwise "S"
	;
	; Returns: Receiving Stock Location,Supply id
	;
	; History:
	; 07-Jul-2006	PO		SR14669: Created
	;-------------------------------------------------------------------------------
	new objDemand,idReceivingLocn,idSupply
 
	set objDemand = $get(^INDRPDEMAND(YM,pidSendingLocn,pidDemand,1))
 
	set idReceivingLocn = $$$INDRPDEMANDRequestingLocation(objDemand)
	set idSupply = $$$INDRPDEMANDOrderNo(objDemand)
 
	set pstrCreator = "S"
 
	if idSupply = "" {
		set idSupply = $order(^INDRPSUPPLYs(YM,6,$$$Index(pidSendingLocn),$$$Index(pidDemand),idReceivingLocn,""))
 
		set pstrCreator = "D"
	}
 
	quit idReceivingLocn_$$$COMMA_idSupply
 
GetDemandKey(pidReceivingLocn,pidSupply,&pstrCreator)
	;-------------------------------------------------------------------------------
	; Get the key for the demand using the key to the supply
	;
	; Params:
	; pidReceivingLocn - Receiving Stock Location
	; pidSupply - Supply Id
	; pstrCreator - OPTIONAL By Reference
	;
	; ByRefs:
	; pstrCreator - if demand was created due to a supply will be "S", otherwise "D"
	;
	; Returns: Sending Stock Location,Demand id
	;
	; History:
	; 07-Jul-2006	PO		SR14669: Created
	;-------------------------------------------------------------------------------
	new objSupply,idSendingLocn,idDemand
 
	set objSupply = $get(^INDRPSUPPLY(YM,pidReceivingLocn,pidSupply,1))
 
	set idSendingLocn = $$$INDRPSUPPLYLocation1(objSupply)
	set idDemand = $$$INDRPSUPPLYOrderNo(objSupply)
 
	set pstrCreator = "D"
 
	if idDemand = "" {
		set idDemand = $order(^INDRPDEMANDs(YM,6,$$$Index(pidReceivingLocn),$$$Index(pidSupply),idSendingLocn,""))
 
		set pstrCreator = "S"
	}
 
	quit idSendingLocn_$$$COMMA_idDemand
]]></Routine>
</Export>