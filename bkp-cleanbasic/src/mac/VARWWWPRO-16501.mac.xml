<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARWWWPRO" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARWWWPRO
#include COMSYS
 
PrepareVariables()
	new strClass, dteStart, dteEnd, strUser, idRecord, key, hideFieldID, direction
 
	set strClass 	= $get(VORG(1))
	set dteStart 	= $get(VORG(2))
	set dteEnd   	= $get(VORG(3))
	set strUser  	= $get(VORG(4))
	set idRecord 	= $get(VORG(5))
	set hideFieldID = $get(VORG(6))
	set direction   = $get(VORG(7))
	
	;Padrão é ascendente	
	if direction = "" set direction = 1	
	
	set key = strClass_";"_dteStart_";"_dteEnd_";"_strUser_";"_idRecord_";"_hideFieldID_";"_direction
 
 	do GoToForm^COMUtilForm("VARWWWPRO",key)
 
	quit
 
ShowResults()
	new strClass, pdteStart, pdteEnd, pUsuario, pidRecord, objAntes, objDepois, count,
		hideFieldID, tableDescription, date, hour, usuario, chave, field, direction
	
	set strClass = $get(VORG(1))
	quit:(strClass = "")
 
 	set pdteStart   = $get(VORG(2))	
	set pdteEnd     = $get(VORG(3))
	set pUsuario    = $get(VORG(4))
	set pidRecord   = $get(VORG(5))
	set hideFieldID = $get(VORG(6))
	set direction	= $get(VORG(7))
	
	set count = 0

	set date = ""
	for {
		set date = $order(^WWWPRO(YM,strClass,date),direction)
		quit:(date = "")
		
		if (pdteStart '= "") {
			continue:(date < pdteStart)
		}
			
		if (pdteEnd '= "") {
			continue:(date > pdteEnd)
		}
		
		set hour = ""
		for {
			set hour = $order(^WWWPRO(YM,strClass,date,hour),direction)
			quit:(hour = "")
			
			
			set usuario = ""
			for {
				set usuario = $order(^WWWPRO(YM,strClass,date,hour,usuario))
				quit:(usuario = "")			
				
				continue:(usuario = "UNKNOWN")
				if (pUsuario '= "") {
					continue:(usuario '= pUsuario)					
				}
				
				set chave = ""
				for {
					set chave = $order(^WWWPRO(YM,strClass,date,hour,usuario,chave))
					quit:(chave = "")
					
					if (pidRecord '= "") {
						continue:(chave '= pidRecord)
					}
					
					set objAntes  = $get(^WWWPRO(YM,strClass,date,hour,usuario,chave,0,1))
					set objDepois = $get(^WWWPRO(YM,strClass,date,hour,usuario,chave,1,1))
						
					if $data(^WWW013(YM,usuario)) > 0 {
						set nomeUsuario = usuario_" - "_$piece($get(^WWW013(YM,usuario,1)),Y,1)
						
					} else {
						set nomeUsuario = usuario
					}
						
					set count = $i(count)
 
  					new lstHeader
 					set lstHeader=""
  					set lstHeader=lstHeader_$listbuild("Sit.")

					set field = ""
					for {
						set field = $order(^WWW003(YM,strClass,field))
						quit:(field = "")							
 
						set pieceAntes  = $piece(objAntes,Y,field)
						set pieceDepois = $piece(objDepois,Y,field)
						
						set fieldDescription = $piece($get(^WWW003(YM,strClass,field,1)),Y,1)
							
						if (pieceAntes '= pieceDepois) {
							if (hideFieldID = $$$YES) {
								set lstHeader = lstHeader_$listbuild(fieldDescription)
							} else {
								set lstHeader = lstHeader_$listbuild(fieldDescription_" ("_field_")")
							}
						}
					}
 						
 					set tableDescription =  count_". Registro "_$$Replace^COMUtilStr(chave,",","/")_" - Alterado em "_$zdate(date,4)_" às "_$ztime(hour,2)_" por "_nomeUsuario
 						
					if $$Start^COMTable(lstHeader,tableDescription,,,$$$NO) {
 						
 						do NewLine^COMTable("white")
						do InsertCell^COMTable("Antes")						
						;do InsertCell^COMTable(objAntes,,,,,,,1)
							
						set field = ""
						for {
							set field = $order(^WWW003(YM,strClass,field))
							quit:(field = "")
									
							set pieceAntes  = $piece(objAntes,Y,field)
							set pieceDepois = $piece(objDepois,Y,field)
								
							if (pieceAntes '= pieceDepois) {
								do InsertCell^COMTable("<font color=red>"_$$FormatByFieldType(strClass,field,pieceAntes)_"</font>")
							}																			
						}
 
						do EndLine^COMTable()							
 
						do NewLine^COMTable("white")
						do InsertCell^COMTable("Depois")
						;do InsertCell^COMTable(objDepois,,,,,,,1)

						set field = ""
						for {
							set field = $order(^WWW003(YM,strClass,field))
							quit:(field = "")
									
							set pieceAntes  = $piece(objAntes,Y,field)
							set pieceDepois = $piece(objDepois,Y,field)
								
							if (pieceAntes '= pieceDepois) {
								do InsertCell^COMTable("<font color=red>"_$$FormatByFieldType(strClass,field,pieceDepois)_"</font>")
							}																		
						}
 
						do EndLine^COMTable()
						do Stop^COMTable()						
					}											
 
				}	
			}
		}
	}

	quit
	
	
FormatByFieldType(pClass, pField,pValue)
	quit:( (pClass = "") || (pField = "") || (pValue = "") ) ""
	
	new formattedValue, inputTypeOnClass
	
	set formattedValue = pValue
	
	set inputTypeOnClass = $piece($get(^WWW003(YM,pClass,pField,1)),Y,3)

	;1-Data		
	if (inputTypeOnClass = 1) {
		set formattedValue = $zdate(formattedValue,4)

	;2-Sim/Não
	} elseif (inputTypeOnClass = 2) {
		set formattedValue = formattedValue_" - "_$$SQLGetYesOrNo^VARSQL(formattedValue)
	
	;4-Inteiro ou 12-Flutuante
	} elseif ( (inputTypeOnClass = 4) || (inputTypeOnClass = 12) ) {
		set formattedValue = $$^WWWTR(0,12,formattedValue)

	;6-Texto
	} elseif (inputTypeOnClass = 6) {
		set formattedValue = pValue_$$GetClassRelationDesc(pClass,pField,pValue)
	
	;14-Data/Hora
	} elseif (inputTypeOnClass = 14) {
		set formattedValue = $zdate($piece(formattedValue,",",1),4)_"&nbsp;"_$ztime($piece(formattedValue,",",2))
	}
	
	quit formattedValue
	
	
GetClassRelationDesc(pClass, pField,pValue)
	quit:( (pClass = "") || (pField = "") || (pValue = "") ) ""
	
	new description, classRelation, classRelationKey
	
	set classRelation    = $piece($get(^WWW003(YM,pClass,pField,1)),Y,8)
	set classRelationKey = $piece($get(^WWW003(YM,pClass,pField,1)),Y,9)
	
	quit:(classRelation = "") ""
	
	if (classRelationKey '= "") {
		xecute "set description = $piece($get(^"_classRelation_"("_YM_","_classRelationKey_","""_pValue_""",1)),""~"",1)"
		
	} else {
		xecute "set description = $piece($get(^"_classRelation_"("_YM_","""_pValue_""",1)),""~"",1)"
	}
	
	if ($get(description) '= "") {
		quit " - "_description	
	}
	
	quit description
 
]]></Routine>
</Export>