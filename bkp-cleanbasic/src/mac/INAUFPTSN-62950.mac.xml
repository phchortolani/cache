<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPTSN" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPTSN(YKEY)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SERIENNUMMERN FÜR UNTERTEILESTRUKTUR FESTLEGEN
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 24.12.2003	FIS
	;-------------------------------------------------------------------------------
	SET YFORM="INAUFPTSN"
	SET %("VAR","YFORM")=YFORM
	;SET %("VAR","YAUSWAHL")=YKEY
	DO ^WWWFORM
	QUIT
	
LIST   ;ANZEIGEN ZUORDNUNGSTABELLEN ;display 
	NEW AUF,POS,YDDSATZ,AUF1,POS1
	SET AUF=$GET(AUF)
	IF AUF="" SET AUF=$GET(VORG(1))
	QUIT:AUF=""
	SET POS=$GET(POS)
	IF POS="" SET POS=$GET(VORG(2))
	QUIT:+POS=0
	
	KILL ^WWWSOR(YUSER)
	SET $PIECE(^INUSER(YM,YBED,1),Y,3)=AUF   ;SPEICHERN AUFTRAG IN BEARBEITUNG ;Save order within adaptation 
	SET $PIECE(^INUSER(YM,YBED,1),Y,4)=POS   ;SPEICHERN POSITION IN BEARBEITUNG ;Save within adaptation 
	SET YDDSATZ=0
	DO START1^WWWTAB
	DO NL^WWWTAB
	FOR YI=4,1,5,"S1","S2" DO
	. DO NH^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. IF +YI'=0 WRITE $$^WWWFELDNAME("INAUFPT","D",YI)
	. IF YI="S1" WRITE $$^WWWTEXT(33709)  ;SERIENNUMMER zugeordnet
	. IF YI="S2" WRITE $$^WWWTEXT(33710)  ;SERIENNUMMER eingabaut
	. WRITE "&nbsp;"
	. DO EH^WWWTAB
	
	DO EL^WWWTAB
	DO VOR^INAUFBERECHT       ;ACCAL,ACCRE,ACCKD... VORBEREITEN FÜR BERECHTUNGEN
	DO LIST1(AUF,POS)
	DO STOP^WWWTAB
	KILL ^WWWSOR(YUSER)
	QUIT
	
LIST1(AUF,POS)  ;DURCHLAUF TEILESTRUKTUR
	NEW YDATEI,YDATEI1,YI,YFELD,TEIL,YII,ACC,AUF1,PRODU,POS1
	
	SET AUF=$GET(AUF)       QUIT:AUF=""
	SET POS=$GET(POS)       QUIT:POS=""
	SET TEILKEY=$GET(TEILKEY)
	IF TEILKEY="" SET TEILKEY=0
	
	SET AUF1=$GET(^INAUF(YM,AUF,1))
	SET ACC=$$^INAUFBERECHT(AUF1)     ;=1 BERECHTIGUNG
	SET POS1=$GET(^INAUFP(YM,AUF,POS,1))
	DO NL^WWWTAB
	FOR YII=1:1:5 DO
	. IF YII'=3 DO NFL^WWWTAB
	. IF YII=3 DO NFR^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. WRITE "<B>"
	. IF YII=1 WRITE $PIECE(POS1,Y,4)  ;ARTIKELNUMMER
	. IF YII=2 WRITE $PIECE(POS1,Y,1)  ;BEZEICHNUNG ;notation 
	. IF YII=3 WRITE $$^WWWZAHL($PIECE(POS1,Y,5),0,$LENGTH($PIECE($PIECE(POS1,Y,5),".",2)))  ;MENGE ;quantum  ;quantity 
	. WRITE "&nbsp;"
	. WRITE "</B>"
	. WRITE YCR
	. DO EF^WWWTAB
	
	DO EL^WWWTAB
	
	;SUCHEN TEILE ;seek 
	SET YDATEI="^INAUFPXL("_""""_YM_""""_","_""""_AUF_""""_","_POS
	IF TEILKEY'=0 FOR YI=1:1  QUIT:$PIECE(TEILKEY,",",YI,99)=""  SET YDATEI=YDATEI_","_$PIECE(TEILKEY,",",YI)
	SET YDATEI=YDATEI_")"
	SET YDATEI1=YDATEI
	FOR  DO  QUIT:YDATEI1=""
	. SET YDATEI1=$QUERY(@YDATEI1)
	. IF $TRANSLATE($PIECE(YDATEI1,",",2),"""")'=$TRANSLATE(AUF,"""") SET YDATEI1="" QUIT  ;FALSCHER AUFTRAG ;order 
	. IF $PIECE(YDATEI1,",",3)'=POS SET YDATEI1="" QUIT  ;FALSCHE POSITION
	. SET TEIL=$TRANSLATE($PIECE(YDATEI1,",",4,99),",)","..")
	. IF TEILKEY'=0 IF $PIECE($PIECE(YDATEI1,",",4,4+$LENGTH(TEILKEY,",")),")",1)'=TEILKEY SET YDATEI1="" QUIT
	. SET YFELD=@YDATEI1
	. ;
	. IF $PIECE(YFELD,Y,4)="" QUIT  ;KEIN ARTIKEL ;no item 
	. IF $PIECE(YFELD,Y,26)=2 QUIT  ;LEISTUNG ;performance 
	. ;IF $PIECE(YFELD,Y,40)'=1 QUIT  ;NUR WENN STÜCK, DA SN-BUCHUNG IMMER 1 !!!
	. IF $PIECE($GET(^INART(YM,$PIECE(YFELD,Y,4),1)),Y,56)'=1 QUIT  ;ARTIKEL OHNE S/N ;item without 
	. IF $DATA(@YDATEI1)=10!($DATA(@YDATEI1)=11) QUIT  ;FERTIGUNG, DA UNTERTEILE
	. ;
	. SET PRODU=0  ;PRODUKTION ABGESCHLOSSEN ;production 
	. IF $PIECE(YFELD,Y,165)'="" IF $PIECE($GET(^INPROSTEMP1(YM,$PIECE(YFELD,Y,165),1)),Y,22)'="" SET PRODU=1
	. IF $PIECE(YFELD,Y,165)'="" IF $PIECE($GET(^INPROSTEMP(YM,$PIECE(YFELD,Y,165),1)),Y,22)'="" SET PRODU=1
	. IF $PIECE(YFELD,Y,165)'="" IF $PIECE($GET(^INPROSTEMP1(YM,$PIECE(YFELD,Y,165),1)),Y,24)=1 SET PRODU=1
	. IF $PIECE(YFELD,Y,165)'="" IF $PIECE($GET(^INPROSTEMP(YM,$PIECE(YFELD,Y,165),1)),Y,24)=1 SET PRODU=1
	. IF $PIECE(YFELD,Y,244)'="" SET PRODU=1
	. ;
	. DO NL^WWWTAB
	. DO NFL^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. IF TEIL'="" FOR YI=0:1:$LENGTH(TEIL,".")-2 WRITE "&nbsp;&nbsp;&nbsp;"
	. WRITE "|_"
	. IF TEIL'="" WRITE "<A HREF="_""""_"#"_""""_" TITLE="_""""_$$^WWWTEXT(32085)_": "_TEIL_""""_">"  ;TEILESTRUKTUR
	. WRITE $PIECE(YFELD,Y,4)  ;ARTIKELNUMMER
	. IF TEIL'="" WRITE "</A>"
	. WRITE "&nbsp;"
	. WRITE YCR
	. DO EF^WWWTAB
	. ;
	. DO NFL^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. WRITE $PIECE(YFELD,Y,1)  ;BEZEICHNUNG ;notation 
	. IF $PIECE(YFELD,Y,165)'="" WRITE "<br>"_$$^WWWFELDNAME("INAUFPT","D",165)_": "_$PIECE(YFELD,Y,165)  ;FERTIGUNGSNUMMER
	. WRITE "&nbsp;"
	. WRITE YCR
	. DO EF^WWWTAB
	. ;
	. DO NFR^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWZAHL($PIECE(YFELD,Y,39),0,$LENGTH($PIECE($PIECE(YFELD,Y,39),".",2)))
	. WRITE "&nbsp;"
	. ;IF $PIECE(YFELD,Y,40)'="" DO
	. . ;WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(YFELD,Y,40),1)),Y,1)
	. . WRITE $PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,$PIECE(YFELD,Y,40),1)),Y,1)      ;BEC;25866;07.06.04;DA ZENTRALE DATEI
	. . WRITE "&nbsp;"
	. ;
	. WRITE YCR
	. DO EF^WWWTAB
	. ;
	. DO NFL^WWWTAB
	. WRITE "<FONT SIZE=2>"
	. ;
	. ;---------------------------- AUSWAHL VOR PRODUKTION -----------------------------
	. IF PRODU=0 DO
	. . KILL ^WWWSOR(YUSER,"SN")
	. . ;
	. . ;SERIENNUMMER-AUSWAHLFELD
	. . WRITE YCR,"<select NAME="_""""_"SN"_$TRANSLATE(TEIL,".","_")_""""
	. . WRITE " size=5 multiple="_""""_"multiple"_""""
	. . WRITE " style="_""""_"padding-top:0; padding-bottom:0; width:180px;"_""""   ;style
	. . WRITE " tabindex="_YDDSATZ
	. . DO ^WWWEVENTCALL("SN"_$TRANSLATE(TEIL,".","_"),"",6)
	. . WRITE ">"
	. . WRITE YCR,"<option value="_""""_""""_">&nbsp;</option>"
	. . ;
	. . ;SUCHEN SERIENNUMMERN ;seek 
	. . DO
	. . . NEW BET,LAP,WED,SN,MNG
	. . . FOR BET=YLOCATION,"" DO
	. . . . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,67)'=1 QUIT:BET'=YLOCATION  ;ZUGRIFF AUF BESTÄNDE ANDERER BETRIEBE NICHT ERLAUBT ! ;upon other Not permissive 
	. . . . SET BET(1)=BET
	. . . . DO:BET(1)'=""  IF BET(1)="" FOR  SET BET(1)=$ORDER(^INWE(YM,$PIECE(YFELD,Y,4),BET(1))) QUIT:BET(1)=""  DO
	. . . . . IF BET="" QUIT:BET(1)=YLOCATION  ;BEREITS DURCHLAUFEN ;yet 
	. . . . . SET LAP="" FOR  SET LAP=$ORDER(^INWE(YM,$PIECE(YFELD,Y,4),BET(1),LAP)) QUIT:LAP=""  DO
	. . . . . . IF $PIECE($GET(^INVORG(YM,YM,1)),Y,157)=1 QUIT:$PIECE($GET(^INLP(YM,BET(1),LAP,1)),Y,8)=1  ;KEIN ZUGRIFF AUF GESPERRTEN LAGERPLATZ ;no upon stock location 
	. . . . . . SET WED=""
	. . . . . . FOR  SET WED=$ORDER(^INWE(YM,$PIECE(YFELD,Y,4),BET(1),LAP,WED)) QUIT:WED=""  DO
	. . . . . . . SET MNG=$PIECE($GET(^INWE(YM,$PIECE(YFELD,Y,4),BET(1),LAP,WED,1)),Y,4)  ;VERFÜGBARE MENGE ;quantity 
	. . . . . . . QUIT:MNG'>0
	. . . . . . . ;
	. . . . . . . SET SN=""
	. . . . . . . FOR  SET SN=$ORDER(^INWES(YM,$PIECE(YFELD,Y,4),BET(1),LAP,WED,SN)) QUIT:SN=""  DO  QUIT:MNG'>0
	. . . . . . . . SET STATUS=0  ;OFFEN ;vulnerable 
	. . . . . . . . IF $DATA(^INAUFPTSNs(YM,1,$$^WWWUMLAU(SN,1))) SET STATUS=1  ;ZUGEORDNET
	. . . . . . . . IF $DATA(^INAUFPTSN(YM,AUF,POS,TEIL,SN)) SET STATUS=2  ;FÜR DIESES TEIL ZUGEORDNET ;to this part 
	. . . . . . . . SET ^WWWSOR(YUSER,"SN",STATUS,SN)=""
	. . . . . . . . SET MNG=MNG-1
	. . . ;
	. . . FOR STATUS=2,0,1 DO
	. . . . SET SN=""
	. . . . FOR  SET SN=$ORDER(^WWWSOR(YUSER,"SN",STATUS,SN)) QUIT:SN=""  DO
	. . . . . WRITE YCR,"<option value="_""""_SN_""""
	. . . . . IF STATUS=2 WRITE " selected="_""""_"selected"_""""   ;FÜR DIESES TEIL ZUGEORDNET ;to this part 
	. . . . . IF STATUS=1 WRITE " style="_""""_"color:"_YRED_";"_""""  ;ANDERWEITIG ZUGEORDNET
	. . . . . WRITE ">"_SN_"</option>"
	. . . ;
	. . . WRITE YCR,"</select>"
	. . ;
	. . DO  ;SN ERFASSEN ;Edit 
	. . . NEW YKEY
	. . . WRITE YCR,"<A HREF="_""""_YAKTION_"EP=WWWMANU&YEXEC=*DO|BESTANZ^INAUFPTSN('"_$PIECE(YFELD,Y,4)_"')"  ;LINK BESTAND
	. . . DO ^WWWCGI
	. . . WRITE """"
	. . . WRITE " TITLE="_""""_$$^WWWTEXT(32121)_""""  ;ANZEIGEN ARTIKELBESTAND ;display 
	. . . WRITE ">"
	. . . ;WRITE "<img src="_""""_YGIF_"seriennr-b.gif"_""""_"border=0>"
	. . . WRITE "<img src="_""""_YGIF_"artikel	.gif"_""""_"border=0>"
	. . . WRITE "</A>"
	. . ;
	. . WRITE YCR,"</select>"
	. . DO ^WWWFORM73(,,"SN"_$TRANSLATE(TEIL,".","_"),1)
	. . KILL ^WWWSOR(YUSER,"SN")
	. ;
	. ;---------------------------- ANZEIGE NACH PRODUKTION -----------------------------
	. IF PRODU=1 DO
	. . NEW MESS
	. . SET SN=""
	. . FOR  SET SN=$ORDER(^INAUFPTSN(YM,AUF,POS,TEIL,SN)) QUIT:SN=""  DO
	. . . WRITE SN
	. . . SET MESS=+$PIECE($GET(^INAUFPTSN(YM,AUF,POS,TEIL,SN,1)),Y,3)
	. . . WRITE "&nbsp;"
	. . . WRITE "<FONT"
	. . . IF MESS>1 WRITE " COLOR="_YRED
	. . . WRITE " SIZE=1>"
	. . . ;WRITE "("_$PIECE($GET(^WWW101(0,"SN-VERMERK",SPRACHE,MESS,1)),Y,1)_")"
	. . . WRITE "("_$PIECE($GET(^WWW101(0,"SN-VERMERK",SPRACHE,MESS,1)),Y,1)_")"     ;BEC;25866;07.06.04;DA ZENTRALE DATEI
	. . . WRITE "</FONT>"
	. . . IF $ORDER(^INAUFPTSN(YM,AUF,POS,TEIL,SN))'="" WRITE "<BR>"
	. . ;
	. . WRITE "&nbsp;"
	. ;
	. DO EF^WWWTAB
	. ;---------------------------- ANZEIGE EINGEBAUTE SN -----------------------------
	. DO
	. . DO NFL^WWWTAB
	. . WRITE "<FONT SIZE=2>"
	. . ;
	. . NEW BET,LAP,WED,SN,YI,FIRST
	. . SET FIRST=0
	. . SET BET=""
	. . FOR  SET BET=$ORDER(^INWEAUFPT(YM,AUF,POS,TEIL,BET)) QUIT:BET=""  DO
	. . . SET LAP=""
	. . . FOR  SET LAP=$ORDER(^INWEAUFPT(YM,AUF,POS,TEIL,BET,LAP)) QUIT:LAP=""  DO
	. . . . SET WED=""
	. . . . FOR  SET WED=$ORDER(^INWEAUFPT(YM,AUF,POS,TEIL,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . . SET SN=$TRANSLATE($PIECE($GET(^INWEAUFPT(YM,AUF,POS,TEIL,BET,LAP,WED,1)),Y,5),",|",";;")
	. . . . . FOR YI=1:1  QUIT:$PIECE(SN,";",YI,999)=""  IF $PIECE(SN,";",YI)'="" DO
	. . . . . . IF FIRST'=0 WRITE "<BR>"
	. . . . . . WRITE $PIECE(SN,";",YI)
	. . . . . . SET FIRST=FIRST+1
	. ;
	. WRITE "&nbsp;"
	. WRITE YCR
	. DO EF^WWWTAB
	. DO EL^WWWTAB
	
	QUIT
	
SAVE	;SAVE SELECTION
	NEW SN,TEIL,YI,AUF,POS
	SET AUF=$GET(VORG(1))
	;SET AUF=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"M",1)),Y,1)
	SET POS=$GET(VORG(2))
	;SET POS=$PIECE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"M",1)),Y,2)
	IF AUF'="" IF POS'="" DO
	. ;SET TEIL(1)="SN" FOR  SET TEIL(1)=$ORDER(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",TEIL(1))) QUIT:TEIL(1)=""  QUIT:$EXTRACT(TEIL(1),1,2)'="SN"  DO
	. SET TEIL(1)="SN" FOR  SET TEIL(1)=$ORDER(%(YQUERY,TEIL(1))) QUIT:TEIL(1)=""  QUIT:$EXTRACT(TEIL(1),1,2)'="SN"  DO
	. . SET TEIL(2)=$TRANSLATE($EXTRACT(TEIL(1),3,999),"_",".")
	. . DO ^WWWSKILL("INAUFPTSN",AUF_","_POS_","_TEIL(2))  ;LÖSCHEN ALTE AUSWAHL ;Delete Selection 
	. . ;SET SN(1)=$TRANSLATE($GET(^WWWDATEN(YM,+$HOROLOG,YUSER,YFORM,"V",TEIL(1),1)),",",";")
	. . SET SN(1)=$TRANSLATE($GET(%(YQUERY,TEIL(1))),",",";")
	. . FOR YI=1:1  QUIT:$PIECE(SN(1),";",YI,999)=""  SET SN(2)=$PIECE(SN(1),";",YI) IF SN(2)'="" DO
	. . . NEW YFORM,YVOR,YOK,YKEY,YFELD
	. . . SET YKEY=AUF_","_POS_","_TEIL(2)_","_SN(2)
	. . . SET YFELD=+$HOROLOG_Y_YBED
	. . . SET YOK=$$^WWWSPEI("INAUFPTSN",YKEY,YFELD,1)  ;SPEICHERN NEUE AUSWAHL ;Save Selection 
	. . . QUIT
	. . QUIT
	. QUIT
	NEW YKEY,YBACK
	SET YKEY=AUF_","_POS
	SET %("VAR","YKEY")=YKEY
	SET %("VAR","YBACK")="INAUFP,"
	DO ^WWWFORM
	QUIT
	
BESTANZ(ART)	  ;BESTANDSANZEIGE
	NEW YKEY,YFORM
	SET YKEY=$GET(ART)  ;ITEM
	SET YFORM="INBESTANZ"
	SET %("VAR","YKEY")=YKEY
	SET %("VAR","YFORM")=YFORM
	SET %("VAR","YPARA")="SN"
	SET $PIECE(^INUSER(YM,YBED,1),Y,2)=YKEY   ;AUS VORGABE ;out of default 
	DO ^WWWFORM
	QUIT
]]></Routine>
</Export>