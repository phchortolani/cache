<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINWEINVELINE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINWEINVELINE
#include COMSYS
#include INConst
#include VARConst
#include COMGridEdit31
	quit

OnBeforeSaveHook(pYKEY, &pYFELD)
	if ($length($$$INWEINVFREE3(pYFELD)) > 0) { // se é uma linha que foi gerada por um usuário
		set idStocktake = $$$KEY1(pYKEY)
		set idCount = $$GetCount^INWEINVEHeader()
		set idUser = $$$INWEINVFREE3(pYFELD)
		set idLinha = $order(^VARInventarioMultDigLinhas(0, 2, 
															$$$Index($$$INWEINVItemNumber(pYFELD)), 
															$$$Index($$$INWEINVLotNo(pYFELD)), 
															$$$Index($$$INWEINVExpiryDate(pYFELD)), 
															$$$Index($$$INWEINVStockLocation(pYFELD)), 
															$$$Index($$$INWEINVProgram1(pYFELD)), 
															idStocktake, 
															idCount, 
															idUser,
															"" ))
		set $$$INWEINVFREE1(pYFELD) = $horolog
		set $$$INWEINVFREE2(pYFELD) = YBED
		do ConfirmSynchronize^VARInventarioMultDig(idStocktake, idCount, idUser, idLinha, "VARInventarioMultDigLinha","",pYFELD)
	}
	quit

OnBeforeDeleteHook(pYFELD)
	if ($length($$$INWEINVFREE3(pYFELD)) > 0 ) {
		quit $$$MakeStatus("Essa linha não pode ser excluída pois foi criada a partir da contagem do usuário "_$$$INWEINVFREE3(pYFELD)_".")
	}	
	quit $$$OK

OnBlur(pYBBN="",pYKEY="",pYFELD="")
	do OnBlur^INWEINVELINE(pYBBN,pYKEY,.pYFELD)

	quit:(($length($get(pYFELD)) = 0) || ($length($get(pYKEY)) = 0))
	new idProduto, line, objLine, idInventario
	set idInventario = $$$KEY1(pYKEY)
	set idLinha = $$$KEY2(pYKEY)
	set idProduto = $$$INWEINVItemNumber(pYFELD)
	set idLocalInventario = $$GetLocalInventario^VARInventarioMultDig(idInventario)
	if ($length(idProduto) > 0) {
		if ($$SQLGetTipoEstoqueLocal^VARSQL(idLocalInventario) '= $$SQLGetEstoquePrincipal^VARSQL(idProduto)) {
			$$$Alert("Esse produto é de "_$$SQLGetEstoquePrincipal^VARSQL(idProduto)_" e o inventário é de "_$$SQLGetTipoEstoqueLocal^VARSQL(idLocalInventario)_", por isso não pode ser inserido.")
			set $$$INWEINVItemNumber(pYFELD) = ""
			$$$GRIDUpdateManualField(idLinha,26,"")
			quit
		}
		
		if (+$$SQLGetCustoMedioProd^VARSQL(idProduto) <= 0) {
			$$$Alert("Esse produto não possui custo médio e não pode ser inserido no inventário. Favor entrar em contato com o suporte")
			set $$$INWEINVItemNumber(pYFELD) = ""
			$$$GRIDUpdateManualField(idLinha,26,"")
			quit
		}

		set line = $order(^INWEINV(YM,idInventario,""))
		while ($length(line) > 0) { 
			set objLine = $$GetStocktakeLine^INStocktaking(idInventario,line)
			quit:($$$INWEINVItemNumber(objLine) = idProduto)
			set line = $order(^INWEINV(YM,idInventario,line))
		}
	
		if ($length(line) = 0) {
			$$$Alert("Atenção: esse produto não faz parte da contagem deste inventário. Certifique-se de que as informações estão corretas. Qualquer dúvida, favor entrar em contato com o suporte.")
		}
		set idGrupo = $$SQLGetGrupoItem^VARSQL(idProduto)
		$$$GRIDUpdateManualField(idLinha,26,"("_idGrupo_") "_$$SQLGetDescGrupoItem^VARSQL(idGrupo)) // preenche o campo grupo
		
	}

	quit

isDuplicado(pIdInventario, pIdLinha)
	new pObjINWEINVELINE, retVal, idLinha, objINWEINVELINE
	set retVal = $$$NO
	set pObjINWEINVELINE = $$$GRIDGetYFELD(pIdInventario_$$$COMMA_pIdLinha)
	set idLinha = $order(^WWWDATEN(0,+$horolog,YUSER,$$$GRIDName,"V","REFERENCEROW",""))
	while ($length(idLinha) > 0) {
		if (idLinha '= pIdLinha) {
			if ($$$ViewableRow(idLinha,$$$GRIDName)) {
				set objINWEINVELINE = $$$GRIDGetYFELD(pIdInventario_$$$COMMA_idLinha)
				if ($$OnlyTrackPrograms^INBundleUtils($$GetLocalInventario^VARInventarioMultDig(pIdInventario))=0) { //Se o local controla lote e validade
				    if (($$$INWEINVItemNumber(objINWEINVELINE) = $$$INWEINVItemNumber(pObjINWEINVELINE))
				    	&& ($$$INWEINVLotNo(objINWEINVELINE) = $$$INWEINVLotNo(pObjINWEINVELINE))
				    	&& ($$$INWEINVExpiryDate(objINWEINVELINE) = $$$INWEINVExpiryDate(pObjINWEINVELINE))
				    	&& ($$$INWEINVProgram1(objINWEINVELINE) = $$$INWEINVProgram1(pObjINWEINVELINE))
				    	&& ($$$INWEINVStockLocation(objINWEINVELINE) = $$$INWEINVStockLocation(pObjINWEINVELINE))) {
					    set retVal = $$$YES
				    }
				}
				else {
				    if (($$$INWEINVItemNumber(objINWEINVELINE) = $$$INWEINVItemNumber(pObjINWEINVELINE))
				    	&& ($$$INWEINVProgram1(objINWEINVELINE) = $$$INWEINVProgram1(pObjINWEINVELINE))
				    	&& ($$$INWEINVStockLocation(objINWEINVELINE) = $$$INWEINVStockLocation(pObjINWEINVELINE))) {
					    set retVal = $$$YES
				    }
				}
				quit:(retVal)
			}
		}
		set idLinha = $order(^WWWDATEN(0,+$horolog,YUSER,$$$GRIDName,"V","REFERENCEROW",idLinha))
	}
	quit retVal

OnBeforeSaveHeader()
	new strStatus, idInventario, idLinha, objINWEINVELINE
	set strStatus = $$$OK
	
	set idInventario = $$$KEY1(YKEY)
	set idLinha = $order(^WWWDATEN(0,+$horolog,YUSER,$$$GRIDName,"V","REFERENCEROW",""))
	while ($length(idLinha) > 0) {
		if ($$$ViewableRow(idLinha,$$$GRIDName)) {
			set objINWEINVELINE = $$$GRIDGetYFELD(idInventario_$$$COMMA_idLinha)
			if ($length(objINWEINVELINE) > 0) {
			    if ($$isDuplicado(idInventario, idLinha)) {
				    set strMsg = "O produto '"_$$$INWEINVItemNumber(objINWEINVELINE)
					if ($$OnlyTrackPrograms^INBundleUtils($$GetLocalInventario^VARInventarioMultDig(idInventario))=0) { //Se o local controla lote e validade
					    if ($length($$$INWEINVLotNo(objINWEINVELINE)) > 0) {
					    	set strMsg = strMsg_"', lote '"_$$$INWEINVLotNo(objINWEINVELINE)
					    }
					    if ($length($$$INWEINVExpiryDate(objINWEINVELINE)) > 0) {
					    	set strMsg = strMsg_"', validade '"_$$SQLGetDataFormatada^VARSQL($$$INWEINVExpiryDate(objINWEINVELINE))
					    }
					}
				    if ($length($$$INWEINVStockLocation(objINWEINVELINE)) > 0) {
				    	set strMsg = strMsg_"', endereço '"_$$$INWEINVStockLocation(objINWEINVELINE)
				    }
				    if ($length($$$INWEINVProgram1(objINWEINVELINE)) > 0) {
				    	set strMsg = strMsg_"', programa '"_$$$INWEINVProgram1(objINWEINVELINE)
				    }
			    
				    set strMsg = strMsg_"'  está repetido nessa contagem."
			    
					set strStatus = $$$MakeStatus("%1", strMsg)
			        quit
			    }
		    }
		    
		}
		set idLinha = $order(^WWWDATEN(0,+$horolog,YUSER,$$$GRIDName,"V","REFERENCEROW",idLinha))
	}
	
    if ($$$ISOK(strStatus)) {
		set strStatus = $$$GRIDSave(YKEY)
    }
	
	quit strStatus
]]></Routine>
</Export>