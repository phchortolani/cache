<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPOS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPOS(YAUFTRAG,YARTIKEL,YMENGE,YWH,YSONDERVK,YKUNDETEXT)
	/*------------------------------------------------------------------------------
	; Description of Function :
	;		ANLEGEN AUFTRAGSPOSITION
	;		Order Line Creation
	;
	;	SET YPOS=$$^INAUFPOS(YAUFTRAG,YARTIKEL,YMENGE,YWH,YSONDERVK)
	;	RÜCKGABEWERT = ""    KEINE POSITION ODER POSITIONSNUMMER (BEI BESTELLUNG)
	;	RETURN VALUE = ""    NO Line OR ITEM NUMBER (WHEN ORDERING)
	; 
	; Called By :
	;	Routines 	INANG, INAUF, INAUFANLAGE, INAUFPOS, INAUFPT, INDCMLayby,
	;				INEDIORDER, INKONFWWW, INPANDOJOB
	;	Forms		?
	; 
	; Inputs : 
	;	YAUFTRAG	= AUFTRAGSNUMMER (NUR WENN BESTELLUNG) ;when 
	;	YARTIKEL	= ARTIKELNUMMER
	;	YMENGE		= MENGE 
	;	YWH			= WARENHERKUNFT 2 (LAGERBESTAND) ODER 1 (BESTELLUNG)  ODER "" (OFFEN)
	;	YSONDERVK	= SONDER-VERKAUFSPREIS
	;	YKUNDETEXT	= alternativer Kundentext FÜR ZUSTAZARTIKEL   ;FAN;13.04.04;25518
	;
	; ByRef :
	;
	; Returns : idOrderLine
	;
	; History :
	; 04-Oct-2006	GRF		Doco
	; 26-Sep-2006	GRF		Avoid second call to ^INAUFKOST
	; 30-Aug-2006	GRF		Doco; quits
	; 07-Mar-2006	GRF		Doco; split copy line for readability
	; 04.01.2001	DT
	;-----------------------------------------------------------------------------*/
	NEW ART,ARTIKEL1,curReturnedCost,KUNDE,LIEFER,POS,SATZ,SATZ1,YA,YFELD,YFORM,YOK,YPOS
	
	SET YOK=""
	SET YKUNDETEXT = $GET(YKUNDETEXT)   ;alternativer Kundentext FÜR ZUSATZARTIKEL   ;FAN;13.04.04;25518
	SET YMENGE	   = $GET(YMENGE)
	SET YAUFTRAG   = $GET(YAUFTRAG)     ;AUFTRAG  ;order 
	SET YFORM	   = "INAUFP"           ;POSITIONEN
	
	FOR YPOS=1:1 QUIT:'$DATA(^INAUFP(YM,YAUFTRAG,YPOS))  ;NEUE AUFTRAGSPOSITION
	
	SET ART=$GET(YARTIKEL)  ;ARTIKELNUMMER 
	QUIT:ART="" YOK
	QUIT:'$DATA(^INART(YM,ART,1)) YOK  ;ARTIKEL NICHT VORHANDEN ;item Not on hand 
	
	SET SATZ1=$GET(^INART(YM,ART,1))  ;ARTIKELDATEN
	IF YKUNDETEXT'="" SET $PIECE(SATZ1,Y,10)=YKUNDETEXT         ;alternativer Kundentext FÜR ZUSATZARTIKEL   ;FAN;13.04.04;25518
	SET SATZ=""
	SET KUNDE=""
	IF $PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,2)=0 SET KUNDE=$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,1)
	;DATEN AUS ARTIKEL ÜBERTRAGEN;Messagebox MB_OK "$delete_apache";bec;25600;feld 356 ergäntzt
	; Copy INART record
	;FOR YA=1,2,3,10,14,15,16,17,26,30,31,32,33,36,37,38,40,41,42,43,44,45,47,48,49,50,53,54,55,57,58,59,62,66,68,70,73,74,75,126,130,131,132,134,136,158,166,171,172,174,178,179,180,181,182,183,184,185,191,192,210,211,356 SET $PIECE(SATZ,Y,YA)=$PIECE(SATZ1,Y,YA)
	FOR YA=1,2,3,10,14:1:17,26,30:1:33,36,37,38,40:1:45,47:1:50,53:1:55,57:1:59,62,66,68       SET $PIECE(SATZ,Y,YA)=$PIECE(SATZ1,Y,YA)
	FOR YA=70,73,74,75,126,130:1:132,134,136,158,166,171,172,174,178:1:185,191,192,210,211,356 SET $PIECE(SATZ,Y,YA)=$PIECE(SATZ1,Y,YA)
	FOR YA=178,179,180,181 IF $PIECE(SATZ1,Y,YA)=1 SET $PIECE(^INAUF(YM,YAUFTRAG,1),Y,YA)=1  ;NEU BERECHNEN ;recent calculate 
	IF $PIECE(SATZ1,Y,46)'="" SET $PIECE(SATZ,Y,40)=$PIECE(SATZ1,Y,46)  ;ANDERE VERKAUFSEINHEIT ALS EINHEIT ;when unity 
	SET $PIECE(SATZ,Y,47)=$PIECE(SATZ,Y,47)*YMENGE/$$^INQTYUNIT(ART)  ;+$P(SATZ,Y,71)  ;HERSTELLKOSTEN+RUESTPREIS
	SET $PIECE(SATZ,Y,74)=$PIECE(SATZ,Y,74)*$$^INPROLOS(ART,YMENGE)   ;WERKZEUGKOSTEN 
	SET $PIECE(SATZ,Y,75)=$PIECE(SATZ,Y,75)*$$^INPROLOS(ART,YMENGE)   ;SONSTIGEKOSTEN
	SET $PIECE(SATZ,Y,5)=+$GET(YMENGE)
	SET $PIECE(SATZ,Y,4)=ART
	IF $GET(YWH)=2 DO  QUIT YOK  ;AUS LAGERBESTAND KEINE ANLAGE EINES NEUEN AUFTRAGES ;out of no layout 
	. NEW BET,LAP,WED,WEN 
	. SET REST=$PIECE(SATZ,Y,5)                      ;MENGE   ;quantity 
	. SET YOK=$$^INARTMINUS(YAUFTRAG,YPOS,ART,REST)  ;BESTAND UMBUCHEN ;rebook 
	
	;SET $PIECE(SATZ,Y,7)=YWH  ;WENN LEISTUNG DANN DIENSTLEISTUNG UND LEISTUNGSFÄHIG
	IF $GET(YWH)=4 SET $PIECE(SATZ,Y,7)=YWH  ;WENN LEISTUNG DANN DIENSTLEISTUNG UND LEISTUNGSFÄHIG;FIS;28.09.04;26470
	SET $PIECE(SATZ,Y,22)=$HOROLOG   ;ERFASST
	SET $PIECE(SATZ,Y,23)=YBED       ;ERFASST
	SET $PIECE(SATZ,Y,24)=""         ;ÄNDERUNG  ;alteration 
	SET $PIECE(SATZ,Y,25)=""         ;MITARB
	SET $PIECE(SATZ,Y,116)=$PIECE(SATZ1,Y,86)  ;AUFSCHLAG ;overcharge 
	;SET $PIECE(SATZ,Y,118)=$PIECE(SATZ1,Y,88)  ;VK
	SET $PIECE(SATZ,Y,120)=$PIECE(SATZ1,Y,90)  ;PREISKZ
	IF $GET(KUNDE)'="" IF $PIECE($GET(^INKUNDE(YM,KUNDE,1)),Y,221)'="" SET $PIECE(SATZ,Y,120)=$PIECE(^(1),Y,221)   ;PREISKZ DES KUNDEN;FIS;24405;04.11.03
	SET $PIECE(SATZ,Y,118)=$$^INSALESPRICE($PIECE(SATZ,Y,4),,$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,6),$PIECE(SATZ,Y,120),$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,1))  ;VK;FIS;03.06.03;23477;PREISE NEU AUS INSALESPRICE
	SET $PIECE(SATZ,Y,124)=$PIECE(SATZ1,Y,92)  ;PROV
	IF $GET(YSONDERVK)'="" SET $PIECE(SATZ,Y,118)=+YSONDERVK  ;SONDERVERKAUFSPREIS
	
	;BESTIMMEN LIEFERANT ;ordain supplier 
	IF $GET(YWH)=1 DO
	. SET LIEFER=$ORDER(^INARTK(YM,ART,""))
	. IF YAUFTRAG'="" IF $PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,2)=2 SET $PIECE(SATZ,Y,7)=1  ;BEI LF-BESTELKLUNGE IMMER SETZEN;FIS;06.05.04;25253
	. IF LIEFER'="" IF $ORDER(^INARTK(YM,ART,LIEFER))=""  DO   ;NUR EIN LIEFERANT=BESTELLEN ;only uni- 
	. . SET $PIECE(SATZ,Y,7)=1  ;BESTELLEN ;book 
	. . SET $PIECE(SATZ,Y,12)=LIEFER
	. . IF YAUFTRAG'="" IF $PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,12)="" DO
	. . . NEW SATZ1,YFOM,YVOR,YOK
	. . . SET SATZ1=$GET(^INAUF(YM,YAUFTRAG,1))
	. . . SET $PIECE(SATZ1,Y,12)=$PIECE(SATZ,Y,12)
	. . . SET YOK=$$^WWWSPEI("INAUF",YAUFTRAG,SATZ1,1)
	
	IF $PIECE(SATZ,Y,7)=1 DO  ;BESTELLEN ;book 
	. IF $PIECE(SATZ,Y,12)="" SET $PIECE(SATZ,Y,12)=$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,12)  ;BEI LIEFERANT ;next to supplier 
	. NEW LIEF,MENGE
	. SET MENGE=YMENGE
	. SET LIEF=$PIECE(SATZ,Y,12)   ;KONDITIONEN FÜR BESTELLUNG ;terms of payment to sales order 
	. QUIT:LIEF=""  ;FIS;06.05.04
	. ;
	. ;IF LIEF'="" IF $DATA(^INARTK(YM,ART,LIEF,1)) SET ^INAUFPK(YM,YAUFTRAG,YPOS,1)=$GET(^INARTK(YM,ART,LIEF,1))
	. ;
	. IF LIEF'="" IF $DATA(^INARTK(YM,ART,LIEF,1)) do GetItemSupplierData^INAUFDISC(LIEF,YAUFTRAG,YPOS,ART)  ;shobby SR12008
	. IF LIEF'="" IF '$DATA(^INARTK(YM,ART,LIEF,1)) DO  ;KONDITION AUS LIEFERANTENSTAMM ;out of 
	. . NEW KOND,KOND1
	. . SET KOND=$PIECE($GET(^INLIEF(YM,LIEF,1)),Y,56)  ;LIEFERANTENKONDITION
	. . QUIT:KOND=""
	. . SET KOND1=$GET(^INKOND(YM,KOND,1))
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,7) =$PIECE(KOND,Y,3)   ;SKONTO
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,9) =$PIECE(KOND,Y,2)   ;TAGE
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,10)=$PIECE(KOND,Y,4)   ;NETTO
	. ;
	. NEW WG,YI
	. SET WG=$PIECE($GET(^INART(YM,ART,1)),Y,30)  ;WARENGRUPPE
	. ;
	. IF WG'="" IF '$DATA(^INARTK(YM,ART,LIEF)) IF $DATA(^INLIEFK(YM,LIEF,WG,1)) DO   ;KONDITION  AUS LIEFERANT ;out of supplier 
	. . SET KOND1=$GET(^INLIEFK(YM,LIEF,WG,1))
	. . FOR YI=2,3,4,5,6,51,52,53 SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,YI)=$PIECE(KOND1,Y,YI)   ;RABATTE
	. . SET KOND=$PIECE(KOND1,Y,7)
	. . QUIT:KOND=""
	. . SET KOND1=$GET(^INKOND(YM,KOND,1))
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,7) =$PIECE(KOND1,Y,3)   ;SKONTO
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,9) =$PIECE(KOND1,Y,2)   ;TAGE
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,10)=$PIECE(KOND1,Y,4)   ;NETTO
	. ;
	. IF LIEF'="" IF ART'="" IF $DATA(^INARTKR(YM,ART,LIEF)) DO   ;MENGENRABATTE EINKAUF
	. . NEW RABATT,RABATT1
	. . SET RABATT1=""
	. . SET RABATT1=$GET(^INARTKR(YM,ART,LIEF,+MENGE,1))                  ;GENAUE MENGEN ;shuffle 
	. . IF RABATT1'="" SET RABATT=+MENGE
	. . IF RABATT1="" SET RABATT=$ORDER(^INARTKR(YM,ART,LIEF,+MENGE),-1)  ;VORIGE STUFE ;step 
	. . IF RABATT'="" SET RABATT1=$GET(^INARTKR(YM,ART,LIEF,RABATT,1))
	. . QUIT:RABATT1=""  ;KEIN RABATT ;no deduction 
	. . IF $PIECE(RABATT1,Y,2)'="" QUIT:$PIECE(RABATT1,Y,2)>$HOROLOG   ;AB GÜLTIG ;Confirm. valuable 
	. . IF $PIECE(RABATT1,Y,3)'="" QUIT:$PIECE(RABATT1,Y,3)<$HOROLOG   ;BIS GÜLTIG ;until valuable 
	. . IF +$PIECE(RABATT1,Y,1)'=0 SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,4)=$PIECE(RABATT1,Y,1)  ;3.RABATT=MENGENRABATT
	. . SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,53)=$$^WWWTEXT(32223)  ;3.RABATT=MENGENRABATT
	. . IF +$PIECE(RABATT1,Y,4)'=0 SET $PIECE(^INAUFPK(YM,YAUFTRAG,YPOS,1),Y,12)=$PIECE(RABATT1,Y,4)  ;WENN BETRAG ALS RABATT ;when Sum when deduction 
	. ;
	. SET YFELD=$GET(^INAUFPK(YM,YAUFTRAG,YPOS,1))
	. SET $PIECE(YFELD,Y,47)=$PIECE(YFELD,Y,12)   ;EINZELEK IN SELBSTKOSTENFELD ;within 
	. SET $PIECE(YFELD,Y,16)=MENGE                ;STANDARDMENGE
	. IF +$PIECE(YFELD,Y,41)'=0 SET $PIECE(YFELD,Y,16)=MENGE*$PIECE(YFELD,Y,41)       ;UMRECNUNGSFAKTOR 
	. SET $PIECE(YFELD,Y,12)=$PIECE(YFELD,Y,47)*$PIECE(YFELD,Y,16)/$$^INQTYUNIT(ART)  ;MENGE * PREIS ;quantity * price 
	. SET $PIECE(YFELD,Y,13)=$$^INNETTO(YFELD)    ;NETTOPREIS ERRECHNE
	. SET ^INAUFPK(YM,YAUFTRAG,YPOS,1)=YFELD      ;SPEICHERN PREISE ;Save 
	
	IF $PIECE(SATZ,Y,26)=2!($PIECE(SATZ,Y,26)=6) DO   ;DIENSTLEISTUNG ODER PAUSCHALLEISUTUNG;TYBD;22,08,2003;
	. SET $PIECE(SATZ,Y,7)=4   ;WENN LEISTUNG DANN DIENSTLEISTUNG UND LEISTUNGSFÄHIG ;when performance And efficient 
	. SET $PIECE(SATZ,Y,90)=1  ;WARE AUSLIEFERUNGSFÄHIG ;wares 
	
	IF $PIECE(SATZ,Y,26)=4 DO  ;KLEINTEILE
	. SET $PIECE(SATZ,Y,7)=2   ;KLEINTEILE AUS LAGER ;out of stock location 
	. SET $PIECE(SATZ,Y,90)=1  ;WARE AUSLIEFERUNGSFÄHIG ;wares 
	. NEW BET,LAP,WED,WEN 
	. SET REST=$PIECE(SATZ,Y,5)  ;MENGE ;quantity 
	. SET YOK=$$^INARTMINUS(YAUFTRAG,YPOS,ART,REST)  ;BESTAND UMBUCHEN ;rebook 
	
	SET $PIECE(SATZ,Y,22)=$HOROLOG   ;ERFASST
	SET $PIECE(SATZ,Y,21)=$GET(YTRAKT)_"/"_YUSER   ;TRANSAKTIONSNUMMER UND USER ;And 
	SET $PIECE(SATZ,Y,23)=YBED   ;ERFASST
	SET $PIECE(SATZ,Y,24)=""     ;ÄNDERUNG  ;alteration 
	SET $PIECE(SATZ,Y,25)=""     ;MITARB
	SET $PIECE(SATZ,Y,116)=$PIECE(SATZ1,Y,86)   ;AUFSCHLAG ;overcharge 
	;SET $PIECE(SATZ,Y,118)=$PIECE(SATZ1,Y,88)  ;VK
	SET $PIECE(SATZ,Y,118)=$$^INSALESPRICE($PIECE(SATZ,Y,4),,$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,6),$PIECE(SATZ1,Y,90),$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,1))  ;VK;FIS;03.06.03;23477;PREISE NEU AUS INSALESPRICE
	SET $PIECE(SATZ,Y,120)=$PIECE(SATZ1,Y,90)   ;PREISKZ
	SET $PIECE(SATZ,Y,124)=$PIECE(SATZ1,Y,92)   ;PROV
	IF $GET(YSONDERVK)'="" SET $PIECE(SATZ,Y,118)=+YSONDERVK  ;SONDERVERKAUFSPREIS
	;IF $PIECE(SATZ,Y,19)="" SET $PIECE(SATZ,Y,19)=$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,19)  ;LIEFERTERMIN
	
	;HISTORY
	DO
	. NEW BETRIEB
	. SET BETRIEB=$PIECE($GET(^INAUF(YM,YAUFTRAG,1)),Y,6)
	. IF BETRIEB="" SET BETRIEB=$GET(YLOCATION)
	. DO ^INARTHIST(ART,$$^WWWTEXT(32507)_" "_YAUFTRAG_"-"_YPOS,BETRIEB)  ;Neue Auftragsposition ; "New Order" 
	
	SET $PIECE(SATZ,Y,119)=$JUSTIFY($PIECE(SATZ,Y,118)*YMENGE/$$^INQTYUNIT(ART),0,2)  ;VK*MENGE
	SET YM1=""
	SET YM2=""
	SET KEY=YAUFTRAG_","_YPOS
	SET YOK=$$^WWWSPEI("INAUFP",KEY,SATZ,1)   ;SPEICHERN DATENSATZ ;Save data record 
	IF $PIECE(SATZ,Y,4)'="" IF $PIECE($GET(^INVORG(YM,YM,1)),Y,217)=1 SET ^INDRPNETCHANGE(YM,$PIECE(SATZ,Y,4),1)=""  ;FIS;29.04.05;SR12200
	SET $PIECE(SATZ,Y,123)=$$^INAUFPNETTO(YAUFTRAG,YPOS)
	
	; vvv 26-Sep-2006
	;IF +$$^INAUFKOST(YAUFTRAG,YPOS)'=0 SET $PIECE(^INAUFP(YM,YAUFTRAG,YPOS,1),Y,47)=$$^INAUFKOST(YAUFTRAG,YPOS)  ;NEURECHNEN POSITIONS-EK
	set curReturnedCost = +$$^INAUFKOST(YAUFTRAG,YPOS)
	IF curReturnedCost SET $PIECE(^INAUFP(YM,YAUFTRAG,YPOS,1),Y,47) = curReturnedCost  ;NEURECHNEN POSITIONS-EK
	; ^^^ 26-Sep-2006
	MERGE ^INAUFPSP(YM,$TRANSLATE(YAUFTRAG,""""),YPOS)=^INARTSP(YM,ART)  ;ARTIKELSPRACHE IN POSITIONEN  ;within 
	
	QUIT YPOS  ;NEUE POSITIONSNUMMER
	
]]></Routine>
</Export>