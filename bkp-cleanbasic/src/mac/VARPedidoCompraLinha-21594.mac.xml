<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPedidoCompraLinha" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPedidoCompraLinha
#include COMSYS
#include COMConst
#include VARConst
#include INConst
 	
LoadGrid(pid="")
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARPedidoCompra.mac
    ; 
    ; Histórico:
    ; 	25-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new YFORM, YFELD, YKEY, YAUSWAHL
	
	set YFORM = "VARPedidoCompraLinha"
	set YFELD = ""
	set YKEY = $get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)	= 1
	set $$$COMGridEditParameterGridName(YAUSWAHL)	= YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)	= '($$$WWW120AuthorizationToModifyData(YVOR)=5)
	set $$$COMGridEditParameterContainer(YAUSWAHL)	= "VARPedidoCompra"
	
	if ((YKEY '= "") || (YKEY '= "+")){
		if (YKEY = "+"){
			$$$GRIDStart(YFORM,YKEY)
			quit
		}
		
		set objPedidoCompra = ^VARPedidoCompra(0,YKEY,1)
		set statusPedidoCompra = $$$VARPedidoCompraStatus(objPedidoCompra)
		if ((statusPedidoCompra = 1) || (statusPedidoCompra = 3)){
			do ForcarAtualizacaoAoMudarAba()
		}
	}
	else {
		do ForcarAtualizacaoAoMudarAba()
	}
 
	$$$GRIDStart(YFORM,YKEY)
	
	quit
	
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnBeforeSave^VARPedidoCompra.mac
    ; 
    ; Histórico:
    ; 	25-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	
	new strStatus
	set strStatus=$$$GRIDSave(pYKEY)
	if $$$ISOK(strStatus) {
	} else {
		set Q=$$$QDontSave
	}
	quit
 
OnAfterDataFields(pYKEY,pYFELD,pYSEITE)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARPedidoCompra.mac
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	set YOPTION1 = 0
	do CanArquivar($piece(YKEY, YKOMMA, 1))
	if ('YQ) {
		set YOPTION1 = 1
	}
	
 	quit
 
OnAfterDelete(pYKEY)
	do DELETE^COMGridEdit31Interface()
	quit
	
OnBeforeSaveHook()
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Nunca deixar o valor booleano ser salvo como vazio "", apenas como 0 ou 1.
    ;
    ; Chamado por:
    ;	Hook OnBeforeSave da classe VARPedidoCompraLinha.
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	new blnTotalmenteRecebida
	set blnTotalmenteRecebida = $$$VARPedidoCompraLinhaQuantidadeTotalmenteAtend(YFELD)
 
	if blnTotalmenteRecebida = "" {
		set $$$VARPedidoCompraLinhaQuantidadeTotalmenteAtend(YFELD) = $$$NO
	}
 
	quit $$$OK
	
OnBlurProduto(pintCol,pintRow,&pobjYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (1)-idProduto no form VARPedidoCompraLinha 
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new idPedidoCompra, idProduto, objProduto, Unidade, Grupo, strStatus, strListItemCodes, 
		strDate, strFocusField, intCurrentLine, blnItemJaIncluido, intGridLine, intSeq
	set strStatus = $$$OK
	
	set idProduto	= $piece(pobjYFELD,Y,1)
	quit:($length(idProduto) = 0)
	
	if $$$ISOK(strStatus) {
		set objProduto	= $get(^INART(0,idProduto,1))
		if ($length(objProduto) = 0) {
			set strStatus = $$$MakeStatus("%1", "Produto "_idProduto_" não cadastrado.")
		}
	}
	if $$$ISOK(strStatus) {
		set idLocalPAM = $$$VARPedidoCompraLocaldeEntrega($$$GRIDYFELDContainer)
		if ($$SQLGetTipoEstoqueLocal^VARSQL(idLocalPAM) '= $$SQLGetEstoquePrincipal^VARSQL(idProduto)) {
			set strStatus = $$$MakeStatus("%1", "Esse produto é de "_$$SQLGetEstoquePrincipal^VARSQL(idProduto)_" e o PAM é de "_$$SQLGetTipoEstoqueLocal^VARSQL(idLocalPAM)_", por isso não pode ser inserido.")
		}
	}
	
	if $$$ISOK(strStatus) {
		quit:(+$$ValidaNatureza(YKEY,.pobjYFELD))
	}
	
	if $$$ISOK(strStatus) {
		if (+$$$INARTFREE34(objProduto) = 1){
			$$$Alert("Atenção: produto isento de ICMS!")
		}
	}
 
	if $$$ISOK(strStatus) {
		//Checking if the item was already used
		set strListItemCodes = $$GetItensJaIncluidos()
	
		set strDate = $piece($horolog, YKOMMA, 1)
		set strFocusField = $get(^WWWDATEN(0, strDate, YUSER, "VARPedidoCompraLinha","V", "FOCUSFIELD"))
		set intCurrentLine = $extract($piece(strFocusField, "_", 1), 2,
			$length(strFocusField))
	
		set blnItemJaIncluido = $$$NO
	
		for intGridLine = 1:1:$length(strListItemCodes, Y) {
			continue:(intGridLine = intCurrentLine)
 
			if ($piece(strListItemCodes, Y, intGridLine) = idProduto) {
				set blnItemJaIncluido = $$$YES
				quit
			}
		}
 
		if (blnItemJaIncluido) {
			// Searches for the "Seq." value of the grid line:
			set intSeq = ""
			for {
				set intSeq = $order(^WWWDATEN(0, strDate, YUSER, "VARPedidoCompraLinha",
					"V", "REFERENCEROW", intSeq))
				quit:(intSeq = "")
 
				quit:($get(^WWWDATEN(0, strDate, YUSER, "VARPedidoCompraLinha",
					"V", "REFERENCEROW", intSeq, 1)) = intGridLine)
			}
 
			set $piece(pobjYFELD, Y, 1) = ""
			set strStatus = $$Erro("O produto '"_idProduto_"' já foi incluído em outra linha do pedido (Seq. = "_intSeq_").")
		}
	}
 
	//Loading other fields
	if $$$ISOK(strStatus) {
		set estoqueCentral = $$GetEstoqueCentral^VARAlertaLinha(idProduto)
		set $$$VARPedidoCompraLinhaUnidade(pobjYFELD)	= Unidade
		set $$$VARPedidoCompraLinhaGrupo(pobjYFELD)		= Grupo
		set $$$VARPedidoCompraLinhaCMM(pobjYFELD)		= $$GetCMMRede^VARAlertaLinha(idProduto)			;CMM para a empresa
		set $$$VARPedidoCompraLinhaEstoque(pobjYFELD)	= $$GetEstoqueRede^VARAlertaLinha(idProduto)		;Estoque para a empresa
		set $$$VARPedidoCompraLinhaEFCentral(pobjYFELD) = $$^WWWTR(0,12,estoqueCentral,0)					;Total disponivel locais centrais
		$$$GRIDUpdateManualField($$$KEY2(YKEY),15,$$GetObservacaoProgramacao(),"VARPedidoCompraLinha")
	}
	else {
		$$$Alert(strStatus)
		set $$$VARPedidoCompraLinhaProduto(pobjYFELD)	= ""
		set $$$VARPedidoCompraLinhaUnidade(pobjYFELD)	= ""
		set $$$VARPedidoCompraLinhaGrupo(pobjYFELD)		= ""
		set $$$VARPedidoCompraLinhaCMM(pobjYFELD)		= ""
		set $$$VARPedidoCompraLinhaEstoque(pobjYFELD)	= ""
		set $$$VARPedidoCompraLinhaEFCentral(pobjYFELD) = ""
	}
	
	quit
	
OnBeforeFormat()
	;-------------------------------------------------------------------------------
    ; Deixa o campo "A Arquivar" editavel quando o form estiver ReadOnly (puder arquivar)
	;-------------------------------------------------------------------------------
	do CanArquivar($piece(YKEY, YKOMMA, 1))
	if ('YQ) {
		if (YBBN = 12) { //12 - Qtd. Arquivada (campo antes do "A Arquivar")
			set ^CacheTemp(YUSER,"Grid","Enabled") = 1
			set ^CacheTempEvent(YUCI,YFORM,"Format") = 1
		}
		else {
			set ^CacheTemp(YUSER,"Grid","Enabled") = 0
			set ^CacheTempEvent(YUCI,YFORM,"Format") = 0
	 		set ^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","AUTHORISATION",1) = 5
		}
	}
	quit	
	
CanArquivar(idPedidoCompra)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função GetValorPadraoAArquivar^VARPedidoCompraLinha.mac
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	new objPedidoCompra
 
	if ($length($get(idPedidoCompra)) = 0) {
		set objPedidoCompra = ""
	}
	else {
		set objPedidoCompra = $get(^VARPedidoCompra(YM,idPedidoCompra,1))
	}
	do CanArquivarDoc^VARPedidoCompra(idPedidoCompra,objPedidoCompra)
	quit	
 
ArquivarIndividual(pidPedidoCompra, pidPedidoCompraLinha, pblnTudo, pblnCancelar, pintQuantidadeAArquivar)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função ArquivarDoc^VARPedidoCompra.mac
    ; 
    ; Histórico:
    ; 	01-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	new objPedidoCompraLinha, fltQuantidadePedido, fltQuantidadeArquivada, fltQuantidadeAtendida, strStatus
 	
	set objPedidoCompraLinha   = $get(^VARPedidoCompraLinha(YM,pidPedidoCompra,pidPedidoCompraLinha,1))
	set fltQuantidadePedido    = $$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha)
	set fltQuantidadeArquivada = +$$$VARPedidoCompraLinhaQuantidadeArquivada(objPedidoCompraLinha)
	set QuantDisponivel		  = +$$GetQuantidadeAAtender(pidPedidoCompra, pidPedidoCompraLinha)
	
	if (pblnTudo) {
		set pintQuantidadeAArquivar = QuantDisponivel
	}
	elseif (pintQuantidadeAArquivar > (QuantDisponivel)) {
		quit $$$MakeStatus("Quantidade solicitada para arquivar do produto "_$$$VARPedidoCompraLinhaProduto(objPedidoCompraLinha)_" é maior do que a disponível("_QuantDisponivel_").")
	}
	
	if (pintQuantidadeAArquivar = (QuantDisponivel)) {
		set $$$VARPedidoCompraLinhaQuantidadeTotalmenteAtend(objPedidoCompraLinha) = $$$YES
	}
	
	set $$$VARPedidoCompraLinhaQuantidadeArquivada(objPedidoCompraLinha) = fltQuantidadeArquivada + pintQuantidadeAArquivar
	set $$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha) = fltQuantidadePedido - pintQuantidadeAArquivar
	
	set strStatus = $$$Save("VARPedidoCompraLinha",pidPedidoCompra_$$$COMMA_pidPedidoCompraLinha,objPedidoCompraLinha,$$$YES)
	quit strStatus
	
ArquivarOnClickJS(pidPedidoCompra)
	new HTML, strYes, pbtnYes, pbtnNo, blnAlgoAArquivar
 
	set blnAlgoAArquivar = $$$NO
	set i = $order(^WWWDATEN(YM,+$horolog,YUSER,"VARPedidoCompraLinha","V","REFERENCEROW",""))
	set count = 0
	while ($length(i) > 0) {
		set count = count + 1
		set intQtde = +$get(^WWWDATEN(YM,+$horolog,YUSER,"VARPedidoCompraLinha","V","Y"_count_"_14",1))
		if (intQtde > 0) {
			set blnAlgoAArquivar = $$$YES
			quit
		}
		set i = $order(^WWWDATEN(YM,+$horolog,YUSER,"VARPedidoCompraLinha","V","REFERENCEROW",i))
	}
 
	if ('blnAlgoAArquivar) {
		$$$StartScript()
		write "alert('A quantidade a ser arquivada de algum dos produtos do PAM deve ser maior que zero.');"
		$$$EndScript()
	}
	else {
		set HTML=""
		set HTML=HTML_"		<label style='font-size:12px;font-weight:bold;margin-top:10px;'>Motivo:</label><br />"
		set HTML=HTML_"		<textarea name='promptText' id='promptText' rows='7' cols='45' wrap='physical' /></textarea><br /><br />"
		set HTML=HTML_"		<div style='text-align:center;'>"
		set HTML=HTML_"			<input type='button' id='btnYesConfirm' value='    Arquivar    ' style='font-weight:bold;margin-right:20px;cursor:pointer;'>"
		set HTML=HTML_"			<input type='button' id='btnNoConfirm'  value='    Cancelar    ' style='font-weight:bold;margin-left:10px;cursor:pointer;'>"
		set HTML=HTML_"		</div>"
	
		set strYes = ""
		set strYes=strYes_"	var strMotivo = document.getElementById('promptText').value;"
		set strYes=strYes_"	if (strMotivo != ''){"
		set strYes=strYes_"		dscPMClose();"
		set strYes=strYes_"		CallBackNow('ArquivarOnClick^VARPedidoCompraLinha', '"_pidPedidoCompra_"', strMotivo);"
		set strYes=strYes_"	} else {"
		set strYes=strYes_"		alert('É necessário informar o motivo do arquivamento.');"
		set strYes=strYes_"	}"
 
		set pbtnYes("Id")="btnYesConfirm"
		set pbtnYes("Code")=strYes
 
		set pbtnNo("Id")="btnNoConfirm"
		set pbtnNo("Code")="dscPMClose();"
 
		do Show^WWWPopupMessage(HTML,"&nbsp;Arquivar Linhas do PAM nº "_pidPedidoCompra,,,,,.pbtnNo,.pbtnYes,$$$YES,"#dcdcdc")
	}
 
	quit
 
ArquivarOnClick(idPedidoCompra, strMotivoArquivamento)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função ArquivarOnClickJS^VARPedidoCompraLinha.MAC
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	new YFORM, YSEITE, intQtdeLinhas, strQtdsAArquivar, intQtde, objPedidoCompra, i, strStatus,
		strMotivoArquivamentoFormatado
	
	set YFORM = "VARPedidoCompraLinha"
 
	set strQtdsAArquivar = ""
	set i = $order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEROW",""))
	set count = 0
	while ($length(i) > 0) {
		set count = count + 1
		set intQtde = +$get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","Y"_count_"_14",1))
		set $piece(strQtdsAArquivar, Y, i) = intQtde 
		set i = $order(^WWWDATEN(YM,+$horolog,YUSER,YFORM,"V","REFERENCEROW",i))
	}
	
	if ($length(strMotivoArquivamento) = 0) {
		$$$StartScript()
		write "alert('Entre com o motivo do arquivamento dos produtos do PAM!');"
		$$$EndScript()
	}
	else {
		if ($length(strMotivoArquivamento, """") = 1) {
			set strMotivoArquivamentoFormatado = strMotivoArquivamento
		}
		else {
			set strMotivoArquivamentoFormatado = ""
			for i = 1:1:$length(strMotivoArquivamento, """") {
				set strMotivoArquivamentoFormatado = strMotivoArquivamentoFormatado_
					$piece(strMotivoArquivamento, """", i)_""""""
			}
		}
 
		set strStatus = $$Transaction^COMTransaction("Arquivar^VARPedidoCompraLinha("""_idPedidoCompra_""","""_strQtdsAArquivar_""","""_strMotivoArquivamentoFormatado_""")", $$$YES)
		if $$$ISOK(strStatus) {
			$$$StartScript()
			write "alert('Produtos do PAM arquivado com sucesso!');"
			$$$EndScript()
 
			set YFORM = "VARPedidoCompra"
			set YSEITE = 3
			do GoToForm^COMUtilForm(YFORM, idPedidoCompra, 4, $get(YPARA), $$$NO)
		}
		else {
			$$$StartScript()
			write "alert('Erro ao arquivar produtos do PAM. => "_$$DecodeError^COMUtilError(strStatus)_"');"
			$$$EndScript()
		}
	}
	quit ""
	
Arquivar(pidPedidoCompra, pstrQtdsAArquivar, pstrMotivoArquivamento)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função ArquivarOnClick^VARPedidoCompraLinha.MAC
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	new idPedidoCompraLinha, intQuantidadeAArquivar, strStatus, objPedidoCompra
 
	$$$Order3(^VARPedidoCompraLinha,YM,pidPedidoCompra,idPedidoCompraLinha)
	
		set intQuantidadeAArquivar = +$piece(pstrQtdsAArquivar, Y, idPedidoCompraLinha)
		
		if (intQuantidadeAArquivar > 0) {
			set strStatus = $$ArquivarIndividual(pidPedidoCompra, idPedidoCompraLinha, 0, 0, intQuantidadeAArquivar)
			if $$$ISERR(strStatus) {
				set $zerror = $$DecodeError^COMUtilError(strStatus)
				zquit 1 GOTO @$ZTRAP
			}
	 
			do RegistrarAlteracao^VARPedidoCompraHistorico(pidPedidoCompra, 3,
				idPedidoCompraLinha, intQuantidadeAArquivar, pstrMotivoArquivamento) ;3 = #define HistAlterArquivar  3
		}
 
	$$$End
 
	quit $$$OK
	
GetQuantidadeAAtender(pidPedidoCompra, pidPedidoCompraLinha)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna a quantidade a atender de uma linha do PAM, que corresponde 
    ;   a quantidade no pedido menos a quantidade já atendida.
    ;
    ; Chamado por:
    ;	Função ArquivarDoc^VARPedidoCompra.mac
    ;	Função ArquivarIndividual^VARPedidoCompraLinha.mac
    ;	Campo Valor Padrão do campo (11)-Quant. Atendida no form VARPedidoCompraLinha
    ; 
    ; Histórico:
    ; 	01-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	new objPedidoCompraLinha, fltQuantidadePedido, fltQuantidadeAtendida, fltQuantidadeAAtender
	
	set objPedidoCompraLinha = $get(^VARPedidoCompraLinha(YM, pidPedidoCompra, pidPedidoCompraLinha, 1))
	
	set fltQuantidadePedido   = $$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha)
	set fltQuantidadeAtendida = $$GetQuantidadeAtendida(pidPedidoCompra, pidPedidoCompraLinha)
	set fltQuantidadeAAtender = fltQuantidadePedido - fltQuantidadeAtendida
	
	quit fltQuantidadeAAtender
 
GetQuantidadeAtendida(pidPedidoCompra, pidPedidoCompraLinha, pidCompra="")
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Calcula a quantidade existente em ordens de compra para a linha do pedido de
    ;	compra.
    ;
    ; Chamado por:
    ;	Função ArquivarDoc^VARPedidoCompra.mac
    ;	Função ArquivarIndividual^VARPedidoCompraLinha.mac
    ;	Campo Valor Padrão do campo (11)-Quant. Atendida no form VARPedidoCompraLinha
    ; 
    ; Histórico:
    ; 	01-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	quit:(($length(pidPedidoCompra) = 0) || ($length(pidPedidoCompraLinha) = 0)) ""
 
	new fltQuantidadeTotal, idCompra, idCompraLinha, objCompraLinha, fltQuantidade 	
 
	//Somando as quantidades em uso 	
 	set fltQuantidadeTotal = 0
 	
	set idCompra = $order(^INAUFPs(0,200,$$$Index(pidPedidoCompra),$$$Index(pidPedidoCompraLinha),"")) 
 	while ($length(idCompra) > 0) { 
 		if ($get(pidCompra) '= idCompra) { // ignora no calculo a compra informada
			set idCompraLinha = $order(^INAUFPs(0,200,$$$Index(pidPedidoCompra),$$$Index(pidPedidoCompraLinha),idCompra,"")) 
	 		while ($length(idCompraLinha) > 0) { 
	 			set objCompraLinha = $get(^INAUFP(0,idCompra,idCompraLinha,1))
	 			set fltQuantidadeTotal = fltQuantidadeTotal + $$$INAUFPQuantity(objCompraLinha)
	 			set idCompraLinha = $order(^INAUFPs(0,200,$$$Index(pidPedidoCompra),$$$Index(pidPedidoCompraLinha),idCompra,idCompraLinha)) 
	 		}
 		}
		set idCompra = $order(^INAUFPs(0,200,$$$Index(pidPedidoCompra),$$$Index(pidPedidoCompraLinha),idCompra))
 	}
 
 	$$$VAR
 	quit fltQuantidadeTotal
	
GetQuantidadeArquivada()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função GetQuantidadeTotal^VARPedidoCompraLinha.mac
    ;	Campo Valor Padrão do campo (12)-Quant. Arquivada no form VARPedidoCompraLinha 
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit:(($length($get(YKEY)) = 0) || ($length($get(YFELD)) = 0) ||
		  ($length($$$VARPedidoCompraLinhaQuantidadeArquivada(YFELD)) = 0)) 0
 
	quit $$$VARPedidoCompraLinhaQuantidadeArquivada(YFELD)
 
GetItensJaIncluidos()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnBlurProduto^VARPedidoCompraLinha.mac
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	new strListItemCodes, strDate, intGridColItemCode, intGridLine, intItemCode
	
	set strListItemCodes = ""
 
	// Loop on grid lines:
	set strDate = $piece($horolog, YKOMMA, 1)
	set intGridColItemCode = 1
	set intGridLine = 0
	for {
		set intGridLine = $increment(intGridLine)
		set intItemCode = $get(^WWWDATEN(0,strDate,YUSER,"VARPedidoCompraLinha","V", "Y"_intGridLine_"_"_intGridColItemCode,1))
		quit:(intItemCode = "")
 
		continue:(+$get(^WWWDATEN(0,strDate,YUSER,"VARPedidoCompraLinha","V","KILLEDRECORD",intGridLine)))
 
		set $piece(strListItemCodes, Y, intGridLine) = intItemCode
	}
 
	quit strListItemCodes
	
GetQuantidadeAAtenderPorProduto(pidItem, pidLocation)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ; 	Verifica em TODOS os pedidos de compra as quantidades que ainda não foram 
    ;	atendidas para um determinado produto.
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;-------------------------------------------------------------------------------
	new fltQuantidade, idPedidoCompra, idPedidoCompraLinha, objPedidoCompra, idLocation
	
	set fltQuantidade = 0
 
	$$$Order5(^VARPedidoCompraLinhas,YM,2,$$$NO,$$$Index(pidItem),idPedidoCompra)
		
		$$$Order6(^VARPedidoCompraLinhas,YM,2,$$$NO,$$$Index(pidItem),idPedidoCompra,idPedidoCompraLinha)
			;set objPedidoCompra = $get(^VARPedidoCompra(YM, idPedidoCompra, 1))
			;set idLocation = $$$VARPedidoCompraLocaldeEntrega(objPedidoCompra)
			;continue:(idLocation '= pidLocation)
				
			set fltQuantidade = fltQuantidade + $$GetQuantidadeAAtender(idPedidoCompra, idPedidoCompraLinha)
		$$$End
	
	$$$End
 
	quit fltQuantidade
 
GetQuantidade()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função GetQuantidadeTotal^VARPedidoCompraLinha.mac
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit:(($length($get(YKEY)) = 0) || ($length($get(YFELD)) = 0) ||
		  ($length($$$VARPedidoCompraLinhaQuantidade(YFELD)) = 0)) 0
 
	quit $$$VARPedidoCompraLinhaQuantidade(YFELD)
	
GetQuantidadeTotal()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo Valor Padrão do campo (13)-Quant. Total no form VARPedidoCompraLinha
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	quit:(($length($get(YKEY)) = 0) || ($length($get(YFELD)) = 0)) 0
 
	quit ($$GetQuantidade() + $$GetQuantidadeArquivada())
 
GetLineNumber(pidPedidoCompra, pidItem)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Pega o número da linha de acordo com o código do produto.
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	new idPedidoCompraLinha
	set idPedidoCompraLinha = $Order(^VARPedidoCompraLinhas(YM, 1, $$$Index(pidItem), pidPedidoCompra, ""))
	quit idPedidoCompraLinha
	
GetFornecedor(pidPedidoCompra, pidPedidoCompraLinha)
	quit:(($length($get(pidPedidoCompra)) = 0) || ($length($get(pidPedidoCompraLinha)) = 0)) ""
 	set idAta = $$$VARPedidoCompraLinhaAta($get(^VARPedidoCompraLinha(0, pidPedidoCompra, pidPedidoCompraLinha, 1)))
 	set idAtaLinha = $$$VARPedidoCompraLinhaItemdaAta($get(^VARPedidoCompraLinha(0, pidPedidoCompra, pidPedidoCompraLinha, 1)))
	quit:(($length($get(idAta)) = 0) || ($length($get(idAtaLinha)) = 0)) ""
	quit $$GetFornecedor^VARAtaLinha(idAta, idAtaLinha)
	
PrintJS(pobjForm)	
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARPedidoCompra.MAC
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	write !, "<script language=""javascript"">"
	&js<
	function arquivar() {
		CallBackNow("ArquivarOnClickJS^VARPedidoCompraLinha", idPedidoCompra);
		objTxt = document.getElementById('promptText');
		if (objTxt) {
			SetFocus("");
 			objTxt.focus();
		}
	}
	>
	write !, "</script>"
	quit
	
UpdateUsedQuantity(pidPedidoCompra, pidPedidoCompraLinha, pfltQuantidadeAIgnorar=0)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Atualiza o campo booleano que diz se a linha já foi totalmente atendida ou não 
	;  	(é só isso mesmo?).
    ;
    ; Chamado por:
    ;	PENDÊNCIA ?
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	/*
	new objPedidoCompraLinha, fltQuantidadeAtendida, strStatus, objPedidoCompra, blnQuantidadeTotalmenteAtend, fltQuantidadeTotal
 
	// Remove aspas da variável:
	// Para depurar, e causar erro na deleção da INAUF, comente a linha a seguir:
 	set pidPedidoCompra = $translate(pidPedidoCompra,"""") 
 	set pidPedidoCompraLinha = $translate(pidPedidoCompraLinha,"""") 
	set objPedidoCompraLinha = $get(^VARPedidoCompraLinha(YM, pidPedidoCompra, pidPedidoCompraLinha, 1))
 
 	// Se não encontrar a linha do PAM, retorna erro:
 	if ( $get(objPedidoCompraLinha) = "" ) {
	 	set strStatus = "0~Erro: não encontrada linha "_pidPedidoCompraLinha_" do PAM "_pidPedidoCompra
	 	quit strStatus
 	}
 
	set blnEncerrado = 0
	if ($$$VARPedidoCompraLinhaQuantidadeTotalmenteAtend(objPedidoCompraLinha) = $$$YES) {
		set objPedidoCompra = $get(^VARPedidoCompra(YM, pidPedidoCompra, 1))
		set blnEncerrado = ($$$VARPedidoCompraStatus(objPedidoCompra) = 3)
	}
 
	set fltQuantidadeAtendida = $$GetQuantidadeAtendida(pidPedidoCompra, pidPedidoCompraLinha) - pfltQuantidadeAIgnorar
 
	if ('blnEncerrado) {
		set blnQuantidadeTotalmenteAtend = ($$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha) = fltQuantidadeAtendida)
		set $$$VARPedidoCompraLinhaQuantidadeTotalmenteAtend(objPedidoCompraLinha) = blnQuantidadeTotalmenteAtend
	}
	else {
		if ($$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha) > fltQuantidadeAtendida) {
			// Devolver quantidade após o PAM ter sido encerrado não
			// deveria ser uma situação válida. De qualquer forma,
			// aqui está a implementação para tratar esse caso.
			set fltQuantidadeTotal = $$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha) + 			
			$$$VARPedidoCompraLinhaQuantidadeArquivada(objPedidoCompraLinha)
 
			set $$$VARPedidoCompraLinhaQuantidade(objPedidoCompraLinha) = fltQuantidadeAtendida
			set $$$VARPedidoCompraLinhaQuantidadeArquivada(objPedidoCompraLinha) = fltQuantidadeTotal - fltQuantidadeAtendida
		}
		else {
			// Este caso não deve acontecer
		}
	}
	set strStatus = $$$Save("VARPedidoCompraLinha",pidPedidoCompra_$$$COMMA_pidPedidoCompraLinha,objPedidoCompraLinha,$$$YES)
	
	*/
	
	quit strStatus
 	
	
SetQuantidadeVisivel()
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteBeforeDataField na customização do campo (4)-Quantidade do form
    ;	VARPedidoCompraLinha.
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	set idPedidoCompra = $piece($get(YKEY), $get(YKOMMA), 1)
	if ($length(idPedidoCompra) > 0) {
		set YOPTION = $$OnBeforeEditAcessHook^VARPedidoCompra($get(^VARPedidoCompra(0, idPedidoCompra, 1)))
		do OnBeforeDataField(YKEY)
	
	} else {
		set YOPTION = 1
	}
 
	quit
	
Erro(pMensagem)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnBlurProduto^VARPedidoCompraLinha.MAC
    ; 
    ; Histórico:
    ; 	28-Jun-2010		Criado
    ;-------------------------------------------------------------------------------	
	$$$Alert(pMensagem)
	
	set $piece(pobjYFELD,Y,1) = ""
	quit $$$YES_pMensagem
	
ForcarAtualizacaoAoMudarAba()
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Força a atualização do grid de linhas do PAM quando muda de aba.
    ; 	Caso contrário, a mudança do estado do campo "a arquivar" feita a seguir ocorre 
	;  	apenas na primeira vez.
    ;
    ; Chamado por:
    ;	Função LoadGrid^VARPedidoCompraLinha.MAC
    ; 
    ; Histórico:
    ; 	05-Jul-2010		Criado
    ;-------------------------------------------------------------------------------	
	set idContainer = $$$COMGridEditParameterContainer(YAUSWAHL)
	kill ^WWWDATEN(YM, +$horolog, YUSER, idContainer, "Grid", YFORM)
	quit
	
GetAFMCountNotReceived(pidPAM, pidItem)
 
 	set intAFMCount = 0
	set idCompra = ""
	for { 
		set idCompra = $order(^INAUFPs(YM,104,$$$Index(pidPAM),idCompra))
		quit:(idCompra = "")
		set idCompraLinha = ""
		for {
			set idCompraLinha = $order(^INAUFP(YM,idCompra,idCompraLinha))
			quit:(idCompraLinha = "")
			
			set objCompraLinha = $get(^INAUFP(YM,idCompra,idCompraLinha,1))
			continue:($$$INAUFPItemNumber(objCompraLinha) '= pidItem)
			if '$$HasItemFullyReceived^VARCompraLinha(idCompra, pidItem) {
				set intAFMCount = intAFMCount + 1
			}
		}
	}
 
	quit intAFMCount
 
ProdutoReadOnly()
	if ($$$VARPedidoCompraStatus(^VARPedidoCompra(YM,$$$GRIDYKEYContainer,1)) = 1) { // 1 - Processado
		quit $$$YES
	}
	quit $$$NO
 
ValidaNatureza(pYKEY, &pobjYFELD="")
	quit:($$$NoKey(pYKEY)) $$Erro("Nenhum PAM selecionado")
	
	set idPedidoCompra	= $$$KEY1(pYKEY)
	set objPedidoCompra	= $get(^VARPedidoCompra(YM,idPedidoCompra,1))
	set NaturezaPedido	= $$$VARPedidoCompraFontedeNatureza(objPedidoCompra)
 
	if (pobjYFELD '= "") {
		set idProduto = $piece(pobjYFELD,Y,1)
		if idProduto = "" {
			set $piece(pobjYFELD,Y,2) = ""	;Unidade
			set $piece(pobjYFELD,Y,3) = ""	;Grupo
			set $piece(pobjYFELD,Y,4) = ""	;Quantidade
			set $piece(pobjYFELD,Y,5) = ""	;CMM
			set $piece(pobjYFELD,Y,6) = ""	;Estoque
			quit
		}
	}
	
	set objProduto 		= $get(^INART(0,idProduto,1))	
	set Grupo	   		= $$$INARTItemGroup(objProduto)
	set Unidade    		= $$$INARTUnitofMeasure(objProduto)
	set ProdutoNatureza	= $$$INARTFREE1(objProduto)
	
	set blnErro = $$$NO
	
	// 25-Ago-2010 Karine : Copied from INRECSummary - Inactive items cannot be received	
	if idProduto '= "" {
		set statusProduto = $$CheckStatusFor^VARItemStatus(idProduto, $$$FldINItemStatusPurchaseOrders)
		if (statusProduto = 0) {
			set $piece(pobjYFELD, Y, 1) = ""
			set descStatus = $$$INItemStatusDescription($get(^INItemStatus(YM,statusProduto,1)))
			set blnErro = $$Erro("O status do produto '"_idProduto_"' não permite criar PAMs.")
		}
	}
	
	if ((NaturezaPedido >= 1) && (NaturezaPedido <= 4)) { //PAM's de Estoque
		
		if ((ProdutoNatureza = 5) || (ProdutoNatureza = 6)) { //Produto é patrimônio ou uso específico
			set blnErro = $$Erro("A natureza de compra do produto não confere com a natureza de entrada do PAM.")
		}	
		
		elseif ($extract(idProduto,1) = "P") { // idProduto começa com P
			set blnErro = $$Erro("O código do produto começa com P. A natureza de entrada do PAM não é patrimônio.")
		}	
	}
	elseif NaturezaPedido = 5 { //PAM de Patrimônio
	
		if ($extract(idProduto,1) '= "P") {
			set blnErro = $$Erro("O código do produto não começa com P.")
		}
 
		elseif ($extract(idProduto,1) = "P") && (ProdutoNatureza '= 3)  {
			set blnErro = $$Erro("A natureza de fornecimento do produto não é patrimônio.")
		}		
	}
	elseif NaturezaPedido = 6 { // PAM de Uso Específico				
		
		if ProdutoNatureza '= 6 {
			set blnErro = $$Erro("A natureza de compra do produto não é uso específico.")
		}
		
		elseif ($extract(idProduto,1) = "P") { // Produto começa com P
			set blnErro = $$Erro("O código do produto começa com P. A natureza de entrada do PAM não é patrimônio.")
		}			
	}
	elseif NaturezaPedido = 7 { // PAM de recebimento regional
		if ((ProdutoNatureza = 5) || (ProdutoNatureza = 6)) { //Produto é patrimônio ou uso específico
			set blnErro = $$Erro("A natureza de compra do produto não confere com a natureza de entrada do PAM.")
		}	
		
		elseif ($extract(idProduto,1) = "P") { // idProduto começa com P
			set blnErro = $$Erro("O código do produto começa com P. A natureza de entrada do PAM não é patrimônio.")
		}	
	}
	
	quit blnErro
 
isGridReadOnly(pidPedidoCompra)
	quit:(pidPedidoCompra = "") $$$NO
	new objPedidoCompra	
	set objPedidoCompra = $get(^VARPedidoCompra(YM,$$$KEY1(pidPedidoCompra),1))
	quit:(objPedidoCompra = "") $$$NO
	// se o status for diferente de "Aberto"
	quit:(+$$$VARPedidoCompraStatus(objPedidoCompra) > 0) $$$YES
 
	quit $$$NO
 
GetObservacaoProgramacao()
	;17-Oct-2014	shobby	ALPHAUP-352: Disabled call to nonexistent tag.
	quit "" ;$$GetObservacaoProgramacao^VARAlertaLocalLinha($$$VARPedidoCompraLinhaProduto(YFELD))
 
GetQuantidadeRecebida(pidPedidoCompra,pidItem)
	new idCompra, idCompraLinha, objCompraLinha, qtyTotalRecebida, qtyTotalEstornada, qtTotalRecebida
	set qtTotalRecebida = 0
	set idCompra = ""
	set idCompra = $order(^INAUFPs(YM,104,$$$Index(pidPedidoCompra),idCompra))
	while(idCompra '= "") { 
		set idCompraLinha = ""
		set idCompraLinha = $order(^INAUFP(YM,idCompra,idCompraLinha))
		while(idCompraLinha '= "") {
			set objCompraLinha = $get(^INAUFP(YM,idCompra,idCompraLinha,1))
			if ($$$INAUFPItemNumber(objCompraLinha) = pidItem) {
				set qtyTotalRecebida  = $$getQuantidadeRecebida^VARCompra(idCompra,idCompraLinha)
				set qtyTotalEstornada = $$getQuantidadeEstornada^VARCompra(idCompra,idCompraLinha) 
				set qtTotalRecebida = qtyTotalRecebida - qtyTotalEstornada
			}
			set idCompraLinha = $order(^INAUFP(YM,idCompra,idCompraLinha))
		}
		set idCompra = $order(^INAUFPs(YM,104,$$$Index(pidPedidoCompra),idCompra))
	}
 
	quit qtTotalRecebida

OnBeforeDataField(pYKEY)
	;-------------------------------------------------------------------------------
 	; Deixa o campo somente leitura caso a linha esteja arquivada.
	;------------------------------------------------------------------------------- 
	quit:(pYKEY = "")
	
	new idpedidoCompra, idPedidoCompraLinha, objLinha
	
	set idPedidoCompra 	 	= $$$KEY1(pYKEY)
	set idPedidoCompraLinha = $$$KEY2(pYKEY)
	
	quit:(idPedidoCompraLinha = "")
	
	set objLinha = $get(^VARPedidoCompraLinha(YM,idPedidoCompra,idPedidoCompraLinha,1))
	set fltQuantidadePedido    = $$$VARPedidoCompraLinhaQuantidade(objLinha)
	set fltQuantidadeArquivada = +$$$VARPedidoCompraLinhaQuantidadeArquivada(objLinha)
	
	;Se a quantidade estiver totalmente arquivada, deixa a linha somente leitura
	if ( (fltQuantidadePedido = 0) && (fltQuantidadeArquivada > 0) ) {
		set YHID = 1
	}

	quit
]]></Routine>
</Export>