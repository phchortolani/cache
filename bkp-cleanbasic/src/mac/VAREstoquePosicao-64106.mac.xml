<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VAREstoquePosicao" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VAREstoquePosicao
	#include COMSYS
	quit

OnAfterSave
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Seta as variáveis recebidas dos campos do form (manual) e recarrega a página.
    ;
    ; Chamado por:
    ;	Campo OnAfterSave do form VAREstoquePosicao.
    ; 
    ; Histórico:
	;	01-Mai-2012		Gustavo		Incluído VARLogAfterSave    
    ;	23-Abr-2012		Lucas		Adicionado filtro por Conta Contábil.
    ; 	16-Jun-2011		Refatorado
    ;-------------------------------------------------------------------------------
	new Local
	set Local = $get(VORG(1))
	
	kill ^VARTempEstoquePosicao(YM,YBED)

	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,1) = Local
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,2) = $get(VORG(2))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,3) = $get(VORG(3))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,4) = $get(VORG(4))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,5) = $get(VORG(5))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,6) = $get(VORG(6))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,7) = $get(VORG(7))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,8) = $get(VORG(8))
	set $piece(^VARTempEstoquePosicao(YM,YBED,1),Y,9) = $get(VORG(9))
	
	set arrParam("YOPTION") = 1
	do VARLogAfterSave^VARUtil(YBED,YFORM)
 	do GoToForm^COMUtilForm("VAREstoquePosicao",Local,,,,,,,.arrParam)	
	
	quit
	
OnAfterDataFields
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	1. Executa os eventos de javascript que manipulam os campos da tela.
    ;	2. Verifica se o usuário utilizou o botão Ok do form.
    ;	3. Seta as variáveis para construção do SQL e monta a query.
    ;	4. Exibe os resultados da query na tela
    ;
    ; Chamado por:
    ;	Campo OnAfterDataFields do form VAREstoquePosicao.
    ; 
    ; Histórico:
	;	01-Mai-2012		Gustavo		Incluído VARLogAfterSave
	;	23-Abr-2012		Lucas		Adicionado filtro por Conta Contábil.
    ; 	16-Jun-2011		Refatorado
    ;-------------------------------------------------------------------------------	
	do PrintJS
	
	//Só apresenta os resultados da consulta caso o usuário tenha clicado no botão 'Ok' do form.
	quit:('$$isAfterSaveLogged^VARUtil(YBED,YFORM))
	do ClearVARLogAterSave^VARUtil(YBED,YFORM)
	
	new objTemp, vLocal, vOrderBy, vFormato, vGrupo, vMostrarEstoque, vUnidade, vMostrarProgramas, vContaContabil
	set objTemp = $get(^VARTempEstoquePosicao(YM,YBED,1))
	
	set vLocal         	  = $piece(objTemp,Y,1)
	set vOrderBy 		  = $piece(objTemp,Y,2)
	set vFormato 		  = $piece(objTemp,Y,3)
	set vGrupo  	      = $piece(objTemp,Y,4)
	set vMostrarEstoque   = $piece(objTemp,Y,5)
	set vUnidade	      = $piece(objTemp,Y,6)
	set vMostrarProgramas = $piece(objTemp,Y,7)
	set vContaContabil    = $piece(objTemp,Y,8)
	set vTipoEstoque      = $piece(objTemp,Y,9)

	if (vMostrarEstoque = "") set vMostrarEstoque = 2
	if (vLocal = "") set vLocal = YKEY	
	quit:( (vOrderBy = "") || (vFormato = "") )

	if ( (vUnidade = "") && (vMostrarEstoque = 1 ) ) {
		$$$Alert("É necessário selecionar uma unidade.")
		quit
	}	
	
	if ( (vLocal = "") && (vMostrarEstoque = 2 ) ) {
		$$$Alert("É necessário selecionar um local de estoque.")
		quit
	}
	
	//Monta query
	set querySQL = $$MontaSQL(vLocal,vOrderBy,vFormato,vGrupo,vUnidade,vContaContabil,vTipoEstoque)
	
	//Exibe resultados
	do ExibeResultados(querySQL,vLocal,vOrderBy,vFormato,vGrupo,vMostrarEstoque,vUnidade,vMostrarProgramas)
	
	quit
	
MontaSQL(pLocal,pOrderBy,pFormato,pGrupo,pUnidade,pvContaContabil,pTipoEstoque)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Monta a consulta SQL que será utilizada na tela.
    ;
    ; Chamado por:
    ;	Método OnAfterDataFields^VAREstoquePosicao.mac
    ; 
    ; Histórico:
    ; 	16-Jun-2011		Refatorado
    ;	12-Jan-2012		Lucas		Adaptado para novo formato "conta contábil"
    ;	23-Abr-2012		Lucas		Adicionado filtro por Conta Contábil.
    ;-------------------------------------------------------------------------------	
	
	new meuSQL,showPhysical
	set meuSQL = ""
	if pFormato=4 {
		set pFormato=1
		set showPhysical=1
	}else {
		set showPhysical=0
	}
	if (pFormato = 3){
		set meuSQL = "SELECT "
		set meuSQL = meuSQL_" Contabil.Cod_ContaContabil AS Código,  "
		set meuSQL = meuSQL_" $$SQLGetDescContaContabil^VARSQL(Contabil.Cod_ContaContabil) as Descrição, "
		set meuSQL = meuSQL_" SUM(Contabil.Quantidade) AS Quantidade,"
		set meuSQL = meuSQL_" SUM(Contabil.ValorTotal) AS Valor_Total "
		set meuSQL = meuSQL_" FROM ( "
	}
	
 	set meuSQL = meuSQL_"SELECT "
 	
 	if (pFormato = 3){
	 	set meuSQL = meuSQL_" FREE3 AS Cod_ContaContabil, "	
 	}
 	
	set meuSQL = meuSQL_" alSOH.dBundleStock.Item, "
	set meuSQL = meuSQL_" I.SearchName as Descricao, "
	set meuSQL = meuSQL_" alSOH.dBundleStock.Storage->Location, "
	set meuSQL = meuSQL_" alSOH.dBundleStock.Storage->Code, "
	
	if ((pFormato = 1) || (pFormato = 3)) {
		set meuSQL = meuSQL_" SUM(+alSOH.dBundleStock.QtyOnHand) AS Quantidade, "
		if (pFormato = 3){
	   		set meuSQL = meuSQL_"(SUM(+alSOH.dBundleStock.QtyOnHand) * $$SQLGetCustoMedio^VARSQL(alSOH.dBundleStock.Item)) as ValorTotal, "
		}
	} else {
		set meuSQL = meuSQL_" +alSOH.dBundleStock.QtyOnHand, "
	}
	
	set meuSQL = meuSQL_" alSOH.dBundleStock.Bundle->LotNumber, "
	set meuSQL = meuSQL_" alSOH.dBundleStock.Bundle->UseByDate, "
	set meuSQL = meuSQL_" alSOH.dBundleStock.Bundle->Program, "
	set meuSQL = meuSQL_" alSOH.dBundleStock.Bundle->Brand, "
	set meuSQL = meuSQL_" I.ItemGroup as Grupo "
	set meuSQL = meuSQL_" FROM alSOH.dBundleStock "
	set meuSQL = meuSQL_" JOIN SQLUser.INART I ON (I.ItemNumber = alSOH.dBundleStock.Item) "
	
	if (pLocal '= "") && (pUnidade = "") {
		set meuSQL = meuSQL_" WHERE alSOH.dBundleStock.Storage->Location = '"_pLocal_"'"	
	} elseif (pLocal = "") && (pUnidade '= "")  {
		set meuSQL = meuSQL_" WHERE $$GetUnidadedoLocal^VARWWW0121(alSOH.dBundleStock.Storage->Location) = '"_vUnidade_"'"
	}
	
	if (pGrupo '= "") {
		set meuSQL = meuSQL_" AND $$SQLGetFiltroGrupo^VARSQL('"_vGrupo_"',alSOH.dBundleStock.Item) = 1 "
	}
	
	if (pvContaContabil '= "") {
		set meuSQL = meuSQL_" AND I.FREE3 = '"_pvContaContabil_"'"
	}
	
	if (pTipoEstoque '="") {
		set meuSQL = meuSQL_" AND I.FREE35 = '"_pTipoEstoque_"'"
	}
	
	if (pFormato=1) {
		if showPhysical=1 {
			set meuSQL = meuSQL_" GROUP BY dBundleStock.Item,alSOH.dBundleStock.Bundle->LotNumber,alSOH.dBundleStock.Bundle->UseByDate"
		}else {
			set meuSQL = meuSQL_" GROUP BY dBundleStock.Item"
		}
	}
	
	
	if (pFormato = 3){
		set meuSQL = meuSQL_" GROUP BY dBundleStock.Item"
		set meuSQL = meuSQL_" ) AS Contabil "
		set meuSQL = meuSQL_" GROUP BY Contabil.Cod_ContaContabil"
	}
	
	set meuSQL = meuSQL_" ORDER BY "_vOrderBy
	set ^zzCarlos("hoje") = meuSQL
	quit meuSQL


ExibeResultados(pquerySQL,pLocal,pOrderBy,pFormato,pGrupo,pMostrarEstoque,pUnidade,pMostrarProgramas)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Exibe os resultados da query em uma tabela.
    ;
    ; Chamado por:
    ;	Método OnAfterDataFields^VAREstoquePosicao.mac
    ; 
    ; Histórico:
    ; 	16-Jun-2011		Refatorado
    ;	12-Jan-2012		Lucas		Adaptado para novo formato "conta contábil"
    ;	23-Abr-2012		Lucas		Adicionado filtro por Conta Contábil.
    ;-------------------------------------------------------------------------------	
	new rsPosicaoEstoque, lstHeader, count, totalQty, ValorTotal, Item, Descricao, Local, Storage,
		Quantidade, Lote, Validade, TotalItem, objItem, idUnit, descUnit, descGroup, conta, descConta,
		idGroup, descGroup, preHintGrupo, posHintGrupo, avgCost, TotalItem, ValorTotal, isBlockedStorage,
		reasonBlocked, prefixStyle, posfixStyle, columnPosition, idPrograma, idMarca, objStorage,
		blnTSU, descStorage, nomeMarca, nomePrograma, physicalStorage, isBlockedPhysicalStorage,showPhysical

	
	set rsPosicaoEstoque = ##class(%ResultSet).%New()  ;Create Result Set Object		
	
	if pFormato=4 {
		set pFormato=1
		set vFormato=1
		set showPhysical=1
	}else {
		set showPhysical=0
	}
	
	do rsPosicaoEstoque.Prepare(pquerySQL)  ;Prepare Query
	do rsPosicaoEstoque.Execute()  		  ;Execute Query
	
	if ( (pUnidade '= "") && (pMostrarEstoque = 1) ) {		
		do PrintMsgEstoquesDaUnidade^VARWWW0121(pUnidade)
	}
	
	kill ^VARTempPosicaoEstoqueGrupo
	set ^VARTempPosicaoEstoqueGrupo = ""
	
	// Tabela
 	set lstHeader = "" 	
 	
 	if (pFormato = "1") { ;Contábil
 		if showPhysical=1 {
 			set lstHeader = lstHeader_$listbuild("Cód.","Produto","Grupo","Lote","Validade","Quantidade&nbsp;<br />&nbsp;&nbsp;Disponível","Quantidade&nbsp;<br />&nbsp;&nbsp;Total","UN","CMMLi","DMML","Custo&nbsp;<br />&nbsp;&nbsp;Médio (R$)","Valor Total (R$)") 	
 		}else {
	 		set lstHeader = lstHeader_$listbuild("Cód.","Produto","Grupo","Quantidade&nbsp;<br />&nbsp;&nbsp;Disponível","Quantidade&nbsp;<br />&nbsp;&nbsp;Total","UN","CMMLi","DMML","Custo&nbsp;<br />&nbsp;&nbsp;Médio (R$)","Valor Total (R$)") 	
 		}
 	}
 	
 	if (pFormato = "2") { ;Físico  	
  		if (pMostrarProgramas = $$$YES) {
			set lstHeader = lstHeader_$listbuild("Cód.","Produto","Grupo","Endereço","Lote","Validade","Programa","Marca","Quantidade","UN","Custo&nbsp;<br />&nbsp;&nbsp;Médio (R$)","Valor Total (R$)")	  		
  		} else {
			set lstHeader = lstHeader_$listbuild("Cód.","Produto","Grupo","Endereço","Lote","Validade","Marca","Quantidade","UN","Custo&nbsp;<br />&nbsp;&nbsp;Médio (R$)","Valor Total (R$)")	  		
  		}	
 	}
 	
 	if (pFormato = "3"){ ; Conta Contábil
	 	set lstHeader = lstHeader_$listbuild("Cód.","Descrição","Quantidade","Valor (R$)") 	
 	} 
 	
 	if $$Start^COMTable(lstHeader) {
		
		set count      = 0
		set totalQty   = 0
		set ValorTotal = 0
		set isBlockedPhysicalStorage = ""
		
		While (rsPosicaoEstoque.Next()) {
			
			if (vFormato = 3){
				set CodigoContabil			= rsPosicaoEstoque.GetData(1)
				set descContabil			= rsPosicaoEstoque.GetData(2)
				set QuantidadeContabil  	= rsPosicaoEstoque.GetData(3)
				set ValorContabil 			= rsPosicaoEstoque.GetData(4)
			}
			else {
				set Item       = rsPosicaoEstoque.GetData(1)
				set Descricao  = rsPosicaoEstoque.GetData(2)
				set Local      = rsPosicaoEstoque.GetData(3)
				set Storage    = rsPosicaoEstoque.GetData(4)
				set Quantidade = rsPosicaoEstoque.GetData(5)
				set Lote       = rsPosicaoEstoque.GetData(6)
				set Validade   = rsPosicaoEstoque.GetData(7)
				set idPrograma = rsPosicaoEstoque.GetData(8)
				set idMarca    = rsPosicaoEstoque.GetData(9)
			}
			
			
			if (vFormato = 3) {
				set ValorTotal = ValorTotal + ValorContabil
			}
			
			if (vFormato '= 3){
				set TotalItem = 0
		 		set objItem   = $get(^INART(YM,Item,1))
		 	
			 	continue:(objItem = "")
 			
				set descItem = $piece(objItem,Y,1)		 	
	 			
 				;Endereço 			
				set descStorage = Storage
 				if (Storage '= "") {
					set objStorage = $get(^INLP(YM,Local,Storage,1))
					set blnTSU     = $piece(objStorage,Y,21)
	 				
 					if Local = $$getFarmaciaAcoesJudiciais^VARParametroCliente(0) {
		 				if $$SQLGetPatientName^VARSQL(Storage) '= ""{
							set descStorage = $$SQLGetPatientName^VARSQL(Storage)
						}
						
					} elseif (blnTSU = $$$YES) {
						
						set physicalStorage = $piece(objStorage,Y,22)					
						if (physicalStorage '= "") {					
							set isBlockedPhysicalStorage = $$checkIfBlockedStorage(Local,physicalStorage)
						}			
				
						set descStorage = physicalStorage_" ["_Storage_"]"					
					}
 				}
				
				;Unidade de Medida		
				set idUnit   = $piece(objItem,Y,40)			
				set descUnit = ""
				
				if (idUnit '= "") {
					set descUnit = $piece($get(^WWW101(YM,"EINHEIT",SPRACHE,idUnit,1)),Y,1)
				}
				
				;Grupo
				set idGroup   = $piece(objItem,Y,30)
				set descGroup = ""
				
				if (idGroup '= "") {
					set descGroup  = $piece($get(^INItemGroup(YM,idGroup,1)),Y,2)
					set preHintGrupo = "<span title = '("_idGroup_") "_descGroup_"'>"
					set posHintGrupo = "</span>"
				}
				
				
				;Marca e Programa
				set nomeMarca    = $$GetBrandName^VARSQL(idMarca)
				set nomePrograma = $$GetProgramName^VARSQL(idPrograma)
				
				;Petik 28/12/2009
				;Alterado para o novo campo Conta Contábil (D278)
				set conta = $piece(objItem,Y,278)
				set descConta = ""
				if (conta '= "") {
					set descConta  = $piece($get(^INPARA(YM,"VARTCICONTACONTABIL","PT",conta,1)),Y,1)
				}
			
				set avgCost    = $$GetCost^INItemCost(Item)
				set TotalItem  = Quantidade * avgCost
				set ValorTotal = ValorTotal + TotalItem
						
				set count = $i(count)
				set totalQty = totalQty + Quantidade			
				
				set isBlockedStorage = ""
				set isBlockedStorage = $$checkIfBlockedStorage(Local,Storage)
				
				set prefixStyle = ""
				set posfixStyle = ""
			
				if ( (Validade < $horolog) && (Validade '= "") && (vFormato = 2) ) {
					set prefixStyle = "<font color=red style=text-decoration:line-through><span title = 'Produto vencido.'>"
					set posfixStyle = "</span></font>"			
			
				} elseif ( ( (isBlockedStorage = $$$YES) || (isBlockedPhysicalStorage = $$$YES) ) && (vFormato = 2) ) {
				
					if (blnTSU = $$$YES) {
						set reasonBlocked = $$getMotivoEndBloqueado^VARWMSUtils(Local,physicalStorage)
					} else {
						set reasonBlocked = $$getMotivoEndBloqueado^VARWMSUtils(Local,Storage)
					}
	
					set prefixStyle = "<font color='gray' style='text-decoration:line-through'><span title = 'Endereço bloqueado. Motivo: "_reasonBlocked_"'>"
					set posfixStyle = "</span></font>"
				}
				
				if (vFormato = 1) {
					if showPhysical=1 {
						set columnPosition = 9
					}else {
						set columnPosition = 7
					}
				}
				if (vFormato = 2) {
					if (pMostrarProgramas = $$$YES) {
						set columnPosition = 11
					} else {
						set columnPosition = 10
					}
				}
			
				if (Validade '= "") set Validade = $zdate(Validade,4)			
			
			
			} // Fim da verificação da linha 231, (if (vFormato '= 3))
			
			do NewLine^COMTable()
	    	
	    	if (vFormato = 3){
		    	do InsertCell^COMTable(CodigoContabil,,,,"CENTER")
		    	do InsertCell^COMTable(descContabil,,,,"CENTER")
		    	do InsertCell^COMTable($$^WWWTR(0,12,QuantidadeContabil),,,,"RIGHT")
		    	do InsertCell^COMTable($$^WWWTR(0,8,ValorContabil,2),,,,"RIGHT")
		    	do EndLine^COMTable()
	    	}
	    	
	    	else {
	    		do InsertCell^COMTable(Item)
	    		do InsertCell^COMTable(descItem,,,,,,,,,1)
	    		do InsertCell^COMTable(preHintGrupo_idGroup_posHintGrupo,,,,"CENTER")
	    		if ((showPhysical=1) && (vFormato=1)) {
		    		do InsertCell^COMTable(prefixStyle_Lote_posfixStyle)
	    			do InsertCell^COMTable(prefixStyle_Validade_posfixStyle)
	    		}
	    	}
	    	
	    	if (vFormato = 2) {
		    	do InsertCell^COMTable(prefixStyle_descStorage_posfixStyle,,,,,,,,,1)
	    		do InsertCell^COMTable(prefixStyle_Lote_posfixStyle)
	    		do InsertCell^COMTable(prefixStyle_Validade_posfixStyle)
	    		
				if (pMostrarProgramas = $$$YES) {
					do InsertCell^COMTable(prefixStyle_nomePrograma_posfixStyle,,,,,,,,,1)
				}
	    		if (nomeMarca = ""){
		    		do InsertCell^COMTable("",,,,,,,,,1)
		    	}
		    	else{
			    	do InsertCell^COMTable(prefixStyle_nomeMarca_posfixStyle,,,,,,,,,1)	
			    }
	    		
	    	}
	    	
	    	if (vFormato '= 3){
		    	if (vFormato = 1) {
			    	if (showPhysical) {
				    	do InsertCell^COMTable(prefixStyle_$$^WWWTR(0,12,$$GetEstoqueDisponivelLote^VARReposicao(Item,Local,$get(Lote),rsPosicaoEstoque.GetData(7)))_posfixStyle,,,,"RIGHT")
			    	}
			    	else {
			    		do InsertCell^COMTable(prefixStyle_$$^WWWTR(0,12,$$GetEstoqueDisponivel^VARReposicao(Item,Local))_posfixStyle,,,,"RIGHT")
			    	}
		    	}
	    		do InsertCell^COMTable(prefixStyle_$$^WWWTR(0,12,Quantidade)_posfixStyle,,,,"RIGHT")
	    		do InsertCell^COMTable($piece(descUnit," - ",1),,,,"CENTER")	
	    		if (vFormato = 1) do InsertCell^COMTable($$getCMMLi^VARReposicao(Local,Item))
	    		if (vFormato = 1) do InsertCell^COMTable($piece($$^WWWTR(0,12,$$GetDM^VARReposicao(Item,Local)),",",1),,,,"RIGHT") //Alterado para a SESPE
	    		do InsertCell^COMTable($$^WWWTR(0,8,avgCost,4),,,,"RIGHT")
	    		do InsertCell^COMTable($$^WWWTR(0,8,TotalItem,2),,,,"RIGHT")
	    		do EndLine^COMTable()
	    	}
		} // fim do while
		
		if (vFormato '= ""){
			do NewLine^COMTable("lightsteelblue")
		}
		
		if (vFormato = 1) {
			if showPhysical=1 {
				set columnPosition = 11
			}else {
				set columnPosition = 9
			}
		}
		if (vFormato = 2) {
			if (pMostrarProgramas = $$$YES) {
				set columnPosition = 11
			} else {
				set columnPosition = 10
			}
		}
		if (vFormato = 3) set columnPosition = 3
		
		if (vFormato '= ""){
			do InsertCell^COMTable("<strong>Valor Total em Estoque</strong>",,,,"RIGHT",,columnPosition)
			do InsertCell^COMTable("<strong>R$ "_$$^WWWTR(0,8,ValorTotal,2)_"</strong>",,,,"RIGHT",,12)		    
		}
	  
		do EndLine^COMTable()
		
		do Stop^COMTable()	
  	}
	
	do rsPosicaoEstoque.Close()

	quit	
	
checkIfBlockedStorage(pidLocal,pidStorage)
	if pidStorage = "" quit ""
	quit $piece($get(^INLP(YM,pidLocal,pidStorage,1)),Y,8)	
	
	
OnBeforeButtonLine(pYFELD)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Carrega listas temporárias que populam os campos dropdown do form.
    ;
    ; Chamado por:
    ;	Campo OnBeforeButtonLine do form VAREstoquePosicao
    ; 
    ; Histórico:
    ; 	16-Jun-2011		Refatorado
    ;-------------------------------------------------------------------------------	
	do LoadListaUnidades^VARWWW0121
	do LoadListaLocaisDeEstoque^VAREstoqueZerado
	do LoadListaOrdenarPor
	quit
	
LoadListaOrdenarPor
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Carrega listas temporárias que populam os campos dropdown do form.
    ;
    ; Chamado por:
    ;	Método OnBeforeButtonLine^VAREstoquePosicao.mac
    ;
    ; Histórico:
    ; 	16-Jun-2011		Refatorado
    ;	12-Jan-2012		Lucas		Adaptação de "nome" para conta contábil	
    ;-------------------------------------------------------------------------------
	set ^COMTempList(YM,YUSER,"VAROrdenarPosEstoque",1,1)="Código"
	set ^COMTempList(YM,YUSER,"VAROrdenarPosEstoque",2,1)="Descrição"
	
	quit
	
Imprimir()
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Imprime o relatório da tela.
    ;-------------------------------------------------------------------------------
	new DescOrdenar,TipoEstoque,MostrarPrograma
	
	set Local       	= $get(VORG(1))
	set OrdenarPor  	= $get(VORG(2))
	set Formato 		= $get(VORG(3))
	set Grupo       	= $get(VORG(4))
	set Unidade			= $get(VORG(6))
	set ContaContabil 	= $get(VORG(8))
	set TipoEstoque	  	= $get(VORG(9))
	set MostrarPrograma = $get(VORG(7))
	
	if (Local = "")&&(Unidade = "") {
		$$$Alert("É necessário preencher o local ou unidade, bem como todos os campos obrigatórios.")
		do GoToForm^COMUtilForm("VAREstoquePosicao",YKEY)
	}
	
	;if (OrdenarPor '= "") {  ;preventing ILLEGAL VALUE error
		;set DescOrdenar = $Case(OrdenarPor,1:"Código",2:"Descrição")
	;}
	// 17-Nov-2011 Karine variável "DescOrdenar" não é usada em lugar nenhum !!!
	
	if (Grupo '= "") {
		set Filtro    = " AND $$SQLGetFiltroGrupo^VARSQL("""_Grupo_""",alSOH.dBundleStock.Item) = 1 "
		set DescGrupo = Grupo_" - "_$piece($get(^INItemGroup(YM,Grupo,1)),Y,2)
	}
	else {
		set Filtro    = ""
		set DescGrupo = "Todos"
	}
	
	if (ContaContabil '= "") {
		set Filtro = Filtro_ " AND I.FREE3 = """_ContaContabil_""""
		set DescContaContabil   = $$SQLGetDescContaContabil^VARSQL(ContaContabil)
	}
	else {
		set DescContaContabil   = "Todas"
	}
	
	do RunReportPosicaoEstoque^VARJasperRunReport(Local,Unidade,OrdenarPor,Formato,Filtro,DescGrupo,DescContaContabil,TipoEstoque,MostrarPrograma)
	
	quit

PrintJS
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Executa os eventos de javascript que manipulam os campos da tela.
    ;
    ; Chamado por:
    ;	Método OnAfterDataFields^VAREstoquePosicao.mac
    ;
    ; Histórico:
    ; 	16-Jun-2011		Criado
    ;	12-Jan-2012		Lucas	Adaptado o OnBlurFormato para formato ContaContábil
    ;-------------------------------------------------------------------------------

	write !, "<script language=""javascript"">"
	
	&js<
	
		function OnLoadEstoquePosicao() {
			OnBlurMostrarEstoque();	
		}
		
		function OnBlurMostrarEstoque() {
			if (document.getElementById('Y#(YFORM)#M5').value == 1) {

				// Desabilita e limpa campo Local de Estoque
				document.getElementById('Y#(YFORM)#M1').value = '';				
				document.getElementById('Y#(YFORM)#M1').onblur();
				document.getElementById('Y#(YFORM)#M1').disabled = true;
				document.getElementById('Y#(YFORM)#M1').style.background = '#E0E0E0';				
				
				// Desabilita e seta campo Formato
				document.getElementById('Y#(YFORM)#M3').value = 1;
				document.getElementById('Y#(YFORM)#M3').onblur();
				document.getElementById('Y#(YFORM)#M3').disabled = true;
				document.getElementById('Y#(YFORM)#M3').style.background = '#E0E0E0';
				
				// Habilita campo Unidade
				document.getElementById('Y#(YFORM)#M6').disabled = false;
				document.getElementById('Y#(YFORM)#M6').style.background = '#EEE8AA';
				
				if (document.getElementById('Y#(YFORM)#M3').value != 2) {
					// Desabilita e limpa campo Mostrar Programas
					document.getElementById('Y#(YFORM)#M7').checked = false;
					document.getElementById('Y#(YFORM)#M7').disabled = true;
				}
				
				// Habilita campo Tipo de Estoque
				document.getElementById('Y#(YFORM)#M9').disabled = false;
				document.getElementById('Y#(YFORM)#M9').style.background = '#FFFAFA';
			
			} else if (document.getElementById('Y#(YFORM)#M5').value == 2) {

				// Habilita campo Local de Estoque			
				document.getElementById('Y#(YFORM)#M1').disabled = false;
				document.getElementById('Y#(YFORM)#M1').style.background = '#EEE8AA';
				
				// Habilita campo Formato
				document.getElementById('Y#(YFORM)#M3').disabled = false;
				document.getElementById('Y#(YFORM)#M3').style.background = '#EEE8AA';				
				
				// Desabilita e limpa campo Unidade
				document.getElementById('Y#(YFORM)#M6').value = '';
				document.getElementById('Y#(YFORM)#M6').onblur();
				document.getElementById('Y#(YFORM)#M6').disabled = true;
				document.getElementById('Y#(YFORM)#M6').style.background = '#E0E0E0';
				
				//if (document.getElementById('Y#(YFORM)#M3').value != 2) {
				// Habilita o campo Mostrar Programas
				//	document.getElementById('Y#(YFORM)#M7').disabled = false;
				//}
				
				// Desabilita e limpa campo Tipo de Estoque
				document.getElementById('Y#(YFORM)#M9').value = '';
				document.getElementById('Y#(YFORM)#M9').onblur();
				document.getElementById('Y#(YFORM)#M9').disabled = true;
				document.getElementById('Y#(YFORM)#M9').style.background = '#E0E0E0';					
			
			}
			
		}
		
		function OnBlurFormato() {
			if ((document.getElementById('Y#(YFORM)#M3').value == 1) || (document.getElementById('Y#(YFORM)#M3').value == 3) || (document.getElementById('Y#(YFORM)#M3').value == 4)) {

				// Desabilita e limpa campo Mostrar Programas
				document.getElementById('Y#(YFORM)#M7').checked = false;
				document.getElementById('Y#(YFORM)#M7').disabled = true;
			
			} else {
				// Habilita o campo Mostrar Programas
				document.getElementById('Y#(YFORM)#M7').disabled = false;	
			}				
		}		
		
		function addEvent(obj, evType, fn) {
 			if (obj.addEventListener) {
   				obj.addEventListener(evType, fn, false);
   				return true;
 			} else if (obj.attachEvent){
   				var r = obj.attachEvent('on'+evType, fn);
   				return r; 
 			} else {
   				return false;
 			}
		}		
	
		addEvent(document.forms[1].YVAREstoquePosicaoM5, 'blur', OnBlurMostrarEstoque);
		addEvent(document.forms[1].YVAREstoquePosicaoM3, 'blur', OnBlurFormato);
		addEvent(window,'load', OnLoadEstoquePosicao);
			
	>
	
	write !, "</script>"
	
	quit


 /* NÃO IMPLEMENTADO!
 	
GroupChange(pGrupo)
	if (^VARTempPosicaoEstoqueGrupo = ""){
		set ^VARTempPosicaoEstoqueGrupo = pGrupo
		quit $$$YES
	}elseif (^VARTempPosicaoEstoqueGrupo = pGrupo){
		quit $$$NO
	}elseif (^VARTempPosicaoEstoqueGrupo '= pGrupo){
		set ^VARTempPosicaoEstoqueGrupo = pGrupo
		quit $$$YES
	}else{
		quit $$$YES
	}
	
 */
	
	
]]></Routine>
</Export>