<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFPPREIS" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFPPREIS
	#include COMSYS
	#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ANZEIGEN VKPREIS INKL MWST ;display Tax 
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 12-Jul-2007	GRF		SR15572: Naked References; doco; quits; YKUNDE already
	; 						checked - don't repeat
	; 20-Sep-2006	Steve S	Macro usage, added quit command
	; 				DT		created
	;-------------------------------------------------------------------------------
	NEW MWST,PREIS,YAUF,YAUF1,YKUNDE,YADRES
	
	QUIT:$GET(YFELD)=""
	
	SET MWST=$$$INAUFPSalesTax(YFELD)
	IF MWST="" SET MWST=1
	SET YAUF=$PIECE(YKEY,",",1)         QUIT:YAUF=""
	
	SET YAUF1=$GET(^INAUF(YM,YAUF,1))
	IF YAUF1'="" DO
	. SET YKUNDE=$$$INAUFCustomerNumber(YAUF1)  ;KUNDENNUMMER FÜR ANSCHRIFT ;to 
	. QUIT:YKUNDE=""
	. SET YADRES=$GET(^INKUNDE(YM,YKUNDE,1))  ;KUNDENNUMMER FÜR ANSCHRIFT ;to     ; SR15572
	. DO  ;MWST NACH PLZ USA ;Tax within ZIP 
	. . NEW PLZ
	. . SET PLZ=$$$INKUNDEZipCodeStreet(YADRES)
	. . QUIT:PLZ=""
	. . IF $PIECE($get(^INPLZ(0,PLZ,1)),Y,11)'="" SET MWST=$PIECE($get(^INPLZ(0,PLZ,1)),Y,11)   ;BEC;25866;07.06.04;DA ZENTRALE DATEI  ; SR15572 Naked Ref
	. ;
	. IF $$$INKUNDECountry(YADRES)'=""            IF $$$INKUNDECountry(YADRES)'=$GET(YCOUNTRY)      SET MWST=0  ;KEIN DEUTSCHLAND ;no Germany 
	. IF $$$INKUNDETaxIDNumber(YADRES)'=""  IF '$FIND($$$INKUNDETaxIDNumber(YADRES),$GET(YCOUNTRY)) SET MWST=0  ;STEUERID AUSLAND ;foreign country 
	. IF $$$INKUNDEControlIdentifiers(YADRES)'="" IF $$$INKUNDEControlIdentifiers(YADRES)'=1        SET MWST=0  ;KEINE STEUER ;no tax 
	. IF $$$INKUNDETaxFreeUntil(YADRES)'=""       IF $$$INKUNDETaxFreeUntil(YADRES)>=+$HOROLOG      SET MWST=0  ;KEINE STEUER BIS ZUM ;no tax until 
	
	QUIT:+MWST=0
	
	SET PREIS = $$$INAUFPNetSalesPrice(YFELD)
	SET MWST  = +$PIECE($GET(^WWW101(0,"MWST",SPRACHE,MWST,1)),Y,1)
	SET PREIS = $JUSTIFY(PREIS+(PREIS/100*MWST),0,2)
	WRITE "&nbsp;&nbsp;"
	WRITE "("
	WRITE $$^WWWZAHL(PREIS,0,2)
	WRITE "&nbsp;"
	WRITE $$^WWWTEXT(32676)     ; "incl. Tax"
	WRITE ")",YCR
	
	quit
]]></Routine>
</Export>