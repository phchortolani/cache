<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARPedidoCompraLinhaTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARPedidoCompraLinhaTable
	
	#include COMSYS
	#include VARConst

	quit

ShowItems(pidEvent,pidParameters)

	set blnHide = +$$$WWW012HideRelationClassIDs($get(^WWW012(0,YM,1)))
	set idGrid  =  $$$DYNTABLEGridLineFocusGrid(pidParameters)

	set lstHeaders=""
	set lstColTypes=""
		
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dPedidoCompraLinha")

	set idKey	= $$ReferenceKey^COMGridEdit31Interface(idGrid,$$$DYNTABLEGridLineFocusRow(pidParameters))
	set idPAM	= $$$KEY1(idKey)
	set idLine	= $$$KEY2(idKey)
	
	set objPAM	= $$$GRIDYFELDContainer
	set objLine	= $$$GRIDGetYFELD(idLine)

	quit:(('$$$VARPedidoCompraContemAta(objPAM)) || ($$$VARPedidoCompraStatus(objPAM) = 1))
	
	set strStatus 	= $$GetLock^WWWMultiLock("VARPedidoCompraLinha",idGrid,idPAM_$$$COMMA_idLine,$$$NO)
	
	set strFunction = "FillLine"
		
	set idItem 	= $$$VARPedidoCompraLinhaProduto(objLine)
	set idxItem = $$$Index(idItem)
	
	if $$$ISERR(strStatus) {
		write "function "_strFunction_"() {"
		write "alert('"_$$$JSText($$$Text(strStatus))_"');"
		write "}"
	} else {
		write "function "_strFunction_"() {"
		write "CallBack(""FillLine^VARPedidoCompraLinhaTable"", getRowNum(getFocusField()), this.DiscData[0], this.DiscData[1], this.DiscData[2]);"
		write "}"
	}
 	
 	set lstHeaders = $listbuild($$$FieldName("VARAtaLinha","P",$$$FldVARAtaLinhaCodAta),
								$$$FieldName("VARAtaLinha","D",$$$FldVARAtaLinhaNoPregao),
								$$$FieldName("VARAtaLinha","D",$$$FldVARAtaLinhaLote),
								$$$FieldName("VARAtaLinha","D",$$$FldVARAtaLinhaFornecedor),
								$$$FieldName("VARAtaLinha","D",$$$FldVARAtaLinhaQuantidade),
								$$$Text("32004"), //Saldo
								$$$Text("32659"), //Reserva
								$$$Text("32002"), //Saldo após Reserva
								$$$FieldName("VARAtaLinha","D",$$$FldVARAtaLinhaDatadeValidade),
								$$$FieldName("VARAtaLinha","D",$$$FldVARAtaLinhaValorUnitario))

	set lstColTypes = $listbuild($listbuild("left","link"),
							     $listbuild("left","link"),
							     $listbuild("center","link"),
							     $listbuild("left","link"),
							     $listbuild("right","link"),
							     $listbuild("right","link"),
							     $listbuild("right","link"),
							     $listbuild("right","link"),
							     $listbuild("center","link"),
							     $listbuild("right","link"))
							     
	set idAta = $order(^VARAtaLinhas(YM,2,idxItem,""))
	while ($length(idAta) > 0) {
		set idAtaLinha = $order(^VARAtaLinhas(YM,2,idxItem,idAta,""))
		while ($length(idAtaLinha) > 0) {
			
			set objAtaLinha = $get(^VARAtaLinha(YM,idAta,idAtaLinha,1))
			if ($length(objAtaLinha) > 0) {
				
				set dtValidade = $$$VARAtaLinhaDatadeValidade(objAtaLinha)
				// Data de validade tem que ser superior a atual
				if (+dtValidade >= +$horolog) {
			
					set stSituacao = $$GetSituacaoItem^VARAtaLinha(idAta,idAtaLinha,2)
					// Situação deve ser igual a 5 - Aguardando ou 6 - Ativo - Em Uso
					if ((stSituacao = 5) || (stSituacao = 6)) {
			
						set fltSaldo   = $$GetSaldoItemQuant^VARAtaLinha(idAta_$$$COMMA_idAtaLinha)
						//Saldo disponível deve ser maior que zero
						if (fltSaldo > 0) {
				
							set fltReserva = +$$GetQuantidadeReservada^VARAtaLinha(idAta_$$$COMMA_idAtaLinha)
				
							if (fltSaldo > fltReserva) {
				
								set idFornecedor = $$$VARAtaLinhaFornecedor(objAtaLinha)
								set strFornecedor = "( "_idFornecedor_" ) "_$$SQLGetDescFornecedor^VARSQL(idFornecedor)
								set fltSaldoPosReserva = fltSaldo - fltReserva
			
								set ^CacheTempDynamicTable(YUCI,YUSER,"dPedidoCompraLinha",$increment(idLine),"data") = idAta_Y_idAtaLinha_Y_fltSaldo
			
								set ^CacheTempDynamicTable(YUCI,YUSER,"dPedidoCompraLinha",idLine) = $select(blnHide:"",1:"("_idAta_") ")_Y_
																					  $$$VARAtaNumeroEdital($get(^VARAta(YM,idAta,1)))_Y_
																					  $$$VARAtaLinhaLote(objAtaLinha)_Y_
																					  strFornecedor_Y_
																					  $$^WWWTR(0,12,$$$VARAtaLinhaQuantidade(objAtaLinha))_Y_
																					  $$^WWWTR(0,12,fltSaldo)_Y_
																					  $$^WWWTR(0,12,fltReserva)_Y_
																					  $$^WWWTR(0,12,fltSaldoPosReserva)_Y_
																					  $$$FormatDate($$$VARAtaLinhaDatadeValidade(objAtaLinha))_Y_
																					  $$^WWWZAHL($$$VARAtaLinhaValorUnitario(objAtaLinha),0,4,1,"","")
							}
						}
					}
				}
			}
			set idAtaLinha = $order(^VARAtaLinhas(YM,2,idxItem,idAta,idAtaLinha))
		}
		set idAta = $order(^VARAtaLinhas(YM,2,idxItem,idAta))
	}

	do DrawTable^WWW120DynTable(lstHeaders,"dPedidoCompraLinha",lstColTypes,pidParameters,strFunction,pidEvent)

	quit

FillLine(pidRow, pidAta, pidLinhaAtaProduto, pcurSaldo)
	new idPregao, idSRP, objSRP
	
	set idPregao = $$$VARAtaNumeroEdital(^VARAta(YM,pidAta,1))
	set idSRP    = $$$VARPregaoNumeroSRP(^VARPregao(YM,idPregao,1))
	set objSRP 	 = $get(^VARSESSOLICPRECO(YM,idSRP,1))
	
	if ($$$VARPedidoCompraFontedeNatureza($$$GRIDYFELDContainer) '= $$$VARSESSOLICPRECONaturezadaEntrada(objSRP)) {
		$$$Alert("A Natureza da Entrada selecionada para este PAM está divergente da natureza cadastrada na SRP do pregão desta ata "_pidAta_".")
	}
	do SetRecord^COMGridEdit31S1("VARPedidoCompraLinha",pidRow,$$$FldVARPedidoCompraLinhaAta_$$$COMMA_pidAta_Y_
															$$$FldVARPedidoCompraLinhaItemdaAta_$$$COMMA_pidLinhaAtaProduto_Y_
															$$$FldVARPedidoCompraLinhaSaldoItemAta_$$$COMMA_pcurSaldo)
	quit
]]></Routine>
</Export>