<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRECTable" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRECTable            
	
#include COMSYS
#include INConst
#include %occInclude
 
ShowItems(pidEvent,pidParameters)
	;-------------------------------------------------------------------------------
	; Creates a Dynamic Table to show existing receipts containing the order on the
	; line.
	;
	; History
	; 20-Oct-2009	DWR		SR16949: Internationalise
	; 16-Oct-2009	GRF		SR16937: move INREC data outside INRECSummary loop; add
	; 							Receipt Reversal lines
	; 27-Apr-2009	DWR		SR16363: modified to only show parent lines, was showing
	; 							sub lines from Invoice tab
	; 02-Apr-2009	GRF		SR16363: Corrected to get Receipts from Index based on
	; 							order and order line.
	; 20-Mar-2009	DavidR	SR16363: Createdfrom INMOVTable
	;-------------------------------------------------------------------------------
	new fltRecQty,fltTotalQty,idGrid,idKey,idReceipt,idRecLine,idSumLine
	new idSumReceipt,idTableLine,idxOrder,idxOrderLine,lstColTypes,lstHeaders
	new objReceipt,objRecLine,objSummary,strProcessed,strStatus
	
	set strStatus = $$$OK
	set idGrid    = $$$DYNTABLEGridLineFocusGrid(pidParameters)
	
	kill ^CacheTempDynamicTable(YUCI,YUSER,"dINRECSummary")
 
	set idKey = $$ReferenceKey^COMGridEdit31Interface(idGrid,$$$DYNTABLEGridLineFocusRow(pidParameters)) 
	
	set idSumReceipt = $$$KEY1(idKey)
	set idSumLine    = $$$KEY2(idKey)
	set objSummary   = $$$GRIDGetYFELD(idSumLine)
	
	set idxOrder     = $$$Index($$$INRECSummaryPurchaseOrder(objSummary))
	set idxOrderLine = $$$Index($$$INRECSummaryPurchaseOrderLine(objSummary))
	
	if idxOrder = " " {
	;	do DrawTable^WWW120DynTable($listbuild("No Order Selected"),"dINRECSummary",$listbuild($listbuild("left","display")),pidParameters,"",pidEvent)
		do DrawTable^WWW120DynTable($listbuild($$$Text("IN01143")),"dINRECSummary",$listbuild($listbuild("left","display")),pidParameters,"",pidEvent)    ;No Order Selected  ; SR16949
	
	} else {
		set fltTotalQty = 0
		set idTableLine = ""
		
	;---------------------------------------
	; RECEIPTS
	;---------------------------------------
		set idReceipt = ""
		for  {
			set idReceipt = $order(^INRECSummarys(YM,1,idxOrder,idxOrderLine,idReceipt))
			quit:idReceipt=""
		
			set objReceipt = $get(^INREC(YM,idReceipt,1))
			if (($$$INRECStatus(objReceipt) = $$$EnumINSTATUSProcessed) || ($$$INRECStatus(objReceipt) = $$$EnumINSTATUSReversed)) {
				set strProcessed = $$$Text("32526") ; "Yes"
			} else {
				set strProcessed = $$$Text("324")   ; "No"
			}
			
			set idRecLine  = ""
			for  {
				set idRecLine = $order(^INRECSummarys(YM,1,idxOrder,idxOrderLine,idReceipt,idRecLine))
				quit:idRecLine=""
				if idRecLine\1=idRecLine { ; 27-Apr-2009
					
					set objSummary = $get(^INRECSummary(YM,idReceipt,idRecLine,1))
					
					set fltRecQty   = $$$INRECSummaryQuantityReceived(objSummary)   ; in Inventory Unit
					set fltTotalQty = fltTotalQty + fltRecQty
					set ^CacheTempDynamicTable(YUCI,YUSER,"dINRECSummary",$increment(idTableLine)) = idReceipt_", "_idRecLine_Y_strProcessed_Y_fltRecQty
				}
			}
		}
		
	;---------------------------------------  SR16937 vvv
	; REVERSALS
	;---------------------------------------
		set idReceipt = ""
		for {
			set idReceipt = $order(^INRECLines(YM,4,idxOrder,idxOrderLine,idReceipt))
			quit:idReceipt=""
		
			set objReceipt = $get(^INREC(YM,idReceipt,1))
			continue:$$$INRECType(objReceipt)'=$$$EnumINRECSupplierReversal
			
			if $$$INRECStatus(objReceipt) = $$$EnumINSTATUSProcessed {
				set strProcessed = $$$Text("IN01141")   ; "Reversal Processed"
			} else {
				set strProcessed = $$$Text("IN01140")   ; "Reversal Pending"
			}
			
			set objReceipt = $get(^INREC(YM,idReceipt,1))
			set idRecLine = ""
			for  {
				set idRecLine = $order(^INRECLines(YM,4,idxOrder,idxOrderLine,idReceipt,idRecLine))
				quit:idRecLine=""
				
				set objRecLine  = $get(^INRECLine(YM,idReceipt,idRecLine,1))
				
				set fltRecQty   = -$$$INRECLineQuantity(objRecLine)    ; Reversed quantity in Inventory Unit
				set fltTotalQty = fltTotalQty + fltRecQty
				set ^CacheTempDynamicTable(YUCI,YUSER,"dINRECSummary",$increment(idTableLine)) = idReceipt_", "_idRecLine_Y_strProcessed_Y_fltRecQty
			}
		}  ; SR16937 ^^^
		
	;	set ^CacheTempDynamicTable(YUCI,YUSER,"dINRECSummary",$increment(idTableLine)) = "Total"_Y_""_Y_fltTotalQty
		set ^CacheTempDynamicTable(YUCI,YUSER,"dINRECSummary",$increment(idTableLine)) = ""_Y_""_Y_"<hr>"
		set ^CacheTempDynamicTable(YUCI,YUSER,"dINRECSummary",$increment(idTableLine)) = "<strong>Total</strong>"_Y_""_Y_fltTotalQty
		set strStatus = $$BuildDynTableHeader(.lstHeaders,.lstColTypes)
		do DrawTable^WWW120DynTable(lstHeaders,"dINRECSummary",lstColTypes,pidParameters,"",pidEvent)		
	}	
 
	quit $$$OK
	
	
BuildDynTableHeader(&plstHeaders,&plstColTypes)
	;-------------------------------------------------------------------------------
	; Setup Table Headers
	; receipt Number - Item Number - Received Amount
	;         (1)               (2)                 (3)
	;     "Receipt, Line"         "Completed"       "Receive Qty"
	;                         
	; 
	; History:
	; 19-Mar-2010	PPP 	SR17235: Updated field header from 'Receive Qty' to 'Received Quantity'
	; 20-Mar-2009	DavidR 	SR16363: Created modified from INMOVTable
	;-------------------------------------------------------------------------------
	
	;---------------------------------------  1
	set plstHeaders  = $listbuild($$$Text(33012)_", "_$$$Text(179))       ; "Receipt" ", " "Line"
	set plstColTypes = $listbuild($listbuild("left","display"))
	
	;---------------------------------------  3
	set plstHeaders  = plstHeaders_$listbuild($$$Text(32494))             ; "Completed"
	set plstColTypes = plstColTypes_$listbuild($listbuild("left","display"))
	
	;---------------------------------------  4     
	set plstHeaders  = plstHeaders_$listbuild($$$Text(34153))             ; "Received Quantity"	//SR17235
	set plstColTypes = plstColTypes_$listbuild($listbuild("right","display"))
	
	quit $$$OK
	
 
]]></Routine>
</Export>