<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRECB" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRECB
#include COMSYS
#include INConst
#include FINConst
	
OnBeforeSave(pidRec,&pobjRec)
	;-------------------------------------------------------------------------------
	; Save validation
	;
    ; Called by:
    ; 	Form    INRECB (+?)
	; 
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 
	; 28-Feb-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	new strStatus,idOrder,idSupplier,objOrder
	
	set strStatus = $$$OK
	
	set idOrder = $$$INRECSupplierOrder(pobjRec)
	if idOrder'="" {
		set idSupplier = $$$INRECSupplier(pobjRec)
		set objOrder = $get(^INAUF(YM,idOrder,1))
		
		if idSupplier '= $$$INAUFSupplierNumber(objOrder) {
			set strStatus = $listbuild("IN00671",idOrder,idSupplier)  ;Order %1 is not for supplier %2
		} else {
			set $$$INRECLocation(pobjRec) 	= $$$INAUFLocation(objOrder)
			set $$$INRECApproval1(pobjRec) 	= $$$INAUFApproval1(objOrder)
			set $$$INRECApproval2(pobjRec) 	= $$$INAUFApproval2(objOrder)
			set $$$INRECApproval3(pobjRec) 	= $$$INAUFApproval3(objOrder)
		}
	}
	
	if $$$ISOK(strStatus) {
		set strStatus = $$BeforeSave^INREC(pidRec,.pobjRec)
	}
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		$$$Error(strStatus)
	}
	
	quit
	
	
CacheLines(pidOrder)
	;-------------------------------------------------------------------------------
	; Cache the order lines that still need to be receipted.
	; Also store the current status of the order.
	;
	; Params:
	;
	; Returns:	^CacheTempRECLine(ns,usr,order) = status
	; 			^CacheTempRECLine(ns,usr,order,item) = cache id
	;
	; History:
	; 28-Feb-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	new blnReceived,blnToReceive,idInvMatch,idxOrder,idxOrderLine,objCache,strOrderStatus
	
	if pidOrder'="" {
		set blnReceived  = $$$NO    // Have we received something?
		set blnToReceive = $$$NO    // Is there something left to receive?
		
		kill ^CacheTempRECLine(YUCI,YUSER,pidOrder)
		
		set idxOrder     = $$$Index(pidOrder)
		set idxOrderLine = ""
		for {
			set idxOrderLine = $order(^FINAPInvMatchCaches(YM,1,idxOrder,idxOrderLine))
			quit:idxOrderLine=""
			
			set idInvMatch = ""
			for {
				set idInvMatch = $order(^FINAPInvMatchCaches(YM,1,idxOrder,idxOrderLine,idInvMatch))
				quit:idInvMatch=""
				
				set objCache = $get(^FINAPInvMatchCache(YM,idInvMatch,1))
				
				if $$$FINAPInvMatchCacheReceipted(objCache) {
					set blnReceived = $$$YES
				} else {
					set ^CacheTempRECLine(YUCI,YUSER,pidOrder,$$$FINAPInvMatchCacheItem(objCache)) = idInvMatch
					set blnToReceive = $$$YES
				}
			}
		}
		set strOrderStatus = $select('blnReceived  : "IN00674",       ; "Not received"
									  blnToReceive : "IN00675",       ; "Partially received"
									  1            : "IN00676")       ; "Fully received"
		
		set ^CacheTempRECLine(YUCI,YUSER,pidOrder) = $$^WWWTEXT(strOrderStatus)
	}
	quit
	
	
OnAfterButtonLine(pobjRec)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 05-Mar-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	do CacheLines($$$INRECSupplierOrder(pobjRec))
	
	quit
	
	
OnBlurPercent(pobjRec,pintFldPercent,pintFldAmount)
	;-------------------------------------------------------------------------------
	; Update amount if percent not blank
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 06-Mar-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	new curAmount,curTotal,fltPercent
	
	set fltPercent = $piece(pobjRec,Y,pintFldPercent)
	
	if fltPercent'="" {
		if fltPercent < 0 {		                 // Can't be less than 0%
			$$$Append(%TXT(1),"#YINRECBD"_pintFldPercent_Y)
		
		} else {
			set curTotal = $$$INRECAmount(pobjRec)
			
			if fltPercent > 100 {		         // Can't be more than 100%
				$$$Append(%TXT(1),"#YINRECBD"_pintFldPercent_Y_100)
				set curAmount = curTotal
			
			} else {
				set curAmount = $$$FCMult(curTotal,fltPercent/100)
			}
			do PopulateDataField^COMUtils("INRECB",pintFldAmount,curAmount,,pobjRec,$$$NO)
		}
	}
	quit
	
	
OnBlurAmount(pobjRec,pcurOldAmount,pintFldAmount,pintFldPercent)
	;-------------------------------------------------------------------------------
	; Remove percent if amount has been changed manually
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 06-Mar-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	new curAmount
	
	set curAmount = $piece(pobjRec,Y,pintFldAmount)
	
	if curAmount'=pcurOldAmount {
		$$$Append(%TXT(1),"#YINRECBD"_pintFldPercent_Y)
	}
	quit
	
	
OnBlurTotal(pobjRec)
	;-------------------------------------------------------------------------------
	; Update amounts based on percents
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 07-Mar-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	do OnBlurPercent(pobjRec,$$$FldINRECPackagingPercent,$$$FldINRECPackagingCost)
	do OnBlurPercent(pobjRec,$$$FldINRECDiscountPercent,$$$FldINRECDiscountAmount)
	do OnBlurPercent(pobjRec,$$$FldINRECTaxPercent,$$$FldINRECTaxAmount)
	
	quit
	
	
GetOrderStatus(pobjRec)
	;-------------------------------------------------------------------------------
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 08-Mar-2007	JW		SR15459: Created
	;-------------------------------------------------------------------------------
	new idOrder,strStatus
	
	set strStatus = ""
	
	set idOrder = $$$INRECSupplierOrder(pobjRec)
	if idOrder'="" {
		set strStatus = $get(^CacheTempRECLine(YUCI,YUSER,idOrder))
	}
	
	quit strStatus
	
	
OnBeforeSaveHook(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Compare Grandpackage with all received package in an order
	; 			fltPackageCostNew = Actually Package
	; 			fltPackageCost    = All Package cost of all old receipts
	; 			fltTotPkgCost     = Total of fltPackageCost + fltPackageCostNew
	; 			fltGrandPackage   = The Package from INAUF (Order)
	;
	; Params: 	pYKEY		: The keys
	; 			pYFELD		: The object of data
	;
	; History:
	; 13-Nov-2007	shobby	Upgraded Form Filter to exclude all forms except INRECB
	; 31-Oct-2007	SCR		Added Form filter
	; 26-Oct-2007	GM		SRBR014608: Created 
	;-------------------------------------------------------------------------------
	new fltPackageCostNew,fltPackageCost,fltGrandPackage,fltTotPkgCost
	new idOrder,idReceipt,objINAUF,objINREC,strStatus
	
	$$$VAR
	
	set strStatus = $$$OK 
	quit:$get(YFORM)'="INRECB" strStatus       ; SCR Only do this for nonstandard forms
	
	set idOrder = $$$INRECSupplierOrder(pYFELD)
 
	set objINAUF = $get(^INAUF(YM,idOrder,1))
	set fltGrandPackage = $$$INAUFPackageAmount(objINAUF)
 	
	if idOrder '="" {
		set fltPackageCostNew = $$$INRECPackagingCost(pYFELD)
		if fltPackageCostNew ="" {
			set strStatus = $listbuild("IN00710")          ; "Fill out the Packaging Cost in Invoice Tab"
		
		} else {
			set fltTotPkgCost = 0
			set idReceipt = ""
			for {
				set idReceipt = $order(^INREC(YM,idReceipt))
				quit:idReceipt=""
				
				set objINREC = $get(^INREC(YM,idReceipt,1))
				if $$$INRECSupplierOrder(objINREC) = idOrder {
				   	set fltPackageCost = $$$INRECPackagingCost(objINREC)	
					set fltTotPkgCost  = fltTotPkgCost + fltPackageCost
				}					
			}
 			set fltTotPkgCost = fltTotPkgCost + fltPackageCostNew ; add in fltTotPkgCost the actually Package cost
 
			if fltGrandPackage ="" {set fltGrandPackage = 0}
			if fltTotPkgCost > fltGrandPackage {
				set strStatus = $listbuild("IN00708",fltTotPkgCost,fltGrandPackage)
			}      ; "The total package receipt of ´%1´ is greater than GrandPackage of ´%2´ in order" 
		}
	}
		
	quit strStatus
	
]]></Routine>
</Export>