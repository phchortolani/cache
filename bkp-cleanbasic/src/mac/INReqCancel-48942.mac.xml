<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INReqCancel" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INReqCancel
	
#include COMSYS
#include COMConst
#include INConst
#include %occInclude
	
#define FormFldINReqCancelAll 13
	
OnBeforeButtonLine(pYM,pYFORM,pintPage,pYKEY,&pYFELD)
	if '$$IsCancellable^INReq(pYKEY) {
		set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
	}
	if ($$$INReqStatus(pYFELD)=8) || ($$$INReqStatus(pYFELD)=9) {
		set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
	}
	quit
	
	
OnBeforeDelete(pYM,pYFORM,pstrKey,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 22-Apr-2010	shobby	SR17267: Created - call INReq equivalent method
	;-------------------------------------------------------------------------------
	do OnBeforeDelete^INReq(pYM,pYFORM,pstrKey,pYFELD,"INReqCancelLine")
	quit
	
	
OnAfterDataFields(pYM,pYFORM,pintPage,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	;			
	; Called By : Form INReqCancel - Execute After Data Fields
	;		
	; Inputs : 
	;
	; ByRef :
	;	YAUSWAHL	GridEdit Options
	; Returns :
	;
	; History :
	; 21-Jan-2009	HQN		Created
	;-------------------------------------------------------------------------------
	new strOldYFORM
	
	if (pintPage=1) && (pYKEY'=$$$NULLOREF) {
		kill ^CacheTempLocked(YUSER)
		do ClearOld^WWWMultiLock(YUSER)
		set strOldYFORM = YFORM
		set YFORM = "INReqLineCancel"
		set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
		set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 400
		set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
		set $$$COMGridEditParameterContainer(YAUSWAHL)     = "INReqCancel"
		set $$$COMGridEditParameterEnabled(YAUSWAHL)       = $$$YES
		if '$$IsCancellable^INReq(pYKEY) {
			set $$$COMGridEditParameterEnabled(YAUSWAHL)   = $$$NO
		}
		do Start^COMGridEdit31(YFORM,pYKEY)
		set YFORM = strOldYFORM
	}
	quit
	
	
OnBeforeSave(pYM,pYFORM,pstrKey,pYFELD)
	;-------------------------------------------------------------------------------
	; Validate that either the INReq has a reason or at least one line is marked as
	; cancelled and has a corresponding reason
	; 
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 13-Aug-2009	GRF		SR16544: get idCell columns once
	; 15-Jun-2009	DWR		SR16544: general tidy of code, removed instances of
	; 							DEV00000 and create language text equivelant
	; 21-Jan-2009	HQN		Created
	;-------------------------------------------------------------------------------
	new blnReasonFound,intColCanc,intColReason,loopLines,strStatus
	
	set strStatus = $$$OK
	set Q         = $$$QSave
	
	if $$$INReqReasonCancellation(pYFELD)="" {
		set intColCanc   = $$$GetFormField("INReqLineCancel",$$$FldINReqLineCancelled)
		set intColReason = $$$GetFormField("INReqLineCancel",$$$FldINReqLineReasonCancellation)
		
		; find reasons on lines
		set blnReasonFound = $$$NO
		for loopLines=1:1:$get(^WWWDATEN(0,+$horolog,YUSER,"INReqLineCancel","V","ROWCOUNT")) {
			if ^WWWDATEN(0,+$horolog,YUSER,"INReqLineCancel","V","Y"_loopLines_"_"_intColCanc,1)=$$$YES {
				if ^WWWDATEN(0,+$horolog,YUSER,"INReqLineCancel","V","Y"_loopLines_"_"_intColReason,1)'="" {
					set blnReasonFound = $$$YES
					quit     ; for break
				}
			}
		}
		if 'blnReasonFound {
			set strStatus = $$$MakeStatus("IN01056")            ; "Cannot process without a reason"  SR16544
			set Q = $$$QDontSave
		}
	}
 	if Q=$$$QSave { //SR15946
	 ;	set strStatus = $$ExecuteRule(pYFORM,pYM,pstrKey,"Save") 
	 	if $$$ISOK(strStatus) {
		 	set strStatus = $$$GRIDSave(pstrKey)
	 	}
 	}
 	if $$$ISERR(strStatus) {
	 	do ReturnError^COMUtilError(strStatus)
	 	set Q = $$$QDontSave
 	}
	quit
	
	
OnAfterSave(pYM,pYFORM,pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Save of reasons ok, attempt to manually-close
	; 
	; Called By :
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 21-Apr-2010	GRF		-: Logic reversal for clarity
	; 13-Aug-2009	GRF		SR16544: clarify 8/9 logic
	; 15-Jun-2009	DWR		SR16544: general tidy of code, removed instances of
	; 							DEV00000 and create language text equivelant
	; 21-Jan-2009	HQN		Created
	;-------------------------------------------------------------------------------
	new blnAllClosed,idReqLine,loopLines,objReqLine,objUReq,objUReqLine,sc,strStatus
	
	set strStatus = $$$OK
	set sc        = $$$OK
	
	; Only manually-close header if header flagged, otherwise close marked lines
	if $$$INReqReasonCancellation(pYFELD) '= "" {
		set objUReq = ##class(alREQ.dUReq).%OpenId("0||"_pYKEY,,.sc)
		if objUReq = $$$NULLOREF {
			set strStatus = $$$MakeStatus("IN01045", pYKEY)   ;"Unable to load Req: %1"
		
		} else {
			set objUReq.Cancelled = $$$YES
			for loopLines=1:1:objUReq.ReqLines.Count() {
				set objUReqLine = objUReq.ReqLines.GetAt(loopLines)
				if (objUReqLine.Status.StatusCode'=8) &&
				   (objUReqLine.Status.StatusCode'=9)    {
					
					set objUReqLine.Cancelled          = $$$YES
					set objUReqLine.ReasonCancellation = $$$INReqReasonCancellation(pYFELD)
					set objUReqLine.DateCancellation   = +$horolog
				}
			}
			set sc = objUReq.ManuallyClose()
		}
		
	} else {
		set blnAllClosed = $$$YES
		set idReqLine = ""
		for {
			set idReqLine = $order(^INReqLine(0,pYKEY,idReqLine))
			quit:idReqLine=""
			
			; vvvv HEVA-1142 Needed to cancel more than 1 line of Requisition at the same time. 
			set objUReqLine = ##class(alREQ.dUReqLine).%OpenId("0||"_pYKEY_"||"_idReqLine,,.sc)
			if (objUReqLine '= $$$NULLOREF) {
				if (('objUReqLine.IsClosed()) && (objUReqLine.Cancelled)) {
					set objUReqLine.DateCancellation = +$horolog
					
					set sc  = objUReqLine.ManuallyClose()
					set sc  = objUReqLine.%Save()
				}
			}
			; ^^^^ HEVA-1142
			
			set objReqLine = $get(^INReqLine(0,pYKEY,idReqLine,1))
			if ($$$INReqLineStatus(objReqLine)'=7) && ($$$INReqLineStatus(objReqLine)'=8) {
				set blnAllClosed = $$$NO
			}
		}
		if blnAllClosed {   ; Auto-Close header
			set objUReq = ##class(alREQ.dUReq).%OpenId("0||"_pYKEY,,.sc)
			if objUReq = $$$NULLOREF {
				set strStatus = $$$MakeStatus("IN01045", pYKEY) ; "Unable to load Req: %1"
			} else {
				set sc = objUReq.AutoClose()
			}
		}
	}
	if $$$ISERR(sc) {
		set strStatus = $$ISStatusToDLStatus^COMUtilError(sc)
	}
	$$$YQHandler(strStatus)
	quit
	

SetCancel(pYKEY)
	do SetCancelVal(pYKEY,$$$INReqCloseAllLines(YFELD))
	quit
	
	
SetCancelVal(pYKEY,blnSet)
	;-------------------------------------------------------------------------------
	; sets the lines on a the grid to show all the remaining values for all lines 
	; if cancel whole requisition
	;	
	; Called by: OnBlur Manual field 13 (checkbox) INReqCancel
	; 
	; Inputs: YKEY, blnSet (YES or NO)
	;
	; Returns:
	;
	; History:
	; 07-Sep-2011	GRF		SR17823: Already have Qty in Inventory Units
	; 25-May-2011	GRF		SR17250: call common function for decimal places
	; 31-Mar-2011	GRF		SR17579: QtyOrdered is in Order Units; QtyReceived and
	;							QtyCancelled are in Invent Units; adjust dec places
	; 10-Apr-2010	GRF		SR17177: GRID Macros
	; 24-Nov-2009	SHOBBY	SR17038: Created
	;-------------------------------------------------------------------------------
	new arrGrid,fltCancelled,id,idForm,idInventUnit,idItem
	new intDecPlaces,intFormFld,objReqLine
	
	$$$GRIDGetContents(.arrGrid)
	
	set idForm = $get(^CacheTemp(YUSER,"Grid","Name"))  ; SR17250
	
	set id = ""
	for {
		set id = $order(arrGrid(pYKEY,id))
		quit:id=""
		
		set objReqLine = arrGrid(pYKEY,id)
		if blnSet {
			set idItem  = $$$INReqLineItem(objReqLine)
			if idItem'="" {
				set idInventUnit = $$$INARTUnitofMeasure($get(^INART(0,idItem,1)))
			} else {
				set idInventUnit = ""
			}
	;		set fltCancelled = ($$$INReqLineQtyOrdered(objReqLine)*fltConv) - $$$INReqLineQtyReceived(objReqLine) ; SR17823
			set fltCancelled = $$$INReqLineQtyRequired(objReqLine) - $$$INReqLineQtyReceived(objReqLine)
			set intFormFld   = $$$GetFormField(idForm,$$$FldINReqLineQtyCancelled)
			set intDecPlaces = $$GetDecimalPlaces^COMUtilNum(idForm,intFormFld,idInventUnit,objReqLine,"") ; SR17250
			if intDecPlaces>0 set fltCancelled = +$justify(fltCancelled,0,intDecPlaces)
			set $$$INReqLineQtyCancelled(objReqLine) = fltCancelled
		
		} else {
			set $$$INReqLineQtyCancelled(objReqLine) = 0
		}
		set arrGrid(pYKEY,id) = objReqLine
	}
	$$$GRIDSetContents(.arrGrid)
	quit
	
	/*
	new arrGrid,fltCancelled,fltConv,id,idForm,idInventUnit,idItem
	new intDecPlaces,intFormFld,objReqLine
	
	$$$GRIDGetContents(.arrGrid)
	
	set idForm = $get(^CacheTemp(YUSER,"Grid","Name"))  ; SR17250
	
	set id = ""
	for {
		set id = $order(arrGrid(pYKEY,id))
		quit:id=""
		
		set objReqLine = arrGrid(pYKEY,id)
		set $$$INReqLineCancelled(objReqLine) = blnSet
		if blnSet {
	; SR17579 vvv
	;		set $$$INReqLineQtyCancelled(objReqLine) = $$$INReqLineQtyOrdered(objReqLine) - $$$INReqLineQtyReceived(objReqLine)
			
			set idItem  = $$$INReqLineItem(objReqLine)
			set fltConv = $$GetUOMConversion^INUOMConversion(idItem,$$$INReqLineUnit(objReqLine),.idInventUnit)
			set fltCancelled = ($$$INReqLineQtyOrdered(objReqLine)*fltConv) - $$$INReqLineQtyReceived(objReqLine)
			
	;		set intDecPlaces = $$GetDecimalsForItemUnit^COMUnit(idItem,idInventUnit)                       ; SR17250 vvv
			set intFormFld   = $$$GetFormField(idForm,$$$FldINReqLineQtyCancelled)
			set intDecPlaces = $$GetDecimalPlaces^COMUtilNum(idForm,intFormFld,idInventUnit,objReqLine,"") ; SR17250 ^^^
			if intDecPlaces>0 set fltCancelled = +$justify(fltCancelled,0,intDecPlaces)
			set $$$INReqLineQtyCancelled(objReqLine) = fltCancelled
	; SR17579 ^^^
		
		} else {
			set $$$INReqLineQtyCancelled(objReqLine) = 0
		}
		set arrGrid(pYKEY,id) = objReqLine
	}
	$$$GRIDSetContents(.arrGrid)
	quit
	*/
	
]]></Routine>
</Export>