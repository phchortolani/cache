<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINAUFPCLOSE" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINAUFPCLOSE
	#include COMSYS
	#include INConst
	#include COMConst
	quit
	
GoToEncerrar(pYKEY)
	do GoToForm^COMUtilForm("VARCompraEncerrar", pYKEY)
	quit

OnBeforeButtonLine(pYKEY,&pobjForm)
	quit:$$$NoKey(pYKEY)
	
	if ($$QtdeItensNaoEncerrados(pYKEY) = 0){
		set $$$WWW120AuthorizationToModifyData(pobjForm) = $$$EnumReadOnly
	}
	quit
	
QtdeItensNaoEncerrados(pYKEY)	
	new qtNaoEncerrado
	set qtNaoEncerrado = 0
	
	&SQL(
	 SELECT count(*)
	 INTO qtNaoEncerrado
	 FROM SQLUser.INAUFP
	 WHERE
	 	INAUFP.OrderNo = :pYKEY
	 	AND INAUFP.LineItemManualyClosed is null
	)
	if 'SQLCODE {
		quit qtNaoEncerrado
	}
	quit qtNaoEncerrado
	
OnBeforeSave(pYKEY)
	;-------------------------------------------------------------------------------
    ; Verifica se o usuário preencheu dados, e encerra os itens da ordem de compra.
    ; Verifica também se algum dos itens que o usuário selecionou tem um recebimento
    ; EM ANDAMENTO para ele. Se for o caso, não deve permitir encerrar até que o 
    ; recebimento seja removido.
    ;
    ; History:
    ; 28-Dez-2012	Gustavo Fiuza	Updated
    ; 07-Nov-2012	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new strStatus, strMotivo
	set strStatus = $$$OK
	
	quit:(pYKEY = "")
	
	if '$data(^VARTempCompraEncerrar(YM,YBED,$$$KEY1(pYKEY))) {
		set strStatus = "Nenhuma linha da ordem de compra foi selecionada para ser encerrada. Favor verificar."		
	}

	set strMotivo = $get(^VARTempCompraEncerrar(YM,YBED,"MOTIVO"))	
	if (strMotivo = "") {
		set strStatus = "Por favor preencha o motivo."		
	}
	
	if $$ExisteRecebimentoEmEdicao(YBED,pYKEY) = $$$YES {
		set strStatus = "Não é possível encerrar pois um ou mais itens selecionados possuem recebimentos em andamento para eles. Consulte a tabela 'Recebimentos em andamento para esta compra' na parte inferior da tela para verificar quais são estes recebimentos."
	}
	
	if $$$ISOK(strStatus) {
		//set strStatus = $$Transaction^COMTransaction("EncerraItensMarcados^VARINAUFPCLOSE("""_pYKEY_""","""_strMotivo_""")",$$$YES)
	}
	
	if $$$ISERR(strStatus) {
		set Q = $$$QDontSave
		do ReturnError^COMUtilError(strStatus)
	}

	quit
	
OnBlurMotivo(strMotivo)
	set ^VARTempCompraEncerrar(YM,YBED,"MOTIVO") = strMotivo
	quit
	
OnAfterDataFields(pYKEY)
	quit:($$$NoKey(pYKEY))

	new lstHeader, idCompraLinha, objCompraLinha, idItem, qtyCompra, qtyTotalRecebida, qtyTotalEstornada,
		qtyRecebida, qtyPendente, UoM, blnEncerrado, blnReadonly, chave, encerradoEm, encerradoPor, motivoEncerr
	
	set lstHeader = ""
	set lstHeader = lstHeader_$listbuild("#","Código","Produto","Quant.&nbsp;<br />&nbsp;&nbsp;Compra","Quant.&nbsp;<br />&nbsp;&nbsp;Recebida","Quant.&nbsp;<br />&nbsp;&nbsp;Pendente","UN","Encerrar","Encerrado Em","Encerrado Por","Motivo")
	
	if $$Start^COMTable(lstHeader,,,,$$$NO) {
		
		$$$Order3(^INAUFP,YM,pYKEY,idCompraLinha)
			
			set objCompraLinha = $get(^INAUFP(YM,pYKEY,idCompraLinha,1))
			
			set idItem    = $$$INAUFPItemNumber(objCompraLinha)
			set UoM 	  = $$$INAUFPQuantityUnit(objCompraLinha)
			set qtyCompra = $$$INAUFPQuantity(objCompraLinha)

			set qtyTotalRecebida  = $$getQuantidadeRecebida^VARCompra(pYKEY,idCompraLinha)
			set qtyTotalEstornada = $$getQuantidadeEstornada^VARCompra(pYKEY,idCompraLinha) 
			
			set qtyRecebida = qtyTotalRecebida - qtyTotalEstornada
			set qtyPendente = qtyCompra - qtyRecebida			
			set:(qtyPendente < 0) qtyPendente = 0 ;Não mostrar valores negativos
						
			set blnEncerrado = $$$INAUFPLineItemManualyClosed(objCompraLinha)
			set encerradoEm  = $$$INAUFPManualCloseDate(objCompraLinha)
			set encerradoPor = $$$INAUFPEmployee(objCompraLinha)
			set motivoEncerr = $$$INAUFPReasonForClosing(objCompraLinha)

			if (encerradoEm '= "") {
				set encerradoEm = $zdate(encerradoEm,4)_" "_$ztime($piece(encerradoEm,",",2))
			}

			set blnReadOnly = $$$NO
			set blnChecked  = $$$NO
			
			if ( (qtyPendente = 0) || (blnEncerrado = $$$YES) ) { 
				set blnReadOnly = $$$YES	
			}

			set chave = pYKEY_$$$COMMA_idCompraLinha
			
			do NewLine^COMTable()
			do InsertCell^COMTable(idCompraLinha)
			do InsertCell^COMTable(idItem)
			do InsertCell^COMTable($$SQLGetDescricaoProduto^VARSQL(idItem),,,,,,,,,$$$YES)
			do InsertCell^COMTable($$^WWWTR(0,12,qtyCompra),,,,"right")
			do InsertCell^COMTable($$^WWWTR(0,12,qtyRecebida),,,,"right")
			do InsertCell^COMTable($$^WWWTR(0,12,qtyPendente),,,,"right")
			do InsertCell^COMTable($$SQLGetDescCompletaUnit^VARSQL(UoM))
			
			if (blnEncerrado = $$$YES) {				
				do InsertCell^COMTable("<font color='red'>Encerrado</font>")
			} elseif (qtyPendente = 0) {
				do InsertCell^COMTable("<font color='blue'>Recebido</font>")
			} else {
				do InsertCellCheckbox^COMTable(chave,"MarcarItem^VARINAUFPCLOSE",$$$NO,blnReadOnly)
			}
			
			do InsertCell^COMTable(encerradoEm)
			do InsertCell^COMTable(encerradoPor)
			do InsertCell^COMTable(motivoEncerr,,,,,,,,,$$$YES)			
			do EndLine^COMTable()
	
		$$$End
		
		do Stop^COMTable()
	}
	
	;Exibe os recebimentos em aberto para a Ordem de Compra
	do PrintTableRecebimentosAbertos^VARCompra(pYKEY)
	
	quit
 	
MarcarItem(pYKEY="",pChecked="")
	;-------------------------------------------------------------------------------
    ; Salva a marcação ou desmarcação do usuário no checkbox com do item selecionado.
    ; 
    ; History:
    ; 07-Nov-2012	Gustavo Fiuza		Created
    ;-------------------------------------------------------------------------------
	quit:(pYKEY = "")
	
	if ($piece(pChecked,",",1) = "true") {		
		set ^VARTempCompraEncerrar(YM,YBED,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1) = ""
	
	} elseif ($piece(pChecked,",",1) = "false") {
		kill ^VARTempCompraEncerrar(YM,YBED,$$$KEY1(pYKEY),$$$KEY2(pYKEY))
	}
	
	quit $$$YES
	
	
EncerraItensMarcados(pidCompra,pstrMotivo)
	quit:(pidCompra = "")
	
	new strStatus, idCompraLinha, objINAUFP, key
	
	set strStatus = $$$OK
	/*
	set ultimoReg = ""
	
	$$$Order4(^VARTempCompraEncerrar,YM,YBED,pidCompra,idCompraLinha)
		
		set objINAUFP = $get(^INAUFP(YM,pidCompra,idCompraLinha,1))
		quit:(objINAUFP = "")

		set $$$INAUFPLineItemManualyClosed(objINAUFP) = $$$YES
		set $$$INAUFPEmployee(objINAUFP)			  = $get(YBED)
		set $$$INAUFPManualCloseDate(objINAUFP)		  = $horolog
		set $$$INAUFPReasonForClosing(objINAUFP)	  = pstrMotivo
		set $$$INAUFPPosCompleted(objINAUFP)		  = $$$YES
		set $$$INAUFPFREE10(objINAUFP)				  = 3
		
		set key = pidCompra_$$$COMMA_idCompraLinha
		
		set strStatus = $$$Save("INAUFP",key,objINAUFP,$$$YES)
	$$$End
	*/
	quit strStatus
	
	
ExisteRecebimentoEmEdicao(YBED,pidCompra)
	;-------------------------------------------------------------------------------
    ; Verifica se o recebimento foi processado ou não. O fato de o recebimento estar
    ; confirmado não deve interferir nesta rotina. O requisito é não permitir que o 
    ; usuário encerre um item que está em processo de recebimento, mesmo que ainda não
    ; esteja confirmado.
    ; 
    ; History:
    ; 02-Jan-2013	Gustavo Fiuza	Created
    ;-------------------------------------------------------------------------------
	new idCompraLinha, idRecebimento, objRecebimento, blnExisteRecebimento
	
	set blnExisteRecebimento = $$$NO
	
	;Linhas que o usuário marcou
	$$$Order4(^VARTempCompraEncerrar,YM,YBED,pidCompra,idCompraLinha)			
		quit:(blnExisteRecebimento = $$$YES)
		
		;Verificando se existe algum recebimento associado a esta compra/linha
		if $data(^INRECSummarys(YM,1,$$$Index(pidCompra),idCompraLinha)) {			
			
			;Caso exista, verifica um a um se algum deles ainda não foi processado
			$$$Order5(^INRECSummarys,YM,1,$$$Index(pidCompra),idCompraLinha,idRecebimento)			
				
				set objRecebimento = $get(^INREC(YM,idRecebimento,1))				
				
				if +$$$INRECStatus(objRecebimento) < 1 {
					set blnExisteRecebimento = $$$YES
					quit
				}
			
			$$$End
		}
	
	$$$End
	
	quit blnExisteRecebimento
]]></Routine>
</Export>