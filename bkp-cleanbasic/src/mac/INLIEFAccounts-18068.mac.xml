<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INLIEFAccounts" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INLIEFAccounts
	
#include COMConst
#include INConst
#include WWWConst
#include COMSYS
	
OnAfterDataFields(pYKEY,pYFELD,pYSEITE)
	;-------------------------------------------------------------------------------
	; Used on INLIEF, if tab is number 3, loads the grid
	;
	; Returns:
	;
	; History:
	; 14-Nov-2006	RGB		Created (SR BR014300)
	;-------------------------------------------------------------------------------
	if YSEITE=3 do LoadGrid(pYKEY)
 	quit
 	
 
LoadGrid(pid="")
	;-------------------------------------------------------------------------------
	; Display the Line Items associated with this Receive.
	;
	; Returns:
	;
	; History:
	; 05-Dec-2008	GRF		Clarify Enabled test
	; 14-Nov-2006	RGB		Created (SR BR014300)
	;-------------------------------------------------------------------------------
	new objInvoice,YFORM,YKEY,YAUSWAHL,YFELD,strData
	
	set YFORM="INLIEFAccountsLine"
	set YFELD=""
	set YKEY=$get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL)    = $$$YES
	set $$$COMGridEditParameterMaximumHeight(YAUSWAHL) = 290
	set $$$COMGridEditParameterGridName(YAUSWAHL)      = YFORM
	set $$$COMGridEditParameterEnabled(YAUSWAHL)       = ($$$WWW120AuthorizationToModifyData(YVOR)'=$$$EnumReadOnly)
	set $$$COMGridEditParameterContainer(YAUSWAHL)     = "INLIEF"
	
	do ^COMGridEdit31
	
	quit
 
 
OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Saves the grid
	;
	; Returns:
	;
	; History:
	; 14-Nov-2006	RGB		Created (SR BR014300)
	;-------------------------------------------------------------------------------
	new strStatus
	set strStatus=$$$GRIDSave(pYKEY)
	if $$$ISOK(strStatus) {
	} else {
		set Q=$$$QDontSave
	}
	quit
	
OnAfterDelete(pYKEY)
	;-------------------------------------------------------------------------------
	; Calls the com grid interface for deleting a line
	;
	; Returns:
	;
	; History:
	; 14-Nov-2006	RGB		Created (SR BR014300)
	;-------------------------------------------------------------------------------
	do DELETE^COMGridEdit31Interface()
	quit
	
OnAfterSave(&pobjSupplier)
	;-------------------------------------------------------------------------------
	; Will set on the INLIEF class the default account chosen on the Accounts grid.
	;
	; Params:	
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 14-Mar-2007	JW		BR014300: Fixed undefined error. Cleaned up.
	; 14-Nov-2006	RGB		Created (SR BR014300)
	;-------------------------------------------------------------------------------
	new idSupplier,intDefaultAccount,idBankLine,strStatus,objINLIEF,objINLIEFAccountsLine
	
	if YKEY'="" {
		set idSupplier        = YKEY
		set intDefaultAccount = 0
		set idBankLine        = ""
		
		while intDefaultAccount=0 {
			set idBankLine = $order(^INLIEFAccountsLine(YM,idSupplier,idBankLine))
			if idBankLine'="" {
				set objINLIEFAccountsLine = $get(^INLIEFAccountsLine(YM,idSupplier,idBankLine,1))
				set intDefaultAccount     = $$$INLIEFAccountsLineDefaultAccount(objINLIEFAccountsLine)
				if intDefaultAccount="" set intDefaultAccount = 0
			}
			if idBankLine="" set intDefaultAccount = 2
		}
		if intDefaultAccount=1 {
			set objINLIEFAccountsLine = $get(^INLIEFAccountsLine(YM,idSupplier,idBankLine,1))
			set $$$INLIEFBankAccountNumber1(pobjSupplier) = $$$INLIEFAccountsLineBankAccountNo(objINLIEFAccountsLine)
			set $$$INLIEFBankCode1(pobjSupplier)          = $$$INLIEFAccountsLineBankCode(objINLIEFAccountsLine)
			set $$$INLIEFSwiftCode1(pobjSupplier)         = $$$INLIEFAccountsLineSwiftCode(objINLIEFAccountsLine)
			set $$$INLIEFBankName1(pobjSupplier)          = $$$INLIEFAccountsLineBankName(objINLIEFAccountsLine)
			set $$$INLIEFCurrentAccount(pobjSupplier)     = 0
		}
	}
	quit
	
]]></Routine>
</Export>