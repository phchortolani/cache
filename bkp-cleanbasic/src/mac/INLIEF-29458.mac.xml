<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INLIEF" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INLIEF
	
#include COMSYS
#include WWWConst
#include INSupStatus
#include INConst
#include COMConst
#include COMSYSDate
	
#define SecondsInDay 86400
#define MonthsInYear 12 
	
OnBlur(YLFN,YFELD,YKEY)
	;-------------------------------------------------------------------------------
	; Whether supplier can be used at this time.
	;
	; Params:	YLFN	:Field Number
	;			YFELD	:Data record
	;			YKE		:Id of the record (not currently used)
	;
	; Returns:	status
	;
	; History:
	; 09-Sep-2007	shobby	SRBR014396: Created
	;-------------------------------------------------------------------------------
	if (YLFN=$$$FldINLIEFName1) {
		if $$$INLIEFShortName(YFELD)="" {
			SET %TXT(1) = $get(%TXT(1))_"#Y"_YFORM_"D"_$$$FldINLIEFShortName_"~"_$$$INLIEFName1(YFELD)    ;Date 
		}
	}
	quit
	
	
IsUsable(pidSupplier,pidForm)
	;-------------------------------------------------------------------------------
	; Whether supplier can be used at this time.
	;
	; Params:	pidSupplier
	;
	; Returns:	status
	;
	; History:
	; 03-May-2013	SCR		CORE-97: Add Validation to VAR code
	; 22-Oct-2010	shobby	SR17540: Ignore invalid supplier id.
	; 23-Feb-2007	JW		SR15453: Inactive Date -> Status
	; 15-Aug-2005	JW		SR12290: Created (copied from FINAPSupplier)
	;-------------------------------------------------------------------------------
	new strStatus,idStatus,intField,strPre,objINLIEF
	
	set strStatus = $$$OK
	
	;if ($get(pidSupplier)'="") && ($extract(pidForm,1,2) = "IN") {
	if ($get(pidSupplier)'="") {  ; HEVA-97
		set objINLIEF = $get(^INLIEF(YM,pidSupplier,1))               ;SR17540
		if objINLIEF'="" {
			set idStatus = $$$INLIEFStatus(objINLIEF)
			if idStatus="" {
				set strStatus = $$$AppError("P0060000014")		// "Mandatory field should not be blank."
		
			} else {
				set strPre = $extract(pidForm,3,5)
				; $$$FldINSupStatusReceiveGoods DEPRECATED - with removal of INWEAUF*/INWEFREI - check INRECSummary w/out ON/OL? SR16361
				set intField = $select(	strPre = "ART"					: $$$FldINSupStatusItemConditions,
									;	pidForm="INAUFNEU"				: $$$FldINSupStatusPurchaseOrders,
										strPre = "AUF"					: $$$FldINSupStatusPurchaseOrders,
										strPre = "Pur"					: $$$FldINSupStatusPurchaseOrders,
										strPre = "DRP"					: $$$FldINSupStatusDRPOrders,
										pidForm [ "Agree"				: $$$FldINSupStatusSourceReqs,
										strPre = "Req"					: $$$FldINSupStatusSourceReqs,
										pidForm = "INWEFREI"			: $$$FldINSupStatusReceiveGoods,
										$extract(pidForm,3,7)="ERECH"	: $$$FldINSupStatusInvoices,
										pidForm = "INReceipt"			: $$$FldINSupStatusReceiveGoods,
										pidForm = "VARCompra"			: $$$FldINSupStatusPurchaseOrders,
										pidForm = "VARAta"				: $$$FldINSupStatusSourceReqs,
                                        1 								:$$$FldINSupStatusOther) ; CORE-97
				set strStatus = $$$HasPermission(pidSupplier,idStatus,intField)
			}
		}
	}
	quit strStatus
	
	
CanReceive(pidSupplier)
	;-------------------------------------------------------------------------------
	; Can we receive goods for this supplier?
	;
	; Params:
	;
	; Returns:	status
	;
	; History:
	; 23-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idStatus
	
	set idStatus  = $$$INLIEFStatus($get(^INLIEF(YM,pidSupplier,1)))
	set strStatus = $$$HasPermission(pidSupplier,idStatus,$$$FldINSupStatusReceiveGoods)
	
	quit strStatus
 
 
CanDRPOrder(pidSupplier)
	;-------------------------------------------------------------------------------
	; Can DRP create an order for this supplier?
	;
	; Params:
	;
	; Returns:	boolean
	;
	; History:
	; 26-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idStatus
	
	set idStatus  = $$$INLIEFStatus($get(^INLIEF(YM,pidSupplier,1)))
	set strStatus = $$$HasPermission(pidSupplier,idStatus,$$$FldINSupStatusDRPOrders)
	
	quit $$$ISOK(strStatus)
	
 
CanSourceReqs(pidSupplier)
	;-------------------------------------------------------------------------------
	; Can we source reqs for this supplier?
	;
	; Params:
	;
	; Returns:	boolean
	;
	; History:
	; 22-Nov-2007	shobby	SRBR014807:Assume that a supplier with no status is inactive.
	; 27-Feb-2007	JW		SR15453: Created
	;-------------------------------------------------------------------------------
	new strStatus,idStatus
	
	set idStatus = $$$INLIEFStatus($get(^INLIEF(YM,pidSupplier,1)))
	if idStatus="" set idStatus = 0                                       ; Inactive
	set strStatus = $$$HasPermission(pidSupplier,idStatus,$$$FldINSupStatusSourceReqs)
	
	quit $$$ISOK(strStatus)
	
	
GetFCCode(pidSupplier="")
	;-------------------------------------------------------------------------------
	; Get the FC Code for the supplier
	; 
	; if the FC Code = the Base Currency, return ""
	;
	; Returns:
	;
	; History:
	; 31-May-2004	PO		SR13591: Created
	;-------------------------------------------------------------------------------
	new SupplierRec,SupplierCurrency,CompanyCurrency
	
	quit:pidSupplier="" ""
	
	set SupplierRec      = $get(^INLIEF(0,pidSupplier,1))
	set SupplierCurrency = $$$INLIEFCurrency(SupplierRec)
	set CompanyCurrency  = $$$WWW012StandardCurrency($get(^WWW012(0,0,1)))
	
	if SupplierCurrency = CompanyCurrency {
		set SupplierCurrency = ""
	}
	quit SupplierCurrency
	
	
ExchangeRateField(LIEF,YINHALT)
	;-------------------------------------------------------------------------------
	; Encapsulate exchange rate function
	;
	; Params: LIEF - supplier
	;		  YINHALT - value (by ref)
	;
	; Returns:
	;
	; History:
	; 27-Oct-2005	JW		SR13074: Created
	;-------------------------------------------------------------------------------
	new WHR
	
	SET WHR = $$$INLIEFCurrency($GET(^INLIEF(YM,LIEF,1)))
	
	IF (WHR'="") && (WHR'=YWHR) {
		IF YINHALT="" {
			SET YINHALT = $$^WWWTR(0,18,$$$WWWWAEUnitPrice($get(^WWWWAE(0,WHR,1))))
		}
	} else {
		SET YHID    = 2
		SET YINHALT = ""
	}
	quit
	
	
PurchaseHistory(pidSupplier="")
	;-------------------------------------------------------------------------------
	;	Show COMView with "PurchaseHistory" favourite to list items purchased from a
	;	supplier.
	; 
	; Called by : Purchase History button on form INLIEF "Supplier Master Data"
	;  the button in supplier master to show purchased line items
	;
	; History:
	; 17-Aug-2006	GRF		SR14856: Doco; boolean macro; explicit parameter specification
	; 09-Aug-2006	FIS		SR14856: Created
	;-------------------------------------------------------------------------------
	if pidSupplier'="" {
		do Initialise^COMViewCustom("INAUFP","",$$$NO,$$$YES)
		do AddFilter^COMViewCustom("D"_$$$FldINAUFPSupplier,"=",pidSupplier)  ;SUPPLIER
		do AddFilter^COMViewCustom("D"_$$$FldINAUFPSource,"=",1)              ;PURCHASE ONLY
		do Start^COMViewCustom()
	}
	quit $$$OK
	
	
OnBeforeSave(pidSupplier,&pobjSupplier)
	;------------------------------------------------------------------\
	; Description of Function :
	;		TESTEN NUMMERNKREIS
	;
	; Params:	pidSupplier		- INLIEF id
	;			pobjSupplier	- INLIEF obj
	;			
	; ByRef :	Q 
	; 
	; Returns :	Nothing
	;
	; History :
	; 15-Feb-2006	RGB		Fixed the issue regarding EFT Payment Supplier SR BR014300
	; 15-Nov-2006	RGB		Added the INLIEFAccounts OnBeforeSave call
	; 20-Sep-2006	JW		SR14940: Fixed dot level. Cleaned up.
	; 20-Sep-2006	JW		SR14612: Moved from INLIEFCHECK. Added Account check.
	; 24-Aug-2006	GRF		Doco; expand commands; FIXME
	; 12-Apr-2006	SC		SR14195/SR14196: Validate Agent type OnBeforeSave.
	; 07.01.03		ULM		Created.
	;------------------------------------------------------------------/	
	new strStatus,objINVORG,idFrom,idTo
	
	quit:$get(pidSupplier)=""
	
	do OnBeforeSave^INLIEFAccounts(YKEY,YFELD)
	do OnAfterSave^INLIEFAccounts(.pobjSupplier)
	
	// Check supplier range in General Parameters
	set objINVORG=$GET(^INVORG(YM,YM,1))
	if (objINVORG'="") {
		set idFrom = $$$INVORGRangeOfSupplierNosFrom(objINVORG)
		set idTo = $$$INVORGTo2(objINVORG)
		
		IF (idFrom'="") && (pidSupplier ']] idFrom) {
			do SendMsg(pidSupplier,pobjSupplier)
			
		} elseif (idTo'="") && (pidSupplier ]] idTo) {
			do SendMsg(pidSupplier,pobjSupplier)
		}
	}
	
	set strStatus = $$$OK
	
	if '$$$INLIEFBroker(pobjSupplier) && '$$$INLIEFSupplier(pobjSupplier) {
		set strStatus = $listbuild("IN00410")     ; "Must select at least one Agent type."
		
	} elseif $$$INLIEFMethodOfPayment(pobjSupplier) = $$$EnumPayByEFT {
		if ($$$INLIEFBankAccountNumber1(pobjSupplier)="") || ($$$INLIEFBankCode1(pobjSupplier)="") {
			set strStatus = $listbuild("IN00545")
		} ; "Method of Payment is EFT. Please enter Bank Account Number 1 and Bank Code 1 in the Account tab"
	}
	
	if $$$ISERR(strStatus) {
		set Q=$$$QDontSave
		$$$Error(strStatus)
	}
	quit	
	
	
SendMsg(pidSupplier,pobjSupplier)	private
	;-------------------------------------------------------------------------------
	; Send a msg regarding the supplier number being out of range.
	;
	; Params:	pidSupplier		- INLIEF id
	;			pobjSupplier	- INLIEF obj
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 20-Sep-2006	JW		SR14940: Created (encapsulated from OnBeforeSave)
	;-------------------------------------------------------------------------------
	new YFORM,YVOR // <-- Is this required? Is this for call to SPEI
	new objRecord,idRecord
	
	IF $get(YBED)'="" {
		set idRecord = $$^WWWNEXT("WWW013M")
		if idRecord '= "" {
			set objRecord = pidSupplier_" "_$$$INLIEFShortName(pobjSupplier)_"\n"_$$$Text(32198)  ;RICHTIGER NUMMERNKREIS ?
			do ^WWWSPEI("WWW013M",YBED_","_idRecord,objRecord)  ;SPEICHERN INFO-NACHRICHT ;Save 
		}
	}
	quit
	
	
OnAfterSave()
	;-------------------------------------------------------------------------------
	; Method Usage
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 22-Feb-2007	RGB		SRBR014300: Created
	;-------------------------------------------------------------------------------
	do ^INFIBKRE
	quit
	
	
OnBeforeSaveHook(pidSupplier, pobjSupplier)
	;-------------------------------------------------------------------------------
	; Check if CPF/CNPJ code is unique and blank
	; **** Note: This is optional code that is called based on an OnBeforeSave VAR hook
	;            being created on the INLIEF class.
	;            Currently it's use is only of interest to SES and Celesc but will apply
	;            to any new Brasilian clients.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns: strStatus
	;
	; History:
	; 22-Oct-2008	shobby	SRBR014968: Changed $lb to $$$MakeStatus
	; 26-Aug-2008	shobby	SRBR014968: initialised a value for strCompanyNumberClean and only test for CPF
	; 									if FREE16 is populated with 1.
	; 30-May-2007	HeberB	SRBR014329: (rewritten)Use symbols to save and search index
	; 15-May-2007	RPW		SRBR014329: Fixed logic, would fail if pidSupplier = the value for the index.
	;						Would still get the error message
	; 14-May-2007	GM		SRBR014329: Parameters removed 
	; 03-May-2007	shobby	SRBR014408: Renamed from OnBeforeSaveDataField
	; 26-Apr-2007	GM		SRBR014329: change prefix of language text
	; 23-Mar-2007	HeberB	SRBR014408:	remove symbols from strCompanyNumber before validating
	; 15-Mar-2007	shobby	SRBR014329:	shobby renamed tag, fixed some coding errors. Fixed reference to index 6
	; 17-Nov-2006	GM		SRBR014329: Created
	;-------------------------------------------------------------------------------
	if $get(Y)="" do ^WWWVAR
		
	new blnCompanyNumberIsUsed,idSupplier,idxCompanyNumber
	new strCompanyNumber,strCompanyNumberClean,strFiscal,strStatus
	
	quit:(pidSupplier="") $$$NO
	
	set strStatus             = $$$OK
	set strCompanyNumber      = $$$INLIEFCompanyNumber(pobjSupplier)
	set strCompanyNumberClean = ""
	set strFiscal = $$$INLIEFFREE16(pobjSupplier)
	
	if strCompanyNumber="" {
		if strFiscal=1 {
			set strStatus = $$$MakeStatus("Com00294")	     ; "Fill out the Person Number(CPF)"
		
		} elseif strFiscal=2 {
			set strStatus = $$$MakeStatus("Com00295")	     ; "Fill out the Company Number(CNPJ)"
		
		} else {
			/* Do not care */
		}
	} else {
		if strFiscal="" {
			set strStatus = $$$MakeStatus("Com00296")      ; "Fill out the "Natureza Fiscal""
		
		} else {
			; if number provided with no symbols add them to be saved and 
			; checked whether has already being used
			; 3N1"."3N1"."3N1"-"2N		2N1"."3N1"."3N1"/"4N1"-"2N
			if '$find(strCompanyNumber,".") {
				set strCompanyNumberClean = strCompanyNumber
				if (strFiscal=2) {	// CNPJ
					set strCompanyNumber =  $extract(strCompanyNumber,1,2)_"."_
											$extract(strCompanyNumber,3,5)_"."_
											$extract(strCompanyNumber,6,8)_"/"_
											$extract(strCompanyNumber,9,12)_"-"_
											$extract(strCompanyNumber,13,14)
				} else {			// CPF
					set strCompanyNumber =  $extract(strCompanyNumber,1,3)_"."_
											$extract(strCompanyNumber,4,6)_"."_
											$extract(strCompanyNumber,7,9)_"-"_
											$extract(strCompanyNumber,10,11)
				}
				set $$$INLIEFCompanyNumber(pobjSupplier) =  strCompanyNumber
			} else {
				set strCompanyNumberClean = $translate(strCompanyNumber,".-/")
			}
 
			set idxCompanyNumber = $$$Index(strCompanyNumber)
			if $data(^INLIEFs(YM,6,idxCompanyNumber)) {			
				set blnCompanyNumberIsUsed = $$$NO	
				$$$Order4(^INLIEFs,YM,6,idxCompanyNumber,idSupplier)
					if (idSupplier'=pidSupplier) {
						set blnCompanyNumberIsUsed = $$$YES	
						quit
					}
				$$$End
				if (blnCompanyNumberIsUsed) {
					set strStatus = $$$MakeStatus("Com00293")
				} ; "This Person Number(CPF) or Company Number(CNPJ) has already been used"
			}		
		}
	}
	
	if $$$ISOK(strStatus) {		
		if (strFiscal=2) {
			set strStatus = $$ValidateCNPJ^COMValidation(strCompanyNumberClean)
		
		} elseif (strFiscal=1) {
			set strStatus = $$ValidateCPF^COMValidation(strCompanyNumberClean)
		}
	}
	quit strStatus
	
	
OnAfterPrimaryKeyVAR(pidSupplier)
	;-------------------------------------------------------------------------------
	; Get values below and loads into CacheTemp to be used on form. Called from
	; 'Execute After Data Field' customization field of Primary Key. Used in conjunction
	; with OnBeforeDataFieldVAR
	; 
	; Data: (date of last purchase)
	; Valor: (value of last purchase)
	; Number of receipt (nota fiscal) of last purchase
	; No MÃªs: $  (Monthly total)
	; No Ano: $ (Year total)
	; 
	; Params: pidSupplier - Supplier Number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 26-Jun-2007	HeberB  SRBR014327(1): Change to comply with INRECB
	; 16-May-2007	RPW		SRBR014327: Removed string from CacheTempDetailsLastPurchase as its not needed
	; 15-May-2007	HeberB	SRBR014327: Changed CacheTemp to CacheTempDetailsLastPurchase
	;							Use of macros
	;							dteLast '= 1 -> indication a receipt was found
	; 14-May-2007	HeberB	SRBR014327: Created based on OnBeforeDataField
	;------------------------------------------------------------------------------- 
	quit:$$$NoKey(pidSupplier)
 
	new arrINREC,dteDeliveryNoteDate,dteLast,dteINRECMonth,dteINRECYear,fltYearValue
	new idINREC,objINREC,idINRECLast,objINRECLast,idINAUF,objINAUF
		
	kill ^CacheTempDetailsLastPurchase(YUSER,YUCI,"DetailsLastPurchase",pidSupplier)
	kill arrINREC
	
	set arrINREC    = ""
	set idINRECLast = ""
	set dteLast     = 1
	
	; loop to find the last INREC and to calculate the total of the month and year
	set idINREC = ""
	for {
		set idINREC = $order(^INREC(YM,idINREC))
		quit:idINREC=""
		
		set objINREC = $get(^INREC(YM,idINREC,1))
 
		continue:($$$INRECStatus(objINREC) '= $$$EnumINSTATUSProcessed)
		continue:($$$INRECDeliveryNoteDate(objINREC) = "")
		continue:(+$$$INRECDeliveryNoteDate(objINREC) < $$$DateFirstDayOfYear(dteLast))
		
		;SRBR014327(1)
		;set idINAUF = $$$INRECFREE10(objINREC)
		set idINAUF = $$$INRECSupplierOrder(objINREC)
		continue:(idINAUF = "")
 
		set objINAUF = $get(^INAUF(YM,idINAUF,1)) 
		
		continue:(pidSupplier '= $$$INAUFSupplierNumber(objINAUF))
		
		if (+$$$INRECDeliveryNoteDate(objINREC) >= +dteLast) {
			set dteLast = $$$INRECDeliveryNoteDate(objINREC) 
			set objINRECLast = objINREC	
			set idINRECLast  = idINREC
		}
		set dteINRECMonth = $$$DateMonth($$$INRECDeliveryNoteDate(objINREC))
		set dteINRECYear =  $$$DateYear($$$INRECDeliveryNoteDate(objINREC))
		
		set arrINREC(dteINRECYear,dteINRECMonth) = $get(arrINREC(dteINRECYear,dteINRECMonth),0) + $$$INRECAmount(objINREC)
	}
	
	if dteLast '= 1 {
		set fltYearValue  = 0	
		set dteINRECMonth = ""
		for {
			set dteINRECMonth = $order(arrINREC($$$DateYear(dteLast),dteINRECMonth))
			quit:dteINRECMonth=""
			
			set fltYearValue = fltYearValue + arrINREC($$$DateYear(dteLast),dteINRECMonth)
		}	
				
		set ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,"LastDate")    = $$^WWWDATE(dteLast)
		set ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,"LastInvoice") = +$$$INRECInvoiceNumber(objINRECLast)
		set ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,"LastAmount")  = $$^WWWZAHL(+$$$INRECAmount(objINRECLast))
		set ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,"MonthValue")  = $$^WWWZAHL(arrINREC($$$DateYear(dteLast),$$$DateMonth(dteLast)))
		set ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,"AnualValue")  = $$^WWWZAHL(fltYearValue)
 
	} else {           // FIXME: Is this even needed
		set ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier) = ""
	}
	quit
	
	
OnBeforeDataFieldVAR(pidSupplier,pstrType)
	;-------------------------------------------------------------------------------
	; Get loaded into CacheTemp to be used on form 
	; 
	; Params: pidSupplier - Supplier Number
	;		  pstrType  - Identifies data to be recovered
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 
	; 15-May-2007	HeberB	SRBR014327: Changed CacheTemp to CacheTempDetailsLastPurchase
	; 14-May-2007	HeberB	SRBR014327: Created based on OnBeforeDataField
	;------------------------------------------------------------------------------- 
	new strValue
	
	quit:$$$NoKey(pidSupplier) ""
	quit:pstrType="" ""
	
	quit:('$data(^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,pstrType))) ""
 
	set strValue = $get(^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,pstrType))
 
	kill ^CacheTempDetailsLastPurchase(YUSER,YUCI,pidSupplier,pstrType)
 
	quit strValue
	
]]></Routine>
</Export>