<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFDRUCK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFDRUCK
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUFTRAGSHISTORY DRUCKEN
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 03-Apr-2009	GRF		Doco; quits; fix quit fault in TITEL
	; 05.05.2003	FAN
	;-------------------------------------------------------------------------------
	NEW YDATUM,YAUF,YPOS,YNR,NR,ANZ
	
	SET YAUF     = $GET(VORG(1))   ;AUFTRAG ;order 
	SET VORG(2)  = $GET(VORG(2))   ;POSITION
	SET VORG(3)  = $GET(VORG(3))   ;AUFTRAG ALLGEMEINDATEN ;order 
	SET VORG(4)  = $GET(VORG(4))   ;ALLE SCHREIBEN ;write 
	SET VORG(5)  = $GET(VORG(5))   ;AUFTRAGSPOSITION ALLGEMEINDATEN
	SET VORG(6)  = $GET(VORG(6))   ;AUFTRAGSPOSITION STÜCKLISTEN
	SET VORG(7)  = $GET(VORG(7))   ;EINGANGSRECHNUNGSKOONTROLLE
	SET VORG(8)  = $GET(VORG(8))   ;AUFTRAGSPOSITION LIEFERANTKONDITION
	SET VORG(9)  = $GET(VORG(9))   ;ZAHLUNGSVEREINBARUNG
	SET VORG(10) = $GET(VORG(10))  ;1 = Ohne Seitenumbruch  ;page make-up 
	IF YAUF=""  DO ^WWWINFO($$^WWWTEXT(46)) QUIT       ;KEINE DATEN VORHAND ;no forehand 
	IF '$DATA(^INAUF(YM,YAUF))  DO ^WWWINFO($$^WWWTEXT(46)) QUIT        ;KEINE DATEN VORHAND                                            KEINE DATEN VORHANDEN ;no forehand 
	IF YAUF'="" IF VORG(3)="" IF VORG(4)="" IF VORG(5)="" IF VORG(6)="" IF VORG(7)="" IF VORG(8)="" IF VORG(9)="" IF VORG(10)="" DO ^WWWINFO($$^WWWTEXT(32310)) QUIT
	SET ANZ=0 
	DO ^WWWBACK
	;-------------------AUFTRAG
	DO
	. NEW YKEY,YFORM
	. IF VORG(3)=1 DO          ;-------------------AUFTRAG ALLGEMEINDATEN
	. . SET NR=3  DO TITEL
	. . SET YKEY=YAUF
	. . SET YFORM="INAUF"
	. . DO ^WWWPRINT
	. . DO TRENNUNG
	. ;
	. IF VORG(4)=1 DO          ;-------------------ALLE SCHREIBEN
	. . SET NR=4  DO TITEL
	. . SET YKEY=YAUF
	. . SET YFORM="INAUF"
	. . DO ^INDMS1($GET(YKEY),2,1)
	. . DO TRENNUNG
	. ;
	. IF VORG(9)=1 IF $DATA(^INAUFPZ(YM,YAUF))  DO          ;-------------------ZAHLUNGSVEREINBARUNG
	. . SET NR=9  DO TITEL
	. . SET YDATUM=""
	. . FOR  SET YDATUM=$ORDER(^INAUFPZ(YM,YAUF,YDATUM)) QUIT:YDATUM=""  DO
	. . . SET YKEY=YAUF_","_YDATUM
	. . . SET YFORM="INAUFPZ"
	. . . DO ^WWWPRINT
	. . ;
	. . DO TRENNUNG
	
	;--------------------AUFTRAGPOSITION
	IF VORG(2)'="" DO 
	. NEW YKEY,YFORM,YPOS
	. QUIT:'$DATA(^INAUFP(YM,YAUF))
	. SET YPOS=""
	. FOR  SET YPOS=$ORDER(^INAUFP(YM,YAUF,YPOS)) QUIT:YPOS=""  DO
	. . QUIT:'$FIND(VORG(2),YPOS)
	. . ;
	. . IF VORG(5)=1 IF $DATA(^INAUFP(YM,YAUF,YPOS))  DO           ;-------------------AUFTRAGSPOSITION ALLGEMEINDATEN
	. . . SET NR=5  DO TITEL
	. . . SET YKEY=YAUF_","_YPOS
	. . . SET YFORM="INAUFP"
	. . . DO ^WWWPRINT
	. . . DO TRENNUNG
	. . ;
	. . IF VORG(8)=1 IF $DATA(^INAUFPK(YM,YAUF,YPOS))  DO          ;-------------------AUFTRAGSPOSITION LIEFERANTKONDITION
	. . . SET NR=8  DO TITEL
	. . . SET YKEY=YAUF_","_YPOS
	. . . SET YFORM="INAUFPK"
	. . . DO ^WWWPRINT
	. . . DO TRENNUNG
	. . ;
	. . IF VORG(6)=1 IF $DATA(^INAUFPXL(YM,YAUF,YPOS))  DO         ;-------------------AUFTRAGSPOSITION STÜCKLISTEN
	. . . SET NR=6  DO TITEL
	. . . SET YKEY=YAUF_","_YPOS
	. . . DO POS^INAUFSLIST(YKEY)
	. . . DO TRENNUNG
	. . ;
	. . IF VORG(7)=1 IF $DATA(^INERECH1(YM,YAUF,YPOS))  DO         ;-------------------EINGANGSRECHNUNGSKOONTROLLE
	. . . SET NR=7  DO TITEL
	. . . SET YLIEF=""
	. . . FOR  SET YLIEF=$ORDER(^INERECH1(YM,YAUF,YPOS,YLIEF)) QUIT:YLIEF=""  DO
	. . . . SET YNR=""
	. . . . FOR  SET YNR=$ORDER(^INERECH1(YM,YAUF,YPOS,YLIEF,YNR)) QUIT:YNR=""  DO
	. . . . . SET YKEY=YAUF_","_YPOS_","_YLIEF_","_YNR
	. . . . . DO ^INAUFKONTR(YKEY)
	. . . ;
	. . . DO TRENNUNG
	
	IF ANZ=0 DO
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWTEXT(33546)   ; Keine Daten für Ihre Auswahl vorhanden. ;nobody Data to Selection 
	. WRITE "</FONT>"
	
	QUIT
	
TITEL   ;
	IF ANZ=1     ; FIXME : Do Nothing line <GRF>
	SET ANZ=ANZ+1
	WRITE YCR,"<hr>"
	WRITE YCR,"<CENTER><H3>"
	DO            ; quit below leaves <HE> without </HE> - DO added with dot levels <GRF>
	. IF $DATA(^WWW1221(0,"INAUFDRUCK",NR,SPRACHE,1)) IF $PIECE($GET(^WWW1221(0,"INAUFDRUCK",NR,SPRACHE,1)),Y,1)'="" WRITE $PIECE(^WWW1221(0,"INAUFDRUCK",NR,SPRACHE,1),Y,1)  QUIT
	. IF $DATA(^WWW122(0,"INAUFDRUCK",NR,1))          IF $PIECE($GET(^WWW122(0,"INAUFDRUCK",NR,1)),Y,12)'=""         WRITE $PIECE(^WWW122(0,"INAUFDRUCK",NR,1),Y,12)
	WRITE YCR,"</H3></CENTER>"
	WRITE YCR,"<hr>"
	QUIT
	
TRENNUNG
	IF +$GET(VORG(10))=0 DO FF^WWWW()  QUIT  ;FORMFEED
	WRITE YCR,"<BR><BR>"
	QUIT
	
]]></Routine>
</Export>