<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTLP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTLP
	#include COMSYS
	#include INConst
	#include WWWConst
 
OnAfterDataFields(pYKEY)
    ;-------------------------------------------------------------------------------
    ; Show item stock locations by Location filter.
    ;
    ; Returns:
    ;
    ; History:
    ; 03-Nov-2005	Steve S		SR13793: Created
    ;-------------------------------------------------------------------------------
	new idItem,lstHeading,idLocn,strLocation,idBin,objINARTLP,strBin
	new strBinRemarks,strKey,strDesc
 
	set idItem=$piece($get(pYKEY),$$$COMMA,1)
	
	if (idItem'="") {
		set lstHeading=""
		set lstHeading=lstHeading_$lb($$$StrINARTLPLocation)
		set lstHeading=lstHeading_$lb($$$StrINARTLPPossibleLocation)
		set lstHeading=lstHeading_$lb($$$StrINARTLPDescription)
		
		do Start^COMTable(lstHeading,$$^WWWFORMNAME("INARTLP"))
		
		set idLocn=""
		for {
			set idLocn=$order(^INARTLP(YM,idItem,idLocn))
			quit:(idLocn="")
			
			set strLocation=idLocn_" - "_$$$WWW0121LocationName($get(^WWW0121(0,YM,idLocn,1)))
			
			set idBin=""
			for {
				set idBin=$order(^INARTLP(YM,idItem,idLocn,idBin))
				quit:(idBin="")
				
				set objINARTLP=$get(^INARTLP(YM,idItem,idLocn,idBin,1))
				
				set strKey=idItem_","_idLocn_","_idBin
				set strBin=idBin
				set strBinRemarks=$$$INLPRemarks($get(^INLP(YM,idLocn,idBin,1)))
				
				if strBinRemarks'="" set strBin=strBin_" ("_strBinRemarks_")"
				
				set strDesc=$$$INARTLPDescription(objINARTLP)
				set strDesc=$$$VStoBreak(strDesc)
				
				do NewLine^COMTable($select(pYKEY=strKey:"SpringGreen",1:""))
				
				do InsertCell^COMTable(strLocation,"INARTLP",strKey)
				do InsertCell^COMTable(strBin,"INARTLP",strKey)
				do InsertCell^COMTable(strDesc,"INARTLP",strKey)
				
				do EndLine^COMTable()	
			}
				
		}
		
		do Stop^COMTable()
		
	}
 
	quit
]]></Routine>
</Export>