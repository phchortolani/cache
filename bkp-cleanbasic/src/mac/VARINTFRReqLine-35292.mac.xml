<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARINTFRReqLine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARINTFRReqLine
#include COMSYS
#include COMConst
#include VARConst
#include INConst
#include COMGridEdit31

OnAfterDataFields
	do PrintJS
	
	quit

PrintJS
	&js<
	function iePrompt(message) {
		var settings = "dialogWidth: 420px; dialogHeight: 200px; center: yes; edge: raised; scroll: no; status: no";		
		return window.showModalDialog("#(YGIF)#VARPromptSize.html", message,settings);
	}
	
	function PromptMotivo() {
		var MotivoRejeicao = iePrompt('Favor inserir o motivo da rejeição da(s) linha(s) selecionada(s):');
		CallBackNow("PreencherMotivo^VARINTFRReqLine", MotivoRejeicao,document.WWW.YUSER.value);
	}
	>
	quit


PreencherMotivo(pMotivo,pYUSER)
	new idINTFR,idKEY,idLinha,objINTFRLine,strStatus
	if (pMotivo'="false"){
		set strStatus = $$$OK
		set idLinha = ""
		set idINTFR = ""
		for{
			set idLinha = $order(^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","DISPLAYED",idLinha))
			quit:(idLinha="")||(strStatus'=$$$OK)
			set idKEY = $$$KEY2($get(^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","REFERENCEKEY",idLinha,1)))
			if ($get(^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","Y"_idLinha_"_22",1))=1)&&
				($get(^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","Y"_idLinha_"_23",1))=""){
					set ^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","Y"_idLinha_"_23",1)= pMotivo
					set idINTFR = $get(^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","REFERENCEKEY"))
					set objINTFRLine = $get(^INTFRLine(0,idINTFR,idKEY,1))
					set $$$INTFRLineQuantity(objINTFRLine)= $get(^WWWDATEN(0,$$$KEY1($horolog),pYUSER,"INTFRReqLine","V","Y"_idLinha_"_3",1))
					set $$$INTFRLineRejectRemainder(objINTFRLine)= 1
					set $$$INTFRLineReasonRejection(objINTFRLine)= pMotivo
					if ($$$INTFRLineQuantity(objINTFRLine)>0) {
						set strStatus = $$$Save("INTFRLine",idINTFR_$$$COMMA_idLinha,objINTFRLine,$$$YES)
					}else{
						set strStatus = 0
					}
			}
		}
		if (strStatus)&&(idINTFR'="") {
			do RedirectForm^COMUtils("INTFR",idINTFR,YBACK_$$$COMMA_"INReqIssue"_$$$COMMA,"REQUISITION")
		}else{
			$$$Alert("Para que o motivo seja preenchido, é necessário:"_$char(10)_"- ""Rejeitar Restante""  marcado"_$char(10)_"- ""Motivo para rejeição"" em branco"_$char(10)_"- ""Quantidade"" maior que 0.")
		}
	}
	quit

OnBlur(&pYFELD)
	new idKEY
	set idKEY = $get(^WWWDATEN(0,$$$KEY1($horolog),YUSER,"INTFRReqLine","V","REFERENCEROW",$$$KEY2(YKEY),1))
	&js<
	var strFieldName = 'Y#(idKEY)#_22';
	var idCheckbox = 'td'+strFieldName+'_checkbox';
	var objCheckbox = document.getElementById(idCheckbox);
	var objTd = objCheckbox.offsetParent;
	>
	if ($$IsRejectable^INTFRReqLine(pYFELD)){
		&js<
		 objCheckbox.removeAttribute('unselectable');
		 objCheckbox.removeAttribute('readonly');
		 objCheckbox.removeAttribute('disabled');
		 objCheckbox.setAttribute('onclick',"this.defaultChecked = this.checked; retval=EventValue('#(YUCI)#','#(YUSER)#','#(YFORM)#','FIX','COMGridEdit31S','CLICK','6','"+strFieldName+";'+event.shiftKey+';;'+event.ctrlKey);");
		>
	}else{
		&js<
		 objCheckbox.setAttribute('unselectable','');
		 objCheckbox.setAttribute('readonly','');
		 objCheckbox.setAttribute('disabled','');
		 objCheckbox.removeAttribute('onclick');
		 objCheckbox.removeAttribute('checked');
		>
		set ^WWWDATEN(0,$$$KEY1($horolog),YUSER,"INTFRReqLine","V","Y"_idKEY_"_22",1) = 0
	}
	
	quit

ButtonLimparTudo(pidINTFR)
	if ($$$NoKey(pidINTFR)) quit
	new arrGrid, idLine
	$$$GRIDGetContents(.arrGrid,"INTFRReqLine")
	set idLine = ""
	for{
		set idLine = $order(arrGrid(pidINTFR,idLine))
		if ($$$NoKey(idLine)) quit
		set $$$INTFRLineQuantity(arrGrid(pidINTFR,idLine)) = 0
	}
	$$$GRIDSetContents(.arrGrid,$$$YES,"INTFRReqLine",$$$NO,$$$NO)
	quit

OnBeforeSave(pidINTFR)
	;do RemoveLinhaZerada(pidINTFR)

	quit

RemoveLinhaZerada(pidINTFR)
	new arrGrid,idLine
	if ($$$NoKey(pidINTFR)) quit
 	$$$GRIDGetContents(.arrGrid,"INTFRReqLine")
	set idLine = ""
	for{
		set idLine = $order(arrGrid(pidINTFR,idLine))
		if ($$$NoKey(idLine)) quit
		if ($$$INTFRLineQuantity(arrGrid(pidINTFR,idLine)) = 0){
			;do KILLEDRECORDSet^COMGridEdit31Globals("INTFRReqLine",$get(^WWWDATEN(YM,+$horolog,YUSER,"INTFRReqLine","V","REFERENCEROW",idLine,1)))
		}
	}
	quit

GetQuantEstoqueDisponivelPrograma(pYKEY)
	new idTFR,objTFR,objTFRLine
	if ($$$NoKey(pYKEY)) quit 0
	set idTFR = $$$KEY1(pYKEY)
	set objTFR = $get(^INTFR(0,idTFR,1))
	set objTFRLine = $get(^INTFRLine(0,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
	if $$$INTFRProgram1(objTFR)="" quit 0
	quit $$^WWWTR(0,4,##class(alSOH.iSOH).GetQtyAvailable($$$INTFRLineItem(objTFRLine),$$$INTFRSendingLocation(objTFR),"",$$$YES,$$$INTFRProgram1(objTFR)))

GetQuantEstoqueNetWork(pYKEY)
	new idTFR,objTFR,objTFRLine
	if ($$$NoKey(pYKEY)) quit 0
	set idTFR = $$$KEY1(pYKEY)
	set objTFR = $get(^INTFR(0,idTFR,1))
	set objTFRLine = $get(^INTFRLine(0,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
	quit $$^WWWTR(0,4,##class(alSOH.iSOH).GetQtyAvailable($$$INTFRLineItem(objTFRLine),"","",$$$YES))

GetQuantEstoqueDisponivel(pYKEY)
	new idTFR,objTFR,objTFRLine
	if ($$$NoKey(pYKEY)) quit 0
	set idTFR = $$$KEY1(pYKEY)
	set objTFR = $get(^INTFR(0,idTFR,1))
	set objTFRLine = $get(^INTFRLine(0,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
	quit $$^WWWTR(0,4,##class(alSOH.iSOH).GetQtyAvailable($$$INTFRLineItem(objTFRLine),$$$INTFRSendingLocation(objTFR),"",$$$YES))

RetornaQuantEstoque(pYKEY)
	new idTFR,objTFR,objTFRLine
	if ($$$NoKey(pYKEY)) quit
	set idTFR = $$$KEY1(pYKEY)
	set objTFR = $get(^INTFR(0,idTFR,1))
	set objTFRLine = $get(^INTFRLine(0,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
	quit $$^WWWTR(0,4,##class(alSOH.iSOH).GetQtyAvailable($$$INTFRLineItem(objTFRLine),$$$INTFRSendingLocation(objTFR),"",$$$YES,$$$INTFRProgram1(objTFR)))

RetornaQuantCMMLi(pYKEY)
	new idTFR,objTFR,objTFRLine
	if ($$$NoKey(pYKEY)) quit
	set idTFR = $$$KEY1(pYKEY)
	set objTFR = $get(^INTFR(0,idTFR,1))
	set objTFRLine = $get(^INTFRLine(0,$$$KEY1(pYKEY),$$$KEY2(pYKEY),1))
	quit $$GetLocationConsumptionAdjusted^INARTConsumption($$$INTFRLineItem(objTFRLine),$$$INTFRReceivingLocation(objTFR))

OnBlurProduto(pYKEY,&YFELD,pGROW)
	quit:$$$KEY1(pYKEY)=""
	
	set idTFR 	= $$$KEY1(pYKEY)
	set objTFR 	= $get(^INTFR(YM,idTFR,1))
	set idSolic = $$$INTFRRequisitionNumber(objTFR)
	
	quit:idSolic=""
	
	set idReqLine 		= ""
	set codProduto 		= $$$INTFRLineItem(YFELD)
	set strProdutos		= $$ItensJaIncluidos()
	set blnJaIncluido 	= $$$NO
	set blnExiste		= $$$NO

	for idLine = 1:1:$length(strProdutos) {
		
		continue:(idLine = pGROW)

		if ($piece(strProdutos,Y,idLine) = codProduto) {
			w "javascript:alert('O produto "_codProduto_" já foi incluído em outra linha desta transferência (linha "_idLine_").');"
			do ClearLineFields
			set blnJaIncluido = $$$YES
			quit
		}
	}
	
	quit:blnJaIncluido=$$$YES
	
	for {
		set idReqLine = $order(^INReqLine(YM,idSolic,idReqLine))
		quit:idReqLine=""
		
		set objReqLine 	= $get(^INReqLine(YM,idSolic,idReqLine,1))
		set itemReq		= $$$INReqLineItem(objReqLine)
		set unitReq		= $$$INReqLineUnit(objReqLine)
		
		if (itemReq = codProduto) {
			
			set blnExiste = $$$YES
			
			if (+$$$INReqLineRejected(objReqLine)) {
				w "javascript:alert('O produto "_itemReq_" já foi rejeitado na solicitação de origem.');"
				do ClearLineFields
				quit
				
			} elseif (+$$$INReqLineCancelled(objReqLine)) {
				w "javascript:alert('O produto "_itemReq_" já foi cancelado na solicitação de origem.');"
				do ClearLineFields
				quit
			
			} elseif (+$$$INReqLineStatus(objReqLine) = 8) {
				w "javascript:alert('O produto "_itemReq_" já foi encerrado na solicitação de origem.');"
				do ClearLineFields
				quit
			
			} elseif (+$$$INReqLineStatus(objReqLine) = 9) {
				w "javascript:alert('O produto "_itemReq_" já foi totalmente atendido na solicitação de origem.');"
				do ClearLineFields
				quit
			
			} else {
				
				set $$$INTFRLineUnit(YFELD)      = unitReq
				set $$$INTFRLineFREE1(YFELD)     = $$$INReqLineQtyOrdered(objReqLine)
				set $$$INTFRLineFREE2(YFELD)     = $$$INReqLineQtyIssued(objReqLine)
				set $$$INTFRLineFREE5(YFELD)	 = $$^WWWTR(0,4,##class(alSOH.iSOH).GetQtyAvailable(itemReq,$$$INTFRSendingLocation(objTFR),"",$$$YES))
				set $$$INTFRLineFREE6(YFELD)	 = $$^WWWTR(0,4,##class(alSOH.iSOH).GetQtyAvailable(itemReq,"","",$$$YES))
				set $$$INTFRLineReference(YFELD) = idSolic_","_idReqLine
			}
		}
	}
	
	if ('blnExiste) {
		w "javascript:alert('O produto "_codProduto_" não existe na solicitação de origem.');"
		do ClearLineFields
		quit
	}
	
	quit
	
ItensJaIncluidos()
	new date, idLine, idField, idItem, strProdutos
	
	set strProdutos = ""
	set date = $piece($h,$$$COMMA,1)
 	
 	set intTotalGridLines = $$$GRIDRowCount

	for idLine=1:1:intTotalGridLines {
			
		set idField = "Y"_idLine_"_"_1 ;(1=Coluna Produto)		
		set idItem  = $get(^WWWDATEN(0,date,YUSER,"INTFRReqLine","V",idField,1))

		if ($get(^WWWDATEN(0,date,YUSER,"INTFRReqLine","V","KILLEDRECORD",idLine)) '= 1) {
			set $piece(strProdutos,Y,idLine) = idItem
		}
	}
	
	quit strProdutos
	
ClearLineFields
	set $piece(YFELD,Y,1)  = ""
	set $piece(YFELD,Y,3)  = ""
	
	quit
]]></Routine>
</Export>