<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW101" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW101
	
#include COMSYS
	
OnAfterPrimaryKey(YKEY="")
	;-------------------------------------------------------------------------------
	; Check if there are any UOM parameters that are marked to distribute when there
	; shouldn't be.  Report only the first occurence.
	;
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 03-Feb-2010	GRF		-: not strStatus, rework as blnStop
	; 16-Nov-2009	DWR		SR16955: layout - no programming changes
	; 20-Oct-2009	shobby	SR16955: Created
	;-------------------------------------------------------------------------------
	new blnStop,idLanguage,idUnit,objWWW101
	
	if ($$$KEY1(YKEY)="EINHEIT") && ($$$DEVMODE) {  ; Only applicable in development environment
		set blnStop  = $$$NO
		set idLanguage = "" 
		for { 
			set idLanguage = $order(^WWW101(0,"EINHEIT",idLanguage)) 
			quit:idLanguage=""
		
			set idUnit = ""
			for { 
				set idUnit = $order(^WWW101(0,"EINHEIT",idLanguage,idUnit)) 
				quit:idUnit=""
				
				set objWWW101 = $get(^WWW101(0,"EINHEIT",idLanguage,idUnit,1))
				if $$$WWW101SendForNewVersion(objWWW101) &&
				   ((idUnit>49) || (+idUnit'=idUnit))       {
					
					write "<FONT color=red>"_$$$Text($listbuild("WWW00128",idLanguage_","_idUnit_"="_$$$WWW101Text(objWWW101)))_"</FONT>"
				; "Warning! Parameter %1 is out of range for a Parameter marked as 'Send For New Version'.  (Numeric id less than 50)."
					set blnStop = $$$YES
				}
				quit:blnStop
			}
			quit:blnStop
		}
	}
	quit
	
	
OnBeforeDataAccess(YFORM="",YKEY="",YFELD="")
	;-------------------------------------------------------------------------------
	; Limit values in the list
	;
	; Params:
	; 
	; ByRef: YKEYCONTAINER - for form WWWDataExMessageRulesD
	;
	; Returns:
	;
	; History:
	; 10-May-2011	PPP		SR17752: "INDispenseToPatient" - Cut the list only to 1 - Inter Departmental,3 - Dispense Sale
	; 14-Aug-2009	shobby	SR16791: More generic method to exclude items from dropdowns.
	; 27-Mar-2009	shobby	SR16437: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$OK
	if YFORM="WWWDataExMessageRulesD" {
		set strStatus = $$OnBeforeDataAccessTest^WWWDataExMessageRulesD(YKEY,YFELD,YKEYCONTAINER)

	//SR17752
	} elseif (YFORM="INDispenseToPatient") {
		//1 - Inter Departmental
		//3 - Dispense Sale
		set strStatus = ((",1,3,")[(","_$$$KEY3(YKEY)_","))

	} elseif (YFORM'="") {
		if $data(^rOBJ(YFORM_"OBDA")) xecute "set strStatus=$$OnBeforeDataAccess^"_YFORM_"OBDA(YKEY,YFELD)"
	}

	quit strStatus
	 
	 
Check(pidDate="",pidSeries="")
	;-------------------------------------------------------------------------------
	; Compares application parameters before and after an upgrade to list
	; those that have changed.  Useful for determining parameters that are
	; distributed but really shouldn't have been.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-Jun-2009	GRF		Refactor $order macros
	; 09-Apr-2007	shobby	SRBR014354: Created
	;-------------------------------------------------------------------------------
	new idDate,idSeries,idDescription,idLanguage,idKey,objWWW101,objWWW101BACKUP
	
	kill ^CacheTempWWW101Check
	if $get(Y)'="" do ^WWWVAR
	
	if pidDate="" {
		set idDate = $order(^WWW101BACKUP(0,""),-1)
	} else {
		set idDate = pidDate
	}
	
	if pidSeries="" {
		set idSeries = $order(^WWW101BACKUP(0,idDate,""),-1)
	} else {
		set idSeries = pidSeries
	}
	
	if (idDate'="") && (idSeries'="") {
		set idDescription = ""
		for {
			set idDescription = $order(^WWW101(0,idDescription))
			quit:idDescription=""
			
			set idLanguage = ""
			for {
				set idLanguage = $order(^WWW101(0,idDescription,idLanguage))
				quit:idLanguage=""
				
				set idKey = ""
				for {
					set idKey = $order(^WWW101(0,idDescription,idLanguage,idKey))
					quit:idKey=""
					
					set objWWW101       = $get(^WWW101(0,idDescription,idLanguage,idKey,1))
					set objWWW101BACKUP = $get(^WWW101BACKUP(0,idDate,idSeries,idDescription,idLanguage,idKey,1))
					if objWWW101'=objWWW101BACKUP {
						set ^CacheTempWWW101Check(0,idDate,idSeries,idDescription,idLanguage,idKey,"BACKUP") = objWWW101BACKUP
						set ^CacheTempWWW101Check(0,idDate,idSeries,idDescription,idLanguage,idKey,"NEW   ") = objWWW101
					}
				}
			}
		}
		
		set idDescription = ""
		for {
			set idDescription = $order(^WWW101BACKUP(0,idDate,idSeries,idDescription))
			quit:idDescription=""
			
			set idLanguage = ""
			for {
				set idLanguage = $order(^WWW101BACKUP(0,idDate,idSeries,idDescription,idLanguage))
				quit:idLanguage=""
				
				set idKey = ""
				for {
					set idKey = $order(^WWW101BACKUP(idDate,idSeries,0,idDescription,idLanguage,idKey))
					quit:idKey=""
					
					set objWWW101       = $get(^WWW101(0,idDescription,idLanguage,idKey,1))
					set objWWW101BACKUP = $get(^WWW101BACKUP(0,idDate,idSeries,idDescription,idLanguage,idKey,1))
					if objWWW101'=objWWW101BACKUP {
						set ^CacheTempWWW101Check(0,idDate,idSeries,idDescription,idLanguage,idKey,"BACKUP") = objWWW101BACKUP
						set ^CacheTempWWW101Check(0,idDate,idSeries,idDescription,idLanguage,idKey,"NEW   ") = objWWW101
					}
				}
			}
		}
	}	
	quit
	
	
OnAfterButtonLine(pidWWW101="")
 	;-------------------------------------------------------------------------------
	; Recovers YVOR value
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-Jun-2009	GRF		SR16598: Authorization Macro
	; 05-May-2007	Karine	BR014574: Disable change parameters in english
	; 16-May-2007	HeberB	SRBR014447: Created
	;-------------------------------------------------------------------------------
 	set $$$WWW120AuthorizationToModifyData(YVOR) = $get(^CacheTempWWW101Enabled(YUCI,YUSER))
 	kill ^CacheTempWWW101Enabled(YUCI,YUSER)
 	;BR014574  vvv
 	;IF $$$KEY2(YKEY) = "EN" { 
	;		set $$$WWW120AuthorizationToModifyData(YVOR)=$$$EnumReadOnly
	;} 
 	;BR014574 ^^^
	quit
	
OnBeforeButtonLine(pidWWW101="")
 	;-------------------------------------------------------------------------------
	; Backup YVOR value and sets RO/RW
	; 
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-Jun-2009	GRF		SR16598: Authorization Macro
	; 09-Jul-2007	GM		SRBR014574: Call routine to let fields read only
	; 16-May-2007	HeberB	SRBR014447: Created
	;-------------------------------------------------------------------------------
 	do ReadOnly^WWWFORM("TSUtilityApplicationParameter") ;BR014574 	
 	
 	set ^CacheTempWWW101Enabled(YUCI,YUSER) = $$$WWW120AuthorizationToModifyData(YVOR)
 	if $get(pidWWW101)'="" {
	 	if '$$EnableDisableField(pidWWW101) {
	 		set $$$WWW120AuthorizationToModifyData(YVOR) = $$$EnumReadOnly
	 	}
 	}
 	
	quit
	
	
EnableDisableField(pidWWW101="")
	;-------------------------------------------------------------------------------
	; Restrict Create of Application Parameters
	; 
	; If the ^SysSetup("Edit","ApplicationParameters") flag *IS NOT* set then
	; 	1) The WWW101 screen is read only for all application parameters 
	; 		 that are not EINHEIT/BERECHTIGUNG.
	; 	2) When the application parameter is EINHEIT/BERECHTIGUNG the screen is read 
	; 		 only for application parameters with an ID less than 50/100.
	; 	3) When the application parameter is EINHEIT/BERECHTIGUNG the screen is editable 
	; 	   for application parameters with an ID greater or equal to 50/100.
	; 	4) When the ^SysSetup flag is not set the reverse is true.
	;
	;	    ^SysSetup("Edit","ApplicationParameters")
	;			    =0 (Var)	    =1 (Dev)
	;		    EINH  'EINH		EINH  'EINH
	;	<50  	 RO    	RO		RW     	RW
	;	>=50 	 RW    	RO 		RO     	RW
	;	EMPTY	 RW		RO 		RW		RW
	;		    BERE  'BERE		BERE  'BERE
	;	<100  	 RO    	RO		RW     	RW
	;	>=100 	 RW    	RO 		RO     	RW
	;	EMPTY	 RW		RO 		RW		RW
	;	
	; Params:
	;
	; Returns:
	;
	; History:
	; 11-Jun-2009	GRF		SR16598: Clear old commented code; clarify if tests
	; 27-Jun-2007	RPW		SRBR014554: Use a variable instead of recalculating IsValidNum
	; 25-Jun-2007	HeberB	SRBR014554: Added BERECHTIGUNG >=100 and updated doco above
	; 21-Jun-2007	HeberB	SRBR014523: Deal with app.param char as >= 50
	; 16-May-2007	HeberB	SRBR014447: Amend doco above and deals with empty KEY3 
	; 14-May-2007	GRF		SRBR014447: Amend doco above
	; 09-May-2007	RPW		SRBR014447: Use good global, not ^Development
	; 03-May-2007	HeberB	SRBR014447: Logic corrected
	; 27-Apr-2007	HeberB	SRBR014447: Created
	;-------------------------------------------------------------------------------
 	new blnEditable,blnKey3Valid
 	
 	set YHID = 0	// Enable field
 	; on edition
 	quit:($$$KEY1(pidWWW101) = "") $$$YES
 	
 	set blnEditable = $$$NO
 	
 	if ($$$KEY3(pidWWW101) = "") {
	 	if '( ($$$KEY1(pidWWW101)'="EINHEIT")                       &&
	 	      ($$$KEY1(pidWWW101)'="BERECHTIGUNG")                  &&
	 	      ($get(^SysSetup("Edit","ApplicationParameters")) = 0) )  {
			set blnEditable = $$$YES 	
 		}
 		
 	} else {
	 	set blnKey3Valid = $ISVALIDNUM($$$KEY3(pidWWW101))
 		
		if ($$$KEY1(pidWWW101)="EINHEIT") {
			if ($$$KEY3(pidWWW101) >= "50") || 'blnKey3Valid   set blnEditable  = $$$YES  ; SR16598
	 	}
		if ($$$KEY1(pidWWW101)="BERECHTIGUNG") {
			if ($$$KEY3(pidWWW101) >= "100") || 'blnKey3Valid  set blnEditable  = $$$YES  ; SR16598
	 	}
		if ($get(^SysSetup("Edit","ApplicationParameters")) = 1) {       //  doesn't need quotes
	 		set blnEditable  = 'blnEditable
	 	}
	}	
	if 'blnEditable {
		set YHID = 2 // Disable field	
	}
	quit blnEditable
	
	
OnBeforeSave(pidWWW101="")
	;-------------------------------------------------------------------------------
	;  Do not allow creation of non-EINHEIT records.
	;  Allow creation of EINHEIT but disallow save when ID is less than 50.
	;
	; Params:
	;
	; Returns:
	;
	; History:
	; 17-May-2007	HeberB		SRBR014447: Created
	;-------------------------------------------------------------------------------
	set Q = $$$QSave
 	if $get(pidWWW101)'="" {
		if '$$EnableDisableField(pidWWW101) {
			set Q = $$$QDontSave
			$$$Alert(33426)            ; "The entered data has not been saved!"
		} 
 	}
	quit
	
	
Get(pstrParam,pstrLang,pstrCode)
	quit $get(^WWW101(0,pstrParam,pstrLang,pstrCode,1))
	
	
OnDataAccess(YKEY,YFORM,pstrParam)
	;-------------------------------------------------------------------------------  
	;
	;
	;                 PROTOTYPING - IN DEVELOPMENT - GRF
	;
	;
	;	Add the following to the Class Call Backs for WWW101 to activate 
	;  $$OnDataAccess^WWW101(YKEY,YFORM,$g(VORG(1)))
	;  
	;  VORG(1) is based on the "Input In Variable" for INRECSummary : Item
	;  
	;  NOTE : not called for grid.
	;-------------------------------------------------------------------------------  
	new blnResult,enumValue,idLanguage,idParam
	
	set idParam    = $$$KEY1(YKEY)
	set idLanguage = $$$KEY2(YKEY)
	set enumValue  = $$$KEY3(YKEY)
	set blnResult  = $$$YES
	
	if idParam = "EINHEIT" {
		if YFORM="INRECSummary" {
			if pstrParam'="" {
				set blnResult = ''$data(^INARTPACK(0,"FSM",enumValue,1))
			}
		}
	}
	
	quit blnResult
	
]]></Routine>
</Export>