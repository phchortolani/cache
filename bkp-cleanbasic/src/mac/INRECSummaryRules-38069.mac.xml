<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INRECSummaryRules" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INRECSummaryRules
	;+++++++++++++++++++++++++++++++++++++++
	; see INRECDoco
	;+++++++++++++++++++++++++++++++++++++++
#include COMSYS
#include INConst
#include %occInclude
#include COMGridEdit31
	
#define LogR(%1,%2) 	;
#define LogRx(%1)		;
#define LogRm(%1)		;
#;define LogR(%1,%2) 	$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))= %1_"^INRECSummaryRules("_%2_") : "_$zh $$$JournalOn
#;define LogRx(%1)		$$$JournalOff s ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
#;define LogRm(%1)		$$$JournalOff m ^zzLogR($g(YBED,"UNK"),$i(^zzLogR))=%1 $$$JournalOn
	
#define FORMFldINRECSummaryLineType      	5
#define FORMFldINRECSummaryItem          	6
#define FORMFldINRECSummaryOrderedUnit    	9
#define FORMFldINRECSummaryQtyRecvd      	10
#define	FORMFldINRECSummaryReceivedUnit  	11
#define FORMFldINRECSummaryUnitCost		 	14
#define FORMFldINRECSummaryExtendedCost  	15
#define FORMFldINRECSummaryCostAfterDisc 	16
#define FORMFldINRECSummaryCostAllocation	20
#define FORMFldINRECSummaryGLAccount    	21
#define FORMFldINRECSummaryDiscCode		 	26
#define	FORMFldINRECSummaryDiscType		 	27
#define FORMFldINRECSummaryDiscRate      	28
#define FORMFldINRECSummaryCostDistrib  	30
#define FORMFldINRECSummaryUnitQtyRecvd    	33
	
#define DELIM	$char(31)
#define EnumTypeStandard	0
#define EnumTypeDiscount	1
#define EnumTypeBonus		2

#define IsItemLine   ((enumLineType=$$$EnumTypeStandard) || (enumLineType=$$$EnumTypeBonus))
#define IsChargeLine ((enumLineType'="") && (enumLineType'=$$$EnumTypeStandard) && (enumLineType'=$$$EnumTypeBonus))

CheckReadOnly(pidFormField,pobjSummary,pidGridRow=0)
	;-------------------------------------------------------------------------------
	;  
	;  "If Condition On Execute" : $$CheckReadOnly^INRECSummaryRules(pidFormField,pstrData,$g(YROW))
	;  "Read Only Field"         : Tick check box
	;  Note : GROW is not always set to correct value when Core Rules
	;         are checked, hence YROW
	;  
	;  Tab 1 : Receipts   : F6, F10, F11
	;  Tab 3 : Invoice    : F5, F6, F14, DF20, F21, F26, F27, F28, F30
 	;  
	; pidFormField
	;  
	; History:
 	; 21-May-2012	GRF		SR18012: Always ReadOnly if processed.
	; 28-Mar-2010	GRF		SR17244: Still block DiscCode before line type is set or
	;							if bonus line (no in-line disc if no cost)
	; 24-Mar-2010	GRF		SR17244: if not setting CacheTemp, don't need to force
	; 							pidGridRow to " "
	; 18-Feb-2010	shobby	SR15753: Commented redundant code
	; 01-Oct-2009	PPP		SR16797: Disable the Item if it for an Order
	; 21-Aug-2009	DWR		SR16837: nolonger disable unit if it is for an order.
	; 06-Aug-2009	GRF		SR16815: split from INRECSummary; UnitQtyRecvd added
	; 21-Jul-2009	GRF		SR16669: extra test for null pidGridRow
	; 16-Jul-2009	GRF		SR16669: separate DiscCode from other charge-type cols.
	; 15-Jul-2009	GRF		SR16669: pass GROW as pidGridRow
	; 15-Jul-2009	GRF		SR16689: Correct INRECSummary property names in macros
	; 29-Jun-2009	GRF		SR16669: selective read only based on INRECSummaryC
	; 15-Jun-2009	GRF		SR16598: Created
	;-------------------------------------------------------------------------------
	new blnFromOrder,blnIsFirmed,blnReadOnly,enumLineType,objReceipt
	
	$$$LogR("CheckReadOnly",pidFormField_"<"_pidGridRow_"<"_$get(^CacheTemp(YUSER,"Grid","YSEITE")))
	
	set objReceipt   = $$$GRIDYFELDContainer
	if $$$INRECStatus(objReceipt) '= $$$EnumINSTATUSUnprocessed quit $$$YES   ; Early Exit   ; SR18012
	
	set enumLineType = $$$INRECSummaryLineType(pobjSummary)
	set blnIsFirmed  = ($$$INRECFirmed(objReceipt) = $$$YES)
	set blnFromOrder = ($$$INRECSummaryPurchaseOrder(pobjSummary)'="")
	set blnReadOnly  = $$$NO
		
	if ($get(^CacheTemp(YUSER,"Grid","YSEITE"))=1) {
		
		if (pidFormField = $$$FORMFldINRECSummaryItem)         ||
		   (pidFormField = $$$FORMFldINRECSummaryReceivedUnit)    {
			
			if blnIsFirmed {
				set blnReadOnly = $$$YES
			}
			if (pidFormField = $$$FORMFldINRECSummaryItem) && blnFromOrder {
				set blnReadOnly = $$$YES
			}
		
		} elseif (pidFormField = $$$FORMFldINRECSummaryQtyRecvd)     ||
		   		 (pidFormField = $$$FORMFldINRECSummaryUnitQtyRecvd)    {
			
			if blnIsFirmed {
				set blnReadOnly = $$$YES
			}
		}
	
	} elseif ($get(^CacheTemp(YUSER,"Grid","YSEITE"))=3) {
		if (pidFormField = $$$FORMFldINRECSummaryItem)         ||
		   (pidFormField = $$$FORMFldINRECSummaryQtyRecvd)     ||
		   (pidFormField = $$$FORMFldINRECSummaryReceivedUnit) ||
		   (pidFormField = $$$FORMFldINRECSummaryUnitQtyRecvd)    {   ; SR16
			
			set blnReadOnly = $$$YES
		
		} elseif pidFormField = $$$FORMFldINRECSummaryUnitCost {
			if '$$$IsItemLine set blnReadOnly = $$$YES
		
		
			
		} elseif pidFormField = $$$FORMFldINRECSummaryDiscCode {
	;		if (enumLineType="") || $$$IsItemLine {                 ; SR17244
			if (enumLineType="") || (enumLineType=$$$EnumTypeBonus) {
				set blnReadOnly = $$$YES
			}
		} elseif (pidFormField = $$$FORMFldINRECSummaryDiscType)       ||
		         (pidFormField = $$$FORMFldINRECSummaryDiscRate)       ||
		         (pidFormField = $$$FORMFldINRECSummaryGLAccount)      ||
		         (pidFormField = $$$FORMFldINRECSummaryCostAllocation) ||
		         (pidFormField = $$$FORMFldINRECSummaryCostDistrib)      { 
			
	;		if (enumLineType="") || $$$IsItemLine || ($$$INRECSummaryDiscountCode(pobjSummary)="") {  ; SR17244
			if ($$$INRECSummaryDiscountCode(pobjSummary)="") {
				set blnReadOnly = $$$YES
			}
		}
	}
	quit blnReadOnly
	
	
CheckMandatory(pidFormField,pobjSummary,pidGridRow=0)
	;-------------------------------------------------------------------------------
	;  
	;  "If Condition On Execute" : $$CheckMandatory^INRECSummaryRules(pidFormField,pstrData,$g(YROW))
	;  "Mandatory Field"         : Tick check box
	;  Note : GROW is not always set to correct value when Core Rules
	;         are checked, hence YROW
	;  
	;  Tab 1 : Receipts   : F6, F10, F11
	;  Tab 3 : Invoice    : F5, F6, F14, DF20, F21, F26, F27, F28, F30
 	;  
	; pidFormField
	;  
	; History
	; 18-Jun-2009	GRF		SR16598: Charge-related columns are not mandatory until
	;							Post validation.
	; 15-Jun-2009	GRF		SR16598: Created
	;-------------------------------------------------------------------------------
	new blnFromOrder,blnIsFirmed,blnMandatory,enumLineType,objReceipt
	
	$$$LogR("CheckMandatory",pidFormField_"<"_pidGridRow_"<"_$get(^CacheTemp(YUSER,"Grid","YSEITE")))
	
	set objReceipt   = $$$GRIDYFELDContainer
	set enumLineType = $$$INRECSummaryLineType(pobjSummary)
	set blnIsFirmed  = ($$$INRECFirmed(objReceipt) = $$$YES)
	set blnFromOrder = ($$$INRECSummaryPurchaseOrder(pobjSummary)'="")
	set blnMandatory = $$$NO
		
	if ($get(^CacheTemp(YUSER,"Grid","YSEITE"))=1) {
		if (pidFormField = $$$FORMFldINRECSummaryItem)         ||
		   (pidFormField = $$$FORMFldINRECSummaryReceivedUnit)    {
			if 'blnFromOrder set blnMandatory = $$$YES
		}
	
	} elseif ($get(^CacheTemp(YUSER,"Grid","YSEITE"))=3) {
		if pidFormField = $$$FORMFldINRECSummaryUnitCost {
			if $$$IsItemLine set blnMandatory = $$$YES
		}
	}
	quit blnMandatory
]]></Routine>
</Export>