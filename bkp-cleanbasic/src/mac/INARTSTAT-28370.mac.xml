<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTSTAT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTSTAT(KEY,MONAT,SalesType)
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		ARTIKELSTATISTIK DES JAHRES
	;
	; Inputs : 
	;
	;
	; ByRef :
	;
	;
	; Returns :
	;
	;
	; History :
	; 04-Jun-2010	GRF		SR17146: call "DD.MM.YYYY" wrapper for WWWDATE1
	; 02-Sep-2005	FIS		SR13264: SELECT SALES TYPE
	; 10.07.2000	DT
	;-------------------------------------------------------------------------------
	NEW JAHR,YVON,YBIS,VMON,BMON,YVON1,YBIS1,UMSATZ,UMSATZ1,UMSATZA,XACHSE
	
	SET KEY       = $GET(KEY)
	SET SalesType = +$GET(SalesType)  ;FIS;31.08.2005;SR13263/SR13264
	
	SET NAME  = $$^WWWTEXT(32059)      ; "Total Value"
	IF KEY'="" SET NAME = $PIECE($GET(^INART(YM,KEY,1)),Y,1)  ;NAME DES ARTIKELS
	
	SET MONAT = $GET(MONAT) ;=1 STATISTIK FÜR VERLAUF 12 MONATE ZURÜCK + AKTUELLEN MONAT ;statistics to trend retro- month 
	SET JAHR  = $$^WWWYEAR($HOROLOG)           ; this year 
	SET YVON  = $$DMY^WWWDATE1("01.01."_JAHR)  ; SR17146
	SET YBIS  = $$DMY^WWWDATE1("31.12."_JAHR)  ; SR17146
	SET VMON  = 1                              ;from month 
	SET BMON  = 12                             ;until month 
	IF MONAT=1 DO                              ;13 MONATE
	. SET YVON1 = $$DMY^WWWDATE1("01.01."_(JAHR-1)) ;Prior Year  ; SR17146 
	. SET YBIS1 = $$DMY^WWWDATE1("31.12."_(JAHR-1))              ; SR17146
	. SET VMON  = $$^WWWMONTH(+$HOROLOG)        ;VOM AKTUELLEN MONAT ;frommonth 
	. SET BMON  = VMON+12                       ;BIS 12 MONATE ZURÜCK (13 MONATE) ;until retro- 
	
	IF MONAT'=1 DO ^WWWBACK
	SET UMSATZ=$$^INUMS(YVON,YBIS,2,KEY,+SalesType,1000)  ;SUCHEN UMSATZ(DATUM1,DATUM2,KATEGORIE1,KEY,ART,EINHEIT,BETRIEB,WARENGRUPPE,VKEK)  ;FIS;31.08.2005;SR13263/SR13264
	IF $PIECE(UMSATZ,",",12)="" SET $PIECE(UMSATZ,",",12)=""
	IF MONAT=1 DO
	. SET UMSATZA = $$^INUMS(YVON1,YBIS1,2,KEY,+SalesType,1000) ;SUCHEN FÜR VERGANGENES JAHR ;seek to year   ;FIS;31.08.2005;SR13263/SR13264
	. IF $PIECE(UMSATZA,",",12)="" SET $PIECE(UMSATZA,",",12)=""
	. SET UMSATZ  = UMSATZA_","_UMSATZ
	. SET UMSATZ  = $PIECE(UMSATZ,",",VMON,BMON)
	
	SET XACHSE=$$^WWWTEXT(30024,,1)
	IF MONAT=1 DO  ;BESCHRIFTUNG XACHSE ;lettering 
	. SET XACHSE=XACHSE_","_XACHSE
	. SET XACHSE=$PIECE(XACHSE,",",VMON,BMON)
	
	WRITE "<BR>"
	DO
	. SET YFELD=""
	. SET $PIECE(YFELD,Y,1)  = "gif"_$PIECE($HOROLOG,",",2)_$RANDOM(10)_".gif"  ;DATEI ;data file 
	. SET $PIECE(YFELD,Y,2)  = 0      ;0 BOX,1 LINES,2 STACK,3 STOCK
	. SET $PIECE(YFELD,Y,3)  = $$^WWWTEXT(32081,,1)
	. IF (SalesType=0) || (SalesType=1) SET $PIECE(YFELD,Y,3)=$PIECE(YFELD,Y,3)_" ("_$PIECE($GET(^WWW101(0,"UMSATZART",SPRACHE,SalesType,1)),Y,1)_")"  ;FIS;2.9.05;SR13264;SHOW SELECTED SALES TYPE
	. IF (SalesType=2) || (SalesType=3) SET $PIECE(YFELD,Y,3)=$PIECE(YFELD,Y,3)_" ("_$PIECE($GET(^WWW101(0,"UMSATZART",SPRACHE,(SalesType-2),1)),Y,1)_")"  ;FIS;2.9.05;SR13264;SHOW SELECTED SALES TYPE
	. SET $PIECE(YFELD,Y,3)  = $PIECE(YFELD,Y,3)_" "_$$^WWWYEAR($HOROLOG)_" ("_KEY_") "_NAME  ;UMSATZ JAHR NUMMER NAME ;volume of trade year numeral Name 
	. SET $PIECE(YFELD,Y,4)  = "BLACK"         ; FARBE ;colour
	. SET $PIECE(YFELD,Y,5)  = XACHSE          ; X-achse MONATE
	. SET $PIECE(YFELD,Y,6)  = $$^WWWTEXT(16)  ; "Month"
	. SET $PIECE(YFELD,Y,7)  = ""              ; Y-ACHSE leer = automatic ;void 
	. SET $PIECE(YFELD,Y,8)  = $$^WWWTEXT(32081,,1)_"(*1000)"  ; "Sales"  ;Y-ACHSE UMSATZ ;volume of trade 
	. SET $PIECE(YFELD,Y,10) = $$^WWWTEXT(32081,,1)            ; "Sales"  ;RUBRIK 1 ;header 
	. SET $PIECE(YFELD,Y,11) = "BLUE"       ;FARBE ;colour 
	. SET $PIECE(YFELD,Y,12) = UMSATZ       ;WERTELISTE   NN,NN,NN,
	. SET $PIECE(YFELD,Y,13) = ""           ;RUBRIK 2 ;header 
	.;SET $PIECE(YFELD,Y,14) = "RED"        ;FARBE
	. SET $PIECE(YFELD,Y,15) = ""           ;WERTELISTE   NN,NN,NN,
	. SET $PIECE(YFELD,Y,16) = ""           ;RUBRIK 3 ;header 
	.;SET $PIECE(YFELD,Y,17) = "GREEN"      ;FARBE
	. SET $PIECE(YFELD,Y,18) = ""           ;WERTELISTE   NN,NN,NN,
	. SET $PIECE(YFELD,Y,19) = ""           ;RUBRIK 4 ;header 
	.;SET $PIECE(YFELD,Y,20) = "YELLOW"     ;FARBE
	. SET $PIECE(YFELD,Y,21) = ""           ;WERTELISTE   NN,NN,NN,
	. SET $PIECE(YFELD,Y,31) = "580"        ;width
	. SET $PIECE(YFELD,Y,32) = "250"        ;height 
	. ;
	. DO ^WWWGRAPH(YFELD)
	
	DO   ;GESAMT ;total  ;total whole 
	. WRITE YCR
	. WRITE "<BR><B>"
	. WRITE "<FONT SIZE=2>"
	. WRITE $$^WWWTEXT(32059)_":  "
	. NEW SUMM
	. FOR YI=1:1:12 SET SUMM=$GET(SUMM)+$PIECE(UMSATZ,",",YI)
	. WRITE $$^WWWZAHL(SUMM,0,0)
	. WRITE "</FONT></B>"
	
	QUIT
]]></Routine>
</Export>