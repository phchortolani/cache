<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INIssueRevLineSearch" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INIssueRevLineSearch
#include COMSYS
#include INConst
#include COMConst
#include %occInclude 
#include WWWConst
#include COMGridEdit31

#define DELIM	$char(31)

#define FORMFldINIssueRevLineSearchLotNo			24
OnBeforeFormat(pYFIELDNAME,pYTEXT,pYFELD)
	;-------------------------------------------------------------------------------
	; 
	; Inputs : 
	;	pYFIELDNAME	- 	Browser Element ID
	;	pYTEXT 		- 	Value currently in element
	;	pYFELD		-	ANM INIssueLine Data record
	;
	; History :
	; 16-Sep-2013	SCR		CORE-93: Created
	;-------------------------------------------------------------------------------
	New intRow,intCol,idClassField,objLine,strBundleValues,idBundle,objBundle,strBundleValues
	new intDupLine,idIssue,idLineNo,objIssue,idItem,objIssueLine

	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)
	
	set objLine		= $$$GRIDGetYFELD($$GetYKEY^COMGridEdit31Interface(intRow))
	set objHeader	= $$$GRIDYFELDContainer
	set FROMLOCATION= $$$INIssueRevFromLocn(objHeader)
	set TOLOCATION	= $$$INIssueRevLineToLocation(objLine)
	set idBundle    = $$$INIssueRevLineBundle(objLine)
	set idIssue		= $$$INIssueRevLineIssue(objLine)
	set idLineNo	= $$$INIssueRevLineRefLine(objLine)
	set idItem		= $$$INIssueRevLineItemNo(objLine)
	if idClassField= $$$FldINIssueRevLineBundle {
	    if (+$$$WWWClientParamCoreChangesHEVA($get(^WWWClientParam(YM,YM,1)))) {
		    set strBundleValues = ""
			if (idIssue'="") && (idLineNo'="") {
				set objIssue	 = $get(^INIssue(YM,idIssue,1))
		    	set objIssueLine = $get(^INIssueLine(YM,idIssue,idLineNo,1))
				set idFromLocn   = $$$INIssueFromLocn(objIssue)
				set idFromStore  = $$$INIssueLineFromStorage(objIssueLine)
				set idBundle     = $$$INIssueLineBundle(objIssueLine)
				set strBundleKey = idItem_"||"_idFromLocn_"||"_idFromStore_"||"_idBundle
				set objBundleStock = ##class(alSOH.dBundleStock).%OpenId(strBundleKey)
				if ($IsObject(objBundleStock)) {
					set objBundle = objBundleStock.Bundle
				}
				else {
					set objBundle = ##class(alSOH.dBundle).%OpenId(idBundle)
				}
				if ($IsObject(objBundle)){
					set strBundleValues = ##class(alSOH.iUSOH).RenderBundle(objBundle,SPRACHE)
				}
			}
			do UpdateManualField^COMGridEdit31Interface(intRow,10,strBundleValues)
		}

	}
	if idClassField=$$$FldINIssueRevLineUnitQuantityReturned {
		if idBundle = "" {
			set YWARNING = $$$YES
			set YTOOLTIP =  $$$Text("IN01435")  ;No Issue Selected
		} elseif $$$INIssueRevLineQuantityReturned(objLine)>( $$$INIssueRevLineQtyConsumed(objLine)+$$$INIssueRevLineQtyAlreadyRetrn(objLine)) {
			set YWARNING = $$$YES
			set YTOOLTIP =  $$$Text("IN01436")  ;Quantity more than Issued Quantity
		} elseif $$DuplicateIssueLines(intRow,.intDupLine) {
			set YWARNING = $$$YES
			set YTOOLTIP =  $$$Text( $listbuild("IN01437",intDupLine))  ;Line Duplicated on line %1
		}
	}
	quit
	
DuplicateIssueLines(pintRow=0,&pintDupLine,&pintSourceLine)
	;-------------------------------------------------------------------------------
	; Check for Duplicate Issue Lines in the grid
	; History :
	; 16-Sep-2013	SCR		CORE-93: Created
	;-------------------------------------------------------------------------------
	new blnDup,arrLines,intLine,objLine,idIssue,idIssueLine,idRev
	new arrIssues
	
	set idRev=$piece(YKEY,",",1)
	do GetGridContents^COMGridEdit31G(.arrLines,"INIssueRevLineSearch")
	
	set blnDup			= $$$NO
	set intLine			= 0
	set pintDupLine		= 0
	set pintSourceLine	= 0
	
	for {
		set intLine=$order(arrLines(idRev,intLine))
		quit:'intLine
		set objLine		= arrLines(idRev,intLine)
		set idIssue		= $$$INIssueRevLineIssue(objLine)
		set idIssueLine	= $$$INIssueRevLineRefLine(objLine)
		continue:idIssue=""  ; Not Selected
		continue:idIssueLine="" ; This should not happen but skip anyway
		if $data(arrIssues(idIssue,idIssueLine)) {
			if 'pintRow {
				set pintSourceLine	= intLine
				set pintDupLine		= arrIssues(idIssue,idIssueLine)
				set blnDup			= $$$YES
			}
			continue
		}
		set arrIssues(idIssue,idIssueLine)=intLine
		
	}
	if pintRow&0 {
		set objLine		= arrLines(idRev,pintRow)
		set idIssue		= $$$INIssueRevLineIssue(objLine)
		set idIssueLine	= $$$INIssueRevLineRefLine(objLine)
		if (idIssue'="")&&(idIssueLine'="") {
			if arrIssues(idIssue,idIssueLine)'=pintRow {
				set pintDupLine		= arrIssues(idIssue,idIssueLine)
				set pintSourceLine	= pintRow
				set blnDup			= $$$YES
			}
		}
	}
	quit blnDup
	

OnBeforeSave(pYKEY,&pYFELD)	
	;-------------------------------------------------------------------------------
	;
	; History :
	; 16-Sep-2013	SCR		CORE-93: Created
	;-------------------------------------------------------------------------------
	quit

OnBlurItem(pidClassField,&pobjLine,pidKeys="",pGridRow)
	;-------------------------------------------------------------------------------
	; On Blur from Item, Qty and Unit fields.
	;
	; Called By : COMGridEdit31Events due to setting on form
	; 
	; Params:	pidClassField	- field id
	;
	; ByRefs:	pobjLine		- current line object
	;			GROW 			- current Grid Row
	; Returns:
	;
	; History:
	; 15-Sep-2013	SCR		CORE-93: Created
	;-------------------------------------------------------------------------------
	new idItem,strStatus,objRev
	
	set idItem		= $$$INIssueRevLineItemNo(pobjLine)
	set strStatus	= $$IsUsable^INARTDataAccess(idItem,YFORM)
	if $$$ISOK(strStatus) {
		set objRev		= $$$GRIDYFELDContainer
		if '$$Search^INIssueRevLineScan(objRev,idItem,"") {
			set strStatus	=  $$$MakeStatus("IN01438",idItem)  ;Can not find Issue for Item %1
			set $$$INIssueRevLineItemNo(pobjLine) = ""
		}
	}
	if $$$ISERR(strStatus) {
		write "javascript:alert('"_$$$Text(strStatus)_"');"

		quit  ; <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< Fast Exit
	}
	set pobjLine	= ""										; Reset the Line
	set $$$INIssueRevLineItemNo(pobjLine)	= idItem
	do ShowItems^INIssueRevTable(1,"INIssueRev"_$$$DELIM_"INIssueRevLineSearch"_$$$DELIM_"dStockBundle"_$$$DELIM_pGridRow) 
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineStockUnit_		$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineQtyConsumed_	$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineBundle_			$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineRefLine_		$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineFromStorage_	$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineToStorage	_	$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineQtyAlreadyRetrn	_$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineIssue_			$$$COMMA)
	do SetRecord^COMGridEdit31S1("INIssueRevLineSearch",pGridRow, $$$FldINIssueRevLineToLocation_		$$$COMMA)
	do UpdateContainer^COMGridEdit31G(,$$$YES)
	quit

]]></Routine>
</Export>