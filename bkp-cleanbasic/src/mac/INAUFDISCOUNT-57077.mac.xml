<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFDISCOUNT" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFDISCOUNT	;INAUFDISCOUNT;FIS;ORDER DISCOUNT;25327;22.06.2004
	;
	;/------------------------------------------------------------------\
	;| Description of Function :
	;|		ORDER DISCOUNT
	;|
	;| Inputs : 
	;|
	;|
	;| ByRef :
	;|
	;|
	;| Returns :
	;|
	;|
	;| History :
	;|
	;| FIS	25327
	;|
	;\------------------------------------------------------------------/
	;
	NEW YYFELD
	SET YKEY=$GET(YKEY)
	IF YKEY="" QUIT
	SET YFELD=$GET(YFELD)
	IF YFELD="" QUIT
	SET YYFELD=YFELD
	DO
	. NEW YFELD,AMOUNT,PERCENT,REASON
	. SET YFELD=$GET(^INAUF(YM,$P(YKEY,",",1),1))
	. QUIT:YFELD=""
	. SET AMOUNT=""
	. SET PERCENT=""
	. SET REASON=""
	. SET SEQ="" FOR  SET SEQ=$ORDER(^INAUFDISCOUNT(YM,$PIECE(YKEY,",",1),SEQ)) QUIT:SEQ=""  DO
	. . SET DISCOUNT=$GET(^INAUFDISCOUNT(YM,$PIECE(YKEY,",",1),SEQ,1))
	. . IF $PIECE(DISCOUNT,Y,3)'="" SET PERCENT=$$AddPercentage(PERCENT,$PIECE(DISCOUNT,Y,3)) QUIT  ;PERCENT DISCOUNT
	. . IF $PIECE(DISCOUNT,Y,4)'="" SET AMOUNT=AMOUNT+$PIECE(DISCOUNT,Y,4)  ;AMOUNT MARKUP
	. . IF $PIECE(DISCOUNT,Y,2)'="" SET REASON=$PIECE($GET(^INREASONCODE(YM,$PIECE(DISCOUNT,Y,2),1)),Y,2)  ;REASON CODE
	. . QUIT
	. SET $PIECE(YFELD,Y,70)=""
	. SET $PIECE(YFELD,Y,210)=""
	. SET $PIECE(YFELD,Y,71)=PERCENT
	. SET $PIECE(YFELD,Y,211)=AMOUNT*-1
	. SET $PIECE(YFELD,Y,210)="- "_REASON
	. DO
	. . NEW YFORM,YVOR,YOK
	. . SET YOK=$$^WWWSPEI("INAUF",$PIECE(YKEY,",",1),YFELD,1)
	. . QUIT
	. QUIT
	QUIT
 
AddPercentage(pfltAmt1,pfltAmt2)
    ;-------------------------------------------------------------------------------
    ; The addition of 2 percentages is not just their sum
    ;
    ; Returns:Float
    ;
    ; History:
    ;  2-Aug-2005	JW			SR12988: Simplified
    ; 23-Jun-2005	Paul K		Created
    ;-------------------------------------------------------------------------------
	quit pfltAmt1 + pfltAmt2 - (pfltAmt1*pfltAmt2/100)
	
	/*
	new fltAmt
	
	set pfltAmt1=pfltAmt1/100
	set pfltAmt2=pfltAmt2/100
	
	set fltAmt=(1-pfltAmt1)*(1-pfltAmt2)
	set fltAmt=1-fltAmt
	set fltAmt=fltAmt*100
	quit fltAmt
	*/
	
	
ONOFF	;SWITCH ON OR OFF
	NEW PERCENT,AMOUNT,SEQ,DISCOUNT
	SET YKEY=$GET(YKEY)
	IF YKEY'="" IF $DATA(^INAUFDISCOUNT(YM,YKEY)) DO
	. ;
	. IF YLFN=70 SET YINHALT=""  ;LINE ITEM DISCOUNT
	. IF YLFN=210 SET YINHALT=""  ;ADDITIONAL DISCOUNT DESCRIPTION
	. IF YLFN=71 DO   ;ORDER DISCOUNT PERCENT
	. . NEW PERCENT
	. . SET PERCENT=""
	. . SET SEQ="" FOR  SET SEQ=$ORDER(^INAUFDISCOUNT(YM,YKEY,SEQ)) QUIT:SEQ=""  DO
	. . . SET DISCOUNT=$GET(^INAUFDISCOUNT(YM,YKEY,SEQ,1))
	. . . IF $PIECE(DISCOUNT,Y,3)'="" SET PERCENT=PERCENT+$PIECE(DISCOUNT,Y,3)  ;PERCENT DISCOUNT
	. . . QUIT
	. . SET YINHALT=PERCENT
	. . QUIT
	. IF YLFN=211 DO   ;SPECIAL MARKUP
	. . NEW AMOUNT
	. . SET AMOUNT=""
	. . SET SEQ="" FOR  SET SEQ=$ORDER(^INAUFDISCOUNT(YM,YKEY,SEQ)) QUIT:SEQ=""  DO
	. . . SET DISCOUNT=$GET(^INAUFDISCOUNT(YM,YKEY,SEQ,1))
	. . . IF $PIECE(DISCOUNT,Y,3)'="" QUIT  ;PERCENT DISCOUNT PREFERRED
	. . . IF $PIECE(DISCOUNT,Y,4)'="" SET AMOUNT=AMOUNT+$PIECE(DISCOUNT,Y,4)  ;AMOUNT MARKUP
	. . . QUIT
	. . SET YINHALT=$$^WWWZAHL(AMOUNT*-1,0,2)  ;LINE ITEM DISCOUNT PERCENT
	. . QUIT
	. SET YHID=2  ;READONLY
	. QUIT
	QUIT
	
LINK	;LINK TO SUBFORM IF EXIST
	IF $GET(YKEY)'="" IF $DATA(^INAUFDISCOUNT(YM,YKEY)) QUIT
	;SET YQ=1,$PIECE(YA,Y,9)=""  ;DISABLE AND HIDE BUTTON
	QUIT
]]></Routine>
</Export>