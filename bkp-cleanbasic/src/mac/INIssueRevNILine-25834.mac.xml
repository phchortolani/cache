<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INIssueRevNILine" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INIssueRevNILine  ; Issue Reverse , No Issue


#include COMConst
#include INConst
#include WWWConst
#include COMSYS
#include COMDebug
#include MEDConst
#define FORMFldINIssueRevLineLotNo	    24
#define FORMFldINIssueRevLineExpiryDate	25
#define FORMFldINIssueRevLineBrand		26
#define FORMFldINIssueRevLineOwner		27
#define FORMFldINIssueRevLineProgram	28
#define FORMFldINIssueRevLineFromStorage 7
#define FORMFldINIssueRevLineToStorage	 8



UpdateContainer(pstrFld,&objGrid,&headYFELD,&headYMFELD)
	;-------------------------------------------------------------------------------
	; Checks whole grid, if a single record requires lot number or expiry,
	; show that column
	;		hide=$$YES or show=$$$NO
	; 
	; Called By : UpdateContainer^COMGridEdit31G  (see matching code in LoadGrid^INADJ)
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 18-Mar-2014	SCR		SESDF-1299: Created
	;-------------------------------------------------------------------------------
	new blnHideBrand,blnHideExpiry,blnHideFrom,blnHideLotNumber,blnHideOwner,blnHideProgram
	new blnHideTo,blnLocalStorage,blnLotNo,blnLotUnique,blnUseByDate,idFromLoc
	new idItem,idToLoc,intBrand,intOwner,intProgram,objFromLoc,objRevLine,objToLoc
	new strLineYKEY

	set blnHideLotNumber= $$$YES
	set blnHideExpiry	= $$$YES
	set blnHideBrand	= $$$YES
	set blnHideOwner	= $$$YES
	set blnHideProgram	= $$$YES
	set blnHideFrom		= $$$YES
	set blnHideTo		= $$$YES
	set idFromLoc		= $$$INIssueRevFromLocn(headYFELD)
	set idToLoc			= $$$INIssueRevToLocn(headYFELD)
	if idFromLoc'="" {
		set objFromLoc		= $get(^WWW0121(0,YM,idFromLoc,1))
	}
	
	set strLineYKEY = ""
	for {
		set strLineYKEY = $order(objGrid($$$GRIDYKEYContainer,strLineYKEY))
		quit:(strLineYKEY = "")
		continue:$data(^WWWDATEN(YM,$horolog,YUSER,"INIssueRevNILine","V","KILLEDRECORD",strLineYKEY))
		
		set objRevLine = $$$GRIDGetYFELD(strLineYKEY)
		set idItem     = $$$INIssueRevLineItemNo(objRevLine)
		if idItem'="" {
			do GetAttributes^INItemPolicyUtils(idItem,.blnLotNo,.blnUseByDate,.blnLotUnique,.intBrand,.intOwner,.intProgram,idFromLoc)
			if blnLotNo                         set blnHideLotNumber = $$$NO
			if blnUseByDate                     set blnHideExpiry    = $$$NO
			if (intBrand=1)   || (intBrand=3)   set blnHideBrand     = $$$NO
			if (intOwner=1)   || (intOwner=3)   set blnHideOwner     = $$$NO
			if (intProgram=1) || (intProgram=3) set blnHideProgram   = $$$NO
		}
	}
	if $$$INIssueRevReturnType(headYFELD)=2 {  ; Show From Storage if it is not a consumption location
		if idToLoc'="" {
			set objToLoc	= $get(^WWW0121(0,YM,idToLoc,1))
			if $$$WWW0121StorageLocn(objToLoc) {
				set blnHideTo	= $$$NO
			}
		}
	}
	
	set blnLocalStorage = $$$WWW0121StorageLocn(objFromLoc)
	if blnLocalStorage {
		set blnHideFrom		= $$$NO
	}

	$$$StartScript()
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineLotNo,blnHideLotNumber,YFORM)
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineExpiryDate,blnHideExpiry,YFORM)
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineBrand,blnHideBrand,YFORM)
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineOwner,blnHideOwner,YFORM)
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineProgram,blnHideProgram,YFORM)
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineFromStorage,blnHideFrom,YFORM)
	$$$GRIDHideShowColumns($$$FORMFldINIssueRevLineToStorage,blnHideTo,YFORM)
	$$$EndScript()
	
	quit
	
OnBeforeFormat(pYFIELDNAME,pYTEXT,pYFELD)
	;-------------------------------------------------------------------------------
	; Called By :
	; 	Form - GridEdit:INInssueRevNILine
	; 
	; Inputs : 
	;	pYFIELDNAME	- 	Browser Element ID
	;	pYTEXT 		- 	Value currently in element
	;	pYFELD		-	ANM INIssueLine Data record
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 18-Mar-2014	SCR		SESDF-1299: Created
	;-------------------------------------------------------------------------------
	new blnHideTo,blnLocalStorage,blnLotNo,blnLotUnique,blnUseByDate,idClassField
	new idFromLoc,idItem,idLoc,idToLoc,intBrand,intCol,intOwner,intProgram,intRow
	new objFromLoc,objHeader,objLine,objToLoc
	
	if $$$INIssueRevStatus($$$GRIDYFELDContainer) = 9 quit
	
	$$$GRIDSplitKey(pYFIELDNAME,intRow,intCol)
	set idClassField = $$$GetClassField(YFORM,intCol)
	
	set objLine		= $$$GRIDGetYFELD($$GetYKEY^COMGridEdit31Interface(intRow))
	set idItem		= $$$INIssueRevLineItemNo(objLine)
	set objHeader	= $$$GRIDYFELDContainer
	set idLoc		= $$$INADJSiteLocation(objHeader)

	do GetAttributes^INItemPolicyUtils(idItem,.blnLotNo,.blnUseByDate,.blnLotUnique,.intBrand,.intOwner,.intProgram,idLoc)
	; Lot Number
	;---------------------------------------
	if (idClassField = $$$FldINIssueRevLineLotNumber) {
		if 'blnLotNo {
			set YENABLED = $$$NO
			
		} else {
			set YMANDATORY = $$$YES
		}
		
	; Mark Expiry Date as Mandatory or R/O
	;---------------------------------------
	} elseif (idClassField = $$$FldINIssueRevLineExpirationDate) {
		if blnUseByDate {
			set YMANDATORY = $$$YES
		} else {
			set YENABLED   = $$$NO 
		}				
		
	; Mark Non-Zero Quantity as Mandatory
	;---------------------------------------
	} elseif (idClassField = $$$FldINIssueRevLineBrand) {
		if intBrand {
			set:intBrand=1 YMANDATORY = $$$YES
		} else {
			set YENABLED = $$$NO 
		}					
		
	} elseif (idClassField = $$$FldINIssueRevLineOwner) {
		if intOwner {
			set:intOwner=1 YMANDATORY = $$$YES
		} else {
			set YENABLED = $$$NO 
		}
				
	} elseif (idClassField = $$$FldINIssueRevLineProgram1) {
		if intProgram {
			set:intProgram=1 YMANDATORY = $$$YES
		} else {
			set YENABLED = $$$NO 
		}					
	} elseif (idClassField = $$$FldINIssueRevLineFromStorage) {
		set idFromLoc		= $$$INIssueRevFromLocn(objHeader)
		if idFromLoc'="" {
			set objFromLoc		= $get(^WWW0121(0,YM,idFromLoc,1))
			set blnLocalStorage = $$$WWW0121StorageLocn(objFromLoc)
			if blnLocalStorage {
				set YMANDATORY	= $$$YES
			}
		}
	} elseif (idClassField = $$$FldINIssueRevLineToStorage) {
		set idToLoc			= $$$INIssueRevToLocn(objHeader)
		if idToLoc'="" {
			set objToLoc	= $get(^WWW0121(0,YM,idToLoc,1))
			if $$$WWW0121StorageLocn(objToLoc) {
				set YMANDATORY	= $$$YES
			}
		}
	}
	quit
OnBlurItem(pidClassField,&pobjLine,pidKeys="",pGridRow)
	;-------------------------------------------------------------------------------
	; On Blur from Item
	; Calculate unit, and storage
	;
	; Called By : COMGridEdit31Events due to setting on form
	; 
	; Params:	pidClassField	- field id
	;
	; ByRefs:	pobjLine		- current line object
	;			GROW 			- current Grid Row
	; Returns:
	;
	; History:
	; 20-Mar-2014	SCR		SESDF-1299:Created
	;-------------------------------------------------------------------------------
	new idItem,idRev,objRev,idToLocn,idFromLocn
	set idItem   = $$$INIssueRevLineItemNo(pobjLine)
	quit:idItem="" 
	set idRev = $$$KEY1(pidKeys)
	if idRev'="" {
		set objRev	= $get(^INIssueRev(YM,idRev,1))
	} else {
		set objRev	= ""
	}
	set idToLocn	= $$$INIssueRevToLocn(objRev)
	set idFromLocn	= $$$INIssueRevFromLocn(objRev)
	set $$$INIssueRevLineFromStorage(pobjLine)	= $$DefaultStkLoc^INARTUtilities(idFromLocn,idItem) 
	set $$$INIssueRevLineStockUnit(pobjLine)	= $$$INARTUnitofMeasure($get(^INART(YM,idItem,1)))
	quit
	

]]></Routine>
</Export>