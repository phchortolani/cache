<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARCompraEntregaLinha" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARCompraEntregaLinha
#include COMSYS
#include COMConst
#include VARConst

LoadGrid(pid="",pObjEntrega)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Função OnAfterDataFields^VARCompraEntrega.mac
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;------------------------------------------------------------------------------- 
	new YFORM, YKEY, YAUSWAHL, YFELD
	set YFORM = "VARCompraEntregaLinha"
	set YFELD = ""
	set YKEY  = $get(pid)
	
	set $$$COMGridEditParameterSharedForm(YAUSWAHL) = 1
	set $$$COMGridEditParameterGridName(YAUSWAHL)	= YFORM
	set $$$COMGridEditParameterContainer(YAUSWAHL)  = "VARCompraEntrega"
	
	// Se form master for só-leitura ou campo "Entrega Uniforme" for marcado, então grid é só-leitura:
	if ( ( $$$WWW120AuthorizationToModifyData(YVOR) = 5 ) || ( $$$VARCompraEntregaEntregauniforme(pObjEntrega) = 1) ) {
		set $$$COMGridEditParameterEnabled(YAUSWAHL) = 0
	}
	else {
		set $$$COMGridEditParameterEnabled(YAUSWAHL) = 1
	}
	
	$$$GRIDStart(YFORM,YKEY)
	
	quit
	
OnExecuteLinhaCompra()
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Verifica se a linha da compra já foi usada em outra linha da mesma entrega,
	;	retornando 1 se não há erro, e 0 se não há erro na linha da compra. 
	;
    ; Chamado por:
    ;	Core rule no. 1 do campo (1)-Linha da Ordem de Compra no form VARCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;-------------------------------------------------------------------------------
	new strStatus, idCompra, numEntregaCorrente, numSeqLinhaEntregaCorrente, numLinhaCompraCorrente,
		numSeqLinhaEntrega, objLinhaEntrega, numLinhaCompra
	
	set strStatus = $$$OK	
	set YKEY = $get(YKEY)
	
	set idCompra = $$$KEY1(YKEY)
	set numEntregaCorrente = $$$KEY2(YKEY)
	
	set numSeqLinhaEntregaCorrente = $$$KEY3(YKEY) 
	quit:(numSeqLinhaEntregaCorrente = "") strStatus
	
	set numLinhaCompraCorrente = $piece($get(YFELD),Y,1) 
	quit:(numLinhaCompraCorrente = "") strStatus
	
	//do AlertInScript^COMConst(idCompra_","_numEntregaCorrente_","_numSeqLinhaEntregaCorrente_","_numLinhaCompraCorrente)
 
	// Se a linha da compra já foi usada em outra linha desta mesma entrega:
	$$$Order4(^VARCompraEntregaLinha,YM,idCompra,numEntregaCorrente,numSeqLinhaEntrega)
		set objLinhaEntrega = $get(^VARCompraEntregaLinha(YM,idCompra,numEntregaCorrente,numSeqLinhaEntrega,1))		
		set numLinhaCompra = $piece(objLinhaEntrega,Y,1)
		if ((numSeqLinhaEntrega '= numSeqLinhaEntregaCorrente) && (numLinhaCompra = numLinhaCompraCorrente)) {
			set strStatus = $$$NO
			do AlertInScript^COMConst("Este produto já foi inserido nesta entrega. Veja linha Seq: "_numSeqLinhaEntrega_".")
			quit
		}
		quit:(strStatus = $$$NO)
	$$$End
	
	quit strStatus
	
OnBlurQuantidade(pYKEYEntregaLinha,pYFELDEntregaLinha)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (2)-Quantidade no form VARCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;------------------------------------------------------------------------------- 
	new updateGrid, idCompra, objEntregaLinha, arrEntregaLinhas, msg, idCompraLinha, 
		objCompraLinha, qtyCompra, percentual, percentualCalculado, qtdEntregaLinha, strStatus
	
	set updateGrid = $$$YES
	
	quit:($get(pYKEYEntregaLinha) = "") $$$YES
			
	set idCompra = $$$KEY1(YKEY)
	quit:($$$KEY3(YKEY) = "") ""
	
	do GetGridContents^COMGridEdit31Interface(.arrEntregaLinhas, "VARCompraEntregaLinha")
	set objEntregaLinha = $get(arrEntregaLinhas($p(YKEY,YKOMMA,1),$p(YKEY,YKOMMA,2),$p(YKEY,YKOMMA,3)))
	quit:($get(objEntregaLinha) = "") $$$YES
	quit:('$data(arrEntregaLinhas) ) $$$YES
	
	set idCompraLinha = $piece(objEntregaLinha,Y,1)
	if (idCompraLinha = "") {
		set $piece(pYFELDEntregaLinha,Y,2) = "" // Zera quantidade
		set $piece(pYFELDEntregaLinha,Y,8) = "" // Zera percentual
 
		set msg = "Primeiro escolha o produto."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_"7"_"',1,1,1);"
		$$$EndScript()
		
		quit ""
	}
		
	set objCompraLinha = $get(^INAUFP(YM,idCompra,idCompraLinha,1))
	set qtyCompra = $piece(objCompraLinha,Y,5)
	
	set percentual = $piece(objEntregaLinha,Y,8)
 
	set qtdEntregaLinha = $piece(objEntregaLinha,Y,2)
	if (+qtdEntregaLinha <= 0) {
		set $piece(pYFELDEntregaLinha,Y,2) = "" // Zera quantidade
		set $piece(pYFELDEntregaLinha,Y,8) = "" // Zera percentual
 
		set msg = "Quantidade deve ser maior que zero."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit $$$YES
	}
 
	// from On execute rule: ------------
	set strStatus = $$ChecarQuant(pYKEYEntregaLinha,pYFELDEntregaLinha)
	
	if ($$$ISERR(strStatus)){
		set $piece(pYFELDEntregaLinha,Y,2) = "" // Zera quantidade
		set $piece(pYFELDEntregaLinha,Y,8) = "" // Zera percentual
 
		set msg	= $piece(strStatus,Y,2)
		do AlertInScript^COMConst(msg)
 
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit $$$NO
	}
	// ----------------- 
 
	// Altera o campo de percentual:
	set percentualCalculado = (qtdEntregaLinha / qtyCompra) * 100
	
	if (percentual '= percentualCalculado) {
		set $piece(pYFELDEntregaLinha,Y,8) = percentualCalculado
	}	
	set arrEntregaLinhas($p(YKEY,YKOMMA,1),$p(YKEY,YKOMMA,2),$p(YKEY,YKOMMA,3)) = objEntregaLinha
		
	quit $$$YES
	

OnBlurPerc(pYKEYEntregaLinha,pidRow,&pYFELD)
	;-------------------------------------------------------------------------------
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (8)-Percentual no form VARCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;------------------------------------------------------------------------------- 
	new updateGrid, idCompra, objEntregaLinha, idCompraLinha, msg, objCompraLinha, arrEntregaLinhas,
		qtyCompra, percentual, percentualCalculado, qtdEntregaLinha, strStatus
	
	set updateGrid = $$$YES
	
	quit:($get(pYKEYEntregaLinha) = "") ""
	
	set idCompra = $$$KEY1(YKEY)
	quit:($$$KEY3(YKEY) = "") ""
	
	do GetGridContents^COMGridEdit31Interface(.arrEntregaLinhas, "VARCompraEntregaLinha")
	set objEntregaLinha = $get(arrEntregaLinhas($p(YKEY,YKOMMA,1),$p(YKEY,YKOMMA,2),$p(YKEY,YKOMMA,3)))
	quit:($get(objEntregaLinha) = "") ""
	quit:('$data(arrEntregaLinhas) ) ""	
	
	set idCompraLinha = $piece(objEntregaLinha,Y,1)
	if ( idCompraLinha = "") {
		set $piece(pYFELD,Y,8) = "" // Zera percentual
		set $piece(pYFELD,Y,2) = "" // Zera quantidade
 
		set msg = "Primeiro escolha o produto."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_"7"_"',1,1,1);"
		$$$EndScript()
		
		quit ""
	}	
	
	set objCompraLinha = $get(^INAUFP(YM,idCompra,idCompraLinha,1))
	set qtyCompra = $piece(objCompraLinha,Y,5)
	
	set percentual = $piece(objEntregaLinha,Y,8)
 	if (+percentual <= 0) || (+percentual > 100) {
		set $piece(pYFELD,Y,8) = "" // Zera percentual
		set $piece(pYFELD,Y,2) = "" // Zera quantidade
 
		set msg = "Percentual deve ser maior que zero e menor que 100."
		do AlertInScript^COMConst(msg)
		
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit ""
	}	
 
	set qtdEntregaLinha = $NORMALIZE(qtyCompra * (percentual/100),0) // Arredondando
	if (qtdEntregaLinha = 0) { 
		set qtdEntregaLinha = 1 
	}

	if ( qtdEntregaLinha < 0 ) { 
		set qtdEntregaLinha = 0 
	}
 
	set $piece(pYFELD,Y,2)=qtdEntregaLinha
	set $piece(objEntregaLinha,Y,2) = qtdEntregaLinha
 
	set strStatus = $$ChecarQuant(pYKEYEntregaLinha,pYFELD)
	
	if ($$$ISERR(strStatus)) {
		set msg = "O percentual de "_$piece(pYFELD,Y,8)_"% causaria esta quantidade em excesso. Digite um percentual menor."
		set $piece(pYFELD,Y,8) = "" // Zera percentual
		set $piece(pYFELD,Y,2) = "" // Zera quantidade
 
		set msg = $piece(strStatus,Y,2)_YCR_msg
		do AlertInScript^COMConst(msg)
 
		$$$StartScript()
		write "moveFocus('tdY"_GROW_"_"_GCOL_"',1,1,1);"
		$$$EndScript()
		
		quit $$$NO
	}
		
	set percentualCalculado = (qtdEntregaLinha / qtyCompra) * 100
	if (percentual '= percentualCalculado) {
		
		set $piece(objEntregaLinha,Y,8) = percentualCalculado
		set $piece(pYFELD,Y,8) = percentualCalculado
 
		set msg = 	"Você digitou um percentual de "_$fnumber(percentual,".")_"%."_$char(10)_
					"Este percentual corresponde a uma quantidade de "_$fnumber((qtyCompra * (percentual/100)),".")_"."_$char(10)_
					"Esta quantidade será arredondada para "_$fnumber(qtdEntregaLinha,".")_"."_$char(10)_
					"Isto causará a alteração do percentual para "_$fnumber(percentualCalculado,".")_"%."
		do AlertInScript^COMConst(msg)
	}
	
	quit percentualCalculado


ChecarQuant(pYKEYEntregaLinha,pYFELDEntregaLinha)
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Verifica se a quantidade nas diversas entregas não excede a quantidade da compra.
	;	Retorna 1 se não houver erro e 0 se houver erro na quantidade da linha da entrega.
	;
    ; Chamado por:
    ;	CriaEntregaUniforme^VARCompraEntrega.mac
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;------------------------------------------------------------------------------- 
	new strStatus, idCompra, numEntregaCorrente, numSeqLinhaEntregaCorrente, 
		numLinhaCompraCorrente, qtdLinhaEntregaCorrente, qtdOutrasEntregas, numEntrega,
		numSeqLinhaEntrega, objLinhaEntrega, numLinhaCompra, qtd, objCompraLinha,
		qtdLinhaCompra, idProduto, restoAEntregar
	
	set strStatus = $$$OK
		
	set idCompra = $piece($get(pYKEYEntregaLinha),YKOMMA,1)
	set numEntregaCorrente = $piece($get(pYKEYEntregaLinha),YKOMMA,2)
	set numSeqLinhaEntregaCorrente = $piece($get(pYKEYEntregaLinha),YKOMMA,3)
	
	set numLinhaCompraCorrente = $piece($get(pYFELDEntregaLinha),Y,1)
	if (numLinhaCompraCorrente = "") {
		set strStatus = "0~Não foi possível determinar o número da linha da compra corrente."
		quit strStatus
	}
	
	set qtdLinhaEntregaCorrente = $piece($get(pYFELDEntregaLinha),Y,2)
	
	//do AlertInScript^COMConst(idCompra_","_numEntregaCorrente_","_numSeqLinhaEntregaCorrente_","_numLinhaCompraCorrente)
 
	// Quantidade em outras entregas:
	set qtdOutrasEntregas = 0
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARCompraEntregaLinha(YM,idCompra,numEntrega))
		quit:(numEntrega = "")
		
		set numSeqLinhaEntrega = ""
		for {
			set numSeqLinhaEntrega = $order(^VARCompraEntregaLinha(YM,idCompra,numEntrega,numSeqLinhaEntrega))
			quit:(numSeqLinhaEntrega = "")
			
			set objLinhaEntrega = $get(^VARCompraEntregaLinha(YM,idCompra,numEntrega,numSeqLinhaEntrega,1))
			set numLinhaCompra = $piece(objLinhaEntrega,Y,1)
			set qtd = $piece(objLinhaEntrega,Y,2)
			
			if ((numEntrega = numEntregaCorrente) && (numSeqLinhaEntrega = numSeqLinhaEntregaCorrente)) {
				continue
			}
			if (numLinhaCompra = numLinhaCompraCorrente)  {
				set qtdOutrasEntregas = qtdOutrasEntregas + qtd
			}
		}
	}
		
	set objCompraLinha = $get(^INAUFP(YM,idCompra,numLinhaCompraCorrente,1))
	set qtdLinhaCompra = $piece(objCompraLinha,Y,5)
	set idProduto = $piece(objCompraLinha,Y,4)
	set restoAEntregar = qtdLinhaCompra - qtdOutrasEntregas
	
	//do AlertInScript^COMConst(qtdLinhaCompra_","_qtdOutrasEntregas_","_restoAEntregar)
	
	if (qtdLinhaEntregaCorrente > restoAEntregar) {
		set strStatus = "0"_Y_"Não é possível definir entrega para o produto "_idProduto_", pois a quantidade na compra ("_qtdLinhaCompra_") será excedida pela quantidade nas entregas ("_(qtdOutrasEntregas + qtdLinhaEntregaCorrente)_")."_Y_(qtdOutrasEntregas + qtdLinhaEntregaCorrente)_Y_qtdLinhaCompra_Y_idProduto 
	}
	else {
 		set strStatus = $$$YES
	}
	
	quit strStatus
	

OnBlurProduto(pintCol,pintRow,&pobjYFELD)
	;-------------------------------------------------------------------------------
	; Descrição:
	;	Retorna o no. do seq. da linha da compra, a partir do código do produto.
	;
    ; Chamado por:
    ;	Campo ExecuteOnBlur do campo (7)-Produto no form VARCompraEntregaLinha.
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;------------------------------------------------------------------------------		
	new idProduto, seqCompra
	set idProduto = $piece(pobjYFELD,Y,7)
	quit:(idProduto = "")
	
	set seqCompra = $piece($get(^COMTempList(YM,YBED,"VARTempCompraLinha",idProduto,1)),Y,2)
	
	set $piece(pobjYFELD,Y,1) = seqCompra
	
	quit
	
	
GetQuantEntr(pidCompra,pidCompraLinha)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna a quantidade de entregas para a linha da compra informada.
    ;
    ; Chamado por:
    ;	Função CriaEntregaUniforme^VARCompraEntrega.mac
    ; 
    ; Histórico:
    ; 	13-Jul-2010		Criado
    ;------------------------------------------------------------------------------- 
	new qtdEntregas, numEntrega, numLinhaEntrega, objLinhaEntrega, qtd
	
	set qtdEntregas = 0
 
	if ($get(pidCompra) = "") || ($get(pidCompraLinha) = "") {
		quit 0
	}
 	
	set numEntrega = ""
	for {
		set numEntrega = $order(^VARCompraEntregaLinhas(YM,1,$$^WWWUMLAU(pidCompra,1),pidCompraLinha,numEntrega))
		quit:(numEntrega = "")
		
		set numLinhaEntrega = ""
		for {
			set numLinhaEntrega = $order(^VARCompraEntregaLinhas(YM,1,$$^WWWUMLAU(pidCompra,1),pidCompraLinha,numEntrega,numLinhaEntrega))
			quit:(numLinhaEntrega = "")
									
			set objLinhaEntrega = $get(^VARCompraEntregaLinha(YM,pidCompra,numEntrega,numLinhaEntrega,1))
			
			set qtd = $piece(objLinhaEntrega,Y,2)
			
			set qtdEntregas = qtdEntregas + qtd
		}
	}

	quit qtdEntregas

GetDataPrevista(pidCompra, pidCompraLinha)
	;-------------------------------------------------------------------------------
    ; Descrição:
    ;	Retorna a data prevista de entrega para a linha da compra informada.
    ;
    ; Histórico:
    ; 	08-Sep-2011		Criado
    ;------------------------------------------------------------------------------- 
	new dataEmpenho, objEntrega, numLinhaEntrega, objLinhaEntrega, qtd

	set dataEmpenho = $$getDataEntregaEmpenho^VARCompra(pidCompra)
	if ($length(dataEmpenho) = 0) quit ""
 
	set objEntrega   = $get(^VARCompraEntrega(YM, pidCompra, pidCompraLinha, 1))
	set prazoEntrega = $$$VARCompraEntregaPrazodeEntrega(objEntrega)

	quit +dataEmpenho + prazoEntrega

]]></Routine>
</Export>