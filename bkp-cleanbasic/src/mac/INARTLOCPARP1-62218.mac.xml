<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INARTLOCPARP1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INARTLOCPARP1
 
Refresh
	;-------------------------------------------------------------------------------
	; Description of Function :
	;	If the location changes (P1) reset the storage lists (D6-D10) to match
	;	the storage places for the new location.
	;	
	; Inputs : 
	;
	; ByRef :
	;	YINHALT = Location
	;	YLFN    = DATEN/PRIMÃ„RFELDNUMMER
	;	YFELD	= Current data
	;
	; Returns :
	;	%TXT(1)
	;
	; History :
	; 18-Oct-2007	GRF		SR15563: copied from INLPP1
	;-------------------------------------------------------------------------------
	new idStorage,loop,strRemark
		
	;  6 Default On Hand Storage
	;  7 Default Receipt Storage
	;  8 Default Despatch Storage
	;  9 Default Missing Storage
	; 10 Default Damaged Storage
	
	set YFELD = $get(^WWWDATEN(YM,+$horolog,YUSER,YFORM,YART,1))  ;data record 
	set %TXT(1)=""
	for loop=6:1:10 {
		set %TXT(1)=%TXT(1)_"#Y"_YFORM_ "D"_loop_"~REMOVE"  ; Clear all options
		set %TXT(1)=%TXT(1)_"#Y"_YFORM_ "D"_loop_"~~ ~0"    ; Clear selected entry?
	}
	
	; Repopulate
	if YINHALT'="" {
		set idStorage = ""
		for loop=1:1 {
			set idStorage = $order(^INLP(YM,YINHALT,idStorage))
			quit:idStorage=""
			
			set strRemark = $extract($piece($get(^INLP(YM,YINHALT,idStorage,1)),Y,10),1,20)
			set %TXT(1)=%TXT(1)_"#Y"_YFORM_ "D6~"_idStorage_"~"_idStorage_" - "_strRemark_"~"_loop
			set %TXT(1)=%TXT(1)_"#Y"_YFORM_ "D7~"_idStorage_"~"_idStorage_" - "_strRemark_"~"_loop
			set %TXT(1)=%TXT(1)_"#Y"_YFORM_ "D8~"_idStorage_"~"_idStorage_" - "_strRemark_"~"_loop
			set %TXT(1)=%TXT(1)_"#Y"_YFORM_ "D9~"_idStorage_"~"_idStorage_" - "_strRemark_"~"_loop
			set %TXT(1)=%TXT(1)_"#Y"_YFORM_"D10~"_idStorage_"~"_idStorage_" - "_strRemark_"~"_loop
		}
	}
	
	quit
	
]]></Routine>
</Export>