<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFKOST" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFKOST(AUFTRAG,POSITION)
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		AUFTRAGS HERSTELLUNGSKOSTEN
	;	DIESES PROGRAMM ERRECHNET DIE SELBSTKOSTEN EINER AUFTRAGSPOSITION AUS PRODUKTION UND EK
	;	This program calculates the first cost of an order item by production and Planned Cost
	;
	; Called By :
	; 		Routines :	INARTK
	; 					INAUF
	; 					INAUFANLAGE
	; 					INAUFP
	; 					INAUFPOS
	; 					INAUFPTT
	; 					INAUFWH
	; 					INAUFWHPICK
	; 					INTAGES1
	; 
	; Inputs :	idOrder
	; 			idOrderLine
	;
	;
	; ByRef :
	;
	;
	; Returns : SELBSTKOST (First cost)
	;
	;
	; History :
	; 12-Sep-2006	GRF		SR15107: reverted changes relating to Receive Qty since no longer
	; 						using D47 to distribute shipping costs.
	; 26-Sep-2006	GRF		SR15107: use QtyRecvd, objINERECH1
	; 22-Sep-2006	GRF		SR15107: Doco; remove unnecessary KOST1 settings
	; 20-Sep-2006	GRF		Doco; quits; split long line - defining variables for
	; 						repeated entries
	; 16-Aug-2005	GRF		SR13142 : Standard Calculation
	; 08-Aug-2005	GRF		SR13142 : Doco; use /3600
	; 05.10.2000	DT
	;-------------------------------------------------------------------------------
	new DAUER,EINHEIT,EINHEIT1,GEWICHT,KOST,KOST1,KOST2,KOST3
	new LOS,MachineCost,MachineRate,MASCHINE,MENGE,MENGENFACTOR
	new objINERECH1,objINMASCH,PERSONEN,PROTOKOLL,QtyRecvd,RUEST,RWERT
	new SELBSTKOST,SELBSTKOSTALT,ServiceOverhead,SKOST,WG,WorkerRate,YFELD,YKALK
	
	SET SELBSTKOST = 0
	SET RWERT      = 0  ;RÜSTWERT  ; Preparation Cost
	
	QUIT:$GET(AUFTRAG)="" SELBSTKOST
	QUIT:$GET(POSITION)="" SELBSTKOST
	
	SET YFELD=$GET(^INAUFP(YM,AUFTRAG,POSITION,1))   ;ARTIKEL DATENSATZ ;item data record 
	IF YFELD="" QUIT SELBSTKOST                                      ;-+-+-+-+-+-+-+- EXIT POINT
	
	;-------------------------------------------------------------------------------
	
	;+++++++++++++++++++++++++++++++++++++++
	;  YFELD		objINAUFP		OrderLine
	;  				objINMASCH		Machine
	;+++++++++++++++++++++++++++++++++++++++
	
	
	;PREISPROTOKOLLIERUNG IN DEN EINZELNEN UNTERSCHRITTEN, ULM,20.03.03,
	SET PROTOKOLL=""
	;SET PROTOKOLL=$$^WWWDATE(+$H)  ;DATUM ;WEM;04.06.2003;AUSKOMMENTIERT, DA DATUM IN DER KLASSE GESPEICHERT WIRD
	
	;		D47			$$$INAUFPCost()
	;		D254		$$$INAUFPNoRecalculationOfCostAllo()
	SET SELBSTKOSTALT=$PIECE(YFELD,Y,47)  ;ALTER PREIS ;old price 
	;KEINE NEUBERECHNUNG DER HERSTELLKOSTEN ;no the
	IF $PIECE(YFELD,Y,254)=1 SET KOST=+SELBSTKOSTALT QUIT KOST       ;-+-+-+-+-+-+-+- EXIT POINT
	
	SET EINHEIT = $PIECE(YFELD,Y,40)
	SET MENGE   =+$PIECE(YFELD,Y,5)  ;MENGE WEGEN RÜSTZEIT ;quantity because of setup time
	IF MENGE=0 SET MENGE=1
	
	; Hr/Min/IndustryMin
	IF $DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO
	. IF EINHEIT=2  SET MENGE=1
	. IF EINHEIT=10 SET MENGE=1
	. IF EINHEIT=11 SET MENGE=1
	
	;SET EINHEIT1=""
	;AUS TYBD;6,2,2005/ NEU INQTYUNIT
	;SET MENGENFACTOR=""
	;IF EINHEIT'="" SET EINHEIT1=$PIECE($GET(^WWW101(0,"EINHEIT",SPRACHE,EINHEIT,1)),Y,1)
	;IF $FIND(EINHEIT1,"/")  IF +$PIECE(EINHEIT1,"/",2)'=0 IF MENGE>1 SET MENGE=$JUSTIFY(MENGE/$PIECE(EINHEIT1,"/",2),0,2)  ;WENN Z.B. MENGENEINHEIT= /100 ;when e.g. 
	;IF '$FIND(EINHEIT1,"/") IF +$PIECE(YFELD,Y,138)'=0    IF MENGE>1 SET MENGE=$JUSTIFY(MENGE/$PIECE(YFELD,Y,138),0,0)     ;WENN Z.B. PEISEINHEIT JE 100  ;TYBD 15.09.2002 ;when e.g. once 
	
	;KALKULATIONSVORGABEN AUS BETRIEBSDATEI SUCHEN ;out of seek 
	SET BETR=$PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,6)    ;BETRIEB
	IF $GET(BETR)="" SET BETR=$GET(YLOCATION)
	IF $GET(BETR)="" SET BETR=$ORDER(^INKALK(YM,""))   ;VORGABE BETRIEB ;default BETRIEB 
	
	;WARENGRUPPE SUCHEN ; Item Group
	SET WG=$PIECE(YFELD,Y,30)
	IF WG="" SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)
	IF WG'="" IF '$DATA(^INKALK(YM,BETR,WG)) SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)   ;DFLT WARENGRUPPE = 1.
	
	SET YKALK=""
	IF BETR'="" IF WG'="" DO 
	. SET YKALK=$GET(^INKALK(YM,BETR,WG,1))
	
	IF BETR'="" IF $PIECE(YFELD,Y,4)'="" IF $DATA(^INKALKART(YM,BETR,$PIECE(YFELD,Y,4))) DO
	. SET YKALK=$GET(^INKALKART(YM,BETR,$PIECE(YFELD,Y,4),1))
	
	SET PERSONEN=+$PIECE(YFELD,Y,49)  ;ANZAHL DER PERSONEN DIE PRODUZIEREN ;Number the who produce 
	IF PERSONEN=0 SET PERSONEN=1
	
	SET KOST  = 0
	SET KOST1 = 0
	SET KOST2 = 0
	SET KOST3 = 0
	
	;	D4		$$$INAUFPItemNumber()
	;	D43		$$$INAUFPWeightInKg()
	;	D70		$$$INAUFPSetupTime()
	SET ARTIKEL = $PIECE(YFELD,Y,4)
	SET RUEST   = $PIECE(YFELD,Y,70)		; Setup time in order line
	SET RUEST   = $$^INARTRUEST(ARTIKEL)	; over-ridden by setup time in item masterfile
											; FIXME : Why?  <GRF> Note: unless YARTX set elsewhere, this returns zero.
	SET GEWICHT = $PIECE(YFELD,Y,43)
	
	;SET DAUER=$PIECE(YFELD,Y,45)
	SET DAUER=0
	IF $PIECE(YFELD,Y,165)'="" SET DAUER=$$^INPROSTEMP0($PIECE(YFELD,Y,165))  ;GESTEMPELTE ZEIT ;time 
	IF +DAUER=0 SET DAUER=$PIECE(YFELD,Y,45)  ;GEPLANTE FERTIGUNGSZEIT
	
	;---------------------------------------
	; 32875 : "Machine Cost"
	; 32082 : "Item Group"
	;--------------------------------------- 
	; SR13142 : replacing code start
	;ERRECHNEN SELBSTKOSTEN ;total production costs 
	IF '$DATA(^INAUFPT(YM,AUFTRAG,POSITION)) {                ;NUR, WENN ARTIKEL KEINE TEILE HAT WIRD GERECHNET
	;	KALKULIEREN DER ZEITEN UND DES MASCHINENEINSATZES ;calculate the And 
		SET MASCHINE = $$$INARTMachine(YFELD)                       ; D57
		IF MASCHINE'="" {
			set objINMASCH  = $get(^INMASCH(YM,MASCHINE,1))
			set MachineRate     = $$$INMASCHMachineCostPerHour(objINMASCH)
			set WorkerRate      = $$$INMASCHWorkerCostPerHour(objINMASCH)
			set ServiceOverhead = $$$INMASCHServiceOverhead(objINMASCH)
			
			if (MachineRate+WorkerRate)=0 {
				set MachineRate=$$$INMASCHProductionCostPerHour(objINMASCH)
				set WorkerRate=0
			}
			set MachineCost = ($justify((MachineRate+ServiceOverhead+(WorkerRate*PERSONEN))*DAUER/3600,0,2)*MENGE)
			set KOST = KOST + MachineCost
		;	                                    "Machine Cost  / "   Machine = MachineCost
			SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(32875,,1)_" / "_$PIECE(objINMASCH,Y,2)_" = "_$$^WWWZAHL(MachineCost)
		} else {
			SET WERT=$$$INKALKAverageLaborCost(YKALK)
			if +DAUER'=0 {
				set MachineCost = ($justify(WERT*PERSONEN*DAUER/3600,0,2)*MENGE)  ;SEKUNDENKOSTEN*SEKUNDENDAUER*MENGE
				SET KOST = KOST+MachineCost
		;		                                   "Machine Cost  / Item Group"  Group = MachineCost
				SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(32875,,1)_" / "_$$^WWWTEXT(32082,,1)_" "_WG_" = "_$$^WWWZAHL(MachineCost)
			}
		}
	}
	; SR13142 : replacing code end
	;-------------------------------------------------------------------------------
	
	/*vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START  replaced code
	;IF '$DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO  ;NUR, WENN ARTIKEL KEINE TEILE HAT WIRD GERECHNET
	. NEW YI,TEIL,EK,ZEIT,WERT,POSITION1
	. ;KALKULIEREN DER ZEITEN UND DES MASCHINENEINSATZES ;calculate the And 
	. SET MASCHINE=$PIECE(YFELD,Y,57)
	. IF MASCHINE'="" DO
	. . SET WERT(1)=$PIECE($GET(^INMASCH(YM,MASCHINE,1)),Y,31)/3600  ;WERT MASCHINE PRO SEKUNDE ;machine rate per second 
	. . SET WERT(2)=$PIECE($GET(^INMASCH(YM,MASCHINE,1)),Y,32)/3600  ;WERT BEDIENER PRO SEKUNDE ;worker rate per second 
	. . ;	D2		$$$INMASCHProductionCostPerHour()
	. . IF +(WERT(1)+WERT(2))=0 SET WERT(1)=0,WERT(2)=$PIECE($GET(^INMASCH(YM,MASCHINE,1)),Y,2)/3600  ;WERT GESAMT PRO SEKUNDE ;total rate per second 
	. . SET KOST=KOST+($JUSTIFY(WERT(1)*DAUER,0,2)*MENGE)  ;SEKUNDENKOSTEN*SEKUNDENDAUER*MENGE
	. . SET KOST=KOST+($JUSTIFY(WERT(2)*DAUER,0,2)*MENGE*PERSONEN)  ;SEKUNDENKOSTEN*SEKUNDENDAUER*MENGE*ANZAHL BEDIENER
	. . ;                            MASCHINENKOSTEN                        MASCHINE                                                     MASCHINENKOSTEN
	. . SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(32875,,1)_" / "_$PIECE($GET(^INMASCH(YM,MASCHINE,1)),Y,2)_" = "_$$^WWWZAHL(($JUSTIFY((WERT(1))*DAUER,0,2)*MENGE)+($JUSTIFY(WERT(2)*DAUER,0,2)*MENGE*PERSONEN))  ;ULM
	. ;
	. IF MASCHINE="" DO
	. . SET WERT=$PIECE(YKALK,Y,1)/3600
	. . SET KOST=KOST+($JUSTIFY(WERT*DAUER,0,2)*MENGE*PERSONEN)  ;SEKUNDENKOSTEN*SEKUNDENDAUER*MENGE
	. . ;                            MASCHINENKOSTEN            WARENGRUPPE                                 MASCHINENKOSTEN
	. . SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(32875,,1)_" / "_$$^WWWTEXT(32082,,1)_" "_WG_" = "_$$^WWWZAHL($JUSTIFY(WERT*DAUER,0,2)*MENGE*PERSONEN)  ;ULM
	. ;
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv DISABLED BLOCK START
	. ;IF $PIECE(YFELD,Y,70)'="" DO  ;RÜSTZEIT       
	. ;. SET WERT=$PIECE(YKALK,Y,1)/3600
	. ;. SET KOST=KOST+(WERT*$PIECE(YFELD,Y,70)*PERSONEN)
	. ;IF $PIECE(YFELD,Y,73)'="" DO  ;RUHEZEIT
	. ;. SET WERT=$PIECE(YKALK,Y,1)/3600
	. ;. SET KOST=KOST+(WERT*$PIECE(YFELD,Y,73)*MENGE*PERSONEN)
	. ;IF $PIECE(YFELD,Y,74)'="" DO  ;WERKZEUGKOSTEN       ; Doesn't consider lot size
	. ;. SET WERT=$PIECE(YFELD,Y,74)
	. ;. SET KOST=KOST+(WERT*MENGE)
	. ;IF $PIECE(YFELD,Y,75)'="" DO  ;EXTRAKOSTEN          ; Doesn't consider lot size
	. ;. SET WERT=$PIECE(YFELD,Y,75)
	. ;. SET KOST=KOST+(WERT*MENGE)
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END
	;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ DISABLED BLOCK END	replaced code */
	
	SET SKOST=+$$$INVORGDefaultCostType($GET(^INVORG(YM,YM,1)))      ; D15
	IF SKOST=0 SET SKOST=1
	
	;--------------------------------------- 
	; 33335 : "Cost From Stock" (first - INWE)           ;EK AUS LAGERBESTAND
	;--------------------------------------- 
	; DEPRECATED - with removal of INWEAUF*/INWE     Replace with INRECSummary/INRECLine & V2? SR16361
	IF '$DATA(^INWEAUF(YM,AUFTRAG,POSITION)) IF '$DATA(^INWEAUFALT(YM,AUFTRAG,POSITION)) IF '$DATA(^INAUFPT(YM,AUFTRAG,POSITION)) IF SKOST=5 IF ARTIKEL'="" DO  ;EK AUS LAGERBESTAND ;Planned Cost out of 
	. NEW BET,LAP,WED,WEN
	. SET BET = ""
	. FOR  SET BET = $ORDER(^INWE(YM,ARTIKEL,BET)) QUIT:BET=""  DO
	. . SET LAP = ""
	. . FOR  SET LAP = $ORDER(^INWE(YM,ARTIKEL,BET,LAP)) QUIT:LAP=""  DO
	. . . SET WED = ""
	. . . FOR  SET WED = $ORDER(^INWE(YM,ARTIKEL,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . SET WEN = $GET(^INWE(YM,ARTIKEL,BET,LAP,WED,1))
	. . . . IF $PIECE(WEN,Y,8)>KOST3 SET KOST3=$PIECE(WEN,Y,8)     ; D8 $$$INWEUnitPrice() or use $$$INRECLineEach()   SR16361
	. ;
	. SET KOST3 = KOST3*MENGE/$$^INQTYUNIT($PIECE(YFELD,Y,4))
	. IF KOST3>KOST SET KOST = KOST3 SET PROTOKOLL = PROTOKOLL_"|"_$$^WWWTEXT(33335,,1)_" = "_$$^WWWZAHL(KOST3)  ;WENN WARE TEUER, DANN ÜBERTRAGEN; ULM
	
	;--------------------------------------- 
	; 33344 : "Cost from Order Line"        ;NORMALE KOSTEN
	; 33345 : "Cost From Item"              ;AUS ARTIKELDATEN
	;--------------------------------------- 
	IF +KOST=0 SET KOST = $PIECE(YFELD,Y,47)
	IF +KOST=0 SET PROTOKOLL = PROTOKOLL_"|"_$$^WWWTEXT(33344,,1)_" = "_$$^WWWZAHL(KOST)
	IF +KOST=0 IF $PIECE(YFELD,Y,4)'="" DO          ; 20-Sep-2006 introduce new level
	. new idItem,objINART
	. set idItem    = $PIECE(YFELD,Y,4)
	. set objINART  = $GET(^INART(YM,idItem,1))
	. SET KOST      = $$^INARTKOST(idItem)-$PIECE(objINART,Y,74)-$PIECE(objINART,Y,75)*(MENGE/$$^INQTYUNIT(idItem))
	. SET PROTOKOLL = PROTOKOLL_"|"_$$^WWWTEXT(33345,,1)_" = "_$$^WWWZAHL(KOST)         ;TYBD;6,1,2004;QTYUNIT
	
	;vvvvv +++++ if/else INAUFPT
	
	SET KOST1=0  ;SELBSTKOSTEN AUS BESTELLUNG ;total production costs out of sales order 
	
	;--------------------------------------- 
	; 33346 : "Cost From Supplier/Item Terms"    ;EK DES LIEFERANTEN  AUS ARTIKELANLAGE
	;--------------------------------------- 
	IF '$DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO
	. ;SET KOST1=9999999999    ; 22-Sep-2006
	. DO
	. . SET EK(1)=$PIECE($GET(^INAUFPK(YM,AUFTRAG,POSITION,1)),Y,13)  ; EK aus ARTIKELLIEFERANTENKONDITION
	. . IF +EK(1)'=0 SET KOST1=EK(1) SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(33346,,1)_" = "_$$^WWWZAHL(KOST1)  ;
	;. ;
	;. IF KOST1=9999999999 SET KOST1=0  ;KEINEN GEFUNDEN    ; 22-Sep-2006
	
	;--------------------------------------- 
	; 32326/33275 : "Manufacturing Costs/Part"     ;ERRECHNEN SELBSTKOSTEN ;total production costs
	; Multiply Part Value by Finished Goods Qty and add to KOST
	;--------------------------------------- 
	IF $DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO 
	. NEW YI,TEIL,EK,ZEIT,WERT,POSITION1
	. SET KOST=0,RUEST=0,DAUER=0,GEWICHT=0  ;ERRECHNEN AUS TEILE ;out of 
	. ;SELBSTKOSTEN DER TEILE ;total production costs the 
	. SET TEIL=""
	. FOR  SET TEIL=$ORDER(^INAUFPT(YM,AUFTRAG,POSITION,TEIL)) QUIT:TEIL=""  IF $PIECE(TEIL,".",2)="" DO
	. . SET POSITION1=$GET(^INAUFPT(YM,AUFTRAG,POSITION,TEIL,1))    ;ARTIKELTEILEINFO                 Herstellkosten          Unterteil ;Production Costs Part 
	. . IF POSITION1'="" SET KOST=KOST+($PIECE(POSITION1,Y,47)*MENGE)  SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(32326,,1)_"/"_$$^WWWTEXT(33275,,1)_" "_$piece(POSITION1,Y,4)_" = "_$$^WWWZAHL($PIECE(POSITION1,Y,47)*MENGE)
	. . SET RUEST   = RUEST   +  $PIECE(POSITION1,Y,70)             ;RUESTZEIT
	. . SET GEWICHT = GEWICHT + ($PIECE(POSITION1,Y,43)*MENGE)      ;GEWICHT ;wt. 
	. . SET DAUER   = DAUER   + ($PIECE(POSITION1,Y,45)*MENGE)      ;AUSFÜHRUNGSZEIT ;execution time 
	
	;^^^^^ +++++ if/else INAUFPT end
	
	
	;--------------------------------------- 
	; 33347 : "Cost from Supplier/Item Terms Confirm."  ;EK AUS AB ARTIKEL-LIEFERANTENKONDITIONEN
	;--------------------------------------- 
	IF $DATA(^INAUFPA(YM,AUFTRAG,POSITION)) DO    ; EK DES LIEFERANTEN AB DER BESTELLUNG ;Planned Cost Confirm. the sales order 
	. SET KOST=0       ;BESTELLUNG LIEGT VOR ;sales order pre- 
	. NEW LIEF,EK,AB,MNG,MNG1,FCT,KOSTX
	. SET KOSTX=KOST1  ;FIS;08.09.04
	. SET LIEF(1)=""   ;BESTER LIEFERANT ;supplier        ; FIXME : Not used <GRF>
	. SET KOST1=9999999999
	. SET MNG1=+$PIECE($GET(^INAUFP(YM,AUFTRAG,POSITION,1)),Y,5)/$$^INQTYUNIT(,AUFTRAG,POSITION)  ;AUFTRAGSMENGE
	. SET LIEF=""
	. FOR  SET LIEF=$ORDER(^INAUFPA(YM,AUFTRAG,POSITION,LIEF)) QUIT:LIEF=""  DO
	. . SET AB=""
	. . FOR  SET AB=$ORDER(^INAUFPA(YM,AUFTRAG,POSITION,LIEF,AB)) QUIT:AB=""  DO
	. . . SET EK(1)=$PIECE($GET(^INAUFPA(YM,AUFTRAG,POSITION,LIEF,AB,1)),Y,13)
	. . . SET MNG=$PIECE($GET(^INAUFPA(YM,AUFTRAG,POSITION,LIEF,AB,1)),Y,16)/$$^INQTYUNIT(,AUFTRAG,POSITION)  ;AB-MENGE
	. . . IF +MNG=0 SET MNG=MNG1
	. . . SET FCT=$PIECE($GET(^INAUFPK(YM,AUFTRAG,POSITION,1)),Y,41)  ;UMRECHNUNGSFAKTOR
	. . . IF +FCT'=0 SET MNG=$JUSTIFY((MNG/FCT),0,2)
	. . . IF +EK(1)'=0 IF $JUSTIFY(MNG1,0,2)'>MNG SET KOST1=EK(1)
	. . . IF +EK(1)'=0 IF $JUSTIFY(MNG1,0,2)>MNG  SET KOST1=$JUSTIFY(((EK(1)/MNG)*MNG1),0,2)  ;FIS;26268;TEIL-AB;19.08.04
	. ;
	. IF KOST1'=9999999999 SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(33347,,1)_" = "_$$^WWWZAHL(KOST1)
	. IF KOST1=9999999999  SET KOST1=0      ;KEINEN GEFUNDEN
	. IF KOST1=0           SET KOST1=KOSTX  ;FIS;08.09.04
	
	;--------------------------------------- 
	; 33335 : "Cost From Stock" (second - INWEAUF)  ;EK AUS LAGERBESTAND
	;--------------------------------------- 
	; DEPRECATED - with removal of INWEAUF*     Replace with INRECSummary/INRECLine & V2? SR16361
	IF $DATA(^INWEAUF(YM,AUFTRAG,POSITION)) DO
	. NEW BET,LAP,WED,WEN
	. SET KOST3=0
	. SET BET=""
	. FOR  SET BET=$ORDER(^INWEAUF(YM,AUFTRAG,POSITION,BET)) QUIT:BET=""  DO
	. . SET LAP=""
	. . FOR  SET LAP=$ORDER(^INWEAUF(YM,AUFTRAG,POSITION,BET,LAP)) QUIT:LAP=""  DO
	. . . SET WED=""
	. . . FOR  SET WED=$ORDER(^INWEAUF(YM,AUFTRAG,POSITION,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . SET WEN=$GET(^INWEAUF(YM,AUFTRAG,POSITION,BET,LAP,WED,1))
	. . . . SET KOST3=KOST3+($PIECE(WEN,Y,8)*$PIECE(WEN,Y,4)/$$^INQTYUNIT($PIECE(YFELD,Y,4)))  ;FIS;27123;03.04.05
	. ;
	. IF KOST3'=0 SET KOST=KOST3 SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(33335,,1)_" = "_$$^WWWZAHL(KOST) 
	
	;--------------------------------------- 
	; 33335 : "Cost From Stock" (third - INWEAUFALT)  ;EK AUS LAGERBESTAND
	;--------------------------------------- 
	; DEPRECATED - with removal of INWEAUF*/INWE     Replace with INRECSummary/INRECLine & V2? SR16361
	IF $DATA(^INWEAUFALT(YM,AUFTRAG,POSITION)) DO
	. NEW BET,LAP,WED,WEN
	. SET KOST3=0
	. SET BET=""
	. FOR  SET BET=$ORDER(^INWEAUFALT(YM,AUFTRAG,POSITION,BET)) QUIT:BET=""  DO
	. . SET LAP=""
	. . FOR  SET LAP=$ORDER(^INWEAUFALT(YM,AUFTRAG,POSITION,BET,LAP)) QUIT:LAP=""  DO
	. . . SET WED=""
	. . . FOR  SET WED=$ORDER(^INWEAUFALT(YM,AUFTRAG,POSITION,BET,LAP,WED)) QUIT:WED=""  DO
	. . . . SET WEN=$GET(^INWEAUFALT(YM,AUFTRAG,POSITION,BET,LAP,WED,1))
	. . . . SET KOST3=KOST3+($PIECE(WEN,Y,8)*$PIECE(WEN,Y,4)/$$^INQTYUNIT($PIECE(YFELD,Y,4)))  ;FIS;27123;03.04.05
	. ;
	. IF KOST3'=0 SET KOST=KOST3 SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(33335,,1)_" = "_$$^WWWZAHL(KOST)
	
	;--------------------------------------- 
	; 33348 : "Cost from Invoice"     ;EK AUS EINGANGRECHNUNG
	;--------------------------------------- 
	IF $DATA(^INERECH1(YM,AUFTRAG,POSITION)) DO    ;EK DES LIEFERANTEN RECHNUNG DER BESTELLUNG ;Planned Cost tab the sales order 
	. SET KOST=0  ;BESTELLUNG LIEGT VOR ;ORDER IS PRESENT (copied without revising comment?)
	. NEW LIEF,EK,RECHN,MNG,MNG1,FCT,GOODSUP,KOSTX
	. SET KOSTX=KOST1  ;FIS;08.09.04
	. SET LIEF(1)=""   ;BESTER LIEFERANT ;supplier        ; FIXME : Not used <GRF> 
	. SET KOST1=9999999999
	. SET EK(1)=""
	. ; vvv  26-Sep-2006  Reverted 12-Sep-2006
	. ; If Received use that quantity, else continue to use ordered.
	. SET MNG(1) = $PIECE($GET(^INAUFP(YM,AUFTRAG,POSITION,1)),Y,5)/$$^INQTYUNIT(,AUFTRAG,POSITION)  ;AUFTRAGSMENGE
	. ;set QtyRecvd = $$$INAUFPIncomingGoodsQuantity($GET(^INAUFP(YM,AUFTRAG,POSITION,1)))    ; D109
	. ;if QtyRecvd'="" set MNG(1) = QtyRecvd/$$^INQTYUNIT(,AUFTRAG,POSITION)
	. ;if QtyRecvd=""  set MNG(1) = $$$INAUFPQuantity($GET(^INAUFP(YM,AUFTRAG,POSITION,1)))/$$^INQTYUNIT(,AUFTRAG,POSITION)  ; D5
	. ;^^^
	. SET MNG(2)=0     ;invoice quantity   ; RECHNUNGSMENGE
	. SET GOODSUP=""   ;GOODS SUPPLIER
	. SET LIEF=""
	. FOR  SET LIEF=$ORDER(^INERECH1(YM,AUFTRAG,POSITION,LIEF)) QUIT:LIEF=""  DO
	. . SET RECHN=""
	. . FOR  SET RECHN=$ORDER(^INERECH1(YM,AUFTRAG,POSITION,LIEF,RECHN)) QUIT:RECHN=""  DO
	. . . set objINERECH1 = $get(^INERECH1(YM,AUFTRAG,POSITION,LIEF,RECHN,1))
	. . . ;SET EK(1)=+$PIECE(objINERECH1,Y,13)
	. . . SET EK(1)  = EK(1)+$PIECE(objINERECH1,Y,13)                            ;FIS;22.07.04;26142
	. . . SET MNG    = $PIECE(objINERECH1,Y,16)/$$^INQTYUNIT(,AUFTRAG,POSITION)  ;RECHNUNGSMENGE
	. . . SET FCT    = $PIECE($GET(^INAUFPK(YM,AUFTRAG,POSITION,1)),Y,41)        ;UMRECHNUNGSFAKTOR
	. . . IF +FCT'=0 SET MNG = $JUSTIFY((MNG/FCT),0,2)
	. . . SET MNG(2) = MNG(2)+MNG
	. . . IF $PIECE(objINERECH1,Y,80)="" SET GOODSUP = LIEF
	. ;
	. IF GOODSUP="" SET EK(1)  = ""      ;NO INVOICE FROM GOODS SUPPLIER YET;08.09.04;FIS;25736
	. IF MNG(2)=0   SET MNG(2) = MNG(1)
	. ;
	. ;vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
	. ;  FIXME : SR15107 Comparing Invoiced to Ordered rather than Invoiced to Received? <GRF>  changed 26-Sep-2006
	. ;            May have been considering partial deliveries?  Unclear what logic
	. ;            should be and what data is present at this stage - investigating.
	. ;            If still have supplier invoices to be entered don't get correct calculation since INERECH1 is incomplete.
	. ;            i.e. Received but not invoiced affecting count but not in EK(1)
	. ;            Reverted to base on Order Qty for consistancy (? !!!)     <DEFERRED FOR PURCHASE ORDER REVIEW>
	. ;
	. ;IF +EK(1)'=0 IF MNG(1)'>MNG(2) SET KOST1=EK(1)
	. ;IF +EK(1)'=0 IF MNG(1)>MNG(2)  SET KOST1=EK(1)/MNG(2)*MNG(1)
	. ;
	. ;  Set KOST1 = Invoice Each * Received Qty
	. ;  *WAS* setting to total for original order quantity - may be a problem if associated costs are all based on original quantity.
	. if EK(1) set KOST1 = EK(1)/MNG(2)*MNG(1)                           ;   26-Sep-2006
	. ;
	. ;^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
	. ;
	. IF KOST1=9999999999 SET KOST1=0  ;KEINEN GEFUNDEN
	. IF EK(1)'="" SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(33348,,1)_"/"_LIEF_"/"_RECHN_" = "_$$^WWWZAHL(KOST1)
	. IF KOST1=0 SET KOST1=KOSTX       ;FIS;08.09.04
	
	IF +KOST1'=0 SET KOST=KOST1
	
	;IF KOST1=0 DO  ;WENN KEINE BESTELLUNG ;when no sales order 
	;. SET SKOST=+$PIECE($GET(^INVORG(YM,YM,1)),Y,15)
	;. IF SKOST=0 SET SKOST=1 ;table-mat 
	;. IF +KOST'=0 IF +KOST1=0 QUIT                  ;NUR EIGENFERTIGUNG VORHANDEN
	;. IF +KOST=0  IF +KOST1'=0 SET KOST=KOST1 QUIT  ;NUR BESTELLWARE VORHANDEN
	;. IF SKOST=1 QUIT                               ;WERT NACH EIGENFERTIGUNG
	;. IF SKOST=2 SET KOST=KOST1 QUIT                ;WERT NACH BESTELLWARE
	;. IF SKOST=3 SET:KOST1<KOST KOST=KOST1 QUIT     ;NACH NIEDRIGSTEM WERT
	;. IF SKOST=4 SET:KOST1>KOST KOST=KOST1 QUIT     ;NACH HÖCHSTEM WERT
	
	;--------------------------------------- 
	; 33349 : "Cost of Tool"   werkzeugKOSTEN
	; 32867 : "Extra Costs"    EXTRSKOSTEN
	;--------------------------------------- 
	DO       ;ZUSATZKOSTEN ;additional costs 
	. IF $PIECE(YFELD,Y,74)'="" DO  ;WERKZEUGKOSTEN
	. . SET WERT=$PIECE(YFELD,Y,74) SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(33349,,1)_" = "_$$^WWWZAHL(WERT)
	. . ;SET KOST=KOST+(WERT*MENGE) 
	. . SET KOST=KOST+WERT
	. ;
	. IF $PIECE(YFELD,Y,75)'="" DO  ;EXTRAKOSTEN
	. . SET WERT=$PIECE(YFELD,Y,75) SET PROTOKOLL=PROTOKOLL_"|"_$$^WWWTEXT(32867,,1)_" = "_$$^WWWZAHL(WERT)  
	. . ;SET KOST=KOST+(WERT*MENGE) 
	. . SET KOST=KOST+WERT
	
	;+++++++++++++++++++++++++++++++++++++++
	;  FIXME : Shouldn't we use the following value before YLOCATION?   <GRF>
	;             SET BETR=$PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,6)
	;          Also - May not *HAVE* a location "1"
	;+++++++++++++++++++++++++++++++++++++++
	
	DO  ;RUESTZEITEN   ; Preparation Times
	. SET RUEST=RUEST*$$^INPROLOS($PIECE(YFELD,Y,4),MENGE)
	. IF +RUEST'=0 SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,70)=RUEST
	. SET RWERT=0
	. DO
	. . NEW WG,BETR,STDWERT
	. . SET BETR=$GET(YLOCATION)
	. . IF BETR="" SET BETR=$ORDER(^INKALK(YM,""))
	. . IF BETR="" SET BETR=$ORDER(^WWW0121(0,YM,""))
	. . IF BETR="" SET BETR=1
	. . SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)
	. . IF WG="" SET WG=$ORDER(^INKALK(YM,BETR,""))
	. . IF WG="" SET WG=0
	. . SET STDWERT=$PIECE($GET(^INKALK(YM,BETR,WG,1)),Y,1)/3600
	. . SET RWERT=$JUSTIFY(RUEST*STDWERT,0,2)
	. ;
	. SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,71)=RWERT
	. SET $PIECE(YFELD,Y,71)=RWERT
	
	SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,45)=DAUER    ;AUSFÜHRUNGSZEIT DES ARTIKELS ;execution time 
	;SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,43)=GEWICHT  ;GEWICHT DES ARTIKELS
	IF $PIECE(YFELD,Y,4)'="" IF $PIECE($GET(^INART(YM,$PIECE(YFELD,Y,4),1)),Y,255)'=1 SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,43)=GEWICHT  ;GEWICHT DES ARTIKELS ;wt. 
	
	IF EINHEIT=10 IF $DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO  ;MINUTEN            ; minutes
	. IF DAUER SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,5)=DAUER\60            ; 20-Sep-2006
	
	IF EINHEIT=11 IF $DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO  ;INDUSTRIE MINUTEN  ;industry minutes
	. IF DAUER SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,5)=DAUER\100           ; 20-Sep-2006
	
	IF EINHEIT=2 IF $DATA(^INAUFPT(YM,AUFTRAG,POSITION)) DO   ;STUNDEN            ;hours 
	. IF DAUER SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,5)=DAUER\3600          ; 20-Sep-2006
	
	;--------------------------------------- 
	; 32326 : "Manufacturing Costs"  ;HERSTELLKOSTEN
	;--------------------------------------- 
	SET PROTOKOLL=PROTOKOLL_"||"_$$^WWWTEXT(32326,,1)_" = "_$$^WWWZAHL(SELBSTKOST+KOST)
	
	;;;DO KALK   ;VK NEURECHNEN
	;;;--> AUSGESCHALTET AM 14.09.04;FIS;26184;VK NICHT NEU RECHNEN
	;;;    (HAT OFFENBAR NIE FUNKTIONIERT, DENN ES WURDE NUR GERECHNET, WENN VK=0. DANN IST ABER DER AUFSCHLAG -100
	;;;     UND DER VK WIRD WIEDER MIT 0 BERECHNET, ALSO KEINE ÄNDERUNG)
	;--> BLEIBT IM GEGENSATZ ZUM ANGEBOT AUSGESCHALTET: 09.02.05;FIS;27283
	;     VK IM AUFTRAG WIRD NUR BEI ÄNDERUNG DES PREISKENNZEICHENS NEU GEÄNDERT, NICHT DURCH SPEICHERN !
	;     EINMALIGER AUFRUF IN DER INAUF BEI NEUANLAGE AUFTRAGSPOSITION
	
	
	SET SELBSTKOST=$JUSTIFY(SELBSTKOST+KOST,0,2)  ;SELBSTKOSTEN ;total production costs 
	IF +SELBSTKOSTALT'=+SELBSTKOST SET ^INAUFPPREISLOG(YM,AUFTRAG,POSITION,+$HOROLOG,$PIECE($HOROLOG,",",2),1)=PROTOKOLL  ;ULM;TYBD3,06,2003
	QUIT SELBSTKOST
	
KALK
	;-------------------------------------------------------------------------------
	; NEURECHNEN DER KAA UND ODER PREISE ;recalculate the cost and/or price
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 07-Dec-2006	JW		SR15274: Added price plan check.
	;-------------------------------------------------------------------------------
	NEW BETR,EK,VK,WG,YKALK
	
	;+++++++++++++++++++++++++++++++++++++++
	;  FIXME : Shouldn't we use the following value before YLOCATION?   <GRF>
	;             SET BETR=$PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,6)
	;          Also - May not *HAVE* a location "1"
	;+++++++++++++++++++++++++++++++++++++++
	
	;KALKULATIONSVORGABEN AUS BETRIEBSDATEI SUCHEN ;find default location for calculation 
	SET BETR=$GET(YLOCATION)
	IF BETR="" SET BETR=$ORDER(^INKALK(YM,""))
	IF BETR="" SET BETR=1
	
	;WARENGRUPPE SUCHEN ;find item group 
	SET WG=$PIECE(YFELD,Y,30)
	IF WG="" SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)
	IF WG="" SET WG=$ORDER(^INKALK(YM,BETR,""))
	SET $PIECE(YFELD,Y,30)=WG  ;SETZEN STANDARDWARENGRUPPE IN AUFTRAG ;typeset within order 
	
	IF WG'="" IF '$DATA(^INKALK(YM,BETR,WG)) SET WG=$PIECE($GET(^INVORG(YM,YM,1)),Y,13)   ;DFLT WARENGRUPPE = 1.
	
	SET YKALK=""
	
	IF BETR'="" IF WG'="" DO 
	. SET YKALK=$GET(^INKALK(YM,BETR,WG,1))
	
	IF BETR'="" IF $PIECE(YFELD,Y,4)'="" IF $DATA(^INKALKART(YM,BETR,$PIECE(YFELD,Y,4))) DO
	. SET YKALK=$GET(^INKALKART(YM,BETR,$PIECE(YFELD,Y,4),1)) 
	
	NEW PRICE,PKZ,KUNDE,DAT
	
	SET PKZ   = $PIECE(YFELD,Y,120)
	SET KUNDE = $PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,1)
	SET DAT   = $PIECE($GET(^INAUF(YM,AUFTRAG,1)),Y,4)
	IF $GET(MENGE)=""      SET MENGE      = $PIECE(YFELD,Y,5)
	IF $GET(SELBSTKOST)="" SET SELBSTKOST = $PIECE(YFELD,Y,47)  ;HK
	;
	; FIXME : If we base D47 on Received/Invoiced Quantity then
	;         can't divide by Order Quantity any more <GRF>
	; FIXME : SR15107 : Use D109 Received quantity if non-null? <GRF>    <DEFERRED FOR PURCHASE ORDER REVIEW>
	;
	SET PRICE=$$^INSALESPRICE($PIECE(YFELD,Y,4),MENGE,$GET(BETR),PKZ,KUNDE,DAT,,,1)    ;BEC;26184;16.08.04;PRICE AUS INSALESPRICE
	
	;NEURECHNEN VK PREISE;09.02.05;FIS;27283
	//IF YKALK'="" DO							SR15274
	if '$$$INVORGPricePlan($GET(^INVORG(YM,YM,1))) IF YKALK'="" DO
	. NEW idPrice,ARTIKEL1
	. IF $PIECE(PRICE,Y,3)=1 DO  QUIT  ; Location Based Price
	. . SET idPrice=$PIECE(PRICE,Y,4)
	. . SET $PIECE(YFELD,Y,116)=""                 ;AUFSCHLAG
	. . SET $PIECE(YFELD,Y,118)=$PIECE(PRICE,Y,1)  ;EINZELPREIS
	. . SET $PIECE(YFELD,Y,119)=""                 ;GESAMT-VK (WIRD SPÄTER BERECHNET)
	. . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,116)=$PIECE(YFELD,Y,116)
	. ;
	. SET $PIECE(YFELD,Y,118)=$PIECE(PRICE,Y,1)  ;DFLT EINZELPREIS;FIS;04.02.05;27283
	. IF $PIECE(YFELD,Y,120)=$PIECE(YKALK,Y,16) QUIT:$PIECE(YKALK,Y,34)'=1  ;PKZ 1 NICHT FIX ;FIS;04.02.05;27283
	. IF $PIECE(YFELD,Y,120)=$PIECE(YKALK,Y,17) QUIT:$PIECE(YKALK,Y,35)'=1  ;PKZ 2 NICHT FIX ;FIS;04.02.05;27283
	. IF $PIECE(YFELD,Y,120)=$PIECE(YKALK,Y,18) QUIT:$PIECE(YKALK,Y,36)'=1  ;PKZ 3 NICHT FIX ;FIS;04.02.05;27283
	. IF $PIECE(YFELD,Y,120)=$PIECE(YKALK,Y,19) QUIT:$PIECE(YKALK,Y,37)'=1  ;PKZ 4 NICHT FIX ;FIS;04.02.05;27283
	. ;
	. IF $PIECE(YKALK,Y,25)=1 DO  ;IMMER NEU BERECHNEN! 1.PREIS ;constantly recent 
	. . SET idPrice=$PIECE(YFELD,Y,120)
	. . IF idPrice="" DO  QUIT  ;KEIN PKZ
	. . . SET $PIECE(YFELD,Y,116)=$PIECE(YKALK,Y,11)  ;AUFSCHLAG
	. . . SET $PIECE(YFELD,Y,118)=""  ;EINZELPREIS
	. . . SET $PIECE(YFELD,Y,119)=""  ;GESAMT-VK (WIRD SPÄTER BERECHNET)
	. ;
	. . IF $PIECE(YKALK,Y,16)=idPrice DO  ;PKZ 1
	. . . SET $PIECE(YFELD,Y,116)=$PIECE(YKALK,Y,11)  ;AUFSCHLAG
	. . . SET $PIECE(YFELD,Y,118)=""  ;EINZELPREIS
	. . . SET $PIECE(YFELD,Y,119)=""  ;GESAMT-VK (WIRD SPÄTER BERECHNET)
	. . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,116)=$PIECE(YFELD,Y,116)
	. ;
	. IF $PIECE(YKALK,Y,26)=1 DO  ;IMMER NEU BERECHNEN! 2.PREIS ;constantly recent 
	. . SET idPrice=$PIECE(YFELD,Y,120)
	. . IF $PIECE(YKALK,Y,17)=idPrice DO  ;PKZ 2
	. . . SET $PIECE(YFELD,Y,116)=$PIECE(YKALK,Y,12)  ;AUFSCHLAG
	. . . SET $PIECE(YFELD,Y,118)=""  ;EINZELPREIS
	. . . SET $PIECE(YFELD,Y,119)=""  ;GESAMT-VK (WIRD SPÄTER BERECHNET)
	. . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,116)=$PIECE(YFELD,Y,116)
	. ;
	. IF $PIECE(YKALK,Y,27)=1 DO  ;IMMER NEU BERECHNEN! 3.PREIS ;constantly recent 
	. . SET idPrice=$PIECE(YFELD,Y,120)
	. . IF $PIECE(YKALK,Y,18)=idPrice DO  ;PKZ 3
	. . . SET $PIECE(YFELD,Y,116)=$PIECE(YKALK,Y,13)  ;AUFSCHLAG
	. . . SET $PIECE(YFELD,Y,118)=""  ;EINZELPREIS
	. . . SET $PIECE(YFELD,Y,119)=""  ;GESAMT-VK (WIRD SPÄTER BERECHNET)
	. . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,116)=$PIECE(YFELD,Y,116)
	. ;
	. IF $PIECE(YKALK,Y,28)=1 DO  ;IMMER NEU BERECHNEN! 4.PREIS ;constantly recent 
	. . SET idPrice=$PIECE(YFELD,Y,120)
	. . IF $PIECE(YKALK,Y,19)=idPrice DO  ;PKZ 4
	. . . SET $PIECE(YFELD,Y,116)=$PIECE(YKALK,Y,14)  ;AUFSCHLAG
	. . . SET $PIECE(YFELD,Y,118)=""  ;EINZELPREIS
	. . . SET $PIECE(YFELD,Y,119)=""  ;GESAMT-VK (WIRD SPÄTER BERECHNET)
	. . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,116)=$PIECE(YFELD,Y,116)
	
	;PREISE UND AUFSCHLAG ;And overcharge 
	DO
	. NEW AUFSCHLAG,VK
	. IF +$PIECE(YFELD,Y,116)=0 IF +$PIECE(YFELD,Y,119)=0 QUIT   ;KEIN AUFSCHLAG UND KEIN VK ;no overcharge And no Sale 
	. IF +$PIECE(YFELD,Y,119)=0 IF +$PIECE(YFELD,Y,116)'=0 DO   ;AUFSCHLAG UND KEIN VK ;overcharge And no Sale 
	. . SET AUFSCHLAG=$PIECE(YFELD,Y,116) IF AUFSCHLAG'=""
	. . SET EK=SELBSTKOST+$PIECE(YFELD,Y,71) IF $PIECE(YFELD,Y,71)'=""
	. . SET VK=0
	. . IF +EK'=0 DO
	. . . SET VK=$JUSTIFY(EK*(100+AUFSCHLAG)/100,0,2)
	. . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,119)=VK  ;GESAMTPREIS
	. . . SET $PIECE(YFELD,Y,119)=VK  ;GESAMTPREIS
	. . IF +MENGE'=0 DO
	. . . SET VK=$JUSTIFY($PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,119)/MENGE*$$^INQTYUNIT(,AUFTRAG,POSITION),0,2) 
	. . . SET RUNDUNG=+$PIECE($GET(^INVORG(YM,YM,1)),Y,54)
	. . . IF +RUNDUNG'=0 DO
	. . . . SET VK=$JUSTIFY(VK,0,0)
	. . . . IF RUNDUNG=2 IF VK>10   SET VK=$JUSTIFY(VK/10,0,0)*10     QUIT
	. . . . IF RUNDUNG=4 IF VK>100  SET VK=$JUSTIFY(VK/100,0,0)*100   QUIT
	. . . . IF RUNDUNG=5 IF VK>1000 SET VK=$JUSTIFY(VK/1000,0,0)*1000 QUIT
	. . . ;
	. . . IF +VK'=0 DO
	. . . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,118)=VK  ;TYBD 13.06.2002
	. . . . SET $PIECE(YFELD,Y,118)=VK  ;EINZEL-VK
	. . . ;
	. . . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,119)=VK*MENGE  ;GESAMTPREIS
	. . . SET $PIECE(YFELD,Y,119)=VK  ;GESAMTPREIS
	. ;
	. IF +$PIECE(YFELD,Y,119)'=0 DO   ;AUFSCHLAG NACHRECHNEN ;overcharge 
	. . DO EH^INBRUTTONETTO           ; Updates YFELD ByRef
	. . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,116)=$PIECE(YFELD,Y,116)  ;AUFSCHLAG ;overcharge 
	. . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,118)=$PIECE(YFELD,Y,118)  ;VK
	. . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,119)=$PIECE(YFELD,Y,119)  ;VK TOTAL
	. . SET $PIECE(^INAUFP(YM,AUFTRAG,POSITION,1),Y,123)=$PIECE(YFELD,Y,123)  ;VK NETTO
	
	QUIT
	
]]></Routine>
</Export>