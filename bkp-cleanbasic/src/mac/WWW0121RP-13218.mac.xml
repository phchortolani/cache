<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="WWW0121RP" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
WWW0121RP
	#include COMSYS
	#include WWWConst
 
GetAddressString(pidCompany,pidLocation,pDelimiter="")
    ;-------------------------------------------------------------------------------
    ; Get a locations address in one string
    ;
    ; Parameters:
    ; 	pidCompany  = Company id
    ; 	pidLocation = Location id
    ; 	pDelimiter  = Delimter parameter (null=comma, T=tab)
    ; 	
    ; Returns: String
    ;
    ; History:
    ; 04-Nov-2005	Tony F	SR12482: Added delimiter parameter
    ; 04-Nov-2005	Tony F	SR12482: Changed sequence of City, State Zip
    ; 16-Aug-2005	Steve S	SR12482: Created
    ;-------------------------------------------------------------------------------
	new strAddress,strStreet,strZip,strCity,strState,objLocation,strDelim
 
	set strAddress=""
	set strDelim=", "
	
	if ($get(pidCompany)'="") && ($get(pidLocation)'="") {
		set objLocation=$get(^WWW0121(0,pidCompany,pidLocation,1))
		
		set strStreet = $$$WWW0121Street(objLocation)
		set strZip    = $$$WWW0121Zipcode(objLocation)
		set strCity   = $$$WWW0121City(objLocation)
		set strState  = $$$WWW0121State(objLocation)
		
		if $get(pDelimiter)="T" {
			set strDelim="|"
		}
		if strStreet'="" set strAddress = strStreet
		if strCity'=""   set strAddress = strAddress_strDelim_strCity	
		if strState'=""  set strAddress = strAddress_", "_strState	
		if strZip'=""    set strAddress = strAddress_" "_strZip
	}
 
	quit strAddress
 
CompanyDetails(pidCompany,pidLocation)
	;-------------------------------------------------------------------------------
	; Used to collect all company related detail and format it accordingly.
	;
	; Params:
	; pidCompany - Company Id
	; pidLocation - Location Id
	;
	; ByRefs: None
	;
	; Returns: list containing company details
	;
	; History:
	; 17-Aug-2006	PO		SR14549: Created
	;-------------------------------------------------------------------------------
	new objCompany,objLocation,strCompanyName,strStreet,strCityStateZip,strTelephone,lstResult
	new strCity,strState,strZipcode,strLocationName
 
	set objCompany 		= $get(^WWW012(0,pidCompany,1))
	set objLocation 	= $get(^WWW0121(0,pidCompany,pidLocation,1))
	
	set strCompanyName 	= $$$WWW012CompanyName(objCompany)
	
	set strStreet 		= $$$WWW0121Street(objLocation)
	set strLocationName = $$$WWW0121LocationName(objLocation)
	set strCity 		= $$$WWW0121City(objLocation)
	set strState 		= $$$WWW0121State(objLocation)
	set strZipcode 		= $$$WWW0121Zipcode(objLocation)
	
	set strCityStateZip = strCity
	
	if strCity '= "" {
		set strCityStateZip = strCityStateZip_", "
	}
	
	set strCityStateZip = strCityStateZip_strState
	if strState '= "" {
		set strCityStateZip = strCityStateZip_" "
	}
	
	set strCityStateZip = strCityStateZip_strZipcode
	
	set strTelephone = $$$WWW0121Tel(objLocation)
	
	set lstResult = $listbuild(strCompanyName,
							   strStreet,
							   strCityStateZip,
							   strTelephone,
							   strLocationName)
	
	quit lstResult
	
	
LocationName(pidCompany,pidLoc)
	;-------------------------------------------------------------------------------
	; Return a location name
	;
	; Params:	pidCompany		: The company number
	; 			pidLoc			: The location number
	;
	; ByRefs:
	;
	; Returns: strName
	;
	; History:
	; 29-Sep-2006	Steve S		Created
	;-------------------------------------------------------------------------------
	new strName
 
	set strName = ""
	
	if ($get(pidCompany)'="")&&($get(pidLoc)'="") {
		set strName = $$$WWW0121LocationName($get(^WWW0121(0,pidCompany,pidLoc,1)))
	}
	
	quit strName
 
 
]]></Routine>
</Export>