<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INIssueSummary" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INIssueSummary
	
#include COMSYS
#include COMConst
#include INConst
#include COMGridEdit31
#include %occInclude
#define FORMFldIssueSummaryRejectRemainder	5
#define FORMFldIssueSummaryRejectReason		6


Enabled(YKEY, YFELD)	;SR17228
	;-------------------------------------------------------------------------------
	; Called By : Form INIssue/INIssueView : Grid Tab (COMGridEditParameter)
	; 
	; History:
	; 15-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new blnOK
	
	set blnOK = ($$$INIssueStatus(YFELD) = 1)
	if $data(^INIssueLine(0,YKEY)) {
		set blnOK = $$$NO
	}
	
	quit blnOK

HideTab1(pidIssue)
	;-------------------------------------------------------------------------------
	; Check if Issue Tab on INIssue is to be displayed (no Issue Summary Lines present)
	; 
	; $$HideTab1^INIssueSummary($get(YKEY)) 
	; 
	; History:
	; 04-Mar-2013 	SCR		HEVA-811: Hide Tab 1 when not 'Review & Pick'
	; 16-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new blnHide,objLoc,idFromLoc,objIssue
	
	; HEVA-811 vvvv
	set blnHide = $$$YES
	if pidIssue'="" {
		set objIssue	= $get(^INIssue(YM,pidIssue,1))
		set idFromLoc	= $$$INIssueFromLocn(objIssue)
	} else {
		set idFromLoc 	= ""
	}

	if idFromLoc'="" {
		set objLoc		= $get(^WWW0121(0,YM,idFromLoc,1))
		set:$$$WWW0121RequisitionIssue(objLoc)=1 blnHide = $$$NO
	}
	;set blnHide = ('$data(^INIssueSummary(0,pidIssue)) && $data(^INIssueLine(0,pidIssue)))
	; HEVA-811 ^^^^
	quit blnHide


HideTab2(pidIssue)
	;-------------------------------------------------------------------------------
	; Check if Pick Tab on INIssue is to be displayed (no Issue Lines present)
	; 
	; $$HideTab2^INIssueSummary($get(YKEY)) 
	; 
	; History:
	; 04-Apr-2013	SCR		HEVA-811: Hide when not printed
	; 04-Mar-2013 	SCR		HEVA-811: Show Tab 2 when not 'Review & Pick'
	; 16-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new blnHide,objIssue
	
	; HEVA-811 vvvv
	set blnHide = $$$NO
	if pidIssue'="" {
		if $data(^INIssueSummary(0,pidIssue)) {
			set objIssue = $get(^INIssue(YM,pidIssue,1))
			set:$$$INIssuePickPrintedTimestamp(objIssue)="" blnHide = $$$YES
			set:'$data(^INIssueLine(0,pidIssue)) blnHide = $$$YES
			;set blnHide = '$data(^INIssueLine(0,pidIssue))
		}
	}
	;set blnHide = '$data(^INIssueLine(0,pidIssue))
	; HEVA-811 ^^^^
	quit blnHide

OnBlurQty(pidGridRow,pfltQty,&pobjLine)
	;-------------------------------------------------------------------------------
	; Description :
	;			
	; Called By : Form INIssueSummary F3 Unit Qty
	;		
	; Inputs : (GROW,YINHALT,.YFELD)
	;	pfltQty - Qty expressed in Req Unit
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 16-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------

	if (pfltQty '= "") {
		set $$$INIssueSummaryBaseQuantity(pobjLine) = $$CalcInventoryQty^INUOMConversion($$$INIssueSummaryItem(pobjLine),$$$INIssueSummaryUnit(pobjLine),pfltQty)
	
	} else {
		set $$$INIssueSummaryBaseQuantity(pobjLine) = 0
	}

	quit

IsRejectable(pYFELD)
	;-------------------------------------------------------------------------------
	; Params: pYFELD Issue Summary record
	;
	; ByRefs: None
	;
	; Returns: Boolean
	;
	; History:
	; 21-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new blnRejectable,idReqLine,objReqLine
	
	//$$$LogR("IsRejectable",$get(pYFELD))
	
	set blnRejectable = $$$YES

	if $$$INIssueSummaryReferenceLine(pYFELD) = "" {
		set blnRejectable = $$$NO
	
	} else {
		set idReqLine = $$$INIssueSummaryReferenceLine(pYFELD)
		set objReqLine = $get(^INReqLine(0,$$$KEY1(idReqLine),$$$KEY2(idReqLine),1))
		if objReqLine = "" {
			set blnRejectable = $$$NO
		
		} elseif ($$$INIssueSummaryBaseQuantity(pYFELD) >= ($$$INReqLineQtyRequired(objReqLine) - $$$INReqLineQtyRejected(objReqLine) - $$$INReqLineQtyCancelled(objReqLine) - $$$INReqLineQtyIssued(objReqLine))) {
			set blnRejectable = $$$NO
		}
	}
	quit blnRejectable


OnBeforeSave(pYKEY,pYFELD)
	;-------------------------------------------------------------------------------
	; Params:
	;
	; ByRefs: None
	;
	; Returns: Nothing
	;
	; History:
	; 21-Aug-2012	PPP		SR18081: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$$YES

	if $$$INIssueSummaryUnitQuantity(pYFELD) < 0 {
		set YRETVAL   = $$$MakeStatus("IN00490")  		; "Quantity can not be negative."
		set strStatus = $$$NO
		
	} elseif ($$$INIssueSummaryRejectRemainder(pYFELD) = $$$YES) {
		if $$$INIssueSummaryRejectionReason(pYFELD) = "" {
			set YRETVAL = $$$MakeStatus("IN00896")     	; "Requires a reason for rejection."
			set strStatus = $$$NO
		}
	
	}
	
	quit strStatus

Review(pidIssue)
	;-------------------------------------------------------------------------------
	; Check if the Issue is Reviewable
	; Params:
	;
	; ByRefs: None
	;
	; Returns: 1 if true
	;
	; History:
	; 05-Mar-2013	SCR		HEVA-811: Created
	;-------------------------------------------------------------------------------
	if (pidIssue = "") quit 0
	quit $data(^INIssueSummary(YM,pidIssue))
	
CheckQty(pidIssue)
	;-------------------------------------------------------------------------------
	; Check that all Lines match the Summary Lines
	; Params:
	;
	; ByRefs: None
	;
	; Returns: Status
	;
	; History:
	; 05-Mar-2013	SCR		HEVA-811: Created
	;-------------------------------------------------------------------------------
	new strStatus,idLine
	
	set strStatus 	= $$$OK
	quit:pidIssue="" strStatus ; Fast Exist
	set idLine		= ""
	for {
		set idLine=$order(^INIssueSummary(YM,pidIssue,idLine))
		quit:idLine=""
		set strStatus=$$CheckLineQty(pidIssue,idLine)
		quit:strStatus'=$$$OK
	}
	
	quit strStatus
	
CheckLineQty(pidIssue,pidLine)
	;-------------------------------------------------------------------------------
	; Check that Lines match a Summary Line
	; Params:
	;
	; ByRefs: None
	; 
	; Returns: Status
	;
	; History:
	; 05-Mar-2013	SCR		HEVA-811: Created
	;-------------------------------------------------------------------------------
	new strStatus,objLine,fltSumQty,fltPickQty,idLine,objSumLine
	
	set strStatus 	= $$$OK
	quit:((pidIssue="") || (pidLine="")) strStatus ; Fast Exist
	set objSumLine	= $get(^INIssueSummary(YM,pidIssue,pidLine,1))
	set fltSumQty	= $$$INIssueSummaryUnitQuantity(objSumLine)
	set fltPickQty	= 0
	set idLine		= ""
	for {
		set idLine		= $order(^INIssueLine(YM,pidIssue,idLine))
		quit:idLine=""
		if pidLine=(idLine\1) {
			set objLine 	= $get(^INIssueLine(YM,pidIssue,idLine,1))
			set fltPickQty	= fltPickQty + $$$INIssueLineQtyIssued(objLine)
		}
	}
	if fltSumQty'=fltPickQty {
		set strStatus = $listBuild("IN01222",fltPickQty,fltSumQty,pidLine)  ;Total Quantity Issued (%1) has to be equal to the Unit Quantity (%2) for Line '%3'
	}
	quit strStatus
	
BOH(YFELD)
	;-------------------------------------------------------------------------------
	; Called By : Form INIssueSummary - Manual Field 7 "BOH"
	;             (WWW122 : Default Variable Input and WWW122C2 : Value Is)
	;
	;
	; History:
	; 23-Apr-2013	SCR		HEVA-811: Use GetQtyAvailable
	; 05-Mar-2013	SCR		HEVA-811: Created
	;-------------------------------------------------------------------------------
	new strIssue,objIssue,idItem,idFromLoc
	
	set objIssue 	= $$$GRIDYFELDContainer
	set idItem		= $$$INIssueSummaryItem(YFELD)
	set idFromLoc	= $$$INIssueFromLocn(objIssue)
	;quit ##class(alSOH.iSOH).GetQtyOnHand(idItem,idFromLoc,"",$$$YES) ;dont include blocked items
	quit ##class(alSOH.iSOH).GetQtyAvailable(idItem,idFromLoc,"",$$$YES) ;dont include blocked items ; HEVA-811

RequestedQty(YFELD)
	;-------------------------------------------------------------------------------
	; Called By : Form INIssueSummary - Manual Field 8 "Requested Quantity"
	;             (WWW122 : Default Variable Input and WWW122C2 : Value Is)
	;
	;
	; History:
	; 07-Mar-2013	SCR		HEVA-811: Created
	;-------------------------------------------------------------------------------
	new strIssue,objIssue,idReq,idReqLine,objReqLine
	
	set objIssue 	= $$$GRIDYFELDContainer
	set idReqLine	=  $$$KEY2($$$INIssueSummaryReferenceLine(YFELD))
	set idReq		=  $$$INIssueReference(objIssue)
	quit:(idReqLine="")||(idReq="") ""
	set objReqLine	= $get(^INReqLine(YM,idReq,idReqLine,1))
	quit $$$INReqLineQtyOrdered(objReqLine) - $$$INReqLineQtyIssued(objReqLine)

UpdateContainer(pstrFld,&objGrid,&headYFELD,&headYMFELD)
	;-------------------------------------------------------------------------------
	; Hide Reject if less is not permitted
	; 
	; Called By : UpdateContainer^COMGridEdit31G
	;		
	; Inputs : 
	;
	; ByRef :
	;
	; Returns :
	;
	; History :
	; 19-Apr-2013	SCR		HEVA-811: Created
	;-------------------------------------------------------------------------------
	new blnShow,objVORG,blnLess
	
	set objVORG	= $get(^INVORG(YM,YM,1))
	set blnLess	= $$$INVORGIssueLessthantheRequested(objVORG)
	set blnShow	= 'blnLess

 	$$$GRIDHideShowColumns($$$FORMFldIssueSummaryRejectRemainder,blnShow,"INIssueSummary")	; F5
 	$$$GRIDHideShowColumns($$$FORMFldIssueSummaryRejectReason,blnShow,"INIssueSummary")		; F6

	quit

]]></Routine>
</Export>