<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INAUFSUCH1" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INAUFSUCH1(YFORM)
	
#include COMSYS
#include INConst
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		SUCHEN AUFTRAG
	;	AUFRUF VIA FORMULARGENERATOR BEI SAVE =    #D ^INAUFSUCH1("INAUFSUCH1")
	;	AUFRUF NACH DATENFELD (VORG(1)=SUCHSTRING) D SUCH^INAUFSUCH1("INAUF")
	;
	; Inputs : 
	;
	;
	; ByRef :
	; VORG(1)=SUCHFUNKTION  (AUS FORMDEFINITION)
	; YARTS=FORMULAR, DAS AUFGERUFEN WIRD
	;
	; Returns :
	;
	;
	; History :
	; 22-May-2006		Steve S		SR14651: Added paging facility
	; FAN	29.5.2001
	;-------------------------------------------------------------------------------
	new MNR,NAME,VORNAME
	
	set %("VAR","YBACK")    = ""
	set %("VAR","YAUSWAHL") = "#"_VORG(1)_"#"_VORG(2)
	set %("VAR","YFORM")    = YFORM
	
	set ^CacheTempCriteria(YUCI,YUSER,"SearchFor")	= $get(VORG(1))
	kill ^CacheTempINAUFSUCH1Stop(YUCI,YUSER)
	if $length($get(VORG(1))) < 3 set ^CacheTempINAUFSUCH1Stop(YUCI,YUSER)=1
	set ^CacheTempCriteria(YUCI,YUSER,"SearchIn")	= $get(VORG(2))
	set ^CacheTempPerPage(YUCI,YUSER)				= $get(VORG(3))
	
	set ANZM=0
	
	kill ^CacheTempOrders(YUCI,YUSER)
	
	do FindOrders()
	do ReloadForm^COMUtilForm()
	quit
	
	
OnBeforeButtonLine()
	;-------------------------------------------------------------------------------
	; Routine to run on before button line
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	if $get(YBACK)="" set YBACK="INAUFBANZ," // stolen from form definition
	if $get(YAUSWAHL)="" do Reset()
	
	quit
	
	
Reset()
	;-------------------------------------------------------------------------------
	; Reset the form
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	kill ^CacheTempOrders(YUCI,YUSER)
	kill ^CacheTempPerPage(YUCI,YUSER)
	kill ^CacheTempCriteria(YUCI,YUSER)
	kill YPARA
 
	quit
		
	
PrintOrders()
	;-------------------------------------------------------------------------------
	; Write out the order rows to the screen
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	new AUF,ACC,AUF1,intCount,intPerPage,intCurPage
 
	set intPerPage = $$GetPerPage()
	set intCurPage = $$GetCurrentPage()
 
	;write "Per Page: "_intPerPage_", Current Page: "_intCurPage_", YPARA="_$get(YPARA)
 
	set intCount=0
	set AUF=""
	for {
		set AUF=$order(^CacheTempOrders(YUCI,YUSER,AUF))
		quit:(AUF="")
		
		set intCount=$increment(intCount)
		continue:(intCount<=((intCurPage-1)*intPerPage))
		quit:(intCount>(intCurPage*intPerPage))
		
		set AUF1=$get(^INAUF(YM,AUF,1))
		
		set ACC=$$^INAUFBERECHT(AUF1)          ;19.12.03;FAN;24831 ;buff 
		if ACC=$$$YES do ^INAUFBANZ1
	}
	quit
	
	
GetPerPage()
	;-------------------------------------------------------------------------------
	; Return the number of items this user is showing per page
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns: intPerPage
	;
	; History:
	; 19-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	new intPerPage
 
	set intPerPage=+$get(^CacheTempPerPage(YUCI,YUSER))
	if (+intPerPage=0) set intPerPage=20
 
	quit intPerPage
		
	
GetCurrentPage()
	;-------------------------------------------------------------------------------
	; Return the page number the user is on
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns: intCurPage
	;
	; History:
	; 19-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	new intCurPage
 
	if $data(^CacheTempRestart(YUSER)) {
		set intCurPage=$get(^CacheTempRestart(YUSER))
	} else { 
		set intCurPage=+$get(YPARA)
	}
	
	if intCurPage=0 set intCurPage=1
	
	if intCurPage>$$Ceiling^COMUtilNum($get(^CacheTempOrders(YUCI,YUSER))/$$GetPerPage()) {
		set intCurPage=1
	}
	quit intCurPage
	
	
ShowPages()
	;-------------------------------------------------------------------------------
	; Show the page hyperlinks
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	new intTotalOrders,intPerPage,intPages,idx,blnCurrent,intCurPage
 
	quit:$get(^CacheTempINAUFSUCH1Stop(YUCI,YUSER))
	 
	set intTotalOrders = $get(^CacheTempOrders(YUCI,YUSER))
	set intPerPage     = $$GetPerPage()
	set intPages       = $$Ceiling^COMUtilNum(intTotalOrders/intPerPage)
	set intCurPage     = $$GetCurrentPage()
 
	if (intPages>1) {
		for idx=1:1:intPages {
			set blnCurrent=(idx=intCurPage)
			write "&nbsp;&nbsp;"
			if 'blnCurrent {
				do InsertLink^COMTable("INAUFSUCH1","",idx,idx)
			} else {
				write "<b>"_idx_"</b>"
			}
		}
	}
	quit
	
	
AddOrder(pidOrder)
	;-------------------------------------------------------------------------------
	; Add this order to my list to show
	;
	; Params: pidOrder		: The order number
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 24-Jan-2007	JW		SR15349: Removed redundant processing. Added YUCI.
	; 24-Jan-2007	SteveS	SR15349: Don't show requisitions.
	; 22-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	new idOrder
	
	if $get(pidOrder)'="" {
		quit:$$$INAUFOrderType($get(^INAUF(YM,pidOrder,1)))=5 ; Requisition
		
		set ^CacheTempOrders(YUCI,YUSER,pidOrder)=""
		set ^CacheTempOrders(YUCI,YUSER)=$get(^CacheTempOrders(YUCI,YUSER))+1
	}
	quit
	
	
OnBlurPerPage(pYINHALT,pYOLDV)
	;-------------------------------------------------------------------------------
	; Code to run on blur of the Per Page field
	;
	; Params: pYINHALT		: The new Per Page value
	; 		  pYOLDV		: The previous value
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 22-May-2006	SteveS	SR14651: Created
	;-------------------------------------------------------------------------------
	new intVal
	
	if $piece(YMFELD,Y,1)'="" {		
		if +$get(pYINHALT)=0 {
			do PopulateDataField^COMUtils("INAUFSUCH1",3,pYOLDV,"M")
		} else {
			set intVal=$$Floor^COMUtilNum(pYINHALT)
			do PopulateDataField^COMUtils("INAUFSUCH1",3,intVal,"M")
			if intVal'=pYOLDV {
				set ^CacheTempRestart(YUSER)=$$$YES
				write "SAVENOW();"
			}
		}
	}
	quit
	
	
SUCH(YARTS)
	;-------------------------------------------------------------------------------
	; Do the search
	;
	; Params:
	;
	; ByRefs:
	;	YARTS=DATEI DIE DURCHSUCHT WIRD ;who 
	;
	; Returns:
	;
	; History:
	; 04-Jun-2010	GRF		SR17146: allow "." or "/" date delimiters - doesn't
	; 							recognise YYYYMMDD versions at this stage
	; 22-May-2006	SteveS	SR14651: Commented
	;-------------------------------------------------------------------------------
	new YFELD,ACCVK,ACCAL,ACCKD,ACCEI,ACCLE,ACCRE,DATUM,SCH,MINDAUFSCHLAG,KREDITLIMIT,ANZM
	new strSearchFor,strSearchIn,objINVORG
	
	;SET VORG(1)=$PIECE(YAUSWAHL,"#",2)
	;SET VORG(2)=$PIECE(YAUSWAHL,"#",3)
	
	//SR14651
	set strSearchFor=$get(^CacheTempCriteria(YUCI,YUSER,"SearchFor"))
	set strSearchIn=$get(^CacheTempCriteria(YUCI,YUSER,"SearchIn"))
	
	set YDDSATZ=0   ;VARIABEL ZUR HELL DUNKELSCHALTUNG ;bright 
	
	write YCR
	
	if strSearchFor'="" if $length(strSearchFor)<3 write "<br>"_"&nbsp;"_"<B>"_$$^WWWTEXT(32517)_"</B>"_"<BR>&nbsp;" QUIT
	if strSearchIn'=""  if strSearchFor'=""        write "&nbsp;"_$$^WWWTEXT(148)_": "_"<B>"_VORG(1)_"</B>"
	if strSearchIn'=""  if strSearchFor'=""        write "&nbsp;&nbsp;"_$$^WWWTEXT(32091)_"&nbsp;&nbsp;"_$$$AppEnum("INAUFSUCH1",strSearchIn)
	
	write "<BR>"
	
	if (strSearchFor'="") && (strSearchIn'="") {
		write "&nbsp;("_$$^WWWTEXT(33975)_": "_(+$get(^CacheTempOrders(YUCI,YUSER)))_")"
		write "<BR>"
	}
	
	set objINVORG=$GET(^INVORG(YM,YM,1))
	SET MINDAUFSCHLAG=+$$$INVORGMinimumOrderMarkup(objINVORG)  		;MINDESAUFSCHLAG DER ERREICHT WERDEN SOLL ;the will 
	SET KREDITLIMIT=+$$$INVORGCredLimitForDistribution(objINVORG)  ;AUSLIEFERUNG NUR MIT KREDITLIMIT DES KUNDEN ;handover only by means of 
	
	SET YDDSATZ=0
	;----------------------------------BERECHTIGUNGEN----------------------------------
	SET YAUSWAHL=3
	DO VOR^INAUFBERECHT     ;19.12.03;FAN;24831;VORBEREITEN-------BERECHTIGUNGEN-
	;----------------------------------KOPF--------------------------------------------
	new YSORTBY,ARTIKEL,NAMESORT
 
	SET YSORTBY=+$GET(%(YQUERY,"YSORTBY"))  ;1= NORMAL , -1 = AB, 2=NAME AUF 3=NAME UP, 4=DATUM AUF, 5=DATUM UP
	IF YSORTBY=0 SET YSORTBY=-1
	SET ARTIKEL=""  ;AUS ARTIKELANLAGE ;out of 
	SET NAMESORT=0  ;NICHT NACH NAMEN ;Not within 
	IF YSORTBY=2 SET NAMESORT=1,YSORTBY=1  ;NACH NAME AUFW. ;within Name 
	IF YSORTBY=3 SET NAMESORT=1,YSORTBY=-1  ;NACH NAME ABW. ;within Name 
	IF YSORTBY=4 SET NAMESORT=2,YSORTBY=1  ;NACH DATUM AUFW. ;within Date 
	IF YSORTBY=5 SET NAMESORT=2,YSORTBY=-1  ;NACH DATUM ABW. ;within Date 
	DO ^INAUFBANZ2(0) //writes the column headers
	;--------------------------------------------------------------------------------------SUCHEN
	//SET ANZM=0
	SET YLEAUF=$PIECE($GET(^WWW126(YM,"INAUF",YBED,1,1)),Y,1)  ;LETZTE AUFTRAGSNUMMER ;last ; <-- Is this used ?
	SET YMAX=0 ; <-- Is this used ?
	SET SUCH="ALL"  
	IF (VORG(1)?1.2N1"."1.2N1".".4N) || (VORG(1)?1.2N1"/"1.2N1"/".4N) { ; Date  ; SR17146
		SET VORG(1)=$$^WWWDATE1(VORG(1))    ; DD.MM.YYYY or MM.DD.YYYY to match FELDFORMAT (can have . or /)
	}
	;------------------------SUCHEN NACH ALLE OFFENE AUFTRAGEN---------------""oder1
	;Q:VORG(2)="" 
	
 	do PrintOrders() //SR14651
	;--------------------------------------------------------------------------------------
	;DO  ;IF ANZM=0 DO
	;IF ANZM=0!($G(YDDSATZ)<20)
	
	if (+$get(^CacheTempOrders(YUCI,YUSER))=0)||($G(YDDSATZ)<$$GetPerPage()) do ShowBlankTable()
	
	//IF ANZM>100 SET YMAX=1
	//IF YMAX=1 write "&nbsp;"_$$^WWWTEXT(32504)
	DO ^WWWFRAME(1)   ;TABLE AUS ;out of 
	kill ^CacheTempRestart(YUSER)
	QUIT
	
	
ShowBlankTable()
	;-------------------------------------------------------------------------------
	; Show 'empty' search table rows
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-May-2006	SteveS	SR14651: Created (code moved from SUCH)
	;-------------------------------------------------------------------------------
	NEW YBLANKLINE,YI
 
	SET YBLANKLINE=$GET(YDDSATZ)
	FOR YBLANKLINE=YBLANKLINE:1:$$GetPerPage() DO
	. WRITE "<TR>"
	. SET YDDSATZ=YDDSATZ+1
	. FOR YI=1:1:14 DO
	. . WRITE "<TD NOWRAP VALIGN=TOP"
	. . IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . WRITE ">"
	. . WRITE "<FONT SIZE=2>"
	. . WRITE "&nbsp;"
	. . WRITE "</TD>"
	. . WRITE YCR
	. ;
	. WRITE YCR
	. IF ACCVK=1 DO   ;NUR BEI BERECHTIGUNG ;only next to 
	. . WRITE "<TD NOWRAP VALIGN=TOP ALIGN=RIGHT"
	. . IF YDDSATZ#2=1 WRITE " BGCOLOR="_YWHITE
	. . WRITE ">"
	. . WRITE "<FONT SIZE=2>"
	. . ;IF +$GET(TOTAL)'=0 WRITE $$^WWWZAHL(TOTAL,0,2)
	. . WRITE "&nbsp;"
	. . WRITE "</TD>"
	. ;
	. WRITE "</TR>"
 
	quit	
	
	
FindOrders()
	;-------------------------------------------------------------------------------
	; For the parameter inputs, scan the order records to find matches.
	;
	; Params:
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 19-May-2006	SteveS	SR14651: Created (code moved from SUCH)
	;-------------------------------------------------------------------------------
	new AUF,ART,CHAR,AUF1,YVAR,DATUM,SCH,MIT
	
	;FIXME: Alot of this code is incorrect -- it uses the "search for" value as
	;		the starting string in the index, when it should be checking all values
	;		and using $find to check for a match.
	
	if VORG(2)'="" IF VORG(2)+10#10=0 quit
	IF VORG(2)=1 IF VORG(1)'="" DO
	. SET SUCH="OFFEN"
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUF1(YM,AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) DO
	. . . IF $FIND(AUF,VORG(1)) do AddOrder(AUF) 
	. . . IF '$FIND(AUF,VORG(1)) IF $FIND($$^WWWUMLAU(AUF1,1),$$^WWWUMLAU(VORG(1),1))  do AddOrder(AUF) 
 
	;------------------------SUCHEN NACH AUFTRAGSNUMMER---------------------------2
	IF VORG(2)=2 IF VORG(1)'="" DO
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUF(YM,AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" IF $FIND(AUF,VORG(1)) DO
	. . . SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCHEN NACH AUFTRAGSDATUM----------------------------3
	IF VORG(2)=3 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,2,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,2,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH KOMMISSION---------------------------------5
	IF VORG(2)=5 IF VORG(1)'="" DO
	. SET YVAR=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET YVAR=$ORDER(^INAUFs(YM,14,YVAR)) QUIT:YVAR=""  QUIT:'$FIND(YVAR,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$E(YVAR,1,$L($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1) 
	. . QUIT:YVAR=""
	. . SET AUF=""
	. . FOR  SET AUF=$ORDER(^INAUFs(YM,14,YVAR,AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)
 
	;------------------------SUCH NACH ZUSTÄNDIGER MITARBEITER--------------------7
	IF VORG(2)=7 IF VORG(1)'="" DO
	. SET MIT=""
	. FOR  SET MIT=$ORDER(^WWW013(0,MIT)) QUIT:MIT=""  DO  Q:ANZM>100
	. . IF MIT'="" DO
	. . . IF $FIND(MIT,$$^WWWUMLAU(VORG(1),1)) DO
	. . . . QUIT:'$DATA(^INAUFs(YM,15,$$^WWWUMLAU(MIT,1)))
	. . . . SET AUF=""
	. . . . FOR  SET AUF=$ORDER(^INAUFs(YM,15,$$^WWWUMLAU(MIT,1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . . . . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
	. . . ;
	. . . IF '$FIND(MIT,$$^WWWUMLAU(VORG(1),1)) IF $FIND($$^WWWUMLAU($P($GET(^WWW013(0,MIT,1)),Y,1),1),$$^WWWUMLAU(VORG(1),1)) DO
	. . . . QUIT:'$DATA(^INAUFs(YM,15,$$^WWWUMLAU(MIT,1)))
	. . . . SET AUF=""
	. . . . FOR  SET AUF=$ORDER(^INAUFs(YM,15,$$^WWWUMLAU(MIT,1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . . . . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH ANSPRECHPARTNER---------------------------------8---VOLLTEXT SUCHEN;FAN;24.08.04;26127
	IF VORG(2)=8 IF VORG(1)'="" DO
	 .NEW I,SUCH
	. SET YVAR=""
	. FOR  SET YVAR=$ORDER(^INAUFs(YM,25,YVAR)) QUIT:YVAR=""  DO  QUIT:ANZM>100
	. . ;FOR I=1:1 QUIT:$PIECE(VORG(1)," ",I,999)=""  SET SUCH=$PIECE(VORG(1)," ",I) IF SUCH'="" IF $FIND($$^WWWUMLAU(YVAR,2),$$^WWWUMLAU(SUCH,2)) DO
	. . SET SUCH=$$^WWWUMLAU(VORG(1),1)
	. . IF $FIND($$^WWWUMLAU(YVAR,2),$$^WWWUMLAU(SUCH,2)) DO
	. . . SET AUF=""
	. . . FOR  SET AUF=$ORDER(^INAUFs(YM,25,YVAR,AUF)) QUIT:AUF=""  DO
	. . . . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)
 
	;------------------------SUCHEN NACH KUNDEBESTELLNUMMER------------------------9
	IF VORG(2)=9 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,24,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,24,$$^WWWUMLAU(VORG(1),1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCHEN NACH KUNDENNUMMER----------------------------12
	IF VORG(2)=12 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,1,$$^WWWUMLAU(VORG(1),1)))
	. SET DATUM=""
	. FOR  SET DATUM=$ORDER(^INAUFs(YM,1,$$^WWWUMLAU(VORG(1),1),DATUM),-1) QUIT:DATUM=""  DO  Q:ANZM>100
	. . SET SCH=""
	. . FOR  SET SCH=$ORDER(^INAUFs(YM,1,$$^WWWUMLAU(VORG(1),1),DATUM,SCH)) QUIT:SCH=""  DO  Q:ANZM>100
	. . . SET AUF=""
	. . . FOR  SET AUF=$ORDER(^INAUFs(YM,1,$$^WWWUMLAU(VORG(1),1),DATUM,SCH,AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . . . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)
 
	;------------------------SUCHEN NACH LIEFERANTENNUMMER------------------------14
	IF VORG(2)=14 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,17,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,17,$$^WWWUMLAU(VORG(1),1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCHEN NACH KUNDEN/LIEFERRANT BEZEICHNUNG------------16
	IF VORG(2)=16 IF VORG(1)'="" DO
	. SET YVAR=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET YVAR=$ORDER(^INAUFs(YM,13,YVAR)) QUIT:YVAR=""  QUIT:'$FIND(YVAR,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$E(YVAR,1,$L($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)  
	. . QUIT:YVAR=""
	. . SET AUF=""
	. . FOR  SET AUF=$ORDER(^INAUFs(YM,13,YVAR,AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)
 
	;------------------------SUCHEN NACH ARTIKELNUMMER-----------------------------21
	IF VORG(2)=21 IF VORG(1)'="" DO
	. SET ART=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET ART=$ORDER(^INAUFPs(YM,6,ART)) QUIT:ART=""  QUIT:'$FIND(ART,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(ART,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)
	. . QUIT:ART=""
	. . SET DATUM=""
	. . FOR  SET DATUM=$ORDER(^INAUFPs(YM,6,ART,DATUM),-1) QUIT:DATUM=""  DO  Q:ANZM>100
	. . . SET AUF=""
	. . . SET YVAR=""
	. . . FOR  SET YVAR=$ORDER(^INAUFPs(YM,6,ART,DATUM,YVAR)) QUIT:YVAR=""  DO  Q:ANZM>100
	. . . . IF YVAR'="" IF YVAR'=AUF SET AUF=YVAR SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)
 
	;------------------------SUCHEN NACH ARTIKELNAME-------------------------------23
	IF VORG(2)=23 IF VORG(1)'="" DO
	. SET ART=$$^WWWUMLAU(VORG(1),1) DO  FOR  SET ART=$ORDER(^INAUFPs(YM,1,ART)) QUIT:ART=""  QUIT:'$FIND(ART,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(ART,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)
	. . QUIT:ART=""
	. . SET AUF=""
	. . SET YVAR=""
	. . FOR  SET YVAR=$ORDER(^INAUFPs(YM,1,ART,YVAR)) QUIT:YVAR=""  DO  Q:ANZM>100
	. . . IF YVAR'="" IF YVAR'=AUF SET AUF=YVAR SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)     ;GLEICH ART IN EINEN AUFTRAG,AUFTRAG NUR EINMAL LISTE
 
	;------------------------SUCH NACH Gesamt KD-Auftrag AB Nr---------------------31
	IF VORG(2)=31 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,4,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,4,$$^WWWUMLAU(VORG(1),1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt KD-Auftrag AB Nr gedruckt am---------32
	IF VORG(2)=32 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,10,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,10,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt LE-Auftrag Bestellung gedruckt am----42
	IF VORG(2)=42 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,5,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,5,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt Auftrag Bereitstellung gedruckt am---52
	IF VORG(2)=52 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,6,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,6,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt KD-Auftrag Lieferschein Nr-----------61
	IF VORG(2)=61 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,7,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,7,$$^WWWUMLAU(VORG(1),1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt KD-Auftrag LE Nr gedruckt am---------62
	IF VORG(2)=62 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,11,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,11,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt KD-Auftrag Rechnungs Nr--------------71
	IF VORG(2)=71 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,8,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,8,$$^WWWUMLAU(VORG(1),1),AUF)) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH Gesamt KD-Auftrag RE Nr gedruckt am---------72
	IF VORG(2)=72 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,12,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,12,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH IN FIBU ÜBERTRAGEN AM-----------------------82
	IF VORG(2)=82 IF VORG(1)'="" DO
	. QUIT:'$DATA(^INAUFs(YM,9,$$^WWWUMLAU(VORG(1),1)))
	. SET AUF=""
	. FOR  SET AUF=$ORDER(^INAUFs(YM,9,$$^WWWUMLAU(VORG(1),1),AUF),-1) QUIT:AUF=""  DO  Q:ANZM>100
	. . IF AUF'="" SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF) 
 
	;------------------------SUCH NACH CHARGENNUMMER-----------------------91
	IF VORG(2)=91 IF VORG(1)'="" DO
	. SET CHAR=$$^WWWUMLAU(VORG(1),1)
	. DO  FOR  SET CHAR=$ORDER(^INAUFPs(YM,15,CHAR)) QUIT:CHAR=""  QUIT:'$FIND(CHAR,$$^WWWUMLAU(VORG(1),1))  DO  QUIT:ANZM>100  QUIT:$EXTRACT(CHAR,1,$LENGTH($$^WWWUMLAU(VORG(1),1)))'=$$^WWWUMLAU(VORG(1),1)
	. . QUIT:CHAR=""
	. . SET AUF=""
	. . SET YVAR=""
	. . FOR  SET YVAR=$ORDER(^INAUFPs(YM,15,CHAR,YVAR)) QUIT:YVAR=""  DO  Q:ANZM>100
	. . . IF YVAR'="" IF YVAR'=AUF SET AUF=YVAR SET AUF1=$GET(^INAUF(YM,AUF,1)) do AddOrder(AUF)     ;GLEICH ART IN EINEN AUFTRAG,AUFTRAG NUR EINMAL LISTE
 
	quit
	
]]></Routine>
</Export>