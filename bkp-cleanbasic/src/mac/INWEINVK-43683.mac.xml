<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INWEINVK" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INWEINVK
	
#include COMSYS
#include INConst
	do OnAfterSave()     ; SR17084 - legacy code in case other direct call
	quit
	
	
OnAfterSave()
	;-------------------------------------------------------------------------------
	; Description of Function :
	;		LÃ–SCHEN INVENTUR DATEI
	;
	; Called By : Form INWEINVK : Execute After Save
	;
	;
	; ByRef : VORG(1)  Stocktake to be deleted
	;
	;
	; Returns :
	;
	;
	; History :
	; 15-Dec-2009	GRF		SR17084: preserve user language within background job;
	; 							use specific tag; cleanup
	; 22-Nov-2007	shobby	SRAdHoc: Allowing deleting of non-numeric
	; 07-Aug-2006	FAN		SR14857: Stocktake Batch/Update changes
	; 03-May-2006	PP&JW	SR14508: Check for postings, delete properly.
	; 06.06.2000	DT
	;-------------------------------------------------------------------------------
	new idLog,idStocktake
	
	set idStocktake = VORG(1)
	
	if $$IsStocktakePosted(idStocktake) {
		do ^WWWINFO($$$Text("IN00457"))  ; "Cannot delete a posted Stocktake"
		
	} else {    ; Generating Stocktake
		do StartLog^COMLog($$^WWWFORMNAME(YFORM)_": "_idStocktake_" ("_$$$INWEINVPRemark($get(^INWEINVP(YM,idStocktake,1)))_")",.idLog) 
		
	;	JOB START(YM,idStocktake,idLog) //::1    ;SR14508
		job START(YM,idStocktake,idLog,$get(SPRACHE))       ; SR17084
		
 		; Ready to goto the Detail form
		do GoToForm^COMUtilForm("COMMsg",idLog)
	}
	quit
	
	
	;START(pidCompany,pidStocktake,pidLog="")  ; SR17084
START(pidCompany,pidStocktake,pidLog="",penumLang="")
	;-------------------------------------------------------------------------------
	; Wrapper for killing stocktake
	;
	; Params:	pidCompany		DEPRECATED - always 0
	; 			pidStocktake	Stocktake No
	; 			pidLog			COMMsgLog id
	; 			penumLang		Initiating user's language
	;
	; ByRefs:
	;
	; Returns:
	;
	; History:
	; 15-Dec-2009	GRF		SR17084: preserve user language within background job;
	; 							use Kill macro
	; 08-Nov-2007	shobby	SRBR014781: Delete of INWEINV and INWEINVCount are now
	; 							handled by the OnBeforeSave hooks.  Success or fail
	; 							reported in the log
	; 26-Oct-2007,  PJones  Kill the counts for this stocktake
	; 25-Oct-2007,  PJones  BR014730, remove lock from sites when deleting stocktake
	; 11-May-2006	JW		SR14508: Created
	;-------------------------------------------------------------------------------
	do ^WWWVAR
	new strLocations,objStocktake,strStatus
	
	if penumLang'="" set (SPRACHE,LANGUAGE) = penumLang    ; SR17084
	
	set YM = pidCompany
	set objStocktake = $get(^INWEINVP(YM,pidStocktake,1))
	
	if pidLog'="" do AddMessage^COMLog(pidLog,$$^WWWTEXT(33892))	 ; "Processing"
	
	set strStatus = $$$Kill("INWEINVP",pidStocktake)       ; SR17084
	
	if pidLog'="" {
		if $$$ISOK(strStatus) {      ; remove lock from sites when deleting stocktake
			set strLocations = $$$INWEINVPSiteLocation(objStocktake) 
			do UnlockSiteLocationList^INLocking(strLocations,$$$EnumINLOCKTYPEStocktake,pidStocktake)
			do AddMessage^COMLog(pidLog,$$^WWWTEXT(32051))           ; "Deleted"
		
		} else {
			do AddMessage^COMLog(pidLog,strStatus)			
		}
	}
	quit
	
	
IsStocktakePosted(pidStocktake)
	;-------------------------------------------------------------------------------
	; Checks if any Stocktake lines ahve been posted
	;
	; Inputs : 
	;
	; Returns :
	;
	; History :
	; 15-Dec-2009	GRF		Order macro
	; 28-Feb-2006	PP&JW	SR14508: Check if Stocktake is posted
	;-------------------------------------------------------------------------------
	new blnPosted,intCnt
	
	set blnPosted = $$$NO
	
	set intCnt = ""
	for {
		set intCnt = $order(^INWEINV(0,pidStocktake,intCnt))
		quit:intCnt=""
		
		if $$$INWEINVPosted($get(^INWEINV(0,pidStocktake,intCnt,1)))'="" {
			set blnPosted = $$$YES
			quit
		}
	}
	
	quit blnPosted
	
]]></Routine>
</Export>