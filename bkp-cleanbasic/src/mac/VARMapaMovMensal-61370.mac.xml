<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="VARMapaMovMensal" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
VARMapaMovMensal
	#include INConst
	#include WWWConst
	#include VARConst
	#define MesInicial $$$VARParametroClienteMesSaldoInicialReceb($get(^VARParametroCliente(0,0,1)))
	#define AnoInicial $$$VARParametroClienteAnoSaldoInicialReceb($get(^VARParametroCliente(0,0,1)))
	quit
	
OnAfterSave
	new Local, Grupo, Mes, Ano
	
	kill ^VARTempMapaMovMensal(YM,YBED)
	
	set Local 	= $get(VORG(1))
	set Grupo 	= $get(VORG(2))
	set Mes 	= $get(VORG(3))
	set Ano		= $get(VORG(4))
	
	set $piece(^VARTempMapaMovMensal(YM,YBED,1),Y,1) = Local
	set $piece(^VARTempMapaMovMensal(YM,YBED,1),Y,2) = Grupo
	set $piece(^VARTempMapaMovMensal(YM,YBED,1),Y,3) = Mes
	set $piece(^VARTempMapaMovMensal(YM,YBED,1),Y,4) = Ano
	
	if ((Mes = "")||(Ano = "")){
		$$$Alert("Preencha todos os campos obrigatórios.")
		do GoToForm^COMUtilForm("VARMapaMovMensal","",,,,,)
		quit
	}
	
	do RunReportMapaMovMensal^VARJasperRunReport(Local,Grupo,Mes,Ano)
	
	quit
	
OnBeforeButtonLine(pYFELD)	
	do LoadListaLocaisDeEstoque
	quit
 	
LoadListaLocaisDeEstoque
	new CodLocal, NomeLocal, objLista, objUsuario, perfilUsuario,
		locaisPermitidos
	
	set objLista = ""
	
	set objUsuario = $get(^WWW013(YM,YBED,1))		
    set perfilUsuario = $$$WWW013UserAccess(objUsuario)
	set locaisPermitidos = $$$WWW013AllowedLocations(objUsuario)
	
 	kill ^COMTempList(YM,YUSER,"VARLocais")
 
	&sql(
		DECLARE CursorLocais CURSOR FOR 
		SELECT Location, LocationName
		INTO :CodLocal, :NomeLocal
		FROM WWW0121
 		WHERE COMPANY = :YM
 		AND StorageLocn = 1
 		ORDER BY to_number(Location) )
 		
	
	&sql(OPEN CursorLocais)
    IF (SQLCODE) {
		QUIT
    }
    
   	FOR { &sql(FETCH CursorLocais)
		QUIT:SQLCODE
		
		; Caso o usuário não seja administrador e o local não esteja na
		; lista de locais permitidos ao usuário, não popula
		if ((perfilUsuario '= 1) &&
			('$find(";"_locaisPermitidos_";", ";"_CodLocal_";"))) {
			continue
		}

		set $piece(objLista, Y, 1) = NomeLocal
		set ^COMTempList(YM,YUSER,"VARLocais",CodLocal,1)=objLista
   	}
 
   	&sql(CLOSE CursorLocais)
 
	quit
	
GetSaldoInicial(pMes,pAno,pGrupoPai="",pLocal="")
	new ValorTotal, DataFinal, FilterLocal
	set ValorTotal 	= 0
	set DataFinal  	= ""
	set FilterLocal = ""
	
	if (pLocal'=""){
		if ($get(^WWW0121(0,0,pLocal,1)) '= ""){
			set FilterLocal = pLocal
		}
	}
	
	if ( (pMes '= $$$MesInicial) || (pAno '= $$$AnoInicial) ){
		set DataInicial = "01/"_$$FillChar^VARSQL(pMes, 2, 0)_"/"_pAno
		set DataFinal = $zdate($zdateh($SYSTEM.SQL.DATEADD("dd",-1,$zdateh(DataInicial,4)),3),4)		
		
		&sql(
			SELECT	SUM($$GetEstoqueOnHandAtDate^VARReposicao(ItemNumber, :FilterLocal, NULL, NULL, TO_DATE(:DataFinal, 'DD/MM/YYYY')) *
					$$SQLGetAverageCostAtDate^VARSQL(ItemNumber, 1, TO_DATE(:DataFinal, 'DD/MM/YYYY')))
			INTO	:ValorTotal
			FROM	INART
			WHERE	Company = 0 
			AND		((:pGrupoPai IS NULL) OR ($$SQLGetGrupoPai^VARSQL($$SQLGetGrupoItem^VARSQL(ItemNumber)) = :pGrupoPai))
		)
		
		quit ValorTotal
	}
	
	quit ValorTotal
	
GetMesAnoInicialFechamento(pMes,pAno)
	new blnPeriodoInicial
	
	set blnPeriodoInicial = $$$NO
	
	if ( (pMes = $$$MesInicial) && (pAno = $$$AnoInicial) ){
		set blnPeriodoInicial = $$$YES
	}
	
	quit blnPeriodoInicial
	
GetReasonCode(pTxnCode)
	$$$VAR

	if ($length(pTxnCode) = 0) quit 0

	quit $$$INADJReasonCode($get(^INADJ(YM, pTxnCode, 1)))
	
GetTipoINADJ(pTxnCode)
	$$$VAR
	
	;1- Doação
	;2- Empréstimo
	
	if ($length(pTxnCode) = 0) quit 0

	quit $$$INADJFREE2($get(^INADJ(YM, pTxnCode, 1)))
	
GetLocalDestinoTransf(pTxnCode)
	new idWWW0121

	$$$VAR

	if ($length(pTxnCode) = 0) quit ""

	set idWWW0121 = $$$INTFRReceivingLocation($get(^INTFR(YM, pTxnCode, 1)))
	if ($length(idWWW0121) = 0) quit ""

	quit idWWW0121
]]></Routine>
</Export>