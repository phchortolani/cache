<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="22" zv="CACHE" ts="2001-01-01 00:00:00">
<Routine name="INLP2Line" type="MAC" languagemode="0" timestamp="58440,0"><![CDATA[
INLP2Line ; NOTE : Form INLP2Line is based on class INLP
	
#include COMGridEdit31
#include INConst
	
OnBeforeDelete(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	;
	; Called By: Execute Before Delete Possibility in form INLP2Line
	; 
	; Inputs:
	;
	; Returns:
	;
	; History:
	; 18-Mar-2011	shobby	SR17590.1: Created
	;-------------------------------------------------------------------------------
	new strStatus
	
	set strStatus = $$OnBeforeDelete^INLP(YKEY,YFELD)   ; use same checks as form INLP
	
	if Q = $$$QDontDelete {
		$$$Alert(strStatus)
	}
	quit
	
	
OnBeforeFormat(pstrYFIELDNAME,pstrText,pobjLine)
	;-------------------------------------------------------------------------------
	; Called By: OnBeforeFormat^COMGridEdit31Events as CallEvent
	; 
	; History
	; 18-Mar-2011	shobby	SR17590.1: Don't access YKEY for unsaved records.
	;-------------------------------------------------------------------------------
	new intCol,intRow
	
	if '$$$NoKey(YKEY) {               ;SR17590.1
		$$$GRIDSplitKey(pstrYFIELDNAME,intRow,intCol)
		if intCol=4 if $$IsDisabled^INLP2Line(YKEY,YFELD) set YBACKGROUNDCOLOR = "silver"
	}
	quit $$$OK
	
	
IsDisabled(YKEY,YFELD)
	;-------------------------------------------------------------------------------
	; Called By: OnBeforeFormat^INLP2Line, Form INLP2Line Core Rules "If Condition On Execute"
	;-------------------------------------------------------------------------------
	new blnDisabled
	
	set blnDisabled = $$$NO
	if ($$$KEY1(YKEY)'="") && ($$$KEY2(YKEY)'="") {
		set blnDisabled = ''$data(^INLP(0,$$$KEY1(YKEY),$$$KEY2(YKEY),1))
	}
	quit blnDisabled
	
	
OnBlur(&YKEY,YBBN,&YFELD)
	;-------------------------------------------------------------------------------
	; The screen will show a partial list of Storages.  This routine prevents user
	; overwriting existing data.
	;   1) If the user tries to create a record with the same ID of an existing
	;      displayed record, focus will move to that cell.
	;   2) If the record exists but is not displayed (Because it isn't a TSU) then it
	;      will be loaded in to the grid.
	;
	; Called By: Form INLP2Line
	; 
	; Inputs:
	;
	; History:
	; 03-Mar-2011	GRF		SR17590: reuse idLocn & idStore
	; 23-Nov-2010	shobby	SR17590: Created
	;-------------------------------------------------------------------------------
	new idLocn,idStore,objINLP
	
	set idLocn  = $$$KEY1(YKEY)
	set idStore = $$$INLPStorage1(YFELD)
	if (idLocn'="") && (idStore'="") {
		if $$GoToUpdatedRow^COMGridEdit31Scan(YFORM,idStore) {
			set YFELD = ""
		} else {
			if $data(^INLP(0,idLocn,idStore)) {
				set objINLP = $get(^INLP(0,idLocn,idStore,1))
				if $$$INLPStorage1(objINLP)="" set $$$INLPStorage1(objINLP) = idStore
				set YKEY  = idLocn_","_idStore
				set YFELD = objINLP
			}
		}
	}
	quit
	
	
OnBeforeDisplayLine(pstrKeys,pobjStore)
	;-------------------------------------------------------------------------------
	; Check whether to display line
	; If multi-pass, and not the line we want to go to, hide it.
	;
	; Called By : COMGridEdit31Events
	; 
	; Params:	pstrKeys	- INLP key (YKEY)
	;			pobjStore	- INLP object (YFELD)
	;
	; ByRefs:
	;
	; Returns:	boolean
	;
	; History:
	; 16-Jun-2011	GRF		SR17716: Created
	;-------------------------------------------------------------------------------
	quit ($$$INLPTemporaryStorageUnit(pobjStore) && $$IsTSUOpen^INLPStatus($$$INLPStatus(pobjStore)))
	;quit $$IsTSUOpen^INLPStatus($$$INLPStatus(pobjStore))
	
]]></Routine>
</Export>